|FICHEROS;
    caut0009 #0;
    cacuenta #1;         || cuentas
    cacuebal #2;
    camaasil #11;        || Asientos
    ca8wb003 #12;

    cabaexp2 #5;         || temporal existencias

    caexistc #6;         || descarga cabs.
    caexistl #7;         || descarga lins.

    canempre #30;        || empresas
    caconimp #8;
    cuentas@ #901;
    inciden@ #911;

    ca8cumay #410;  || Control de Balances
    ca8m0000 #420;  || Descripcion Numero de Balance
    ca8m0001 #421;  || Def Balance Textos
    ca8m0002 #422;  || Def Balance Linea Cuentas
    ca8m0011 #431;  || Reparto 8y9
    ca8m0012 #432;  ||             Linea

    caw00011 #65,MANTE;  || Errores
    caw00036 #965;
|FIN;

|VARIABLES;
   &entrada  = 1;
   digito    = 0;
   aAlfa1    = "";
   aAlfa2    = "";
   aAlfa3    = "";
   aAlfa4    = "";
   nNume1    = 0;
   nNume2    = 0;
   nPara1    = 0;
   nCampo    = 0;
   nLong     = 0,
   nLongT    = 0;
   cue_perdi = "";
   dig_perdi = 0;
   nivel     = 0;
   traraf    = 0;

   nDebeAsto   = 0;
   nHaberAsto  = 0;
   nDebeTotal  = 0;
   nHaberTotal = 0;
   nDebeCta    = 0;
   nHaberCta   = 0;
   nDebe       = 0;
   nHaber      = 0;

   nDDiario    = 0;
   nHDiario    = 0;

   nError      = 0;
   nNumero     = 0;
   swmalo      = 0;
   aError      = "";
   aDCuenta    = "";
   aHCuenta    = "";
   nError1     = 0;

   nError2     = 0;
   nError3     = 0;
   nError4     = 0;
   nError5     = 0;
   nError6     = 0;
   nSuma       = 0;
   nGrabaExis  = 0;
   nSwGraba    = 0;
   aFichero    = "";
   aMas        = "";
   aDef        = "";
   aPrimera    = "";
   aUltima     = "";
   nSuper      = 0;
   nCam4       = 0;
   aCam5       = "";
   nCam6       = 0;
   nCam7       = 0;
   nCam8       = 0;
   antapu      = 0;

   aCampo10_n   = "";
   varcamp     = 0;
   mascara     = "";
   x           = 0;

   &d_mes = 0;
   &h_mes = 0;
   &estru = 0;
   &r_emp = 0;
   &r_per = 0;
   &r_eje = 0;
   &r_nom = "";

   sw = 0;
   antdia = 0;
   antfec = @;
   antdoc = 0;
   debe_p = 0;
  haber_p = 0;
   mes_ini = 0;
   mes_fin = 0;
   mesini  = 0;
   mesfin  = 0;
   nSaldo1 = 0;
   nSaldo2 = 0;
   nExis   = 0;
   nNivel  = 0;

   nSol1   = 0;  || Revisar Entrada de Asientos
   nSol2   = 0;  || Revisar Plan Contable
   nSol3   = 0;  || Revisar estructuras de Balances
   nSol4   = 0;  || Revisar Existencias
   nSol5   = 0;  || Recalculo de Saldos
   nSol6   = 0;  || Solo problema de las estructuras
   nSol7   = 0;  || Problema de niveles en Plan Contable
   nSol8   = 0;  || Mal reparto 8 y 9
   nSol9   = 0;  || Problema con Cuenta 129
   &enErrorW17 = 0;

   &nPlan  = 0;
   &nLinea = 0;
   &eaText = "";
   informe = "caut0009";

   nInkey = 0;
   nDNumero = 0;
   nHNumero = 0;

  nHay    = 0;
  nHayA   = 0;
  nHayE   = 0;
  nHay129 = 0;
  nSwCier = 0;

   &enCam0 = 0;
   &enCam1 = 0;
   &enCam2 = 0;
   &enCam6 = 0;
   &enCam7 = 0;
   &enCam8 = 0;
   &enActivo   = 0;
   &enPasivo   = 0;
   &enGastos   = 0;
   &enIngresos = 0;

   fecini = @;
   fecfin = @;
   nElAny = 0;
   aLaFecha1 = "";
   aLaFecha2 = "";
   nMes      = 0;

   NumMes    = 0;
   NombreMes = "";
   &MaximoDigitos = 0;
   aLong     = "";
   nErrorDiario = 0;
   nMovi     = 0;
   nSwOp     = 0;
   nImporte  = 0;

   aDCta = "";
   aHCta = "";
|FIN;

|INCLUYE acceso_i;
|INCLUYE varnue_i;
|INCLUYE digito_i;
|INCLUYE defect_i;
|INCLUYE puntos_i;

|| ========================================================================
||                        CALCULOS PANTALLA
|| ========================================================================
|PROCESO inicio_g; |TIPO 8;
  |HAZ inicio;
  |HAZ eVarEmpresa;
|FPRC;

|PROCESO NombreMes;
  |SI NumMes = 0;  NombreMes = "Apertura  "; |FINSI;
  |SI NumMes = 1;  NombreMes = "Enero     "; |FINSI;
  |SI NumMes = 2;  NombreMes = "Febrero   "; |FINSI;
  |SI NumMes = 3;  NombreMes = "Marzo     "; |FINSI;
  |SI NumMes = 4;  NombreMes = "Abril     "; |FINSI;
  |SI NumMes = 5;  NombreMes = "Mayo      "; |FINSI;
  |SI NumMes = 6;  NombreMes = "Junio     "; |FINSI;
  |SI NumMes = 7;  NombreMes = "Julio     "; |FINSI;
  |SI NumMes = 8;  NombreMes = "Agosto    "; |FINSI;
  |SI NumMes = 9;  NombreMes = "Septiembre"; |FINSI;
  |SI NumMes = 10; NombreMes = "Octubre   "; |FINSI;
  |SI NumMes = 11; NombreMes = "Noviembre "; |FINSI;
  |SI NumMes = 12; NombreMes = "Diciembre "; |FINSI;
  |SI NumMes = 13; NombreMes = "Cierre    "; |FINSI;
|FPRC;
|| ========================================================================
|PROCESO pintames; |TIPO 0;
  nNume1 = Campo + 2;
  |SI #0Campo ! 0; |Y #0Campo ! 13;
      NumMes = dig1 + (#0Campo - 1);
      |SI NumMes > 12;
          NumMes = NumMes - 12;
      |FINSI;
  |SINO;
      NumMes = #0Campo;
  |FINSI;
  |HAZ NombreMes;
  #0nNume1 = NombreMes; |PINTA #0nNume1;

  |SI Campo = 5; |Y  #0#4 > #0#5;
      |MENAV "    Error de Entrada ...";
      |ERROR;
  |FINSI;
|FPRC;

|PROCESO pintames2; |TIPO 0;
  |SI #0Campo ! 0; |Y #0Campo ! 13;
      NumMes = dig1 + (#0Campo - 1);
      |SI NumMes > 12;
          NumMes = NumMes - 12;
      |FINSI;
  |SINO;
      NumMes = #0Campo;
  |FINSI;
  |HAZ NombreMes;

  |ATRI I;
  NombreMes = (NombreMes % 112);
  |SI Campo = 12;  |PINTA 1918, NombreMes;  |FINSI;
  |SI Campo = 13;  |PINTA 1947, NombreMes;  |FINSI;
  |ATRI 0;
  |SI Campo = 13;  |Y #0#12 > #0#13;
      |MENAV "    Error de Entrada ...";
      |ERROR;
  |FINSI;
|FPRC;

|PROCESO nivel; |TIPO 7;
 |C_M #0#9, 1,"N";
 |C_M #0#10,1,"N";
 aAlfa1 = #0#8;
 |SI #0#1 = 1; |C_M #0#9, 1,aAlfa1; |FINSI;
 |SI #0#1 = 2; |C_M #0#10,1,aAlfa1; |FINSI;

 |SI dig3 = 1; MaximoDigitos = dig4; |FINSI;
 |SI dig3 = 2; MaximoDigitos = dig5; |FINSI;
 |SI dig3 = 3; MaximoDigitos = dig6; |FINSI;
 |SI dig3 = 4; MaximoDigitos = dig7; |FINSI;
 |SI dig3 = 5; MaximoDigitos = dig8; |FINSI;
 |SI dig3 = 6; MaximoDigitos = dig9; |FINSI;

 |SI aAlfa1 = "N";
     #0#9   = MaximoDigitos; |PINTA #0#9;
     #0#10  = dig3;          |PINTA #0#10;
 |FINSI;
|FPRC;

|PROCESO miraniv; |TIPO 0;

  |SI Campo = 9;
     nLongT = #0#9;
     |SI nLongT > 4;
         nivel = 0;
         |SI nLongT = dig4; nivel = 1;  |FINSI;
         |SI nLongT = dig5; nivel = 2;  |FINSI;
         |SI nLongT = dig6; nivel = 3;  |FINSI;
         |SI nLongT = dig7; nivel = 4;  |FINSI;
         |SI nLongT = dig8; nivel = 5;  |FINSI;
         |SI nLongT = dig9; nivel = 6;  |FINSI;
         |SI nivel = 0;
             |MENAV "    Error. Longitud incorrecta";
             |ERROR;
         |FINSI;
     |FINSI;
  |SINO;
      nivel = #0#10;
      |SI nivel = 1;|Y dig4 = 0;  |AVISO;  |ERROR;  |FINSI;
      |SI nivel = 2;|Y dig5 = 0;  |AVISO;  |ERROR;  |FINSI;
      |SI nivel = 3;|Y dig6 = 0;  |AVISO;  |ERROR;  |FINSI;
      |SI nivel = 4;|Y dig7 = 0;  |AVISO;  |ERROR;  |FINSI;
      |SI nivel = 5;|Y dig8 = 0;  |AVISO;  |ERROR;  |FINSI;
      |SI nivel = 6;|Y dig9 = 0;  |AVISO;  |ERROR;  |FINSI;
  |FINSI;
|FPRC;

|PROCESO estruct; |TIPO 0;
  |C_M #0Campo, 0, aAlfa1;
  |SI aAlfa1 = "N"; |ACABA; |FINSI;

  |ABRE #6;
  #6#0 = #0#11;
  |LEE 040#6=;
  |SI FSalida ! 0;
      |MENAV "    No existe la estructura de descarga.";
      |CIERRA #6;
      |ERROR;
      |ACABA;
  |FINSI;
  |HAZ cue_per;
  |CIERRA #6;
|FPRC;

|PROCESO cue_per; || calcula la cuenta de perdidas y ganancias
  cue_perdi = #6#2;
  dig_perdi = #6#3;
|FPRC;

|PROCESO descri; |TIPO 6;
 |C_M #0Campo,0,aAlfa1;
 |SI aAlfa1 = "N"; |ACABA; |FINSI;

 |ABRE #420;
 |DDEFECTO #420;
 #420#0 = #0Campo;
 |LEE 001#420=;
 |SI FSalida ! 0;
     |MENAV "   Error. No existe Numero de Balance";
     |ERROR;
 |FINSI;
 |CIERRA #420;
 nCampo = Campo + 1;
 #0nCampo = #420#1; |PINTA #0nCampo;
|FPRC;

|CALCULO QueHago;
 |SI #0#0 = 1;
     aAlfa1 = "S";
     aAlfa2 = "N";
 |SINO;
     aAlfa1 = "N";
     aAlfa2 = "S";
 |FINSI;
 |C_M #0#1,  1, aAlfa2;
 |C_M #0#2,  1, aAlfa2;
 |C_M #0#8,  1, aAlfa2;
 |C_M #0#11, 1, aAlfa2;
 |C_M #0#12, 1, aAlfa2;
 |C_M #0#13, 1, aAlfa2;
 |C_M #0#14, 1, aAlfa2;
 |C_M #0#15, 1, aAlfa2;
 |C_M #0#16, 1, aAlfa2;
 |C_M #0#17, 1, aAlfa2;
 |C_M #0#18, 1, aAlfa1;
 |C_M #0#19, 1, aAlfa1;
 |C_M #0#22, 1, aAlfa1;

 |SI enEjercicio < 2008;
     |C_M #0#1, 1, "N";
     #0#1 = 2;
     |PINTA #0#1;
 |FINSI;
|FCAL;

|CALCULO QueHago2;

 |SI #0#0 = 1;  #0#1 = 2; |PINTA #0#1; |FINSI;
 |SI #0#1 = 1;
     aAlfa1 = "S";
     aAlfa2 = "N";
     nPlan  = 2008;
 |SINO;
     aAlfa1 = "N";
     aAlfa2 = "S";
     nPlan  = 1990;
 |FINSI;

 |C_M #0#2,  1, aAlfa1;
 |C_M #0#9,  1, aAlfa1;
 |C_M #0#10, 1, aAlfa2;
|FCAL;

|CALCULO cuenta; |TIPO 6;
     p_alfa1 = #0Campo;
     salidas  = FSalida;
     |HAZ punto;
     |SI p_error ! 0;  |ERROR;  |ACABA;  |FINSI;
     |PINTA #0Campo;
     |SI #0Campo = "            ";  |ACABA;  |FINSI;

     |HAZ SacaNivel;
     digito = Campo + 2;
     #0digito = enNivel;
|FCAL;

|| ****************************************************************
|PROCESO conexi;
  d_mes = #0#12;
  h_mes = #0#13;
  estru = #0#11;
  r_emp = empre;
  r_per = ejerc;
  r_eje = #30#26;
  r_nom = #30#1;
  |SUB_EJECUTA "caexicon";
|FPRC;

|| ------------------------------------------------------------------------
|PROCESO GrabaError;
  nNumero = nNumero + 1;
  #965#0 = nNumero;
  #965#1 = aError;
  #965#2 = nError;
  |GRABA 020#965n;

  aAlfa3 = "";
  #65#0 = nError;
  |LEE 040#65=;
  |SI FSalida ! 0;
    || ... Errores de los asientos
      |SI nError = 1; aAlfa3 = "ASIENTOS CON ERROR EN LA CLAVE "; nSol1 = 1; nSol5 = 1; |FINSI;
      |SI nError = 2; aAlfa3 = "CUENTAS ERRONEAS EN ASIENTOS CONTABLES ";   nSol1 = 1; nSol5 = 1; |FINSI;
      |SI nError = 3; aAlfa3 = "CODIGO DEL CONCEPTO CON VALOR 000 EN APUNTES ";        nSol1 = 1; nSol5 = 1; |FINSI;
      |SI nError = 4; aAlfa3 = "ASIENTOS DESCUADRADOS EN EL DIARIO ";       nSol1 = 1; |FINSI;
      |SI nError = 5; aAlfa3 = aError;                                      nSol5 = 1; |FINSI;

      |SI nError = 10; aAlfa3 = "ERROR EN LAS CUENTAS DEL PLAN CONTABLE ";  nSol2 = 1; nSol5 = 1; |FINSI;
      |SI nError = 11; aAlfa3 = aError;  nSol2 = 1; nSol5 = 1; |FINSI;
      |SI nError = 12; aAlfa3 = "CUENTAS CON PASE DIRECTO MAL INFORMADAS";  nSol2 = 1; nSol5 = 1;|FINSI;
      |SI nError ] 13; |Y nError [ 16; aAlfa3 = aError; |FINSI;
      |SI nError = 14; aAlfa3 = "ERROR EN NIVELES DE LAS CUENTAS"; nSol5 = 1; nSol7 = 1; |FINSI;

      |SI nError = 6; aAlfa3 = "CUENTAS NO DEFINIDAS EN ESTRUCTURA BALANCE"; nSol3 = 1; |FINSI;
      |SI nError = 7; aAlfa3 = "CUENTA DE PERDIDAS Y GANANCIAS NO DEFINIDA EN ESTRUCTURA DE BALANCES"; nSol3 = 1; nSol4 = 1; |FINSI;
      |SI nError = 8; aAlfa3 = "CUENTAS EXISTENCIAS NO DEFINIDAS EN ESTRUCTURA BALANCE";  nSol3 = 1; nSol4 = 1; |FINSI;
      |SI nError = 9; aAlfa3 = "CTAS VARIACION EXIS. NO DEFINIDAS EN ESTRUCTURA BALANCE"; nSol3 = 1; nSol4 = 1; |FINSI;

      |SI nError = 17; aAlfa3 = "BALANCE DE SUMAS Y SALDOS DESCUADRADO. ";  nSol5 = 1; |FINSI;
      |SI nError = 18; aAlfa3 = "DIFERENCIA ENTRE EL ACTIVO Y EL PASIVO. "; nSol3 = 1; |FINSI;
      |SI nError = 19; aAlfa3 = "EN LA DEFINICION BALANCE 2008: CTAS CON SIGNO DIFERENTE AL ESTANDAR";  nSol3 = 1; |FINSI;

      |SI nError = 20; aAlfa3 = aError; nSol4 = 1; |FINSI;
      |SI nError = 21; aAlfa3 = "ERROR EN CTA PERDIDAS Y GANANCIAS DE ESTR.DESCARGA"; nSol4 = 1; |FINSI;
      |SI nError = 22; aAlfa3 = "ERROR EN CTA EXISTENCIAS DE ESTRUCTURA DESCARGA";    nSol4 = 1;|FINSI;
      |SI nError = 23; aAlfa3 = "ERROR EN CTA VARIACION EXISTENCIAS DE ESTR.DESCARGA"; nSol4 = 1;|FINSI;
      |SI nError = 24; aAlfa3 = "ERROR EN ACUMULADO IMPORTE EXISTENCIAS"; nSol4 = 1;|FINSI;
      |SI nError = 25; aAlfa3 = "CUENTAS EN DESCARGA CON NIVEL MAL INFORMADO. -- CORREGIDO"; nSol4 = 1; |FINSI;
      |SI nError = 26; aAlfa3 = "CUENTAS 6 Y 7 CON SALDOS DE APERTURA "; nSol1 = 1;|FINSI;
      |SI nError = 27; aAlfa3 = aError; |FINSI;
      |SI nError = 28; aAlfa3 = "ERRORES EN INGRESOS Y GASTOS IMPUTADOS AL PATRIMONIO NETO"; nSol7 = 1;|FINSI;
      |SI nError = 30; aAlfa3 = "LA CUENTA 129 TIENE SALDO DEL RESULTADO DEL EJERCICIO ANTERIOR "; nSol8 = 1; |FINSI;
      #65#1 = aAlfa3;
      |GRABA 020#65n;
 |FINSI;
|FPRC;
|| ------------------------------------------------------------------------
|PROCESO acortacuen;
  |SI nivel < enNivel;
      |SI nivel = 1;  traraf = 100 + dig4;  |FINSI;
      |SI nivel = 2;  traraf = 100 + dig5;  |FINSI;
      |SI nivel = 3;  traraf = 100 + dig6;  |FINSI;
      |SI nivel = 4;  traraf = 100 + dig7;  |FINSI;
      |SI nivel = 5;  traraf = 100 + dig8;  |FINSI;
      eaCuenta = eaCuenta % traraf;
      eaCuenta = (eaCuenta + "              ") % 112;
  |FINSI;
|FPRC;

|PROCESO buscacta;
 |SI enNivel = 0; |ACABA; |FINSI;

 |HAZ acortacuen;
 #1#0 = eaCuenta;
 #1#1 = nivel;
 |LEE 040#1=;
 |SI FSalida ! 0;
     nError = 10;
     aError = "NO EXISTE LA CUENTA DE MAYOR " + eaCuenta;
     swmalo = 1;
     |HAZ GrabaError;
 |FINSI;
|FPRC;

|PROCESO LeoCtaEst;
 nSwGraba = 0;
 nSaldo1  = 0;
                aAlfa4 = "CTA PERDIDAS Y GANANCIAS ";  nError = 21;
 |SI nExis = 2; aAlfa4 = "CTA EXISTENCIAS ";           nError = 22; |FINSI;
 |SI nExis = 3; aAlfa4 = "CTA VARIACION EXISTENCIAS "; nError = 23; |FINSI;
 |HAZ SacaNivel;
 |SI enNivel = 0;
     aError = aAlfa4 + "FUERA DE NIVEL EN DESCARGA: " + eaCuenta;
     swmalo = 1;
     |DDEFECTO #1;
     #1#0 = eaCuenta;
 |SINO;
     |SI enNivel ! nNivel;
         nSwGraba = 1;
         nNivel   = enNivel;
     |FINSI;
     |DDEFECTO #1;
     #1#0 = eaCuenta;
     #1#1 = nNivel;
     |LEE 001#1=;
     |SI FSalida ! 0;
         aError = "NO EXISTE " + aAlfa4+ ": " + eaCuenta;
         swmalo = 1;
     |SINO;
         |SI #1#3 = "N";
            aError = aAlfa4 + " SIN ASIENTO DIRECTO: " + eaCuenta;
            swmalo = 1;
         |FINSI;
     |FINSI;
  |FINSI;
  |SI swmalo = 1;
      |HAZ GrabaError;
  |SINO;
      |HAZ balance;
  |FINSI;
  |SI nExis = 2;
      nSaldo1 = (#1mesini ! EURODB);
  |SINO;
      varcamp = ((#0#4 * 3) + 11);
      |PARA x = #0#4; |SI x [ #0#5; |HACIENDO x = x + 1;  || Bucle de campos
            nSaldo1  = (nSaldo1 + #1varcamp) ! EURODB;
            varcamp = varcamp + 3;
      |SIGUE;
  |FINSI;
|FPRC;

|PROCESO GrabaAuxb2;
 |DDEFECTO #5;
 #5#1 = nExis;
 #5#5 = eaCuenta;
 |LEE 040#5=;
 |SI FSalida ! 0;
     |DDEFECTO #5;
     #5#1  = nExis;
     #5#5  = eaCuenta;
     #5#11 = nSaldo1;
     |GRABA 020#5c;
 |FINSI;
 #5#12 = #5#12 + nSaldo2;
 |GRABA 020#5a;
|FPRC;

|PROCESO RepasoLin;
  |SI #7#1 = 1;
      |SI #7#2 = doce; |Y #7#5 = doce; |Y #7#8 = doce;
          || ... No hay error
          |ACABA;
      |FINSI;
  |FINSI;

  nGrabaExis = 0;
  nExis = 2;
  eaCuenta = #7#2;
  nNivel   = #7#3;
  |HAZ LeoCtaEst;
  |SI nSwGraba = 1; nGrabaExis = 1; #7#3 = nNivel; |FINSI;

  nSaldo2 = #7mes_ini;
  |HAZ GrabaAuxb2;

  nSaldo2 = #7mes_ini - #7mes_fin;
  nExis = 3;
  eaCuenta = #7#5;
  nNivel   = #7#6;
  |HAZ LeoCtaEst;
  |SI nSwGraba = 1; nGrabaExis = 1; #7#6 = nNivel; |FINSI;
  |SI nSaldo2 < 0; nSaldo2 = 0; |FINSI;
  |HAZ GrabaAuxb2;

  nSaldo2 = #7mes_ini - #7mes_fin;
  eaCuenta = #7#8;
  nNivel   = #7#9;
  |HAZ LeoCtaEst;
  |SI nSwGraba = 1; nGrabaExis = 1; #7#9 = nNivel; |FINSI;
  |SI nSaldo2 > 0; nSaldo2 = 0; |FINSI;
  |HAZ GrabaAuxb2;

  |SI nGrabaExis = 1;
      |GRABA 020#7a;
      nError = 25;
      aError = "CUENTAS EN DESCARGA CON NIVEL MAL INFORMADO LINEA: " + #7#1;
      |HAZ GrabaError;
  |FINSI;
  |LIBERA #7;
|FPRC;

|PROCESO RepasoAcum;
  nExis   = #5#1;
  nSaldo1 = #5#11 ! EURODB;
  nSaldo2 = #5#12 ! EURODB;
  eaCuenta = #5#5;
  |SI nSaldo2 ! nSaldo1; |Y nExis = 2;
      nError = 24;
      aError = "ERROR ACUMULADO DE EXISTENCIAS INICIALES EN CUENTA " + eaCuenta;
      |HAZ GrabaError;
  |FINSI;
  |SI nSaldo2 ! nSaldo1; |Y nExis = 3; |Y nSaldo1 ! 0;
      nError = 24;
      aError = "ERROR ACUMULADO VARIACION DE EXISTENCIAS EN CUENTA " + eaCuenta;
      |HAZ GrabaError;
  |FINSI;
|FPRC;

|DEFBUCLE RepasoAcum;
 #5#1;
 ;
 INICIO;
 FINAL;
 ;
 NULL, RepasoAcum;
|FIN;

|PROCESO BuscaEnExis;
 nHayA = 1;
 |SI #7#2 = #1#0;
     nHayE = 1;
     |ERROR10;
 |FINSI;
|FPRC;

|DEFBUCLE BuscaEnExis;
 #7#2;
 ;
 #6#0, aDCuenta, 0;
 #6#0, aHCuenta, 9;
 e;
 NULL, BuscaEnExis;
|FIN;

|PROCESO PlanExistencias;
  nDebe  = 0;
  nHaber = 0;
  varcamp = ((#0#4 * 3) + 9);
  |PARA x = #0#4; |SI x [ #0#5; |HACIENDO x = x + 1;  || Bucle de campos
        nDebe   = (nDebe  + #1varcamp) ! EURODB;
        varcamp = varcamp + 1;      || Se situa en  el campo del haber
        nHaber  = (nHaber + #1varcamp) ! EURODB;
        varcamp = varcamp + 2;      || Se situa en el campo debe
  |SIGUE;

  |SI nDebe ! 0; |O nHaber ! 0;
     eaCuenta = #1#0 % 104;
     aDCuenta = (eaCuenta + "            ") % 112;
     aHCuenta = (eaCuenta + "999999999999") % 112;
     nHayA = 0;
     nHayE = 0;
     |BUCLE BuscaEnExis;
     |SI nHayA ! 0;
        || ... Existen de su grupo
        |SI nHayE = 0;
            nError = 22;
            aError = "CTA EXISTENCIAS NO INFORMADA EN LA ESTRUCTURA: " + #1#0;
            nSol4  = 1;
            |HAZ GrabaError;
        |FINSI;
     |FINSI;
  |FINSI;
|FPRC;

|DEFBUCLE PlanExistencias;
 #1#1;
 3;
 "3           ", 0,"S";
 "399999999999", 9,"S";
 e;
 NULL, PlanExistencias;
|FIN;

|PROCESO BuscaDeExis;
 |SI #7#2 = doce; |ACABA; |FINSI;

 nHayA = 1;
 |SI #7#5 = #1#0; |O #7#8 = #1#0;
     nHayE = 1;
     |ERROR10;
 |FINSI;
|FPRC;

|DEFBUCLE BuscaDeExis;
 #7#1;
 ;
 #6#0, 01;
 #6#0, 99;
 e;
 NULL, BuscaDeExis;
|FIN;

|PROCESO PlanExVariacion;
  nDebe  = 0;
  nHaber = 0;
  varcamp = ((#0#4 * 3) + 9);
  |PARA x = #0#4; |SI x [ #0#5; |HACIENDO x = x + 1;  || Bucle de campos
        nDebe   = (nDebe  + #1varcamp) ! EURODB;
        varcamp = varcamp + 1;      || Se situa en  el campo del haber
        nHaber  = (nHaber + #1varcamp) ! EURODB;
        varcamp = varcamp + 2;      || Se situa en el campo debe
  |SIGUE;

  |SI nDebe ! 0; |O nHaber ! 0;
     eaCuenta = #1#0 % 104;
     aDCuenta = (eaCuenta + "            ") % 112;
     aHCuenta = (eaCuenta + "999999999999") % 112;
     nHayA = 0;
     nHayE = 0;
     |BUCLE BuscaDeExis;
     |SI nHayA ! 0;
        || ... Existen de su grupo
        |SI nHayE = 0;
            nError = 22;
            aError = "CTA VARIACION DE EX. NO INFORMADA EN LA ESTRUCTURA: " + #1#0;
            nSol4  = 1;
            |HAZ GrabaError;
        |FINSI;
     |FINSI;
  |FINSI;
|FPRC;

|DEFBUCLE PlanExVariacion;
 #1#1;
 3;
 aDCta, 0, "S";
 aHCta, 9, "S";
 e;
 NULL, PlanExVariacion;
|FIN;

|PROCESO ComprobarExis;
  aFichero = ("g" + Usuario) % 108;
  |NOME_DAT #5 aFichero;
  |DELINDEX #5;

  |ABRE #6;
  #6#0 = #0#11;
  |LEE 040#6=;
  |SI FSalida ! 0;
      nError = 20;
      aError = "NO EXISTE LA ESTRUCTURA DE DESCARGA.";
      nSol4  = 1;
      |HAZ GrabaError;
      |CIERRA #6;
      |ACABA;
  |FINSI;

  |ABRE #1;
  cue_perdi = #6#2; |QBF cue_perdi;
  dig_perdi = #6#3;
  |SI cue_perdi = "";
      nError = 21;
      aError = "CUENTA PERDIDAS Y GANANCIAS EN BLANCO.";
      |HAZ GrabaError;
  |SINO;
      nExis = 1;
      eaCuenta = cue_perdi;
      nNivel   = dig_perdi;
      |HAZ LeoCtaEst;
      |SI nSwGraba = 1;
          #6#3 = nNivel;
          |GRABA 020#6a;
          nError = 25;
          aError = "CUENTAS EN DESCARGA CON NIVEL MAL INFORMADO. -- CORREGIDO";
          |HAZ GrabaError;
      |FINSI;
  |FINSI;
  |LIBERA #6;
  |CIERRA #6;

  |ABRE #5;
  |BUCLELIN 0RepasoLin#6;
  |CIERRA #5;
  |CIERRA #1;

  |BUCLE RepasoAcum;

  |BUCLE PlanExistencias;


  aDCta = "6100        ";
  aHCta = "619999999999"; |BUCLE PlanExVariacion;

  aDCta = "7100        ";
  aHCta = "719999999999"; |BUCLE PlanExVariacion;
|FPRC;
|| ********************************************************************
|| .... Cuentas del grupo 8 y 9
|PROCESO LeoCta8y9;
 nSwGraba = 0;
 swmalo = 0;
                aAlfa4 = "CTA DE INGRESOS O GASTOS ";  nError = 28;
 |SI nExis = 2; aAlfa4 = "CONTRAPARTIDOA DE ING./GA";  |FINSI;

     |DDEFECTO #1;
     #1#0 = eaCuenta;
     |LEE 001#1=;
     |SI FSalida ! 0;
         aError = "NO EXISTE " + aAlfa4+ ": " + eaCuenta;
         swmalo = 1;
     |SINO;
         |SI #1#3 = "N";
            aError = aAlfa4 + " SIN ASIENTO DIRECTO: " + eaCuenta;
            swmalo = 1;
         |FINSI;
     |FINSI;

  |SI swmalo = 1;
      |HAZ GrabaError;
  |SINO;
      |HAZ balance;
  |FINSI;
|FPRC;

|PROCESO Repas_reparto;
   nError = 28;
   swmalo = 0;
   |SI #12#1 ! #12#3;
       |SI #12#2 = 0; |Y #12#4 = 1;
           aError = "No existe Cuenta en Plan Contable " + #12#0;
       |FINSI;
       |SI #12#2 = 1; |Y #12#4 = 0;
           aError = "No existe Reparto de la Cuenta  " + #12#0;
       |FINSI;
       |SI #12#2 = 1; |Y #12#4 = 1;
           aError = "Importe Reparto incorrecto de la Cuenta " + #12#0;
       |FINSI;
       swmalo = 1;
       |HAZ GrabaError;
   |FINSI;
|FPRC;

|DEFBUCLE Repas_reparto;
   #12#1;
   ;
   "            ";
   "zzzzzzzzzzzz";
   ;
   NULL, Repas_reparto;
|FIN;

|PROCESO Grabaw3;
 |DDEFECTO #12;
 #12#0 = eaCuenta;
 |LEE 001#12=;
 |SI FSalida ! 0;
     #12#0 = eaCuenta;
     |GRABA 020#12c;
 |FINSI;

 |SI nSwOp = 1;
     #12#1 = nImporte;
     #12#2 = 1;
 |SINO;
     #12#3 = nImporte;
     #12#4 = 1;
 |FINSI;
 |GRABA 020#12a;
|FPRC;

|PROCESO reparto_lin;
  aAlfa1 = #432#2; |QBF aAlfa1;
  |SI aAlfa1 ! "";
      eaCuenta  = #432#2;
      nExis     = 2;
      |HAZ LeoCta8y9;
  |FINSI;
|FPRC;

|DEFBUCLE reparto_lin;
  #432#1;
  ;
  #431#0, 01;
  #431#0, 99;
  e;
  NULL, reparto_lin;
|FIN;

|PROCESO reparto;
    nExis = 1;
    eaCuenta = #431#0;
    |HAZ LeoCta8y9;

    |SI #431#8 = "D";
        nImporte = #431#4;
    |SINO;
        nImporte = - #431#4;
    |FINSI;
    nSwOp = 2;
    |HAZ Grabaw3;

    aAlfa1 = #431#2; |QBF aAlfa1;
    |SI aAlfa1 ! "";
        nExis = 2;
        eaCuenta  = #431#2;
        |HAZ LeoCta8y9;
    |SINO;
        |BUCLE reparto_lin;
    |FINSI;
|FPRC;

|DEFBUCLE reparto;
  #431#1;
  ;
  "            ";
  "zzzzzzzzzzzz";
  e;
  NULL, reparto;
|FIN;

|PROCESO MiroSiHay;
  |SI nHay = -1; nHay = 0; |FINSI;
  nNume1 = 0;
  |PARA nPara1 = 9; |SI nPara1 [ 47; |HACIENDO nPara1 = nPara1 + 1;
        |SI #1nPara1 ! 0; nNume1 = 1; |SAL; |FINSI;
  |SIGUE;
  |SI nNume1 ! 0;  nHay = 1; |FINSI;

  nSwOp = 1;
  eaCuenta = #1#0;
  nImporte = #1#53 - #1#50;
  |HAZ Grabaw3;
|FPRC;

|DEFBUCLE MiroSiHay;
   #1#1;
   3;
   "8           ", 0, "S";
   "999999999999", 9, "S";
   ;
   NULL, MiroSiHay;
|FIN;

|PROCESO Comprobar8y9;

  aFichero = ("i" + Usuario) % 108;
  |NOME_DAT #12 aFichero;
  |DELINDEX #12;

  |ABRE #12;
  nHay = -1;
  |BUCLE MiroSiHay;

  |ABRE #1;
  |BUCLE reparto;
  |CIERRA #1;
  |CIERRA #12;

  |BUCLE Repas_reparto;
|FPRC;
|| ********************************************************************

|| ------------------------------------------------------------------------
|PROCESO haycambio;
  |SI antdoc = 0; || Documentos 0
      nError = 1;
      aAlfa2 = antdia; aAlfa2 = ("00" + aAlfa2) % -102;
      aAlfa3 = antfec;
      aAlfa2 = aAlfa2 + "-" + aAlfa3;
      aAlfa3 = antdoc; aAlfa3 = ("000000" + aAlfa3) % -106;
      aAlfa2 = aAlfa2 + "-" + aAlfa3;
      aError = "DOCUMENTO IGUAL A CERO " + aAlfa2;
      swmalo = 1;
      |HAZ GrabaError;
  |FINSI;
  |SI debe_p ! haber_p;
      nError = 4;
      aAlfa2 = antdia; aAlfa2 = ("00" + aAlfa2) % -102;
      aAlfa3 = antfec;
      aAlfa2 = aAlfa2 + "-" + aAlfa3;
      aAlfa3 = antdoc; aAlfa3 = ("000000" + aAlfa3) % -106;
      aAlfa2 = aAlfa2 + "-" + aAlfa3;
      aError = "ASIENTO DESCUADRADO: "+ aAlfa2;
      nError = 4;
      swmalo = 1;
      |HAZ GrabaError;
  |FINSI;
  |SI antapu > 9999; || mas de 9999 lineas
      nError = 1;
      aAlfa2 = antdia; aAlfa2 = ("00" + aAlfa2) % -102;
      aAlfa3 = antfec;
      aAlfa2 = aAlfa2 + "-" + aAlfa3;
      aAlfa3 = antdoc; aAlfa3 = ("000000" + aAlfa3) % -106;
      aAlfa2 = aAlfa2 + "-" + aAlfa3;
      aError = "ASIENTO CON MAS DE 9999 APUNTES " + aAlfa2;
      swmalo = 1;
      |HAZ GrabaError;
  |FINSI;
  |SI antdia = 0; |Y antfec ! fec1;
      nError = 1;
      aAlfa2 = antdia; aAlfa2 = ("00" + aAlfa2) % -102;
      aAlfa3 = antfec;
      aAlfa2 = aAlfa2 + "-" + aAlfa3;
      aAlfa3 = antdoc; aAlfa3 = ("000000" + aAlfa3) % -106;
      aAlfa2 = aAlfa2 + "-" + aAlfa3;
      aError = "ASTO EN DIARIO 00 CON FECHA DIFERENTE A APERTURA " + aAlfa2;
      swmalo = 1;
      |HAZ GrabaError;
 |FINSI;

  antdia  = #11#0;
  antfec  = #11#1;
  antdoc  = #11#2;
  debe_p  = 0;
  haber_p = 0;
  swmalo  = 0;
|FPRC;

|PROCESO LeoCamaasil;

    nSwGraba = 0;
    swmalo   = 0;
    aAlfa1 = #11#0; aAlfa1 = ("00" + aAlfa1) % -102;
    aAlfa2 = #11#1;
    aAlfa1 = aAlfa1 + "-" + aAlfa2;
    aAlfa2 = #11#2; aAlfa2 = ("000000" + aAlfa2) % -106;
    aAlfa1 = aAlfa1 + "-" + aAlfa2;
    aAlfa2 = #11#3; aAlfa2 = ("0000" + aAlfa2) % -104;
    aAlfa1 = aAlfa1 + "-" + aAlfa2;

    |SI #11#1 < fec1;                              || Fecha
        nError = 1;
        aError = "FECHA ASTO INFERIOR AL EJERCICIO CONTABLE: "+ aAlfa1;
        swmalo = 1;
    |FINSI;
    |SI #11#1 > fec2;
        nError = 1;
        aError = "FECHA ASTO SUPERIOR AL EJERCICIO CONTABLE: " + aAlfa1;
        swmalo = 1;
    |FINSI;
    |SI #11#3 = 0; |Y #11#2 ! 0;
        nError = 1;
        aError = "APUNTE IGUAL A CERO " + aAlfa1;
        swmalo = 1;
    |FINSI;
    |SI swmalo = 1;  |HAZ GrabaError; |ACABA; |FINSI;

    |SI #11#1 < fecini; |O #11#1 > fecfin; |ACABA; |FINSI;

    |SI sw = 0;
        sw = 1;
        antdia  = #11#0;
        antfec  = #11#1;
        antdoc  = #11#2;
        debe_p  = 0;
        haber_p = 0;
        swmalo  = 0;
        antapu  = 0;
    |FINSI;

    |SI #11#0 ! antdia;  |O #11#1 ! antfec;  |O #11#2 ! antdoc;
        |HAZ haycambio;
    |FINSI;

    |SI #11#6 = 0;
        nError = 3;
        aError = "CONCEPTO CONTABLE IGUAL A CERO: " + aAlfa1;
        swmalo = 1;
        |HAZ GrabaError;
    |FINSI;

    eaCuenta = #11#4; |QBF eaCuenta;
    |SI eaCuenta = "";
        nError = 2;
        aError = "CUENTA EN BLANCO EN EL APUNTE: " + aAlfa1;
        swmalo = 1;
    |SINO;
        |HAZ SacaNivel;
        |SI enNivel = 0;
            nError = 2;
            aError = "CUENTA FUERA DE NIVEL EN EL APUNTE: " + aAlfa1;
            swmalo = 1;
        |SINO;
            |SI enNivel ! #11#5;
                #11#5    = enNivel;
                nError4  = 1;
                nSwGraba = 1;
            |FINSI;
            #1#0 = #11#4;
            #1#1 = #11#5;
            |LEE 001#1=;
            |SI FSalida ! 0;
                nError = 2;
                aError = "NO EXISTE CUENTA EN EL APUNTE: " + aAlfa1;
                swmalo = 1;
            |SINO;
                |SI #1#3 = "N";
                    nError = 2;
                    aError = "CUENTA SIN ASIENTO DIRECTO EN EL APUNTE: " + aAlfa1;
                    swmalo = 1;
                |FINSI;
              |FINSI;
        |FINSI;
     |FINSI;

     |SI swmalo = 1;
         |HAZ GrabaError;
     |FINSI;

     |SI nivel ! dig3;
         |HAZ buscacta;
     |FINSI;

     |SI #11#8 = "D";
         nDebeAsto = (nDebeAsto + #11#9) ! EURODB;
         debe_p    = (debe_p + #11#9) ! EURODB;
     |SINO;
         nHaberAsto = (nHaberAsto + #11#9) ! EURODB;
         haber_p    = (haber_p + #11#9) ! EURODB;
     |FINSI;

     |SI eaCuenta ] #0#18; |Y eaCuenta [ #0#19;
         |SI #11#8 = "D";
             nDebeTotal = (nDebeTotal + #11#9) ! EURODB;
         |SINO;
             nHaberTotal = (nHaberTotal + #11#9) ! EURODB;
         |FINSI;
     |FINSI;

   |SI nSwGraba = 1;
       |GRABA 020#11a;
   |FINSI;
   |LIBERA #11;
   antapu = #11#3;
|FPRC;

|DEFBUCLE LeoCamaasil;
 #11#1;
 ;
 nDDiario, "01.01.0000", 000000, 0000;
 nHDiario, "31.12.9999", 999999, 9999;
 be;
 NULL, LeoCamaasil;
|FIN;

|| -------------------------------------------------------------------------
|PROCESO MiraSuper0;
 |SI #901#0 ! #1#0;
     nSuper = 1;
     |ERROR10;
 |FINSI;
|FPRC;

|DEFBUCLE MiraSuper;
 #901#1002;
 3;
 aDCuenta, 0, "S";
 aHCuenta, 9, "S";
 ;
 NULL, MiraSuper0;
|FIN;

|PROCESO MiraSuper;
  aAlfa1 = #1#0; |QBF aAlfa1;
  |SI aAlfa1 = ""; |ACABA; |FINSI;

  nSuper = 0;
  aDCuenta = #1#0;
  aHCuenta = #1#0; |QBF aHCuenta;
  aHCuenta = (aHCuenta + "9999999999999") % 112;
  |BUCLE MiraSuper;
  |SI nSuper = 1;
      nError = 12;
      aError = "LA CUENTA " + #1#0+ " ES DE PASE DIRECTO N. CORREGIDO";
      #1#3   = "N";
      nSwGraba = 1;
      |HAZ GrabaError;
  |FINSI;
|FPRC;

|PROCESO balance;
   nCam4  = #1#4; aCam5 = #1#5;
   nCam6  = #1#6; nCam7 = #1#7;
   nCam8  = #1#8;
   nMensa = 1;
   |HAZ defect;
   |SI #0#1 = 2;
       |SI nNoBal = 1;
           nError = 6 + nExis;
           aError = "NO EXISTE EN BALANCES 1990 LA CUENTA: " + #1#0;
           |HAZ GrabaError;
           swmalo = 1;
       |SINO;
           |SI #1#4 ! nCam4; |O #1#5 ! aCam5; |O #1#6 ! nCam6;
             |O #1#7 ! nCam7; |O #1#8 ! nCam8;
                 nError1  = 1;
                 nSwGraba = 1;
           |FINSI;
       |FINSI;
   |FINSI;

   |SI #0#1 = 1;
       |SI nNoBal8 = 1;
           nError = 6 + nExis;
           aError = "NO EXISTE EN BALANCES 2008 LA CUENTA: " + #1#0;
           |HAZ GrabaError;
       |SINO;

           |SI #0#2 ! 0;
               aCampo10_n = aCampo10;
               nDBal = 0; nHBal = 0;
               |HAZ defect;
               |SI nNoBal8 = 0; |Y aCampo10 ! aCampo10_n;
                  |SI enActivo ! enPasivo;
                      nError = 19;
                      aError = "AVISO. SIGNO " + aCampo10_n + " EN BALANCE 2008 CUENTA: " + #1#0;
                      |HAZ GrabaError;
                  |FINSI;
                  nError5 = 1;
               |FINSI;
               nDBal = #0#2; nHBal = #0#2;
           |FINSI;
       |FINSI;
   |FINSI;
|FPRC;

|PROCESO LeoCuentas;
  nMovi  = 0;
  nExis  = 0;
  swmalo = 0;
  aAlfa1 = #1#0; |QBF aAlfa1;

  eaCuenta = #1#0;
  |HAZ SacaNivel;
  |SI enNivel = 0; |Y aAlfa1 ! "";
      nError = 10;
      aError = "LONGITUD DE CUENTA FUERA DE NIVEL " + eaCuenta;
      swmalo = 1;
      |HAZ GrabaError;
      |ACABA;
  |FINSI;

  nSwGraba = 0;
  |SI enNivel ! #1#1; |Y aAlfa1 ! "";
      nError = 14;
      aError = "ERROR EN NIVEL DE LA CUENTA: " + eaCuenta;
      |HAZ GrabaError;
      |ACABA;
  |FINSI;

  |SI aAlfa1 ! "";
     mascara = #1#0; |QBF mascara;
     mascara = (mascara + "zzzzzzzzzzzz") % 112;
     |SI mascara ! #1#54; |O #1#55 ! #1#1;
         nError3 = 1;
         #1#54 = mascara;
         #1#55 = #1#1;
         nSwGraba = 1;
     |FINSI;
  |FINSI;

  |SI #1#3 ! "S"; |Y #1#3 ! "N";
      nError = 12;
      aError = "LA CUENTA " + #1#0+ " MAL INFORMADO CAMPO: PASE DIRECTO. CORREGIDO";
      #1#3   = #1#3 > "";
      |SI #1#3 ! "S"; #1#3 = "N"; |FINSI;
      nSwGraba = 1;
      |HAZ GrabaError;
  |FINSI;

  aAlfa1 = #1#0 % 103;
  |SI aAlfa1 = "129"; |Y #1#3 = "S";
      nNume1 = #1#53 - #1#50;
      |SI nNume1 ! 0;
          nHay129 = 1;
      |FINSI;
  |FINSI;

  aAlfa1 = #1#0 % 101;
  |SI aAlfa1 = "6"; |O aAlfa1 = "7";
      |SI #1#9 ! 0; |O #1#10 ! 0; |O #1#11 ! 0;
          nError6 = 1;
      |FINSI;
  |FINSI;

  nSuma = 0;
  |SI #1#1 < nivel; |Y #1#3 = "N";
      nSuma = 1;
  |FINSI;
  |SI #1#1 > nivel; nSuma = 1; |FINSI;

  |SI aAlfa1 = ""; |Y #1#3 = "S";
      |SI #1#53 ! 0;
          nError = 11;
          aError = "CUENTA CONTABLE EN BLANCO CON SALDO";
          swmalo = 1;
          |HAZ GrabaError;
          |ACABA;
      |FINSI;
  |FINSI;

  |SI aPrimera = "";
      aPrimera = #1#0;
  |FINSI;
  aUltima = #1#0;

  |SI nSuma = 0;
      varcamp = ((#0#4 * 3) + 9);
      |PARA x = #0#4; |SI x [ #0#5; |HACIENDO x = x + 1;  || Bucle de campos

            nDebeCta  = (nDebeCta + #1varcamp) ! EURODB;
            |SI #1varcamp ! 0; nMovi = 1; |FINSI;

            varcamp = varcamp + 1;      || Se situa en  el campo del haber

            nHaberCta = (nHaberCta + #1varcamp) ! EURODB;
            |SI #1varcamp ! 0; nMovi = 1; |FINSI;

            varcamp = varcamp + 2;      || Se situa en el campo debe
      |SIGUE;
  |FINSI;

  |SI #1#3 = "S"; |Y #1#1 < nivel;
      |HAZ MiraSuper;
  |FINSI;

  |SI #0#0 ! 1; |Y nSuma = 0; |Y nMovi ! 0;
      |HAZ balance;
      |SI #0#1 = 1;
          aAlfa1 = #1#0 % 101;
          |SI aAlfa1 ] "1"; |Y  aAlfa1 [ "5"
              |SI aCam5 ! "A"; |Y aCam5 ! "P";
                  nError1  = 1;
                  nSwGraba = 1;
              |FINSI;
          |FINSI;
          |SI aAlfa1 = "6"; |O  aAlfa1 = "7"
              |SI aCam5 ! "D"; |Y aCam5 ! "H";
                  nError1  = 1;
                  nSwGraba = 1;
              |FINSI;
          |FINSI;
          |SI aAlfa1 = "8"; |O  aAlfa1 = "9"
              |SI aCam5 ! "I"; |Y aCam5 ! "G";
                  nError1  = 1;
                  nSwGraba = 1;
              |FINSI;
          |FINSI;
     |FINSI;
  |FINSI;

  |SI nSwGraba = 1;
      |GRABA 020#1a;
  |FINSI;
  |LIBERA #1;
|FPRC;

|DEFBUCLE LeoCuentas;
   #1#1;
   ;
   #0#18,0;
   #0#19,9;
   be;
   NULL, LeoCuentas;
|FIN;

|PROCESO Chequeo3; |TIPO 3;

 |HAZ eVarEmpresa;
 |HAZ LaFechaEmp;

 |SI #0#17 = "S"; |HAZ conexi; |FINSI;

  nErrorDiario = 0;
  nSol1 = 0;
  nSol2 = 0;
  nSol3 = 0;
  nSol4 = 0;
  nSol5 = 0;
  nSol6 = 0;
  nSol7 = 0;
  nSol8 = 0;
  nSol9 = 0;
  mes_ini = #0#12 + 11; mesini = (#0#12 * 3) + 11;
  mes_fin = #0#13 + 11; mesfin = (#0#13 * 3) + 11;
  |SI #0#0 = 1;
      nivel = #0#22;
  |SINO;
      |SI #0#1 = 2;
          nivel = #0#10;
      |SINO;
          nivel = dig3;
      |FINSI;
      #0#18 = "            ";
      #0#19 = "999999999999";
  |FINSI;

  |DBASE aMas;
  aDef = aMas + "def/cacuenta";
  |CARGA_DEF aDef, cuentas@;
  |SI FSalida ! 0;
      |MENAV "    Error al Carga fichero de Cuentas";
      |ACABA;
  |FINSI;

  |DBASE aMas;
  aDef = aMas + "def/caw00011";
  |CARGA_DEF aDef, inciden@;
  |SI FSalida ! 0;
      |DESCARGA_DEF #cuentas@;
      |MENAV "    Error al Carga fichero de Incidencias";
      |ACABA;
  |FINSI;

  aFichero = ("e" + Usuario) % 108;
  |NOME_DAT #65  aFichero;
  |NOME_DAT #911 aFichero;
  |DELINDEX #65;
  aFichero = ("f" + Usuario) % 108;
  |NOME_DAT #965 aFichero;
  |DELINDEX #965;

  nDDiario = 0; nHDiario = 99;
  |SI #0#4 > 0; nDDiario  =  1; |FINSI;
  |SI #0#6 < 13; nHDiario = 98; |FINSI;

  |ABRE #65;
  |ABRE #965;
  |HAZ VeoCuadre;
  |SI nError = 0;
    ||  |CONFI "    SBalance Cuadrado. ¨Desea lanzar el Chequeo?";
    ||  |SI FSalida ! 0;
    ||      |VETE Fin;
    ||  |FINSI;
  |SINO;
      |HAZ PantallaDes;
  |FINSI;

  nDebeAsto   = 0;
  nHaberAsto  = 0;
  nDebeTotal  = 0;
  nHaberTotal = 0;
  nDebeCta    = 0;
  nHaberCta   = 0;

|| ............. Primer Paso, comprobar los asientos contables
  nSwCier = 0;
  |ABRE #11;
  #11#0 = 99;
  #11#1 = fec1;
  #11#2 = 1;
  |LEE 041#11];
  |SI FSalida = 0;
      nSwCier = 1;
  |FINSI;
  |CIERRA #11;

  sw      = 0;
  nNumero = 0;
  nError  = 0;  ||Tipo de Error

  nError4 = 0;
  nError5 = 0;
  |ABRE #1;
  |BUCLE LeoCamaasil;
  |SI sw = 1;
      |HAZ haycambio;
  |SINO;
      nErrorDiario = 1;
      |CONFI "    NNO EXISTEN ASIENTOS EN EL PERIODO SELECCIONADO. CONTINUAR ";
      |SI FSalida ! 0;
          |CIERRA #1;
          |VETE Fin;
      |FINSI;
  |FINSI;
  |CIERRA #1;
  |SI nError4 = 1;
      nError = 5;
      aError = "NIVEL DE CUENTAS MAL INFORMADO EN APUNTES. ";
      |HAZ GrabaError;
  |FINSI;

|| ............. Segundo Paso, comprobar Plan Contable
  nDBal    = #0#2;
  nHBal    = #0#2;
  aPrimera = "";
  aUltima  = "";
  nError1  = 0;
  nError2  = 0;
  nError3  = 0;
  nError6  = 0;
  nHay129  = 0;
  |BUCLE LeoCuentas;
  |SI nError3 = 1;
      nError = 13;
      aError = "ERROR EN LA TERCERA CLAVE DEL PLAN CONTABLE. -- CORREGIDO";
      |HAZ GrabaError;
  |FINSI;
  |SI nError6 = 1;
      nError = 26;
      aError = "ERROR CUENTAS GRUPO 6 Y 7 CON SALDOS APERTURA";
      |HAZ GrabaError;
  |FINSI;
||  |SI nError2 = 1;  en agicont no puedo graba la cuenta
||      nError = 14;
||      aError = "ERROR EN NIVELES DE CUENTAS. -- CORREGIDO";
||      nSol5  = 1;
||      |HAZ GrabaError;
||  |FINSI;
  |SI nError1 = 1;
      |SI #0#1 = 2;
          nError = 15;
          aError = "CUENTAS QUE NO CORRESPONDEN A BALANCES 1990. -- CORREGIDO";
          |HAZ GrabaError;
      |SINO;
          nError = 27;
          aError = "CUENTAS QUE NO CORRESPONDEN AL TIPO EN BALANCES. -- CORREGIDO";
          |HAZ GrabaError;
      |FINSI;
  |FINSI;

  |SI nError5 = 1;
      || nError = 19;
      || aError = "EN LA DEFINICION DEL BALANCE 2008: CUENTAS CON SIGNO DIFERENTE AL ESTANDAR";
      || |HAZ GrabaError;
  |FINSI;

  |SI nHay129 ! 0; |Y nSwCier ! 0;
      nError = 30;
      aError ="Cta 129 con Saldo. Emp.Cerrada y NO existe Asto Distribucion";
      |HAZ GrabaError;
  |FINSI;

  |SI nDebeTotal ! nDebeCta; |O nHaberTotal ! nHaberCta;
      |SI nErrorDiario = 0;
          nError = 16;
          aError = "ERROR: LOS ACUMULADOS DE LAS CUENTAS DISTINTOS A LOS DEL DIARIO";
          nSol5  = 1;
          |HAZ GrabaError;
      |FINSI;
  |FINSI;

  |SI #0#18 [ aPrimera; |Y #0#19 ] aUltima;
      |SI nDebeCta ! nHaberCta;
          nNume2 = nDebeCta - nHaberCta;
          nError = 17;
          aError = "DESCUADRE: Total Debe: " + nDebeCta + " Total Haber: " + nHaberCta + " Dif: " + nNume2;
          |HAZ GrabaError;
      |FINSI;
  |FINSI;

|| ... Comprobar las estructuras de Existencias
  |SI #0#0 ! 1;
      |HAZ ComprobarExis;
  |FINSI;

|| ... Comprobar el Reparto de las Cuentas 8 y 9
  |SI #0#0 ! 1;
      |HAZ Comprobar8y9;
  |FINSI;

  |SI #0#0 ! 1;
      |SI enPasivo ! enActivo;
          |SI nNumero = 0; nSol6 = 1; |FINSI;
          nNume2 = enActivo - enPasivo;
          nError = 18;
          aError = "DESCUADRE: Activo: " + enActivo + " Pasivo: " + enPasivo + " Dif: " + nNume2;
          |HAZ GrabaError;
      |FINSI;
  |FINSI;
  |SI nErrorDiario = 1; nSol5 = 0; |FINSI;
  nErrorDiario = 0;

|ET Fin;
  |CIERRA #65;
  |CIERRA #965;

  |SI nNumero ! 0;
      |HAZ ErrorBalances;
  |SINO;
      |SI nErrorDiario = 0; |MENAV "    SIN INCIDENCIAS"; |FINSI;
      |SI nHay129 ! 0; |Y nSwCier = 0;
          |MENAV "0000La cuenta 129 tiene saldo. Recuerde hacer el asiento de distribucion antes del cierre";
      |FINSI;
  |FINSI;
  |DESCARGA_DEF #inciden@;
  |DESCARGA_DEF #cuentas@;
|FPRC;

||**************************************************************************
|PROCESO LosErrores_d;
 eaText = #965#1;
 |PRINT;
 nLinea = 3;
|FPRC;

|DEFBUCLE LosErrores_d;
 #965#2;
 ;
 nError,000001;
 nError,999999;
 ;
 NULL, LosErrores_d;
|FIN;

|PROCESO LosErrores;
 eaText = #911#1;
 |PRINT;
 nLinea = 1;
 nError = #911#0;
 |SI nInkey ! 13; |BUCLE LosErrores_d; |FINSI;
 nLinea = 2;
|FPRC;

|DEFBUCLE LosErrores;
 #911#1001;
 ;
 nDNumero;
 nHNumero;
 ;
 NULL, LosErrores;
|FIN;

|PROCESO LasSol;
   |SI nSol6 ! 0;
       eaText ="EL ACTIVO Y EL PASIVO ESTAN DESCUADRADOS Y NO HAY INCIDENCIAS. ";
       |PRINT;
       nLinea = 5;
       eaText = "EL ERROR PUEDE ESTAR EN LA DEFINICION DE LA ESTRUCTURA DEL BALANCE.";
       |PRINT;
    |FINSI;
    eaText  = " PARA SOLUCIONAR LOS PROBLEMAS:";
    |PRINT;
    nLinea = 5;
    |SI nSol1 = 1;
        eaText= "- MODIFIQUE LOS ASIENTOS POR ENTRADA DE APUNTES";
        |PRINT;
    |FINSI;
    |SI nSol2 = 1;
        eaText = "- MODIFIQUE LAS CUENTAS ERRONEAS Y LANCE EL TESTEO DEL PLAN CONTABLE";
        |PRINT;
    |FINSI;
    |SI nSol7 = 1;
        eaText = "- LANCE EL LIMPIAR PLAN CONTABLE";
        |PRINT;
    |FINSI;
    |SI nSol3 = 1;
        |SI #0#1 = 2;  eaText = "- REVISE LAS ESTRUCTURAS DE BALANCE Y LANCE EL TESTEO DEL PLAN CONTABLE"; |FINSI;
        |SI #0#1 = 1;  eaText = "- REVISE LAS ESTR. DE BALANCE Y LANCE EL CHEQUEO PLAN C. Y ESTR. BALANCE 2008"; |FINSI;
        |PRINT;
        |SI nSol6 = 1; eaText = "- (Puede recuperar la estructura standar del programa)"; |PRINT; |FINSI;
    |FINSI;
    |SI nSol4 = 1;
        eaText = "- REVISE LAS ESTRUCTURAS DE EXISTENCIAS ";
        |PRINT;
    |FINSI;
    |SI nSol5 = 1;
        eaText = "- LANCE LA RECUPERACION DE SALDOS CONTABLES";
        |PRINT;
    |FINSI;
    |SI nSol8 = 1;
        eaText = "- REVISE CONF.CIERRE INGRESOS Y GASTOS A PAT.NETO";
        |PRINT;
    |FINSI;
    |SI nSol9 = 1;
        |SI nSwCier = 1;
            eaText = "- ANULE EL CIERRE REALIZADO                      ";
            |PRINT;
        |FINSI;
        eaText = "- REALICE ASTO DISTRIBUCION DE RESULTADOS EJ.ANT.";
        |PRINT;
    |FINSI;
|FPRC;

|PROCESO Imprimir;
   nDNumero = 000001;
   nHNumero = 999999;
   |SI nInkey = 11;
       nDNumero = #65#0;
       nHNumero = #65#0;
   |FINSI;

   |IMPRE 0;
   |SI FSalida = 0;
       |INFOR informe;
       |SI FSalida = 0;
           nLinea = 0;
           |BUCLE LosErrores;
           |SI nInkey = 12;
               nLinea = 4;
               |HAZ LasSol;
           |FINSI;
           |PIEINF;
           |FININF;
       |SINO;
            |MENAV "    No existe Informe";
       |FINSI;
       |FINIMP;
    |SINO;
       |MENAV "    Proceso abortado";
    |FINSI;
|FPRC;

|PROCESO Max965;
 #965#0 = 1;
 #965#2 = nError;
|FPRC;

|PROCESO Min965;
 #965#0 = 999999;
 #965#2 = nError;
|FPRC;

|PROCESO Tipo11_w011; |TIPO 11;
 nInkey = FSalida;
 |BLIN 4;
 |SI nInkey = 10;
     |PUSHV 0501 2380;
     nError = #65#0;
     |PINPA #0#965;
     |ATRI S;
     |PINTA 0603, #65#1;
     |ENTLINEAL #2#965, -2, 5, 22, 0, Max965, Min965;
     |POPV;
     |ACABA;
 |FINSI;
 |SI nInkey = 11; |O nInkey = 12;
     |HAZ Imprimir;
 |FINSI;
 nInkey = 0;
|FPRC;

|PROCESO Tipo7_w011; |TIPO 7;
 |MENSA "    <F2> Ver Detalle Error, <F3> Imprimir Detalle Error, <F4> Imprimir Todo";
|FPRC;

|PROCESO Max65;
 #65#0 = 1;
|FPRC;

|PROCESO Min65;
 #65#0 = 999999;
|FPRC;

|PROCESO ErrorBalances;
      |SI nHay129 ! 0; |Y nSwCier = 0;
          |ABRE #965;
          |ABRE #65;
          nError = 30;
          aError ="La cuenta 129 tiene saldo. Recuerde hacer el asiento de distribucion antes del cierre";
          |HAZ GrabaError;
          |CIERRA #65;
          |CIERRA #965;
      |FINSI;

      |C_P #65#0,1,0603;
      |C_P #65#1,1,0610;

      aAlfa1 = " " * 80;
      |PINTA 0501, aAlfa1;
      |PINTA 0601, aAlfa1;
      |PINTA 0701, aAlfa1;
      |PINTA 0801, aAlfa1;
      |PINTA 0901, aAlfa1;
      |PINTA 1001, aAlfa1;
      |PINTA 1101, aAlfa1;
      |PINTA 1201, aAlfa1;
      |PINTA 1301, aAlfa1;
      |PINTA 1401, aAlfa1;
      |PINTA 1501, aAlfa1;
      |PINTA 1601, aAlfa1;
      |PINTA 1701, aAlfa1;
      |PINTA 1801, aAlfa1;
      |PINTA 1901, aAlfa1;
      |PINTA 2001, aAlfa1;
      |PINTA 2101, aAlfa1;
      |PINTA 2201, aAlfa1;
      |PINTA 2301, aAlfa1;

      |ATRI S;
      aAlfa1 = "ATENCION: BALANCE DESCUADRADO. ERRORES DETECTADOS";
      |PINTA 0503,aAlfa1;|ATRI 0;
      || aAlfa1 = "=" * 80;
      ||| PINTA 0601, aAlfa1;
      |CUADRO 0602 2380;
      |ATRI I;
      |PINTA 1503, "El detalle equivale a 'Testear Plan Contable` y 'Chequeo Asientos`";
      |ATRI S;
      nNume1 = 1604;
      |SI nSol6 ! 0;
          |PINTA 1604, "El activo y el pasivo estan descuadrados y no hay incidencias. ";
          |PINTA 1704, "El error puede estar en la definicion de la estructura balance ";
          |PINTA 2004, "- (Puede recuperar la estructura standar del programa)";
          nNume1 = 1804;
      |FINSI;
      |SI nSol1 ! 0; |O nSol2 ! 0; |O nSol3 ! 0; |O nSol4 ! 0; |O nSol5 ! 0; |O nSol7 ! 0; |O nSol8 ! 0; |O nSol9 ! 0;
          aAlfa1 = "Para solucionar los problemas:";
          |PINTA nNume1, aAlfa1;
          nNume1 = nNume1 + 100;
          |SI nSol1 = 1;
              aAlfa1 = "- Modifique los Asientos por Entrada de Apuntes";
              |PINTA nNume1, aAlfa1;
              nNume1 = nNume1 + 100;
         |FINSI;
         |SI nSol2 = 1;
              aAlfa1 = "- Modifique las Cuentas erroneas y lance el Testear Plan Contable";
              |PINTA nNume1, aAlfa1;
              nNume1 = nNume1 + 100;
         |FINSI;
         |SI nSol7 = 1;
              aAlfa1 = "- Lance el Limpiar Plan Contable";
              |PINTA nNume1, aAlfa1;
              nNume1 = nNume1 + 100;
         |FINSI;
         |SI nSol3 = 1;
              aAlfa1 = "- Revise las Estructuras de Balance ";
              |SI #0#1 = 2 ; aAlfa1 = "- Revise las Estr. Balance y lance el Testear Plan Contable"; |FINSI;
              |SI #0#1 = 1 ; aAlfa1 = "- Revise las Estr. Balance y lance el Chequeo Plan C. y Estr.Balance 2008"; |FINSI;
              |PINTA nNume1, aAlfa1;
              nNume1 = nNume1 + 100;
         |FINSI;
         |SI nSol4 = 1;
              aAlfa1 = "- Revise las Estructuras de Existencias ";
              |PINTA nNume1, aAlfa1;
              nNume1 = nNume1 + 100;
         |FINSI;
         |SI nSol5 = 1;
              aAlfa1 = "- Recalcule los Saldos Contable";
              |PINTA nNume1, aAlfa1;
              nNume1 = nNume1 + 100;
         |FINSI;
         |SI nSol8 = 1;
              aAlfa1 = "- Revise Conf.Cierre Ingresos y Gastos PN";
              |PINTA nNume1, aAlfa1;
              nNume1 = nNume1 + 100;
         |FINSI;
         |SI nSol9 = 1;
              |SI nSwCier = 1;
                  aAlfa1 = "- Anule Cierre. Realice Asto Distr.Resulta.";
              |SINO;
                  aAlfa1 = "- Realice Asto Distribucion Res. Ejer.Ant.";
              |FINSI;
              |PINTA nNume1, aAlfa1;
              nNume1 = nNume1 + 100;
         |FINSI;
      |FINSI;
      |ATRI 0;
      |ENTLINEAL #1#65, -1, 5, 14, 0, Max65, Min65;
|FPRC;

|| ***********************************************************************
|PROCESO PantallaDes;
 |BLIN 21;
 |BLIN 22;
 aAlfa1 = "BALANCE DE SUMAS Y SALDOS";
 |SI #0#0 = 2; aAlfa1 = "BALANCE DE SITUACION Y DE RESULTADOS"; |FINSI;
 aAlfa1 = aAlfa1 + " DESCUADRADO";
 |ATRI S;
 |PINTA 2103, aAlfa1;
 |ATRI 0;
 |SI #0#0 = 2;
     |PINTA 2203, "Realizando chequeo de Asientos, Cuentas y estructuras de descarga ... ";
 |SINO;
     |PINTA 2203, "Realizando chequeo de Asientos y Cuentas ... ";
 |FINSI;
|FPRC;

|PROCESO SumasySaldos;
  |SI #1#1 < nivel; |Y #1#3 = "N";
      |ACABA;
  |FINSI;
  |SI #1#1 > nivel; |ACABA; |FINSI;

  varcamp = ((#0#4 * 3) + 9);
  |PARA x = #0#4; |SI x [ #0#5; |HACIENDO x = x + 1;  || Bucle de campos
        nDebeCta  = (nDebeCta + #1varcamp) ! EURODB;
        varcamp = varcamp + 1;      || Se situa en  el campo del haber
        nHaberCta = (nHaberCta + #1varcamp) ! EURODB;
        varcamp = varcamp + 2;      || Se situa en el campo debe
  |SIGUE;
|FPRC;

|DEFBUCLE SumasySaldos;
   #1#1;
   ;
   #0#18, 0;
   #0#19, 9;
   e;
   NULL, SumasySaldos;
|FIN;

|PROCESO VeoCuadre;
  nError = 0;
  |SI #0#0 = 1;  ||Sumas y saldos.
      nDebeCta  = 0;
      nHaberCta = 0;
      |BUCLE SumasySaldos;
      |SI nDebeCta ! nHaberCta;
          nError = 1;
      |FINSI;
  |FINSI;
  |SI #0#0 = 2;  || ...  Situacion y Resultados
      |SI #0#1 = 2; ||Formato Plan 1990
          enCam0 = #0#4;
          enCam1 = #0#5;
          enCam6 = #0#11;
          enCam7 = #0#12;
          enCam8 = #0#13;
          |SUB_EJECUTA "cabatopa;Chequeo";
          enActivo = enActivo ! EURODB;
          enPasivo = enPasivo ! EURODB;
          |SI enActivo ! enPasivo; nError = 1; |FINSI;
          || .. |SI enIngresos ! enGastos; nError = 1; |FINSI;
      |FINSI;
      |SI #0#1 = 1;
          enCam0 = #0#4;
          enCam1 = #0#5;
          enCam2 = #0#2;
          enCam6 = #0#11;
          enCam7 = #0#12;
          enCam8 = #0#13;
          |SUB_EJECUTA "ca8ys001;Chequeo";
          enActivo = enActivo ! EURODB;
          enPasivo = enPasivo ! EURODB;
          |SI enActivo ! enPasivo; nError = 1; |FINSI;
          || .. |SI enIngresos ! enGastos; nError = 1; |FINSI;
      |FINSI;
  |FINSI;
|FPRC;

|PROCESO SacaNivel;
   aLong = eaCuenta;
   |QBF aLong;
   aLong = aLong % 0;
   nLong = aLong;
   enNivel = 0;
   |SI nLong = dig4; |Y dig4 ! 0; enNivel = 1; |FINSI;
   |SI nLong = dig5; |Y dig5 ! 0; enNivel = 2; |FINSI;
   |SI nLong = dig6; |Y dig6 ! 0; enNivel = 3; |FINSI;
   |SI nLong = dig7; |Y dig7 ! 0; enNivel = 4; |FINSI;
   |SI nLong = dig8; |Y dig8 ! 0; enNivel = 5; |FINSI;
   |SI nLong = dig9; |Y dig9 ! 0; enNivel = 6; |FINSI;
|FPRC;


|PROCESO LaFechaEmp;
    fecini = fec1;
    fecfin = fec2;
    |SI #0#4 ] 1;
        nMes = #0#4; |HAZ LaFecha;
        fecini = aLaFecha1;
    |FINSI;
    |SI #0#5 = 0;
        fecfin = fec1;
    |SINO;
        |SI #0#5 [ 12;
            nMes = #0#5; |HAZ LaFecha;
            fecfin = aLaFecha2;
        |FINSI;
    |FINSI;
|FPRC;

|PROCESO LaFecha;

  nElAny = enEjercicio;
  |SI nMes ! 0; |Y nMes ! 13;
     NumMes = dig1 + (nMes - 1);
     |SI NumMes > 12; NumMes = NumMes - 12; |FINSI;
  |SINO;
      NumMes = nMes;
  |FINSI;
  |SI NumMes < dig1; nElAny = nElAny + 1; |FINSI;

  aAlfa1 = NumMes; |QBF aAlfa1;
  aAlfa1 = ("00" + aAlfa1) % -102;
  aAlfa2 = nElAny;

  aLaFecha1 = "01." + aAlfa1 + "." + aAlfa2;

  aAlfa3 = "31.";
  |SI NumMes = 4; |O NumMes = 6; |O NumMes = 9; |O NumMes = 11;
      aAlfa3 = "30.";
  |SINO;
      |SI NumMes = 2;
          aAlfa3 = "28.";
          nNume1 = nElAny / 4; nNume2 = (nElAny / 4) ! 0;
          |SI nNume1 = nNume2;
              aAlfa3 = "29.";
          |FINSI;
      |FINSI;
  |FINSI;
  aLaFecha2 = aAlfa3 + aAlfa1 + "." + aAlfa2;
|FPRC;
