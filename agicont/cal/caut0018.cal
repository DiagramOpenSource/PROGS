|FICHEROS;
  caut0018;

  caivarep;
  caivasop;
  caivaasi;
  cacuenta;
  caclipro;
  calibros;
  camaasil;
  casiim01;
  casiim03;
  cacon300;

  caivatm4;
|FIN;

|VARIABLES;
   aAlfa      = "";
   aAlfa1     = "";
   aAlfa2     = "";
   nNume1     = 0;
   nNume2     = 0;
   nPara1     = 0;
   nPara2     = 0;

     &eaNomFich = "";
     &enQSerie  = 0;
     &dirfich0  = "";

     aParam     = "";
     nEmpresa   = 0;
     nPeriodo   = 0;
     nOk        = 0;
     a340          = "";
     aPais         = "";
     nNumLin       = 0;
     nBase         = 0;

     &enGrabaSii   = 0;
     &eaSiiPro     = "";
     &enSiiLib     = 0;
     &eaSiiDoc     = "";
     &enSiiDoc     = 0;
     &eaSiiSer     = "";
     &eaSiiRef     = "";
     &enSiiLin     = 0;
     &enSiiIva     = 0;
     &enSiiMod     = 0;
     &eaSiiCta     = "";
     &eaSiiDig     = "";
     &eaSiiCpt     = "";
     &eaSiiDes     = "";
     &enSiiL       = 0;
     &eaSiiEje     = "";
     &enSiiPer     = 0;
     &enSiiRet     = 0;
     &enSiiUlt     = 0;
     &eaSiiCIF     = "";
     &enSiiCon     = 0;
     &eaTConfig    = "";
     &SwCambia     = 0;
     &enTConLin    = 0;

     &eaCodNif     = "";
     &ewNif        = 0;
     &eaIdenPais   = "";
     &aTipoIVA     = "";
     aReferencia   = "";
|FIN;

|INCLUYE acceso_i;
|INCLUYE varnue_i;

|| **********************************************************************
|CALCULO libroh; | TIPO 0;   ||  hasta diario
   |SI #caut0018#2 < #caut0018#1;  |ERROR;  |FINSI;
|FCAL;

|CALCULO hque; |TIPO 7;
 |SI enQSerie = 0;
     |C_M #caut0018#3,1,"N"; #caut0018#3 = "  "; |PINTA #caut0018#3;
     |C_M #caut0018#4,1,"N"; #caut0018#4 = "zz"; |PINTA #caut0018#4;
 |SINO;
     |C_M #caut0018#3,1,"S";
     |C_M #caut0018#4,1,"S";
 |FINSI;
|FCAL;

|CALCULO serieh; | TIPO 0;   ||  hasta documento
   |SI #caut0018#4 < #caut0018#3;  |ERROR;  |FINSI;
|FCAL;

|CALCULO facturah; | TIPO 0;   ||  hasta documento
   |SI #caut0018#6 < #caut0018#4;  |ERROR;  |FINSI;
|FCAL;

|CALCULO LaFecha; |TIPO 0;
 |SI #caut0018#Campo# < fec1; |O #caut0018#Campo#  > fec2;
     |MENAV "    Error, Fecha Incorrecta ";
     |ERROR;
     |ACABA;
 |FINSI;
 |SI Campo = 8; |Y #caut0018#7 > #caut0018#8;
     |MENAV "    Error, Fecha Incorrecta ";
     |ERROR;
 |FINSI;
|FCAL;

|| **********************************************************************
|PROCESO LeeCliPro;
     |ABRE #calibros;
     #calibros#0 = enSiiLib;
     |LEE 000#calibros.=;
     |SI FSalida ! 0;  |DDEFECTO #calibros;  |FINSI;
     |CIERRA #calibros;

     |ABRE #cacon300;
     #cacon300#0 = eaSiiCpt;
     |LEE 000#cacon300.=;
     |SI FSalida ! 0;  |DDEFECTO #cacon300;  |FINSI;
     |CIERRA #cacon300;

     aTipoIVA = #calibros#2;

     |ABRE #caclipro;
     |SI aTipoIVA = "R";
         #caclipro#23 = "C";
     |SINO;
         #caclipro#23 = "P";
     |FINSI;
     #caclipro#10 = eaSiiCta;
     #caclipro#11 = eaSiiDig;
     |LEE 000#caclipro.=;
     |SI FSalida ! 0;  |DDEFECTO #caclipro;  |FINSI;

     eaSiiCIF   = #caclipro#9;
     aPais      = #caclipro#24; |QBF aPais;
     eaIdenPais = "01";

     |SI aPais = "";
         aPais = "ES";
     |FINSI;

     |SI aPais = "ESP";
         aPais = "ES";
     |FINSI;

     aAlfa   = aPais;  |QBF aAlfa;
     |SI aAlfa = "GR";  aPais = "EL";  |FINSI;

     |SI aPais ! "ES";
                            eaIdenPais = "06";
         |SI aAlfa = "DE";  eaIdenPais = "02";  |FINSI;
         |SI aAlfa = "AT";  eaIdenPais = "02";  |FINSI;
         |SI aAlfa = "BE";  eaIdenPais = "02";  |FINSI;
         |SI aAlfa = "BG";  eaIdenPais = "02";  |FINSI;
         |SI aAlfa = "CY";  eaIdenPais = "02";  |FINSI;
         |SI aAlfa = "DK";  eaIdenPais = "02";  |FINSI;
         |SI aAlfa = "SI";  eaIdenPais = "02";  |FINSI;
         |SI aAlfa = "EE";  eaIdenPais = "02";  |FINSI;
         |SI aAlfa = "FI";  eaIdenPais = "02";  |FINSI;
         |SI aAlfa = "FR";  eaIdenPais = "02";  |FINSI;
         |SI aAlfa = "EL";  eaIdenPais = "02";  |FINSI;
         |SI aAlfa = "GB";  eaIdenPais = "02";  |FINSI;
         |SI aAlfa = "NL";  eaIdenPais = "02";  |FINSI;
         |SI aAlfa = "HU";  eaIdenPais = "02";  |FINSI;
         |SI aAlfa = "IT";  eaIdenPais = "02";  |FINSI;
         |SI aAlfa = "IE";  eaIdenPais = "02";  |FINSI;
         |SI aAlfa = "LV";  eaIdenPais = "02";  |FINSI;
         |SI aAlfa = "LT";  eaIdenPais = "02";  |FINSI;
         |SI aAlfa = "LU";  eaIdenPais = "02";  |FINSI;
         |SI aAlfa = "MT";  eaIdenPais = "02";  |FINSI;
         |SI aAlfa = "PL";  eaIdenPais = "02";  |FINSI;
         |SI aAlfa = "PT";  eaIdenPais = "02";  |FINSI;
         |SI aAlfa = "CZ";  eaIdenPais = "02";  |FINSI;
         |SI aAlfa = "SK";  eaIdenPais = "02";  |FINSI;
         |SI aAlfa = "RO";  eaIdenPais = "02";  |FINSI;
         |SI aAlfa = "SE";  eaIdenPais = "02";  |FINSI;
         |SI aAlfa = "HR";  eaIdenPais = "02";  |FINSI;
     |FINSI;

     |SI aTipoIVA = "R";
         |SI #cacon300#2 = 3;
             eaIdenPais = "02";
         |FINSI;

         |SI #cacon300#2 = 8;
             eaIdenPais  = "04";
         |FINSI;
     |FINSI;

     |SI aTipoIVA = "S";
          |SI #cacon300#1 = 2;  |O #cacon300#1 = 7;
              eaIdenPais  = "04";
          |FINSI;
     |FINSI;

     aAlfa = eaSiiCIF % 101;   |QBF aAlfa;
     |SI aAlfa = "X";  |O aAlfa = "Y";
         eaIdenPais  = "01";
     |FINSI;
|FPRC;

|PROCESO Defectos;

     |SI aTipoIVA = "R";
         a340 = #cacon300#5;
      |FINSI;

     |SI aTipoIVA = "S";
          a340 = #cacon300#6;
     |FINSI;

     |DDEFECTO #casiim03;
     |SI nNumLin = 0;

         #casiim03#6 = "F1";

         |SI eaSiiCIF = "";
             #casiim03#6 = "F2";
         |FINSI;

         |SI enSiiUlt > 1;
             #casiim03#6 = "F4";
         |FINSI;

         |SI a340 = "D";
             #casiim03#6  = "R4";
             #casiim03#13 = "I";

             |SI eaSiiCIF = "";
                 #casiim03#6 = "R5";
             |FINSI;
         |FINSI;

         || Clave operacion

         #casiim03#7 = "01";

         |SI aTipoIVA = "R";
             nOk = 0;
             |SI a340 = "Z";  nOk = 1;  |FINSI;
             |SI a340 = "0";  nOk = 1;  |FINSI;
             |SI a340 = "1";  nOk = 1;  |FINSI;
             |SI a340 = "2";  nOk = 1;  |FINSI;
             |SI a340 = "3";  nOk = 1;  |FINSI;
             |SI a340 = "4";  nOk = 1;  |FINSI;
             |SI a340 = "5";  nOk = 1;  |FINSI;
             |SI a340 = "6";  nOk = 1;  |FINSI;
             |SI a340 = "7";  nOk = 1;  |FINSI;
             |SI a340 = "8";  nOk = 1;  |FINSI;
             |SI a340 = "9";  nOk = 1;  |FINSI;

             |SI nOk = 1;
                 #casiim03#7 = "07";
             |FINSI;

             |SI eaSiiEje = "2017";
                 |SI #caivaasi#46 = "M"; |Y enSiiPer [ 6;
                     #casiim03#7 = "16";
                 |FINSI;
                 |SI #caivaasi#46 = "T"; |Y enSiiPer [ 2;
                     #casiim03#7 = "16";
                 |FINSI;
             |FINSI;
         |FINSI;

         |SI aTipoIVA = "S";
             || Clave operacion

             nOk = 0;
             |SI a340 = "Z";  nOk = 1;  |FINSI;
             |SI a340 = "0";  nOk = 1;  |FINSI;
             |SI a340 = "1";  nOk = 1;  |FINSI;
             |SI a340 = "2";  nOk = 1;  |FINSI;
             |SI a340 = "3";  nOk = 1;  |FINSI;
             |SI a340 = "4";  nOk = 1;  |FINSI;
             |SI a340 = "5";  nOk = 1;  |FINSI;
             |SI a340 = "6";  nOk = 1;  |FINSI;
             |SI a340 = "7";  nOk = 1;  |FINSI;
             |SI a340 = "8";  nOk = 1;  |FINSI;
             |SI a340 = "9";  nOk = 1;  |FINSI;

             |SI nOk = 1;
                 #casiim03#7 = "07";
             |FINSI;

             |SI eaIdenPais = "02";
                 #casiim03#7 = "09";
             |FINSI;

             |SI eaSiiEje = "2017";
                 |SI #caivaasi#46 = "M"; |Y enSiiPer [ 6;
                     #casiim03#7 = "14";
                 |FINSI;
                 |SI #caivaasi#46 = "T"; |Y enSiiPer [ 2;
                     #casiim03#7 = "14";
                 |FINSI;
             |FINSI;
         |FINSI;

         #casiim01#67 = "N";
         |SI #casiim01#3 = "F2";  |O #casiim01#3 = "F4";
              |O #casiim01#3 = "R5";
                #casiim01#67 = "S";
         |FINSI;

     |FINSI;

     |SI nNumLin ! 0;
        aAlfa = #caclipro#9 % 101;

                                                  nOk = 1;
        |SI eaIdenPais  = "01";  |Y aAlfa ! "N";  nOk = 0;  |FINSI;
        |SI eaIdenPais  = "07";  |Y aAlfa ! "N";  nOk = 0;  |FINSI;
        |SI #casiim01#3 = "F2";                   nOk = 0;  |FINSI;
        |SI #casiim01#3 = "F4";                   nOk = 0;  |FINSI;
        |SI #casiim01#3 = "R5";                   nOk = 0;  |FINSI;

        |SI nOk = 1;
            |SI eaIdenPais ! "01";
                #casiim03#10 = "E";
            |FINSI;
        |FINSI;

        |SI enSiiIva > 0;
            #casiim03#11 = "S1";
        |SINO;
            #casiim03#11 = "E6";
            |SI #casiim01#4 = "02";
                #casiim03#11 = "E2";
            |FINSI;
        |FINSI;
    |FINSI;
    |HAZ LosCamposNuevos;
|FPRC;
|| //////////////////////////////////////////////////////////////

|PROCESO CampoDesdeM03;
  |ABRE #casiim03;
  |DDEFECTO #casiim03;
  #casiim03#15 = eaTConfig;
  #casiim03#0  = enTConLin;
  |LEE 041#casiim03.=
  |CIERRA #casiim03;

  |SI eaSiiEje = "2017";
     |SI #caivaasi#46 = "M"; |Y enSiiPer [ 6;
         |SI aTipoIVA = "R";
             |SI #casiim03#7 ! "11"; |Y #casiim03#7 ! "12"; |Y #casiim03#7 ! "13";
                  #casiim03#7 = "16";
             |FINSI;
         |SINO;
             #casiim03#7 = "14";
         |FINSI;
     |FINSI;
     |SI #caivaasi#46 = "T"; |Y enSiiPer [ 2;
         |SI aTipoIVA = "R";
             |SI #casiim03#7 ! "11"; |Y #casiim03#7 ! "12"; |Y #casiim03#7 ! "13";
                  #casiim03#7 = "16";
             |FINSI;
         |SINO;
             #casiim03#7 = "14";
         |FINSI;
     |FINSI;
  |FINSI;

  |HAZ LosCamposNuevos;
|FPRC;

|PROCESO NReferencia;
  |SI #caivaasi#54 = "S"; |Y aTipoIVA = "R";
      eaSiiRef = enSiiDoc;
      |SI #caivaasi#49 = "S";
           eaSiiRef = ("000000" + eaSiiRef) % -106;
      |FINSI;
      aAlfa = eaSiiSer; |QBF aAlfa;
      |SI aAlfa ! "";
          eaSiiRef = eaSiiSer + #caivaasi#48 + eaSiiRef;
      |FINSI;
      #casiim01#66 = eaSiiRef;
  |FINSI;
|FPRC;

|PROCESO LosCamposNuevos;
  aAlfa = ("000000" + enSiiDoc) % -106;
  eaSiiDoc = eaSiiSer + aAlfa;

  #casiim01#0 = eaSiiPro;
  #casiim01#1 = enSiiLib;
  #casiim01#2 = eaSiiDoc;
  |LEE 101#casiim01.=;
  |SI FSalida ! 0;
      |DDEFECTO #casiim01;
      #casiim01#0 = eaSiiPro;
      #casiim01#1 = enSiiLib;
      #casiim01#2 = eaSiiDoc;

      |GRABA 020#casiim01.b;
  |FINSI;

  |SI nNumLin = 0;
      #casiim01#3 = #casiim03#6;
      #casiim01#4 = #casiim03#7;
      #casiim01#5 = #casiim03#8;
      #casiim01#6 = #casiim03#9;
      #casiim01#7 = #casiim03#10;  || Tipo 1
      #casiim01#8 = #casiim03#11;  || Nivel 1

      aAlfa = "";
      |SI #casiim03#14 ! "C";
          |SI #casiim03#14 ! "D";
              aAlfa = eaSiiDes; |QBF aAlfa;
              aAlfa = aAlfa + " ";
          |FINSI;
          |SI #casiim03#14 ! "F";
              aAlfa1 = #casiim03#12; |QBF aAlfa1;
              aAlfa = aAlfa + aAlfa1;
              |QBF aAlfa;
          |FINSI;
          #casiim01#13 = aAlfa;
      |SINO;
          |HAZ eDesSiim01;
          #casiim01#13 = eaSiiDes;
      |FINSI;

      #casiim01#15 = #casiim03#13;
      #casiim01#10 = #casiim03#18;
      #casiim01#67 = #casiim03#19;
      #casiim01#68 = #casiim03#20;

      |SI aReferencia ! "";
          #casiim01#66 = aReferencia;
      |SINO;
          |HAZ NReferencia;
      |FINSI;

      |PDEFECTO #casiim01, 50, 66;
  |FINSI;

  |SI nNumLin = 1;
      #casiim01#7  = #casiim03#10;  || Tipo 1
      #casiim01#8  = #casiim03#11;  || Nivel 1
      #casiim01#50 = nBase;         || Base 1
  |FINSI;

  |SI nNumLin = 2;
      #casiim01#51 = nBase;   || Base  2
      #casiim01#52 = #casiim03#10;  || Tipo  2
      #casiim01#53 = #casiim03#11;  || Nivel 2
  |FINSI;

  |SI nNumLin = 3;
      #casiim01#54 = nBase;   || Base  3
      #casiim01#55 = #casiim03#10;  || Tipo  3
      #casiim01#56 = #casiim03#11;  || Nivel 3
  |FINSI;

  |SI nNumLin = 4;
      #casiim01#57 = nBase;   || Base  4
      #casiim01#58 = #casiim03#10;  || Tipo  4
      #casiim01#59 = #casiim03#11;  || Nivel 4
  |FINSI;

  |SI nNumLin = 5;
      #casiim01#60 = nBase;   || Base  5
      #casiim01#61 = #casiim03#10;  || Tipo  5
      #casiim01#62 = #casiim03#11;  || Nivel 5
  |FINSI;

  |SI nNumLin = 6;
      #casiim01#63 = nBase;   || Base  6
      #casiim01#64 = #casiim03#10;  || Tipo  6
      #casiim01#65 = #casiim03#11;  || Nivel 6
  |FINSI;

  |GRABA 020#casiim01.a;
  |LIBERA #casiim01;

|FPRC;

|| ////////////////////////////////////////////////////////////////////////
|PROCESO PorLinea;
  |HAZ ePorLinea;
  |SI SwCambia ! 0;
      |HAZ CampoDesdeM03;
  |SINO;
      |HAZ Defectos;
  |FINSI;
|FPRC;

|PROCESO LineaIVA0;
  |PARA nBase = 1; |SI nBase [ 5; |HACIENDO nBase = nBase + 1;
        |SI nBase = 1; nNume1 =  8; nNume2 = 11; |FINSI;
        |SI nBase = 2; nNume1 =  9; nNume2 = 12; |FINSI;
        |SI nBase = 3; nNume1 = 10; nNume2 = 13; |FINSI;
        |SI nBase = 4; nNume1 = 52; nNume2 = 54; |FINSI;
        |SI nBase = 5; nNume1 = 53; nNume2 = 55; |FINSI;
        |SI #caivarep#nNume1# ! 0;
            enSiiIva = #caivarep#nNume2#;
            nNumLin  = nNumLin + 1;
            |HAZ PorLinea;
        |FINSI;
  |SIGUE;
|FPRC;

|PROCESO CabecIVA0;

  |SI aTipoIVA = "R";
      eaSiiPro  = "caivarep";
      enSiiLib  = #caivarep#0;
      eaSiiSer  = #caivarep#27;
      enSiiDoc  = #caivarep#2;
      enSiiCon  = #caivarep#6;
      eaSiiDes  = #caivarep#7;
      eaSiiCta  = #caivarep#4;
      eaSiiDig  = #caivarep#5;
      enSiiPer  = #caivarep#44;
  |SINO;
      eaSiiPro  = "caivasop";
      enSiiLib  = #caivasop#0;
      eaSiiSer  = #caivasop#27;
      enSiiDoc  = #caivasop#2;
      enSiiCon  = #caivasop#6;
      eaSiiDes  = #caivasop#7;
      eaSiiCta  = #caivasop#4;
      eaSiiDig  = #caivasop#5;
      enSiiPer  = #caivasop#44;
  |FINSI;

  |SI #caut0018#20 = "N";
      aAlfa = ("000000" + enSiiDoc) % -106;
      eaSiiDoc = eaSiiSer + aAlfa;

      #casiim01#0 = eaSiiPro;
      #casiim01#1 = enSiiLib;
      #casiim01#2 = eaSiiDoc;
      |LEE 040#casiim01.=;
      |SI FSalida = 0;
          nOk = 0;
          |SI #caut0018#3 ! "F1";
              nOk = 1;
          |SINO;
              |PARA nPara1 = 4; |SI nPara1 [ 8; |HACIENDO nPara1 = nPara1 + 1;
                    aAlfa = #caut0018#nPara1#; |QBF aAlfa;
                    |SI aAlfa ! "";
                        nOk = 1;
                    |FINSI;
              |SIGUE;
          |FINSI;
          aAlfa = #caut0018#13; |QBF aAlfa;
          |SI aAlfa ! ""; nOk = 1; |FINSI;
          |SI nOk = 1; |ACABA; |FINSI;
      |FINSI;
  |FINSI;

  eaSiiCpt = enSiiCon;
  |HAZ LeeCliPro;
  nNumLin   = 0;
  |HAZ PorLinea;

  |SI aTipoIVA = "R";
      |HAZ LineaIVA0;
  |FINSI;
  enGrabaSii = 1;
|FPRC;

|DEFBUCLE CabecIVARep;
  #caivarep#1;
  3,44;
  #caut0018#1, #caut0018#3, #caut0018#5, #caut0018#7, #caut0018#18;
  #caut0018#2, #caut0018#4, #caut0018#6, #caut0018#8, #caut0018#19;
  e;
  NULL, CabecIVA0;
|FIN;

|DEFBUCLE CabecIVASop;
  #caivasop#1;
  3,44;
  #caut0018#1, #caut0018#3, #caut0018#5, #caut0018#7, #caut0018#18;
  #caut0018#2, #caut0018#4, #caut0018#6, #caut0018#8, #caut0018#19;
  e;
  NULL, CabecIVA0;
|FIN;

|| ////////////////////////////////////////////////////////////////////

|PROCESO IniEmpresa;
    |SI #caivatm4#0 ! 0;
        cod1 = #caivatm4#0;
        cod2 = #caivatm4#1;
        emEmp = 1;  || Sin mensaje
        |HAZ leelo;
        |SI swerror ! 0;  |ACABA;   |FINSI;
        |HAZ eVarEmpresa;
    |FINSI;

    |SI nEmpresa ! -1;
        |CIERRA #caivarep;
        |CIERRA #caivasop;
        |CIERRA #casiim01;
    |FINSI;

    |SI #caivatm4#0 ! 0;
        |DBASE dirfich0; |QBF dirfich0;
        dirfich0 = dirfich0 + "fich/";
        aAlfa1   = ("00000" + #caivatm4#0) % -105;
        aAlfa2   = ("0"     + #caivatm4#1) % -101;
        dirfich0 = dirfich0 + aAlfa1 + aAlfa2 + "/";
        |QBF dirfich0;
    |SINO;
        |DFICE dirfich0; |QBF dirfich0;
    |FINSI;

    |PATH_DAT #caivarep#dirfich0#;
    |PATH_DAT #caivasop#dirfich0#;
    |PATH_DAT #caivaasi#dirfich0#;
    |PATH_DAT #cacuenta#dirfich0#;
    |PATH_DAT #camaasil#dirfich0#;
    |PATH_DAT #caclipro#dirfich0#;
    |PATH_DAT #casiim03#dirfich0#;
    |PATH_DAT #casiim01#dirfich0#;
    |PATH_DAT #cacon300#dirfich0#;

    |HAZ CojoConfig;
    |SI #caivaasi#51 = "N";  swerror = 1; |ACABA; |FINSI;

    enQSerie = 0;
    |SI #caivaasi#44 = "S"; enQSerie = 1;|FINSI;
    #caut0018#7 = "01.01.2000";
    #caut0018#8 = "31.12.2099";

    enGrabaSii = 0;
    |ABRE #caivasop;
    |ABRE #caivarep;
    |ABRE #casiim01;

    eaSiiEje = enEjercicio;
|FPRC;

|PROCESO PorAuxiliar;
  |SI nEmpresa ! #caivatm4#0; |O nPeriodo ! #caivatm4#1;
      |HAZ IniEmpresa;
      |SI swerror ! 0;  |ACABA;   |FINSI;
      nEmpresa = #caivatm4#0;
      nPeriodo = #caivatm4#1;
  |FINSI;

  aTipoIVA    = #caivatm4#2;
  aReferencia = #caivatm4#6; |QBF aReferencia;
  |SI aTipoIVA = "R";
      #caivarep#0  = #caivatm4#3;
      #caivarep#27 = #caivatm4#4;
      #caivarep#2  = #caivatm4#5;
      |LEE 141#caivarep.=;
      |SI FSalida = 0;
          |HAZ CabecIVA0;
      |FINSI;
      |LIBERA #caivarep;
  |FINSI;
  |SI aTipoIVA = "S";
      #caivasop#0  = #caivatm4#3;
      #caivasop#27 = #caivatm4#4;
      #caivasop#2  = #caivatm4#5;
      |LEE 141#caivasop.=;
      |SI FSalida = 0;
          |HAZ CabecIVA0;
      |FINSI;
      |LIBERA #caivasop;
  |FINSI;
|FPRC;

|DEFBUCLE PorAuxiliar;
  #caivatm4#1;
  ;
  00000, 0, " ", 01, "  ", 000001;
  99999, 9, "z", 99, "zz", 999999;
  e;
  NULL, PorAuxiliar;
|FIN;

|| /////////////////////////////////////////////////////////////////////
|CALCULO ElCambio; |TIPO 12;

  |SI #caut0018#17 = "N";
      |ACABA;
  |FINSI;

  |SI aParam = "";
      eaSiiEje = enEjercicio;
      |ABRE #casiim01;
      |SI #caut0018#0 ! "S";
          aTipoIVA = "R";
          |BUCLE CabecIVARep;
      |FINSI;
      |SI #caut0018#0 ! "R";
          aTipoIVA = "S";
          |BUCLE CabecIVASop;
      |FINSI;
      |CIERRA #casiim01;
  |SINO;

      |NOME_DAT #caivatm4#eaNomFich#;
      nEmpresa = -1;
      nPeriodo = -1;
      |BUCLE PorAuxiliar;
      |SI nEmpresa ! -1;
          |CIERRA #caivarep;
          |CIERRA #caivasop;
          |CIERRA #casiim01;
      |FINSI;
      |DELINDEX #caivatm4;
  |FINSI;
|FCAL;

|| **************************************************************************

|PROGRAMA;
   aParam = PARAMETRO;
   |QBF aParam;

   |SI aParam = "";
       |HAZ inicio;
       |HAZ eVarEmpresa;
       |SI enEjercicio < 2017;
           |MENAV "0000Ejercicio No Correcto";
           |ACABA;
       |FINSI;
   |FINSI;

   |DDEFECTO #caut0018;

   |SI aParam = "";

       |HAZ CojoConfig;

       |ABRE #caivaasi;
       |DDEFECTO #caivaasi;
       |LEE 001#caivaasi.p;
       |CIERRA #caivaasi;
       enQSerie = 0;
       |SI #caivaasi#44 = "S"; enQSerie = 1;|FINSI;
       #caut0018#7 = fec1;
       #caut0018#8 = fec2;

       |CLS;
       |CABEZA "Recalculo Datos Fras SII";
       |PINPA #0#caut0018;
       |PINDA #0#caut0018;
       |ENDATOS #1#caut0018;

   |SINO;

        #caut0018#17 = "S";
        |HAZ ElCambio;

   |FINSI;
|FPRO;
|| **************************************************************************
