|FICHEROS;
   caextrfa #0;

   camaasil #1;
   camaasic #2;
   cacuenta #3;
   caivarep #4;
   caclipro #5;
   camanban #6;
   caivasop #7;
   calicont #31;
   caconimp #8;

   canempre #30;
   caw00022 #999;
|FIN;

|VARIABLES;
   &entrada  = 1;
   aAlfa     = "";
   aAlfa1    = "";
   aAlfa2    = "";
   nCampo    = 0;
   nFactura  = 0;
   contra_td = "";
   contra_th = "";
   contra_d  = "";
   contra_h  = "";
   sw_con    = 0;          || 1 = EL CONCEPTO ES BUENO
   nHay      = 0;
   aDCuenta  = "";
   aHCuenta  = "";
   varalf    = "";
   aFichero  = "";

   enEmpOrg  = 0;
   enPerOrg  = 0;
   eaFEmp    = "";
   nEmpresa  = 0;
   nPeriodo  = 0;
   dirfich1  = "";

   cta       = 0;
   long      = "  ";
   swdig     = 0;

   x         = 0;
   digito    = 0;
   informe   = "";
   swex_con  = 0;
   sw_prim   = 0;         || 0 = PRIMERA VEZ
   sw_cuen   = 0;          ||1 = Hemos Leido la cuenta
   swtotal   = 0;

   &fFechaFra = @;
   &swCliPro = 0;
   &swant    = 0;
   &sw_cab   = 0;
   &s_debe   = 0;
   &s_haber  = 0;
   &s_saldo  = 0;
   &sw_inf   = 0;
   &debe     = 0;
   &haber    = 0;
   &direc    = "";
   &codpos   = "";
   &pobla    = "";
   &provi    = "";
   &cif      = "";
   &telef    = "";
   &contra   = "";
   &contra_t = "";
   &cuenta   = "";
   &dirfich0 = "";
|FIN;

|INCLUYE acceso_i;
|INCLUYE varnue_i;
|INCLUYE puntos_i;
|INCLUYE condes_i;

|| *********************************************************************
||                             COMIENZO
|| *********************************************************************

|PROCESO recoge;
   #0#2 = fec1;
   |SI #0#75 = "S"; #0#2 = "01.01.2000"; |FINSI;
                 |PINTA #0#2;
   #0#3  = fec2; |PINTA #0#3;
   #0#77 = fec2; |PINTA #0#77;
|FPRC;

|PROCESO seleccio; |TIPO 0;
   |SI #caextrfa#4 = "N";
       |C_M #caextrfa#05, 1, "S"; |C_M #caextrfa#06, 1, "S";
       |C_M #caextrfa#07, 1, "S"; |C_M #caextrfa#08, 1, "S";
       |C_M #caextrfa#09, 1, "S"; |C_M #caextrfa#10, 1, "S";
       |C_M #caextrfa#11, 1, "S"; |C_M #caextrfa#12, 1, "S";
       |C_M #caextrfa#13, 1, "S"; |C_M #caextrfa#14, 1, "S";
       |C_M #caextrfa#15, 1, "S"; |C_M #caextrfa#16, 1, "S";
       |C_M #caextrfa#17, 1, "S"; |C_M #caextrfa#18, 1, "S";
       |C_M #caextrfa#19, 1, "S"; |C_M #caextrfa#20, 1, "S";
       |C_M #caextrfa#21, 1, "S"; |C_M #caextrfa#22, 1, "S";
       |C_M #caextrfa#23, 1, "S"; |C_M #caextrfa#24, 1, "S";
       |C_M #caextrfa#25, 1, "S"; |C_M #caextrfa#26, 1, "S";
       |C_M #caextrfa#27, 1, "S"; |C_M #caextrfa#28, 1, "S";
       |C_M #caextrfa#33, 1, "N"; |C_M #caextrfa#34, 1, "N";
       #caextrfa#33 = 1;       |PINTA #caextrfa#33;
       #caextrfa#34 = 999;     |PINTA #caextrfa#34;
   |SINO;
       |C_M #caextrfa#05, 1, "N"; |C_M #caextrfa#06, 1, "N";
       |C_M #caextrfa#07, 1, "N"; |C_M #caextrfa#08, 1, "N";
       |C_M #caextrfa#09, 1, "N"; |C_M #caextrfa#10, 1, "N";
       |C_M #caextrfa#11, 1, "N"; |C_M #caextrfa#12, 1, "N";
       |C_M #caextrfa#13, 1, "N"; |C_M #caextrfa#14, 1, "N";
       |C_M #caextrfa#15, 1, "N"; |C_M #caextrfa#16, 1, "N";
       |C_M #caextrfa#17, 1, "N"; |C_M #caextrfa#18, 1, "N";
       |C_M #caextrfa#19, 1, "N"; |C_M #caextrfa#20, 1, "N";
       |C_M #caextrfa#21, 1, "N"; |C_M #caextrfa#22, 1, "N";
       |C_M #caextrfa#23, 1, "N"; |C_M #caextrfa#24, 1, "N";
       |C_M #caextrfa#25, 1, "N"; |C_M #caextrfa#26, 1, "N";
       |C_M #caextrfa#27, 1, "N"; |C_M #caextrfa#28, 1, "N";
       |C_M #caextrfa#33, 1, "S"; |C_M #caextrfa#34, 1, "S";
   |FINSI;
|FPRC;

|PROCESO seleccio2; |TIPO 0;
   |SI #caextrfa#41 = "N";
       |C_M #caextrfa#42, 1, "S"; |C_M #caextrfa#43, 1, "S";
       |C_M #caextrfa#44, 1, "S"; |C_M #caextrfa#45, 1, "S";
       |C_M #caextrfa#46, 1, "S"; |C_M #caextrfa#47, 1, "S";
       |C_M #caextrfa#48, 1, "S"; |C_M #caextrfa#49, 1, "S";
       |C_M #caextrfa#50, 1, "S"; |C_M #caextrfa#51, 1, "S";
       |C_M #caextrfa#52, 1, "S"; |C_M #caextrfa#53, 1, "S";
       |C_M #caextrfa#54, 1, "S"; |C_M #caextrfa#55, 1, "S";
       |C_M #caextrfa#56, 1, "S";
       |C_M #caextrfa#0, 1, "N"; |C_M #caextrfa#1, 1, "N";
       #caextrfa#0 = "            "; #caextrfa#31 = 0; |PINTA #caextrfa#0;
       #caextrfa#1 = "            "; #caextrfa#32 = 9; |PINTA #caextrfa#1;
   |SINO;
       |C_M #caextrfa#0, 1, "S";  |C_M #caextrfa#1, 1, "S";
       |C_M #caextrfa#42, 1, "N"; |C_M #caextrfa#43, 1, "N";
       |C_M #caextrfa#44, 1, "N"; |C_M #caextrfa#45, 1, "N";
       |C_M #caextrfa#46, 1, "N"; |C_M #caextrfa#47, 1, "N";
       |C_M #caextrfa#48, 1, "N"; |C_M #caextrfa#49, 1, "N";
       |C_M #caextrfa#50, 1, "N"; |C_M #caextrfa#51, 1, "N";
       |C_M #caextrfa#52, 1, "N"; |C_M #caextrfa#53, 1, "N";
       |C_M #caextrfa#54, 1, "N"; |C_M #caextrfa#55, 1, "N";
       |C_M #caextrfa#56, 1, "N";
   |FINSI;
|FPRC;

|PROCESO cuenta; |TIPO 6;
  p_alfa1 = #0Campo;
  salidas = FSalida;
  |HAZ punto;
  |SI p_error ! 0;     |ERROR;  |ACABA; |FINSI;
  |SI #0Campo = "            "; |ACABA; |FINSI;
  |SI Campo [ 1;
       digito = Campo + 31;
  |SINO;
       digito = Campo + 15;
  |FINSI;
  cta  = #0Campo;
  long = cta % 0;
  swdig = 0;
  |SI long = dig4; swdig = 1; #0digito = 1; |FINSI;
  |SI long = dig5; swdig = 1; #0digito = 2; |FINSI;
  |SI long = dig6; swdig = 1; #0digito = 3; |FINSI;
  |SI long = dig7; swdig = 1; #0digito = 4; |FINSI;
  |SI long = dig8; swdig = 1; #0digito = 5; |FINSI;
  |SI long = dig9; swdig = 1; #0digito = 6; |FINSI;
  |SI swdig = 0;
      |MENAV "0000Longitud de Cuenta Fuera de Nivel !";
      |ERROR;
  |FINSI;
|FPRC;

|PROCESO fechas;
  |SI #0Campo > fec2;
      |MENAV "0000ERROR ESTA FECHA NO CORRESPONDE AL EJERCICIO";
      |ERROR;
      |ACABA;
  |FINSI;
  |SI Campo = 3; |Y #0#3 < #0#2;
      |MENAV "0000Error Fecha Hasta menor que Fecha Desde";
      |ERROR;
  |FINSI;
  |SI Campo = 77; |Y #0#77 < #0#76;
      |MENAV "0000Error Fecha Hasta menor que Fecha Desde";
      |ERROR;
  |FINSI;
|FPRC;

|PROCESO no_dep; |TIPO 7;
   aAlfa1 = #canempre#24;
   |C_M #caextrfa#36, 1, aAlfa1;
   |C_M #caextrfa#37, 1, aAlfa1;
|FPRC;

|| **********************************************************************
|PROCESO leecuen;
   |DDEFECTO #cacuenta;
   #cacuenta#0 = #camaasil#4;
   #cacuenta#1 = #camaasil#5;
   |LEE 040#3=;

   direc  = "";
   codpos = "";
   pobla  = "";
   provi  = "";
   cif    = "";
   telef  = "";

   swCliPro = 0;
   #caclipro#23 = "C";
   #caclipro#10 = #camaasil#4;
   #caclipro#11 = #camaasil#5;
   |LEE 040#caclipro.=;
   |SI FSalida = 0;
      swCliPro = 1;
   |SINO;
      #caclipro#23 = "P";
      #caclipro#10 = #camaasil#4;
      #caclipro#11 = #camaasil#5;
      |LEE 040#caclipro.=;
      |SI FSalida = 0;
          swCliPro = 1;
      |FINSI;
   |FINSI;

   |SI swCliPro = 1;
      direc  = #caclipro#2;
      varalf = #caclipro#3;
      varalf = "0" + varalf;
      varalf = varalf % -102;
      codpos = varalf;
      varalf = #caclipro#4;
      varalf = "00" + varalf;
      varalf = varalf % -103;
      codpos = codpos + varalf;
      pobla  = #caclipro#6;
      provi  = #caclipro#5;
      cif    = #caclipro#9;
      telef  = #caclipro#7;

   |SINO;
      #camanban#10 = #camaasil#4;
      #camanban#11 = #camaasil#5;
      |LEE 040#camanban.=;
      |SI FSalida = 0;
           direc  = #camanban#2;
           varalf = #camanban#3;
           varalf = "00" + varalf;
           varalf = varalf % -102;
           codpos = varalf;
           varalf = #camanban#4;
           varalf = "000" + varalf;
           varalf = varalf % -103;
           codpos = codpos + varalf;
           pobla = #camanban#6;
           provi = #camanban#5;
           cif   = #camanban#14;
           telef = #camanban#7;
           swCliPro = 1;
      |FINSI;
   |FINSI;
   s_debe  = 0;
   s_haber = 0;
   s_saldo = 0;
   swant   = 0;
   sw_cuen = 1;
|FPRC;

|| *************************************************************************

|PROCESO buclepan;

   |SI sw_prim = 0;
       cuenta = #camaasil#4;
       sw_prim  = 1;
       |HAZ leecuen;
   |FINSI;
   |SI cuenta ! #camaasil#4;
       |SI #caextrfa#35 = "N";
           sw_cab = 0;
           sw_inf = 2;
           |PRINT; swtotal = 1;
           sw_cab = 0;
       |SINO;
           |PIEINF;
       |FINSI;
       cuenta = #camaasil#4;
       |HAZ leecuen;
   |FINSI;

   |SI #camaasil#8 = "D";
       debe = #camaasil#9;
       haber = 0;
   |SINO;
       debe = 0;
       haber = #camaasil#9;
   |FINSI;
   |SI #camaasil#8 = "D";  contra = contra_d; contra_t = contra_td; |FINSI;
   |SI #camaasil#8 = "H";  contra = contra_h; contra_t = contra_th; |FINSI;

   |SI #caextrfa#35 = "N";  |Y sw_cuen = 1;
       sw_cab = 1;
       sw_inf = 0;
       |PRINT; swtotal = 1;
       sw_cab = 0;
       sw_cuen = 0;
   |FINSI;
   sw_inf = 1;
   sw_cab = 0;
   |PRINT; swtotal = 1;

   sw_cab = 0;
||   s_debe  = s_debe  + debe;
||   s_haber = s_haber + haber;
||   s_saldo = s_debe - s_haber;
   swant = 1;
|FPRC;

|PROCESO LeeAuxiliar;
  |DDEFECTO #camaasil;
  |DDEFECTO #camaasic;
  #camaasil#0 = #caw00022#0;
  #camaasil#1 = #caw00022#1;
  #camaasil#2 = #caw00022#2;
  #camaasil#3 = #caw00022#3;
  #camaasil#4 = #caw00022#4;
  #camaasil#5 = #caw00022#5;
  #camaasil#6 = #caw00022#6;
  #camaasil#7 = #caw00022#7;
  #camaasil#8 = #caw00022#8;
  #camaasil#9 = #caw00022#9;
  |SI #camaasil#8 = "D";
      #camaasil#16 = #camaasil#4;
      #camaasil#17 = #camaasil#5;
  |SINO;
      #camaasil#10 = #camaasil#4;
      #camaasil#11 = #camaasil#5;
  |FINSI;
  #camaasil#12 = #caw00022#12;
  #camaasil#13 = #caw00022#13;
  #camaasil#14 = #caw00022#14;
  #camaasil#15 = #caw00022#15;
  #camaasil#21 = #caw00022#18;
  #camaasil#24 = #caw00022#19;
  #camaasic#0  = #camaasil#0;
  #camaasic#1  = #camaasil#1;
  #camaasic#2  = #camaasil#2;
  #camaasic#3  = #caw00022#20;
  contra_h     = #caw00022#10;
  contra_d     = #caw00022#10;
  fFechaFra    = #caw00022#21;
  |SI #camaasil#12 ! "R"; |Y #camaasil#12 ! "S";
      fFechaFra = "01.01.0000";
  |FINSI;
  |HAZ buclepan;
|FPRC;

|DEFBUCLE ElAuxiliar_F;
 #caw00022#2;
 ;
 "            ", "01.01.0000", "01.01.0000", 000000, 00, 0001;
 "zzzzzzzzzzzz", "31.12.2999", "31.12.2999", 999999, 99, 9999;
 e;
 NULL, LeeAuxiliar;
|FIN;

|DEFBUCLE ElAuxiliar_A;
 #caw00022#1;
 ;
 "            ", "01.01.0000", 000000, 00, 0001;
 "zzzzzzzzzzzz", "31.12.2999", 999999, 99, 9999;
 e;
 NULL, LeeAuxiliar;
|FIN;

|| //////////////////////////////////////////////////////////////////////
|PROCESO ElPathFicheros;
   |PATH_DAT #1 dirfich0;
   |PATH_DAT #2 dirfich0;
   |PATH_DAT #3 dirfich0;
   |PATH_DAT #4 dirfich0;
   |PATH_DAT #5 dirfich0;
   |PATH_DAT #6 dirfich0;
   |PATH_DAT #7 dirfich0;
   |PATH_DAT #8 dirfich0;
   |PATH_DAT #31 dirfich0;
|FPRC;

|PROCESO cabecera;
   |DDEFECTO #2;
   #camaasic#0 = #camaasil#0;
   #camaasic#1 = #camaasil#1;
   #camaasic#2 = #camaasil#2;
   |LEE 040#2=;
|FPRC;

|PROCESO leocontra;
   contra = "";
   contra_t = "";
   contra_td = "";
   contra_th = "";

   contra_d = #camaasic#10;
   |SALVA_DATOS #3;  |DDEFECTO #3;  #3#0 = #camaasic#10;  #3#1 = #camaasic#11;  |LEE 000#3=;
   contra_td = #3#2;
   |REPON_DATOS #3;

   contra_h = #camaasic#8;
   |SALVA_DATOS #3;  |DDEFECTO #3;  #3#0 = #camaasic#8;   #3#1 = #camaasic#9;   |LEE 000#3=;
   contra_th = #3#2;
   |REPON_DATOS #3;

    swex_con = 0;
    #31#0 = #camaasil#0;
    #31#1 = #camaasil#1;
    #31#2 = #camaasil#2;
    #31#3 = #camaasil#3;
    |LEE 001#31=;
    |SI FSalida = 0;
        swex_con = 1;
        contra_h = #31#4;
        contra_d = #31#4;
        |SALVA_DATOS #3;
        |DDEFECTO #3;
        #3#0 = #31#4;
        #3#1 = #31#5;
        |LEE 000#3=;
        contra_td = #3#2;
        contra_th = #3#2;
        |REPON_DATOS #3;
   |FINSI;
|FPRC;

|PROCESO leofactura;
  nFactura  = 0;
  fFechaFra = #camaasil#1;
  |SI #camaasil#12 = "R";
      #caivarep#0  = #camaasil#13;
      #caivarep#27 = #camaasil#15;
      #caivarep#2  = #camaasil#14;
      |LEE 041#caivarep.=;
      |SI FSalida = 0;
          nFactura = 1;
          fFechaFra = #caivarep#3;
      |FINSI;
  |FINSI;
  |SI #camaasil#12 = "S";
      #caivasop#0  = #camaasil#13;
      #caivasop#27 = #camaasil#15;
      #caivasop#2  = #camaasil#14;
      |LEE 041#caivasop.=;
      |SI FSalida = 0;
          nFactura = 1;
          fFechaFra = #caivasop#3;
      |FINSI;
  |FINSI;
|FPRC;
|| //////////////////////////////////////////////////////////////////////
|PROCESO caextrfa_M;

   sw_con = 1;
   |SI #caextrfa#4 = "N";
       sw_con = 0;
       |PARA x = 5; |SI x [ 28; |HACIENDO x = x + 1;
            |SI #caextrfa#x#  = #camaasil#6;
                sw_con = 1;
                |SAL;
            |FINSI;
       |SIGUE;
   |FINSI;
   |SI sw_con = 0; |ACABA;  |FINSI;

   |HAZ cabecera;
   |HAZ leocontra;
   |HAZ leofactura;
   |SI nFactura = 0; |Y #caextrfa#38 = "S";
       |ACABA;
   |FINSI;
   |SI nFactura = 1;
       |SI fFechaFra < #caextrfa#76; |O fFechaFra > #caextrfa#77;
           |ACABA;
       |FINSI;
   |FINSI;

   |SI #camaasil#8 = "D";  contra = contra_d; contra_t = contra_td; |FINSI;
   |SI #camaasil#8 = "H";  contra = contra_h; contra_t = contra_th; |FINSI;

   #caw00022#0 = #camaasil#0;
   #caw00022#1 = #camaasil#1;
   #caw00022#2 = #camaasil#2;
   #caw00022#3 = #camaasil#3;
   #caw00022#4 = #camaasil#4;
   #caw00022#5 = #camaasil#5;
   #caw00022#6 = #camaasil#6;
   #caw00022#7 = #camaasil#7;
   #caw00022#8 = #camaasil#8;
   #caw00022#9 = #camaasil#9;
   #caw00022#10 = contra;
   #caw00022#11 = enNivel;
   #caw00022#12 = #camaasil#12;
   #caw00022#13 = #camaasil#13;
   #caw00022#14 = #camaasil#14;
   #caw00022#15 = #camaasil#15;
   #caw00022#18 = #camaasil#21;
   #caw00022#19 = #camaasil#24;
   #caw00022#20 = #camaasic#3;
   #caw00022#21 = fFechaFra;
   |GRABA 040#caw00022.n;
   nHay = 1;
|FPRC;

|DEFBUCLE caextrfa0_M;
   #camaasil#3;
   0, 6, 21;
   aDCuenta, 0, #0#2, 000000, #0#39, #0#33, #0#36;
   aHCuenta, 9, #0#3, 999999, #0#40, #0#34, #0#37;
   e;
   NULL, caextrfa_M;
|FIN;

|PROCESO GraboAuxiliar;
   |ABRE #caw00022;
   |ABRE #camaasic;
   |ABRE #caivarep;
   |ABRE #caivasop;
   |ABRE #cacuenta;
   |ABRE #calicont;
   |SI #caextrfa#41 = "N";
       |PARA nCampo = 42; |SI nCampo [ 56; |HACIENDO nCampo = nCampo + 1;
             aAlfa1 = #caextrfa#nCampo#; |QBF aAlfa1;
             |SI aAlfa1 ! "";
                 aDCuenta = #caextrfa#nCampo#;
                 aHCuenta = #caextrfa#nCampo#;
                 |BUCLE caextrfa0_M;
             |FINSI;
       |SIGUE;
   |SINO;
       aDCuenta = #caextrfa#0;
       aHCuenta = #caextrfa#1;
       |BUCLE caextrfa0_M;
   |FINSI;
   |CIERRA #calicont;
   |CIERRA #cacuenta;
   |CIERRA #caivasop;
   |CIERRA #caivarep;
   |CIERRA #camaasic;
   |CIERRA #caw00022;
|FPRC;

|| ********************************************************************
|PROCESO CalFechas;
   |SI #canempre#26 < 80;
       enEjercicio = #canempre#26 + 2000;
   |SINO;
       enEjercicio = #canempre#26 + 1900;
   |FINSI;

   cod2 = #canempre#26;
   tra1 = 100 + cod2;
   clave2 = tra1 % -102;
   tra1 = 100 + #canempre#11;
   mes1 = tra1 % -102;
   |SI cod2 < 80;
      work1 = "01." + mes1 + ".20" + clave2;
   |SINO;
      work1 = "01." + mes1 + ".19" + clave2;
   |FINSI;
   fec1 = work1;
   tra1 = 100 + #canempre#12;
   mes1 = tra1 % -102;
   |SI #canempre#11 > #canempre#12;
      cod2 = cod2 + 1;
      |SI cod2 > 99; cod2 = cod2 - 100;    |FINSI;
   |FINSI;
   dia1 = "31";
   |SI #canempre#12 = 4; |O #canempre#12 = 6; |O #canempre#12 = 9; |O #canempre#12 = 11;
      dia1 = "30";
   |FINSI;
   |SI #canempre#12 = 2;
      dia1 = "28";
      |SI cod2 = 88; |O cod2 = 92; |O cod2 = 96; |O cod2 = 0; |O cod2 = 4; |O cod2 = 8;
          |O cod2 = 12; |O cod2 = 16; |O cod2 = 20; |O cod2 = 24;
         dia1 = "29";
      |FINSI;
   |FINSI;
   tra1 = 100 + cod2;
   clave2 = tra1 % -102;
   |SI cod2 < 80;
      work1 = dia1 + "." + mes1 + ".20" + clave2;
   |SINO;
      work1 = dia1 + "." + mes1 + ".19" + clave2;
   |FINSI;
   fec2 = work1;
|FPRC;

|PROCESO MultiEmpM1;

   |HAZ CalFechas;
   |SI #caextrfa#2 > fec2; |O #caextrfa#3 < fec1;
       |ACABA;  || Fuera de la seleccion
   |FINSI;

   |DFICB dir_fich; |QBF dir_fich;
   aAlfa1 = ("00000" + #canempre#2) % -105;
   aAlfa2 = ("0"     + #canempre#3) % -101;
   dirfich0 = dir_fich + aAlfa1 + aAlfa2 + "/";
   |HAZ ElPathFicheros;

   |HAZ GraboAuxiliar;
|FPRC;

|DEFBUCLE MultiEmpM0;
   #canempre#1;
   ;
   enEmpresa, 0;
   enEmpresa, 9;
   ;
   NULL,MultiEmpM1;
|FIN;

|PROCESO HazTodo;
   nHay = 0;
   |DELINDEX #caw00022;

   |SI #caextrfa#75 = "S";
       nEmpresa = enEmpresa;
       nPeriodo = enPeriodo;
       dirfich1 = dirfich0;

       |BUCLE MultiEmpM0;

       enEjercicio = enEmpOrg;
       enPeriodo  = enPerOrg;
       |HAZ inicio;

       dirfich0 = eaFEmp;
       |HAZ ElPathFicheros;
   |FINSI;

   |HAZ GraboAuxiliar;

   |SI nHay ! 0;
       sw_prim   = 0;
       sw_inf    = 0;
       sw_cuen   = 0;
       swtotal   = 0;

       |ABRE #cacuenta;
       |ABRE #caclipro;
       |ABRE #camanban;
       |SI #caextrfa#29 = "A";
           |BUCLE ElAuxiliar_A;
       |SINO;
           |BUCLE ElAuxiliar_F;
       |FINSI;
       |CIERRA #camanban;
       |CIERRA #caclipro;
       |CIERRA #cacuenta;
       |SI #caextrfa#35 = "N";
           sw_cab = 0;
           sw_inf = 2;
           |SI swtotal = 1; |PRINT; |FINSI;
           sw_cab = 0;
       |FINSI;
       |SI swtotal = 1;
           |PIEINF;
       |FINSI;
   |FINSI;
|FPRC;

|| //////////////////////////////////////////////////////////////////

|PROCESO Imprimir;
   informe = "caextrfa";
   |SI #caextrfa#35 = "S";  informe = "caextrf1";  |FINSI;
   |INFOR informe;
   |SI FSalida ! 0;
       |MENAV "0000Error en el Informe ";
       |ACABA;
   |FINSI;

   |CIERRA #cacuenta;

   aFichero = "0w" + Usuario;
   |NOME_DAT #caw00022#aFichero#;
   |DELINDEX #caw00022;

   |DFICE dirfich0; |QBF dirfich0;
   |HAZ HazTodo;

   |FININF;
   |DELINDEX #caw00022;

|FPRC;

|| ////////////////////////////////////////////////////////////////

|PROGRAMA;

   |HAZ inicio;
   |HAZ eVarEmpresa;
   |HAZ VerCaconimp;
   enEmpOrg = enEmpresa;
   enPerOrg = enPeriodo;
   |DFICE eaFEmp;
   dirfich0 = eaFEmp;

   |CLS;
   |CABEZA "Extractos por Impresora";

   |ABRE #0;
   |DDEFECTO #0;
   |LEE 041#0=;
   |SI FSalida ! 0;
       |DDEFECTO #0;
       #0#2 = fec1;
       |SI #0#75 = "S"; #0#2 = "01.01.2000"; |FINSI;
       #0#3  = fec2;
       #0#77 = fec2;
       |GRABA 020#0c;
   |FINSI;

   FSalida = 0;
   |PARA;|SI FSalida = 0; |HACIENDO;

         |PINPA #0#caextrfa;
         |PINDA #0#caextrfa;             || Pinta los datos por defecto
         |ENDATOS #1#caextrfa;           || Seleccion informe

         |SI FSalida = 0;                || Si el conforme es igua a S
             |IMPRE 0;
             |SI FSalida ! 0;
                 |MENAV "0000Proceso abortado ...";
             |SINO;
                 |HAZ Imprimir;
                 |FINIMP;
             |FINSI;
         |FINSI;
   |SIGUE;
   |GRABA 020#0a;
   |CIERRA #0;

|FPRO;
