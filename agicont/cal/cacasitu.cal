|FICHEROS;
    cacasitu #0;
    cacuenta #1;         || cuentas
    caparbal #2;         || partidas de balance
    cabaexp2 #5;         || temporal situacion
    caexistc #6;         || descarga cabs.
    caexistl #7;         || descarga lins.
    canempre #30;        || empresas
    caconimp #8;
    cabucem1 #100;

    caconcep #22;
    camaasil #23;
    caseslin #110;
|FIN;

|VARIABLES;
  aFichero = "";
  &pathbal = "";
  &balance = 1;
    &entrada = 1;
    varcamp = 0;
    mes = 0;
    nivel = 0;
    sw_pasa = 0;
    linea = 0;
    FEstado = 0;
    activo = 0;
    activo1 = 0;
    pasivo = 0;
    pasivo1 = 0;
    explo = 0;
    debe1 = 0;
    haber1 = 0;
    x = 0;
    &eldesde = 0;
    &elhasta = 0;
    sw = 0;
    partida = 0;
    agrupaci = 0;
    epigrafe = 0;
    sw_epi = 0;
    aAlfa   = "";
    letrames = "";
    informe  = "";
    tipo = "";
    tipo1 = "";
    cue_perdi = "";
    dig_perdi = 0;
    &sw_inf = 0;
    &cantidad = 0;
    &cantidad1 = 0;
    &variable = "";
    &canti = 0;
    &canti1 = 0;
    &doh = "";
    &anu = 0;
    &anu1 = 0;
    &raya1 = "";
    ray1 = "            -------------------------------------------------------------------"
    ray2 = "                                              ---------------------------------"
    ray3 = "                                              ================================="
    mes_fin = 0;
    descar = 0;
    traraf = 0;
    nivelet = 0;
    &ant_agru = "";
    &ant_epig = "";
    &ant_impo = 0;
    &ant_impo1 = 0;
    t_agru = 0;
    t_agru1 = 0;
    t_epig = 0;
    t_epig1 = 0;

    para1 = 0;
    para2 = 0;
    para3 = 0;
    para4 = 0;
    nume1 = 0;
    nume2 = 0;
    nume3 = 0;
    nume4 = 0;
    alfa1 = "";
    alfa2 = "";
    alfa3 = "";
    alfa4 = "";
    ult_cuent = "";
    ult_nivel = 0;
    nue_cuent = "";
    nue_nivel = 0;
    act_cuent = "";
    act_nivel = 0;
    carac = 0;
    guarda_can = 0;
    guarda_can1 = 0;
    suma1 = 0;
    suma2 = 0;
    suma3 = 0;
    suma4 = 0;
    suma5 = 0;
    suma6 = 0;
    suma11 = 0;
    suma12 = 0;
    suma13 = 0;
    suma14 = 0;
    suma15 = 0;
    suma16 = 0;
    llegoa = 0;
    dos = 0;
    acumula = 0;
    &r_emp = 0;
    &r_per = 0;
    &r_eje = 0;
    &r_nom = "";
    &d_mes = 0;
    &h_mes = 0;
    &estru = 0;
    nom1 = "";
  &aparam = "";
  sw_exp = 0;
  descn = 0;

  swexis = 0;
  alar_c = "";
  Compara = 0;

  Per1       = 0;
  Per2       = 0;
  Comodin    = "";
  Comodin1   = "";
  Handle     = 0;
  Numeracion = 0;
  Calc       = 0;
  Empresa    = 0;
  Periodo    = 0;

   nDEstru = 0;
   nHEstru = 0;
   nSw     = 0;
   aCuenta = "";
   nNivel  = 0;
   nTSaldo = 0;
   nSwOp   = 0;
   d_cuen  = "";
   h_cuen  = "";
   d_digi  = 0;
   h_digi  = 0;
   tracan5 = 0;

  &eaFicbal = "";
  &eaNomSes = "";
  &enNumSes = 0;
  &eaDepSes = "";
  &eaDesDep = "";
  &enMasDep = 0;
  nswPrim  = 0;
  nSwError = 0;
  nSwPrint = 0;
  &eaUnidadBasico = "";
  &eaUnidadBTmp   = "";
|FIN;

|INCLUYE acceso_i;
|INCLUYE digito_i;
|INCLUYE dsmone_i;

|PROCESO antesque;
  aparam = PARAMETRO; |QBF aparam;
  |SI aparam = "fech";
      |HAZ AntesSesion;
      |NOME_DAT #1 eaFicbal;
  |FINSI;
|FPRC;

|PROCESO PintoSes; |TIPO 7;
  aparam = PARAMETRO; |QBF aparam;
  |SI aparam = "fech";
      |HAZ AntesSesion;
      |SI eaNomSes > "";
          |ATRI I; |PINTA 0505, "SESION: "; |PINTA 0513,eaNomSes;
          |SI eaDepSes > "";
              |PINTA 0558, "Departamento: "; |PINTA 0572, eaDepSes;
          |FINSI;
          |ATRI 0;
      |FINSI;
  |FINSI;
|FPRC;

____________________________________/ CALCULOS DE PANTALLA
|PROCESO mayor1;
  #0Campo = #0Campo > "  ";
  |PINTA #0Campo;
|FPRC;

|PROCESO mayor;
  alfa1 = #0Campo > " ";
  #0Campo = alfa1;
  |PINTA #0Campo;
  |SI #0Campo = "N";
      #0#24 = empre;
      #0#25 = ejerc;
      |CAMPO_MODIFICA #0#24,1,"N";
      |CAMPO_MODIFICA #0#25,1,"N";
      |PINTA #0#24;
      |PINTA #0#25;
  |SINO;
      |CAMPO_MODIFICA #0#24,1,"S";
      |CAMPO_MODIFICA #0#25,1,"S";
  |FINSI;
|FPRC;

|PROCESO deentrada;  |TIPO 7;
  |SI #0#24 = 0;  |Y #0#25 = 0;
      #0#24 = empre;
      #0#25 = ejerc - 1;
      |SI #0#25 < 0; #0#25 = 9; |FINSI;
      |PINTA #0#24;
      |PINTA #0#25;
  |FINSI;
|FPRC;

|PROCESO esbuena;
  |SI FSalida = 9;  |ACABA;  |FINSI;
  |ABRE #30;
  #30#2 = #0#24;
  #30#3 = 0;
  |LEE 011#30];
  |SI FSalida ! 0;  |O #30#2 ! #0#24;
      |MENAV "     NO Existe la empresa Contable";
      |ERROR;
  |FINSI;
  |CIERRA #30;
|FPRC;

|PROCESO consulta;
  |SI FSalida ! 9;  |ACABA;  |FINSI;
  |CIERRAT #30;
  r_emp = 0;
  |SUB_EJECUTA "caconemp.run";
  |ABRET #30;
  |ERROR;
  |SI Campo = 24;
      #0#24 = r_emp;
      #0#25 = r_per;
      |PTEC 802;
      |PTEC 802;
  |FINSI;
|FPRC;

|PROCESO esbuena1;
  |SI FSalida = 2; |ACABA; |FINSI;
  |ABRE #30;
  #30#2 = #0#24;
  #30#3 = #0#25;
  |LEE 011#30=;
  |SI FSalida ! 0;
      |MENAV "     NO Existe Periodo en la empresa Contable";
      |CIERRA #30;
      |ERROR;
      |ACABA;
  |FINSI;
  |SI #30#13 ! dig3;  |O #30#14 ! dig4;  |O #30#15 ! dig5;
                      |O #30#16 ! dig6;  |O #30#17 ! dig7;
                      |O #30#18 ! dig8;  |O #30#19 ! dig9;
      |MENAV "     NO CORRESPONDEN LOS NIVELES";
      |CIERRA #30;
      |ERROR;
      |ACABA;
  |FINSI;
  #0#26 = #30#1;
  anu1 = 1900 + #30#26;
  nom1 = #30#1;
  |SI anu1 [ 1980;
      anu1 = anu1 + 100;
  |FINSI;
  |PINTA #0#26;
  #30#2 = empre;
  #30#3 = ejerc;
  |LEE 011#30=;
  anu = 1900 + #30#26;
  |SI anu [ 1970;
      anu = anu + 100;
  |FINSI;
  |CIERRA #30;
|FPRC;

|PROCESO pintames; |TIPO 0;
  varcamp = Campo + 2;                || Coge el campo del nombre del mes
  |SI #0Campo ! 0; |Y #0Campo ! 13;
      mes = #30#11 + (#0Campo - 1);   || Operacion para saber que mes es
      |SI mes > 12; mes = mes - 12; |FINSI;
  |SINO;
      mes = #0Campo;
  |FINSI;
  |HAZ mesletra;                          || Recoge el mes en letra
  |SI Campo = 7;  |O Campo = 8;
      |ATRI I;
      |SI Campo = 7;
          |PINTA 1518, letrames;
      |SINO;
          |PINTA 1555, letrames;
      |FINSI;
      |ATRI 0;
  |SINO;
      #0varcamp = letrames;
      |PINTA #0varcamp;                       || Pinta el nombre del mes
  |FINSI;
  |SI Campo = 1;
      |SI #0#0 > #0#1;
          |MENAV "    Error de Entrada ...";
          |ERROR;
      |FINSI;                         || Si el mes hasta es menor al desde
  |FINSI;
  |SI Campo = 8;
      |SI #0#7 > #0#8;
          |MENAV "     Error de Entrada ...";
          |ERROR;
      |FINSI;
  |FINSI;
|FPRC;

|PROCESO mesletra;
  |SI mes = 0;  letrames = "Apertura  "; |FINSI;
  |SI mes = 1;  letrames = "Enero     "; |FINSI;
  |SI mes = 2;  letrames = "Febrero   "; |FINSI;
  |SI mes = 3;  letrames = "Marzo     "; |FINSI;
  |SI mes = 4;  letrames = "Abril     "; |FINSI;
  |SI mes = 5;  letrames = "Mayo      "; |FINSI;
  |SI mes = 6;  letrames = "Junio     "; |FINSI;
  |SI mes = 7;  letrames = "Julio     "; |FINSI;
  |SI mes = 8;  letrames = "Agosto    "; |FINSI;
  |SI mes = 9;  letrames = "Septiembre"; |FINSI;
  |SI mes = 10; letrames = "Octubre   "; |FINSI;
  |SI mes = 11; letrames = "Noviembre "; |FINSI;
  |SI mes = 12; letrames = "Diciembre "; |FINSI;
  |SI mes = 13; letrames = "Cierre    "; |FINSI;
|FPRC;

|PROCESO estruct; |TIPO 0;

  |SI #0#6 = 0;
      |SI #0#0 ! 0; |O #0#1 ! 12;
          |MENAV "    Para informar Estructura 00, seleccione ejercicio completo";
          |ERROR;
          |ACABA;
      |FINSI;
      |ATRI S; |PINTA 1429, " Conc.Explotacion:                              "; |ATRI 0;
      |C_M #0#28,1,"S";
      |C_V #0#28,1,"S";
      #0#28 = 1; |PINTA #0#28;
  |SINO;
      |PINTA 1429, "                                                ";
      |C_M #0#28,1,"N";
      |C_V #0#28,1,"N";
      #0#28 = 1;
  |FINSI;

  |ABRE #6;
  |SI #0#6 = 0;
      |LEE 040#6p;
  |SINO;
      #6#0 = #0#6;
      |LEE 040#6=;
  |FINSI;
  |SI FSalida ! 0;
      |MENAV "    No existe la estructura de descarga.";
      |ERROR;
  |SINO;
      cue_perdi = #6#2;
      dig_perdi = #6#3;
  |FINSI;
  |CIERRA #6;
|FPRC;

|PROCESO LeoConcep;
 |C_M #0#28,0,alfa1;
 |SI alfa1 = "N"; |ACABA; |FINSI;

 |ABRE #22;
 #22#0 = #0#28;
 |LEE 001#22=;
 |SI FSalida ! 0;
     |MENAV "    Error. No existe Concepto";
     |ERROR;
 |SINO;
     alfa1 = #22#1; alfa1 = alfa1 % 124;
     |ATRI I; |PINTA 1452, alfa1; |ATRI 0;
 |FINSI;
 |CIERRA #22;
|FPRC;

|PROCESO numnivel; |TIPO 0;
  |SI #0Campo > #30#13;
      |MENAV "    El numero de niveles no coincide con el definido en la Empresa ...";
      |ERROR;
  |FINSI;
  nivelet = 0;
  |CAMPO_MODIFICA #0#19,1,"S";
  |CAMPO_MODIFICA #0#20,1,"S";
  |CAMPO_MODIFICA #0#21,1,"S";
  |CAMPO_MODIFICA #0#22,1,"S";
  |CAMPO_MODIFICA #0#23,1,"S";
|FPRC;

|PROCESO nivelet; |TIPO 7;
  |SI #0#17 < 6; #0#23 = 0; |PINTA #0#23; |CAMPO_MODIFICA #0#23,1,"N"; |FINSI;
  |SI #0#17 < 5; #0#22 = 0; |PINTA #0#22; |CAMPO_MODIFICA #0#22,1,"N"; |FINSI;
  |SI #0#17 < 4; #0#21 = 0; |PINTA #0#21; |CAMPO_MODIFICA #0#21,1,"N"; |FINSI;
  |SI #0#17 < 3; #0#20 = 0; |PINTA #0#20; |CAMPO_MODIFICA #0#20,1,"N"; |FINSI;
  |SI #0#17 < 2; #0#19 = 0; |PINTA #0#19; |CAMPO_MODIFICA #0#19,1,"N"; |FINSI;
|FPRC;

|PROCESO nivelrep; |TIPO 0;
  |SI nivelet = 0;
      nivelet = #0#18;
  |FINSI;
  |PARA x = Campo - 1;  |SI x ] 18;  |HACIENDO x = x - 1;
        |SI #0Campo = #0x;  |Y #0Campo ! 0;
            |MENAV "     Nivel repetido ...";
            |ERROR;
            |ACABA;
            |SAL;
        |FINSI;
        |SI nivelet < #0x;
            nivelet = #0x;
        |FINSI;
  |SIGUE;
|FPRC;

|PROCESO let_nivel;
  |SI nivelet = 0;
      nivelet = #0#18;
  |FINSI;
  |PARA x = 23;  |SI x ] 18;  |HACIENDO x = x - 1;
        |SI nivelet < #0x;
            nivelet = #0x;
        |FINSI;
  |SIGUE;
|FPRC;

|PROCESO conexi;

  nCam1 = empre;
  nCam2 = ejerc;

  d_mes = #0#7;
  h_mes = #0#8;
  estru = #0#6;
  r_emp = empre;
  r_per = ejerc;
  r_eje = #30#26;
  r_nom = #30#1;
  |SI #0#6 = 0; |ACABA; |FINSI;

  |SUB_EJECUTA "caexicon";
  |SI #0#27 = "N"; |ACABA; |FINSI;
    r_emp = #0#24;
    r_per = #0#25;
    r_eje = anu1 - 2000; |SI r_eje < 0; r_eje = anu1 - 1900; |FINSI;
    r_nom = nom1;
  |SUB_EJECUTA "caexicon.run";
  empre = nCam1;
  ejerc = nCam2;
  eaMoneda = aMonedaA; |HAZ MonedasI;
|FPRC;

|| ____________________________________/

|PROCESO grabatr3;       || grabar perdidas y ganacias
  |SI #0#6 ! 0;
      |ABRE #1;
      #1#0 = cue_perdi;
      #1#1 = dig_perdi;
      |LEE 000#1=;
      |SI FSalida ! 0;
          |MENAV "     ATENCION NO EXISTE CUENTA DE PERDIDAS Y GANANCIAS";
          |CIERRA #1;
          |ERROR;
          |ACABA;
      |FINSI;
      |CIERRA #1;
      |HAZ cargatra;
      || #5acumula = 0;
      |SI #1#5 = "A";
          |PARA x = eldesde; |SI x [ elhasta; |HACIENDO x = x + 3;
                impMoneda = #1x;
                |SI aMonedaC ! aMonedaA; |Y dos = 2; |HAZ ElCMoneda; |FINSI;
                #5acumula = #5acumula + impMoneda;
          |SIGUE;
      |SINO;
          |PARA x = eldesde; |SI x [ elhasta; |HACIENDO x = x + 3;
                impMoneda = #1x;
                |SI aMonedaC ! aMonedaA; |Y dos = 2; |HAZ ElCMoneda; |FINSI;
                #5acumula = #5acumula + (impMoneda * -1);
          |SIGUE;
      |FINSI;
      #5acumula = #5acumula + explo;
      cue_perdi = #5#5;
      dig_perdi = #5#6;
      |GRABA 020#5a;
      |LIBERA #5;
 |SINO;
      |HAZ TodasLas129;
 |FINSI;
|FPRC;

|| *************************************************************************

|PROCESO AstoExplota;
 |SI #23#8 = "D";
     tracan5 = tracan5 + #23#9;
 |SINO;
     tracan5 = tracan5 - #23#9;
 |FINSI;
|FPRC;

|DEFBUCLE AstoExplota;
 #23#3;
 6;
 #1#0, #1#1, "01.01.0000", 000001, #0#28;
 #1#0, #1#1, "31.12.2999", 999999, #0#28;
 e;
 NULL, AstoExplota;
|FIN;

|PROCESO grabar129;
 tracan5 = 0;
 |BUCLE AstoExplota;
 |SI #1#5 = "P";
     tracan5 = - tracan5;
 |FINSI;
 |SI tracan5 ! 0;
      |HAZ cargatra;
      #5acumula = #5acumula + tracan5;
      |GRABA 020#5a;
      |LIBERA #5;
 |FINSI;
|FPRC;

|DEFBUCLE grabar129;
 #1#1;
 3;
 d_cuen, d_digi, "S";
 h_cuen, h_digi, "S";
 be;
 NULL, grabar129;
|FIN;

|PROCESO TodasLas129;
 d_cuen  = "129         "; d_digi = 0;
 h_cuen  = "129zzzzzzzzz"; h_digi = 9;
 |BUCLE grabar129;
|FPRC;

|PROCESO ver_exp;
  mes_fin = #0#7 + 11;  || mes iniciales
  descar = #7mes_fin;
  mes_fin = #0#8 + 11;  || mes finales
  descar = descar - #7mes_fin; || iniciales - finales

  |SI aMonedaC ! aMonedaA; |Y dos = 2;
      impMoneda = descar;
      |HAZ ElCMoneda;
      descar = impMoneda;
  |FINSI;

  |SI descar ] 0;
      act_cuent = #7#5;     || cuenta descarga D
      act_nivel = #7#6;     || digito descarga D
  |SINO;
      act_cuent = #7#8;     || cuenta descarga H
      act_nivel = #7#9;     || digito descarga H
  |FINSI;
  |SI descar = 0; |ACABA; |FINSI;
  |SI act_cuent = #1#0; |Y act_nivel = #1#1;
     sw_exp = 1;
     |SI #1#5 = "H"; descar = -descar; |FINSI;
     descn = descn + descar;
 |FINSI;
|FPRC;

|DEFBUCLE cabasitu9;
 #7#1;
 ;
 nDEstru, 01;
 nHEstru, 99;
 e;
 NULL, ver_exp;
|FIN;

|PROCESO calexpl01;
  sw_exp = 0;
  descn= 0;
  |BUCLE cabasitu9;
  |SI sw_exp = 1
      |SI #1#5 = "D";
          |PARA x = eldesde; |SI x [ elhasta; |HACIENDO x = x + 3;
                impMoneda = #1x;
                |SI aMonedaC ! aMonedaA; |Y dos = 2; |HAZ ElCMoneda; |FINSI;
                debe1 = debe1 - impMoneda;
          |SIGUE;
          debe1 = debe1 + descn;
      |SINO;
          |PARA x = eldesde; |SI x [ elhasta; |HACIENDO x = x + 3;
                impMoneda = #1x;
                |SI aMonedaC ! aMonedaA; |Y dos = 2; |HAZ ElCMoneda; |FINSI;
                haber1 = haber1 - (impMoneda * -1);
          |SIGUE;
          haber1 = haber1 + descn;
     |FINSI;
  |FINSI;
|FPRC;

|DEFBUCLE calexpl0;
 #1#4;
 3;
 "D", 1, 01, 00, 00,"S";
 "H", 4, 99, 99, 99,"S";
 e;
 NULL,calexpl01;
|FIN;

|PROCESO explota1;
  |SI #1#3 = "N";  |ACABA;  |FINSI;
  |SI #1#5 = "D";
      |PARA x = eldesde; |SI x [ elhasta; |HACIENDO x = x + 3;
           impMoneda = #1x;
            |SI aMonedaC ! aMonedaA; |Y dos = 2; |HAZ ElCMoneda; |FINSI;
            debe1 = debe1 + impMoneda;
      |SIGUE;
  |SINO;
      |PARA x = eldesde; |SI x [ elhasta; |HACIENDO x = x + 3;
            impMoneda = #1x;
            |SI aMonedaC ! aMonedaA; |Y dos = 2; |HAZ ElCMoneda; |FINSI;
            haber1 = haber1 + (impMoneda * -1);
      |SIGUE;
  |FINSI;
  canti = haber1 - debe1;
  explo = canti;
|FPRC;

|DEFBUCLE explota;
 #1#4;
 3;
 " ", 1,  0,  0,  0, "S";
 "z", 4, 99, 99, 99, "S";
 e;
 NULL, explota1;
|FIN;

|PROCESO sacacambio;
  alfa1 = alfa1 + "            ";
  #1#0 = alfa1 % 112;
  #1#1 = x;
  |LEE 000#1=;
  |SI FSalida ! 0;
      #1#0 = alfa1 % 112;
      #1#2 = "NO EXISTE CUENTA EN MAESTRO";
  |FINSI;
  guarda_can = cantidad;
  guarda_can1 = cantidad1;
  |SI x = 1; cantidad = suma1; cantidad1 = suma11; suma1 = 0; suma11 = 0; |FINSI;
  |SI x = 2; cantidad = suma2; cantidad1 = suma12; suma2 = 0; suma12 = 0; |FINSI;
  |SI x = 3; cantidad = suma3; cantidad1 = suma13; suma3 = 0; suma13 = 0; |FINSI;
  |SI x = 4; cantidad = suma4; cantidad1 = suma14; suma4 = 0; suma14 = 0; |FINSI;
  |SI x = 5; cantidad = suma5; cantidad1 = suma15; suma5 = 0; suma15 = 0; |FINSI;
  |SI x = 6; cantidad = suma6; cantidad1 = suma16; suma6 = 0; suma16 = 0; |FINSI;
  sw_inf = 6;
  |PRINT;
  cantidad = guarda_can;
  cantidad1 = guarda_can1;
  llegoa = x;
|FPRC;


|PROCESO haycambio;
  x = act_nivel - 1;
|ET repetimos;
  |SI x = #0#18;  |O x = #0#19;  |O x = #0#20;  |O x = #0#21;
                  |O x = #0#22;  |O x = #0#23;
      carac = 0;
      |SI x = 6;   carac = dig9;  |FINSI;
      |SI x = 5;   carac = dig8;  |FINSI;
      |SI x = 4;   carac = dig7;  |FINSI;
      |SI x = 3;   carac = dig6;  |FINSI;
      |SI x = 2;   carac = dig5;  |FINSI;
      |SI x = 1;   carac = dig4;  |FINSI;
      |SI carac ! 0;
          nume1 = carac + 100;
          alfa1 = act_cuent % nume1;
          alfa2 = nue_cuent % nume1;
          |SI alfa1 ! alfa2;
              |HAZ sacacambio;
          |FINSI;
      |FINSI;
  |FINSI;
  x = x - 1;
  |SI x ! 0;  |VETE repetimos;  |FINSI;
|FPRC;

|PROCESO cambio1;
  x = llegoa - 1;
  |SI x [ 0;  |ACABA;  |FINSI;
|ET repeti1;
  |SI x = #0#18;  |O x = #0#19;  |O x = #0#20;  |O x = #0#21;
                  |O x = #0#22;  |O x = #0#23;
      carac = 0;
      |SI x = 6;   carac = dig9;  |FINSI;
      |SI x = 5;   carac = dig8;  |FINSI;
      |SI x = 4;   carac = dig7;  |FINSI;
      |SI x = 3;   carac = dig6;  |FINSI;
      |SI x = 2;   carac = dig5;  |FINSI;
      |SI x = 1;   carac = dig4;  |FINSI;
      |SI carac ! 0;
          nume1 = carac + 100;
          alfa1 = act_cuent % nume1;
          |HAZ sacacambio;
      |FINSI;
  |FINSI;
  x = x - 1;
  |SI x ! 0;  |VETE repeti1;  |FINSI;
  llegoa = nivelet;
|FPRC;

|PROCESO exis_3000;
  swexis = 1;
  |ERROR10;
|FPRC;

|DEFBUCLE exis1;
 #7#2;
 ;
 nDEstru, #1#0, #1#1;
 nHEstru, alar_c, 9;
 e;
 NULL,exis_3000;
|FIN;

|PROCESO calculit;
  |HAZ digitos2;
  |SI d_error ! 0;  |ACABA;  |FINSI;
  |SI #1#1 > nivelet;    |ACABA;  |FINSI;

  |SI #0#6 ! 0;
      |SI #1#0 = cue_perdi;  |ACABA;  |FINSI;
  |SINO;
      act_cuent = #1#0 % 103;
      |SI act_cuent = "129"; |ACABA; |FINSI;
  |FINSI;

  act_cuent = #1#0 % 101;
  swexis = 0;
  |SI act_cuent = "3";
      alar_c = #1#0; |HAZ alargacuen;
      |BUCLE exis1;
  |FINSI;
  |SI swexis = 1; |ACABA; |FINSI;

  |SI #1#3 = "S";  |O #1#1 = nivelet;
      |HAZ cargatra;
      #5acumula = 0;
      |SI #1#5 = "A";
          |PARA x = eldesde; |SI x [ elhasta; |HACIENDO x = x + 3;
                impMoneda = #1x;
                |SI aMonedaC ! aMonedaA; |Y dos = 2; |HAZ ElCMoneda; |FINSI;
                #5acumula = #5acumula + impMoneda;
          |SIGUE;
      |SINO;
          |PARA x = eldesde; |SI x [ elhasta; |HACIENDO x = x + 3;
                impMoneda = #1x;
                |SI aMonedaC ! aMonedaA; |Y dos = 2; |HAZ ElCMoneda; |FINSI;
                #5acumula = #5acumula + (impMoneda * -1);
           |SIGUE;
      |FINSI;
      |GRABA 020#5a;
      |LIBERA #5;
  |FINSI;
|FPRC;

|DEFBUCLE todas_ctas;
 #1#1;
5,4,6,7,8;
"            ", 0, "A", 0, 01, 00, 00;
"zzzzzzzzzzzz", 9, "P", 0, 99, 99, 99;
 e;
 NULL, calculit;
|FIN;

|PROCESO rup_tipo;
  sw_inf = 5;  |PRINT;                     || linea en blanco

  |SI tipo = "A";
      variable = "                T O T A L    A C T I V O: ";
      canti = activo;
      canti1 = activo1;
  |SINO;
      variable = "                T O T A L    P A S I V O: ";
      canti = pasivo;
      canti1 = pasivo1;
  |FINSI;
  raya1 = ray1;
  sw_inf = 10; |PRINT;
  sw_inf = 1;  |PRINT;                     || linea de totales
  raya1 = ray3;
  sw_inf = 10; |PRINT;
  |PIEINF;
  doh = "P A S I V O";
  tipo = #5#0;
|FPRC;

|PROCESO rup_agru;
  |SI ant_agru ! "";
      ant_impo = t_agru;
      ant_impo1  = t_agru1;
      raya1 = ray1;
      sw_inf = 10; |PRINT;  || raya
      sw_inf = 7;  |PRINT;  || agrupacion (con importe)
      sw_inf = 5;  |PRINT;  || linea blanco
  |FINSI;

  agrupaci = #5#2;

  t_agru = 0;
  t_agru1 = 0;
|FPRC;

|PROCESO rup_epig;
  |SI ult_nivel ! 1;
      nue_cuent = "";
      nue_nivel = 0;
  |FINSI;

  |SI ant_epig ! "";
      ant_impo = t_epig;
      ant_impo1 = t_epig1;
      raya1 = ray2;
      sw_inf = 10; |PRINT;  || raya
      sw_inf = 2;  |PRINT;  || epigrafe
      sw_inf = 5;  |PRINT;  || linea blanco
  |FINSI;
  epigrafe = #5#3;
  partida = #5#4;

  t_epig = 0;
  t_epig1 = 0;
  ult_cuent = "";
  ult_nivel = 0;
|FPRC;

|PROCESO impresio;
  |SI #5#11 = 0; |Y #5#12 = 0;  |ACABA;  |FINSI;
  cantidad = #5#11;
  cantidad1 = #5#12;
  |SI #5#0 = "A";
      activo = activo + #5#11;
      activo1 = activo1 + #5#12;
  |SINO;
      pasivo = pasivo + #5#11;
      pasivo1 = pasivo1 + #5#12;
  |FINSI;
  |SI sw = 0;
      tipo = #5#0;
      agrupaci = #5#2;
      epigrafe = #5#3;
      partida = #5#4;
      |SI #5#0 = "A";
          doh = "A C T I V O";
      |SINO;
          doh = "P A S I V O";
      |FINSI;
      act_cuent = #5#5;
      act_nivel = #5#6;
      sw = 1;
  |FINSI;

  nue_cuent = #5#5;
  nue_nivel = #5#6;

  |HAZ haycambio;


  |SI tipo ! #5#0;
      |HAZ cambio1;
      |HAZ rup_epig;
      |HAZ rup_agru;
      |HAZ rup_tipo;
  |SINO;
      |SI agrupaci ! #5#2;
          |HAZ cambio1;
          |HAZ rup_epig;
          |HAZ rup_agru;
      |SINO;
          |SI epigrafe ! #5#3;
              |HAZ cambio1;
              |HAZ rup_epig;
          |FINSI;
      |FINSI;
  |FINSI;

  act_cuent = #5#5;
  act_nivel = #5#6;
  tipo = #5#0;
  agrupaci = #5#2;
  epigrafe = #5#3;
  partida = #5#4;
  #1#0 = #5#5;
  #1#2 = #5#7;
  sw_inf = 6;
  |PRINT;
  llegoa = nivelet;
  suma1 = suma1 + cantidad;
  suma2 = suma2 + cantidad;
  suma3 = suma3 + cantidad;
  suma4 = suma4 + cantidad;
  suma5 = suma5 + cantidad;
  suma6 = suma6 + cantidad;
  suma11 = suma11 + cantidad1;
  suma12 = suma12 + cantidad1;
  suma13 = suma13 + cantidad1;
  suma14 = suma14 + cantidad1;
  suma15 = suma15 + cantidad1;
  suma16 = suma16 + cantidad1;
  nue_cuent = #5#5;
  nue_nivel = #5#6;
  ant_agru = #5#8;
  |QBF ant_agru;

  ant_epig = #5#9;
  |QBF ant_epig;

  t_agru = t_agru + #5#11;
  t_agru1 = t_agru1 + #5#12;
  t_epig = t_epig + #5#11;
  t_epig1 = t_epig1 + #5#12;
|FPRC;

|DEFBUCLE imprimiendo;
 #5#1;
 ;
"A", 0, 01, 00, 00, "            ", 0;
"P", 0, 99, 99, 99, "zzzzzzzzzzzz", 9;
 ;
 NULL, impresio;
|FIN;

|PROCESO final;
  |HAZ cambio1;
  |HAZ rup_epig;
  |HAZ rup_agru;
  |HAZ rup_tipo;
|FPRC;


|PROCESO acortacuen;
  |SI nivelet < #1#1;
      |SI nivelet = 1;  traraf = 100 + dig4;  |FINSI;
      |SI nivelet = 2;  traraf = 100 + dig5;  |FINSI;
      |SI nivelet = 3;  traraf = 100 + dig6;  |FINSI;
      |SI nivelet = 4;  traraf = 100 + dig7;  |FINSI;
      |SI nivelet = 5;  traraf = 100 + dig8;  |FINSI;
      #1#0 = #1#0 % traraf;
      #1#1 = nivelet;
      #1#0 = #1#0 + "              ";
      #1#0 = #1#0 % 112;
      |LEE 010#1=;
  |FINSI;
|FPRC;

|PROCESO cargatra;
  |HAZ acortacuen;
  #5#0 = #1#5;
  #5#1 = #1#4;
  #5#2 = #1#6;
  #5#3 = #1#7;
  #5#4 = #1#8;
  #5#5 = #1#0;
  #5#6 = #1#1;
  #5#7 = #1#2;
  |LEE 000#5=;
  |SI FSalida ! 0;
      |DDEFECTO #5;
      #5#0 = #1#5;
      #5#1 = #1#4;
      #5#2 = #1#6;
      #5#3 = #1#7;
      #5#4 = #1#8;
  ||    |HAZ acortacuen;
      #5#5 = #1#0;
      #5#6 = #1#1;
      #5#7 = #1#2;
      #5#11 = 0;
      #5#12 = 0;
      |GRABA 020#5c;
  |FINSI;
  |DDEFECTO #2;
  #2#0 = #1#5;
  #2#1 = #1#6;
  #2#3 = #1#7;
  #2#5 = #1#8;
  |LEE 000#2=;
  |SI FSalida ! 0;
      #2#2 = "";
      #2#4 = "";
      #2#6 = "";
  |FINSI;
  #5#8 = #2#2;
  #5#9 = #2#4;
  #5#10 = #2#6;
|FPRC;

|PROCESO descarg;
  |HAZ digitos2;
  |SI d_error ! 0;  |ACABA;  |FINSI;
||---------------------------------  grabo CUENTAS DE EXISTENCIAS EN TRABAJO

  mes_fin = #0#8 + 11;  || mes finales
  |HAZ cargatra;
  descar = #7mes_fin;
  |SI aMonedaA ! aMonedaC; |Y dos = 2;
      impMoneda = descar; |HAZ ElCMoneda; descar = impMoneda;
  |FINSI;

  |SI #1#5 = "P"; descar = -descar; |FINSI;
  #5acumula = #5acumula + descar;
  |GRABA 020#5a;
  |LIBERA #5;
|FPRC;

|PROCESO lineades;
  #1#0 = #7#2;
  #1#1 = #7#3;
  |LEE 000#1=;
  |SI FSalida = 0;
      |HAZ descarg;
  |FINSI;
|FPRC;

|DEFBUCLE LeeDescarga;
 #7#1;
 ;
 nDEstru, 01;
 nHEstru, 99;
 ;
 NULL, lineades;
|FIN;

|PROCESO Borrar;
    activo  = 0;
    activo1 = 0;
    pasivo  = 0;
    pasivo1 = 0;
    explo   = 0;
    debe1   = 0;
    haber1  = 0;
    sw = 0;
    partida  = 0;
    agrupaci = 0;
    epigrafe = 0;
    sw_epi   = 0;
    t_agru   = 0;
    t_agru1  = 0;
    t_epig   = 0;
    t_epig1  = 0;

    suma1    = 0;
    suma2    = 0;
    suma3    = 0;
    suma4    = 0;
    suma5    = 0;
    suma6    = 0;
    suma11   = 0;
    suma12   = 0;
    suma13   = 0;
    suma14   = 0;
    suma15   = 0;
    suma16   = 0;
    descn = 0;
    tracan5 = 0;
|FPRC;
____________________________________/ CALCULO PRINCIPAL
|PROCESO HagoTodo;

  |HAZ Borrar;
  sw = 0;
  eldesde = (#0#0 * 3 ) + 11;
  elhasta = (#0#1 * 3 ) + 11;
  |PARA dos = 1;  |SI dos [ 2;  |HACIENDO dos = dos + 1;
        acumula = 11;
        |SI dos = 2;  |Y #0#27 = "N";
            |SAL;
        |FINSI;
        |SI dos = 2;
            acumula = 12;
            |DBASE dir_fich;
            dir_fich = dir_fich + "fich/";
            alfa1 = #0#24;   alfa1 = "00000" + alfa1;   alfa1 = alfa1 % -105;
            alfa2 = #0#25;   alfa2 = "0"   + alfa2;   alfa2 = alfa2 % -101;
            alfa3 = dir_fich + alfa1 + alfa2 + "/";
            |PATH_DAT #1 alfa3;
            |PATH_DAT #2 alfa3;
            |PATH_DAT #6 alfa3;
            |PATH_DAT #7 alfa3;
            |PATH_DAT #8 alfa3;
            |PATH_DAT #23 alfa3;
            |ABRE #30;
            #30#2 = #0#24;
            #30#3 = #0#25;
            |LEE 011#30=;
            |CIERRA #30;

            enEmpresa = #0#24;
            enPeriodo = #0#25;
            |HAZ LeeMonedaI;
            aMonedaC = eaMoneda;
            |SI aMonedaA = "E";  EURODB = 2; |FINSI;
        |FINSI;
        |ABRE #8;
        |LEE 111#8p;
        |SI #8#2 = "S";  |Y #8#3 = "N";  |Y aparam = "";
            |SI dos = 2;
                pathbal = alfa3;
            |SINO;
                |DFICE pathbal;
            |FINSI;
            balance = 1;
            |CIERRA #8;
            |SUB_EJECUTA "careccue.tab";
        |FINSI;
        |CIERRA #8;

||... Modificar Cuentas a balance segun saldo
  |SI dos = 1;
      r_emp = empre;
      r_per = ejerc;
  |SINO;
      r_emp = #0#24;
      r_per = #0#25;
  |FINSI;
  |SUB_EJECUTA "carecsig.run";
|| ---------------------------------------------------------------------

        |SI aMonedaA = "E";  EURODB = 2; |FINSI;

        |ABRET #1;                    || plan contable
        |ABRE #2;                     || partidas de balance
        |ABRE #5;
        |ABRE #6;                    ||
        explo = 0;
        debe1 = 0;
        haber1 = 0;
        linea = 0;                   ||
        |SI #0#6 ! 0;
            #6#0 = #0#6;                 ||  Calculo Existencias
            |LEE 000#6=;                 ||
            |BUCLELIN 2lineades#6;       ||
        |SINO;
             |BUCLE LeeDescarga;
        |FINSI;
        |CIERRA #6;                  ||

        |BUCLE calexpl0;             ||  calcula explota - variacion;
        |BUCLE explota;              ||  Calcula explotacion;
        |HAZ grabatr3;               ||  Graba perdidas y ganancias
        |BUCLE todas_ctas;           ||  calcula resto de cuentas
        |CIERRAT #1;
        |CIERRA #2;
        |CIERRA #5;
  |SIGUE;

          |ABRE #30;
          #30#2 = empre;
          #30#3 = ejerc;
          |LEE 011#30=;
          |CIERRA #30;
          eaMoneda = aMonedaA; |HAZ MonedasI;

  |ABRET #1;
  |BUCLE imprimiendo;          || lectura e impresion
  |HAZ final;
  |CIERRAT #1;
  |DELINDEX #5;
|FPRC;

|PROCESO PorSesiones;
   eaDepSes = #110#1;
   eaDesDep = #110#7;

   alfa1 = #110#6; |QBF alfa1;
   |SI alfa1 ! "";
       eaFicbal  = alfa1;
       |NOME_DAT #1 eaFicbal;
       |HAZ HagoTodo;
   |FINSI;
|FPRC;

|DEFBUCLE PorSesiones;
 #110#1;
 ;
 enNumSes, "  ";
 enNumSes, "zz";
 ;
 NULL, PorSesiones;
|FIN;

|PROCESO impre; |TIPO 3;

  aMonedaA = eaMoneda;
  |SI #0#27 = "N"; aMonedaC = aMonedaA; |FINSI;
  |HAZ let_nivel;
  |HAZ antesque;
  |HAZ conexi;

  aFichero = ("5c" + Usuario) % 108;
  |NOME_DAT #5 aFichero;

  |ABRE #5; |CIERRA #5; |DELINDEX #5;

  nDEstru = #0#6; nHEstru = #0#6;
  |SI #0#6 = 0; nDEstru = 01; nHEstru = 99; |FINSI;

  |IMPRE 0;
  |SI FSalida ! 0;  |ACABA;  |FINSI;
  |HAZ los_informes;
  |SI #0#27 = "S";
      informe = que_i1;
  |SINO;
      informe = que_i2;
  |FINSI;
  |INFOR informe;
  |SI FSalida ! 0;  |FINIMP;  |ACABA;  |FINSI;

  |SI enMasDep = 0;
      |HAZ HagoTodo;
  |SINO;
      |BUCLE PorSesiones;
      eaDepSes = "TODOS";
  |FINSI;
  |FININF;
  |FINIMP;
|FPRC;

|PROCESO alargacuen;
  |QBF alar_c;
  alar_c = alar_c + "99999999999999";
  alar_c = alar_c % 112;
|FPRC;

|PROCESO Empresas;
     #48#2 = #cabucem1#0;
     #48#3 = #cabucem1#1;
     |LEE 000#48=;
     |SI FSalida = 0;
        |DBASE fich_alta; |QBF fich_alta;
        fich_alta = fich_alta + "fich/" + (("00000" + #cabucem1#0) % -105) + #cabucem1#1 + "/";
        |PATH_DAT #0 fich_alta; |PATH_DAT #1 fich_alta;
        |PATH_DAT #2 fich_alta; |PATH_DAT #5 fich_alta;
        |PATH_DAT #6 fich_alta; |PATH_DAT #7 fich_alta;
        |PATH_DAT #8 fich_alta; |PATH_DAT #23 fich_alta;
        #30#2 = #48#2; #30#3 = #48#3; |LEE 000#30=;
        |HAZ inicio;
        aMonedaA = eaMoneda;
        |DDEFECTO #0;
        |SI Compara = 1;
           #cacasitu#27 = "S";
           #cacasitu#24 = #cabucem1#0;
           |SI #cabucem1#1 > 0;
              #cacasitu#25 = #cabucem1#1 - 1;
           |SINO;
              #cacasitu#25 = 9;
           |FINSI;
           |HAZ esbuena; |HAZ esbuena1;
        |FINSI;
        #cacasitu#0  = Per1; #cacasitu#7 = Per1;
        #cacasitu#1  = Per2; #cacasitu#8 = Per2;
        Campo = 0; |HAZ pintames;
        Campo = 1; |HAZ pintames;
        #cacasitu#17 = #48#13;
        #cacasitu#18 = #48#14;
        #cacasitu#19 = #48#15;
        #cacasitu#20 = #48#16;
        #cacasitu#21 = #48#17;
        #cacasitu#22 = #48#18;
        #cacasitu#23 = #48#19;

        |HAZ impre;

        |DBASE Comodin1; |QBF Comodin1;
        Comodin1 = Comodin1 + "hacienda/PresLibr/" + (("00000" + #cabucem1#0) % -105) + #cabucem1#1 + "/";
        Comodin1 = Comodin1 + "INV_CUEN*.xls";
        |_PDIR Comodin1;
        |SI FSalida = 0;
           |PARA; |SI FSalida = 0; |HACIENDO;
              Comodin = Comodin1 % -107; Comodin = Comodin % 0103;
              Numeracion = Comodin; Numeracion = Numeracion + 1;
              |_SDIR Comodin1;
           |SIGUE;
        |SINO;
           Numeracion = 1;
        |FINSI;
        aAlfa = "";
        Comodin = eaUnidadBTmp + "/tmp1.xls";
        |XABRE "C", Comodin, Handle;
        |SI FSalida ] 0;
           |XCIERRA Handle;
           |DBASE Comodin1; |QBF Comodin1;
           Comodin1 = Comodin1 + "hacienda/PresLibr/";
           |MKDIR Comodin1;
           Comodin1 = Comodin1 + (("00000" + #cabucem1#0) % -105) + #cabucem1#1 + "/";
           |MKDIR Comodin1;
           Comodin1 = Comodin1 + "INV_CUEN_" + (("000" + Numeracion) % -103) + ".xls";
           |IP_REMOTO aAlfa; |QBF aAlfa;
           |SI aAlfa ! "";
              |RECIBE_FICHERO Comodin, Comodin1;
           |SINO;
              |COPIA_FICHERO Comodin, Comodin1;
           |FINSI;
           Numeracion = Numeracion + 1;
           FSalida = 1;

           |PARA; |SI FSalida ! 0; |HACIENDO;
              |RFBORRA Comodin;
              |SI FSalida ! 0;
                 |MENAV "    Termine la tarea de Excel que tiene activa";
              |FINSI;
           |SIGUE;
        |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE Empresas;
#cabucem1#1;
;
INICIO;
FINAL;
;
NULL,Empresas;
|FIN;

|PROGRAMA;
   aparam = PARAMETRO; |QBF aparam;

   |SI aparam = ""; |O aparam = "fech";
      |HAZ inicio;
      |CLS;
      |CABEZA "Balance Situacion II";
      |PINPA #0#0; |PINDA #0#0;
      |ENDATOS #1#0;
      |SI FSalida = 0;
         |HAZ impre;
      |FINSI;
   |SINO;
      Comodin = PARAMETRO;
      Comodin = Comodin - ",C";
      |SI FSalida ! 0;
         Compara = 1;
      |SINO;
         Compara = 0;
      |FINSI;
      Comodin = Comodin - ",";
      |SI FSalida ! 0;
         Calc = ((FSalida / 100) ! 0) + 99;
         Comodin1 = Comodin % 0101;
         Per1 = Comodin1; Comodin = Comodin - Comodin1;
         Per2 = Comodin;
      |FINSI;
      Empresa = #48#2; Periodo = #48#3;
      |ABRE #48; |BUCLE Empresas; |CIERRA #48;
      #48#2 = Empresa;
      #48#3 = Periodo;
      |LEE 000#48=;
      |SI FSalida = 0;
         |DBASE fich_alta; |QBF fich_alta;
         fich_alta = fich_alta + "fich/" + (("00000" + #cabucem1#0) % -105) + #cabucem1#1 + "/";
         |PATH_DAT #0 fich_alta; |PATH_DAT #1 fich_alta;
         |PATH_DAT #2 fich_alta; |PATH_DAT #5 fich_alta;
         |PATH_DAT #6 fich_alta; |PATH_DAT #7 fich_alta;
         |PATH_DAT #8 fich_alta; |PATH_DAT #23 fich_alta;
      |FINSI;
   |FINSI;
|FPRO;
