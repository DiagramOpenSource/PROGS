|FICHEROS;
    catem001;
    catem002;

    cacuenta;
    camanrec;

    agif041@;
    agif114@;
|FIN;;

|VARIABLES;
    &entrada = 1;

    aAlfa    = "";
    aAlfa1   = "";
    aAlfa2   = "";
    aAlfa3   = "";
    nCampo   = 0;
    nInkey   = 0;
    nNume1   = 0;
    nNume2   = 0;
    nPara1   = 0;
    aDef     = "";
    aPath    = "";
|FIN;

|INCLUYE acces2_i;
|INCLUYE varnue_i;
|INCLUYE puntos_i;
|| *********************************************************************

|CALCULO DeDonde; |TIPO 0;
   |SI #catem001#15 = "C";
       aAlfa1 = "N";
       #catem001#16 = ""; |PINTA #catem001#16;
       #catem001#17 = 0;  |PINTA #catem001#17;
       #catem001#18 = 0;  |PINTA #catem001#18;
   |SINO;
       aAlfa1 = "S";
   |FINSI;
   |C_M #catem001#16, 1, aAlfa1;
   |C_M #catem001#17, 1, aAlfa1;
   |C_M #catem001#18, 1, aAlfa1;
|FCAL;

|CALCULO PonBarra; |TIPO 0;
    nInkey = FSalida;
    |C_M #catem001#Campo#,0,aAlfa1;
    |SI aAlfa1 = "N"; |ACABA; |FINSI;

    |SI nInkey  = 10; |O nInkey  = 11;
        |SI nInkey = 10;
            aAlfa1 = "D c:\";
            |BROWSE aAlfa1;
            aAlfa1 = aAlfa1 % 270;
        |FINSI;
        |SI nInkey = 11;
            |DBASS aAlfa1;
            aAlfa1 = aAlfa1 + "dscomer8";
        |FINSI;
        #catem001#Campo# = aAlfa1;
        |PINTA #catem001#Campo#
    |FINSI;

    aAlfa1 = #catem001#Campo#;
    |QBF aAlfa1;
    |SI aAlfa1 = "";
        |MENAV "0000Error, Introduzca un directorio";
        |ERROR;
        |ACABA;
    |FINSI;

    aAlfa2 = aAlfa1 % -101;
    |SI aAlfa2 ! "/";|Y aAlfa2 ! "\";
         aAlfa1 = aAlfa1 + "/";
         #catem001#Campo# = aAlfa1;
         |PINTA #catem001#Campo#;
    |FINSI;

    aDef =  aAlfa1 + "def/agifa041.mas";
    |CARGA_DEF aDef, agif041@;
    |SI FSalida ! 0;
        |MENAV "    Imposible encontrar aplicacion en el path indicado";
        |ERROR;
    |SINO;
        |DESCARGA_DEF #agif041@;
        |SI #catem001#17 = 0;
            #catem001#17 = enEmpresa;
            #catem001#18 = 5;
        |FINSI;
        aAlfa1 = #catem001#16 - "dscomer8";
        |C_M #catem001#18,1,"S";
        |SI FSalida ! 0;
            #catem001#18 = 2;
            |C_M #catem001#18,1,"N";
        |FINSI;
        |PINTA #catem001#17;
        |PINTA #catem001#18;
    |FINSI;
|FCAL;

|CALCULO NoCero; |TIPO 0;
    nInkey = FSalida;
    |C_M #catem001#Campo#,0,aAlfa1;
    |SI aAlfa1 = "N"; |ACABA; |FINSI;

    |SI #catem001#Campo# = 0; |ERROR; |FINSI;
|FCAL;

|PROCESO seleccion;
   |SI #catem001#2 = "X";
       |PARA nCampo = 3; |SI nCampo [ 14; |HACIENDO nCampo = nCampo + 1;
             |C_M #catem001#nCampo#, 1, "N";
             #catem001#nCampo# = 0;
             |PINTA #catem001#nCampo#;
       |SIGUE;
   |FINSI;

   |SI #catem001#2 = "S";
       |C_M #catem001#3, 1, "S";
       |C_M #catem001#4, 1, "S";
       |PARA nCampo = 5; |SI nCampo [ 14; |HACIENDO nCampo = nCampo + 1;
             |C_M #catem001#nCampo#, 1, "N";
             #catem001#nCampo# = 0;
             |PINTA #catem001#nCampo#;
       |SIGUE;
   |FINSI;

   |SI #catem001#2 = "N";
       |C_M #catem001#3, 1, "N"; #catem001#3 = 01; |PINTA #catem001#3;
       |C_M #catem001#4, 1, "N"; #catem001#4 = 99; |PINTA #catem001#4;

       |PARA nCampo = 5; |SI nCampo [ 14; |HACIENDO nCampo = nCampo + 1;
             |C_M #catem001#nCampo#, 1, "S";
       |SIGUE;
  |FINSI;
|FPRC;

|CALCULO NoMasRec; |TIPO 0;
     |SI #catem001#Campo# = 0;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 14;  |HACIENDO nCampo = nCampo + 1;
               #catem001#nCampo# = 0;  |C_M #catem001#nCampo#, 1, "N";
         |SIGUE;
     |SINO;
         |PARA nCampo = Campo + 1;  |SI nCampo [ 14;  |HACIENDO nCampo = nCampo + 1;
               |C_M #catem001#nCampo#, 1, "S";
         |SIGUE;
     |FINSI;
|FCAL;

|PROCESO inf;
    #cacuenta#0 = "            ";
    #cacuenta#1 = 0;
|FPRC;

|PROCESO sup;
    #cacuenta#0 = "zzzzzzzzzzzz";
    #cacuenta#1 = 9;
|FPRC;

|CALCULO cuefin; |TIPO 6;
    nInkey = FSalida;

    aAlfa = #catem002#2 - "*";
    |SI FSalida = 0;
        p_alfa1 = #catem002#2;
        salidas = FSalida;
        |HAZ punto;
        |SI p_error ! 0;  |ERROR;  |ACABA;  |FINSI;
    |FINSI;
|FCAL;

|CALCULO cuenta;  |TIPO 0;
     |SI nInkey = 10;
         |ABRE #cacuenta;
         #cacuenta#0 = #catem002#2;
         |CONSULTA_F_CLAVE #1#cacuenta, inf, sup;
         #catem002#2 = #cacuenta#0;
         |PINTA #catem002#2;
         |CIERRA #cacuenta;
     |FINSI;

     aAlfa = #catem002#2 - "*";
     |SI FSalida ! 0;
         aAlfa = #catem002#2; |QBF aAlfa;
         aAlfa1 = aAlfa % 0;
         nNume1 = aAlfa1;
         aAlfa1 = "";
         aAlfa3 = "";
         |PARA nPara1 = 1; |SI nPara1 [ nNume1; |HACIENDO nPara1 = nPara1 + 1;
               nNume2 = (nPara1 * 100) + 1;
               aAlfa2 = aAlfa % nNume2;
               aAlfa1 = aAlfa1 + aAlfa2;
               |SI aAlfa2 = "*";
                   |SAL;
               |SINO;
                   aAlfa3 = aAlfa3 + aAlfa2;
               |FINSI;
         |SIGUE;
         #catem002#2 = aAlfa1; |PINTA #catem002#2;
         #catem002#4 = "Grupo de Cuentas " + aAlfa3;
         |PINTA #catem002#4;
     |SINO;
          eaCuenta = #catem002#2;
          |HAZ eSacaNivel;
          |SI enNivel = 0;
              |MENAV "0000Longitud de Cuenta Fuera de Nivel ";
              |ERROR;
          |SINO;
              |ABRE #cacuenta;
              #cacuenta#0 = eaCuenta;
              #cacuenta#1 = enNivel;
              |LEE 041#cacuenta.=;
              |SI FSalida ! 0;
                  |MENAV "0000Error. No existe Cuentas";
                  |ERROR;
              |SINO;
                  |SI #cacuenta#3 = "N";
                      |MENAV "0000Error. Cuenta sin pase directo";
                      |ERROR;
                  |SINO;
                      #catem002#4 = #cacuenta#2;
                      #catem002#3 = "D";
                      |SI #cacuenta#5 = "H"; |O #cacuenta#5 = "P"; |O #cacuenta#5 = "G";
                          #catem002#3 = "H";
                      |FINSI;
                      |PINTA #catem002#4;
                      |PINTA #catem002#3;
                  |FINSI;
              |FINSI;
              |CIERRA #cacuenta;
          |FINSI;
     |FINSI;
|FCAL;
