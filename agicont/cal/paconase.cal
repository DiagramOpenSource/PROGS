|FICHEROS;
    paconase #00;   || captura 'CONASE4'
    ctempre  #01;   || empresas 'CONASE4'
    camandia #02;   || diarios 'AGICONT'
    ctlipun1 #03;   || apuntes lins. 'CONASE4'
    camaasic #04;   || apuntes cabs. 'AGICONT'
    camaasil #05;   || apuntes lins. 'AGICONT'
    ctcuen1  #06;   || cuentas 'CONASE4'
    cacuenta #07;   || cuentas 'AGICONT'
    caclipro #08;   || clientes/proveedores  'AGICONT'
    ctivar   #10;   || iva repercutido 'CONASE4'
    caivarep #11;   || iva repercutido 'AGICONT'
    ctivas   #12;   || iva soportado 'CONASE4'
    caivasop #13;   || iva soportado 'AGICONT'
    ctconcep #14;   || conceptos 'CONASE4'
    caconcep #15;   || conceptos 'AGICONT'
    ctlicobr #16;   || cobros lins. 'CONASE4'
    camancob #17;   || cobros 'AGICONT'
    ctlipago #18;   || pagos lins. 'CONASE4'
    camanpag #19;   || pagos 'AGICONT'
    ctcabrem #20;   || remesas cabs. 'CONASE4'
    ctlinrem #21;   || remesas lins. 'CONASE4'
    caremesc #22;   || remesas cabs. 'AGICONT'
    caremesl #23;   || remesas lins. 'AGICONT'
    ctcabamo #24;   || amortiz. cabs. 'CONASE4'
    ctlinamo #25;   || amortiz. lins. 'CONASE4'
    camocabe #26;   || amortiz. cabs. 'AGICONT'
    camoline #27;   || amortiz. lins. 'AGICONT'
    ctcunive #29;   || cuentas de nivel 'CONASE4'
    ctcapun1 #31;   || apuntes cabs. 'CONASE4'
    ctcacobr #32;   || cobros cabs. 'CONASE4'
    ctcapago #33;   || pagos cabs. 'CONASE4'
    ctparam  #34;   || parametros generales 'CONASE4'
    calibros #35;   || libros 'AGICONT'

    camogrup #36;   || Grupos de Amortizaciones
|FIN;

|VARIABLES;
    &enDiario   = 0;
    &enContador = 0;
    alfa1 = "";
    alfa2 = "";
    alfa3 = "";
    alfa4 = "";
    nume1 = 0;
    nume2 = 0;
    nume3 = 0;
    nume4 = 0;
    fech1 = @;
    fech2 = @;
    fech3 = @;
    fech4 = @;
    para1 = 0;
    para2 = 0;
    para3 = 0;
    para4 = 0;
    FEstado = 0;
    path_dat = "";
    nome_dat = "";
    des_fec = @;
    has_fec = @;
    cuenta = "";
    digito = 0;
    seleccio = 0;
    mayor = 0;
    cuentas_nivel = 0;
    especial = 0;
    descrip = "";
    registro = 0;
    &sw_conase4 = 1;

    grupoact = 0;
    congrupo = 0;
    linAmort = 0;
    cta68 = "";
    cta28 = "";
    dig68 = 0;
    dig28 = 0;
    sw_grupo = 0;
|FIN;

|INCLUYE acceso_i;

|PROCESO mayus;
  #0Campo = #0Campo > " ";
  |PINTA #0Campo;
|FPRC;

*****************************************************************************
                        CALCULOS DESDE CAMPOS
*****************************************************************************

|PROCESO path_conase4; |TIPO 7;         || desde campo 'path'
  |SI FSalida ! 0;  |ACABA;  |FINSI;
  |DBASS path_dat;
  |QBF path_dat;
  path_dat = path_dat + "conase4/fich/";
  |QBF path_dat;
  #0#0 = path_dat;
  |PINTA #0#0;
|FPRC;

|PROCESO path_bueno; |TIPO 0;           || desde campo 'path'
    path_dat = #0#0;
|QBF path_dat;
|PATH_DAT #1 path_dat;
|FPRC;

|PROCESO lee_conase4; |TIPO 0;          || desde campo 'empresa'
 |HAZ path_bueno;
 |SI FSalida ! 2;
     |PATH_DAT #1 path_dat;
     |ABRE #1;
     #1#0 = #0#1;
     |LEE 001#1=;
     |SI FSalida ! 0;
         |DDEFECTO #1;
         |MENAV "    ATENCION!!! Empresa Inexistente o Path Incorrecto ...";
         |ERROR;
     |SINO;
         |HAZ path_defini;
     |FINSI;
     |ATRI I;
    |PINTA 1016, #1#1;
    |ATRI 0;
    |CIERRA #1;
|FINSI;
|FPRC;

|PROCESO path_defini;                   || desde calculo 'lee_conase4'
|SI #0#21 = 3;
        alfa1 = #0#1;   alfa1 = "000" + alfa1;   alfa1 = alfa1 % -103;
|SINO;
        alfa1 = #0#1;   alfa1 = "00" + alfa1;   alfa1 = alfa1 % -102;
|FINSI;
    path_dat = path_dat + alfa1 + "/";
|FPRC;

|PROCESO comprueba; |TIPO 0;       || desde campo 'nro. digitos'
|SI #0#13 ! 0; |Y FSalida ! 2;
        nume1 = #0#12 + #0#13;
    |SI nume1 > 10;
        |MENAV "    ATENCION!!! La cuenta mide 9 digitos ...";
        |ERROR;
    |SINO;
            nume1 = 9 - #0#13;
        |SI dig3 = 1;   nume2 = dig4;   |FINSI;
        |SI dig3 = 2;   nume2 = dig5;   |FINSI;
        |SI dig3 = 3;   nume2 = dig6;   |FINSI;
        |SI dig3 = 4;   nume2 = dig7;   |FINSI;
        |SI dig3 = 5;   nume2 = dig8;   |FINSI;
        |SI dig3 = 6;   nume2 = dig9;   |FINSI;
        |SI nume1 ! nume2;
            |MENAV "    ATENCION!!! El nivel no corresponde con el ultimo definido ...";
            |ERROR;
        |FINSI;
    |FINSI;
|FINSI;
|FPRC;

|PROCESO nomod_rep; |TIPO 0;       || desde campo 'iva rep.'  (INHIBIDO!!!)
    alfa1 = #0#5;
|CAMPO_MODIFICA #0#14, 1, alfa1;
|FPRC;

|PROCESO nomod_sop; |TIPO 0;       || desde campo 'iva sop.'  (INHIBIDO!!!)
    alfa1 = #0#6;
|CAMPO_MODIFICA #0#15, 1, alfa1;
|FPRC;

|PROCESO nomod_cob; |TIPO 0;       || desde campo 'cobros'
    alfa1 = #0#8;
|CAMPO_MODIFICA #0#16, 1, alfa1;
|CAMPO_MODIFICA #0#22, 1, alfa1;
|FPRC;

|PROCESO nomod_pag; |TIPO 0;       || desde campo 'pagos'
    alfa1 = #0#9;
|CAMPO_MODIFICA #0#17, 1, alfa1;
|CAMPO_MODIFICA #0#23, 1, alfa1;
|FPRC;

|PROCESO nomod_rem; |TIPO 0;       || desde campo 'remesas'
    alfa1 = #0#10;
|CAMPO_MODIFICA #0#18, 1, alfa1;
|FPRC;

|PROCESO nomod_apu; |TIPO 0;       || desde campo 'apuntes'
    alfa1 = #0#3;
|CAMPO_MODIFICA #0#19, 1, alfa1;
|CAMPO_MODIFICA #0#25, 1, alfa1;
|FPRC;

|PROCESO creacion; |TIPO 0;
|SUB_EJECUTA "cacrebal.run";
|FPRC;

|PROCESO lee_libro;
  |ABRE #35;
  #35#0 = #0Campo;
  |LEE 001#35=;
  |SI FSalida ! 0;  |DDEFECTO #35;  |FINSI;
  |CIERRA #35;
|FPRC;

|PROCESO cheq_libros; |TIPO 0;
|HAZ lee_libro;
|SI #35#2 ! "S";
    |MENAV "    ATENCION!!! Libro de I.V.A. Repercutido ...";
    |ERROR;
|FINSI;
|FPRC;

|PROCESO cheq_libror; |TIPO 0;
|HAZ lee_libro;
|SI #35#2 ! "R";
    |MENAV "    ATENCION!!! Libro de I.V.A. Soportado ...";
    |ERROR;
|FINSI;
|FPRC;

|PROCESO captura; |TIPO 3;
|SI #0#20 = "S";  |HAZ inicio;  |HAZ cuenive;  |FINSI;
|SI #0#04 = "S";  |HAZ inicio;  |HAZ cuentas;  |FINSI;
|SI #0#03 = "S";  |HAZ inicio;  |HAZ apuntes;  |FINSI;
|SI #0#05 = "S";  |HAZ inicio;  |HAZ ivarepe;  |FINSI;
|SI #0#06 = "S";  |HAZ inicio;  |HAZ ivasopo;  |FINSI;
|SI #0#07 = "S";  |HAZ inicio;  |HAZ concept;  |FINSI;
|SI #0#08 = "S";  |HAZ inicio;  |HAZ cobros;   |FINSI;
|SI #0#09 = "S";  |HAZ inicio;  |HAZ pagos;    |FINSI;
|SI #0#10 = "S";  |HAZ inicio;  |HAZ remesas;  |FINSI;
|SI #0#11 = "S";  |HAZ inicio;  |HAZ amortiz;  |FINSI;
|FPRC;

|PROCESO noparte1; |TIPO 7;
     alfa1 = #0#26;
     |C_M #0#27, 1, alfa1;
     |C_M #0#28, 1, alfa1;
     |C_M #0#32, 1, alfa1;
     |C_M #0#33, 1, alfa1;
|FPRC;

|PROCESO noparte2; |TIPO 7;
     alfa1 = #0#29;
     |C_M #0#30, 1, alfa1;
     |C_M #0#31, 1, alfa1;
     |C_M #0#34, 1, alfa1;
     |C_M #0#35, 1, alfa1;
|FPRC;

*****************************************************************************
                    CAPTURA DE CUENTAS DE NIVEL
*****************************************************************************

|PROCESO cun_nivel2;
|ATRI I;
|PINTA 1232, #7#0;
|PINTA 1245, #7#1;
|ATRI 0;
|FPRC;

|PROCESO cun_nivel1;
    seleccio = 1;
|DDEFECTO #7;
    cuenta = #29#0;
|HAZ apun_lins2;
|SI digito ! 0;
        #7#00 = cuenta; || cuenta
        #7#01 = digito; || digito
    |LEE 101#7=;
        FEstado = FSalida;
    |DDEFECTO #7;
        #7#00 = cuenta; || cuenta
        #7#01 = digito; || digito
        #7#02 = #29#1;  || descripcion
        #7#03 = "N";    || pase directo no
        alfa1 = #7#00;
    |QBF alfa1;
        alfa1 = alfa1 + "zzzzzzzzzzzz";
        alfa1 = alfa1 % 112;
        #7#54 = alfa1;        || cuenta (clave 3¦)
        #7#55 = #7#01;        || digito (clave 3¦)
    |HAZ cun_nivel2;
    |SI FEstado = 0;  |GRABA 020#7a;  |FINSI;
    |SI FEstado ! 0;  |GRABA 020#7n;  |FINSI;
    |LIBERA #7;
|FINSI;
|FPRC;

|DEFBUCLE cun_nivel;
 #29#1;
 ;
 "       ";
 "zzzzzzz";
 e;
 NULL, cun_nivel1;
|FIN;

|PROCESO cuenive;
|CAMPO_ATRIBUTO #0#20, P, 0, 0;
|PINTA #0#20;
    cuentas_nivel = 1;
|PATH_DAT #29 path_dat;
|ABRET #7;
|BUCLE cun_nivel;
|CIERRAT #7;
    cuentas_nivel = 0;
|CAMPO_ATRIBUTO #0#20, D, 0, 0;
|PINTA #0#20;
|FPRC;

*****************************************************************************
                        CAPTURA DE CUENTAS
*****************************************************************************

|PROCESO saldos_cuentas;
|PARA para1 = 6; |SI para1 [ 50; |HACIENDO para1 = para1 + 1;
        para2 = para1 + 3;
        #7para2 = #6para1;
|SIGUE;
|FPRC;

|PROCESO cuentas4;
|ATRI I;
|PINTA 1332, #7#0;
|PINTA 1345, #7#1;
|ATRI 0;
|FPRC;

|PROCESO cuentas3;
  |DDEFECTO #8;
  #8#23 = "P";
  #8#00 = alfa2;       || codigo proveedor
  #8#01 = #6#1;        || razon social
  #8#02 = #6#3;        || direccion
  #8#05 = #6#5;        || provincia
  #8#06 = #6#4;        || poblacion
  #8#09 = #6#2;        || dni/cif
  #8#10 = #7#0;        || cuenta
  #8#11 = #7#1;        || digito
  |GRABA 040#8n;
|FPRC;

|PROCESO cuentas2;
  |DDEFECTO #8;
  #8#23 = "C";
  #8#00 = alfa2;       || codigo cliente
  #8#01 = #6#1;        || razon social
  #8#02 = #6#3;        || direccion
  #8#05 = #6#5;        || provincia
  #8#06 = #6#4;        || poblacion
  #8#09 = #6#2;        || dni/cif
  #8#10 = #7#0;        || cuenta
  #8#11 = #7#1;        || digito
  |GRABA 040#8n;
|FPRC;

|PROCESO cuentas1;
    seleccio = 1;
    cuenta = #6#0;
|HAZ apun_lins2;
    #7#00 = cuenta; || cuenta
    #7#01 = digito; || digito
|LEE 101#7=;
    FEstado = FSalida;
|DDEFECTO #7;
    #7#00 = cuenta; || cuenta
    #7#01 = digito; || digito
    #7#02 = #6#1;   || descripcion
    #7#03 = "S";    || pase directo si
    alfa1 = #7#00;
|QBF alfa1;
    alfa1 = alfa1 + "zzzzzzzzzzzz";
    alfa1 = alfa1 % 112;
    #7#54 = alfa1;        || cuenta (clave 3¦)
    #7#55 = #7#01;        || digito (clave 3¦)

|SI #0#24 = "S";
    |HAZ saldos_cuentas;
|FINSI;

    alfa1 = #7#0 % 103;
    alfa2 = #7#0 % 406;

|SI alfa1 = "430"; |O alfa1 = "440"; |HAZ cuentas2;   |FINSI;      || cliente
|SI alfa1 = "400"; |O alfa1 = "410";
                   |O alfa1 = "514";
                   |O alfa1 = "515";
                   |O alfa1 = "523";
    |HAZ cuentas3;
|FINSI;      || proveedor
|HAZ cuentas4;
|SI FEstado = 0;  |GRABA 020#7a;  |FINSI;
|SI FEstado ! 0;  |GRABA 020#7n;  |FINSI;
|LIBERA #7;
|FPRC;

|DEFBUCLE cuentas;
 #6#1;
 ;
 "         ";
 "zzzzzzzzz";
 e;
 NULL, cuentas1;
|FIN;

|PROCESO cuentas;
 |DELINDEX #8;
 |CAMPO_ATRIBUTO #0#4, P, 0, 0;
 |PINTA #0#4;
    alfa1 = #0#2;
    nome_dat = "ctcuen" + alfa1;
 |PATH_DAT #6 path_dat;
 |NOME_DAT #6 nome_dat;
 |ABRET #7;
 |ABRET #8;
    seleccio = 0;
 |BUCLE cuentas;
 |CIERRAT #7;
 |CIERRAT #8;
 |SI seleccio ! 0;
     |SUB_EJECUTA "carecepi.run";        || recalcula epigrafes
     |SUB_EJECUTA "catestcu.run";        || testea plan contable
 |FINSI;
 |CAMPO_ATRIBUTO #0#4, D, 0, 0;
 |PINTA #0#4;
|FPRC;

*****************************************************************************
                        CAPTURA DE APUNTES
*****************************************************************************

|PROCESO apun_lins8;
 #14#0 = #5#6;
 |LEE 001#14=;
 |SI FSalida ! 0;  |DDEFECTO #14;  |FINSI;
 alfa1 = #14#1;
 |QBF alfa1;
 descrip = alfa1 + " " + #3#7;       || construye la descripcion

 |SI #0#25 = "S";
     alfa1 = #31#6;   |QBF alfa1;
     descrip = alfa1 + "/" + descrip;     || documento CONASE4
 |FINSI;
|FPRC;

|PROCESO apun_lins7;
 enDiario = especial;
 enContador = mayor;
 |HAZ GrabaContador;
|FPRC;

|PROCESO apun_lins6;
|SI #0#12 ! 0;
        nume1 = #0#12 + 99;
        alfa1 = cuenta % nume1;         || coge la parte izquierda
        nume1 = ((10 - (#0#12 + #0#13)) + 100) * -1;
    |SI nume1 = 0;
            alfa2 = "";
    |SINO;
            alfa2 = cuenta % nume1;         || coge la parte derecha
    |FINSI;
        alfa3 = alfa1 + alfa2;
        cuenta = alfa3;
|FINSI;
|FPRC;

|PROCESO apun_lins5;
    #2#0 = #5#0;
|LEE 001#2=;
|SI FSalida ! 0;
    |DDEFECTO #2;
        #2#0 = #5#0;
        #2#1 = "DIARIO " + #2#0;
    |GRABA 020#2n;
|FINSI;
|FPRC;

|PROCESO apun_lins4;
|ATRI I;
|PINTA 1435, #5#0;  || diario
|PINTA 1438, #5#1;  || fecha
|PINTA 1449, #5#2;  || documento
|ATRI 0;
|SI mayor < #5#2;
        mayor = #5#2;
|FINSI;
|FPRC;

|PROCESO apun_lins3;
|DDEFECTO #4;
    #4#0 = #5#0;    || diario
    #4#1 = #5#1;    || fecha
    #4#2 = #5#2;    || documento
|LEE 101#4=;
    FEstado = FSalida;
    #4#0 = #5#0;    || diario
    #4#1 = #5#1;    || fecha
    #4#2 = #5#2;    || documento
    #4#3 = #4#2;    || asiento
|SI #5#8 = "D";
        #4#4 = #4#4 + #5#9;
    |SI #4#09 = 0; #4#08 = #5#4; #4#09 = #5#5; |FINSI;      || contrp.HABER
|FINSI;
|SI #5#8 = "H";
        #4#5 = #4#5 + #5#9;
    |SI #4#11 = 0; #4#10 = #5#4; #4#11 = #5#5; |FINSI;      || contrp.DEBE
|FINSI;
    #4#6 = #4#4 - #4#5;  || saldo
    #4#7 = 0;            || asiento predefinido
|SI FEstado = 0;  |GRABA 020#4a;  |FINSI;
|SI FEstado ! 0;  |GRABA 020#4n;  |FINSI;
|LIBERA #4;
|HAZ apun_lins4;
|HAZ apun_lins5;
|FPRC;

|PROCESO apun_lins2;
|SI cuentas_nivel = 0;
    |HAZ apun_lins6;
|FINSI;
    alfa1 = cuenta;
|QBF alfa1;
    alfa2 = alfa1 % 0;
    nume1 = alfa2;
    digito = 0;
|SI nume1 ! 0;
    |SI nume1 = dig4;  digito = 1;  |FINSI;
    |SI nume1 = dig5;  digito = 2;  |FINSI;
    |SI nume1 = dig6;  digito = 3;  |FINSI;
    |SI nume1 = dig7;  digito = 4;  |FINSI;
    |SI nume1 = dig8;  digito = 5;  |FINSI;
    |SI nume1 = dig9;  digito = 6;  |FINSI;
|FINSI;
|FPRC;

|PROCESO apun_lins1;
|HAZ apun_lins0;
    seleccio = 1;
|DDEFECTO #5;
    #5#00 = especial;    || diario
    #5#01 = #3#1;        || fecha
    #5#02 = #3#2;        || documento
    #5#03 = #3#3;        || linea
    cuenta = #3#4;
|HAZ apun_lins2;
    #5#04 = cuenta;      || cuenta
    #5#05 = digito;      || digito
    #5#06 = #3#6;        || concepto
|HAZ apun_lins8;
    #5#07 = descrip;     || descripcion
    #5#08 = #3#5;        || signo
    #5#09 = #3#8;        || importe
|SI #5#08 = "D";  #5#16 = #5#4;  #5#17 = #5#5;  |FINSI;  || cuenta DEBE
|SI #5#08 = "H";  #5#10 = #5#4;  #5#11 = #5#5;  |FINSI;  || cuenta HABER
    #5#12 = #3#9;        || soportado/repercutido
    #5#13 = 0;           || libro
    #5#14 = 0;           || factura/orden
    #5#15 = "  ";        || serie
    #5#18 = " ";         || punteado
    #5#19 = " ";         || tipo acumulador IVA
    #5#20 = 0;           || nro. acumulador IVA
    #5#21 = "  ";        || departamento
|GRABA 020#5n;
|SI FSalida = 0;
    |HAZ apun_lins3;
|FINSI;
|FPRC;

|PROCESO apun_lins0;
|DDEFECTO #31;
    #31#0 = #3#0;        || diario
    #31#1 = #3#1;        || fecha
    #31#2 = #3#2;        || documento
|LEE 000#31=;                 || lee la cabecera de 'CONASE4'
|SI FSalida = 0;
        especial = #3#0;      || diario original
|SINO;
        especial = #0#19;     || diario de apuntes sin cabeceras
|FINSI;
|FPRC;

|DEFBUCLE apun_lins;
 #3#1;
 ;
 "  ", des_fec,      1,    1;
 "zz", has_fec, 999999, 9999;
 e;
 NULL, apun_lins1;
|FIN;

|PROCESO apuntes;
|DELINDEX #4;
|DELINDEX #5;
|CAMPO_ATRIBUTO #0#3, P, 0, 0;
|PINTA #0#3;
    alfa1 = #0#2;
    nome_dat = "ctlipun" + alfa1;
|PATH_DAT #3 path_dat;
|NOME_DAT #3 nome_dat;
    alfa1 = #0#2;
    nome_dat = "ctcapun" + alfa1;
|PATH_DAT #31 path_dat;
|NOME_DAT #31 nome_dat;
|PATH_DAT #14 path_dat;
|ABRET #2;
|ABRET #4;
|ABRET #5;
|ABRET #14;
|ABRET #31;
    des_fec = "01.01.1900";
    has_fec = "31.12.2999";
    seleccio = 0;
|BUCLE apun_lins;
|CIERRAT #2;
|CIERRAT #4;
|CIERRAT #5;
|CIERRAT #14;
|CIERRAT #31;
|HAZ apun_lins7;
|SI seleccio ! 0;|Y #0#24 = "N";
    |SUB_EJECUTA "carecupe.run";        || recalcula saldos
|FINSI;
|CAMPO_ATRIBUTO #0#3, D, 0, 0;
|PINTA #0#3;
|FPRC;

*****************************************************************************
                        CAPTURA DE IVA REPERCUTIDO
*****************************************************************************

|PROCESO ivareper5;
|ATRI I;
|PINTA 1535, #11#2;  || factura
|PINTA 1542, #11#3;  || fecha
|ATRI 0;
|FPRC;

|PROCESO ivareper41;
    #5#12 = "R";         || soportado/repercutido
    #5#13 = #11#0;       || libro
    #5#14 = #11#2;       || factura/orden
|SI #5#09 = #11#08;  #5#19 = "B";  #5#20 = 1;  |FINSI;
|SI #5#09 = #11#09;  #5#19 = "B";  #5#20 = 2;  |FINSI;
|SI #5#09 = #11#10;  #5#19 = "B";  #5#20 = 3;  |FINSI;
|SI #5#09 = #11#14;  #5#19 = "I";  #5#20 = 1;  |FINSI;
|SI #5#09 = #11#15;  #5#19 = "I";  #5#20 = 2;  |FINSI;
|SI #5#09 = #11#16;  #5#19 = "I";  #5#20 = 3;  |FINSI;
|SI #5#09 = #11#20;  #5#19 = "R";  #5#20 = 1;  |FINSI;
|SI #5#09 = #11#21;  #5#19 = "R";  #5#20 = 2;  |FINSI;
|SI #5#09 = #11#22;  #5#19 = "R";  #5#20 = 3;  |FINSI;
    alfa1 = #5#4 % 103;
|SI alfa1 = "477"; |Y #5#20 = 0;  #5#19 = "I";  #5#20 = 4;  |FINSI;
|GRABA 020#5a;
|LIBERA #5;
|FPRC;

|DEFBUCLE ivareper40;
 #5#1;
 ;
 #11#45, #11#46, #11#47,    1;
 #11#45, #11#46, #11#47, 9999;
 be;
 NULL, ivareper41;
|FIN;

|PROCESO ivareper4;
|BUCLE ivareper40;
|FPRC;

|PROCESO ivareper31;
|ERROR10;
|FPRC;

|DEFBUCLE ivareper30;
 #3#1;
 ;
 "  ", #10#1, #10#27,    1;
 "zz", #10#1, #10#27, 9999;
 e;
 NULL, ivareper31;
|FIN;

|PROCESO ivareper3;
    alfa1 = #0#2;
    nome_dat = "ctlipun" + alfa1;
|PATH_DAT #3 path_dat;
|NOME_DAT #3 nome_dat;
    #3#0 = "01";
|BUCLE ivareper30;
|FPRC;

|PROCESO ivareper2;
    alfa1 = #11#3;   alfa1 = alfa1 % -104;     || saca a¤o
    alfa2 = "31.03." + alfa1;  fech1 = alfa2;  || primer trimestre
    alfa2 = "30.06." + alfa1;  fech2 = alfa2;  || segundo trimestre
    alfa2 = "30.09." + alfa1;  fech3 = alfa2;  || tercer trimestre
|SI #11#3 [ fech1;                   nume1 = 1;  |FINSI;
|SI #11#3 > fech1;|Y #11#3 [ fech2;  nume1 = 2;  |FINSI;
|SI #11#3 > fech2;|Y #11#3 [ fech3;  nume1 = 3;  |FINSI;
|SI #11#3 > fech3;                   nume1 = 4;  |FINSI;
|FPRC;

|PROCESO lee_cliente;
|DDEFECTO #8;
    #8#23 = "C";
    #8#10 = cuenta;
    #8#11 = digito;
|LEE 000#8=;
|FPRC;

|PROCESO ivareper1;
|DDEFECTO #11;
    #11#00 = #0#14;      || libro
|SI #0#26 = "N";
        #11#02 = #10#29; || factura
        #11#27 = "";     || serie
|SINO;
     |SI #0#27 ! 0;
          nume1 = ((#0#27 * 100) + #0#32);
          alfa1 = #10#0 % nume1;
          #11#27 = alfa1;
     |FINSI;
     |SI #0#28 ! 0;
          nume1 = ((#0#28 * 100) + #0#33);
          alfa1 = #10#0 % nume1;
          #11#2 = alfa1;
     |FINSI;
|FINSI;
|LEE 000#11=;
|SI FSalida = 0;
     alfa1 = "    Factura [" + #11#0 + "." + #11#27 + "." + #11#2 + "] duplicada ...";
     |MENAV alfa1;
     |ACABA;
|FINSI;

    #11#03 = #10#02;     || fecha
    cuenta = #10#06;
|HAZ apun_lins2;
    #11#04 = cuenta;     || cuenta
    #11#05 = digito;     || digito
|HAZ lee_cliente;
    #11#06 = 1;          || concepto
    #11#07 = "Fra. " + #10#0;      || descripcion
    #11#08 = #10#12;               || base 1
    #11#09 = #10#13;               || base 2
    #11#10 = #10#14;               || base 3
    #11#52 = #10#15;               || base 4
    #11#53 = #10#16;               || base 5
    #11#14 = #10#17;               || cuota iva 1
    #11#15 = #10#18;               || cuota iva 2
    #11#16 = #10#19;               || cuota iva 3
    #11#56 = #10#20;               || cuota iva 4
    #11#57 = 0;                    || cuota iva 5 (exento)
    #11#20 = #10#21;               || cuota rec. 1
    #11#21 = #10#22;               || cuota rec. 2
    #11#22 = #10#23;               || cuota rec. 3
    #11#60 = #10#24;               || cuota rec. 4
    #11#61 = 0;                    || cuota rec. 5 (exento)
|SI #11#14 ! 0;  #11#11 = #34#9;  |FINSI;         || (%) iva 1
|SI #11#15 ! 0;  #11#12 = #34#10; |FINSI;         || (%) iva 2
|SI #11#16 ! 0;  #11#13 = #34#11; |FINSI;         || (%) iva 3
|SI #11#56 ! 0;  #11#54 = #34#12; |FINSI;         || (%) iva 4
    #11#55 = 0;                         || (%) iva 5 (exento)
|SI #11#20 ! 0;  #11#17 = #34#13; |FINSI;         || (%) rec. 1
|SI #11#21 ! 0;  #11#18 = #34#14; |FINSI;         || (%) rec. 2
|SI #11#22 ! 0;  #11#19 = #34#15; |FINSI;         || (%) rec. 3
|SI #11#60 ! 0;  #11#58 = #34#16; |FINSI;         || (%) rec. 4
    #11#59 = 0;                         || (%) rec. 5 (exento)
|SI #10#04 = "S";   #11#23 = "N";   |FINSI;    || no deducible/si export.
|SI #10#04 = "N";   #11#23 = "S";   |FINSI;    || si deducible/no export.
    #11#26 = #10#26;     || total factura
    #11#28 = "N";        || liquidada
|HAZ ivareper2;
    #11#44 = nume1;      || periodo
|HAZ ivareper3;
    #11#45 = #3#0;       || diario
    #11#46 = #10#1;      || fecha
    #11#47 = #10#27;     || documento
|SI #10#03 = "A";   #11#48 = "S";    |FINSI;   || si inversiones/si autoc.
|SI #10#03 ! "A";   #11#48 = "N";    |FINSI;   || no inversiones/no autoc.
    #11#49 = "";         || departamento
    #11#50 = #8#1;       || nombre
    #11#51 = #8#9;       || cif
|GRABA 020#11n;
|HAZ ivareper4;
|HAZ ivareper5;
|FPRC;

|DEFBUCLE ivareper;
 #10#1;
 1;
      1, fec1;
 999999, fec2;
 e;
 NULL, ivareper1;
|FIN;

|PROCESO ivarepe;
|DELINDEX #11;
|CAMPO_ATRIBUTO #0#5, P, 0, 0;
|PINTA #0#5;
|PATH_DAT #10 path_dat;
|PATH_DAT #34 path_dat;
|ABRE #34;
    #34#0 = "param";
|LEE 001#34=;
|SI FSalida = 0;
    |ABRET #8;
    |ABRET #11;
    |BUCLE ivareper;
    |CIERRAT #8;
    |CIERRAT #11;
|SINO;
    |MENAV "    Fichero de Parametros Generales sin crear en 'CONASE4' ...";
|FINSI;
|CIERRA #34;
|CAMPO_ATRIBUTO #0#5, D, 0, 0;
|PINTA #0#5;
|FPRC;

*****************************************************************************
                      CAPTURA DE IVA SOPORTADO
*****************************************************************************

|PROCESO ivasopor5;
|ATRI I;
|PINTA 1635, #13#2;  || factura
|PINTA 1642, #13#3;  || fecha
|ATRI 0;
|FPRC;

|PROCESO ivasopor41;
    #5#12 = "S";         || soportado/repercutido
    #5#13 = #13#0;       || libro
    #5#14 = #13#2;       || factura/orden
|SI #5#09 = #13#08;  #5#19 = "B";  #5#20 = 1;  |FINSI;
|SI #5#09 = #13#09;  #5#19 = "B";  #5#20 = 2;  |FINSI;
|SI #5#09 = #13#10;  #5#19 = "B";  #5#20 = 3;  |FINSI;
|SI #5#09 = #13#14;  #5#19 = "I";  #5#20 = 1;  |FINSI;
|SI #5#09 = #13#15;  #5#19 = "I";  #5#20 = 2;  |FINSI;
|SI #5#09 = #13#16;  #5#19 = "I";  #5#20 = 3;  |FINSI;
    alfa1 = #5#4 % 103;
|SI alfa1 = "477"; |Y #5#20 = 0;  #5#19 = "I";  #5#20 = 4;  |FINSI;
|GRABA 020#5a;
|LIBERA #5;
|FPRC;

|DEFBUCLE ivasopor40;
 #5#1;
 ;
 #13#45, #13#46, #13#47,    1;
 #13#45, #13#46, #13#47, 9999;
 be;
 NULL, ivasopor41;
|FIN;

|PROCESO ivasopor4;
|BUCLE ivasopor40;
|FPRC;

|PROCESO ivasopor31;
|ERROR10;
|FPRC;

|DEFBUCLE ivasopor30;
 #3#1;
 ;
 "  ", #12#2, #12#24,    1;
 "zz", #12#2, #12#24, 9999;
 e;
 NULL, ivasopor31;
|FIN;

|PROCESO ivasopor3;
    alfa1 = #0#2;
    nome_dat = "ctlipun" + alfa1;
|PATH_DAT #3 path_dat;
|NOME_DAT #3 nome_dat;
    #3#0 = "01";
|BUCLE ivasopor30;
|FPRC;

|PROCESO ivasopor2;
    alfa1 = #13#3;   alfa1 = alfa1 % -104;     || saca a¤o
    alfa2 = "31.03." + alfa1;  fech1 = alfa2;  || primer trimestre
    alfa2 = "30.06." + alfa1;  fech2 = alfa2;  || segundo trimestre
    alfa2 = "30.09." + alfa1;  fech3 = alfa2;  || tercer trimestre
|SI #13#3 [ fech1;                   nume1 = 1;  |FINSI;
|SI #13#3 > fech1;|Y #13#3 [ fech2;  nume1 = 2;  |FINSI;
|SI #13#3 > fech2;|Y #13#3 [ fech3;  nume1 = 3;  |FINSI;
|SI #13#3 > fech3;                   nume1 = 4;  |FINSI;
|FPRC;

|PROCESO lee_provee;
|DDEFECTO #8;
    #8#23 = "P";
    #8#10 = cuenta;
    #8#11 = digito;
|LEE 000#8=;
|FPRC;

|PROCESO ivasopor1;
|DDEFECTO #13;
    #13#00 = #0#15;      || libro
|SI #0#29 = "N";
        #13#02 = #12#0;  || registro
        #13#27 = "";     || serie
|SINO;
     |SI #0#30 ! 0;
          nume1 = ((#0#30 * 100) + #0#34);
          alfa1 = #12#1 % nume1;
          #13#27 = alfa1;
     |FINSI;
     |SI #0#31 ! 0;
          nume1 = ((#0#31 * 100) + #0#35);
          alfa1 = #12#1 % nume1;
          #13#2 = alfa1;
     |FINSI;
|FINSI;
|LEE 000#13=;
|SI FSalida = 0;
     alfa1 = "    Factura [" + #13#0 + "." + #13#27 + "." + #13#2 + "] duplicada ...";
     |MENAV alfa1;
     |ACABA;
|FINSI;

    #13#03 = #12#03;     || fecha
    cuenta = #12#07;
|HAZ apun_lins2;
    #13#04 = cuenta;     || cuenta
    #13#05 = digito;     || digito
|HAZ lee_provee;
    #13#06 = 1;          || concepto
    #13#07 = "Fra. " + #12#1;      || descripcion
    #13#08 = #12#13;               || base 1
    #13#09 = #12#14;               || base 2
    #13#10 = #12#15;               || base 3
    #13#52 = #12#16;               || base 4
    #13#53 = #12#17;               || base 5
    #13#14 = #12#18;               || cuota iva 1
    #13#15 = #12#19;               || cuota iva 2
    #13#16 = #12#20;               || cuota iva 3
    #13#56 = #12#21;               || cuota iva 4
    #13#57 = 0;                    || cuota iva 5
    #13#20 = 0;                    || cuota rec. 1
    #13#21 = 0;                    || cuota rec. 2
    #13#22 = 0;                    || cuota rec. 3
    #13#60 = 0;                    || cuota rec. 4
    #13#61 = 0;                    || cuota rec. 5
|SI #13#14 ! 0;  #13#11 = #34#9;  |FINSI;         || (%) iva 1
|SI #13#15 ! 0;  #13#12 = #34#10; |FINSI;         || (%) iva 2
|SI #13#16 ! 0;  #13#13 = #34#11; |FINSI;         || (%) iva 3
|SI #13#56 ! 0;  #13#54 = #34#12; |FINSI;         || (%) iva 4
    #13#55 = 0;                         || (%) iva 5
    #13#17 = 0;                         || (%) rec. 1
    #13#18 = 0;                         || (%) rec. 2
    #13#19 = 0;                         || (%) rec. 3
    #13#58 = 0;                         || (%) rec. 4
    #13#59 = 0;                         || (%) rec. 5
    #13#23 = #12#5;      || deducible
    #13#26 = #12#23;     || total factura
    #13#28 = "N";        || liquidada
|HAZ ivasopor2;
    #13#44 = nume1;      || periodo
|HAZ ivasopor3;
    #13#45 = #3#0;       || diario
    #13#46 = #12#2;      || fecha
    #13#47 = #12#24;     || documento
|SI #12#04 = "A";   #13#48 = "S";    |FINSI;   || si inversiones/si autoc.
|SI #12#04 ! "A";   #13#48 = "N";    |FINSI;   || no inversiones/no autoc.
    #13#49 = "";         || departamento
    #13#50 = #8#1;       || nombre
    #13#51 = #8#9;       || cif
|GRABA 020#13n;
|HAZ ivasopor4;
|HAZ ivasopor5;
|FPRC;

|DEFBUCLE ivasopor;
 #12#1;
 ;
 fec1,      1;
 fec2, 999999;
 e;
 NULL, ivasopor1;
|FIN;

|PROCESO ivasopo;
|DELINDEX #13;
|CAMPO_ATRIBUTO #0#6, P, 0, 0;
|PINTA #0#6;
|PATH_DAT #12 path_dat;
|PATH_DAT #34 path_dat;
|ABRE #34;
    #34#0 = "param";
|LEE 001#34=;
|SI FSalida = 0;
    |ABRET #8;
    |ABRET #13;
    |BUCLE ivasopor;
    |CIERRAT #8;
    |CIERRAT #13;
|SINO;
    |MENAV "    Fichero de Parametros Generales sin crear en 'CONASE4' ...";
|FINSI;
|CIERRA #34;
|CAMPO_ATRIBUTO #0#6, D, 0, 0;
|PINTA #0#6;
|FPRC;

*****************************************************************************
                      CAPTURA DE CONCEPTOS
*****************************************************************************

|PROCESO concept2;
|ATRI I;
|PINTA 1732, #15#0;
|ATRI 0;
|FPRC;

|PROCESO concept1;
    #15#0 = #14#0;       || concepto
|LEE 101#15=;
    FEstado = FSalida;
|DDEFECTO #15;
    #15#0 = #14#0;       || concepto
    #15#1 = #14#1;       || descripcion
|SI FEstado = 0;  |GRABA 020#15a;  |FINSI;
|SI FEstado ! 0;  |GRABA 020#15n;  |FINSI;
|LIBERA #15;
|HAZ concept2;
|FPRC;

|DEFBUCLE concepto;
 #14#1;
 ;
  1;
 99;
 e;
 NULL, concept1;
|FIN;

|PROCESO concept;
|CAMPO_ATRIBUTO #0#7, P, 0, 0;
|PINTA #0#7;
|PATH_DAT #14 path_dat;
|ABRET #15;
|BUCLE concepto;
|CIERRAT #15;
|CAMPO_ATRIBUTO #0#7, D, 0, 0;
|PINTA #0#7;
|FPRC;

*****************************************************************************
                      CAPTURA DE COBROS
*****************************************************************************

|PROCESO cobros4;
    #32#0 = #16#0;
|LEE 001#32=;
|SI FSalida ! 0;
    |DDEFECTO #32;
|FINSI;
|FPRC;

|PROCESO cobros3;
    #10#0 = #16#2;       || factura
    #10#1 = #16#3;       || fecha factura
|LEE 001#10=;
|SI FSalida ! 0;|O #0#22 = "N";
        registro = #16#0;
|SINO;
        registro = #10#29;
|FINSI;
|FPRC;

|PROCESO cobros2;
|ATRI I;
|PINTA 1835, #17#0;
|PINTA 1838, #17#1;
|PINTA 1845, #17#3;
|ATRI 0;
|FPRC;

|PROCESO cobros1;
|HAZ cobros3;
|HAZ cobros4;
|DDEFECTO #17;
    #17#00 = #0#14;       || libro
    #17#01 = registro;    || factura
    #17#02 = "";          || serie
    #17#03 = #16#1;       || nro. vencimiento
    cuenta = #32#5;
|HAZ apun_lins2;
    #17#04 = cuenta;      || cuenta (de la cabecera)
    #17#05 = digito;      || digito
    #17#06 = #16#3;       || fecha emision
    #17#07 = #16#4;       || fecha vencimiento
    #17#08 = #16#5;       || importe
    #17#09 = 0;           || gastos negociacion
    #17#10 = 0;           || gastos impagados
    #17#11 = #0#16;       || tipo recibo
    #17#12 = #16#16;      || nro. remesa
    #17#13 = "N";         || emitido
    #17#14 = #17#7;       || fecha vencimiento original
    #17#26 = #17#8;       || importe original
|GRABA 020#17n;
|HAZ cobros2;
|FPRC;

|DEFBUCLE cobro;
 #16#1;
 3;
      1,  1, fec1;
 999999, 99, fec2;
 e;
 NULL, cobros1;
|FIN;

|PROCESO cobros;
|DELINDEX #17;
|CAMPO_ATRIBUTO #0#8, P, 0, 0;
|PINTA #0#8;
|PATH_DAT #16 path_dat;
|PATH_DAT #32 path_dat;
|PATH_DAT #10 path_dat;
|ABRET #10;
|SELECT #2#10;
|ABRET #17;
|ABRET #32;
|BUCLE cobro;
|CIERRAT #10;
|CIERRAT #17;
|CIERRAT #32;
|CAMPO_ATRIBUTO #0#8, D, 0, 0;
|PINTA #0#8;
|FPRC;

*****************************************************************************
                      CAPTURA DE PAGOS
*****************************************************************************

|PROCESO pagos6;
    #33#12 = #18#13;
|LEE 001#33=;
|SI FSalida ! 0;
    |DDEFECTO #33;
|FINSI;
|FPRC;

|PROCESO pagos5;
    registro = #12#0;
|ERROR10;
|FPRC;

|DEFBUCLE pagos4;
 #12#1;
 1;
 #18#1,         0, #18#0;
 #18#1, 999999999, #18#0;
 e;
 NULL, pagos5;
|FIN;

|PROCESO pagos3;
    registro = #18#13;
|SI #0#23 = "S";
    |PATH_DAT #12 path_dat;
    |BUCLE pagos4;
|FINSI;
|FPRC;

|PROCESO pagos2;
|ATRI I;
|PINTA 1935, #19#0;
|PINTA 1938, #19#1;
|PINTA 1945, #19#3;
|ATRI 0;
|FPRC;

|PROCESO pagos1;
|HAZ pagos3;
|HAZ pagos6;
|DDEFECTO #19;
    #19#00 = #0#15;       || libro
    #19#01 = registro;    || orden
    #19#03 = #18#2;       || nro. vencimiento
    cuenta = #33#4;
|HAZ apun_lins2;
    #19#04 = cuenta;      || cuenta (de la cabecera)
    #19#05 = digito;      || digito
    #19#06 = #18#1;       || fecha emision
    #19#07 = #18#3;       || fecha vencimiento
    #19#08 = #18#4;       || importe
    #19#09 = 0;           || gastos negociacion
    #19#11 = #0#17;       || tipo recibo
    #19#12 = 0;           || nro. pagare
    #19#13 = "N";         || emitido
|GRABA 020#19n;
|HAZ pagos2;
|FPRC;

|DEFBUCLE pago;
 #18#1;
 1;
      1,  1, fec1;
 999999, 99, fec2;
 e;
 NULL, pagos1;
|FIN;

|PROCESO pagos;
|DELINDEX #19;
|CAMPO_ATRIBUTO #0#9, P, 0, 0;
|PINTA #0#9;
|PATH_DAT #18 path_dat;
|PATH_DAT #33 path_dat;
|ABRET #19;
|ABRET #33;
|BUCLE pago;
|CIERRAT #19;
|CIERRAT #33;
|CAMPO_ATRIBUTO #0#9, D, 0, 0;
|PINTA #0#9;
|FPRC;

*****************************************************************************
                      CAPTURA DE REMESAS
*****************************************************************************

|PROCESO remesas3;
|ATRI I;
|PINTA 2035, #23#0;
|PINTA 2041, #23#1;
|ATRI 0;
|FPRC;

|PROCESO remesas2;
|DDEFECTO #23;
    #23#00 = #21#0;       || nro. remesa
    #23#01 = #21#1;       || linea
    #23#02 = #0#14;       || libro
    #23#03 = #21#2;       || nro. factura
    #23#04 = "";          || serie
    #23#05 = #21#4;       || nro. cobro
    #23#06 = #21#7;       || fecha vto.
    #23#07 = #21#8;       || importe
    #23#08 = #21#13;      || dias
    #23#09 = #21#9;       || (%) interes
    #23#10 = #21#10;      || interes
    cuenta = #21#12;
|HAZ apun_lins2;
    #23#11 = cuenta;      || cuenta
    #23#12 = digito;      || digito
    #23#13 = #21#5;       || nombre
    #23#14 = #21#6;       || poblacion
|GRABA 020#23n;
|HAZ remesas3;
|FPRC;

|PROCESO remesas1;
|DDEFECTO #22;
    #22#00 = #20#0;       || nro. remesa
    #22#01 = #20#1;       || fecha remesa
    #22#02 = #20#3;       || codigo banco
    #22#03 = #20#4;       || nombre banco
    #22#04 = #20#15;      || cta. cte.
    #22#05 = #20#6;       || numero de efectos
    #22#06 = #20#7;       || comision por efecto
    #22#07 = #0#18;       || tipo de recibo
    #22#08 = #20#12;      || emitida
    #22#09 = #20#13;      || contabilizada
    #22#10 = #20#8;       || total remesa
    #22#11 = #20#9;       || total comision
    #22#12 = #20#10;      || total intereses
    #22#13 = #20#11;      || total gastos
    cuenta = #20#14;
|HAZ apun_lins2;
    #22#14 = cuenta;      || cuenta
    #22#15 = digito;      || digito
|GRABA 020#22n;
|BUCLELIN 2remesas2#20;
|FPRC;

|DEFBUCLE remesa;
 #20#1;
 1;
     1, fec1;
 99999, fec2;
 e;
 NULL, remesas1;
|FIN;

|PROCESO remesas;
|DELINDEX #22;
|DELINDEX #23;
|CAMPO_ATRIBUTO #0#10, P, 0, 0;
|PINTA #0#10;
|PATH_DAT #20 path_dat;
|PATH_DAT #21 path_dat;
|ABRET #22;
|ABRET #23;
|BUCLE remesa;
|CIERRAT #22;
|CIERRAT #23;
|CAMPO_ATRIBUTO #0#10, D, 0, 0;
|PINTA #0#10;
|FPRC;

*****************************************************************************
                      CAPTURA DE AMORTIZACIONES
*****************************************************************************
|PROCESO sigrupo;
 sw_grupo = 1;
 |ERROR10;
|FPRC;

|DEFBUCLE vergrup;
 #36#1;
 1,2,3,4;
 0000, cta68, dig68, cta28, dig28;
 9999, cta68, dig68, cta28, dig28;
 e;
 NULL, sigrupo;
|FIN;

|PROCESO vergrupo;
 grupoact = 0;
 cuenta = #24#2;
 |HAZ apun_lins2;
 cta28 = cuenta;     || cuenta
 dig28 = digito;     || digito
 cuenta = #24#3;
 |HAZ apun_lins2;
 cta68 = cuenta;     || cuenta
 dig68 = digito;     || digito
 sw_grupo = 0;
 |BUCLE vergrup;
 |SI sw_grupo = 0;  || No ha encontrado grupo
     congrupo = congrupo + 1;
     |DDEFECTO #36;
     #36#0 = congrupo;
     #36#1 = cta68;
     #36#2 = dig68;
     #36#3 = cta28;
     #36#4 = dig28;
     |GRABA 020#36n;
 |FINSI;
 grupoact = #36#0;
|FPRC;

|PROCESO amortiz3;
|ATRI I;
|PINTA 2132, #27#0;
|PINTA 2141, #27#2;
|ATRI 0;
|FPRC;

|PROCESO amortiz2;
 linAmort = linAmort + 1;
 |DDEFECTO #27;
 #27#00 = #25#0;      || elemento
 #27#01 = 0;          || numero
 #27#02 = linAmort;   || linea
 #27#03 = #25#2;      || a¤o
 #27#04 = #25#3;      || tipo
 #27#05 = #25#4;      || importe fiscal
 #27#06 = #25#5;      || contabilizado
 alfa1 = "31.12." + #27#3;
 #27#07 = alfa1;      || fecha
 #27#09 = #25#4;      || importe contable
 |GRABA 020#27n;
 |HAZ amortiz3;
|FPRC;

|PROCESO amortiz1;
 |HAZ vergrupo;

 |DDEFECTO #26;
    #26#00 = #24#0;      || codigo elemento
    #26#01 = 0;          || numero
    #26#02 = #24#1;      || descripcion
    cuenta = #24#4;
|HAZ apun_lins2;
    #26#03 = cuenta;     || cuenta
    #26#04 = digito;     || digito
    #26#05 = #24#5;      || proveedor
    #26#06 = #24#6;      || fecha compra
    #26#07 = #24#7;      || importe compra
    #26#08 = #24#8;      || nueva/usada
    #26#09 = #24#9;      || lugar destino
    #26#10 = #24#10;     || tipo amortizacion fiscal
    #26#11 = #24#11;     || (%) amortizacion fiscal
    #26#12 = #24#12;     || acum. amortizacion fiscal
    #26#13 = #24#14;     || valor residual fiscal
    #26#14 = 0;          || a¤os amortiz. fiscal
    #26#15 = grupoact;   || grupo
    #26#16 = #24#10;     || tipo amortizacion contable
    #26#17 = #24#11;     || (%) amortizacion contable
    #26#18 = #24#12;     || acum. amortizacion contable
    #26#19 = #24#14;     || valor residual contable
    #26#20 = 0;          || a¤os amortiz. contable
    #26#23 = "00.00.0000";    || fecha venta
 |GRABA 020#26n;
 |HAZ linCompra;
 |BUCLELIN 2amortiz2#24;
|FPRC;

|| ---------------------- Faltaba poner Linea Compra
|PROCESO linCompra;
  linAmort = 1;
  alfa1 = #26#6;       || Fecha Compra Cabecera
  alfa2 = alfa1 % -104; || A¤o Compra
  |DDEFECTO #27;
  #27#00 = #26#0;      || elemento
  #27#01 = #26#1;      || numero
  #27#02 = linAmort;   || linea
  #27#03 = alfa2;      || a¤o
  #27#04 = "C";        || tipo COMPRA
  #27#05 = #26#7;      || importe COMPRA
  #27#06 = "S";        || contabilizado
  #27#07 = #26#6;      || fecha de COMPRA
  #27#09 = #26#7;      || importe COMPRA
  |GRABA 020#27n;
|FPRC;

|DEFBUCLE amortiza;
 #24#1;
 ;
 "        ";
 "zzzzzzzz";
 e;
 NULL, amortiz1;
|FIN;

|PROCESO amortiz;
|DELINDEX #26;
|DELINDEX #27;
|DELINDEX #36;
|CAMPO_ATRIBUTO #0#11, P, 0, 0;
|PINTA #0#11;
|PATH_DAT #24 path_dat;
|PATH_DAT #25 path_dat;
|ABRET #26;
|ABRET #27;
|ABRET #36;
congrupo = 0;
|BUCLE amortiza;
|CIERRAT #26;
|CIERRAT #27;
|CIERRAT #36;
|CAMPO_ATRIBUTO #0#11, D, 0, 0;
|PINTA #0#11;
|FPRC;
