|FICHEROS;
  cavenaut #0;
  camodven #1;
|FIN;

|VARIABLES;
   nInkey = 0;
   aAlfa1 = "";
   aAlfa2 = "";
   aAlfa3 = "";
   aAlfa4 = "";
   aAlfa5 = "";
   fFech1 = @;
   fFech2 = @;
   nNume1 = 0;
   nNume2 = 0;
   nNume3 = 0;

   cab = "Modificar Ventas Diarias";

   error1 = "     NO EXISTE VENTAS AUTOMATICAS";
   errorfec1 = "     Fecha erronea DD/MM";
   errorfec3 = "     No existe ese dia el a¤o no es bisiesto";
   modificar = "2325S Es correcta la modificacion S/N : ";
   continuar = "2325S Continuar S/N : ";
   x = 0;
   y = 0;
   vacio = "              ";
   vac1 = "                                                                  ";
   num = 0;
   total = 0;
   varalfa = "";
   varalfa1 = "";
   varalfa2  = "";
   vardia = 0;
   varmes = 0;
   fechadesde = "";
   fechadesde1 = "";
   fechadesde2 = "";
   fechahasta = "";
   fechahasta1 = "";
   fechahasta2 = "";

   dia2     = 31;
   dia3     = 60;
   dia4     = 91;
   dia5     = 121;
   dia6     = 152;
   dia7     = 182;
   dia8     = 213;
   dia9     = 244;
   dia10    = 274;
   dia11    = 305;
   dia12    = 335;
   diapri   = "";
   diapri1  = 0;
   bisiesto = "";
   swbis    = 0;
   anyo     = "";
   mes      = "";
   mesnum   = 0;
   dia      = "";
   dianum   = 0;

   sw_exist = 0;

   cantidad = 0;
   nada = 0;

   {366}cant = 0;

   {-1}valfec = 0;
   valfec1  = 31;
   valfec2  = 29;
   valfec3  = 31;
   valfec4  = 30;
   valfec5  = 31;
   valfec6  = 30;
   valfec7  = 31;
   valfec8  = 31;
   valfec9  = 30;
   valfec10 = 31;
   valfec11 = 30;
   valfec12 = 31;
|FIN;

|INCLUYE acceso_i;

|| -------------------------- Proceso de Pantalla

|CALCULO No12; |TIPO 12;
|FCAL;

|CALCULO  fechbien;|TIPO 0;        ||Calcula si la fecha es correcta
  nInkey = FSalida;
  |SI nInkey = 1;
      |PTEC 807;
  |FINSI;

   valfec1  = 31; valfec2  = 29; valfec3  = 31;
   valfec4  = 30; valfec5  = 31; valfec6  = 30;
   valfec7  = 31; valfec8  = 31; valfec9  = 30;
   valfec10 = 31; valfec11 = 30; valfec12 = 31;

   varalfa = #1Campo;
   |SI varalfa = "    ";
       |ERROR;
       |ACABA;
   |FINSI;

   |SI varalfa = "2902"; |Y swbis = 1;
       |MENAV errorfec3;
       |ERROR;
       |ACABA;
   |FINSI;

   varalfa1 = varalfa / 2;
   varalfa2 = varalfa % A302;
   vardia = varalfa1;
   varmes = varalfa2;
   |SI varmes [ 0;|O varmes > 12;
       |MENAV errorfec1;
       |ERROR;
       |ACABA;
   |FINSI;

   Ivalfec = valfec1 <-;
   Ivalfec = Ivalfec + varmes;
   Ivalfec = Ivalfec - 1;
   |SI vardia > valfec; |O vardia [ 0;
       |MENAV errorfec1;
       |ERROR;
   |FINSI;
|FCAL;

|CALCULO dia; |TIPO 0;

   mes = #1#1 % A302;
   dia = #1#1 % A102;

   |SI mes = 01;  mesnum = dia1;  |FINSI;
   |SI mes = 02;  mesnum = dia2;  |FINSI;
   |SI mes = 03;  mesnum = dia3;  |FINSI;
   |SI mes = 04;  mesnum = dia4;  |FINSI;
   |SI mes = 05;  mesnum = dia5;  |FINSI;
   |SI mes = 06;  mesnum = dia6;  |FINSI;
   |SI mes = 07;  mesnum = dia7;  |FINSI;
   |SI mes = 08;  mesnum = dia8;  |FINSI;
   |SI mes = 09;  mesnum = dia9;  |FINSI;
   |SI mes = 10;  mesnum = dia10; |FINSI;
   |SI mes = 11;  mesnum = dia11; |FINSI;
   |SI mes = 12;  mesnum = dia12; |FINSI;

   dianum = dia;
   Icant = cant1 <-;
   Icant = Icant + dianum + mesnum;
   Icant = Icant - 1;
   cantidad = cant;

   #1#5 = cantidad; |PINTA #1#5;
   #1#2 = cantidad; |PINTA #1#2;

   aAlfa1 = #1#1 % A102;
   aAlfa2 = #1#1 % A302;
   aAlfa3 = #0#94;
   aAlfa4 = aAlfa1 + "." + aAlfa2 + "." + aAlfa3;
   fFech1 = aAlfa4;
   fFech2 = fFech1 % 1;
   aAlfa5 = fFech2 % 1199;
   |PINTA 1127, "         ";
   |ATRI R; |PINTA 1127, aAlfa5; |ATRI 0;
|FCAL;


|CALCULO modifi; |TIPO 0;
   #1#6  = #1#6 - #1#5 + #1#2; |PINTA #1#6;
|FCAL;
|CALCULO antes; |TIPO 7;
   #1#6  = #1#6 + #1#5 - #1#2; |PINTA #1#6;
|FCAL;


|PROCESO ElGrabar;

 |SI #1#7 = "N";
     #1#6  = total;
     |PINTA #1#6;
 |SINO;
     total = total - #1#5 + #1#2;
     #1#6 = total; |PINTA #1#6;

     cant = #1#2;
     |HAZ graba;
 |FINSI;
|FPRC;

|| ------------------------ Departamento
|CALCULO AntesDep; |TIPO 7;
|FCAL;

|CALCULO TrasDep; |TIPO 0;
|FCAL;
|| -----------------------------------------------------------------

|PROGRAMA;

   |HAZ inicio;
   dia1 = 0;

   |CLS;
   |PINPA #0#1;
   |CABEZA cab;
   |DDEFECTO #1;

||   |ENCAMPO #3#1;
||   |SI FSalida ! 0;    |ACABA;   |FINSI;
||   |C_M #1#3,1,"N";

   |ABRE #0;

   |HAZ clave;
   |SI sw_exist = 4;
         |MENAV error1;
         |CIERRA #0;
         |ACABA;
   |FINSI;

   |HAZ anyo;

   |PARA nada=0;  |SI nada = 0;  |HACIENDO nada=nada;

         #1#1 = "";  |PINTA #1#1; |PINTA 1127, "         ";
         #1#2 = 0;   |PINTA #1#2;
         #1#5 = 0;   |PINTA #1#5;
         #1#6 = total;  |PINTA #1#6;
         |ENDATOS #2#1;
         |SI FSalida ! 0;
             nada = FSalida;
         |SINO;
             |HAZ ElGrabar;
             nada = 0;
         |FINSI;
   |SIGUE;
   |LIBERA #0;
   |CIERRA #0;
|FPRO;

|| *************************************************************************
||---------------------------------------------------------------------

|PROCESO graba;
   Icant = cant1 <-;
   |PARA y = 1;  |SI y [ 4;  |HACIENDO y = y + 1;
      #0#0 = 1;
      #0#1 = y;
      |LEE 110#0=;
      num = 1;
      |PARA x = 1;  |SI x [ 92;  |HACIENDO x = x + 1;
         num = num + 1;
         #0num = cant;
         Icant = Icant + 1;
         |SI x = 91;  |Y y [ 2;
            |SAL;
         |FINSI;
      |SIGUE;
      |GRABA 020#0a;
      |LIBERA #0;
   |SIGUE;
|FPRC;

|| -------------------------------------------------------------
|PROCESO clave;  ||Lee las ventas

   sw_exist = 0;
   Icant = cant1 <-;
   total = 0;
   |PARA y = 1;  |SI y [ 4;  |HACIENDO y = y + 1;
      #0#0 = 1;
      #0#1 = y;
      || #0#95 = #1#3;
      |LEE 040#0=;
      |SI FSalida ! 0;
         |DDEFECTO #0;
         sw_exist = sw_exist + 1;
      |FINSI;
      num = 1
      |PARA x = 1;  |SI x [ 92;  |HACIENDO x = x + 1;
         num = num + 1;
         cant = #0num;
         total = total + cant;
         Icant = Icant + 1;
         |SI x = 91; |Y y [ 2;
            |SAL;
         |FINSI;
      |SIGUE;
   |SIGUE;
   #1#6 = total;
   |ATRI P; |PINTA #1#6; |ATRI 0;
|FPRC;

|PROCESO anyo;  ||Lee a¤o
   ||Calcular si es o no anyo bisiesto;

   aAlfa1 = #0#94; |QBF aAlfa1;
   aAlfa2 = aAlfa1 % -102;
   aAlfa3 = "0";
   swbis = 0;

   nNume1 = #0#94;
   nNume2 = (nNume1 / 4) ! 0; nNume3 = nNume1 / 4;
   |SI nNume2 = nNume3;
       swbis = 1;
       aAlfa3 = "1";
   |FINSI;

    aAlfa4 = "01.01." + aAlfa1;
    fFech1 = aAlfa4;
    fFech2 = fFech1 % 1;
    aAlfa4 = fFech2 % 1199; |QBF aAlfa4;

    |SI aAlfa4 = "Lunes";     aAlfa5 = "1"; |FINSI;
    |SI aAlfa4 = "Martes";    aAlfa5 = "2"; |FINSI;
    |SI aAlfa4 = "Miercoles"; aAlfa5 = "3"; |FINSI;
    |SI aAlfa4 = "Jueves";    aAlfa5 = "4"; |FINSI;
    |SI aAlfa4 = "Viernes";   aAlfa5 = "5"; |FINSI;
    |SI aAlfa4 = "Sabado";    aAlfa5 = "6"; |FINSI;
    |SI aAlfa4 = "Domingo";   aAlfa5 = "7"; |FINSI;

    anyo = aAlfa2 + aAlfa3 + aAlfa5;
|FPRC;
