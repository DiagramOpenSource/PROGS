|FICHEROS;
     cainprev #0;

     camanpag #1;
     capagcab #10;
     capaglin #11;

     caclipro #2;
     caivasop #3;
     cacuenta #5;
     camanban #6;

     catmppre #999;
|FIN;

|VARIABLES;
     nCampo   = 0;
     nInkey   = 0;
     aFichero = "";
     nSwHay   = 0;
     nNumero  = 0;
     nImporte = 0;

     nBusca    = 0;
     aCtaBanco = "";
     nDigBanco = 0;
     nBanco    = 0;
     aCtaProv  = "";
     nDigProv  = 0;
     aNomBanco = "";
     aNomProv  = "";
     aCC1Banco = "";
     aCC2Banco = "";
     eaDescr   = "";

     sw         = 0;
     informe    = "";
     &ult_cta   = "";
     &ult_ban   = "";
     &ult_banco = 0;
     &ult_dia = "";
     &ult_mes = "";
     &mes     = "";
     &total   = 0;
     &total1  = 0;
     &total2  = 0;
     &sw_inf  = 0;
     &cp      = "";
     alfa1    = "";
     novale   = 0;
|FIN;

|INCLUYE acceso_i;
|INCLUYE puntos_i;

|| ***********************************************************************
|CALCULO Cuenta; |TIPO 6;
  salidas = FSalida;
  p_alfa1 = #0Campo;
  |HAZ punto;
  |SI p_error ! 0;  |ERROR;  |ACABA;  |FINSI;
|FCAL;

|RUTINA inf5;
 #5#0 = "40          ";
 #5#1 = dig3;
|FRUT;

|RUTINA sup5;
 #5#0 = "52zzzzzzzzzz";
 #5#1 = dig3;
|FRUT;

|CALCULO CueCons; |TIPO 0;
 |SI FSalida = 10;
     |ABRE #5;
     |CONSULTA_F_CLAVE #1#5, inf5, sup5;
     |CIERRA #5;
     #0Campo  = #5#0; |PINTA #0Campo;
     nCampo   = nCampo + 4;
     #0nCampo = #5#1;
 |FINSI;
|FCAL;

|| ---------------------------------------------------------------------
|PROCESO el_mes;
    |SI ult_mes = "01";      mes = "ENERO";       |FINSI;
    |SI ult_mes = "02";      mes = "FEBRERO";     |FINSI;
    |SI ult_mes = "03";      mes = "MARZO";       |FINSI;
    |SI ult_mes = "04";      mes = "ABRIL";       |FINSI;
    |SI ult_mes = "05";      mes = "MAYO";        |FINSI;
    |SI ult_mes = "06";      mes = "JUNIO";       |FINSI;
    |SI ult_mes = "07";      mes = "JULIO";       |FINSI;
    |SI ult_mes = "08";      mes = "AGOSTO";      |FINSI;
    |SI ult_mes = "09";      mes = "SEPTIEMBRE";  |FINSI;
    |SI ult_mes = "10";      mes = "OCTUBRE";     |FINSI;
    |SI ult_mes = "11";      mes = "NOVIEMBRE";   |FINSI;
    |SI ult_mes = "12";      mes = "DICIEMBRE";   |FINSI;
|FPRC;

|PROCESO busca_cta;

  |SI #0#16 = "P";
     |ABRE #2;
     |DDEFECTO #2;
     #2#23 = "P";
     #2#10 = #999#4;
     #2#11 = #999#5;
     |LEE 001#2=;
     |CIERRA #2;
  |FINSI;

  |SI #0#16 = "B";
     |ABRE #6;
     |DDEFECTO #6;
     #6#0 = #999#16;
     |LEE 001#6=;
     |CIERRA #6;
  |FINSI;
|FPRC;

|| ---------------------------------------------------------------------
|PROCESO ImpRegistro;
  |SI sw = 0;
      sw = 1;
      |HAZ busca_cta;
      sw_inf = 0;
      |SI #0#16 = "P"; |O #0#16 = "B";
          |PRINT;
      |FINSI;
      ult_cta   = #999#4;  ||Proveedor
      ult_banco = #999#16; ||Banco
      ult_dia   = #999#7;  ||Fecha Vencimiento
      ult_ban   = #999#16; |QBF ult_ban;
      ult_ban   = ("000" + ult_ban) % -103;
      alfa1     = #999#7; alfa1 = alfa1 % 402;
      ult_mes   = alfa1;
  |FINSI;

  |SI #0#16 = "P";
      |SI ult_cta ! #999#4;
          sw_inf = 2;    ||Imprime el primer total
          |PRINT;
          sw_inf = 0;
          |HAZ busca_cta;
          |PRINT;
          total = 0;
          ult_cta   = #999#4;  ||Proveedor
     |FINSI;
  |FINSI;

  |SI #0#16 = "B";
      |SI ult_banco ! #999#16;
          sw_inf = 2;    ||Imprime el primer total
          |PRINT;
          sw_inf = 0;
          |HAZ busca_cta;
          |PRINT;
          total = 0;
          ult_banco = #999#16;
          ult_ban   = #999#16; |QBF ult_ban;
          ult_ban   = ("000" + ult_ban) % -103;
     |FINSI;
  |FINSI;

  |SI #0#16 = "V";
      alfa1 = #999#7;  alfa1 = alfa1 % 402;
      |SI ult_mes ! alfa1;
          sw_inf = 2;
          |PRINT;
          sw_inf = 3;
          |HAZ el_mes;
          |PRINT;
          total = 0;
          total2 = 0;
          ult_mes = alfa1;
          ult_dia = #999#7;
     |SINO;
         |SI ult_dia ! #999#7;
               sw_inf = 2;    ||Imprime el primer total
               |PRINT;
               total = 0;
               ult_dia = #999#7;
          |FINSI;
     |FINSI;
  |FINSI;

  sw_inf = 1;
  |PRINT;
  total  = total  + #999#8;
  total1 = total1 + #999#8;
  total2 = total2 + #999#8;
  ult_cta   = #999#4;  ||Proveedor
  ult_banco = #999#16; ||Banco
  ult_dia   = #999#7;  ||Fecha Vencimiento
  ult_ban   = #999#16; |QBF ult_ban;
  ult_ban   = ("000" + ult_ban) % -103;
  alfa1     = #999#7; alfa1 = alfa1 % 402;
  ult_mes   = alfa1;
|FPRC;

|DEFBUCLE PorVto;
 #999#4;
 ;
 #0#6, "01.01.0000", 0000000001;
 #0#7, "31.12.2999", 9999999999;
 ;
 NULL, ImpRegistro;
|FIN;

|DEFBUCLE PorBanco;
 #999#3;
 ;
 #0#10, #0#6, 0000000001;
 #0#11, #0#7, 9999999999;
 ;
 NULL, ImpRegistro;
|FIN;

|DEFBUCLE PorProve;
 #999#2;
 ;
 #0#4, 0, #0#6, 0000000001;
 #0#5, 9, #0#7, 9999999999;
 ;
 NULL, ImpRegistro;
|FIN;
|| ---------------------------------------------------------------------

|PROCESO Imprimir;
  informe   = "cainprev";
  |SI #0#16 = "B"; informe = "cainpreb"; |FINSI;
  |SI #0#16 = "P"; informe = "cainprep"; |FINSI;

  |INFOR informe;
  |SI FSalida ! 0;
      |MENAV "    No existe informe";
      |ACABA;
  |FINSI;

 sw = 0;
 |SI #0#16 = "V"; |BUCLE PorVto;   |FINSI;
 |SI #0#16 = "B"; |BUCLE PorBanco; |FINSI;
 |SI #0#16 = "P"; |BUCLE PorProve; |FINSI;
 |SI sw = 1;
     |SI #0#16 = "V";
           sw_inf = 2;
           |PRINT;
           sw_inf = 3;
           |HAZ el_mes;
           |PRINT;
     |FINSI;
     |SI #0#16 = "P"; |O #0#16 = "B";
         sw_inf = 2;
         |PRINT;
     |FINSI;
     |PIEINF;
 |FINSI;
 |FININF;
|FPRC;

|| ................. Datos auxiliares
|PROCESO LeeIva;
     eaDescr = "";
     |DDEFECTO #3;
     #3#0  = #1#0;
     #3#2  = #1#1;
     #3#27 = #1#2;
     |LEE 001#3=;
     eaDescr = #3#7;
|FPRC;

|PROCESO BuscaProv;
     |DDEFECTO #5;
     #5#0 = aCtaProv;
     #5#1 = nDigProv;
     |LEE 041#5=;
     aNomProv = #5#2;
|FPRC;

|PROCESO BuscaBanco;
 aNomBanco = "";
 aCC1Banco = "";
 aCC2Banco = "";
 |DDEFECTO #6;
 |SI nBusca = 2;
     |SELECT #2#6;
     nBanco = 0;
     #6#10  = aCtaBanco;
     #6#11  = nDigBanco;
 |SINO;
     aCtaBanco = "";
     nDigBanco = 0;
     #6#0      = nBanco;
 |FINSI;
 |LEE 001#6=;
 |SI FSalida = 0;
     nBanco    = #6#0;
     aCtaBanco = #6#10;
     nDigBanco = #6#11;
     aNomBanco = #6#1;
     aCC1Banco = #6#8;
     aCC2Banco = #6#9;
 |FINSI;
 |SELECT #1#6;
|FPRC;


|| ..........Auxiliar de Pagares
|PROCESO CreaAuxiliar_2;

 |SI #10#8 = "S"; |ACABA; |FINSI;  ||Ya esta vencido

 nBusca    = 2;
 aCtaBanco = #10#3;
 nDigBanco = #10#4;
 |HAZ BuscaBanco;
 |SI nBanco < #0#10; |O nBanco > #0#11;
     |ACABA;
 |FINSI;  || no es el banco bueno

 aCtaProv = #10#1;
 nDigProv = #10#2;
 |HAZ BuscaProv;

 |DDEFECTO #999;
 nNumero = nNumero + 1;
 #999#19 = nNumero;

 #999#0  = 0;
 #999#1  = 0;
 #999#2  = "";
 #999#3  = 0;
 #999#4  = #10#1;
 #999#5  = #10#2;
 #999#6  = #10#5;
 #999#7  = #10#6;
 #999#8  = #10#9;
 #999#9  = 0;
 #999#10 = #10#0;      || numero de pagare
 #999#11 = aCtaBanco;
 #999#12 = nDigBanco;
 #999#13 = aNomBanco;
 #999#14 = aCC1Banco;
 #999#15 = aCC2Banco;
 #999#16 = nBanco;
 #999#17 = aNomProv;
 alfa1   = #10#0; |QBF alfa1;
 alfa1   = ("000000" + alfa1) % -106;
 alfa1   = "Pagare numero: " + alfa1;
 #999#18 = alfa1;
 |GRABA 040#999n;
 nSwHay = 1;
|FPRC;

|DEFBUCLE graba_temp_pagare;
 #10#1;
 1, 6;
 #0#14, #0#4, #0#6;
 #0#15, #0#5, #0#7;
 e;
 NULL, CreaAuxiliar_2;
|FIN;

|| ..........Auxiliar de Pagos
|PROCESO CreaAuxiliar_1;

 nImporte = (#1#8 - #1#23) ! EURODB;
 |SI nImporte = 0; |O #1#22 = "S";
     |ACABA;
 |FINSI;

 nBusca = 1;
 nBanco = #1#26;
 |HAZ BuscaBanco;

 aCtaProv = #1#4;
 nDigProv = #1#5;
 |HAZ BuscaProv;

 |DDEFECTO #999;
 nNumero = nNumero + 1;
 #999#19 = nNumero;

 #999#0  = #1#0;
 #999#1  = #1#1;
 #999#2  = #1#2;
 #999#3  = #1#3;
 #999#4  = #1#4;
 #999#5  = #1#5;
 #999#6  = #1#6;
 #999#7  = #1#7;
 #999#8  = nImporte;  || Importe a imprimir
 #999#9  = #1#11;
 #999#10 = #1#12;

 #999#11 = aCtaBanco;
 #999#12 = nDigBanco;
 #999#13 = aNomBanco;
 #999#14 = aCC1Banco;
 #999#15 = aCC2Banco;
 #999#16 = nBanco;
 #999#17 = aNomProv;
 #999#18 = #1#28;

 eaDescr = #999#18; |QBF eaDescr;
 |SI eaDescr = "";
     |HAZ LeeIva;
     #999#18 = eaDescr;
 |FINSI;

 |GRABA 040#999n;
 nSwHay = 1;
|FPRC;

|DEFBUCLE graba_temp_pago;
 #1#1;
 11, 4, 7, 26, 12;  || tipo recibo, proveedor, vto, banco, pagare
 #0#0, "  ", #0#2, 000, #0#12, #0#4, #0#6, #0#10, 000000;
 #0#1, "zz", #0#3, 999, #0#13, #0#5, #0#7, #0#11, 000000;
 e;
 NULL, CreaAuxiliar_1;
|FIN;
|| /////////////////////////////////////////////////////////////////////

|CALCULO inicia; |TIPO 3;

 aFichero = "ym" + Usuario;
 |NOME_DAT #999 aFichero;
 |DELINDEX #999;

 |IMPRE 0;
 |SI FSalida ! 0;  |ACABA;  |FINSI;

 nNumero = 0;
 nSwHay  = 0;
 |ABRE #3;
 |ABRE #5;
 |ABRE #6;
 |ABRE #999;
 |BUCLE graba_temp_pago;
 |BUCLE graba_temp_pagare;
 |CIERRA #999;
 |CIERRA #6;
 |CIERRA #5;
 |CIERRA #3;

 |SI nSwHay = 0;
     |MENAV "    Seleccion vacia";
 |SINO;
     |HAZ Imprimir;
 |FINSI;

 |FINIMP;
 |DELINDEX #999;
|FCAL;
