|FICHEROS;
   ca8m0031 #804;
   ca8m0033 #805;

   imag8v4@ #14;
   imag804@ #904;

   :/agicont/def/ca8m0000 #420;  || Def Balance
   :/agicont/def/ca8m0001 #421;  || Def Balance Textos
   :/agicont/def/ca8m0002 #422;  || Def Balance Linea Cuentas

    &ficher06@ #442;  || Def Balance Estado
    &ficher07@ #443;  || Lineas
    &ficher12@ #444;  || Columnas
    &ficher13@ #445;  || Lineas Texto
|FIN;

|VARIABLES;
   &nBalance = 0;
   &nBalNum  = 0;
   &nCtaNum  = 0;
   &aLucrat  = "";

   &aDef06   = "";
   &aDef07   = "";
   &aDef12   = "";
   &aDef13   = "";

   nLinea    = 0;
   nCampo0   = 0;
   nCampo1   = 0;
   aCampo4   = "";
   aTipoA    = "";
   aAlfa1    = "";
   aAlfa2    = "";
   aAlfa3    = "";
   aAlfa4    = "";
   aAlfa5    = "";
   nNume1    = 0;
   nNume2    = 0;
   &enSwCero = 0;
   nPara1    = 0;
   nPara2    = 0;
   nPara3    = 0;
   nColum    = 0;
   nDLinea   = 0;
   nHLinea   = 0;
   nQueHago  = 0;
   aMas      = "";
   aDef      = "";
   aColum    = "";
   nImporte  = 0;
   nSwDescarga = 0;

   &enEmpresa   = 0;
   nActual      = 0;
   &enEjerBal   = 0;
   &enPlan      = 0;
   &eaPrograma  = "";
   &eaExtension = "";
   nSw          = 0;
   nDEjer       = 0;
   nHEjer       = 0;
   &enDPlan     = 0;
   &wExCa8M0    = 0;
   &wPatOfi     = 0;
   &wTipoPat    = 0;
|FIN;

|INCLUYE varnue_i;

|PROCESO GrabaAuxiliar;

         nLinea = nLinea + 1;
         |DDEFECTO #805;
         #805#0 = enEmpresa;
         #805#1 = enEjercicio;
         #805#2 = nLinea;

         #805#3  = #421#9;    ||Texto
         #805#4  = "N"; |SI #421#7 = 0; #805#4 = "G"; |FINSI;
         |SI aCampo4 = ""; aCampo4 = #805#4; |FINSI;
         #805#8  = #421#8;
         |SI #805#8 = "A"; #805#4 = aCampo4; |FINSI;

         #805#9  = #421#2;
         #805#10 = #421#3;
         #805#11 = #421#4;
         #805#12 = #421#5;
         #805#13 = #421#6;
         #805#14 = #421#7;
         #805#15 = #421#10;
         #805#16 = #421#11;   ||Casilla Cuentas Anuales
         #805#17 = #421#12;   ||Casilla Impuesto
         #805#5  = #421#13;   ||Nota memoria
         |GRABA 040#805n;
|FPRC;

|DEFBUCLE GrabaAuxiliar;
 #421#1;
 ;
 enEjerBal, nCampo0, nCampo1, 0, " ", 00, 0, 00, 00, 0;
 enEjerBal, nCampo0, nCampo1, 9, "z", 99, 9, 99, 99, 9;
 ;
 NULL, GrabaAuxiliar;
|FIN;

|PROCESO GrabaAux_Nuevo;
         |DDEFECTO #804;
         #804#0 = enEmpresa;
         #804#1 = enEjercicio;
         #804#2 = #442#1;
         #804#3 = #442#2;
         #804#4 = #442#3;
         #804#5 = #442#6;
         |HAZ eCambioT;
         #804#43 = #442#21; #804#44 = #442#22; #804#45 = #442#23;
         #804#46 = #442#24; #804#47 = #442#25; #804#48 = #442#26;
         #804#49 = #442#27; #804#50 = #442#28; #804#51 = #442#29;
         #804#52 = #442#30; #804#53 = #442#31; #804#54 = #442#32;
         #804#55 = #442#33;
         |GRABA 040#804n;
|FPRC;

|PROCESO eCambioT;

 aAlfa1 = #804#5-"200X";
 |SI aAlfa1 = #804#5; |ACABA; |FINSI;

 aAlfa1 = enEjercicio;
 nNume1 = enEjercicio - 1; aAlfa2 = nNume1;
 nNume1 = enEjercicio - 2; aAlfa3 = nNume1;

 aAlfa4 = #804#5; |QBF aAlfa4;

 aAlfa5 = aAlfa4 - "200X-2 y anter";
 |SI aAlfa4 ! aAlfa5;
     aAlfa4 = aAlfa5 + " " + aAlfa3 + " y anteriores";
 |SINO;
     aAlfa5 = aAlfa4 - "200X-2 y anteriores";
     |SI aAlfa4 ! aAlfa5;
         aAlfa4 = aAlfa5 + " " + aAlfa3 + " y anteriores";
     |SINO;
         aAlfa5 = aAlfa4 - "200X-2";
         |SI aAlfa4 ! aAlfa5;
             aAlfa4 = aAlfa5  + aAlfa3;
         |SINO;
             aAlfa5 = aAlfa4 - "200X-1";
             |SI aAlfa4 ! aAlfa5;
                 aAlfa4 = aAlfa5 + aAlfa2;
             |SINO;
                 aAlfa5 = aAlfa4 - "200X";
                 |SI aAlfa4 ! aAlfa5;
                     aAlfa4 = aAlfa5 + aAlfa1;
                 |FINSI;
             |FINSI;
         |FINSI;
     |FINSI;
 |FINSI;
 #804#5 = aAlfa4;
|FPRC;

|DEFBUCLE GrabaAux_Nuevo;
 #442#1003;
 ;
 enEjerBal, nCampo0, 001;
 enEjerBal, nCampo0, 999;
 ;
 NULL, GrabaAux_Nuevo;
|FIN;

|PROCESO eCrearVacio;

  |SI nBalance = 4;
      nLinea  = 0;
      nCampo0 = nBalNum;
      nCampo1 = 4;
      |ABRE #805
      |BUCLE GrabaAuxiliar;
      |CIERRA #805
  |FINSI;

  |SI nBalance = 3;
      nLinea  = 0;
      nCampo0 = nBalNum;
      nCampo1 = 3;
      |ABRE #804;
      |BUCLE GrabaAux_Nuevo;
      |CIERRA #804;
  |FINSI;
|FPRC;

|| ------------------------------------------------------------------------
|| ------------------------------------------------------------------------
|PROCESO MiroDefinicion;
  ||... Campos "X" dejarlos a 0
  |DDEFECTO #442;
  #442#34= enEjerBal;
  #442#0 = nBalNum;
  #442#1 = nLinea;
  |LEE 041#442=;
|FPRC;

|PROCESO Total804;
 nLinea = #904#2;
 |HAZ MiroDefinicion;
 |SI #442#5 ! "S";
     |PARA nPara1 = 6; |SI nPara1 [ 42; |HACIENDO nPara1 = nPara1 + 1;
           #804nPara1 = #804nPara1 + #904nPara1;
     |SIGUE;
 |FINSI;
|FPRC;

|DEFBUCLE Total804;
 #904#1003;
 ;
 enEmpresa, enEjercicio, nDLinea;
 enEmpresa, enEjercicio, nHLinea;
 e;
 NULL, Total804;
|FIN;

|PROCESO Modifico_4;
  |SI nQueHago = 0;              ||Poner a 0 acumulado de Variaciones
      |PDEFECTO #804, 31, 43;
  |FINSI;

  |SI nQueHago = 1; |O nQueHago = 2;
      nLinea = #804#2;
      |HAZ MiroDefinicion;

     |SI nQueHago = 1;              ||Totales
         |SI #442#5 = "S";
             |SI #442#20 > 0;
                 nDLinea = nLinea + 1;
                 nHLinea = nLinea + #442#20;
             |SINO;
                 nDLinea = nLinea + #442#20;
                 nHLinea = nLinea - 1;
             |FINSI;
             |PDEFECTO #804,6,43;
             |BUCLE Total804;
         |FINSI;
     |FINSI;

     |SI nQueHago = 2;              ||Suma de Cuentas + Variaciones
         #804#18 = 0;
         |PARA nPara1 = 6; |SI nPara1 [ 17; |HACIENDO nPara1 = nPara1 + 1;
               nNume1 = nPara1 + 1;
               aColum = #442nNume1;

               nPara2 = nPara1 + 13;
               nPara3 = nPara1 + 25;

               |SI aColum ! "X";
                   #804nPara1 = #804nPara2 + #804nPara3;
                   #804#18 = #804#18 + #804nPara1;
               |SINO;
                   #804nPara1 = 0;
                   #804nPara2 = 0;
                   #804nPara3 = 0;
               |FINSI;
         |SIGUE;
     |FINSI;
 |FINSI;

 |GRABA 020#804a;
 |LIBERA #804;
|FPRC;

|DEFBUCLE Modifico_4;
 #804#1;
 ;
 enEmpresa, enEjercicio, 001;
 enEmpresa, enEjercicio, 999;
 be;
 NULL, Modifico_4;
|FIN;

|PROCESO Act_M804;
 #804#0 = enEmpresa;
 #804#1 = enEjercicio;
 #804#2 = nLinea;
 |LEE 101#804=;
 |SI FSalida = 0;
     nNume1 = nColum + 30;
     #804nNume1 = #804nNume1 + nImporte;
     |GRABA 020#804a;
 |FINSI;
 |LIBERA #804;
|FPRC;

|PROCESO LeeVariacion;
  nLinea = #14#4;
  nColum = #14#5;
  |HAZ MiroDefinicion;
  nNume1 = nColum + 6;
  aColum = #442nNume1;

  |SI aColum ! "X";
      nImporte = #14#6;
      |HAZ Act_M804;
  |FINSI;
|FPRC;

|DEFBUCLE LeeVariacion;
 #14#1003;
 ;
 enEmpresa, enEjercicio, 0001;
 enEmpresa, enEjercicio, 9999;
 e;
 NULL, LeeVariacion;
|FIN;

|PROCESO eElCalFinal804;

 nCampo0 = nBalNum;
 nCampo1 = 3;
 |DBASE aMas;
 aDef = aMas + "def/ca8m0031";
 |CARGA_DEF aDef, imag804@;
 |SI FSalida ! 0; |ACABA; |FINSI;

 aDef = aMas + "def/ca8m0032";
 |CARGA_DEF aDef, imag8v4@;
 |SI FSalida ! 0;
     |DESCARGA_DEF #imag804@;
     |ACABA;
 |FINSI;

 |SI enSwCero = 1;
     nQueHago = 0;
     |BUCLE Modifico_4;
 |FINSI;

 |ABRE #442;
 |SI enSwCero ! 2;
     |ABRE #804;
     |BUCLE LeeVariacion;
     |CIERRA #804;
 |FINSI;

 nQueHago = 1;
 |BUCLE Modifico_4;

 nQueHago = 2;
 |BUCLE Modifico_4;
 |CIERRA #442;

 |DESCARGA_DEF #imag8v4@;
 |DESCARGA_DEF #imag804@;
|FPRC;

|PROCESO eCargaECPN;
  nSwDescarga = 1;
  |SI HaySociedad = 0;
      |DBASE aMas;
      |SI aDef06 = "";
          aDef06 = aMas + "def/ca8m0006";
          |CARGA_DEF aDef06, ficher06@;
          |SI FSalida ! 0;
              aDef06 = "";
              |MENAV "    Error al Carga fichero de Columnas";
              |ACABA;
          |FINSI;
      |FINSI;
      |SI aDef07 = "";
          aDef07 = aMas + "def/ca8m0007";
          |CARGA_DEF aDef07, ficher07@;
          |SI FSalida ! 0;
              aDef07 = "";
              |MENAV "    Error al Carga fichero de Columnas";
              |ACABA;
          |FINSI;
      |FINSI;
      |SI aDef12 = "";
          aDef12 = aMas + "def/ca8mtcol";
          |CARGA_DEF aDef12, ficher12@;
          |SI FSalida ! 0;
              aDef12 = "";
              |MENAV "    Error al Carga fichero de Columnas";
              |ACABA;
          |FINSI;
      |FINSI;

      |SI aDef13 = "";
          aDef13 = aMas + "def/ca8mtlin";
          |CARGA_DEF aDef13, ficher13@;
          |SI FSalida ! 0;
              aDef13 = "";
              |MENAV "    Error al Carga fichero de Columnas";
              |ACABA;
          |FINSI;
      |FINSI;
  |SINO;

      |SI aDef06 = "";
          aDef06 = aPContas + "def/soda8m02";
          |CARGA_DEF aDef06, ficher06@;
          |SI FSalida ! 0;
              aDef06 = "";
              |MENAV "    Error al Carga fichero de Columnas";
              |ACABA;
          |FINSI;
      |FINSI;
      |SI aDef07 = "";
          aDef07 = aPContas + "def/soda8m03";
          |CARGA_DEF aDef07, ficher07@;
          |SI FSalida ! 0;
              aDef07 = "";
              |MENAV "    Error al Carga fichero de Columnas";
          |FINSI;
      |FINSI;
      |SI aDef12 = "";
          aDef12 = aPContas + "def/soda8tco";
          |CARGA_DEF aDef12, ficher12@;
          |SI FSalida ! 0;
              aDef12 = "";
              |MENAV "    Error al Carga fichero de Columnas";
              |ACABA;
          |FINSI;
      |FINSI;

      |SI aDef13 = "";
          aDef13 = aPContas+ "def/soda8tli";
          |CARGA_DEF aDef13, ficher13@;
          |SI FSalida ! 0;
              aDef13 = "";
              |MENAV "    Error al Carga fichero de Lineas Texto";
              |ACABA;
          |FINSI;
      |FINSI;
  |FINSI;
|FPRC;

|PROCESO eDesCargaECPN;
  |SI aDef13 ! ""; |DESCARGA_DEF #ficher13@; aDef13 = ""; |FINSI;
  |SI aDef12 ! ""; |DESCARGA_DEF #ficher12@; aDef12 = ""; |FINSI;
  |SI aDef07 ! ""; |DESCARGA_DEF #ficher07@; aDef07 = ""; |FINSI;
  |SI aDef06 ! ""; |DESCARGA_DEF #ficher06@; aDef06 = ""; |FINSI;
|FPRC;

|| ***************************************************************

|PROCESO PrimeraPas;
  nSw = 1;
  |ERROR10;
|FPRC;

|DEFBUCLE PrimeraPas;
  #421#1;
  ;
  nDEjer, nBalNum, 0, 0, " ",00,00,00,00,0;
  nHEjer, nBalNum, 9, 9, "z",99,99,99,99,9;
  e;
  NULL, PrimeraPas;
|FIN;

|DEFBUCLE SegundaPas;
  #442#1003;
  ;
  nDEjer, nBalNum, 001;
  nHEjer, nBalNum, 999;
  e;
  NULL, PrimeraPas;
|FIN;

|PROCESO eBorraCa8m0000;

    |SI nBalNum ! 0; |Y nBalNum ! 1; |Y nBalNum ! 10;
       |Y nBalNum ! 30; |Y nBalNum ! 31; |Y nBalNum ! 32;
         |Y nBalNum ! 40; |Y nBalNum ! 41; |Y nBalNum ! 42;
         nDEjer = 2008;
         nHEjer = 2099;
         nSw = 0;
         |BUCLE PrimeraPas;
         |SI nSw = 0;
            |HAZ eCargaECPN;
            |BUCLE SegundaPas;
            |HAZ eDesCargaECPN;
         |FINSI;
         |SI nSw = 0;
             |ABRE #420;
             #420#0 = nBalNum;
             |LEE 141#420=;
             |SI FSalida = 0;
                 |BORRA 020#420a;
             |FINSI;
             |LIBERA #420;
             |CIERRA #420;
         |FINSI;

    |FINSI;
|FPRC;

|PROCESO eExisteCa8m0000;
    wExCa8M0 = 0;
    nDEjer = enDPlan;
    nHEjer = enDPlan;
    nSw = 0;
    |BUCLE PrimeraPas;
    |SI nSw = 0;
        |HAZ eCargaECPN;
        |BUCLE SegundaPas;
        |HAZ eDesCargaECPN;
    |FINSI;
    wExCa8M0 = nSw;
|FPRC;

|PROCESO eVerEsPatron;
  wPatOfi = 0;

  |SI enDPlan = 2008; |O enDPlan = 2015;
     |O enDPlan = 2016; |O enDPlan = 2018;

      |SI wTipoPat = 0;
          |SI nBalNum = 0; |O nBalNum = 1;
             |O nBalNum = 30; |O nBalNum = 31;
                wPatOfi = 1;
          |SINO;
              |SI enDPlan = 2008;
                  |SI nBalNum = 20; |O nBalNum = 21;
                      wPatOfi = 1;
                  |FINSI;
              |SINO;
                  |SI enDPlan = 2016; |O enDPlan = 2018;
                     |SI nBalNum = 40; |O nBalNum = 41;
                         wPatOfi = 1;
                     |FINSI;
                  |FINSI;
              |FINSI;
          |FINSI;
     |FINSI;

      |SI wTipoPat = 1;
          |SI nBalNum = 0; |O nBalNum = 1; |O nBalNum = 10;
             |O nBalNum = 30; |O nBalNum = 31; |O nBalNum = 32;
                wPatOfi = 1;
          |SINO;
              |SI enDPlan = 2008;
                  |SI nBalNum = 5; |O nBalNum = 6; |O nBalNum = 7;
                    |O nBalNum = 20; |O nBalNum = 21; |O nBalNum = 22;
                      wPatOfi = 1;
                  |FINSI;
              |SINO;
                  |SI enDPlan = 2016; |O enDPlan = 2018;
                      |SI nBalNum = 40; |O nBalNum = 41; |O nBalNum = 42;
                          wPatOfi = 1;
                      |FINSI;
                  |FINSI;
              |FINSI;
          |FINSI;
     |FINSI;
  |FINSI;
|FPRC;
