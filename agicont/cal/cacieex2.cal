|FICHEROS;
    cacieex4 #0;   || Auxiliar Apuntes Lineas
    cacieex2 #11;  || def de proceso Cabecera
    cacuenta #1;   || cuentas
    camaasic #2;   || apuntes cabs.
    camaasil #3;   || apuntes lins.
    caconcep #4;   || conceptos automaticos
    camandia #5;   || diarios
|FIN;

|VARIABLES;
    &enDiario   = 0;
    &enContador = 0;
    &entrada = 1;
    &modcon  = 0;

    &swpanta = 0;
    &swcalcu = 0;
    &algun   = 0;
    diari = 0;
    fecha = @;
    docum = 0;
    linea = 0;
    estado = 0;
    salidas = 0;

    importe = 0;
    ini = 0;
    fecalf = @;
    mes = "";
    mesfec = 0;
    mesac = 0;
    a = 0;
    b = 0;
    c = 0;
    d = 0;
    x = 0;
    y = 0;
    cta_t = 0;
    dig_t = 0;
    alfa1 = "";
    alfa2 = "";
    long = 0;
    cont = 0;
    sw = 0;
    doce = "            ";

    am = 0;
    bm = 0;
    cm = 0;
    swalta = 0;
    pasado = 0;
    avisos = "";

    nume1 = 0;
    nume2 = 0;
    xx = 0;
    ctdeant = "";
    dideant = 0;
    cthaant = "";
    dihaant = 0;
    cuant = "";
    diant = 0;
    coant = 0;
    deant = "";
    dhant = "";
    imant = 0;
    trant = "";
    drant = 0;
    concepto = "";

   nLaBaseImp = 0;

   aAlfa1 = "";
   enEjercicio = 0;
   nTtip1 = 0;
   nTtip2 = 0;

|FIN;

|INCLUYE inici1_i;
|INCLUYE punto3_i;
|INCLUYE tempo_i;
|INCLUYE condes_i;

========================================================================
                        CALCULOS PANTALLA
========================================================================
|PROCESO verdatos;
 |SI swcalcu = 1; |Y algun = 0;
     |ACABA;
 |FINSI;
 |SUB_EJECUTA "cargacie.run";
 swcalcu = 1;
|FPRC;

|PROCESO grtipo2; |TIPO 7;
  nLaBaseImp = 15000000; || Preparado para comparacion Euro
  |SI enEjercicio ] 2005;
      nLaBaseImp = 20000000; || Preparado para comparacion Euro
  |FINSI;
  |SI eaMoneda = "E";
      nLaBaseImp = (nLaBaseImp / EURO) ! EURODB;
      |SI enEjercicio ] 2005; nLaBaseImp = nLaBaseImp - 0.01; |FINSI;
  |FINSI;
  |SI enEjercicio ] 2011;
      nLaBaseImp = 300000;
  |FINSI;

  |C_M #11#32,1,"S";
  #11#6 = #11#3 + #11#5;
  |SI #11#6 [ nLaBaseImp;
      |C_M #11#32,1,"N";
      #11#32 = nTtip2; |PINTA #11#32;
  |FINSI;
|FPRC;

|PROCESO calcu35; |TIPO 0;
  salidas = FSalida;
  |SI salidas = 1; |O salidas = 9;
      |SI salidas = 9; |HAZ verdatos; |FINSI;
      |ERROR;
      |ACABA;
  |FINSI;

  nLaBaseImp = 15000000; || Preparado para comparacion Euro
  |SI enEjercicio ] 2005;
      nLaBaseImp = 20000000; || Preparado para comparacion Euro
  |FINSI;
  |SI eaMoneda = "E";
      nLaBaseImp = (nLaBaseImp / EURO) ! EURODB;
      |SI enEjercicio ] 2005; nLaBaseImp = nLaBaseImp - 0.01; |FINSI;
  |FINSI;

  |SI enEjercicio ] 2011;
      nLaBaseImp = 300000;
  |FINSI;

  #11#6 = #11#3 + #11#5;
  |SI enEjercicio [ 2015;
      |SI #11#6 < nLaBaseImp;
          #11#8 = ((#11#6 * #11#7) / 100) ! EURODB;  ||Redondeo EURO
      |SINO;
          #11#8 = ((nLaBaseImp * #11#7) / 100) ! EURODB;  ||Redondeo EURO
          nume1 = #11#6 - nLaBaseImp;
          nume2 = ((nume1 * #11#32) / 100) ! EURODB;  ||Redondeo EURO
          #11#8 = #11#8 + nume2;
      |FINSI;
  |SINO;
      #11#8 = ((#11#6 * #11#7) / 100) ! EURODB;  ||Redondeo EURO
  |FINSI;

  #11#10 = #11#8 - #11#9;
  |PINTA #11#6;
  |PINTA #11#8;
  |PINTA #11#10;

  |SI #11#10 ] 0;
      |HAZ ganamos;
  |SINO;
      |HAZ perdemos;
  |FINSI;
  |PINTA #11#15; |PINTA #11#16; |PINTA #11#18; |PINTA #11#20; |PINTA #11#30; |PINTA #11#21;
  |PINTA #11#22; |PINTA #11#23; |PINTA #11#25; |PINTA #11#27; |PINTA #11#31; |PINTA #11#28;
  a = #11#10;
  |SI #11#10 < 0; a = -a; |FINSI;
  #11#12 = a; |PINTA #11#12;
  #11#13 = a; |PINTA #11#13;
  #11#14 = 0;      |PINTA #11#14;
|FPRC;

|PROCESO ganamos;
  a = Campo;
  Campo = 10;
  #0Campo = "630.        "
  p_alfa1 = #0Campo;
  salidas = FSalida;
  |HAZ punto3;
  #11#16 = #0#10;
  cta_t = 16;
  dig_t = 17;
  |HAZ cuenta_t;
  #11#21 = #11#10;
  Campo = 8;
  #0Campo = "4752.       ";
  p_alfa1 = #0Campo;
  salidas = FSalida;
  |HAZ punto3;
  #11#25 = #0#8;
  cta_t = 25;
  dig_t = 26;
  |HAZ cuenta_t;
  #11#28 = #11#10;
  Campo = a;
  #11#18 = "            ";
  #11#19 = 0;
  #11#23 = "            ";
  #11#24 = 0;
  concepto = #11#20; |HAZ leeconcepto; #11#30 = #4#1;
  concepto = #11#27; |HAZ leeconcepto; #11#31 = #4#1;
|FPRC;

|PROCESO perdemos;
  a = Campo;
  Campo = 10;
  #0Campo = "4745.       ";
  p_alfa1 = #0Campo;
  salidas = FSalida;
  |HAZ punto3;
  #11#16 = #0#10;
  cta_t = 16;
  dig_t = 17;
  |HAZ cuenta_t;
  #11#21 = -#11#10;
  Campo = 8;
  #0Campo = "630.        ";
  p_alfa1 = #0Campo;
  salidas = FSalida;
  |HAZ punto3;
  #11#25 = #0#8;
  cta_t = 25;
  dig_t = 26;
  |HAZ cuenta_t;
  #11#28 = -#11#10;
  Campo = a;
  #11#18 = "            ";
  #11#19 = 0;
  #11#23 = "            ";
  #11#24 = 0;
|FPRC;

|PROCESO cuenta_t;
  alfa1 = #11cta_t;
  |QBF alfa1;
  alfa2 = alfa1 % 0;
  long = alfa2;
  cont = 0;
  sw = 0;
  |SI long = dig4;  sw = 1;  #11dig_t = 1;  |FINSI;
  |SI long = dig5;  sw = 1;  #11dig_t = 2;  |FINSI;
  |SI long = dig6;  sw = 1;  #11dig_t = 3;  |FINSI;
  |SI long = dig7;  sw = 1;  #11dig_t = 4;  |FINSI;
  |SI long = dig8;  sw = 1;  #11dig_t = 5;  |FINSI;
  |SI long = dig9;  sw = 1;  #11dig_t = 6;  |FINSI;
  |SI long = 0;     sw = 1;  #11dig_t = 0;  |FINSI;
|FPRC;

|| ========================================================================
|PROCESO defectos; |TIPO 7;
  |SI pasado = 0;
      #11#0 = fec2;
      #11#1 = 1;
      |HAZ documento;
      #11#2 = enContador;
      pasado = 1;
      |PINTA #11#1; |PINTA #11#0; |PINTA #11#2;
  |FINSI;
|FPRC;

|PROCESO diario; |TIPO 0;
 salidas = FSalida;
 |SI salidas = 1; |O salidas = 9;
     |SI salidas = 9; |HAZ verdatos; |FINSI;
     |ERROR;
 |FINSI;
|FPRC;

|PROCESO topes; |TIPO 0;
  salidas = FSalida;
  |SI salidas = 1; |O salidas = 9;
      |SI salidas = 9; |HAZ verdatos; |FINSI;
      |ERROR;
      |ACABA;
  |FINSI;
  |SI #11Campo < fec1; |O #11Campo > fec2;
      |MENAV "    ESTA FECHA NO CORRESPONDE AL EJERCICIO";
      |ERROR;
  |FINSI;
|FPRC;

|PROCESO leedocum; |TIPO 0;
 salidas = FSalida;
 |SI salidas = 2; |ACABA; |FINSI;
 |SI salidas = 9;     || |O salidas = 1;
     |SI salidas = 9; |HAZ verdatos; |FINSI;
     |ERROR;
     |ACABA;
 |FINSI;
 |ABRE #2;
 #2#0 = #11#1;
 #2#1 = #11#0;
 #2#2 = #11#2;
 |LEE 001#2=;
 |SI FSalida = 0;
     |MENAV "    ERROR!. El Asiento Contable YA existe ... ";
     |ERROR;
     |ACABA;
 |FINSI;
 |CIERRA #2;

 |HAZ altalin;
|FPRC;

|PROCESO altalin;
 |ABRET #0;

 |DDEFECTO #0;
 #0#0 = "A";
 #0#1 = #11#15;
 |SI #11#16 ! doce;
     #0#2 = #11#16;
     #0#3 = #11#17;
     #0#6 = "D";
     #0#10 = #11#16;
     #0#11 = #11#17;
 |SINO;
     #0#2 = #11#18;
     #0#3 = #11#19;
     #0#6 = "H";
     #0#8 = #11#18;
     #0#9 = #11#19;
 |FINSI;
 #0#4 = #11#20;
 #0#5 = #11#30;
 #0#7 = #11#21;

 |SALVA_DATOS #0;
 |LEE 001#0=;
 estado = FSalida;
 |REPON_DATOS #0;
 |SI estado ! 0; |GRABA 020#0n; |FINSI;
 |SI estado = 0; |GRABA 020#0a; |FINSI;

 |DDEFECTO #0;
 #0#0 = "A";
 #0#1 = #11#22;
 |SI #11#23 ! doce;
     #0#2 = #11#23;
     #0#3 = #11#24;
     #0#6 = "D";
     #0#10 = #11#23;
     #0#11 = #11#24;
 |SINO;
     #0#2 = #11#25;
     #0#3 = #11#26;
     #0#6 = "H";
     #0#8 = #11#25;
     #0#9 = #11#26;
 |FINSI;
 #0#4 = #11#27;
 #0#5 = #11#31;
 #0#7 = #11#28;
 |SALVA_DATOS #0;
 |LEE 001#0=;
 estado = FSalida;
 |REPON_DATOS #0;
 |SI estado ! 0; |GRABA 020#0n; |FINSI;
 |SI estado = 0; |GRABA 020#0a; |FINSI;
 |CIERRAT #0;
 |HAZ guarda;
|FPRC;

|| ********************************************************************
|PROCESO congruenl;
  sw = 0;
  #11#11 = "N";
  |SI #11#14 ! 0;
      |MENAV "    EL ASIENTO NO ESTA CUADRADO";
  |FINSI;
  a = #11#12;
  |SI #11#10 [ 0; a = -a; |FINSI;
  |SI #11#10 ! a;
      |MENAV "    EL ASIENTO NO COINCIDE CON EL IMPUESTO A DEVENGAR";
  |FINSI;
  #11#11 = "S";
|FPRC;

|| ********************************************************************
|| *********** LINEAS *************************************************
|| ********************************************************************
|PROCESO funcion9;  |TIPO 11;
 salidas = FSalida;
 |SI salidas = 1;
     |HAZ congruenl;
     salidas = 0;
 |FINSI;
 |SI salidas = 9;
     swpanta = 1;
     |HAZ verdatos;
     |ERROR;
 |FINSI;
|FPRC;

|PROCESO saalta; |TIPO 0;
 salidas = FSalida;
 |SI FEntrada = 101; |Y #0#1 ! 1;
     |SI coant ! 0;
         #0#4 = coant;
         #0#5 = deant;
         #0#6 = dhant;
     |FINSI;
     |PINTA #0#4;
     |PINTA #0#5;
 |FINSI;
|FPRC;

|PROCESO pintasaldo; |TIPO 2;
 |HAZ signo;
 |SI #0#6 = "D"; #11#12 = #11#12 +. #0#7; |FINSI;
 |SI #0#6 = "H"; #11#13 = #11#13 +. #0#7; |FINSI;
 #11#14 = #11#12 - #11#13;
 |PINTA #11#12;
 |PINTA #11#13;        || acumulados asiento
 |PINTA #11#14;
|FPRC;

|PROCESO guarda; |TIPO 3;          || desde campo 'linea'
  alfa1 = #0#10;   |QBF alfa1;
  alfa2 = #0#8;    |QBF alfa2;
  |SI alfa1 = "";  |Y alfa2 = "";    ||al menos una cuenta cargada;
      avisos = "ATENCION!!! Apunte sin cuentas ...";
      |HAZ aviso5;
      |PAUSA;
      |HAZ aviso9;
      |ERROR;
      |ACABA;
  |FINSI;
  |SI #0#7 = 0;                       || importe distinto de cero
      avisos = "ATENCION!!! Importe cero ...";
      |HAZ aviso5;
      |PAUSA;
      |HAZ aviso9;
      |ERROR;
      |ACABA;
  |FINSI;
  |SI alfa1 ! "";
      ctdeant = #0#10;
      dideant = #0#11;
  |FINSI;
  |SI alfa2 ! "";
      cthaant = #0#8;
      dihaant = #0#9;
  |FINSI;
  cuant = #0#10;
  diant = #0#11;
  coant = #0#4;
  deant = #0#5;
  dhant = #0#6;
  imant = #0#7;
  trant = #0#8;
  drant = #0#9;
|FPRC;

|PROCESO concepto; |TIPO 0;             || desde campo 'concepto'
 salidas = FSalida;
 |ABRE #4;
 #4#0 = #0#4;
 |LEE 040#4=;
 |SI FEntrada = 101;
     |SI #0#4 = coant; |Y #11#12 ! #11#13;
         #0#5 = deant;
     |SINO;
         #0#5 = #4#1;
    |FINSI;
 |SINO;
    |SI #0#4 = coant;
        #0#5 = deant;
    |SINO;
        #0#5 = #4#1;
    |FINSI;
 |FINSI;
 |PINTA #0#5;
 |CIERRA #4;
|FPRC;

|PROCESO c_descrip; |TIPO 7;
 |PTEC 816;
|FPRC;

|PROCESO descrip; |TIPO 0;
 salidas = FSalida;
  |SI FSalida = 15;
      #0#5 = deant;
      |PINTA #0#5;
  |FINSI;
|FPRC;

|PROCESO cuefin; |TIPO 0;
  |HAZ signo;
  cta_t = Campo;
  dig_t = Campo + 1;
  #1#0 = #0cta_t
  #1#1 = #0dig_t;
  |LEE 001#1=;
  |SI FSalida ! 0;  |ERROR;  |ACABA;  |FINSI;
  |SI #1#3 = "N";
      |MENAV "     La Cuenta NO tiene PASE DIRECTO";
      |ERROR;
      |ACABA;
  |FINSI;
|FPRC;

|PROCESO cuenta; |TIPO 6;                    || desde campo 'cuenta'
  salidas = FSalida;
  |SI salidas = 15;
      |SI Campo = 8;
          #0#8 = cthaant;
          #0#9 = dihaant;
      |FINSI;
      |SI Campo = 10;
          #0#10 = ctdeant;
          #0#11 = dideant;
      |FINSI;
      |PINTA #0Campo;
      salidas = 0;
  |FINSI;
  p_alfa1 = #0Campo;
  salidas = FSalida;
  |HAZ punto3;
  |SI p_error ! 0;  |ERROR;  |ACABA;  |FINSI;
  cta_t = Campo;
  dig_t = cta_t + 1;
  |HAZ cuenta_tl;
|FPRC;

|PROCESO cuenta_tl;
  alfa1 = #0cta_t;
  |QBF alfa1;
  alfa2 = alfa1 % 0;
  long = alfa2;
  cont = 0;
  sw = 0;
  |SI long = dig4;  sw = 1;  #0dig_t = 1;  |FINSI;
  |SI long = dig5;  sw = 1;  #0dig_t = 2;  |FINSI;
  |SI long = dig6;  sw = 1;  #0dig_t = 3;  |FINSI;
  |SI long = dig7;  sw = 1;  #0dig_t = 4;  |FINSI;
  |SI long = dig8;  sw = 1;  #0dig_t = 5;  |FINSI;
  |SI long = dig9;  sw = 1;  #0dig_t = 6;  |FINSI;
  |SI long = 0;     sw = 1;  #0dig_t = 0;  |FINSI;
  |SI sw = 0;
      #0dig_t = 0;
      |MENAV "      Longitud de Cuenta Fuera de Nivel ";
      |ERROR;
      |ACABA;
  |FINSI;
|FPRC;

|PROCESO c_cuenta; |TIPO 7;
  xx = (FEntrada / 100) ! 0;
  |CAMPO_MODIFICA #0#8,  1, "S";
  |CAMPO_MODIFICA #0#10, 1, "S";
 || |SI xx ! 1;              || no modificable cuenta en blanco en no alta
      |SI #0#8 ! doce; |O #0#10 ! doce;
          |SI Campo=8;  |Y #0#10 ! doce; |CAMPO_MODIFICA #0#8, 1,"N"; |FINSI;
          |SI Campo=10; |Y #0#8  ! doce; |CAMPO_MODIFICA #0#10,1,"N"; |FINSI;
      |FINSI;
 ||  |FINSI;
|FPRC;

|PROCESO c_importe; |TIPO 7;
 |HAZ signo;
 xx = (FEntrada / 100) ! 0;
 |SI xx = 1;
     #0#7 = imant;
     |PINTA #0#7;
 |FINSI;
|FPRC;

|PROCESO nocero; |TIPO 0;
 salidas = FSalida;
 |HAZ aviso9;
 |SI #0Campo = 0; |Y salidas ! 11;
     |ERROR;
     |ACABA;
 |SINO;
     |SI salidas = 11;   || <F3>
         |SI #0#6 = "D";  #0#7 = #11#14 * -1;  |FINSI;
         |SI #0#6 = "H";  #0#7 = #11#14;       |FINSI;
         |PINTA #0#7;
     |FINSI;
 |FINSI;
|FPRC;

|PROCESO signo;
 |SI #0#10 ! doce;
     #0#6 = "D";
     #0#2 = #0#10;
     #0#3 = #0#11;
     #0#8 = "";
     #0#9 = 0;
 |FINSI;
 |SI #0#8 ! doce;
     #0#6 = "H";
     #0#2 = #0#8;
     #0#3 = #0#9;
     #0#10 = "";
     #0#11 = 0;
 |FINSI;
|FPRC;


|| ------------------------------------------------------------------------
|| ------------------------------------------------------------------------
|| ------------------------------------------------------------------------
|PROCESO CalcuBene;
     aAlfa1 = #11#0;
     aAlfa1  = aAlfa1 % 103;
     |SI aAlfa1 = "630"; |O aAlfa1 = "633"; |O aAlfa1 = "638";
         || no sumo, resultado antes de impuesto
     |SINO;
         #11#3 = #11#3 + #1#53;
     |FINSI;
|FPRC;

|DEFBUCLE beneficio;
   #1#1;
   3;
   "6           ", 0, "S";
   "7zzzzzzzzzzz", 9, "S";
   e;
   NULL, CalcuBene;
|FIN;


|| ------------------------------------------------------------------------

|PROGRAMA;
  |HAZ comienzo;
  |SI #canempre#26 [ 80;
      enEjercicio = #canempre#26 + 2000;
  |SINO;
      enEjercicio = #canempre#26 + 1900;
  |FINSI;

  |CLS;
  |ABRE #11;
  |CIERRA #11;
  |DELINDEX #11;
  |ABRE #0;
  |CIERRA #0;
  |DELINDEX #0;

  |ABRET #11;
  |CABEZA "Asto. Impuesto SOCIEDADES";
  |PINPA #0#11;

     aAlfa1 = "120.202,41Euros: ";
     |SI eaMoneda ! "E";
         aAlfa1 = "20.000.000Ptas: ";
     |FINSI;
     |SI enEjercicio < 2005;
         aAlfa1 = "90.151,81 Euros:";
         |SI eaMoneda ! "E";
             aAlfa1 = "15.000.00 Ptas: ";
         |FINSI;
     |FINSI;
     |SI enEjercicio ] 2011;
         aAlfa1 = "300.000,00Euros: ";
     |FINSI;

     |SI enEjercicio < 2007;
         #11#7  = 30;
         #11#32 = 35;
     |FINSI;
     |SI enEjercicio ] 2015;
         #11#7  = 25;
         #11#32 = 28;
     |FINSI;

     nTtip1 = #11#7; nTtip2 = #11#32;
     |PINTA 0856, aAlfa1; |PINTA #11#7;
     |PINTA 0956, aAlfa1; |PINTA #11#32;

     |ATRI I;
     |PINTA 2410,"ESTOY CALCULANDO EL RESULTADO CONTABLE";

     #11#29 = "A";
     |LEE 101#11=;
     |SI FSalida ! 0;
         |DDEFECTO #11;
         |GRABA 020#11b;
     |FINSI;
     #11#7  = nTtip1;
     #11#32 = nTtip2;

     #11#3 = 0;
     |BUCLE beneficio;

     |ATRI 0;  |PINTA 2410,"                                      ";

     |SI #11#3 [ 0;
         #11#4 = "BENEFICIO";
    |SINO;
         #11#4 = "PERDIDAS";
     |FINSI;
     #11#3 = -#11#3;

     || Saldo Acreedor = Haber - Debe  positivo

     |PINTA #11#3;
     |PINTA #11#4;
     |ABRET #1;
     |ENDATOS #2#11;
     |SI FSalida ! 0; |O #11#11 = "N";
         |CIERRAT #11;
         |CIERRAT #1;
         |DELINDEX #11;
         |DELINDEX #0;
         |ACABA;
     |FINSI;
     |CIERRAT #11;
     |CIERRAT #1;

     |CONFI "2455SEs correcto (S/N)      ";
     |SI FSalida = 0;
         |HAZ apuntes;
         |HAZ final;
     |FINSI;

     |DELINDEX #11;
     |DELINDEX #0;
|FPRO;

|| ************************************************************************
|| *********** Calculos de Actualizaci¢n de Apunte
|| ************************************************************************
|PROCESO grapuntes;
 |SI #0#7 = 0; |ACABA; |FINSI;
 |SI linea = 0;
     linea = 1;
 |SINO;
     linea = linea + 10;
 |FINSI;

 |DDEFECTO #3;
 #3#0 = diari;
 #3#1 = fecha;
 #3#2 = docum;
 #3#3 = linea;
 |SI #0#10 ! doce;             ||asiento de debe
     #3#4 = #0#10;
     #3#5 = #0#11;
     #3#8 = "D";
     #3#10 = "";
     #3#11 = 0;
     #3#16 = #0#10;
     #3#17 = #0#11;
 |SINO;
     #3#4 = #0#8;
     #3#5 = #0#9;
     #3#8 = "H";
     #3#10 = #0#8;
     #3#11 = #0#9;
     #3#16 = "";
     #3#17 = 0;
 |FINSI;
 #3#6 = #0#4;
 #3#7 = #0#5;
 enNumCon = #3#6; |HAZ VerConcepto; #3#24 = eaDesCon;
 #3#9 = #0#7;
 #3#21 = "";
 |GRABA 020#3n;
 |HAZ cabecera;   || Cabecera Asiento
 |HAZ actualiz1;  || Cuenta Contable
 |HAZ actualiz3;  || Diario

 |HAZ altatra;
|FPRC;

|DEFBUCLE linapun;
 #0#1;
 ;
 "A", 001;
 "A", 999;
 e;
 NULL, grapuntes;
|FIN;

|PROCESO apuntes;
  || .................. Abre fichero Auxiliar de Cuentas
  |ABRE #8;
  |LEE 101#8u;
  |SI FSalida ! 0;
      |DDEFECTO #8;
      #8#0 = 1;
  |SINO;
      #8#0 = #8#0 + 1;
  |FINSI;
  |GRABA 101#8n;
  codcon = #8#0;
  |LIBERA #8;
  || ----------------------------------------------------------
  linea = 0;
  diari = #11#1;
  fecha = #11#0;
  docum = #11#2;
  |ABRE #1;
  |ABRE #2;
  |ABRE #3;
  |ABRE #5;
  |HAZ leecab;
  |BUCLE linapun;
  |CIERRA #1;
  |CIERRA #2;
  |CIERRA #3;
  |CIERRA #5;
  |CIERRA #8;
|FPRC;

|PROCESO final;
  |SI modcon ! 0;
      |SUB_EJECUTA "camaapua.run";
      |HAZ grabadocum;
  |SINO;
      |ABRE #8;
      |DDEFECTO #8;
      #8#0 = codcon;
      |LEE 001#8=;
      |SI FSalida = 0;
          |BORRA 020#8a;
      |FINSI;
      |CIERRA #8;
  |FINSI;
|FPRC;

****************************************************************************
   ACTUALIZACION DE CUENTAS Y CABECERAS DE APUNTES Y LECTURA ULT.ASIENTO
****************************************************************************
|PROCESO leecab;
 linea = 0;
 #2#0 = #11#1;
 #2#1 = #11#0;
 #2#2 = #11#2;
 |LEE 001#2=;
 |SI FSalida = 0;
     #3#0 = #2#0;
     #3#1 = #2#1;
     #3#2 = #2#2;
     #3#3 = 9999;
     |LEE 040#3];
     |SI FSalida = 0;
         |LEE 040#3a;
         |SI #3#0 = #2#0; |Y #3#1 = #2#1; |Y #3#2 = #2#2;
             linea = #3#3;
         |FINSI;
     |SINO;
         |LEE 040#3u;
         |SI FSalida = 0; |Y #3#0 = #2#0; |Y #3#1 = #2#1; |Y #3#2 = #2#2;
             linea = #3#3;
         |FINSI;
     |FINSI;
  |FINSI;
|FPRC;

|PROCESO cabecera;
 #2#00 = #3#0; ||  diario
 #2#01 = #3#1; ||  fecha
 #2#02 = #3#2; ||  documento
 |LEE 101#2=;
 |SI FSalida ! 0;
     |DDEFECTO #2;
     #2#00 = #3#0; ||  diario
     #2#01 = #3#1; ||  fecha
     #2#02 = #3#2; ||  documento
     #2#03 = #3#2; ||  asiento
     |GRABA 020#2b;
 |FINSI;
 |SI #3#08 = "D";
     #2#04 = #2#04 + #3#09;
     |SI #2#09 = 0; #2#08 = #3#4; #2#09 = #3#5; |FINSI;  || contrp.HABER
 |SINO;
     #2#05 = #2#05 + #3#09;
     |SI #2#11 = 0; #2#10 = #3#4; #2#11 = #3#5; |FINSI;  || contrp.DEBE
 |FINSI;
 |GRABA 020#2a;
 |LIBERA #2;
|FPRC;

|PROCESO actualiz1;
  #1#0 = #3#4;
  #1#1 = #3#5;
  |LEE 101#1=;
  |SI  FSalida ! 0;
       #1#0 = #3#4;
       #1#1 = #3#5;
       #1#3 = "S";
       |GRABA 020#1b;
  |FINSI;
  |SI #3#0 = 0;
      |SI #3#8 = "D";
          #1#9 = #1#9 + #3#9;
          #1#51 = #1#51 + #3#9;
      |SINO;
          #1#10 = #1#10 + #3#9;
          #1#52 = #1#52 + #3#9;
      |FINSI;
      #1#11 = #1#9 - #1#10;
      #1#53 = #1#51 - #1#52;
  |SINO;
      |SI #3#0 = 99;
          |SI #3#8 = "D";
              #1#48 = #1#48 + #3#9;
              #1#51 = #1#51 + #3#9;
          |SINO;
              #1#49 = #1#49 + #3#9;
              #1#52 = #1#52 + #3#9;
          |FINSI;
          #1#50 = #1#48 - #1#49;
          #1#53 = #1#51 - #1#52;
      |SINO;
          |HAZ actualiz2;
      |FINSI;
  |FINSI;
  |SI #1#58 < #11#0;
      #1#58 = #11#0;
  |FINSI;

  |GRABA 020#1a;
  |LIBERA #1;
|FPRC;

|PROCESO actualiz2;
 ini = dig1;      || mes inicial
 fecalf = #11#0;
 mes = fecalf % 402;
 mesfec = mes;
 |SI ini = 1;
     mesac = mesfec;
 |SINO;
     mesac = (mesfec - ini) + 1;
     |SI mesac [ 0;
         mesac = (13 - ini) + mesfec;
     |FINSI;
 |FINSI;
 am = (mesac * 3) + 9;
 bm = am + 1;
 cm = am + 2;
 |SI #3#8 = "D";
     #1am = #1am + #3#9;
     #1#51 = #1#51 + #3#9;
 |SINO;
     #1bm = #1bm + #3#9;
     #1#52 = #1#52 + #3#9;
 |FINSI;
 #1cm = #1am - #1bm;
 #1#53 = #1#51 - #1#52;
|FPRC;

|PROCESO actualiz3;
 #5#0 = #11#1;
 |LEE 121#5=;
 |SI FSalida = 0;
     |SI #3#8 = "D";  #5#2 = #5#2 + #3#9;  |FINSI;
     |SI #3#8 = "H";  #5#3 = #5#3 + #3#9;  |FINSI;
     |GRABA 020#5a;
 |FINSI;
 |LIBERA #5;
|FPRC;

**********************************************************************
    ACTUALIZACION DE NIVELES INFERIORES DE CUENTAS
**********************************************************************
|PROCESO altatra;
  modcon = 1;
  #8#0 = codcon;
  #8#1 = #3#4;
  #8#2 = #3#5;
  #8#3 = #3#1;
  #8#4 = #3#8;
  #8#6 = #3#0;
  |LEE 101#8=;
  |SI FSalida ! 0;
      |DDEFECTO #8;
      #8#0 = codcon;
      #8#1 = #3#4;
      #8#2 = #3#5;
      #8#3 = #3#1;
      #8#4 = #3#8;
      #8#6 = #3#0;
      |GRABA 101#8b;
  |FINSI;
  #8#5 = #8#5 + #3#9;
  |GRABA 101#8a;
  |LIBERA #8;
|FPRC;


|| Lee documento y conceptos
|| ***********************************
|PROCESO documento;
 enDiario = #0#1;
 |HAZ contador;
|FPRC;

|PROCESO grabadocum;
     |SI enContador = #11#2;
         enContador = #11#2;
         enDiario   = #0#1;
         |HAZ GrabaContador;
     |FINSI;
|FPRC;

|PROCESO leeconcepto;
 |ABRE #4;
 |DDEFECTO #4;
 #4#0 = concepto;
 |LEE 040#4=;
 |CIERRA #4;
|FPRC;

|| ***********************************************************************
|PROCESO aviso5;         || desde calculo 'c_importe' (camaasil)
|PUSHV 09101171;
|HAZ color_f;
|CUADRO 09 10 11 71;
||            123456789012345678901234567890123456789012345678901234567890
|HAZ color_c;

|QBF avisos;
    alfa1 = avisos % 0;  nume1 = alfa1;
    nume2 = ((60 - nume1) / 2) ! 0;     nume1 = 60 - nume1 - nume2;
    avisos = (" " * nume1) + avisos + (" " * nume2);        || lo centra

|PINTA 1011, avisos;
|ATRI 0;
|AVISO;
|FPRC;

|PROCESO aviso9;         || desde calculo 'descuadre', 'modbaj'
|ATRI 0;
|POPV;
|FPRC;

|PROCESO color_c;        || activa el color caracter en avisos
|COLOR 14, 0;
|ATRI 0;
|FPRC;

|PROCESO color_f;        || activa el color graficos en avisos
|COLOR 4, 0;
|ATRI 0;
|FPRC;
