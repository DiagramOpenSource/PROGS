|FICHEROS;
  cientasc #1;
  cientasl #0;
  cacuenta #2;
  cientcon #3;
  cisalant #4;
  caconcep #5;
|FIN;

|VARIABLES;
  &entrada = 1;
  sw       = 0;
  cta      = "            ";
  ctanum   = 0;
  ctaalf   = " ";
  longalf  = " ";
  longnum  = " ";
  nada = "";
  nivel    = 0;
    &fec1 = @;
    &fec2 = @;
    &cod_emp1 = "";
    &cod_emp2 = "";
    &clave1 = "";
    &clave2 = "";
    &cod1 = 0;
    &cod2 = 0;
    &empresa = "";
    &dir_fich = "";
    &dig1 = 0;
    &dig3 = 0;
    &dig4 = 0;
    &dig5 = 0;
    &dig6 = 0;
    &dig7 = 0;
    &dig8 = 0;
    &dig9 = 0;
    &work1 = "";
    &tra1 = 0;
    &mes1 = "";
    &dia1 = "";
    &codcon = 0;
    &swerror = 0;
    &empre = 0;
    &ejerc = 0;
    salidas = 0;

    elige_i = "";
    que_i1 = "";
    que_i2 = "";
    &fich_alta = "";
    nume1 = 0;
    &total = 0;
    &sumal = 0;
    &difer = 0;
  modo = 0;
  &descrira = "";
  ini = 0;
  fecalf = "";
  mes = "";
  mesfec = 0;
  mesac = 0;
  a = 0;
  b = 0;
  c = 0;
  signo = "";
  elimporte = 0;
  &sigapu = "";
  coant = 0;
  &fecant = @;
  &fecapu = @;
  saliext = 0;
  &ext1 = "";
  &ext2 = "";
 &EURO = 1;
 &EURODB = 0;
 &EURODV = 0;
 &EUROTEXTO = "Cambio de Moneda";
 &swOperacion = 0;  || 0 = Lectura / 1 = Alta
 &enEmpresa = 0;
 &enPeriodo = 0;
 &eaMoneda  = "";
|FIN;

|INCLUYE punto4_i;

|RUTINA menorl;
  #3#0 = #0#0;
  #3#1 = #0#1;
  #3#2 = #0#2;
  #3#3 = 1;
|FRUT;

|RUTINA mayorl;
  #3#0 = #0#0;
  #3#1 = #0#1;
  #3#2 = #0#2;
  #3#3 = 99999;
|FRUT;

|PROCESO hazlasuma;
  sumal = sumal + #3#8;
|FPRC;

|DEFBUCLE hazlasuma;
#3#1;
;
#0#0, #0#1, #0#2, 0;
#0#0, #0#1, #0#2, 99999;
;
NULL,hazlasuma;
|FIN;

|PROCESO contrapartidas;
  |PUSHV 0708 2174;
  total = #0#7;
  sumal = 0;
  |BUCLE hazlasuma;
  difer = total - sumal;
  |SI sumal = 0;
      |ABRE #3;
      #3#0 = #0#0;
      #3#1 = #0#1;
      #3#2 = #0#2;
      #3#3 = 1;
      |ABRE #4;
      #4#0 = #0#0;
      #4#1 = #0#1;
      |LEE 101#4=;
      #3#4 = #4#5;
      #3#5 = #4#6;
      |CIERRA #4;
      #3#6 = #0#10;
      #3#7 = #0#11;
      #3#8 = #0#7;
      |ABRET #2;
      |SI #0#6 = "D";  signo = "H";  |SINO;  signo = "D";  |FINSI;
      elimporte = #3#8;
      |HAZ actualiza;
      |CIERRAT #2;
      |GRABA 101#3n;
      |CIERRA #3;
      sumal = #0#7;
      difer = 0;
  |FINSI;
  |ENTLINEAL #1#3, 4, 1, 20, 1, menorl, mayorl;
  |POPV;
|FPRC;

|RUTINA blanco;
  |ABRET #2;
  #2#0 = #0#8;
  #2#1 = #0#9;
  |LEE 001#2=;
  |SI FSalida ! 0;
      |DDEFECTO #2;
      #2#0 = #0#8;
      #2#1 = #0#9;
      #2#3 = "S";       || pase directo si
      |PARA nume1 = 1; |SI nume1 [ dig3; |HACIENDO nume1 = nume1 + 1;
           |SI nume1 = 1;  #2#1 = dig4;  |FINSI;
           |SI nume1 = 2;  #2#1 = dig5;  |FINSI;
           |SI nume1 = 3;  #2#1 = dig6;  |FINSI;
           |SI nume1 = 4;  #2#1 = dig7;  |FINSI;
           |SI nume1 = 5;  #2#1 = dig8;  |FINSI;
           |SI nume1 = 6;  #2#1 = dig9;  |FINSI;
           |GRABA 040#2n;
      |SIGUE;
      #2#1 = 0;
      |GRABA 040#2n;        || graba con el nivel 0 (por si acaso)
  |FINSI;
  |CIERRAT #2;
|FRUT;

|PROCESO elnivelin;  |TIPO 6;
  salidas = FSalida;
  saliext = FSalida;
  p_alfa1 = #0Campo;
  |HAZ punto4;
  |SI p_error ! 0;  |ERROR;  |ACABA;  |FINSI;
  cta = #0Campo;
  |QBF cta;
  |SI cta = "";
      |HAZ blanco;
      |HAZ contrapartidas;
      |ACABA;
  |FINSI;
  nada = cta % A101;
  |SI nada ! "0";
      ctanum = cta;
      ctaalf = ctanum;
      longnum = ctaalf % A0;
  |SINO;
      ctanum = 1;
      ctaalf = cta;
      longnum = ctaalf % A0;
  |FINSI;
  longalf = cta    % A0;
  |SI longnum ! longalf;|O ctanum = 0;
      |MENAV "     CUENTA ERRONEA !!";
      |PINTA #0#8;
      |ERROR;
      |ACABA;
  |FINSI;
  nivel= 0;
  sw = 0;
  |SI longnum = dig4; sw = 1; nivel = 1; |FINSI;
  |SI longnum = dig5; sw = 1; nivel = 2; |FINSI;
  |SI longnum = dig6; sw = 1; nivel = 3; |FINSI;
  |SI longnum = dig7; sw = 1; nivel = 4; |FINSI;
  |SI longnum = dig8; sw = 1; nivel = 5; |FINSI;
  |SI longnum = dig9; sw = 1; nivel = 6; |FINSI;
  |SI sw = 0;
      |MENAV "     LONGITUD DE CUENTA FUERA DE NIVEL !";
      |PINTA #0#8;
      |ERROR;
      |ACABA;
  |FINSI;
  #0#9 = nivel;
  |SI saliext = 9;
      ext1 = #0#8;
      ext2 = #0#9;
      |SUB_EJECUTA "caextrap"
  |FINSI;
|FPRC;

|PROCESO elniveli0;  |TIPO 0;
  |ABRET #2;
  #2#0 = #0#8;
  #2#1 = #0#9;
  |LEE 101#2=;
  |CIERRAT #2;
  |SI #2#3 ! "S";
      |MENAV "     LA CUENTA NO ES DE PASE DIRECTO";
      |ERROR;
      |ACABA;
  |FINSI;
  |ABRET #2;
  #2#0 = #0#8;
  #2#1 = #0#9;
  |LEE 001#2=;
  |SI FSalida ! 0;  |DDEFECTO #2;  |FINSI;
  |CIERRAT #2;
|FPRC;

|PROCESO quemodifi;
  |SI #0#8 = "            ";
      |CAMPO_MODIFICA #0#9,  1, "N";
      |CAMPO_MODIFICA #0#10, 1, "N";
      |CAMPO_MODIFICA #0#11, 1, "N";
  |SINO;
      |CAMPO_MODIFICA #0#9,  1, "S";
      |CAMPO_MODIFICA #0#10, 1, "S";
      |CAMPO_MODIFICA #0#11, 1, "S";
  |FINSI;
|FPRC;

|PROCESO borracontra;
  elimporte = -#3#8;
  |SI #0#6 = "D";  signo = "H";  |SINO;  signo = "D";  |FINSI;
  |HAZ actualiza;
  |BORRA 101#3a;
|FPRC;

|DEFBUCLE borracontra;
#3#1;
;
#0#0, #0#1, #0#2, 1;
#0#0, #0#1, #0#2, 99999;
;
NULL,borracontra;
|FIN;

|PROCESO noescero;
  |SI #0#8 ! "            ";
      |ABRET #2;
      |BUCLE borracontra;
      |CIERRAT #2;
  |FINSI;
|FPRC;

|PROCESO esbaja;  |TIPO 1;
  modo = ((FEntrada / 100) ! 0);
  |SI modo = 3;
      |ABRET #2;
      |BUCLE borracontra;
      |CIERRA #2;
  |FINSI;
|FPRC;

|PROCESO duplico;
  |SI FSalida = 15;
      #0#11 = descrira;
      |PINTA #0#11;
  |FINSI;
|FPRC;

|PROCESO otroconce;
  modo = ((FEntrada / 100) ! 0);
  |SI modo = 1;  |Y #0#10 = #0#4;
      #0#11 = descrira;
      |PINTA #0#11;
  |FINSI;
  |SI #0#10 ! #0#4;
      |ABRE #5;
      #5#0 = #0#10;
      |LEE 101#5=;
      #0#11 = #5#1;
      |PINTA #0#11;
      |CIERRA #5;
  |FINSI;
|FPRC;

|RUTINA actualmes;
  ini = dig1;
  fecalf = #0#3;
  mes = fecalf % 402;
  mesfec = mes;
  |SI ini = 1;
      mesac = mesfec;
  |SINO;
      mesac = (mesfec - ini) + 1;
      |SI mesac [ 0;
          mesac = (13 - ini) + mesfec;
      |FINSI;
  |FINSI;
  a = (mesac * 3) + 9;
  b = a + 1;
  c = a + 2;
  |SI signo = "D";
      #2a = #2a + elimporte;
      #2#51 = #2#51 + elimporte;
  |SINO;
      #2b = #2b + elimporte;
      #2#52 = #2#52 + elimporte;
  |FINSI;
  #2c = #2a - #2b;
  #2#53 = #2#51 - #2#52;
|FRUT;

|RUTINA actualiza;
  #2#0 = #3#4;
  #2#1 = #3#5;
  |LEE 101#2=;
  |SI FSalida = 0;
      |HAZ actualmes;
      |SI #2#58 < #0#3;  #2#58 = #0#3;  |FINSI;
      |GRABA 020#2a;
  |FINSI;
  |LIBERA #2;
|FRUT;

|PROCESO alfin;  |TIPO 7;
  |PTEC 816;
|FPRC;

|PROCESO concepto; |TIPO 0;
 modo = ((FEntrada / 100) ! 0);
 |ABRE #5;
 #5#0 = #0#4;
 |LEE 040#5=;
 |SI modo = 1;
     #0#5 = #5#1;
     #0#10 = #0#4; |PINTA #0#10;
 |SINO;
    |SI #0#6 ! coant;
        #0#7 = #5#1;
    |FINSI;
 |FINSI;
 |PINTA #0#7;
 |CIERRA #5;
 coant = #0#6;
|FPRC;

|PROCESO descripcion; |TIPO 0;
  |SI FSalida = 15;
      #0#5 = descrira;
      |PINTA #0#5;
  |FINSI;

  modo = ((FEntrada / 100) ! 0);
  |SI modo = 1;
      #0#11 = #0#5;
      |PINTA #0#11;
  |FINSI;

  descrira = #0#5;
|FPRC;

|PROCESO debehaber;
  #0Campo = #0Campo > " ";
  |SI #0Campo = "0";  #0Campo = "D";  |FINSI;
  |SI #0Campo = "1";  #0Campo = "H";  |FINSI;
  |PINTA #0Campo;
  sigapu = #0Campo;
|FPRC;

|PROCESO importe;
  |SI #0Campo = 0;
      |MENAV  "    ATENCION IMPORTE CERO";
      |ERROR;
      |ACABA;
  |FINSI;
|FPRC;

|PROCESO lafecha; |TIPO 0;
  fecapu = #0#3;
  |SI #0#3 < fec1;  |O #0#3 > fec2;
      |MENAV "     LA FECHA NO ES DEL EJERCICIO CONTABLE";
      |ERROR;
      |ACABA;
  |FINSI;
  |SI #0#3 < #1#3;
      |MENAV "     FECHA YA ACTUALIZADA";
      |ERROR;
      |ACABA;
  |FINSI;
  fecant = #0#3;
|FPRC;
