|FICHEROS;
    imppluxe #0;

    caconcep #11;
    caclipro #12;   || clientes/proveedores
    caivarep #13;   || iva repercutido
    calibros #14;   || Libros Iva
    caivasop #15;   || iva soportado

    camandia #20;   || diarios AGICONT
    camaasic #22;   || asientos cabs. AGICONT
    camaasil #23;   || asientos lins. AGICONT
    cacuenta #24;   || cuentas AGICONT
    caprovin #25;   || Provincias
    catradia #27;   || diario de trabajo
    cacuebal #28;   || cuentas de balance

    imptlux1 #40;   || def trabajo importacion Asientos
    imptlux2 #41;   || def trabajo importacion Facturas
    imppeur3 #42;   || def trabajo Cuentas
    imptlux3 #43;   || Conceptos
    ca8m0002 #422;
|FIN;

|VARIABLES;
    &modcon = 0;

    alfa1 = "";
    alfa2 = "";
    alfa3 = "";
    alfa4 = "";
    alfa5 = "";
    alfa6 = "";
    alfa7 = "";
    x     = 0;
    nume1 = 0;
    nume2 = 0;
    nume3 = 0;
    nume4 = 0;
    nume5 = 0;
    para1 = 0;
    para2 = 0;
    para3 = 0;
    para4 = 0;

 desl_rep = "";
 desl_sop = "";
    estado  = 0;
    signe   = "";
    conte   = "";
    digcu   = 0;
    descli  = "";
    hascli  = "";
    nifcli  = "";
    numfac  = 0;
    numfac1 = "";
    numfac2 = "";
    tipof   = "";
    diario  = 0;
    docume  = 0;
    fecha   = @;
    linea   = 0;

    rlong   = 0;
    rtipo   = 0;
    afecha  = "";
    anume   = "";
    importe = 0;
    elmenos = "";

    ncfac   = 0;
    descrip = "";
    ordant  = 0;
    fecant  = "";
    nocta   = 0;
    swfac   = 0;
    swbas   = 0;
    swiva   = 0;
    asient  = 0;
    sw      = 0;
    trim1   = "";
    trim2   = "";
    trim3   = "";

    ini = 0;
    fecalf = "";
    mes = "";
    mesfec = 0;
    mesac = 0;
    a = 0;
    b = 0;
    c = 0;

    lon = 0;
    aa = 0;
    mensa = "";
    aar = "";
    traba = "";
    errcuent = "aaaaaaaaaaaa";

    fabre = "";
    fcanl = "";
    field = "";

    guarda_pos = 0;
    RegAstos = 0;
    RegFact  = 0;
    sw_fecha = 0;

     CampoDni   = "";
     Letra1     = "";
     Letra2     = "";
     Letra3     = "";
     Letra4     = "";
     Letra5     = "";
     Letra6     = "";
     Letra7     = "";
     Letra8     = "";
     Caracter   = "";
     Carac1     = 0;
     Carac2     = 0;
     Carac3     = 0;
     Carac4     = 0;
     Carac5     = 0;
     XPara      = 0;
     titulo     = "";

    &eaVarDni  = "";
    &enCalcCif = 0;
|FIN;

|INCLUYE acceso_i;
|INCLUYE tempo_i;
|INCLUYE varnue_i;
|INCLUYE defec1_i;

|| *************************************************************************
|| ********************* CALCULOS PANTALLA
|| *************************************************************************

|PROCESO mayus; |TIPO 0;
 #0Campo = #0Campo > #0Campo;
 |PINTA #0Campo;
 |SI Campo = 11; |ACABA; |FINSI;

 alfa1 = #0Campo;
 |SI Campo = 0; |C_M #0#4,1, alfa1; |FINSI;
 |SI Campo = 1; |C_M #0#5,1, alfa1;  |C_M #0#8, 1,alfa1; |FINSI;
 |SI Campo = 2;
     |C_M #0#6, 1, alfa1;
     |C_M #0#9, 1, alfa1;
     |C_M #0#13,1, alfa1;
     |C_M #0#14, 1,alfa1;
     |C_M #0#17,1, alfa1;
 |FINSI;
 |SI Campo = 3;
     |C_M #0#7, 1, alfa1;
     |C_M #0#10,1, alfa1;
     |C_M #0#15,1, alfa1;
     |C_M #0#16,1, alfa1;
     |C_M #0#18,1, alfa1;
 |FINSI;
|FPRC;

|PROCESO libro; |TIPO 0;
 |ABRE #14;
 #14#0 = #0Campo;
 |LEE 001#14=;
 |SI FSalida ! 0;
     |MENAV "0000Atencion.! NO EXISTE LIBRO DE IVA";
     |CIERRA #14;
     |ERROR;
     |ACABA;
 |FINSI;
 |SI Campo = 9;
     |SI #14#2 ! "R";
         |MENAV "0000Atencion.! Libro de IVA Soportado ... ";
         |ERROR;
     |SINO;
         desl_rep = #14#1;
         |ATRI I; |PINTA 1527, #14#1; |ATRI 0;
     |FINSI;
 |SINO;
     |SI #14#2 ! "S";
         |MENAV "0000Atencion.! Libro de IVA Repercutido ... ";
         |ERROR;
     |SINO;
         desl_sop = #14#1;
         |ATRI I; |PINTA 1827, #14#1; |ATRI 0;
     |FINSI;
 |FINSI;
 |CIERRA #14;
|FPRC;
|| ************************************************************************
|| *************************************************************************

|PROGRAMA;
 |CLS;
 |CABEZA "Enlace LUXE";
 |PINPA #0#0;
 |HAZ inicio;
 |HAZ eVarEmpresa;
 |DDEFECTO #0;
 |ABRE #0;
 |LEE 101#0p;
 |SI FSalida ! 0; |GRABA 020#0b; |FINSI;
 |PINDA #0#0;
 |ENDATOS #1#0;
 |SI FSalida = 0;
     |SI #0#11 = "SI"; |HAZ tipo3; |FINSI;
     #0#11 = "NO";
     |GRABA 020#0a;
 |FINSI;
 |LIBERA #0;
 |CIERRA #0;

 || ............... Terminar
 |SI modcon = 0;
     |ABRET #27;
     |DDEFECTO #27;
     #27#0 = codcon;
     |LEE 040#27=;
     |BORRA 101#27a;
     |CIERRAT #27;
 |SINO;
     |CORRE "camaapua.tab"
 |FINSI;
|FPRO;

|| **************************************************************************
|| ...................... Repercutido
|PROCESO fra_rep;
 |DDEFECTO #13;
 #13#0  = #0#9;      || libro
 #13#2  = numfac;    || nro. fra.
 #13#27 = "  ";      || serie (inhibida)
 |LEE 101#13=;
 estado = FSalida;
 |SI estado = 0;
     |AVISO;
     alfa1 = "    La factura [" + #13#0 + "-" + #13#2 + "] YA existe ...";
     |MENSA alfa1;
     |CONFI "    NRemplazarla (S/N): ";
     |SI FSalida = 0;
         |HAZ gra_factura_r;
     |SINO;
         |MENAV "    Deberia revisar el Libro de IVA Repercutido ...";
         |DDEFECTO #13;
     |FINSI;
 |SINO;
     |HAZ gra_factura_r;
 |FINSI;
 |LIBERA #13;
|FPRC;

|PROCESO gra_factura_r;
 #13#1 = desl_rep;
 #13#3 = #41#4;          || Fecha
 #13#4 = conte;          || Cuenta
 #13#5 = digcu;          || digito cuenta
 #13#6 = #0#13;          || Concepto
 descrip = #0#14; |QBF descrip;
 |SI #0#17 = "S";
     alfa1 = #41#1; alfa1 = ("000000" + alfa1) % -106;
     descrip = descrip + " " + alfa1;
 |FINSI;
 #13#7 = descrip;        || Descripcion

 |SI #41#6 ! 0; #13#26 = #41#6; |FINSI;
 |HAZ rep_bases;
 |HAZ periodo;
 #13#50 = #24#2;
 #13#51 = nifcli;
 |SI estado ! 0; |GRABA 020#13n; |FINSI;
 |SI estado = 0; |GRABA 020#13a; |FINSI;
 |BORRA 020#41a;
 |LIBERA #41;
|FPRC;

|PROCESO rep_bases;
 |SI #41#13 ! 0; |O #41#15 ! 0; |O #41#17 ! 0;
     |SI swbas < 4;
         nume1 = 8 + swbas;  x = 3;
     |SINO;
         nume1 = 48 + swbas; x = 2;
     |FINSI;
     nume2 = nume1 + x; nume3 = nume2 + x;
     nume4 = nume3 + x; nume5 = nume4 + x;
     swbas = swbas + 1;
     #13nume1 = #41#13;                    || Base
     #13nume2 = #41#14; #13nume3 = #41#15; || IVA
     #13nume4 = #41#16; #13nume5 = #41#17; || Recargo
 |FINSI;
|FPRC;

|PROCESO fra_rep2;
 #13#0  = #0#9;      || libro
 #13#2  = numfac;    || nro. fra.
 #13#27 = "  ";      || serie (inhibida)
 |LEE 101#13=;
 estado = FSalida;
 |SI estado = 0;
     |HAZ rep_bases;
     |GRABA 020#13a;
 |SINO;
     |HAZ gra_factura_r;
 |FINSI;
 |LIBERA #13;
 |BORRA 020#41a;
 |LIBERA #41;
|FPRC;

|| ...................... Soportado
|PROCESO fra_sop;
 |DDEFECTO #15;
 #15#0  = #0#10;      || libro
 #15#2  = numfac;    || nro. fra.
 #15#27 = "  ";      || serie (inhibida)
 |LEE 101#15=;
 estado = FSalida;
 |SI estado = 0;
     |AVISO;
     alfa1 = "    La factura [" + #15#0 + "-" + #15#2 + "] YA existe ...";
     |MENSA alfa1;
     |CONFI "    NRemplazarla (S/N): ";
     |SI FSalida = 0;
         |HAZ gra_factura_s;
     |SINO;
         |MENAV "    Deberia revisar el Libro de IVA Soportado ...";
         |DDEFECTO #15;
     |FINSI;
 |SINO;
     |HAZ gra_factura_s;
 |FINSI;
 |LIBERA #15;
|FPRC;

|PROCESO gra_factura_s;
 #15#1 = desl_sop;
 #15#3 = #41#4;          || Fecha
 #15#4 = conte;          || Cuenta
 #15#5 = digcu;          || digito cuenta
 #15#6 = #0#15;          || Concepto
 descrip = #0#16; |QBF descrip;
 |SI #0#18 = "S";
     descrip = descrip + " " + #41#5;
 |FINSI;
 #15#7 = descrip;        || Descripcion

 |SI #41#6 ! 0; #15#26 = #41#6; |FINSI;
 |HAZ sop_bases;
 |HAZ periodo_s;
 #15#50 = #24#2;
 #15#51 = nifcli;
 |SI estado ! 0; |GRABA 020#15n; |FINSI;
 |SI estado = 0; |GRABA 020#15a; |FINSI;
 |BORRA 020#41a;
 |LIBERA #41;
|FPRC;

|PROCESO sop_bases;
 |SI #41#13 ! 0; |O #41#15 ! 0; |O #41#17 ! 0;
     |SI swbas < 4;
         nume1 = 8 + swbas; x = 3;
     |SINO;
         nume1 = 48 + swbas; x = 2;
     |FINSI;
     nume2 = nume1 + x; nume3 = nume2 + x;
     nume4 = nume3 + x; nume5 = nume4 + x;
     swbas = swbas + 1;
     #15nume1 = #41#13;                    || Base
     #15nume2 = #41#14; #15nume3 = #41#15; || IVA
     #15nume4 = #41#16; #15nume5 = #41#17; || Recargo
 |FINSI;
|FPRC;

|PROCESO fra_sop2;
 #15#0  = #0#10;     || libro
 #15#2  = numfac;    || nro. fra.
 #15#27 = "  ";      || serie (inhibida)
 |LEE 101#15=;
 estado = FSalida;
 |SI estado = 0;
     |HAZ sop_bases;
     |GRABA 020#15a;
 |SINO;
     |HAZ gra_factura_s;
 |FINSI;
 |LIBERA #15;
 |BORRA 020#41a;
 |LIBERA #41;
|FPRC;

|| --------------------------------------------------------------------
|PROCESO encuentra;
 alfa1 = #24#2; |QBF alfa1;
 alfa2 = #41#3; |QBF alfa2;
 alfa3 = alfa1 - alfa2;
 |SI alfa3 ! alfa1;
     alfa5 = #24#0 % 103;
     |SI tipof = "V";
         |SI alfa5 = "430"; |O alfa5 = "440";
             alfa5 = "C";
             conte = #24#0;
             digcu = #24#1;
             swfac = 1;
             |ERROR10;
         |FINSI;
      |SINO;
         |SI alfa5 = "400"; |O alfa5 = "410"; |O alfa5 = "514";
                            |O alfa5 = "515"; |O alfa5 = "523";
             alfa5 = "P";
             conte = #24#0;
             digcu = #24#1;
             swfac = 1;
             |ERROR10;
         |FINSI;
      |FINSI;
 |FINSI;
|FPRC;

|DEFBUCLE buscli;
 #24#2;
 3;
 descli, "S";
 hascli, "S";
 e;
 NULL, encuentra;
|FIN;

|PROCESO cuen_clpr;
 swfac  = 0;
 nifcli = "";
 descli = #41#3;
 hascli = #41#3; |QBF hascli;
 hascli = (hascli + ("z" * 36) ) % 136;
 |BUCLE buscli;

 |SI swfac = 1;
     #12#23 = alfa5;
     #12#10 = conte;
     #12#11 = digcu;
     |LEE 000#12=;
     |SI FSalida = 0;
         nifcli = #12#9;
     |FINSI;
 |SINO;
     conte = "000000000000" + #41#2;
     digcu = dig3;
     |SI dig3 = 1; lon = dig4; |FINSI;
     |SI dig3 = 2; lon = dig5; |FINSI;
     |SI dig3 = 3; lon = dig6; |FINSI;
     |SI dig3 = 4; lon = dig7; |FINSI;
     |SI dig3 = 5; lon = dig8; |FINSI;
     |SI dig3 = 6; lon = dig9; |FINSI;
     nume1 = 100 + (lon - 2); nume1 = - nume1;
     conte = conte % nume1;
     |SI tipof = "V"; conte = "43" + conte; |FINSI;
     |SI tipof = "C"; conte = "40" + conte; |FINSI;
     #42#0  = conte;    || Cuenta Contable
     #42#2  = #41#3;    || Titulo Cuenta
     #42#6  = "";       || Direccion/DNI
     |HAZ altacuenta;
 |FINSI;
|FPRC;

|| --------------------------------------------------------------------
|PROCESO primera;
  numfac = #41#1;
  |HAZ cuen_clpr;
  swbas = 0;
  |SI tipof  = "C";
      |HAZ fra_sop;
  |SINO;
      |HAZ fra_rep;
  |FINSI;
|FPRC;

|PROCESO otrabase; || Segunda Linea
  |SI #41#13 = 0; |Y #41#15 = 0; |Y #41#17 = 0;  || Es descuento
      |ACABA;
  |FINSI;
  |SI tipof  = "C";
      |HAZ fra_sop2;
  |SINO;
      |HAZ fra_rep2;
  |FINSI;
|FPRC;

|PROCESO hacer2;
 |PINTA 2230, #41#1;
 |PINTA 2237, #41#4;
 |SI sw = 0; |O numfac ! #41#1;
     |HAZ primera;
     sw = 1;
 |SINO;
     |HAZ otrabase;
 |FINSI;
|FPRC;

|DEFBUCLE luxe2;
 #41#2;
 ;
 000001, 000001;
 999999, 999999;
 e;
 NULL, hacer2;
|FIN;

|PROCESO pase_facturas;
 |ABRE #12;     || Clientes/Proveedores
 |SI tipof = "V"; |ABRE #13; |FINSI;    || IVA Repercutido
 |SI tipof = "C"; |ABRE #15; |FINSI;    || IVA Soportado
 |ABRET #24;     || Plan Contable

 sw = 0;
 |BUCLE luxe2;

 |CIERRA #12;
 |SI tipof = "V"; |CIERRA #13; |FINSI;    || IVA Repercutido
 |SI tipof = "C"; |CIERRA #15; |FINSI;    || IVA Soportado
 |CIERRAT #24;

 |DELINDEX #41;
|FPRC;

||**************************************************************************
|PROCESO pase_auxiliar2;
 rtipo = 0;
 alfa1 = field % 0101;
 |SI alfa1 < "0"; |O alfa1 > "9";
     |SI alfa1 ! " ";  |ACABA; |FINSI;
     alfa1 = field;
     |QBF alfa1;
     |SI alfa1 = ""; |ACABA; |FINSI;
     alfa1 = field;
     |QBT alfa1; alfa1 = alfa1 % 105;
     |SI alfa1 = "TOTAL"; |ACABA; |FINSI;
     rtipo = 1;
     alfa2 = alfa1 % 0;
     rlong = alfa2;
 |FINSI;

 |SI rtipo = 0;
     |DDEFECTO #41;
     alfa1  = field % 0106;
     #41#1  = alfa1;             || Codigo
     alfa1  = field % 0805;
     #41#2  = alfa1;             || Cliente
     #41#3  = field % 1930;      || Nombre Cliente/Proveedor
     afecha = field % 8108;      || Fecha Registro DD/MM/AA
     |HAZ fecha;
     #41#4 = afecha;           || Fecha dd.mm.aaaa
 |SINO;
     |PDEFECTO #41, 5,18;
 |FINSI;

 RegFact  = RegFact + 1;
 #41#0    = RegFact;

 |SI tipof = "V"; |HAZ auxiliar_v; |FINSI;
 |SI tipof = "C"; |HAZ auxiliar_c; |FINSI;

 |PINTA 2230, #41#1;
 |PINTA 2237, #41#4;

 |GRABA 020#41n;
|FPRC;

|PROCESO auxiliar_v;
 anume = field % 9012;    || Total
 |HAZ importe;
 #41#6 = importe;
 #41#7 = field % 10301;   || Signo

 anume = field % 10512;   || Neto
 |HAZ importe;
 #41#8 = importe;

 #41#9 = field % 11810;   || Cuenta Contable
 conte = #41#9;
 |HAZ cuentas;
 #41#10 = digcu;          || Digito Contable

 |SI rtipo = 0; |O rlong ] 129;
     alfa1  = field % 12903;  || Tipo
     #41#11 = alfa1;
 |FINSI;

 |SI rtipo = 0; |O rlong ] 133;
     #41#12 = field % 13315;  || Descripcion
 |FINSI;

 |SI rtipo = 0; |O rlong ] 149;
     anume  = field % 14912;  || Base
     |HAZ importe;
     #41#13 = importe;
 |FINSI;

 |SI rtipo = 0; |O rlong ] 162;
     anume = field % 16205;   || % IVA
     |HAZ importe;
     #41#14 = importe;
     anume  = field % 16812;  || Cuota IVA
     |HAZ importe;
     #41#15 = importe;
 |FINSI;

 |SI rtipo = 0; |O rlong ] 181;
     anume  = field % 18105;   || % Recargo
     |HAZ importe;
     #41#16 = importe;
     anume  = field % 18712;  || Cuota Recargo
     |HAZ importe;
     #41#17 = importe;
 |FINSI;
|FPRC;

|PROCESO auxiliar_c;
 #41#5 = field % 9015;    || Documento

 anume = field % 11412;   || Total
 |HAZ importe;
 #41#6 = importe;
 #41#7 = field % 12701;   || Signo

 anume = field % 12912;   || Neto
 |HAZ importe;
 #41#8 = importe;

 #41#9 = field % 14210;   || Cuenta Contable
 conte = #41#9;
 |HAZ cuentas;
 #41#10 = digcu;          || Digito Contable

 |SI rtipo = 0; |O rlong ] 153;
     alfa1  = field % 15303;  || Tipo
     #41#11 = alfa1;
 |FINSI;

 |SI rtipo = 0; |O rlong ] 157;
     #41#12 = field % 15715;  || Descripcion
 |FINSI;

 |SI rtipo = 0; |O rlong ] 173;
     anume  = field % 17312;  || Base
     |HAZ importe;
     #41#13 = importe;
 |FINSI;

 |SI rtipo = 0; |O rlong ] 186;
     anume = field % 18605;   || % IVA
     |HAZ importe;
     #41#14 = importe;
     anume  = field % 19212;  || Cuota IVA
     |HAZ importe;
     #41#15 = importe;
 |FINSI;
|FPRC;
|| *****************************************************************
|PROCESO nueva;
 fecha = #40#1;       || Fecha
 |SI fecha < fec1; |O fecha > fec2; sw_fecha = 1; |FINSI;
 docume = #40#2;      || Documento
 asient = docume;     || Asiento
 |HAZ cabecera;

 |PINTA 2230, #22#0;
 |PINTA 2233, #22#1;
 |PINTA 2244, #22#2;
|FPRC;

|PROCESO hacer;
 |SI sw = 0;
     |HAZ nueva;
     fecant = #40#1;
     ordant = #40#2;
     sw = 1;
 |FINSI;
 |SI ordant ! #40#2; |O fecant ! #40#1;
     |HAZ nueva;
     fecant = #40#1;
     ordant = #40#2;
 |FINSI;

 conte  = #40#3;
 digcu  = #40#4;
 titulo = #40#5;
 signe  = #40#7;

 |DDEFECTO #23;
 #23#0 = diario;
 #23#1 = fecha;
 #23#2 = docume;
 linea = linea + 10;
 #23#3 = linea;
 #23#4 = conte;
 #23#5 = digcu;
 #23#6 = #40#8; |SI #23#6 = 0; #23#6 = 1; |FINSI;
 #23#7 = #40#9;
 #23#8 = signe;
 #23#9 = #40#6;
 |HAZ adapta;
 |HAZ a_diario;
 |HAZ a_cuenta;

 |PINTA 2251, #23#3;

 |GRABA 020#23n;
 |HAZ gratra;
 |HAZ act_cabecera;

 |BORRA 020#40a;
|FPRC;

|DEFBUCLE luxe1;
 #40#2;
 ;
 "01.01.1900", 000001;
 "31.12.2090", 999999;
 e;
 NULL, hacer;
|FIN;

|| --------------------------------------------------------------------
|PROCESO hacer3;
 #11#0 = #43#0;
 |LEE 001#11=;
 |SI FSalida ! 0;
     #11#0 = #43#0;
     #11#1 = #43#1;
     |GRABA 020#11n;
 |FINSI;
|FPRC;

|DEFBUCLE luxe3;
 #43#1;
 ;
 001;
 999;
 e;
 NULL, hacer3;
|FIN;
|| -------------------------------------------
|PROCESO pase_asientos;
 |ABRE #20;     || Diarios
 |ABRE #22;     || Apuntes Cabs.
 |ABRE #23;     || Apuntes Lins
 |ABRE #24;     || Plan Contable
 sw = 0;
|| .................. Abre fichero Auxiliar de Cuentas
 |ABRE #27;
 |LEE 101#27u;
 |SI FSalida ! 0;
     |DDEFECTO #27;
     #27#0 = 1;
 |SINO;
     #27#0 = #27#0 + 1;
 |FINSI;
 |GRABA 101#27n;
 codcon = #27#0;
 |LIBERA #27;

 diario = #0#8;
 |BUCLE luxe1;

 |CIERRA #20;
 |CIERRA #22;
 |CIERRA #23;
 |CIERRA #24;
 |CIERRA #27;

 |ABRE #11;
 |BUCLE luxe3;
 |CIERRA #11;

 |DELINDEX #40;
 |DELINDEX #43;
|FPRC;

||**************************************************************************
|PROCESO pase_auxiliar1;
 alfa1 = field % 0101;
 |SI alfa1 < "0"; |O alfa1 > "9"; |ACABA; |FINSI;  || No es Apunte

 |DDEFECTO #40;
 RegAstos = RegAstos + 1;
 #40#0 = RegAstos;

 afecha = field % 0108;    || Fecha Contable DD/MM/AA
 |HAZ fecha;
 #40#1 = afecha;           || Fecha dd.mm.aaaa

 alfa1 = field % 1105;    || Numero Asiento
 #40#2 = alfa1;           || Numero Asiento
 #40#3 = field % 1810;    || Cuenta Contable

 conte = #40#3;
 |HAZ cuentas;
 #40#4 = digcu;           || Digito Contable
 #40#5 = field % 2930;    || Titulo Cuenta

 anume = field % 6012;    || Importe
 |HAZ importe;
 #40#6 = importe;

 #40#7 = field % 7401;    || Signo

 alfa1 = field % 7703;
 #40#8 = alfa1;           || Concepto Contable
 #40#9 = field % 8130;    || Descripcion
 |HAZ concepto;

 |PINTA 2230, #40#2;
 |PINTA 2237, #40#1;

 |GRABA 020#40n;
|FPRC;

|PROCESO concepto;
 |ABRE #11;
 #11#0 = #40#8;
 |LEE 001#11=;
 |SI FSalida = 0;
     |CIERRA #11;
     |ACABA;
 |FINSI;
 |CIERRA #11;
 #43#0 = #40#8;
 |LEE 001#43=;
 |SI FSalida ! 0;
     #43#1 = #40#9;
     |GRABA 020#43n;
 |SINO;
     alfa3 = #40#9; |QBF alfa3;
     alfa2 = #43#1; |QBF alfa2;
     alfa4 = alfa2 % 0;
     nume4 = alfa4;
     alfa7 = "";
     alfa6 = "";
     alfa5 = "";
     |PARA para1 = 1; |SI para1 [ nume4; |HACIENDO para1 = para1 + 1;
           nume3 = (para1 * 100) + 1;
           alfa4 = alfa2 % nume3;
           |SI alfa4 ! " ";
               alfa5 = alfa5 + alfa4;
           |SINO;
               alfa6 = alfa3 - alfa5;
               |SI alfa6 ! alfa3;
                    alfa7 = alfa7 + alfa5 + " ";
               |FINSI;
               alfa5 = "";
           |FINSI;
      |SIGUE;
      |SI alfa5 ! "";
          alfa6 = alfa3 - alfa5;
          |SI alfa6 ! alfa3;
               alfa7 = alfa7 + alfa5 + " ";
          |FINSI;
          alfa5 = "";
      |FINSI;
      |SI alfa7 ! ""; #43#1 = alfa7; |FINSI;
      |GRABA 020#43a;
 |FINSI;
|FPRC;

|| *****************************************************************
|PROCESO altacuenta;
 guarda_pos = Pos;
 Pos = -1;

 digcu = 0;
 alfa1 = #42#0; |QBF alfa1;
 alfa2 = alfa1 % 0;
 nume1 = alfa2;
 |SI nume1 = dig4;  digcu = 1;  |FINSI;
 |SI nume1 = dig5;  digcu = 2;  |FINSI;
 |SI nume1 = dig6;  digcu = 3;  |FINSI;
 |SI nume1 = dig7;  digcu = 4;  |FINSI;
 |SI nume1 = dig8;  digcu = 5;  |FINSI;
 |SI nume1 = dig9;  digcu = 6;  |FINSI;
 |SI nume1 = 0;     digcu = 0;  |FINSI;
 |SI digcu = 0;
     mensa = "    Cuenta Fuera de NIVEL: " + conte;
     |MENAV mensa;
 |FINSI;

 conte = (alfa1 + "            ") % 112;
 #24#0 = conte;
 #24#1 = digcu;
 |LEE 001#24=;
 |SI FSalida ! 0;
     |DDEFECTO #24;
     #24#0 = conte;
     #24#1 = digcu;
     #24#2 = #42#2;
     #24#3 = "N";
     |SI #24#1 = dig3;
         #24#3 = "S";
     |FINSI;
     |HAZ defect1;
     |GRABA 020#24n;
 |FINSI;

 |SI #24#3 = "N";  |ACABA;  |FINSI;     || Si no es del Ultimo Nivel Termina

 alfa5 = conte % 103;
 |SI alfa5 = "430"; |O alfa5 = "440";
     alfa5 = "C"; |HAZ cuentas2;
 |FINSI; || cliente
 |SI alfa5 = "400"; |O alfa5 = "410";
                    |O alfa5 = "514";
                    |O alfa5 = "515";
                    |O alfa5 = "523";
     alfa5 = "P"; |HAZ cuentas2;
 |FINSI;      || proveedor
|FPRC;

|PROCESO cuentas2;
 #12#23 = alfa5;
 #12#10 = conte;
 #12#11 = digcu;
 |LEE 000#12=;
 |SI FSalida ! 0;
     |DDEFECTO #12;
     #12#23 = alfa5;         || C/P
     #12#10 = conte;         || Cuenta Contable
     #12#11 = digcu;         || Digito Contable
     #12#0  = #42#0;         || Codigo
     #12#1  = #42#2;         || Nombre

     CampoDni = #42#6;
     |HAZ LetraDni;
     #12#9 = CampoDni;       || NIF
     |GRABA 020#12n;
  |FINSI;
  Pos = guarda_pos;
|FPRC;

|PROCESO pase_cta;
 alfa1 = field % 0101;
 |SI alfa1 < "0"; |O alfa1 > "9"; |ACABA; |FINSI;  || No es una Cuentas

 |DDEFECTO #42;
 #42#0  = field %  0110;    || Cuenta Contable
 #42#2  = field %  1330;    || Titulo Cuenta
 #42#6  = field %  4515;    || Direccion/DNI

 |PINTA 2229, #42#0;
 |HAZ altacuenta;
|FPRC;

|| ********************************************************************
|PROCESO tipo3;

 |D_CUADRO 2111, 2356;
 |PINTA 2212, "                                            ";

|ET pcuentas;
 |SI #0#0 ! "S"; |VETE pasiento; |FINSI;

 || ... Cuentas
  fabre = #0#4; |QBT fabre;
  fabre = "rt  " + fabre;
  |FABRE fabre;
  fcanl = FSalida;
  |SI fcanl < 0;
      alfa1 = "    [" + (fabre % 5) + "] inaccesible o inexistente ...";
      |MENAV alfa1;
  |SINO;
      |ATRI I; |PINTA 2212, " Pasando Cuenta:                            "; |ATRI 0;
      |ABRE #12;
      |ABRE #24;
      |ABRE #25;
      |PARA; |SI; |HACIENDO;
             field = fcanl + " 250";
             |FLEE field;
             |SI FSalida < 0; |SAL; |FINSI;
             |HAZ pase_cta;
      |SIGUE;
      |CIERRA #12;
      |CIERRA #24;
      |CIERRA #25;
  |FINSI;
  |FCIERRA fcanl;
  Pos = -1;

|| *******************************************************************
|ET pasiento;
  sw_fecha = 0;
  |SI #0#1 ! "S"; |VETE pventas; |FINSI;

  || ... Asientos
  fabre = #0#5; |QBT fabre;
  fabre = "rt  " + fabre;
  |FABRE fabre;
  fcanl = FSalida;
  |SI fcanl < 0;
      alfa1 = "    [" + (fabre % 5) + "] inaccesible o inexistente ...";
      |MENAV alfa1;
  |SINO;
      |ATRI I; |PINTA 2212, " Leyendo Asiento:                           "; |ATRI 0;
      RegAstos = 0;
      |HAZ nom_usu;
      alfa1 = "a" + nom_tmp;
      alfa2 = "e" + nom_tmp;
      |NOME_DAT #40 alfa1;
      |NOME_DAT #43 alfa2;
      |ABRE #40;
      |CIERRA #40;
      |DELINDEX #40;
      |DELINDEX #43;

      |ABRE #40;
      |ABRE #43;
      |PARA; |SI; |HACIENDO;
             field = fcanl + " 250";
             |FLEE field;
             |SI FSalida < 0; |SAL; |FINSI;
             |HAZ pase_auxiliar1;
      |SIGUE;
      |CIERRA #40;
      |CIERRA #43;
  |FINSI;
  |FCIERRA fcanl;
  Pos = -1;

  |SI RegAstos ! 0;
      |ATRI I; |PINTA 2212, " Pasando Asiento:                           "; |ATRI 0;
      |HAZ pase_asientos;
  |FINSI;
  |SI sw_fecha = 1;
     |MENAV "    Hay apuntes con fecha fuera de periodo ...";
  |FINSI;

|| *******************************************************************
|ET pventas;
  sw_fecha = 0;
  |SI #0#2 ! "S"; |VETE pcompras; |FINSI;

  || ... Ventas
  fabre = #0#6; |QBT fabre;
  fabre = "rt  " + fabre;
  |FABRE fabre;
  fcanl = FSalida;
  |SI fcanl < 0;
      alfa1 = "    [" + (fabre % 5) + "] inaccesible o inexistente ...";
      |MENAV alfa1;
  |SINO;
      |ATRI I; |PINTA 2212, " Leyendo Ventas:                            "; |ATRI 0;
      RegFact  = 0;
      tipof    = "V";
      |HAZ nom_usu;
      alfa2 = "b" + nom_tmp;
      |NOME_DAT #41 alfa2;
      |ABRE #41;
      |CIERRA #41;
      |DELINDEX #41;

      |ABRE #41;
      |PARA; |SI; |HACIENDO;
             field = fcanl + " 250";
             |FLEE field;
             |SI FSalida < 0; |SAL; |FINSI;
             |HAZ pase_auxiliar2;
      |SIGUE;
      |CIERRA #41;
  |FINSI;
  |FCIERRA fcanl;
  Pos = -1;

  |SI RegFact ! 0;
      |ATRI I; |PINTA 2212, " Pasando Ventas:                            "; |ATRI 0;
      |HAZ pase_facturas;
  |FINSI;
|| *******************************************************************

|ET pcompras;

  sw_fecha = 0;
  |SI #0#3 ! "S"; |ACABA; |FINSI;

  || ... Compras
  fabre = #0#7; |QBT fabre;
  fabre = "rt  " + fabre;
  |FABRE fabre;
  fcanl = FSalida;
  |SI fcanl < 0;
      alfa1 = "    [" + (fabre % 5) + "] inaccesible o inexistente ...";
      |MENAV alfa1;
  |SINO;
      |ATRI I; |PINTA 2212, " Leyendo Compras:                           "; |ATRI 0;
      RegFact  = 0;
      tipof    = "C";
      |HAZ nom_usu;
      alfa2 = "c" + nom_tmp;
      |NOME_DAT #41 alfa2;

      |ABRE #41;
      |CIERRA #41;
      |DELINDEX #41;

      |ABRE #41;
      |PARA; |SI; |HACIENDO;
             field = fcanl + " 250";
             |FLEE field;
             |SI FSalida < 0; |SAL; |FINSI;
             |HAZ pase_auxiliar2;
      |SIGUE;
      |CIERRA #41;
  |FINSI;
  |FCIERRA fcanl;
  Pos = -1;

  |SI RegFact ! 0;
      |ATRI I; |PINTA 2212, " Pasando Compras:                           "; |ATRI 0;
      |HAZ pase_facturas;
  |FINSI;

|FPRC;

|| ************* Rutinas varias
|PROCESO cuentas;
 nocta = 0;
 |QBF conte;
 |SI conte = ""; nocta = 1; |ACABA; |FINSI;
 digcu = 0;
 alfa2 = conte % 0;
 nume1 = alfa2;
 |SI nume1 = dig4;  digcu = 1;  |FINSI;
 |SI nume1 = dig5;  digcu = 2;  |FINSI;
 |SI nume1 = dig6;  digcu = 3;  |FINSI;
 |SI nume1 = dig7;  digcu = 4;  |FINSI;
 |SI nume1 = dig8;  digcu = 5;  |FINSI;
 |SI nume1 = dig9;  digcu = 6;  |FINSI;
 |SI nume1 = 0;     digcu = 0;  |FINSI;
 |SI digcu = 0; nocta = 1; |FINSI;
 conte = (conte + "            ") % 112;
|FPRC;

|PROCESO fecha;
 alfa1 = afecha % 102;      || dia
 alfa2 = afecha % 402;      || mes
 alfa3 = afecha % 702;     || a¤o
 nume1 = alfa3;
 |SI nume1 > 80;  alfa3="19"+alfa3;  |SINO;  alfa1="20"+alfa3;  |FINSI;
 alfa4 = alfa1 + "." + alfa2 + "." + alfa3;
 afecha = alfa4;
|FPRC;

|PROCESO importe;
 elmenos = "";
 importe = 0;
 |QBT anume;
 alfa1 = anume % 0;
 nume2 = alfa1;
 alfa2 = "";
 |PARA nume1 = 1; |SI nume1 [ nume2; |HACIENDO nume1 = nume1 + 1;
       nume3 = (nume1 * 100) + 1;
       alfa3 = anume % nume3;
       |SI alfa3 = "-"; |Y elmenos = ""; elmenos = alfa3; |FINSI;
       |SI alfa3 = ",";
           alfa2 = alfa2 + alfa3;
       |FINSI;
       |SI alfa3 ] "0"; |Y alfa3 [ "9";
           alfa2 = alfa2 + alfa3;
       |FINSI;
 |SIGUE;
 alfa2 = elmenos + alfa2;
 importe = alfa2;
|FPRC;

|PROCESO leeprovin;
 |DDEFECTO #25;
 #25#0 = #12#3;
 |LEE 001#25=;
|FPRC;

|PROCESO LetraDni;
     eaVarDni   = CampoDni;
     enCalcCif  = 3;
     |HAZ CalculoDNI;
     CampoDni = eaVarDni;
     CampoDni = ("000000000" + CampoDni) % -109;
|FPRC;

|PROCESO gratra; || trabajo para actualizar cuentas del Plan Contable
 modcon = 1;
 #27#0 = codcon;
 #27#1 = #23#4;
 #27#2 = #23#5;
 #27#3 = #23#1;
 #27#4 = #23#8;
 #27#6 = #23#0;
 |LEE 101#27=;
 |SI FSalida ! 0;
     |DDEFECTO #27;
     #27#0 = codcon;
     #27#1 = #23#4;
     #27#2 = #23#5;
     #27#3 = #23#1;
     #27#4 = #23#8;
     #27#6 = #23#0;
     |GRABA 101#27b;
 |FINSI;
 #27#5 = #27#5 + #23#9;
 |GRABA 101#27a;
 |LIBERA #27;
|FPRC;

|PROCESO cabecera; || lee la cabecera.
|DDEFECTO #22;
  #22#0 = diario;
  #22#1 = fecha;
  #22#2 = docume;
  #22#3 = asient;
  linea = -9;
  |GRABA 020#22n;
  |SI FSalida ! 0;
      #23#0 = #22#0;
      #23#1 = #22#1;
      #23#2 = #22#2;
      #23#3 = 9999;
      |LEE 040#23];
      |SI FSalida = 0;
          |LEE 040#23a;
          |SI #23#0 = #22#0; |Y #23#1 = #22#1; |Y #23#2 = #22#2;
              linea = #23#3;
          |FINSI;
      |SINO;
          |LEE 040#23u;
          |SI FSalida = 0; |Y  #23#0 = #22#0; |Y #23#1 = #22#1; |Y #23#2 = #22#2;
              linea = #23#3;
          |FINSI;
      |FINSI;
  |FINSI;
|FPRC;

|PROCESO act_cabecera;
  #22#0 = diario;
  #22#1 = fecha;
  #22#2 = docume;
  |LEE 101#22=;
  |SI #23#8 = "D";
       #22#4 = #22#4 + #23#9;
       |SI #22#09 = 0; #22#08 = #23#4; #22#09 = #23#5; |FINSI;  || contrp.HABER
  |SINO;
       #22#5 = #22#5 + #23#9;
       |SI #22#11 = 0; #22#10 = #23#4; #22#11 = #23#5; |FINSI;  || contrp.DEBE
  |FINSI;
  #22#6 = #22#4 - #22#5;        || saldo
  |GRABA 020#22a;
  |LIBERA #22;
|FPRC;

|PROCESO adapta;
 |SI #23#8 = "D";
     #23#16 = #23#4;
     #23#17 = #23#5;
     #23#10 = "";
     #23#11 = 0;
 |SINO;
     #23#16 = "";
     #23#17 = 0;
     #23#10 = #23#4;
     #23#11 = #23#5;
 |FINSI;
|FPRC;

|PROCESO a_diario;
 |ABRE #20;
 #20#0 = #23#0;
 |LEE 101#20=;
 |SI FSalida ! 0;              || actualiza el diario
     |DDEFECTO #20;
     #20#0 = #23#0;
     #20#1 = "<DIARIO SIN DEFINIR>";
    |GRABA 020#20b;
 |FINSI;
 |SI #23#8 = "D";  #20#2 = #20#2 + #23#9;  |FINSI;
 |SI #23#8 = "H";  #20#3 = #20#3 + #23#9;  |FINSI;
 |GRABA 020#20a;
 |LIBERA #20;
 |CIERRA #20;
|FPRC;

|PROCESO periodo;
 alfa1 = #13#3;   alfa1 = alfa1 % -104;      || saca a¤o
 alfa2 = "31.03." + alfa1;  trim1 = alfa2;  || primer trimestre
 alfa2 = "30.06." + alfa1;  trim2 = alfa2;  || segundo trimestre
 alfa2 = "30.09." + alfa1;  trim3 = alfa2;  || tercer trimestre
 |SI #13#3 [ trim1;                    #13#44 = 1;  |FINSI;
 |SI #13#3 > trim1;|Y #13#3 [ trim2;   #13#44 = 2;  |FINSI;
 |SI #13#3 > trim2;|Y #13#3 [ trim3;   #13#44 = 3;  |FINSI;
 |SI #13#3 > trim3;                    #13#44 = 4;  |FINSI;
|FPRC;

|PROCESO periodo_s;
 alfa1 = #15#3;   alfa1 = alfa1 % -104;     || saca a¤o
 alfa2 = "31.03." + alfa1;  trim1 = alfa2;  || primer trimestre
 alfa2 = "30.06." + alfa1;  trim2 = alfa2;  || segundo trimestre
 alfa2 = "30.09." + alfa1;  trim3 = alfa2;  || tercer trimestre
 |SI #15#3 [ trim1;                    #15#44 = 1;  |FINSI;
 |SI #15#3 > trim1;|Y #13#3 [ trim2;   #15#44 = 2;  |FINSI;
 |SI #15#3 > trim2;|Y #13#3 [ trim3;   #15#44 = 3;  |FINSI;
 |SI #15#3 > trim3;                    #15#44 = 4;  |FINSI;
|FPRC;

|| ****** ACTUALIZACION DE CUENTAS
|PROCESO a_cuenta;
  #24#0 = #23#4;
  #24#1 = #23#5;
  |LEE 101#24=;
  |SI FSalida ! 0;
      |LIBERA #24;
      |DDEFECTO #24;
      #24#0 = #23#4;
      #24#1 = #23#5;
      #24#2 = titulo;
      #24#3 = "S";
      |HAZ defect1;
      |GRABA 020#24b;
  |FINSI;
  |SI #23#0 = 0;
      |SI #23#8 = "D";
          #24#9  = #24#9 + #23#9;
          #24#51 = #24#51 + #23#9;
      |SINO;
          #24#10 = #24#10 + #23#9;
          #24#52 = #24#52 + #23#9;
      |FINSI;
      #24#11 = #24#9 - #24#10;
      #24#53 = #24#51 - #24#52;
  |SINO;
      |SI #23#0 = 99;
          |SI #23#8 = "D";
              #24#48 = #24#48 + #23#9;
              #24#51 = #24#51 + #23#9;
          |SINO;
              #24#49 = #24#49 + #23#9;
              #24#52 = #24#52 + #23#9;
          |FINSI;
          #24#50 = #24#48 - #24#49;
          #24#53 = #24#51 - #24#52;
       |SINO;
          |HAZ actualiz2;
       |FINSI;
  |FINSI;
  |SI #24#58 < #23#1;
      #24#58 = #23#1;
  |FINSI;
  |GRABA 020#24a;
  |LIBERA #24;
|FPRC;

|PROCESO actualiz2;
    ini = dig1;      || numero de niveles
    fecalf = #23#1;
    mes = fecalf % 402;
    mesfec = mes;
    |SI ini = 1;
        mesac = mesfec;
    |SINO;
        mesac = (mesfec - ini) + 1;
        |SI mesac [ 0;
            mesac = (13 - ini) + mesfec;
        |FINSI;
    |FINSI;
    a = (mesac * 3) + 9;
    b = a + 1;
    c = a + 2;
   |SI #23#8 = "D";
        #24a = #24a + #23#9;
        #24#51 = #24#51 + #23#9;
   |SINO;
        #24b = #24b + #23#9;
        #24#52 = #24#52 + #23#9;
   |FINSI;
   #24c = #24a - #24b;
   #24#53 = #24#51 - #24#52;
|FPRC;
||**************************************************************************
