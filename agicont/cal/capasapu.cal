|FICHEROS;
   capasapu #0;

   caenlace #10;
   cacuenta #11;
   cacuebal #12;
|FIN;

|VARIABLES;
   aAlfa    = "";
   aAlfa1   = "";
   CR       = "";
   LF       = "";
   TAB      = "";
   aVarAlfa = "";
   aCuenta  = "";
   aLong    = "";
   aChar    = "";

   Handle1 = 0;
   nSalir  = 0;
   nChar   = 0;
   nCalc   = 0;
   nCont   = 0;
   nAntDoc = 0;
   nNivel  = 0;
   nLong   = 0;

{200}Campos = "";
|FIN;

|PROCESO MiraFichero;
   aAlfa = #capasapu#Campo#; |QBF aAlfa;
   |SI aAlfa = ""; |Y Campo = 4; |ACABA; |FINSI;
   |XABRE "E", aAlfa, Handle1;
   |SI FSalida < 0;
      |MENAV "    No se encuentra el fichero indicado";
      |ERROR;
   |FINSI;
|FPRC;

|PROCESO SacaNivel;
   aAlfa = aCuenta; |QBF aAlfa;
   aLong = aAlfa % 0; nLong = aLong;
   nNivel = 0;
   |SI nLong = #48#14; nNivel = 1; |FINSI;
   |SI nLong = #48#15; nNivel = 2; |FINSI;
   |SI nLong = #48#16; nNivel = 3; |FINSI;
   |SI nLong = #48#17; nNivel = 4; |FINSI;
   |SI nLong = #48#18; nNivel = 5; |FINSI;
   |SI nLong = #48#19; nNivel = 6; |FINSI;
|FPRC;

|PROCESO GrabaReg;
   |DDEFECTO #caenlace;
   aAlfa = Campos6; |QBF aAlfa;
   aAlfa = aAlfa - "APERTURA";
   |SI FSalida = 0;
      #caenlace#0 = #capasapu#1;
   |SINO;
      #caenlace#0 = 0;
   |FINSI;
   #caenlace#1 = Campos3;
   aAlfa = aVarAlfa % 2105; nCalc = aAlfa;
   #caenlace#2 = Campos1;
   #caenlace#3 = Campos2;

   aAlfa = " " * 68; |PINTA 2310, aAlfa;
   aAlfa1 = #caenlace#0; aAlfa1 = ("00" + aAlfa1) % -102;
   aAlfa = "Importando asiento ";
   aAlfa = aAlfa + aAlfa1 + "/" + #caenlace#1 + "/";
   aAlfa1 = #caenlace#2; aAlfa1 = ("000000" + aAlfa1) % -106;
   aAlfa = aAlfa + aAlfa1 + "/";
   aAlfa1 = #caenlace#3; aAlfa1 = ("0000" + aAlfa1) % -104;
   aAlfa = aAlfa + aAlfa1;
   |ATRI I; |PINTA 2310, aAlfa; |ATRI N;

   aCuenta = Campos5; |HAZ SacaNivel;
   #caenlace#4 = aAlfa;
   #caenlace#5 = nNivel;
   #caenlace#6 = #capasapu#2;
   aAlfa = Campos6; |QBF aAlfa; aAlfa = aAlfa + " (";
   aAlfa1 = Campos1; |QBF aAlfa1; aAlfa1 = ("000000" + aAlfa1) % -106; aAlfa = aAlfa + aAlfa1 + ",";
   aAlfa1 = Campos2; |QBF aAlfa1; aAlfa1 = ("0000"   + aAlfa1) % -104; aAlfa = aAlfa + aAlfa1 + ")";
   #caenlace#7 = aAlfa;
   |SI Campos8 ! 0;
      #caenlace#8 = "D";
      #caenlace#9 = Campos8;
   |SINO;
      #caenlace#8 = "H";
      #caenlace#9 = Campos9;
   |FINSI;
   #caenlace#15 = "01";
   |GRABA 020#caenlace.n;
|FPRC;

|PROCESO ImportaApuntes;

   |PARA nCont = 0; |SI nCont [ 100; |HACIENDO nCont = nCont + 1;
      ICampos = Campos1<-; ICampos = ICampos + nCont;
      Campos = "";
   |SIGUE;

   ICampos = Campos1<-;
   aAlfa = #0#0; |QBF aAlfa; aAlfa = "rb  " + aAlfa;
   |FABRE aAlfa;
   |SI FSalida ] 0;
       Handle1 = FSalida;
       nSalir = 0; aAlfa = "";
       |PARA; |SI nSalir = 0; |HACIENDO;
          aChar = Handle1 + " 1"; |FLEE aChar;
          |SI FSalida [ 0;
             nSalir = 1;
          |SINO;
             |SI aChar ! TAB;
                |SI aChar = CR; |O aChar = LF;
                   aChar = Handle1 + " 1"; |FLEE aChar;
                   |SI FSalida > 0;
                      |SI aChar ! LF; |Y aChar ! CR; Pos = Pos - 1; |FINSI;
                   |FINSI;
                   aAlfa = aAlfa - &34; aAlfa = aAlfa - &34;
                   |QBF aAlfa; |SI aAlfa = "NULL"; aAlfa = ""; |FINSI;
                   Campos = aAlfa; aAlfa = "";
                   |HAZ GrabaReg;
                   aAlfa = ""; ICampos = Campos1<-; Campos3 = "";
                |SINO;
                   nChar = &aChar;
                   |SI nChar = 241; aChar = "¤"; |FINSI;
                   |SI nChar = 209; aChar = "¥"; |FINSI;
                   |SI nChar = 186; aChar = "§";  |FINSI;
                   |SI nChar = 170; aChar = "¦";  |FINSI;
                   nCalc = &aChar;
                   |SI nCalc = 128; |O aChar = LF; |O aChar = CR; aChar = ""; |FINSI;
                   aAlfa = aAlfa + aChar;
                |FINSI;
             |SINO;
                aAlfa = aAlfa - &34; aAlfa = aAlfa - &34;
                |QBF aAlfa; |SI aAlfa = "NULL"; aAlfa = ""; |FINSI;
                Campos = aAlfa; aAlfa = "";
                ICampos = ICampos + 1; nCont = nCont + 1;
             |FINSI;
          |FINSI;
       |SIGUE;
/*
       aAlfa = aAlfa - &34; aAlfa = aAlfa - &34;
       |QBF aAlfa; |SI aAlfa = "NULL"; aAlfa = ""; |FINSI;
       Campos = aAlfa; aAlfa = "";
       |HAZ GrabaReg;
*/
       FSalida = Handle1; |FCIERRA FSalida;
   |SINO;
       |MENAV "    Fichero no encontrado";
   |FINSI;
|FPRC;

|PROCESO GrabaCue;
   |DDEFECTO #cacuenta;
   aCuenta = Campos1; |QBF aAlfa; |HAZ SacaNivel;
   aLong = aCuenta % 0; nLong = aLong;
   |SI nLong ! #capasapu#5; |ACABA; |FINSI;

   aAlfa = " " * 68; |PINTA 2310, aAlfa;
   aAlfa = "Importando cuenta " + aCuenta + " " + Campos2; |QBF aAlfa;
   |ATRI I; |PINTA 2310, aAlfa; |ATRI N;

   #cacuenta#0 = aCuenta;
   #cacuenta#1 = nNivel;
   |LEE 000#cacuenta.=;
   |SI FSalida ! 0;
      |DDEFECTO #cacuenta;
      #cacuenta#0 = aCuenta;
      #cacuenta#1 = nNivel;
      #cacuenta#2 = Campos2;
      |SI nNivel = #48#13;
         #cacuenta#3 = "S";
      |SINO;
         #cacuenta#3 = "N";
      |FINSI;
      |ABRE #cacuebal;
      |PARA nCont = 6; |SI nCont ] 1; |HACIENDO nCont = nCont - 1;
         nCalc = 13 + nCont; nCalc = 100 + #48#nCalc#;
         aAlfa = aCuenta % nCalc;
         #cacuebal#0 = aAlfa;
         |LEE 000#cacuebal.=;
         |SI FSalida = 0;
            #cacuenta#4 = #cacuebal#1;
            #cacuenta#5 = #cacuebal#2;
            #cacuenta#6 = #cacuebal#3;
            #cacuenta#7 = #cacuebal#4;
            #cacuenta#8 = #cacuebal#5;
            |SAL;
         |FINSI;
      |SIGUE;
      |CIERRA #cacuebal;
      nCalc = Campos16; #cacuenta#9  = nCalc;
      nCalc = Campos17; #cacuenta#10 = nCalc;
      #cacuenta#11 = #cacuenta#9 - #cacuenta#10;
      |GRABA 020#cacuenta.n;
   |FINSI;
|FPRC;

|PROCESO ImportaPlanCont;

   |PARA nCont = 0; |SI nCont [ 100; |HACIENDO nCont = nCont + 1;
      ICampos = Campos1<-; ICampos = ICampos + nCont;
      Campos = "";
   |SIGUE;

   ICampos = Campos1<-;
   aAlfa = #0#4; |QBF aAlfa; aAlfa = "rb  " + aAlfa;
   |FABRE aAlfa;
   |SI FSalida ] 0;
       Handle1 = FSalida;
       nSalir = 0; aAlfa = "";
       |PARA; |SI nSalir = 0; |HACIENDO;
          aChar = Handle1 + " 1"; |FLEE aChar;
          |SI FSalida [ 0;
             nSalir = 1;
          |SINO;
             |SI aChar ! TAB;
                |SI aChar = CR; |O aChar = LF;
                   aChar = Handle1 + " 1"; |FLEE aChar;
                   |SI FSalida > 0;
                      |SI aChar ! LF; |Y aChar ! CR; Pos = Pos - 1; |FINSI;
                   |FINSI;
                   aAlfa = aAlfa - &34; aAlfa = aAlfa - &34;
                   |QBF aAlfa; |SI aAlfa = "NULL"; aAlfa = ""; |FINSI;
                   Campos = aAlfa; aAlfa = "";
                   |HAZ GrabaCue;
                   aAlfa = ""; ICampos = Campos1<-; Campos3 = "";
                |SINO;
                   nChar = &aChar;
                   |SI nChar = 241; aChar = "¤"; |FINSI;
                   |SI nChar = 209; aChar = "¥"; |FINSI;
                   |SI nChar = 186; aChar = "§";  |FINSI;
                   |SI nChar = 170; aChar = "¦";  |FINSI;
                   nCalc = &aChar;
                   |SI nCalc = 128; |O aChar = LF; |O aChar = CR; aChar = ""; |FINSI;
                   aAlfa = aAlfa + aChar;
                |FINSI;
             |SINO;
                aAlfa = aAlfa - &34; aAlfa = aAlfa - &34;
                |QBF aAlfa; |SI aAlfa = "NULL"; aAlfa = ""; |FINSI;
                Campos = aAlfa; aAlfa = "";
                ICampos = ICampos + 1; nCont = nCont + 1;
             |FINSI;
          |FINSI;
       |SIGUE;
       FSalida = Handle1; |FCIERRA FSalida;
   |SINO;
       |MENAV "    Fichero no encontrado";
   |FINSI;
|FPRC;

|PROGRAMA;
   CR = &10; LF = &13; TAB = &9; Pos = -1;

   |ABRE #capasapu;
   |LEE 000#capasapu.=;
   |SI FSalida ! 0;
      |DDEFECTO #capasapu;
      |GRABA 020#capasapu.c;
   |FINSI;

   |CLS;
   |CABEZA "Pase formato 1";
   |PINPA #0#0; |PINDA #0#0;
   |ENDATOS #1#0;
   |SI FSalida = 0;
      |GRABA 020#capasapu.a;
      |DELINDEX #caenlace; |ABRE #caenlace;
      |ABRE #cacuenta;
      aAlfa = #0#4; |QBF aAlfa;
      |SI aAlfa ! ""; |HAZ ImportaPlanCont; |FINSI;
      |HAZ ImportaApuntes;
      |CIERRA #cacuenta; |CIERRA #caenlace;
      |DFICE aAlfa; |QBF aAlfa; aAlfa = "dsapunte.tab;" + aAlfa;
      |SUB_EJECUTA aAlfa;
   |FINSI;

   |CIERRA #capasapu;
|FPRO;
