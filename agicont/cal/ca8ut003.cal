|FICHEROS;
  cacuenta  #1;
  ca8ut003 #10;

  ca8ctrle #400;
  ca8comay #401;
  ca8cumay #410;

  :/agicont/def/cacuebal #5;    || cuentas de balance
  :/agicont/def/caparbal #6;    || partidas balances
  :/agicont/def/caparexp #7;    || partidas explotacion
  :/agicont/def/caagrupa #8;    || agrupaciones
  :/agicont/def/caepigra #9;    || epigrafes

  agim0019 #119;

  ca8m0002 #422;
  ca8cuma@ #912;

  canempre #30;
  refer00@ #900;
|FIN;

|VARIABLES;
   aAlfa1 = "";
   aAlfa2 = "";
   nNume1 = 0;
   nNume2 = 0;

   fFecha = @;
   opcion = 0;
   {-1}menu = "";
   menu1 = "2400";
   menu2 = "2";
   menu3 = " Elija: ";
   menu4 = "MS";
   menu5 = " Modificar ";
   menu6 = " Salir ";
   aNom  = "";
   aDef912 = "";

   nOp        = 0;
   nError     = 0;
   mesdes     = "";
   meshas     = "";
   num        = 0;
   mes        = "";
   mascara    = "";
   aMas       = "";
   nEstado    = 0;
   aDCamp3    = "";
   aHCamp3    = "";
   aImp       = "";
   informe    = "ca8ut003";
   nVale      = 0;
   nPara1     = 0;
   nHayCambio = 0;
   nMovi      = 0;
   unoI       = "";
   unoF       = "";
   nHay       = 0;

   &eaPrograma  = "";
   &eaExtension = "";
   &enPlan      = 0;
   &nBalNum     = 0;
   aDCta        = "";
   aHCta        = "";
   nSw          = 0;
   nSw30        = 0;
   nSw31        = 0;
   nSw32        = 0;

   aAlfa        = "";
   nDatoImp     = 0;
   aTexImp      = "";
   aPathImp     = "";
   nHay02       = 0;
   nHay03       = 0;
   nHay04       = 0;
   nTipoCta     = 0;
   nCtaNum      = 0;
   aElProgr     = "";
|FIN;

|INCLUYE varnue_i;
|INCLUYE acceso_i;
|INCLUYE defec2_i;

|| **************************************************************************
|PROCESO LeoTabla; |TIPO 6;
  |ABRE #401;
  #401#0 = #10#8;
  |LEE 001#401=;
  |SI FSalida ! 0;
      |MENAV "    Error, no existe Codigo de Cuentas de Mayor";
      |ERROR;
  |SINO;
      #10#9 = #401#1; |PINTA #10#9;
      nTipoCta = #401#3;
  |FINSI;
  |CIERRA #401;
|FPRC;

|| ****************************************************************
|PROCESO LeoPorNivel;
  nHay = 1;
|FPRC;

|DEFBUCLE LeoPorNivel;
  #410#1;
  ;
  enEjerBal, #10#8,unoI;
  enEjerBal, #10#8,unoF;
  ;
  NULL, LeoPorNivel;
|FIN;

|PROCESO BuscoDefecto;
   nVale = 1;
   uno = eaCuenta; |QBF uno;
   dos = uno + "000000000000";
   uno = dos % A105;
   |HAZ leodefectos;
   |SI nVale = 0; |ACABA;  |FINSI;

   nHay = 0;
   uno  = dos % A104;
   unoI = uno + "0";
   unoF = uno + "9";
   |CIERRA #410;
   |BUCLE LeoPorNivel;
   |ABRE #410;

   |SI nHay = 0;
       |HAZ leodefectos;
       |SI nVale = 0; |ACABA;  |FINSI;

       nHay = 0;
       uno  = dos % A103;
       unoI = uno + "0";
       unoF = uno + "9";
       |CIERRA #410;
       |BUCLE LeoPorNivel;
       |ABRE #410;
       |SI nHay = 0;
          |HAZ leodefectos;
       |FINSI;
   |FINSI;
|FPRC;

|PROCESO leodefectos;
     #410#12 = enEjerBal;
     #410#0  = uno;
     #410#10 = #10#8;
     |LEE 001#410=;
     |SI FSalida = 0; nVale = 0; |FINSI;
|FPRC;

|PROCESO PasoNivel;
   enNivel = 0;
   aAlfa1  = eaCuenta; |QBF aAlfa1;
   aAlfa1  = aAlfa1 % 0;
   nNume1  = aAlfa1;
   |SI nNume1 = dig4;  enNivel = 1;  |FINSI;
   |SI nNume1 = dig5;  enNivel = 2;  |FINSI;
   |SI nNume1 = dig6;  enNivel = 3;  |FINSI;
   |SI nNume1 = dig7;  enNivel = 4;  |FINSI;
   |SI nNume1 = dig8;  enNivel = 5;  |FINSI;
   |SI nNume1 = dig9;  enNivel = 6;  |FINSI;
|FPRC;

|PROCESO LeoPlan;
   eaCuenta = #912#0;
   |HAZ PasoNivel;
   |SI enNivel = 0;  |ACABA;  |FINSI;    || No la Pasa

   |DDEFECTO #1;
   #1#0 = #912#0;
   |LEE 101#1=;
   nEstado = FSalida;
   #1#0  = #912#0;  #1#1  = enNivel;
   #1#2  = #912#9;  || Descripcion Abreviada
   #1#3  = "N";     || Pase directo
   nMensa  = 1;
   |HAZ defec2;
   || la 3a clave esta formada por la cta + zzzz y el campo fantasma del nivel;
   mascara = #1#0;
   |QBF mascara;
   mascara = mascara +"zzzzzzzzzzzz";
   mascara = mascara % 105;
   #1#54   = mascara;
   #1#55   = enNivel;
   #1#56   = mesdes;
   #1#57   = meshas;

   |SI nEstado = 0;  |GRABA 020#1a;  |FINSI;
   |SI nEstado ! 0;  |GRABA 020#1n;  |FINSI;
   |LIBERA #1;

   |PINTA 1948, #1#0;
|FPRC;

|DEFBUCLE LeoPlan;
 #912#1003;
 ;
 enEjerBal, #10#8, "     ";
 enEjerBal, #10#8, "zzzzz";
 e;
 NULL, LeoPlan;
|FIN;

|PROCESO Cuentas;
 eaCuenta = #1#0; |QBF eaCuenta;
 aAlfa1  = eaCuenta % 0;
 nNume1  = aAlfa1;
 |SI nNume1 = 0; |ACABA; |FINSI;

 |SI nOp = 0;
     |HAZ BuscoDefecto;
     |SI nVale = 1;
         nMovi = 0;
         |PARA nPara1 = 9; |SI nPara1 [ 53; |HACIENDO nPara1 = nPara1 + 1;
               |SI #1nPara1 ! 0; nMovi = 1; |SAL; |FINSI;
         |SIGUE;
         |SI nMovi = 1; nError = 1; |ERROR10; |FINSI;
     |FINSI;
 |FINSI;

 |SI nOp = 1;
     |SI nNume1 [ 4;
         |BORRA 020#1a;
     |SINO;
         |HAZ BuscoDefecto;
         |SI nVale = 1; |BORRA 020#1a; |FINSI;
     |FINSI;
 |FINSI;

 |SI nOp = 2;
     |HAZ BuscoDefecto;
     |SI nVale = 1;
         nMovi = 0;
         |PARA nPara1 = 9; |SI nPara1 [ 53; |HACIENDO nPara1 = nPara1 + 1;
               |SI #1nPara1 ! 0; nMovi = 1; |SAL; |FINSI;
         |SIGUE;
         |SI nMovi = 1; |PRINT; |FINSI;
     |FINSI;
 |FINSI;

 |LIBERA #1;
|FPRC;

|DEFBUCLE Cuentas;
 #1#1;
 3;
 "            ", 0, aDCamp3;
 "zzzzzzzzzzzz", 9, aHCamp3;
 be;
 NULL, Cuentas;
|FIN;

|PROCESO Imprimir;
  |IMPRE 0;
  |SI FSalida ! 0;
      |MENAV "    Proceso abortado";
      |ACABA;
  |FINSI;
  |INFOR informe;
  |SI FSalida ! 0;
      |MENAV "    Error no existe informe";
      |FINIMP;
      |ACABA;
  |FINSI;

  aDCamp3 = "S";
  aHCamp3 = "S";
  nOp     = 2; ||Saca el informe de las cuentas con movimientos
  |BUCLE Cuentas;
  |PIEINF;
  |FININF;
  |FINIMP;
|FPRC;

|PROCESO Cambio;
 |ABRE #410;
 nError = 0;
 aDCamp3 = "S";
 aHCamp3 = "S";
 nOp     = 0;       || Compruebo si hay cuentas con saldo que no estan en el nuevo
 |PINTA 1915, " Comprobando Plan Contable de la Empresa ... ";
 |BUCLE Cuentas;
 |SI nError = 1;
     |PUSHV  1611 2062;
     |CUADRO 1611 2062;
     |ATRI R;
     aImp = "S";
     |PINTA 1712, " Existe Cuentas con Movimientos que no estan  en  ";
     |PINTA 1812, " el Plan Contable elegido. Proceso interrumpido.  ";
     |PINTA 1912, " Desea obtener un listado de estas cuentas S/N:   ";
      E_sup  = 1;
      E_inf  = "SNsn";
      aImp = 1960 ? 0;
      aImp = aImp > " "; |ATRI P; |PINTA 1960, aImp; |ATRI 0;
      |POPV;
      |SI aImp = "S"; |HAZ Imprimir; |FINSI;
      #10#8 = #400#2;
      #10#9 = aNom;
 |FINSI;
 |PINTA 1915, "                                             ";
 |SI nError = 0;
     |PINTA 1915, " Borrando Cuentas ...                        ";
     aDCamp3 = " ";
     aHCamp3 = "z";
     nOp = 1;
     |BUCLE Cuentas;
     |PINTA 1915, " Recogiendo Nuevo Plan Contable:             ";
     |ABRE #1;
     |BUCLE LeoPlan;
     |CIERRA #1;
     |PINTA 1915, "                                             ";

     |ABRE #400;
     #400#0 = enEmpresa;
     #400#1 = enPeriodo;
     |LEE 001#400=;
     |SI FSalida = 0;
         #400#2 = #10#8;
         aNom   = #10#9;
         fFecha = ~; #400#6 = fFecha;
         aAlfa1 = Usuario % 810; #400#7 = aAlfa1;
         |GRABA 020#400a;
     |FINSI;
     |CIERRA #400;
     nHayCambio = 1;

     |HAZ ActBalImpuesto;
 |FINSI;
 |CIERRA #410;
|FPRC;

|CALCULO wTipo12; |TIPO 12;
  |SI #10#8 ! #400#2;
      |CONFI "    SCambiar Codigo Ctas Mayor ";
      |SI FSalida = 0;
          |HAZ Cambio;
      |SINO;
          #10#8 = #400#2;
          #10#9 = aNom;
      |FINSI;
      |PINTA #10#8;
      |PINTA #10#9;
  |FINSI;
|FCAL;

|| **************************************************************************
|PROCESO MiraDef;
 #400#2 = 1;
 |ABRE #1;
 #1#0 = "8           ";
 |LEE 001#1];
 |SI FSalida = 0; #400#2 = 0; |FINSI;
 |CIERRA #1;
|FPRC;
|| **************************************************************************

|PROCESO VerImpuesto;
    nDatoImp = 0;
    |SI HaySociedad = 0; |ACABA; |FINSI;

    nHay02 = 0;
    nHay03 = 0;
    nHay04 = 0;

    aPathImp = aPContas + "fich/";
    aTexImp = "";

    aElProgr = aPContas + "def/isom0001.mas";
    |CARGA_DEF aElProgr, refer00@;
    |SI FSalida = 0;
        |PATH_DAT #900 aPathImp;
        |ABRE #900;
        #900#0 = enEmpresa;
        #900#1 = enEjercicio;
        #900#2 = 0;
        |LEE 041#900];
        |SI FSalida = 0; |Y enEmpresa = #900#0; |Y enEjercicio = #900#1;
            nHay02 = 1; nHay04 = 1;
            aTexImp = "Impuesto de Sociedades";
        |FINSI;
        |CIERRA #900;
        |DESCARGA_DEF #900;
    |FINSI;

    aElProgr = aPContas + "def/cubal801.mas";
    |CARGA_DEF aElProgr, refer00@;
    |SI FSalida = 0;
        |PATH_DAT #900 aPathImp;
        |ABRE #900;
        #900#0 = enEmpresa;
        #900#1 = enEjercicio;
        #900#2 = 1;
        |LEE 041#900];
        |SI FSalida = 0; |Y #900#0 = enEmpresa; |Y #900#1 = enEjercicio;
            nHay03 = 1; nHay04 = 1;
            |SI nHay02 = 1; aTexImp = aTexImp + " y "; |FINSI;
            aTexImp = aTexImp + "Cuentas Anuales ";
        |FINSI;
        |CIERRA #900;
        |DESCARGA_DEF #900;
    |FINSI;

    |SI nHay04 = 1;
        aTexImp = " - " + aTexImp + "                                                    ";
        aTexImp = aTexImp % 0152;
        |PUSHV 16132066;
        |D_CUADRO 1613,2066;
        |ATRI R;
        |PINTA 1714," ATENCION:  Esta Empresa tiene datos informados en  ";
        |PINTA 1814, aTexImp;
        |PINTA 1914," No es recomendable el Cambio del Plan Contable.    ";
        |ATRI r;
        |PAUSA;
        |CONFI "    NContinuar con el Proceso";
        |SI FSalida ! 0;
            nDatoImp = 1;
        |FINSI;
        |POPV;
   |FINSI;
|FPRC;

|PROCESO ActBalImpuesto;

    |SI nTipoCta = 1;
        nBalNum = 0;
        nCtaNum = 0;
        |SI #10#8 = 20; |O #10#8 = 30;
            nBalNum = #10#8;
            nCtaNum = #10#8;
        |FINSI;
    |SINO;
        nBalNum = 1;
        nCtaNum = 1;
        |SI #10#8 = 21; |O #10#8 = 31;
            nBalNum = #10#8;
            nCtaNum = #10#8;
        |FINSI;
    |FINSI;

 || Lee Ficha Sociedades
    |ABRE #agim0019;
    #agim0019#0 = enEmpresa;
    #agim0019#1 = 99;
    |LEE 041#agim0019.];
    |SI FSalida = 0;
        |LEE 141#agim0019.a;
    |SINO;
        |LEE 141#agim0019.u;
    |FINSI;
    |SI FSalida = 0; |Y #agim0019#0 = enEmpresa;

        |SI nBalNum ! 0; |O #agim0019#87 ! 10;
            #agim0019#87 = nBalNum;
        |FINSI;
        |SI nCtaNum ! 0; |O #agim0019#88 ! 10;
            #agim0019#88 = nCtaNum;
        |FINSI;
        |GRABA 020#agim0019.a;
    |FINSI;
    |LIBERA #agim0019;
    |CIERRA #agim0019;

    |SI HaySociedad = 1;

        aPathImp = aPContas + "fich/";

        aElProgr = aPContas + "def/cuacceso.mas";
        |CARGA_DEF aElProgr, refer00@;
        |SI FSalida = 0;

            |PATH_DAT #900 aPathImp;
            |ABRE #900;
            #900#0 = enEmpresa;
            #900#1 = enEjercicio;
            |LEE 141#900=;
            |SI FSalida = 0;
                |SI #900#14 ! #10#8;  ||cambia de Plan
                    #900#14 = #10#8;
                    |SI nBalNum ! 0; |O #900#15 ! 10;
                        #900#15 = nBalNum;
                    |FINSI;
                    |SI nCtaNum ! 0; |O #900#16 ! 10;
                        #900#16 = nCtaNum;
                    |FINSI;
                    |GRABA 020#900a;
                |FINSI;
            |FINSI;
            |LIBERA #900;
            |CIERRA #900;
            |DESCARGA_DEF #900;
        |FINSI;

        aElProgr = aPContas + "def/maempr01.mas";
        |CARGA_DEF aElProgr, refer00@;
        |SI FSalida = 0;

            |PATH_DAT #900 aPathImp;
            |ABRE #900;
            #900#0 = enEmpresa;
            #900#1 = enEjercicio;
            |LEE 141#900=;
            |SI FSalida = 0;
                |SI nBalNum ! 0; |O #900#101 ! 10;
                    #900#101 = nBalNum;
                |FINSI;
                |SI nCtaNum ! 0; |O #900#102 ! 10;
                    #900#102 = nCtaNum;
                |FINSI;
                |GRABA 020#900a;
            |FINSI;
            |LIBERA #900;
            |CIERRA #900;
            |DESCARGA_DEF #900;
        |FINSI;
    |FINSI;
|FPRC;
|| **************************************************************************

|PROGRAMA;
   nHayCambio = 0;
   |HAZ inicio;
   |HAZ eVarEmpresa;
   |SI enEjercicio < 2008;
       aAlfa1 = "    Proceso para el Nuevo Plan Contable 2008. ";
       |MENAV aAlfa1;
       |ACABA;
   |FINSI;
   num = #canempre#11; |HAZ ElMes; mesdes = mes;
   num = #canempre#12; |HAZ ElMes; meshas = mes;

   |DFICE fich_alta;
   |ABRE #400;
   |DDEFECTO #400;
   #400#0 = enEmpresa;
   #400#1 = enPeriodo;
   |LEE 001#400=;
   |SI FSalida ! 0;
       |DDEFECTO #400;
       |HAZ MiraDef;
       #400#0 = enEmpresa;
       #400#1 = enPeriodo;
       #400#5 = 0;
       fFecha = ~; #400#6 = fFecha;
       aAlfa1 = Usuario % 810; #400#7 = aAlfa1;
       |GRABA 020#400n;
   |FINSI;
   |CIERRA #400;

   |ABRE #401;
   #401#0 = #400#2;
   |LEE 001#401=;
   |CIERRA #401;

   #10#0 = enEmpresa;
   #10#1 = enPeriodo;
   #10#2 = eaNombreEmp;
   #10#3 = enEjercicio;
   #10#4 = #400#3;
   #10#5 = #400#4;

   #10#8 = #400#2;
   #10#9 = #401#1;
   aNom  = #10#9;

   |SI #400#5 = 0;
       #10#10 = "Creacion";
   |SINO;
       #10#10 = "Apertura";
   |FINSI;
   #10#11 = #400#6;
   #10#12 = #400#7;

   |DBASE aMas;
   aDef912 = aMas + "def/ca8cumay";
   |CARGA_DEF aDef912, ca8cuma@;
   |SI FSalida ! 0;
       |MENAV "    Error al Carga fichero de Cuentas de Mayor";
       nError    = 1;
       aDef912   = "";
       |VETE Terminar;
   |FINSI;

   |CLS;
   |CABEZA "Control Plan Contable de la Empresa";
   |PINPA #0#10;
   |PINDA #0#10;

   FSalida = 2;
   |PARA; |SI; |HACIENDO;
          |MENU menu;
          opcion = FSalida;
          |SI opcion = 1;
              |HAZ VerImpuesto;
              |SI nDatoImp = 0;
                  |ENDATOS #1#10;
              |FINSI;
          |FINSI;
          |SI opcion = 2; |SAL;  |FINSI;
   |SIGUE;

|ET Terminar;

   |SI aDef912 ! ""; |DESCARGA_DEF #ca8cuma@; |FINSI;
   |SI nHayCambio = 1;
       aAlfa1 = #10#0; aAlfa1 = ("00000" + aAlfa1) % -105;
       aAlfa2 = #10#1; aAlfa2 = ("0"     + aAlfa2) % -101;
       aAlfa1 = "carecupe canempr1 " + aAlfa1 + aAlfa2 + ";cambio";
       |SUB_EJECUTA_NP aAlfa1;
   |FINSI;
|FPRO;

|PROCESO ElMes;
   |SI num =  1; mes = "Enero";     |FINSI;
   |SI num = 12; mes = "Diciembre"; |FINSI;
   |SI num =  2; mes = "Febrero";   |FINSI;
   |SI num =  3; mes = "Marzo";     |FINSI;
   |SI num =  4; mes = "Abril";     |FINSI;
   |SI num =  5; mes = "Mayo";      |FINSI;
   |SI num =  6; mes = "Junio";     |FINSI;
   |SI num =  7; mes = "Julio";     |FINSI;
   |SI num =  8; mes = "Agosto";    |FINSI;
   |SI num =  9; mes = "Septiembre";|FINSI;
   |SI num = 10; mes = "Octubre";   |FINSI;
   |SI num = 11; mes = "Noviembre"; |FINSI;
|FPRC;
