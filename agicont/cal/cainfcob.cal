|FICHEROS;
     cainfcob #0;
     camancob #1;
     caclipro #2;
     caivarep #3;
     cacuenta #5;
|FIN;

|VARIABLES;
     sw = 0;
     &ult_cta = "";
     &total = 0;
     &total1 = 0;
     &sw_inf = 0;
     &empres = "";
     &cp = "";
     &impor = 0;
     alfa1 = "";
     novale = 0;

     Parte1 = "";
     Parte2 = "";
     Posic1 = 0;
     Posic2 = 0;
     Comodin = "";
     Long = "";
     LongNum = 0;
     FEstado = 0;
     MaxDig = 0;
|FIN;

|INCLUYE acceso_i;
|INCLUYE puntos_i;

||------------------------------------------------------------------------
|CALCULO mayor; |TIPO 0;
    alfa1 = #0Campo > " ";
    #0Campo = alfa1;
    |PINTA #0Campo;
|FCAL;

|CALCULO Cuenta; |TIPO 6;
  salidas = FSalida;
  p_alfa1 = #0Campo;
  |HAZ punto;
  |SI p_error ! 0;  |ERROR;  |ACABA;  |FINSI;
|FCAL;

|RUTINA inf5;
 #5#0 = "43          ";
 #5#1 = dig3;
|FRUT;

|RUTINA sup5;
 #5#0 = "44zzzzzzzzzz";
 #5#1 = dig3;
|FRUT;

|CALCULO CueCons; |TIPO 0;
 |SI FSalida = 10;
     |ABRE #5;
     |CONSULTA_F_CLAVE #1#5, inf5, sup5;
     |CIERRA #5;
     #0Campo = #5#0; |PINTA #0Campo;
 |FINSI;
|FCAL;

|| ------------------------------------------------------------------------
|PROCESO imprim;
     |HAZ verbueno;
     |SI novale = 1; |ACABA; |FINSI;
     |SI sw = 0;
          sw = 1;
          sw_inf = 0;
          |HAZ busca_cuen;
          |PRINT;
          ult_cta = #1#4;
     |FINSI;
     |SI ult_cta ! #1#4;
          sw_inf = 2;    ||Imprime el primer total
          |PRINT;
          sw_inf = 0;
          |HAZ busca_cuen;
          |PRINT;
          total = 0;
     |FINSI;
     sw_inf = 1;
     |HAZ leeiva;
     |PRINT;
     total = total + impor;
     total1 = total1 + impor;
     ult_cta = #1#4;
|FPRC;

|PROCESO leeiva;
     |DDEFECTO #3;
     #3#0  = #1#0;
     #3#2  = #1#1;
     #3#27 = #1#2;
     |LEE 001#3=;
|FPRC;

|PROCESO busca_cuen;
     |DDEFECTO #2;
     #2#23 = "C";
     #2#10 = #1#4;
     #2#11 = #1#5;
     |LEE 001#2=;
     |SI FSalida = 0;
         cp = (("000" + #2#3) % -102) + (("000" + #2#4) % -103);
     |SINO;
         cp = "";
         |ABRE #5;
         |DDEFECTO #5;
         #5#0 = #1#4;
         #5#1 = #1#5;
         |LEE 001#5=;
         #2#1 = #5#2;
         |CIERRA #5;
    |FINSI;
|FPRC;

|DEFBUCLE lee_cobro;
 #1#2;
 0, 1, 3, 11; || selecciona libro+factura+cobro+tipo recibo
 #0#4, 0, #0#6, #0#0 , #0#2 ,   0, #0#12;
 #0#5, 9, #0#7, #0#1 , #0#3 , 999, #0#13;
 e;
 NULL, imprim;
|FIN;

|| ********************************************************
|CALCULO inicia; |TIPO 3;

     |IMPRE 0;
     |SI FSalida = 0;
          |INFOR "cainfcob";
          |SI FSalida = 0;
               empres = #48#1;
               |ABRE #3;
               |ABRE #2;
               |BUCLE lee_cobro;
               |SI sw = 1;
                    sw_inf = 2;
                    |PRINT;
               |FINSI;
               |CIERRA #3;
               |CIERRA #2;
               |PIEINF;
               |FININF;
          |SINO;
               |MENAV "0000Informe Inexistente";
          |FINSI;
          |FINIMP;
     |FINSI;
|FCAL;

|PROCESO verbueno;
   novale = 0;
   impor = 0;
   |SI #0#14 = "T";
       impor = #1#8; || importe total cobro
       |ACABA;
   |FINSI;
   |SI #0#14 = "S";
       impor = #1#24; || importe cobrado
       |SI impor = 0;
           novale = 1;
       |FINSI;
       |ACABA;
   |FINSI;
   |SI #0#14 = "N";
        impor = #1#8 - #1#24; || importe total - liquidado
       |SI impor = 0; |O #1#23 = "S";
           novale = 1;
           impor = 0;
       |FINSI;
       |ACABA;
   |FINSI;
|FPRC;
