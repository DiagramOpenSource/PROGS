|FICHEROS;
  cay00020 #0;
  camaasil #3;
  cabaexp2 #4;
  cacuenta #5;
|FIN;

|VARIABLES;
   &entrada = 1;
   informe  = "";
   inform1  = "cay00020";
   inform2  = "cay10020";

   nHayDesc = 0;
   nDebe    = 0;
   nHaber   = 0;
   nSaldo   = 0;
   aAlfa1   = "";
   aAlfa2   = "";
   nNume1   = 0;

   eaCuenta = "";
   eswCta   = 0;
   doce     = "            ";
   aLong    = "";
   nLong    = 0;
   enNivel  = 0;
   aFichero = "";
   aDFicOri = "";
   aDFicAct = "";
   &r_emp = 0;
   &r_per = 0;

   nMes = 0;
   aLetrames = "";
|FIN;

|INCLUYE acceso_i;
|INCLUYE puntos_i;

|| **********************************************************************
|| ***** Calculos de Pantalla
|| **********************************************************************
|PROGRAMA;
  |CLS;
  |CABEZA "Comprobacion Extracto y Balance";

  |HAZ inicio;
  |DDEFECTO #0;
  |PINPA #0#0;

  |ABRE #5;
  |LEE 001#5u;
  #0#5 = #5#0;
  #0#7 = #5#1;
  |CIERRA #5;

  |PINDA #0#0;

  |ENDATOS #1#0;
  |SI FSalida = 0;
      |HAZ Cuadrar;
  |FINSI;
|FPRO;

|| **************************************************************************
|CALCULO cuenta; |TIPO 6;
  p_alfa1 = #0Campo;
  salidas = FSalida;
  |HAZ punto;
  |SI p_error ! 0;
      |MENAV "     ERROR EN CUENTA";
      |ERROR;
      |ACABA;
  |FINSI;
  eaCuenta = #0Campo; |HAZ SacaNivel;
  nNume1 = Campo + 2;
  #0nNume1 = enNivel;
|FCAL;

|PROCESO pintames; |TIPO 0;
  nNume1 = Campo + 2;                || Coge el campo del nombre del mes
  |SI #0Campo ! 0; |Y #0Campo ! 13;
      nMes = #30#11 + (#0Campo - 1);   || Operacion para saber que mes es
  |SINO;
      nMes = #0Campo;
  |FINSI;
  |HAZ mesletra;                          || Recoge el mes en letra
  #0nNume1 = aLetrames;
  |PINTA #0nNume1;                         || Pinta el nombre del mes

  |SI Campo = 1; |Y  #0#0 > #0#1;
      |MENAV "    Error de Entrada ...";
      |ERROR;
  |FINSI;
|FPRC;

|PROCESO mesletra;
  |SI nMes = 0;  aLetrames = "Apertura  "; |FINSI;
  |SI nMes = 1;  aLetrames = "Enero     "; |FINSI;
  |SI nMes = 2;  aLetrames = "Febrero   "; |FINSI;
  |SI nMes = 3;  aLetrames = "Marzo     "; |FINSI;
  |SI nMes = 4;  aLetrames = "Abril     "; |FINSI;
  |SI nMes = 5;  aLetrames = "Mayo      "; |FINSI;
  |SI nMes = 6;  aLetrames = "Junio     "; |FINSI;
  |SI nMes = 7;  aLetrames = "Julio     "; |FINSI;
  |SI nMes = 8;  aLetrames = "Agosto    "; |FINSI;
  |SI nMes = 9;  aLetrames = "Septiembre"; |FINSI;
  |SI nMes = 10; aLetrames = "Octubre   "; |FINSI;
  |SI nMes = 11; aLetrames = "Noviembre "; |FINSI;
  |SI nMes = 12; aLetrames = "Diciembre "; |FINSI;
  |SI nMes = 13; aLetrames = "Cierre    "; |FINSI;
|FPRC;


|| --------------------------------------------------------------------------
|| --------------------------------------------------------------------------
|| --------------------------------------------------------------------------

|| ... Desde el extracto contable de la Empresa Actual
|PROCESO CreaDatos2;
  |DDEFECTO #4;
  #4#5 = #3#4;
  #4#6 = #3#5;
  |LEE 101#4=;
  |SI FSalida ! 0;
      |DDEFECTO #4;
      #4#5 = #3#4;
      #4#6 = #3#5;

      #5#0 = #4#5;
      #5#1 = #4#6;
      |LEE 001#5=;
      |SI FSalida ! 0; |DDEFECTO #5; |FINSI;

      #4#7  = #5#2;   ||Nombre Cuenta
      #4#11 = #5#51;  ||Debe
      #4#12 = #5#52;  ||Haber
      #4#13 = #5#53;  ||Saldo
      |GRABA 020#4b;
  |FINSI;
  |SI #3#8 = "D";
      #4#14 = #4#14 + #3#9;
  |SINO;
      #4#15 = #4#15 + #3#9;
  |FINSI;
  #4#16 = #4#14 - #4#15;
  |GRABA 020#4a;
  |LIBERA #4;
|FPRC;

|DEFBUCLE CreaDatos1;
 #3#1;
 4;
 00, fec1, 000001, 0001, #0#4;
 99, fec2, 999999, 9999, #0#5;
 e;
 NULL, CreaDatos2;
|FIN;

|| -----------------------------------------
|PROCESO ReponDatos;
 |SI #0#8 = "S";
     |SI #4#11 = #4#14; |Y #4#12 = #4#15;
         |ACABA;    || ... No Imprime
     |FINSI;
     |SI #0#9 = 2;
         |SI #4#13 = #4#16;
             |ACABA;    || ... No Imprime
         |FINSI;
     |FINSI;
 |FINSI;
 |PRINT;
  nHayDesc = 1;
|FPRC;

|DEFBUCLE ReponDatos;
 #4#1;
 ;
 INICIO;
 FINAL;
 ;
 NULL, ReponDatos;
|FIN;

|| -----------------------------------------
|PROCESO Cuadrar;

  |IMPRE 0;
  |SI FSalida ! 0; |ACABA; |FINSI;

  informe = inform1; |SI #0#9 = 2; informe = inform2; |FINSI;
  |INFOR informe;
  |SI FSalida ! 0; |FINIMP; |ACABA; |FINSI;

  aFichero = ("z" + Usuario) % 108;
  |NOME_DAT #4 aFichero;
  |DELINDEX #4;

  |ABRE #4;
  |ABRE #5;
  |BUCLE CreaDatos1;
  |CIERRA #5;
  |CIERRA #4;

  nDebe    = 0;
  nHaber   = 0;
  nHayDesc = 0;
  |BUCLE ReponDatos;
  |SI nHayDesc ! 0;
      |PIEINF;
  |FINSI;

  |FININF;
  |FINIMP;
  |DELINDEX #4;
|FPRC;

||***********************************************************************

|PROCESO SacaNivel;
   eswCta = 1;     ||Errores en Cuenta
   aLong = eaCuenta;
   |QBF aLong;
   aLong = aLong % 0;
   nLong = aLong;
   enNivel = 0;
   |SI nLong = dig4; |Y dig4 ! 0; eswCta = 0; enNivel = 1; |FINSI;
   |SI nLong = dig5; |Y dig5 ! 0; eswCta = 0; enNivel = 2; |FINSI;
   |SI nLong = dig6; |Y dig6 ! 0; eswCta = 0; enNivel = 3; |FINSI;
   |SI nLong = dig7; |Y dig7 ! 0; eswCta = 0; enNivel = 4; |FINSI;
   |SI nLong = dig8; |Y dig8 ! 0; eswCta = 0; enNivel = 5; |FINSI;
   |SI nLong = dig9; |Y dig9 ! 0; eswCta = 0; enNivel = 6; |FINSI;

   |SI eswCta = 1; |Y  nLong ! 0;
       |MENAV "      Longitud de Cuenta Fuera de Nivel ";
       |ERROR;
   |FINSI;
|FPRC;
