|FICHEROS;
    ciactasc #0;    || Pantalla
    camaasic #1;    || Apuntes Cabs
    camaasil #2;    || Apuntes Lins
    cientcon #3;    || apuntes contrapartidas
    cientasc #4;    || apuntes ctas cabs.
    cientasl #5;    || apuntes ctas lins.
    cisalant #6;    || Saldo Anterior.

    catradia #9;    || Trabajo Cuentas
    camandia #10;   || Diario
    cacuenta #11;   || Cuentas
|FIN;

|VARIABLES;
  &enDiario   = 0;
  &enContador = 0;
  &entrada = 1;
  &modcon  = 0;

  tipoAct  = 0;
  cam      = 0;
  alfa1    = "";
  nume1    = 0;

  estado   = 0;

  cta_con  = "";
  dig_con  = 0;
  con_con  = 0;
  des_con  = "";
  signo    = "";
  imp_con  = 0;

  ini = 0;
  fecalf = "";
  mes = "";
  mesfec = 0;
  mesac = 0;
  a = 0;
  b = 0;
  c = 0;
  cuedebe  = 0;
  cuehaber = 0;
  cuediario = 0;
  cuefecha  = @;

  paso     = 0;
  men2   = "    Longitud de Cuenta Fuera de Nivel ";
  digito = 0;
  cta_t  = 0;
  cta    = "";
  long   = "";
  swdig  = 0;

  diario   = 0;
  fecha    = @;
  docume   = 0;
  asient   = 0;
  linea    = 0;

  debe    = 0;
  haber   = 0;
  entfec  = @;
  tdebe   = 0;
  thaber  = 0;
  &antdia  = 0;
  &antfec  = @;
  &antdoc  = 0;
  &numero  = 0;
  &error   = "";

  lacontra = "";
  total    = 0;
  swalgun  = 0;

  swbueno  = 0;
  swnuevo  = 1;
  sw_error = 0;
  swcuadre = 0; || 0 = cuadrado; Descuadres -> 1 = pasar a Asientos
                ||                             2 = Salir de Actualizacion
|FIN;

|INCLUYE acceso_i;
|INCLUYE puntos_i;
|INCLUYE tempo_i;
|INCLUYE condes_i;

|| *************************************************************************
||                        DESDE CAMPOS DE PANTALLA
|| *************************************************************************

|PROCESO defectos; |TIPO 7;
 |SI paso = 0;
     |ABRE #4;
     |LEE 001#4p;
     |SI FSalida = 0;
         #0#0 = #4#0; #0#1 = #4#1;
         |PINTA #0#0;
     |FINSI;
     |LEE 001#4u;
     |SI FSalida = 0;
         #0#2 = #4#0; #0#3 = #4#1;
         |PINTA #0#2;
     |FINSI;
     |CIERRA #4;
     |HAZ documento;
     paso = 1;
 |FINSI;
|FPRC;

|PROCESO cuenta; |TIPO 6;
 |SI FSalida = 2; |ACABA; |FINSI;
 p_alfa1 = #0Campo;
 salidas = FSalida;
 |HAZ punto;
 |SI p_error ! 0;  |ERROR;  |ACABA;  |FINSI;
 |SI #0Campo = "            ";  |ACABA;  |FINSI;
 digito = Campo + 1;
 cta  = #0Campo; |QBF cta;
 long = cta % 0; nume1 = long;
 swdig = 0;
 |SI nume1 = dig4; swdig = 1; #0digito = 1; |FINSI;
 |SI nume1 = dig5; swdig = 1; #0digito = 2; |FINSI;
 |SI nume1 = dig6; swdig = 1; #0digito = 3; |FINSI;
 |SI nume1 = dig7; swdig = 1; #0digito = 4; |FINSI;
 |SI nume1 = dig8; swdig = 1; #0digito = 5; |FINSI;
 |SI nume1 = dig9; swdig = 1; #0digito = 6; |FINSI;
 |SI swdig = 0;
     |MENAV men2;
     |ERROR;
 |FINSI;
|FPRC;

|PROCESO otravez;
 cta_t  = Campo - 1;
 digito = Campo;
 cta    = #0cta_t; |QBF cta;
 long   = cta % 0;
 swdig  = 0;
 |SI long = dig4; swdig = 1; #0digito = 1; |FINSI;
 |SI long = dig5; swdig = 1; #0digito = 2; |FINSI;
 |SI long = dig6; swdig = 1; #0digito = 3; |FINSI;
 |SI long = dig7; swdig = 1; #0digito = 4; |FINSI;
 |SI long = dig8; swdig = 1; #0digito = 5; |FINSI;
 |SI long = dig9; swdig = 1; #0digito = 6; |FINSI;
|FPRC;

|PROCESO ctah; | TIPO 0;   ||  hasta cuenta
  |SI #0#0 > #0#2;  |ERROR;  |FINSI;
|FPRC;

|PROCESO mayor; |TIPO 0;
  alfa1 = #0Campo > " ";
  #0Campo = alfa1;
  |PINTA #0Campo;
|FPRC;

|PROCESO documento; |TIPO 7;
  enDiario = #0#5;
  |HAZ contador;
  #0#6 = enContador;
  |PINTA #0#6;
|FPRC;

|PROCESO comdocum; |TIPO 0;
  |ABRE #1;
  |SELECT #3#1;
  #1#2 = #0#6;
  |LEE 001#1=;
  |SI FSalida = 0;
      |MENAV "    ERROR!. EL DOCUMENTO YA EXISTE ! ";
      |ERROR;
  |FINSI;
  |SELECT #1#1;
  |CIERRA #1;
|FPRC;

|| **************************************************************************
|| ............................... Comprobar Cuadre
|PROCESO sumacontra0;
 swalgun = 1;
 total = total + #3#8;
|FPRC;

|DEFBUCLE sumacontra;
 #3#1;
 ;
 #5#0, #5#1, #5#2, 00001;
 #5#0, #5#1, #5#2, 99999;
 e;
 NULL, sumacontra0;
|FIN;

|PROCESO verlineas;
  cam = 1;
  lacontra = #5#8;
  |QBF lacontra;
  |SI lacontra ! "";
      |ACABA;
  |SINO;
      total   = 0;
      swalgun = 0;
      |BUCLE sumacontra;
      |SI swalgun = 0; |O total ! #5#7;
          swcuadre = 1;
          |ERROR10;
      |FINSI;
  |FINSI;
|FPRC;

|DEFBUCLE comcuablo;
 #5#1;
 ;
 #0#0, 0, 00000;
 #0#2, 9, 99999;
 e;
 NULL, verlineas;
|FIN;

|PROCESO comcuadre;
 |PUSHV 1501 1580;
 |ATRI I;
 |PINTA 1522, "Comprobando Cuadre ...";
 |ATRI 0;
 cam      = 0;
 swcuadre = 0;
 |BUCLE comcuablo;

 |SI swcuadre ! 0;
     |CONFI "1510NContrapartidas con Descuadres. ¨ Desea Continuar ? ";
     |SI FSalida ! 0;
         swcuadre = 2;
     |FINSI;
 |FINSI;
 |POPV;
|FPRC;

|| **************************************************************************
|| ............................... Crear Asientos
|PROCESO impresora;
  |SI sw_error = 0;
      |IMPRE 0;
      |SI FSalida ! 0;  |ACABA;  |FINSI;
      |INFOR "cadescua";
      |SI FSalida ! 0;  |FINIMP;  |ACABA;  |FINSI;
      sw_error = 1;
  |FINSI;
  numero = numero + 1;
  antdia = #2#0;
  antfec = #2#1;
  antdoc = #2#2;
  |PRINT;
|FPRC;

|PROCESO nuevoAsi;
  debe   = 0;
  haber  = 0;
  entfec = #5#3;
  swnuevo = 0;
  linea = -9;

  diario = #0#5;
  fecha  = #5#3;
  docume = #0#6;
  #0#6 = #0#6 + 1;

  swbueno = 0;
|FPRC;

|| -------------------------------------------------------------------------
|PROCESO apuntes;
 |SI #5#7 = 0; |ACABA; |FINSI;

 |SI tipoAct = 1;
     |HAZ nuevoAsi;
 |SINO;
     |SI entfec ! #5#3;
         |HAZ nuevoAsi;
     |FINSI;
 |FINSI;
 cam = 1;

|| .............................. PROCESO grabalinea;
  |SI swnuevo = 0;
      |HAZ cabecera;
      swnuevo = 1;
  |FINSI;
  |PINTA 1522, #5#0;
  |PINTA 1535, #5#2;
  |PINTA 1541, #5#3;

|| ..................... Asiento Cuenta
  |DDEFECTO #2;
  #2#0 = diario;
  #2#1 = fecha;
  #2#2 = docume;
  linea = linea + 10;
  #2#3 = linea;
  #2#4 = #5#0;        || Cuenta Contable
  #2#5 = #5#1;        || digito control
  #2#6 = #5#4;        || concepto
  enNumCon = #2#6; |HAZ VerConcepto; #2#24 = eaDesCon;
  #2#7 = #5#5;        || Descripcion
  #2#8 = #5#6;        || Signo
  #2#9 = #5#7;        || Importe
  |SI #2#8 = "D";
      tdebe = tdebe + #2#9;
  |SINO;
      thaber = thaber + #2#9;
  |FINSI;
  |HAZ adapta;

  |GRABA 020#2n;
  |LIBERA #2;
  |HAZ actualizaciones;

  |SI #5#6 = "D";
      signo = "H";
  |SINO;
      signo = "D";
  |FINSI;
|| ............ Contrapartidas;
  lacontra = #5#8;
  |QBF lacontra;
  |SI lacontra ! "";
      |HAZ estacontra;
  |SINO;
      |HAZ otrascontra;
  |FINSI;

  |BORRA 020#5a;
  |LIBERA #5;

  |SI swbueno = 1;
      error = "Asto Cuadrado con Contrapartida Defecto.";
      |HAZ impresora;
  |FINSI;
|FPRC;

|PROCESO estacontra;
  |DDEFECTO #2;
  #2#0 = diario;
  #2#1 = fecha;
  #2#2 = docume;
  linea = linea + 10;
  #2#3 = linea;
  #2#4 = #5#8;        || Contrapartida Contable
  #2#5 = #5#9;        || digito control
  #2#6 = #5#10;       || concepto
  enNumCon = #2#6; |HAZ VerConcepto; #2#24 = eaDesCon;
  #2#7 = #5#11;       || Descripcion
  #2#8 = signo;       || Signo
  #2#9 = #5#7;        || Importe
  |HAZ adapta;

  |GRABA 020#2n;
  |LIBERA #2;
  |HAZ actualizaciones;
|FPRC;

|PROCESO grabacontra;
  |DDEFECTO #2;
  #2#0 = diario;
  #2#1 = fecha;
  #2#2 = docume;
  linea = linea + 10;
  #2#3 = linea;
  #2#4 = cta_con;     || Contrapartida Contable
  #2#5 = dig_con;     || digito control
  #2#6 = con_con;     || concepto
  enNumCon = #2#6; |HAZ VerConcepto; #2#24 = eaDesCon;
  #2#7 = des_con;     || Descripcion
  #2#8 = signo;       || Signo
  #2#9 = imp_con;     || Importe
  |HAZ adapta;

  |GRABA 020#2n;
  |LIBERA #2;
  |HAZ actualizaciones;
|FPRC;

|PROCESO contras1;
 swalgun = 1;
 total = total + #3#8;
 cta_con = #3#4;               || Contrapartida Contable Por defecto
 dig_con = #3#5;               || digito control
 con_con = #3#6;               || concepto
 des_con = #3#7;               || Descripcion
 imp_con = #3#8;               || Importe
 |HAZ grabacontra;
|FPRC;

|DEFBUCLE contras0;
 #3#1;
 ;
 #5#0, #5#1, #5#2, 00001;
 #5#0, #5#1, #5#2, 99999;
 e;
 NULL, contras1;
|FIN;

|PROCESO otrascontra;
 total   = 0;
 swalgun = 0;
 swbueno = 0;
 |BUCLE contras0;
 |SI swalgun = 0; |O total ! #5#7;
     swbueno = 1;
     cta_con = #6#5;               || Contrapartida Contable Por defecto
     dig_con = #6#6;               || digito control
     con_con = #5#4;               || concepto
     des_con = #5#5;               || Descripcion
     imp_con = (#5#7 - total);     || Importe
     |HAZ grabacontra;
     |HAZ actuaespecial;           || Actualiza la cuenta del cuadre
 |FINSI;
|FPRC;
|| -------------------------------------------
|| -------------------------------------------

|| -------------------------------------------
|DEFBUCLE contabiliza1;
 #5#2;
 ;
 #4#0, #4#1, "01.01.1900", 00001;
 #4#0, #4#1, "31.12.2100", 99999;
 e;
 NULL, apuntes;
|FIN;

|PROCESO cabs_ctas;
 #6#0 = #4#0;
 #6#1 = #4#1;
 |LEE 101#6=;
 |SI FSalida ! 0;
     |DDEFECTO #6;
     #6#0 = #4#0;
     #6#1 = #4#1;
     |GRABA 020#6b;
 |FINSI;
 tdebe  = 0;
 thaber = 0;
 cam    = 0;
 |BUCLE contabiliza1;

 #6#3 = #6#3 + tdebe - thaber;
 #6#4 = fecha;
 |GRABA 020#6a;
 |LIBERA #6;

 |BORRA 020#4a;
 |LIBERA #4;
|FPRC;

|DEFBUCLE contabiliza0;
 #4#1;
 ;
 #0#0, 0;
 #0#2, 9;
 be;
 NULL, cabs_ctas;
|FIN;

|| **************************************************************************
|PROCESO actual; |TIPO 3;
 |SI #0#7 = "N"; |ACABA; |FINSI;

 tipoAct = #0#4;
 cam = 0;
 |HAZ comcuadre;
 |SI swcuadre = 2; |ACABA; |FINSI;
 |SI cam = 0; |ACABA; |FINSI;

|| .................. Abre fichero Auxiliar de Cuentas
  |ABRE #9;
  |LEE 101#9u;
  |SI FSalida ! 0;
      |DDEFECTO #9;
      #9#0 = 1;
  |SINO;
      #9#0 = #9#0 + 1;
  |FINSI;
  |GRABA 101#9n;
  codcon = #9#0;
  |LIBERA #9;
|| ----------------------------------------------------------

 |ABRE #1;
 |ABRE #2;
 |ABRE #6;

 |BUCLE contabiliza0;

 |SI sw_error = 1;
     |PIEINF;
     |FININF;
     |FINIMP;
 |FINSI;

 |CIERRA #1;
 |CIERRA #2;
 |CIERRA #6;
 |CIERRA #9;

|| ............... Terminar

|| ... Actualiza cabeceras de Bloques
   enContador = docume;
   enDiario   = #0#5;
   |HAZ GrabaContador;

 |SI modcon = 0;
     |ABRET #9;
     |DDEFECTO #9;
     #9#0 = codcon;
     |LEE 040#9=;
     |BORRA 101#9a;
     |CIERRAT #9;
 |SINO;
     |CORRE "camaapua.tab"
 |FINSI;
|FPRC;

|| **************************************************************************
|PROCESO actualizaciones;
  |SI #2#8 = "D";
      debe = debe + #2#9;
  |SINO;
      haber = haber + #2#9;
  |FINSI;
  |HAZ gratra;
  |HAZ act_cabecera;
  |HAZ act_diario;
|FPRC;

|| ............ Subrutina de Asientos
|PROCESO adapta;
|SI #2#8 = "D";
        #2#16 = #2#4;
        #2#17 = #2#5;
        #2#10 = "";
        #2#11 = 0;
|SINO;
        #2#16 = "";
        #2#17 = 0;
        #2#10 = #2#4;
        #2#11 = #2#5;
|FINSI;
|FPRC;

|PROCESO cabecera; || lee la cabecera.
  |DDEFECTO #1;
  #1#0 = diario;
  #1#1 = fecha;
  #1#2 = docume;
  #1#3 = docume;
  linea = -9;
  |GRABA 020#1n;
  |SI FSalida ! 0;
      #2#0 = #1#0;
      #2#1 = #1#1;
      #2#2 = #1#2;
      #2#3 = 9999;
      |LEE 040#2];
      |SI FSalida = 0;
          |LEE 040#2a;
          |SI #2#0 = #1#0; |Y #2#1 = #1#1; |Y #2#2 = #1#2;
              linea = #2#3;
          |FINSI;
      |SINO;
          |LEE 040#2u;
          |SI FSalida = 0; |Y  #2#0 = #1#0; |Y #2#1 = #1#1; |Y #2#2 = #1#2;
              linea = #2#3;
          |FINSI;
      |FINSI;
  |FINSI;
|FPRC;

|PROCESO act_cabecera;
  #1#0 = diario;
  #1#1 = fecha;
  #1#2 = docume;
  |LEE 101#1=;
  |SI #2#8 = "D";
       #1#4 = #1#4 + #2#9;
       |SI #1#09 = 0; #1#08 = #2#4; #1#09 = #2#5; |FINSI;  || contrp.HABER
  |SINO;
       #1#5 = #1#5 + #2#9;
       |SI #1#11 = 0; #1#10 = #2#4; #1#11 = #2#5; |FINSI;  || contrp.DEBE
  |FINSI;
  #1#6 = #1#4 - #1#5;        || saldo
  |GRABA 020#1a;
  |LIBERA #1;
|FPRC;

|PROCESO gratra;
  modcon = 1;               || graba el temporal para la actualiz. de ctas.
  #9#0 = codcon;
  #9#1 = #2#4;
  #9#2 = #2#5;
  #9#3 = #2#1;
  #9#4 = #2#8;
  #9#6 = #2#0;
  |LEE 101#9=;
  |SI FSalida ! 0;
      |DDEFECTO #9;
        #9#0 = codcon;
        #9#1 = #2#4;
        #9#2 = #2#5;
        #9#3 = #2#1;
        #9#4 = #2#8;
        #9#6 = #2#0;
      |GRABA 020#9b;
  |FINSI;
  #9#5 = #9#5 + #2#9;
  |GRABA 020#9a;
  |LIBERA #9;
|FPRC;

|PROCESO act_diario; || actualiza diario
  |ABRE #10;
  #10#0 = #0#5;
  |LEE 101#10=;
  |SI FSalida ! 0;
      |DDEFECTO #10;
      #10#0 = #0#5;
      #10#1 = "<DIARIO SIN DEFINIR>";
      |GRABA 020#10b;
  |FINSI;

  |SI #2#8 = "D";
      #10#2 = #10#2 + #2#9;
  |SINO;
      #10#3 = #10#3 + #2#9;
  |FINSI;
  |GRABA 020#10a;
  |LIBERA #10;
  |CIERRA #10;
|FPRC;

|PROCESO actuaespecial;
 |ABRE #11;
 #11#0 = #2#4;
 #11#1 = #2#5;
 |LEE 011#11=;
 |SI FSalida = 0;
     cuedebe  = 0;
     cuehaber = 0;
     cuediario = #2#0;
     cuefecha  = #2#1;
     |SI #2#8 = "D";
         cuedebe =  #2#9;
     |SINO;
         cuehaber =  #2#9;
     |FINSI;
     |HAZ a_cuenta;
     |GRABA 020#11a;
 |FINSI;
 |LIBERA #11;
 |CIERRA #11;
|FPRC;

|PROCESO a_actualiza;                || rutina 'a_cuentas'
  ini = dig1;
  fecalf = cuefecha;
  mes = fecalf % 402;
  mesfec = mes;
  |SI ini = 1;
      mesac = mesfec;
  |SINO;
      mesac = (mesfec - ini) + 1;
      |SI mesac [ 0;
          mesac = (13 - ini) + mesfec;
      |FINSI;
  |FINSI;
  a = (mesac * 3) + 9;
  b = a + 1;
  c = a + 2;
  #11a   = #11a   + cuedebe;
  #11#51 = #11#51 + cuedebe;
  #11b   = #11b   + cuehaber;
  #11#52 = #11#52 + cuehaber;
  #11c   = #11a   - #11b;
  #11#53 = #11#51 - #11#52;
|FPRC;

|PROCESO a_cuenta;
  |SI cuediario = 0;
      #11#9  = #11#9  + cuedebe;        || diario cero (saldos iniciales)
      #11#51 = #11#51 + cuedebe;
      #11#10 = #11#10 + cuehaber;
      #11#52 = #11#52 + cuehaber;
      #11#11 = #11#9  - #11#10;
      #11#53 = #11#51 - #11#52;
  |SINO;
      |SI cuediario = 99;
          #11#48 = #11#48 + cuedebe;   || diario 99 (saldos finales)
          #11#51 = #11#51 + cuedebe;
          #11#49 = #11#49 + cuehaber;
          #11#52 = #11#52 + cuehaber;
          #11#50 = #11#48 - #11#49;
          #11#53 = #11#51 - #11#52;
      |SINO;
          |HAZ a_actualiza;           || saldos del mes correspondiente
      |FINSI;
  |FINSI;
|FPRC;
