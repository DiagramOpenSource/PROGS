|FICHEROS;
    cacambi1 #0;
    canempre #30;
    cacuenta #10;   || plan contable
    cacambiw #90;   || def. trabajo
    cacambix #91;   || tmp. trabajo
|FIN;

|VARIABLES;
    alfa1 = "";
    alfa2 = "";
    alfa3 = "";
    alfa4 = "";
    alfa5 = "";
    nume1 = 0;
    nume2 = 0;
    nume3 = 0;
    nume4 = 0;
    para1 = 0;
    para2 = 0;
    para3 = 0;
    para4 = 0;
    fech1 = @;
    fech2 = @;
    fech3 = @;
    fech4 = @;
    elsw1 = 0;
    elsw2 = 0;
    elsw3 = 0;
    elsw4 = 0;
    FEstado = 0;
    seleccio = 0;
    modo = 0;
    sw_rup = 0;

    a_empres = "";
    d_empres = "";
    a_nivels = 0;
    d_nivels = 0;
    longi = 0;
    listo = 0;
    a_cuentas = 0;
    d_cuentas = 0;

    dire = "";
    buf_dir = 0;
    ele_dir = 0;
    fiche = "";
    traba1 = "";
    traba = "";
    &r_emp = 0;
    &r_per = 0;
    varcamp = 0;
  temporal1 = "";
  temporal2 = "";
|FIN;
____________________________________/ CALCULOS DE PANTALLA
|PROCESO mayus; |TIPO 0;
  #0Campo = #0Campo > #0Campo;
  |PINTA #0Campo;
|FPRC;

|PROCESO consulta;
  |SI FSalida ! 9;  |ACABA;  |FINSI;
  r_emp = 0;
  |SUB_EJECUTA "caconemp.run";
  |ERROR;
  |SI r_emp = 0; |ACABA; |FINSI;
      #0Campo = r_emp;
      varcamp = Campo + 1;
      #0varcamp = r_per;
      |PTEC 802;
      |PTEC 802;
|FPRC;


|PROCESO existe; |TIPO 0;
  |DFICB alfa1;
  |QBF alfa1;
  |SI FSalida = 2;  |ACABA;  |FINSI;
  |HAZ lee_actual;
  |SI #30#2 ! #0#0;|O #30#3 ! #0#1;
      |ABRE #30;
      #30#2 = #0#0;
      #30#3 = #0#1;
      |LEE 000#30=;
      |SI FSalida ! 0;
          |MENAV "    Empresa/Ejercicio no definida ...";
          |ERROR;
      |SINO;
           a_empres = #30#0;
           a_empres = ("000000" + a_empres) % -106;
           |PARA para1 = 0; |SI para1 [ 7; |HACIENDO para1 = para1 + 1;
                 para2 = para1 + 13;
                 #90para1 = #30para2;
           |SIGUE;
           a_nivels = #90#0;
           |ATRI I;     |PINTA 1021, #30#1;  |ATRI 0;
      |FINSI;
      |CIERRA #30;
  |SINO;
      |MENAV "    Empresa Actual ...";
      |ERROR;
  |FINSI;
|FPRC;

|PROCESO lee_actual;
  |DFICE alfa1;
  |QBF alfa1;
  alfa1 = alfa1 % -107;
  alfa2 = alfa1 % 105;      || empresa
  alfa3 = alfa1 % 601;      || ejercicio

  |ABRE #30;
  #30#2 = alfa2;
  #30#3 = alfa3;
  |LEE 020#30=;        || tiene que estar!
  |CIERRA #30;

  d_empres = #30#0;
  d_empres = ("0000000" + d_empres) % -106;
  |PARA para1 = 10; |SI para1 [ 16; |HACIENDO para1 = para1 + 1;
        para2 = para1 + 3;
        #90para1 = #30para2;
  |SIGUE;
  d_nivels = #90#10 + 10;
|FPRC;
____________________________________/
|PROCESO caso2;
  |SI listo ! 0;  |ACABA;  |FINSI;
  nume1 = #10#1;                                   || si es el ultimo nivel
  |SI #10#1 = a_nivels;|Y #90nume1 < #90d_nivels;  ||/y los digitos del
      nume2 = #90d_nivels - #90nume1;              ||/ultimo nivel destino
      alfa1 = "0" * nume2;                         ||/son mayores
      alfa2 = #10#0;    |QBF alfa2;
      |DDEFECTO #91;
      #91#0 = #10#0;         || cuenta actual
      #91#1 = #10#1;         || nivel actual
      #91#2 = alfa2 + alfa1; || cuenta destino
      #91#3 = d_nivels - 10; || nivel destino
      d_cuentas = d_cuentas + 1;
      #91#4 = d_cuentas + 10000;
      #91#5 = #10#2;         || nombre cuenta
      |GRABA 020#91n;
      listo = 1;
  |FINSI;
|FPRC;

|PROCESO caso1;
  |SI listo ! 0;  |ACABA;  |FINSI;
  |PARA para1 = 11; |SI para1 [ 16; |HACIENDO para1 = para1 + 1;
        |SI longi = #90para1;
            |DDEFECTO #91;
            #91#0 = #10#0;          || cuenta actual
            #91#1 = #10#1;          || nivel actual
            #91#2 = #10#0;          || cuenta destino
            #91#3 = para1 - 10;     || nivel destino
            d_cuentas = d_cuentas + 1;
            #91#4 = d_cuentas;
            #91#5 = #10#2;          || nombre cuenta
            |GRABA 020#91n;
            listo = 1;
            |SAL;
        |FINSI;
  |SIGUE;
|FPRC;

|PROCESO cacuenta1;
    alfa1 = #10#0;  |QBF alfa1;
    alfa2 = alfa1 % 0;
    longi = alfa2;
    listo = 0;      || switch para saber si la cuenta esta cambiada

|HAZ caso1;         || cambia nivel, no cambia longitud
                    ||/no cambia nivel, no cambia longitud
|HAZ caso2;         || cambia nivel, cambia longitud
                    ||/no cambia nivel, cambia longitud
|FPRC;

|DEFBUCLE cacuenta;
 #10#1;
 ;
 "            ", 0;
 "zzzzzzzzzzzz", 9;
 ;
 NULL, cacuenta1;
|FIN;
____________________________________/
|PROCESO registros1;
    a_cuentas = a_cuentas + 1;
|FPRC;

|DEFBUCLE registros;
 #10#1;
 ;
 "            ", 0;
 "zzzzzzzzzzzz", 9;
 ;
 NULL, registros1;
|FIN;
____________________________________/ CALCULO PRINCIPAL Y COPIA DE EMPRESA
|PROCESO copia_empresa;
  |DFICB alfa1;
  alfa2 = alfa1 + a_empres;
  alfa3 = alfa1 + d_empres;

  alfa2 = alfa2 + "/"; || borrando el fichero de preparacion de la empresa
  alfa3 = alfa3 + "/"; ||/origen no se machacara el que pudiera existir
  |PATH_DAT #91 alfa2;     ||/en la empresa actual
  |DELINDEX #91;
  |PATH_DAT #91 alfa3;

    dire = "";
|DIRECTORIO dire;
    fiche = dire + "agicont/fich/agicont.dir";
|LEESECUENCIAL fiche, buf_dir, ele_dir, 1, 0;
|SI ele_dir > 0;
    |PARA para1 = 1; |SI para1 [ ele_dir; |HACIENDO para1 = para1 + 5;
          para2 = para1 + 3;
          |DEL_BUF buf_dir, para2, alfa1;
          alfa1 = alfa1 % 101;
          |SI alfa1 = "1";|O alfa1 = "2";
              |DEL_BUF buf_dir, para1, alfa1;
              traba1 = alfa1;
              |HAZ copiando;
          |FINSI;
    |SIGUE;
|SINO;
        alfa1 = "    [" + fiche + "] NO accesible en lectura.";
    |MENAV alfa1;
|FINSI;
|FINDIM buf_dir;
|FPRC;

|PROCESO copiando;
  traba = " COPIANDO FICHERO: " + traba1 + "        ";
  |PINTA 2211, traba;
  temporal1 = alfa2 +  traba1 + ".ixx";
  temporal2 = alfa3 + traba1 + ".ixx";
  |COPIA_FICHERO temporal1,temporal2;
  |SI FSalida ! 0;  |ACABA;  |FINSI;
  temporal1 = alfa2 + traba1 + ".dat";
  temporal2 = alfa3 + traba1 + ".dat";
  |COPIA_FICHERO temporal1,temporal2;
  |SI FSalida ! 0;
      temporal2 = alfa3 + traba1 + ".ixx";
      |FBORRA temporal2;
      |ACABA;
  |FINSI;
  temporal1 = alfa2 + traba1 + ".ddx";
  temporal2 = alfa3 + traba1 + ".ddx";
  |COPIA_FICHERO temporal1,temporal2;
|FPRC;

|PROCESO tipo3; |TIPO 3;
  |SI #0#2 ! "SI";  |ACABA;  |FINSI;
  |HAZ copia_empresa;
  |SI #0#3 = "N";
      |ATRI P;  |MENSA "    Preparando ...";    |ATRI 0;
      |DELINDEX #91;
      |ABRE #91;
      |BUCLE registros;
      |BUCLE cacuenta;
      alfa1 = "    Cuentas Origen: " + a_cuentas + " <> Cuentas Destino: " + d_cuentas;
      |MENAV alfa1;
      |CIERRA #91;
      |ATRI R;  |BLIN 4;                      |ATRI 0;
  |FINSI;
|FPRC;
