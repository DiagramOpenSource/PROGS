|FICHEROS;
  careccue #0;
  caconimp #8;
  canempre #30;
|FIN;

|VARIABLES;
  &balance = 0;
  &pathbal = "";
  &entrada = 0;
  nume = 0;
  desde = 0;
  hasta = 0;
  desde1 = 0;
  men6 = "    SEs correcto (S/N)    ";
  &swqueh = 0;
  alfa2 = "";
  alfa3 = "";
|FIN;

|INCLUYE acceso_i;
|INCLUYE varnue_i;       || recuperacion de saldos
|INCLUYE recupe_i;       || recuperacion de saldos

|PROCESO acumular;
  |PDEFECTO #1,9,54;
  |GRABA 020#1a;
  |LIBERA #1;
|FPRC;

|DEFBUCLE borraacu;
 #1#1;
 3;
 "            ",0,"N";
 "zzzzzzzzzzzz",9,"N";
 be;
 NULL, acumular;
|FIN;

|PROCESO acumulasaldo;
  nume = 9;
  desde1 = 0; hasta = 0;
  |SI #1#1 = 6;  hasta = 224;  |FINSI;
  |SI #1#1 = 5;  hasta = 179;  |FINSI;
  |SI #1#1 = 4;  hasta = 134;  |FINSI;
  |SI #1#1 = 3;  hasta = 89;   |FINSI;
  |SI #1#1 = 2;  hasta = 44;   |FINSI;
  |SI #1#1 = 1;  |ACABA;       |FINSI;
  |PARA desde = 0;  |SI desde [ hasta;  |HACIENDO desde = desde + 1;
        #0desde  = #0desde + #1nume;
        nume = nume + 1;
        |SI nume > 53;  nume = 9;  |FINSI;
  |SIGUE;
|FPRC;

|PROCESO grabasaldo;
  |PDEFECTO #1,9,54;
  nume = 9;
  |SI #1#1 = 6;
      |MENAV "     Atencion!! Existen Cuentas de Nivel 6 SIN Pase Directo";
      |ACABA;
  |FINSI;
  desde1 = 0; hasta = 0;
  |SI #1#1 = 5;  desde1 = 180;  hasta = 224;  |FINSI;
  |SI #1#1 = 4;  desde1 = 135;  hasta = 179;  |FINSI;
  |SI #1#1 = 3;  desde1 = 90;   hasta = 134;  |FINSI;
  |SI #1#1 = 2;  desde1 = 45;   hasta = 89;   |FINSI;
  |SI #1#1 = 1;  desde1 = 0;    hasta = 44;   |FINSI;
  |PARA desde = desde1;  |SI desde [ hasta;  |HACIENDO desde = desde + 1;
        #1nume = #0desde;
        nume = nume + 1;
        #0desde = 0;
  |SIGUE;
|FPRC;

|PROCESO tratacuen;
  |SI #1#3 = "S";
      |HAZ acumulasaldo;
  |SINO;
      |HAZ grabasaldo;
      |GRABA 020#1a;
      |LIBERA #1;
  |FINSI;
|FPRC;

|DEFBUCLE sumacuen;
 #1#3;
 ;
 "            ",0;
 "zzzzzzzzzzzz",9;
 be;
 NULL, tratacuen;
|FIN;

|PROGRAMA;

  |CLS;
  |CABEZA "Recuperacion Plan Contable";
  |HAZ inicio;
  |HAZ eVarEmpresa;
  |SI balance = 0;
      swqueh = 1;
      |PINPA #0#0;
      |CONFI men6;
      |SI FSalida ! 0;  |ACABA;  |FINSI;
  |SINO;
      |PATH_DAT #1 pathbal;
      |PATH_DAT #8 pathbal;
      |PATH_DAT #3 pathbal;
      |PATH_DAT #5 pathbal;
      |PATH_DAT #10 pathbal;
      alfa1 = pathbal % -107;
      alfa2 = alfa1 % 105; alfa3 = alfa1 % 601;
      |ABRE #30;
      |DDEFECTO #30;
      #30#2 = alfa2;
      #30#3 = alfa3;
      |LEE 001#30=;
      |CIERRA #30;
      |PINPA #1#0;
  |FINSI;
  || ...  |BUCLE borraacu;
  |SI swqueh = 1;
      |PDEFECTO #0,0,225;
      |BUCLE sumacuen;
  |SINO;
      |HAZ carecupe;
  |FINSI;
  |LIBERA #8;
  |CIERRAT #8;
  |ABRE #8;
  |LEE 111#8p;
  |SI FSalida ! 0;
      |DDEFECTO #8;
      #8#3 = "S";
      |GRABA 011#8n;
  |SINO;
      #8#3 = "S";
      |GRABA 011#8a;
  |FINSI;
  |LIBERA #8;
  |CIERRA #8;
|FPRO;

|PROCESO carecupe;
  |NOPARA;
  conforme = "S";
  fini = "01.01.0000";
  ffin = "31.12.9999";
  cini = 1;
  cfin = 999;
  |HAZ recuperacion;
  |SIPARA;
|FPRC;
