|FICHEROS;
  ca8m0000 #0;
  ca8m0001 #1,MANTE;
  ca8m0002 #2,MANTE;
  ca8z0001 #10;

  ca8m0021 #21;
  ca8m0022 #22;

  ca8w0001 #50,MANTE;
|FIN;

|VARIABLES;
 aParam    = "";
 &nBalNum  = 0;
 &nBalance = 0;
 &aLucrat  = "";
 nInkey    = 0;
 aAlfa1    = "";
 nNumero   = 0;
 &enDPlan     = 0;
 &wPatOfi     = 0;
 &wTipoPat    = 0;
 nOpera       = 0;
|FIN;

|INCLUYE varnue_i;
|INCLUYE acceso_i;

|| ******************************************** Calculos Pantalla
|CALCULO wTipo12; |TIPO 12;
|FCAL;

|CALCULO descri;  |TIPO 0;
  nInkey = FSalida;
  |SI nInkey = 1; nInkey =  -1; |ACABA; |FINSI;

  |SI nInkey = 10;
      |HAZ CopiarNum1;
      |ERROR;
      |ACABA;
  |FINSI;

  enDPlan  = #10#4;
  nBalNum  = #10#2;
  wTipoPat = 1;
  |HAZ eVerEsPatron;
  |SI wPatOfi = 1;
      |CONFI "    NCodigo de Patron Oficial. Continuar S/N";
      |SI FSalida ! 0;
          |ERROR;
          |ACABA;
      |FINSI;
  |FINSI;

  |SI nInkey = 11;
      |CONFI "    NConfirmacion baja(S/N) ...";
      |SI FSalida = 0;
          |HAZ Borrar;
          |ERROR;
      |FINSI;
  |FINSI;
|FCAL;

|PROCESO PantaEspecial;
  |SI nOpera = 0;
      |SI aLucrat = "S";
          |C_A #1#9,1,38;
          |C_M #2#13, 1, "S"; |C_V #2#13, 1, "S";
          |C_P #2#8,  1, 1365;
          |C_P #2#9,  1, 1369;
          |C_P #2#10, 1, 1375;
          |C_P #2#11, 1, 1377;

          |PINTA 0764, "ÂÄÄÄÄÄÄÄÄÄÄÄÄ";
          |ATRI P; |PINTA 0765, "Casillas:"; |ATRI 0;
          |PINTA 0864, "³";
          |ATRI S; |PINTA 0865, "Ctas.Anu."; |ATRI 0;
          |PINTA 0964, "³";
          |ATRI S; |PINTA 0965, "Nota Mem."; |ATRI 0;
          |PINTA 1064, "³";
          |ATRI S; |PINTA 1065, "Imp.Socie."; |ATRI 0;
          |PINTA 1164, "Ã";
          |ATRI P; |PINTA 1165, " Relacion Ctas "; |ATRI 0;
          |PINTA 1264, "³";
          |ATRI R; |PINTA 1265, "Ln. Ctas. S C I"; |ATRI 0;

          |PINTA 1364, "³   ³           ³";
          |PINTA 1464, "³   ³           ³";
          |PINTA 1564, "³   ³           ³";
          |PINTA 1664, "³   ³           ³";
          |PINTA 1764, "³   ³           ³";
          |PINTA 1864, "³   ³           ³";
          |PINTA 1964, "³   ³           ³";
          |PINTA 2064, "³   ³           ³";
          |PINTA 2164, "³   ³           ³";
          |PINTA 2264, "³   ³           ³";
          |PINTA 2364, "ÁÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÙ";
      |FINSI;
  |FINSI;
  |SI nOpera = 1;
      |C_A #1#9,1,40;
      |C_M #2#13, 1, "N"; |C_V #2#13, 1, "N";
      |C_P #2#8,  1, 1367;
      |C_P #2#9,  1, 1371;
      |C_P #2#10, 1, 1377;
      |C_P #2#11, 1, 1379;
      |PINPA #0#10;
      |PINDA #0#10;
  |FINSI;
|FPRC;
|| /////////////////////// PROCESOS DE TRABAJO \\\\\\\\\\\\\\\\\\\\
|RUTINA min;
  #1#14 = #10#4;
  #1#0  = #10#2;
  #1#1  = #10#0;
  #1#2  = 0;
  #1#3  = " ";
  #1#4  = 00;
  #1#5  = 0;
  #1#6  = 00;
  #1#7  = 00;
  #1#10 = 0;
  |SI nBalance = 0;
      #1#2 = 0;
      #1#3 = "A";
  |FINSI;
  |SI nBalance = 1;
      #1#2 = 1;
      #1#3 = "R";
  |FINSI;
  |SI nBalance = 2;
      #1#2 = 6;
      #1#3 = "E";
  |FINSI;
  |SI nBalance = 4;
      #1#2 = 7;
      #1#3 = "A";
  |FINSI;
|FRUT;

|RUTINA max;
  #1#14 = #10#4;
  #1#0  = #10#2;
  #1#1  = #10#0;
  #1#2  = 9;
  #1#3  = "z";
  #1#4  = 99;
  #1#5  = 9;
  #1#6  = 99;
  #1#7  = 99;
  #1#10 = 9;
  |SI nBalance = 0;
      #1#2 = 0;
      #1#3 = "P";
  |FINSI;
  |SI nBalance = 1;
      #1#2 = 5;
      #1#3 = "R";
  |FINSI;
  |SI nBalance = 2;
      #1#2 = 6;
      #1#3 = "E";
  |FINSI;
  |SI nBalance = 2;
      #1#2 = 9;
      #1#3 = "Z";
  |FINSI;
|FRUT;

|PROCESO Tipo3;  |TIPO 3;
  aLucrat = #10#5;
  nOpera  = 0;
  |SI nBalance = 0; |O nBalance = 4;
      |ENTLINEAL #1#1, -5, 2, 22, 0, min, max;
  |FINSI;
  |SI nBalance = 1;
      nOpera  = 0;|HAZ PantaEspecial;
      |ENTLINEAL #1#1, -4, 2, 22, 0, min, max;
      nOpera  = 1;|HAZ PantaEspecial;
  |FINSI;
  |SI nBalance = 2;
      |ENTLINEAL #1#1, -6, 2, 22, 0, min, max;
  |FINSI;
|FPRC;
|| **************************************************************************

|PROCESO BorroCtasD;
 |BORRA 140#22a;
 |LIBERA #22;
|FPRC;

|DEFBUCLE BorroCtasD;
 #22#1;
 ;
 #21#14, #21#0, #21#1, #21#2, #21#3, #21#4, #21#5, #21#6, #21#7, #21#10, 000;
 #21#14, #21#0, #21#1, #21#2, #21#3, #21#4, #21#5, #21#6, #21#7, #21#10, 999;
 be;
 NULL, BorroCtasD;
|FIN;

|PROCESO BorroDesglose;
 |BORRA 140#21a;
 |LIBERA #21;
 |BUCLE BorroCtasD;
|FPRC;

|DEFBUCLE BorroDesglose;
 #21#1;
 ;
 #10#4, nNumero, #10#0, 0, " ",00,00,00,00,0;
 #10#4, nNumero, #10#0, 9, "z",99,99,99,99,9;
 be;
 NULL, BorroDesglose;
|FIN;

|PROCESO BorroCtas;
 |BORRA 140#2a;
 |LIBERA #2;
|FPRC;

|DEFBUCLE BorroCtas;
 #2#1;
 ;
 #1#14, #1#0, #1#1, #1#2, #1#3, #1#4, #1#5, #1#6, #1#7, #1#10, 000;
 #1#14, #1#0, #1#1, #1#2, #1#3, #1#4, #1#5, #1#6, #1#7, #1#10, 999;
 be;
 NULL, BorroCtas;
|FIN;

|PROCESO BorroLineas;
 |BORRA 140#1a;
 |LIBERA #1;
 |BUCLE BorroCtas;
|FPRC;

|DEFBUCLE BorroLineas;
 #1#1;
 ;
 #10#4, nNumero, #10#0, 0, " ",00,00,00,00,0;
 #10#4, nNumero, #10#0, 9, "z",99,99,99,99,9;
 be;
 NULL, BorroLineas;
|FIN;

|PROCESO Borrar;
 nNumero = #10#2;
 |BUCLE BorroLineas;
 |BUCLE BorroDesglose;

 |SI nNumero ! 0; |Y nNumero ! 1; |Y nNumero ! 10;
     |Y nNumero ! 30; |Y nNumero ! 31; |Y nNumero ! 32;
     nBalNum = nNumero;
     |HAZ eBorraCa8m0000;
 |FINSI;
 |PINPA #0#10;
 #10#2 = 0;
 #10#3 = "";
 |PINDA #0#10;
|FPRC;

|| ****************** PROCESO para copiar a¤os
|PROCESO CopiaCtas;
     |SALVA_FICHA #2;
     #2#0 = #50#1;
     |GRABA 020#2n;
     |REPON_FICHA #2;
     |LIBERA #2;
|FPRC;

|DEFBUCLE CopiaCtas;
 #2#1;
 ;
 #1#14, #1#0, #1#1, #1#2, #1#3, #1#4, #1#5, #1#6, #1#7, #1#10, 000;
 #1#14, #1#0, #1#1, #1#2, #1#3, #1#4, #1#5, #1#6, #1#7, #1#10, 999;
 e;
 NULL, CopiaCtas;
|FIN;

|PROCESO CopiarLin;
     |SALVA_FICHA #1;
     #1#0 = #50#1;
     |GRABA 020#1n;
     |REPON_FICHA #1;
     |LIBERA #1;
     |BUCLE CopiaCtas;
|FPRC;

|DEFBUCLE CopiaLineas;
     #1#1;
     ;
     #10#4, #50#0, #10#0, 0, " ", 00, 00, 00, 00, 0;
     #10#4, #50#0, #10#0, 9, "z", 99, 99, 99, 99, 9;
     be;
     NULL, CopiarLin;
|FIN;


|PROCESO LeeDefinicion;
 |SI Campo = 1; |Y #50#1 = #50#0;
     |MENAV "    Error, origen y destino igual";
     |ERROR;
     |ACABA;
 |FINSI;

 |SI Campo = 1;
     enDPlan  = #10#4;
     nBalNum  = #50#1;
     wTipoPat = 1;
     |HAZ eVerEsPatron;
     |SI wPatOfi = 1;
         |CONFI "    NCodigo de Patron Oficial. Continuar S/N";
         |SI FSalida ! 0;
             |ERROR;
             |ACABA;
         |FINSI;
     |FINSI;
 |FINSI;

 |ABRE #0;
 #0#0 = #50Campo;
 |LEE 101#0=;
 |SI FSalida = 0;
     |SI Campo = 0; #50#2 = #0#1; |PINTA #50#2; |FINSI;
     |SI Campo = 1;
         #50#3 = #0#1; |PINTA #50#3;
         |CONFI "    NYa existe Definicion, Continuar ";
         |SI FSalida ! 0;
             |ERROR;
         |FINSI;
     |FINSI;
 |SINO;
     |SI Campo = 1; #50#3 = #50#2; |PINTA #50#3; |FINSI;
     |SI Campo = 0;
         |MENAV "    No existe definicion Origen ";
         |ERROR;
     |FINSI;
 |FINSI;
 |LIBERA #0;
 |CIERRA #0;
|FPRC;

|CALCULO CopiarNum1;

 |PUSHV 0809 1457;
 |PINPA #0#50;
 #50#0 = #10#2;
 #50#1 = #10#2 + 1; |SI #50#1 > 0; #50#1 = 1; |FINSI;
 #50#4 = "Definicion de Balances";
 |PINDA #0#50;
 |ENDATOS #1#50;
 |SI FSalida ! 0;
     |POPV;
     |ACABA;
 |FINSI;

 |ABRE #1;
 |DDEFECTO #1;
 #1#14= #10#4;
 #1#0 = #50#0;
 #1#1 = #10#0;
 |LEE 001#1];
 |SI FSalida ! 0; |O #1#0 ! #50#0; |O #1#1 ! #10#0; |O #1#14 ! #10#4;
     |MENAV "    Copia cancelada. No existe Tabla Origen ...";
     |POPV;
     |CIERRA #1;
     |ACABA;
 |FINSI;

 |DDEFECTO #1;
 #1#14= #10#4;
 #1#0 = #50#1;
 #1#1 = #10#0;
 |LEE 101#1];
 |SI FSalida = 0; |Y #1#0 = #50#1; |Y #1#1 = #10#0; |Y #1#14 = #10#4;
     |AVISO;
     |CONFI "    NRegrabar Tabla Destino YA EXISTENTE (S/N): ";
     |SI FSalida ! 0;
         |MENAV "    Copia cancelada ...";
         |POPV;
         |CIERRA #1;
         |ACABA;
     |SINO;
         |CIERRA #1;
         nNumero = #1#0;
         |BUCLE BorroLineas;
     |FINSI;
 |SINO;
     |CIERRA #1;
 |FINSI;

 |ABRE #0;
 #0#0 = #50#1;
 |LEE 101#0=;
 |SI FSalida ! 0;
     #0#1 = #50#3;
     |GRABA 020#0n;
 |SINO;
     #0#1 = #50#3;
     |GRABA 020#0a;
 |FINSI;
 |LIBERA #0;
 |CIERRA #0;

 |BUCLE CopiaLineas;
 |POPV;

 #10#2 = #50#1;
 #10#3 = #50#3;
 |PTEC 809;
 |PTEC 808;
|FCAL;

|| **************************************************************************
|PROGRAMA;
   aParam = PARAMETRO; |QBF aParam;
   aAlfa1 = aParam % 205;
   |SI aAlfa1 = "";
       |HAZ inicio;
       |HAZ eVarEmpresa;
       |SI enEjercicio < 2008;
           |MENAV "    Plan Contable de 2008. No modificables en este Ejercicio";
           |ACABA;
       |FINSI;
   |SINO;
       enEjerBal = aAlfa1;
       |SI enEjerBal < 2008; enEjerBal = 2008; |FINSI;
   |FINSI;

   aParam = aParam % 101;
   |SI aParam = ""; aParam = "0"; |FINSI;
   nBalance   = aParam;

   |DDEFECTO #10;
   #10#0 = nBalance;
   #10#4 = enEjerBal;
   |C_M #2#11,1,"S"; |C_V #2#11, 1, "S";
   |C_M #2#13,1,"N"; |C_V #2#13, 1, "N";
   |SI #10#0 = 0;
       #10#1 = "BALANCE DE SITUACION AL CIERRE DEL EJERCICIO";
       |C_M #1#2,  1, "N"; |C_V #1#2,  1, "N";
       |C_P #1#7,1,0918;
     ||  |C_M #1#10, 1, "N"; |C_V #1#10, 1, "N";
   |FINSI;
   |SI #10#0 = 1;
       #10#1 = "CUENTA DE PERDIDAS Y GANACIAS CORRESPONDIENTE AL EJERCICIO ";
       |C_M #1#3, 1, "N"; |C_V #1#3, 1, "N";
       |C_P #1#7,1,0918;
   |FINSI;
   |SI #10#0 = 2;
       #10#1 = "ESTADO DE INGRESOS Y GASTOS RECONOCIDOS";
       |C_M #1#2,  1, "N"; |C_V #1#2,  1, "N";
       |C_M #1#3,  1, "N"; |C_V #1#3, 1, "N";
       |C_M #1#10, 1, "N"; |C_V #1#10, 1, "N";
       |C_P #1#4,1,0904;
   |FINSI;
   |SI #10#0 = 4;
       #10#1 = "ESTADO DE FLUJO DE EFECTIVO";
       |C_M #1#2,  1, "N"; |C_V #1#2,  1, "N";
       |C_P #1#7,1,0918;
       |C_V #1#12, 1, "N"; |C_M #1#12, 1, "N";
       |C_M #2#11,1,"N";   |C_V #2#11, 1, "N";
       |C_M #2#13,1,"S";   |C_V #2#13, 1, "S";
   |FINSI;

   |CLS;
   |CABEZA "Defincion de Balances";
   |PINPA #0#10;
   |PINDA #0#10;
   |SI #10#0 = 0; |O #10#0 = 4;
       |ATRI P; |PINTA 0818, "P/Sp"; |ATRI 0;
       |SI #10#0 = 4;
           |ATRI R; |PINTA 1279, "I"; |ATRI 0;
           |PINTA 1066, "ÃÄÄÄÄÄÄÄÄÄÄÄÄÄ´";
           |PINTA 1166, "³";
           |PINTA 1180, "³";
       |FINSI;
   |FINSI;
   |SI #10#0 = 1;
       |ATRI P; |PINTA 0802, "B";
       |ATRI P; |PINTA 0818, "P/Sp"; |ATRI 0;
   |FINSI;
   |SI #10#0 = 2;
       |PINTA 0701, "ÚÄÄ";
       |ATRI P; |PINTA 0802, "Agrup"; |ATRI 0;
       |PINTA 0903, " "; |PINTA 1003, " ";
       |PINTA 1103, " "; |PINTA 1203, " ";
       |PINTA 1303, " "; |PINTA 1403, " ";
       |PINTA 1503, " "; |PINTA 1603, " ";
       |PINTA 1703, " "; |PINTA 1803, " ";
       |PINTA 1903, " "; |PINTA 2003, " ";
       |PINTA 2103, " "; |PINTA 2203, " ";
       |PINTA 2301, "ÀÄÄ";
   |FINSI;

   FSalida = 1;
   |PARA; |SI 0 < FSalida; |HACIENDO;
          |ENDATOS #1#10;
          |SI 0 [ FSalida;
              |SI nInkey = -1; |SAL; |FINSI;
              |HAZ Tipo3;
              FSalida = 1;
          |FINSI;
   |SIGUE;
|FPRO;
