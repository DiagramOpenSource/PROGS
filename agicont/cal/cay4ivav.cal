|FICHEROS;
    cay4ivav #0;
    caivarep #1;
    cacuenta #3;
    cacongru #6;
    calibros #10;

    cabucem1 #100;
    caivarea #120;
    caivaref #112;
|FIN;

|VARIABLES;
    FEstado = 0;
    Handle  = 0;
    Comodin = "";
    informe1 = "";
    informe2 = "";
    campo    = 0;
    &entrada = 1;
    sw = 0;
    sw_pie = 0;
    contaiva = 0;
    contarec = 0;
    tiva11 = 0;
    sw_punt = 0;
    pepe = 0;
    fecalf = "";
    men1   = "    NO EXISTE EMPRESA";
    men2   = "    NO existe el Grupo de Conceptos";
    mensa1 = "    ERROR ESTA FECHA NO CORRESPONDE AL EJERCICIO";
    traba1 = "";
    aAlfa = "";
    modo = 0;
    x = 0;
    y = 0;
    z = 0;
    p = 0;
    hasta = 0;
    fectra = @;
    fectrac = @;
    blanco = "  ";
    ultimo = "zz";
    &regis = 0;
    &varalf = "";
    &total1 = 0;
    &total2 = 0;
    &total3 = 0;
    &total4 = 0;
    &total5 = 0;
    sw_total = 0;
    &tb = 0;
    &ti = 0;
    &tr = 0;
    &tt = 0;
    &td = 0;
    &te = 0;
    &descri0 = "";
    &descri1 = "";
    &descri2 = "";
    &varnum1 = 0;
    &varnum2 = 0;
    &varnum3 = 0;
    &tiva1 = 0;
    &tiva3 = 0;
    &trec1 = 0;
    &trec3 = 0;
    &ttotal1 = 0;
    &ttotal3 = 0;
    &swlin = 0;
    &hoja = 0;
    &fecha_i = @;
    &fecha_n = @;
    &fecha_a = "";
    &fecha_b = "";
    &esw     = 0;
    &fecinf = @;

    &basenod = 0;
    &cuotnod = 0;

    &base = 0;
    &tpiva = 0;
    &civa = 0;
    &ree = 0;
    &cree = 0;
    &vlinea = "";
    &vtitol = "";
    &heescrito = 0;
    zz = 0;
    prim = 0;

    {10}iva = 0;
    {10}baseiva = 0;
    {10}cuotiva = 0;
    {10}rec = 0;
    {10}baserec = 0;
    {10}cuotrec = 0;

    runnom = "";
    sw_con = 0;
    alfa1 = "";
    alfa2 = "";
    alfa3 = "";
    alfa4 = "";
    para1 = 0;
    para2 = 0;
    para3 = 0;
    para4 = 0;

    uno = 0;
    dos = 0;
    titulo1    = "";
    &titulo2   = "";
    dinver1    = "";
    hinver1    = "";
    Comodin1   = "";
    Numeracion = 0;
    &enBasImp  = 0;
    &eaNomImp  = "";
    &eaSerie   = "";
    &eaLegEmp = "";
    &eaLegPer = "";
    &efFecLeg  = @;
    &eaFormato = "";
    &eaUnidadBasico = "";
    &eaUnidadBTmp   = "";
|FIN;

|INCLUYE acceso_i;
|INCLUYE varnue_i;
|INCLUYE vardep_i;

|| ____________________________________/ CALCULOS DE PANTALLA
|PROCESO mayus; |TIPO 0;
  #0Campo = #0Campo > #0Campo;
  |PINTA #0Campo;
|FPRC;

|PROCESO mayus1; |TIPO 0;
 |HAZ mayus;
 |C_M #0#1, 1,"S"; |C_M #0#6, 1,"S";
 |C_M #0#7, 1,"S"; |C_M #0#38,1,"S";
 |C_M #0#39,1,"S"; |C_M #0#41,1,"S"; |C_M #0#57,1,"S";

 |C_M #0#50,1,"S"; |C_M #0#51,1,"S";
 |C_M #0#52,1,"S"; |C_M #0#53,1,"S";
 |C_M #0#54,1,"S"; |C_M #0#56,1,"S"; |C_M #0#58,1,"S";

 |SI #0#49 = "I";
     |CAMPO_MODIFICA #0#1,  1, "N";
     |CAMPO_MODIFICA #0#6,  1, "N";
     |CAMPO_MODIFICA #0#7,  1, "N";
     |CAMPO_MODIFICA #0#38, 1, "N";
     |CAMPO_MODIFICA #0#39, 1, "N";
     |CAMPO_MODIFICA #0#41, 1, "N";
     |CAMPO_MODIFICA #0#57, 1, "N";
 |FINSI;
 |SI #0#49 = "R";|O #0#49 = "F";
     |CAMPO_MODIFICA #0#50, 1, "N";
     |CAMPO_MODIFICA #0#51, 1, "N";
     |CAMPO_MODIFICA #0#52, 1, "N";
     |CAMPO_MODIFICA #0#53, 1, "N";
     |CAMPO_MODIFICA #0#54, 1, "N";
     |CAMPO_MODIFICA #0#56, 1, "N";
     |CAMPO_MODIFICA #0#58, 1, "N";
 |FINSI;
|FPRC;

|PROCESO sacaimpo;
  |SI #0#37 = "S";
      |SI #0#49 = "R";|O #0#49 = "D";|O #0#49 = "F";
          |CAMPO_MODIFICA #0#38, 1, "N"; #0#38 = 0;    |PINTA #0#38;
          |CAMPO_MODIFICA #0#39, 1, "N"; #0#39 = 0;    |PINTA #0#39;
                                         #0#40 = 0;    |PINTA #0#40;
          |CAMPO_MODIFICA #0#41, 1, "N"; #0#41 = #0#7; |PINTA #0#41;
          |CAMPO_MODIFICA #0#57, 1, "N"; #0#57 = 0;    |PINTA #0#57;
      |FINSI;
      |SI #0#49 = "I";|O #0#49 = "D";
          |CAMPO_MODIFICA #0#53, 1, "N"; #0#53 = 0;     |PINTA #0#53;
          |CAMPO_MODIFICA #0#54, 1, "N"; #0#54 = 0;     |PINTA #0#54;
                                         #0#55 = 0;     |PINTA #0#55;
          |CAMPO_MODIFICA #0#56, 1, "N"; #0#56 = #0#52; |PINTA #0#56;
          |CAMPO_MODIFICA #0#58, 1, "N"; #0#58 = 0;     |PINTA #0#58;
      |FINSI;
  |SINO;
      |SI #0#49 = "R";|O #0#49 = "D";|O #0#49 = "F";
          |CAMPO_MODIFICA #0#38 ,1, "S";
          |CAMPO_MODIFICA #0#39 ,1, "S";
          |CAMPO_MODIFICA #0#41 ,1, "S";
          |CAMPO_MODIFICA #0#57 ,1, "S";
      |FINSI;
      |SI #0#49 = "I";|O #0#49 = "D";
          |CAMPO_MODIFICA #0#53 ,1, "S";
          |CAMPO_MODIFICA #0#54 ,1, "S";
          |CAMPO_MODIFICA #0#56 ,1, "S";
          |CAMPO_MODIFICA #0#58 ,1, "S";
      |FINSI;
  |FINSI;
|FPRC;

|PROCESO sumaimpo;
  #0#40 = #0#38 + #0#39 + #0#57;
  |PINTA #0#40;
|FPRC;

|PROCESO sumaimpo2;
  #0#55 = #0#53 + #0#54 + #0#58;
  |PINTA #0#55;
|FPRC;

|PROCESO fechad;
  |SI #0Campo < fec1; |O #0Campo > fec2;
      |MENAV mensa1;
      |ERROR;
      |ACABA;
  |FINSI;
|FPRC;

|PROCESO fechah;
  campo = Campo - 1;
  |SI #0Campo < fec1; |O #0Campo > fec2; |O #0Campo < #0campo;
      |MENAV mensa1;
      |ERROR;
      |ACABA;
  |FINSI;
|FPRC;

|PROCESO cheq_libro; |TIPO 0;
  |SI #10#2 ! "R";
      |MENAV "    ATENCION!!! Libro de I.V.A. Soportado ...";
      |ERROR;
  |FINSI;
|FPRC;

|PROCESO lee_libro1;
  |ERROR10;
|FPRC;

|DEFBUCLE lee_libro;
 #10#1;
 2;
  1, "R";
 99, "R";
 e;
 NULL, lee_libro1;
|FIN;

|PROCESO deflib; |TIPO 7;
  |SI modo ! 0; |ACABA; |FINSI;
  |DDEFECTO #10;
  |BUCLE lee_libro;
  #0#0 = #10#0;
  |PINTA #0#0;
|FPRC;

|PROCESO selecci2; |TIPO 0;
|SI #0#42 = "N";
    |CAMPO_MODIFICA #0#11, 1, "S";
    |CAMPO_MODIFICA #0#43, 1, "N";
    |CAMPO_MODIFICA #0#44, 1, "N";
    |CAMPO_MODIFICA #0#45, 1, "N";
    |CAMPO_MODIFICA #0#46, 1, "N";
        #0#43 = 0;      |PINTA #0#43;
        #0#44 = 0;      |PINTA #0#44;
        #0#45 = 0;      |PINTA #0#45;
        #0#46 = 0;      |PINTA #0#46;
|SINO;
    |CAMPO_MODIFICA #0#11, 1, "N";
    |CAMPO_MODIFICA #0#43, 1, "S";
    |CAMPO_MODIFICA #0#44, 1, "S";
    |CAMPO_MODIFICA #0#45, 1, "S";
    |CAMPO_MODIFICA #0#46, 1, "S";
        #0#11 = "N";    |PINTA #0#11;
|FINSI;
|FPRC;

|PROCESO leegrupo; |TIPO 0;
 |SI FSalida = 2; |ACABA; |FINSI;
 |SI #0Campo = 0; |ACABA; |FINSI;
 |ABRE #6;
 #6#0 = #0Campo;
 |LEE 001#6=;
 salidas = FSalida;
 |CIERRA #6;
 |SI salidas ! 0;
     |MENAV men2;
     |ERROR;
 |FINSI;
|FPRC;

|PROCESO cuadro; |TIPO 7;
 |PUSHV  1521 2280;
 |CUADRO 1521 2280;
 |ATRI R;
 |PINTA 1622, " ----------- FORMATO LIBRO FACTURAS EMITIDAS ------------ ";
 |PINTA 1722, " ( 1) Descr. Fra., Nombre cliente, varias lineas          ";
 |PINTA 1822, " ( 2) Nombre y NIF cliente, una linea                     ";
 |PINTA 1922, " ( 3) Descr. Fra., Nombre cliente, una linea              ";
 |PINTA 2022, " ( 4) Nombre y NIF cliente, una linea s/recargo           ";
 |PINTA 2122, " ( 5) Descr. Fra., Nombre cliente, una linea s/recargo    ";
 |ATRI r;
|FPRC;

|PROCESO otrocuad; |TIPO 7;
 |PUSHV  1644 2280;
 |CUADRO 1644 2280;
 |ATRI R;
 |PINTA 1745, " -------- ORDEN FACTURAS --------  ";
 |PINTA 1845, "1- Libro+FECHA FRA.+SERIE+Nro.Fra. ";
 |PINTA 1945, "2- Libro+FECHA CBLE+SERIE+Nro.Fra. ";
 |PINTA 2045, "3- Libro+SERIE+FECHA FRA.+Nro.Fra. ";
 |PINTA 2145, "4- Libro+SERIE+FECHA CBLE+Nro.Fra. ";
 |ATRI r;
|FPRC;

|PROCESO repon; |TIPO 0;
 |POPV;
|FPRC;

|PROCESO no_entres;
  alfa1 = #0#11;
  |PARA campo = 12;  |SI campo [ 35;  |HACIENDO campo = campo + 1;
        |CAMPO_MODIFICA #0campo, 1, alfa1;
  |SIGUE;
  |SI alfa1 = "N";
      |PDEFECTO #0,12,36;
      |PINDA #0#0;
  |FINSI;
|FPRC;

|PROCESO nopidas;
  |SI #0Campo ! 0;  |ACABA;  |FINSI;
  alfa1 = "N";
  |PARA campo = Campo + 1;  |SI campo [ 35;  |HACIENDO campo = campo + 1;
        |CAMPO_MODIFICA #0campo, 1, alfa1;
        #0campo = 0;
  |SIGUE;
  |SI Campo = 35;  |PINDA #0#0;  |FINSI;
|FPRC;

|PROCESO paso; |TIPO 0;
 |SI FSalida = 1;
     |ERROR;
     |PTEC 815;
 |FINSI;
|FPRC;
|| **********************************************************************
|PROCESO anterior;
  |SI #0#37 = "N";  |ACABA;  |FINSI;
  |HAZ conceptos;
  |SI sw_con = 1;     |ACABA;   |FINSI;
  |HAZ puntero2;
  |HAZ contador;
|FPRC;

|PROCESO SoloResumen;
     |SI #0#2 ! "X";
          |PRINT;
     |FINSI;
|FPRC;

|PROCESO imprimir;
  |HAZ conceptos;
  |SI sw_con = 1;     |ACABA;   |FINSI;

  esw = 0;
  |DDEFECTO #112;
  #112#0 = #1#0;
  #112#1 = #1#27;
  #112#2 = #1#2;
  |LEE 001#112=;
  |SI #0#10 = 1; |O #0#10 = 3;
      fecha_i = #1#3;   fecha_a = #1#3;
      fecha_n = #112#3; fecha_b = #112#3;
      |SI #1#3 ! #112#3; |Y #112#3 > "01.01.0000"; esw = 1; |FINSI;
  |SINO;
       fecha_i = #1#46;  fecha_a = #1#46;      || fecha cont.
       fecha_n = #112#3; fecha_b = #112#3;
       |SI #1#46 ! #112#3; |Y #112#3 > "01.01.0000"; esw = 1; |FINSI;
  |FINSI;

  |SI sw = 0;
     varalf = titulo1;
     |QBF varalf;
     varalf = varalf;
     sw = 1;
 |FINSI;
 sw_pie = 1;
 |HAZ leecuen;

 total1 = 0;  total2 = 0;  total3 = 0;  total4 = 0;  total5 = 0;
 sw_total = 0;

 |SI #1#53 ! 0; |O #1#57 ! 0; |O #1#61 ! 0;
     total5 = #1#26;  sw_total = 1;
     |VETE yatotal;
 |FINSI;

 |SI #1#52 ! 0; |O #1#56 ! 0; |O #1#60 ! 0;
     total4 = #1#26;  sw_total = 1;
     |VETE yatotal;
 |FINSI;

 |SI #1#10 ! 0; |O #1#16 ! 0; |O #1#22 ! 0;
     total3 = #1#26;  sw_total = 1;
     |VETE yatotal;
 |FINSI;

 |SI #1#09 ! 0; |O #1#15 ! 0; |O #1#21 ! 0;
     total2 = #1#26;  sw_total = 1;
     |VETE yatotal;
 |FINSI;

 |SI #1#08 ! 0; |O #1#14 ! 0; |O #1#20 ! 0;
     total1 = #1#26;
 |FINSI;

 |ET yatotal;
 |HAZ contador;
  |SI #0#47 > 1;
      |HAZ nuevar;
  |SINO;
      |HAZ SoloResumen;
  |FINSI;
 |HAZ puntero1;
 |HAZ puntero2;
|FPRC;

|PROCESO nuevar;
  |SI #0#47 = 2; |O #0#47 = 4;
      vtitol = "CLIENTE                              N.I.F.";
      vlinea = #1#50 + " " + #1#51;
  |FINSI;
  |SI #0#47 = 3;
      |SI swModulo ! 1872;
          vtitol = "CLIENTE                   DESCRIPCION";
          vlinea = #1#50;
          vlinea = vlinea % 125;
          vlinea = vlinea + " " + #1#7;
      |SINO;
          vtitol = "CLIENTE                DESCRIPCION";
          vlinea = #1#50;
          vlinea = vlinea % 122;
          vlinea = vlinea + " " + #1#7;
      |FINSI;
  |FINSI;
  |SI #0#47 = 5;
      vtitol = "CLIENTE                               DESCRIPCION";
      vlinea = #1#50 + " " + #1#7;
  |FINSI;
  heescrito = 0;
  base = 0; tpiva = 0; civa = 0;
  ree = 0; cree = 0;
  |SI #1#8  = 0; |Y #1#14 = 0; |Y #1#20 = 0;
   |Y #1#9  = 0; |Y #1#15 = 0; |Y #1#21 = 0;
   |Y #1#10 = 0; |Y #1#16 = 0; |Y #1#22 = 0;
   |Y #1#52 = 0; |Y #1#56 = 0; |Y #1#60 = 0;
   |Y #1#53 = 0; |Y #1#57 = 0; |Y #1#61 = 0;
      base  = #1#8;
      tpiva = #1#11;
      civa  = #1#14;
      ree   =  #1#17;
      cree  = #1#20;
      |HAZ SoloResumen;
      vlinea = "";
      heescrito = 1;
  |FINSI;
  |SI #1#8 ! 0; |O #1#14 ! 0; |O #1#20 ! 0;
      base = #1#8;
      tpiva =  #1#11;
      civa = #1#14;
      ree =  #1#17;
      cree = #1#20;
      |HAZ SoloResumen;
      vlinea = "";
      heescrito = 1;
  |FINSI;
  |SI #1#9 ! 0; |O #1#15 ! 0; |O #1#21 ! 0;
      base = #1#9;
      tpiva =  #1#12;
      civa = #1#15;
      ree =  #1#18;
      cree = #1#21;
      |HAZ SoloResumen;
      vlinea = "";
      heescrito = 1;
  |FINSI;
  |SI #1#10 ! 0; |O #1#16 ! 0; |O #1#22 ! 0;
      base = #1#10;
      tpiva =  #1#13;
      civa = #1#16;
      ree =  #1#19;
      cree = #1#22;
      |HAZ SoloResumen;
      vlinea = "";
      heescrito = 1;
  |FINSI;
  |SI #1#52 ! 0; |O #1#56 ! 0; |O #1#60 ! 0;
      base = #1#52;
      tpiva =  #1#54;
      civa = #1#56;
      ree =  #1#58;
      cree = #1#60;
      |HAZ SoloResumen;
      vlinea = "";
      heescrito = 1;
  |FINSI;
  |SI #1#53 ! 0; |O #1#57 ! 0; |O #1#61 ! 0;
      base = #1#53;
      tpiva =  #1#55;
      civa = #1#57;
      ree =  #1#59;
      cree = #1#61;
      |HAZ SoloResumen;
      vlinea = "";
      heescrito = 1;
  |FINSI;
|FPRC;

|DEFBUCLE cay4ivav0;          ||ORDENADO 1
 #1#3;                                  || libro,fecha fra.,serie,numero
 48;
 #0#0,  #0#3, #0#59,      0, dinver1;
 #0#48, #0#4, #0#60, 999999, hinver1;
 ;
 NULL, imprimir;
|FIN;

|DEFBUCLE cay4ivav3;          ||ORDENADO 2
 #1#1;
 46,48;
 #0#0,  #0#59,      0, #0#3, dinver1;
 #0#48, #0#60, 999999, #0#4, hinver1;
 ;
 NULL, NULL, NULL, NULL, NULL, imprimir;
 #1#0, #1#46, #1#27, #1#2;              || libro,fecha con.,serie,numero
|FIN;

|DEFBUCLE cay4ivav0_0;          ||ORDENADO 3
 #1#1;
 3,48;
 #0#0,  #0#59,      0, #0#3, dinver1;
 #0#48, #0#60, 999999, #0#4, hinver1;
 ;
 NULL, NULL, NULL, NULL, NULL, imprimir;
 #1#0, #1#27, #1#3, #1#2;              || libro,serie, fecha fra,numero
|FIN;

|DEFBUCLE cay4ivav3_0;          ||ORDENADO 4
 #1#1;
 46,48;
 #0#0,  #0#59,      0, #0#3, dinver1;
 #0#48, #0#60, 999999, #0#4, hinver1;
 ;
 NULL, NULL, NULL, NULL, NULL, imprimir;
 #1#0, #1#27, #1#46, #1#2;              || libro,fecha con.,serie,numero
|FIN;

|| ----------------------------------
|DEFBUCLE cay4ivav1;          ||ORDENADO 1
 #1#3;                                  || libro,fecha fra.,serie,numero
 48;
 #0#0,  fec1,   #0#59,      0, dinver1;
 #0#48, fectra, #0#60, 999999, hinver1;
 ;
 NULL, anterior;
|FIN;

|DEFBUCLE cay4ivav2;          ||ORDENADO 2
 #1#1;
 46,48;
 #0#0,  #0#59,      0, fec1,   dinver1;
 #0#48, #0#60, 999999, fectra, hinver1;
 ;
 NULL, NULL, NULL, NULL, NULL, anterior;
 #1#0, #1#46, #1#27, #1#2;              || libro,fecha con.,serie,numero
|FIN;

|DEFBUCLE cay4ivav1_0;          ||ORDENADO 3
 #1#1;
 3,48;
 #0#0,  #0#59,      0, fec1,   dinver1;
 #0#48, #0#60, 999999, fectra, hinver1;
 ;
 NULL, NULL, NULL, NULL, NULL, anterior;
 #1#0, #1#27, #1#3, #1#2;              || libro,serie, fecha fra.,numero
|FIN;

|DEFBUCLE cay4ivav2_0;          ||ORDENADO 4
 #1#1;
 46,48;
 #0#0,  #0#59,      0, fec1,   dinver1;
 #0#48, #0#60, 999999, fectra, hinver1;
 ;
 NULL, NULL, NULL, NULL, NULL, anterior;
 #1#0, #1#27, #1#46, #1#2;              || libro,serie, fecha cble.,numero
|FIN;


|PROCESO Empresas;
    #cabucem1#0 = eaLegEmp;
    #cabucem1#1 = eaLegPer;
    cod1 = eaLegEmp;
    cod2 = eaLegPer;
    emEmp = 1;
    |HAZ leelo;
    |SI swerror ! 0;  |ACABA;   |FINSI;

     |DBASE fich_alta; |QBF fich_alta;
     fich_alta = fich_alta + "fich/" + (("00000" + #cabucem1#0) % -105) + #cabucem1#1 + "/";
     |PATH_DAT #0 fich_alta; |PATH_DAT #1 fich_alta;
     |PATH_DAT #3 fich_alta; |PATH_DAT #6 fich_alta;
     |PATH_DAT #10 fich_alta;
     |ABRE #0;
     |HAZ inicio;
     |HAZ eVarEmpresa;

     modo = 1;
     #0#36 = 1;
     |LEE 000#0=;
     |SI FSalida ! 0;
        |DDEFECTO #0;
        #0#3 = fec1;
        #0#4 = fec2;
        #0#49 = "D";
        |GRABA 011#0b;
     |FINSI;
     #0#49 = "D";
     #0#3 = fec1;
     #0#4 = fec2;
     modo = 0;
     #0#5 = efFecLeg;
     fecinf = #0#5;
     #0#6 = 0;
     |SI #0#7 > 1; #0#7 = 1; |FINSI;
     #0#37 = "S";
     #0#38 = 0;
     #0#39 = 0;
     #0#40 = 0;
     #0#41 = 1;
     #0#51 = 0;
     |SI #0#52 > 1;  #0#52 = 1; |FINSI;
     |PDEFECTO #0, 53, 59;

     |HAZ impre;
     |SI #0#4 ! fec2;
         #0#3 = #0#4 + 1;
     |SINO;
         #0#3 = fec2;
     |FINSI;
     #0#4 = fec2;
     #0#37 = "N";

     |GRABA 011#0a;
     |LIBERA #0;
     |CIERRA #0;


     |DBASE Comodin1; |QBF Comodin1;
     Comodin1 = Comodin1 + "hacienda/PresLibr/" + (("00000" + #cabucem1#0) % -105) + #cabucem1#1 + "/";
     |SI eaFormato ! "S";
         Comodin1 = Comodin1 + "FAC_EMIT*.xls";
     |SINO;
         Comodin1 = Comodin1 + "FAC_EMIT*.pdf";
     |FINSI;
     |_PDIR Comodin1;
     |SI FSalida = 0;
        |PARA; |SI FSalida = 0; |HACIENDO;
            Comodin = Comodin1 % -107; Comodin = Comodin % 0103;
            Numeracion = Comodin; Numeracion = Numeracion + 1;
            |_SDIR Comodin1;
        |SIGUE;
     |SINO;
        Numeracion = 1;
     |FINSI;

     |SI eaFormato ! "S";
         Comodin = eaUnidadBTmp + "/tmp1.xls";
     |SINO;
         Comodin = eaUnidadBTmp + "/FacEmiti.pdf";
     |FINSI;
     |XABRE "C", Comodin, Handle;
     |SI FSalida ] 0;
        |XCIERRA Handle;
        |DBASE Comodin1; |QBF Comodin1;
        Comodin1 = Comodin1 + "hacienda/PresLibr/";
        |MKDIR Comodin1;
        Comodin1 = Comodin1 + (("00000" + #cabucem1#0) % -105) + #cabucem1#1 + "/";
        |MKDIR Comodin1;
        |SI eaFormato ! "S";
            Comodin1 = Comodin1 + "FAC_EMIT_" + (("000" + Numeracion) % -103) + ".xls";
        |SINO;
            Comodin1 = Comodin1 + "FAC_EMIT_" + (("000" + Numeracion) % -103) + ".pdf";
        |FINSI;
        |IP_REMOTO aAlfa; |QBF aAlfa;
        |SI aAlfa ! "";
           |RECIBE_FICHERO Comodin, Comodin1;
        |SINO;
           |COPIA_FICHERO Comodin, Comodin1;
        |FINSI;
        FSalida = 1;

        |PARA; |SI FSalida ! 0; |HACIENDO;
           |RFBORRA Comodin;
           |SI FSalida ! 0; |Y eaFormato ! "S";
              |MENAV "    Termine la tarea de Excel que tiene activa";
           |FINSI;
        |SIGUE;
     |FINSI;
|FPRC;

|| *******************************************************************
|| ********** PROGRAMA ***********************************************
|| *******************************************************************
|PROGRAMA;

  |HAZ BuscaModulo;
  |SI PARAMETRO = "";
     |CLS;
     |CABEZA "Reg. Facturas Venta";
     |ABRE #0;
     |HAZ inicio;
     |HAZ eVarEmpresa;
     modo = 1;
     #0#36 = 1;
     |LEE 111#0=;
     |SI FSalida ! 0;
         |DDEFECTO #0;
         #0#3 = fec1;
         #0#4 = fec2;
         |GRABA 011#0b;
         modo = 0;
     |FINSI;

     |PINPA #0#0;
     #0#5 = ~;
     |PINDA #0#0;
     |ENDATOS #1#0;
     |SI FSalida ! 0;  |CIERRA #0;  |ACABA;  |FINSI;
     |PINDA #0#0;

     fecinf = #0#5;
     |HAZ impre;
     |SI #0#4 ! fec2;
         #0#3 = #0#4 + 1;
     |SINO;
         #0#3 = fec2;
     |FINSI;
     #0#4 = fec2;
     #0#37 = "N";

     |GRABA 011#0a;
     |LIBERA #0;
     |CIERRA #0;
  |SINO;
     || |ABRE #48; |BUCLE Empresas; |CIERRA #48;
     |HAZ Empresas;
  |FINSI;
|FPRO;

|| *****************************************************
|PROCESO impre;
  |SI PARAMETRO ! "";
      |SI eaFormato ! "S";
          Impresora = eaNomImp + "?"+ eaUnidadBTmp + "\tmp1,,,?Facturas emitidas";
      |SINO;
          Impresora = eaNomImp + ";"+ eaUnidadBTmp + "\FacEmiti.pdf";
      |FINSI;
      |SI enBasImp ! 0;
          |IMPRE -1;
          |SI FSalida ! 0;
              Impresora = Impresora - eaNomImp;
              |IMPRE 0;
          |FINSI;
      |SINO;
          |IMPRE 0;
      |FINSI;
  |SINO;
      |IMPRE 0;
  |FINSI;
  |SI FSalida ! 0; |ACABA; |FINSI;
  |HAZ los_informes;
  |SI #0#47 > 1;
      |SI #99#1 = "M"; |Y #99#4 ! "N"; que_i1 = "cay51vav"; |FINSI;
      |SI #99#1 = "M"; |Y #99#4 ! "S"; que_i1 = "cry51vav"; |FINSI;
  |FINSI;
  |SI #0#47 > 3;
      |SI #99#1 = "M"; |Y #99#4 ! "N"; que_i1 = "cay52vav"; |FINSI;
      |SI #99#1 = "M"; |Y #99#4 ! "S"; que_i1 = "cry52vav"; |FINSI;
  |FINSI;
  |SI #99#1 = "L"; #0#47 = 1; |FINSI;
  informe1 = que_i1;
  informe2 = que_i2;

|| ... incorporar Formato de Crystal
  |SI Impresora = "CrystalReport"; |O eaFormato = "S";
      || ....
      #0#2 = "N";  || quitar tambien
  |FINSI;

  |SI #0#49 = "R"; uno = 1; dos = 1; |FINSI;
  |SI #0#49 = "I"; uno = 2; dos = 2; |FINSI;
  |SI #0#49 = "D"; uno = 1; dos = 2; |FINSI;
  |SI #0#49 = "F"; uno = 1; dos = 1; |FINSI;

|PARA para2 = uno; |SI para2 [ dos; |HACIENDO para2 = para2 + 1;
  contaiva = 0;
  contarec = 0;
  |HAZ borrapunt;

  |INFOR informe1;
  |SI FSalida ! 0; |FINIMP; |ACABA; |FINSI;

  |SI para2 = 1;
      hoja    = #0#6;
      tb      = #0#38;
      |SI informe1 = "cay51vav";
         ti = #0#39;
         tr = #0#57;
      |SINO;
         ti = #0#39 + #0#57;
      |FINSI;
      tt      = #0#40;
      dinver1 = "N";
      hinver1 = "N";
      |SI #0#49 = "F";
           dinver1 = " ";
           hinver1 = "z";
      |FINSI;
      titulo1 = #0#1;
      regis   = #0#41 - 1;
  |SINO;
      hoja    = #0#51;
      tb      = #0#53;
      |SI informe1 = "cay51vav";
         ti = #0#54;
         tr = #0#58;
      |SINO;
         ti = #0#54 + #0#58;
      |FINSI;
      tt      = #0#55;
      dinver1  = "S";
      hinver1  = "S";
      titulo1 = #0#50;
      regis   = #0#56 - 1;
  |FINSI;

   sw = 0;
   sw_pie = 0;
   hasta  = #0#3;
   hasta  = hasta - 1;
   fectra = hasta;

  |SI #0#10 = 1; |BUCLE cay4ivav1; |FINSI;
  |SI #0#10 = 2; |BUCLE cay4ivav2; |FINSI;
  |SI #0#10 = 3; |BUCLE cay4ivav1_0; |FINSI;
  |SI #0#10 = 4; |BUCLE cay4ivav2_0; |FINSI;

  |ABRE #3;
  |ABRE #112;
  |SI #0#10 = 1; |BUCLE cay4ivav0; |FINSI;
  |SI #0#10 = 2; |BUCLE cay4ivav3; |FINSI;
  |SI #0#10 = 3; |BUCLE cay4ivav0_0; |FINSI;
  |SI #0#10 = 4; |BUCLE cay4ivav3_0; |FINSI;
  |CIERRA #112;
  |CIERRA #3;

  |SI sw_pie = 1; |Y #0#2 ! "X"; |PIEINF; |FINSI;
  |FININF;
  |SI sw_pie = 1; |Y #0#2 ! "N"; |HAZ impre2; |FINSI;

  |SI para2 = 1;
      #0#6 = hoja;
      #0#38 = tb + te;
      #0#39 = ti;
      #0#57 = tr;
      #0#40 = tt;
      #0#41 = regis + 1;
  |SINO;
      #0#51 = hoja;
      #0#53 = tb + te;
      #0#54 = ti;
      #0#58 = tr;
      #0#55 = tt;
      #0#56 = regis + 1;
  |FINSI;
|SIGUE;
|FINIMP;
|FPRC;

|PROCESO contador;
  |SI para2 = 1;
      |SI #0#7 = 0;
          regis = #1#2;
      |SINO;
          regis = regis + 1;
      |FINSI;
  |SINO;
      |SI #0#52 = 0;
          regis = #1#2;
      |SINO;
          regis = regis + 1;
      |FINSI;
  |FINSI;
|FPRC;

|PROCESO congru;
  sw_con = 1;
  |ABRE #6;
  |PARA y = 43; |SI y [ 46; |HACIENDO y = y + 1;
        |SI #0y ! 0;
            #6#0 = #0y;
            |LEE 001#6=;
            |SI FSalida = 0;
                |PARA x = 2; |SI x [ 21; |HACIENDO x = x + 1;
                      |SI #6x  = #1#6;
                          sw_con = 0;
                          |SAL;
                      |FINSI;
                |SIGUE;
            |FINSI;
        |FINSI;
        |SI sw_con = 0; |SAL; |FINSI;
  |SIGUE;
 |CIERRA #6;
|FPRC;

|PROCESO conceptos;
  |SI #0#42 = "S";
      |HAZ congru;
      |ACABA;
  |FINSI;
  sw_con = 0;
  |SI #0#11 ! "S";  |ACABA;  |FINSI;
  sw_con = 1;
  |PARA para1 = 12; |SI para1 [ 35; |HACIENDO para1 = para1 + 1;
        |SI #0para1 ! 0;|Y #0para1 = #1#6;
            sw_con = 0;
            |SAL;
        |FINSI;
  |SIGUE;
|FPRC;


|PROCESO impre2;
  |MODULO runnom;
  |INFOR informe2;
  Iiva = iva1 <-;
  Ibaseiva = baseiva1 <-;
  Icuotiva = cuotiva1 <-;
  tiva1 = 0;
  tiva11 = 0;
  tiva3 = 0;
  prim = 0;
  |PARA x = 1; |SI x [ contaiva; |HACIENDO x = x + 1;
        varnum1 = baseiva;
        varnum2 = iva;
        varnum3 = cuotiva;
        tiva1 = tiva1 + varnum1;
        tiva11 = tiva11 + varnum1;
        tiva3 = tiva3 + varnum3;
        |SI prim = 0;
            |SI runnom = "cay4ivac";  descri0 = "SOPORTADO  ";  |FINSI;
            |SI runnom = "cay4ivav";  descri0 = "REPERCUTIDO";  |FINSI;

            |SI para2 = 2; descri0 = descri0 + " (Inversion)"; |FINSI;
            descri1 = "I.V.A.";
            descri2 = "======";
            swlin = 1;
            |PRINT;
            prim = 1;
        |FINSI;
        swlin = 2;
        |PRINT;
        Ibaseiva = Ibaseiva + 1;
        Icuotiva = Icuotiva + 1;
        Iiva = Iiva + 1;
  |SIGUE;
  tiva1 = tiva1 + basenod;
  tiva3 = tiva3 + cuotnod;
  |SI basenod ! 0;|O cuotnod ! 0;    swlin = 4;     |PRINT;   |FINSI;
  |SI contaiva ! 0;   swlin = 3;     |PRINT;   |FINSI;
  ttotal3 = ttotal3 + tiva3;

  || --------------------------------------------------------
  Irec = rec1 <-;                        || Imprime resume recargo
  Ibaserec = baserec1 <-;
  Icuotrec = cuotrec1 <-;
  tiva1 = 0;
  tiva3 = 0;
  prim = 0;
  |PARA x = 1; |SI x [ contarec; |HACIENDO x = x + 1;
        varnum1 = baserec;
        varnum2 = rec;
        varnum3 = cuotrec;
        tiva1 = tiva1 + varnum1;
        tiva3 = tiva3 + varnum3;
        |SI prim = 0;
            descri1 = "RECARGO DE EQUIVALENCIA";
            descri2 = "=======================";
            swlin = 1;
            |PRINT;
            prim = 1;
        |FINSI;
        swlin = 2;
        |PRINT;
        Ibaserec = Ibaserec + 1;
        Icuotrec = Icuotrec + 1;
        Irec = Irec + 1;
  |SIGUE;
  |SI contarec ! 0;   swlin = 3;     |PRINT;   |FINSI;
  ttotal1 = tiva11 + basenod;
  ttotal3 = ttotal3 + tiva3;
  |PIEINF;
  |FININF;
|FPRC;

|PROCESO puntero1;   || Carga el iva
  |PARA x =  8; |SI x [ 10; |HACIENDO x = x + 1;
        y = x + 3;
        z = y + 3;
        |SI #1#23 = "N";
            basenod = basenod + #1x;
            cuotnod = cuotnod + #1z;
        |SINO;
            |SI #1x ! 0; |O #1z ! 0;
                |HAZ carga_iva;
            |FINSI;
        |FINSI;
  |SIGUE;
  |PARA x = 52; |SI x [ 53; |HACIENDO x = x + 1;
        y = x + 2;
        z = y + 2;
        |SI #1#23 = "N";
            basenod = basenod + #1x;
            cuotnod = cuotnod + #1z;
        |SINO;
            |SI #1x ! 0; |O #1z ! 0;
                |HAZ carga_iva;
            |FINSI;
        |FINSI;
  |SIGUE;

  |PARA x =  8; |SI x [ 10; |HACIENDO x = x + 1;   || Carga el recargo
        y = x + 9;
        z = y + 3;
        |SI #1#23 = "N";
            basenod = basenod + #1z;
        |SINO;
            |SI #1x ! 0; |Y #1z ! 0;
                |HAZ carga_rec;
            |FINSI;
        |FINSI;
  |SIGUE;
  |PARA x =  52; |SI x [ 53; |HACIENDO x = x + 1;   || Carga el recargo
        y = x + 6;
        z = y + 2;
        |SI #1#23 = "N";
           basenod = basenod + #1z;
       |SINO;
           |SI #1x ! 0; |Y #1z ! 0;
               |HAZ carga_rec;
           |FINSI;
       |FINSI;
  |SIGUE;
|FPRC;

|PROCESO carga_iva;
|SI #1x ! 0; |O #1z ! 0;
        Iiva = iva1 <-;
        Ibaseiva = baseiva1 <-;
        Icuotiva = cuotiva1 <-;

    |SI contaiva = 0;
            baseiva = baseiva + #1x;
            iva = #1y;
            cuotiva = cuotiva + #1z;
            contaiva = contaiva + 1;
            sw_punt = 0;
    |SINO;
            sw_punt = 1;
        |PARA pepe = 1; |SI pepe [ contaiva; |HACIENDO pepe = pepe + 1;
            |SI iva = #1y;
                |SI sw_punt = 1;
                        baseiva = baseiva + #1x;
                        cuotiva = cuotiva + #1z;
                        sw_punt = 0;
                |FINSI;
            |SINO;
                    Ibaseiva = Ibaseiva + 1;
                    Icuotiva = Icuotiva + 1;
                    Iiva = Iiva + 1;
            |FINSI;
        |SIGUE;

        |SI sw_punt = 1;
                baseiva = baseiva + #1x;
                cuotiva = cuotiva + #1z;
                iva = #1y;
                contaiva = contaiva + 1;
        |FINSI;
    |FINSI;
|FINSI;
|FPRC;

|PROCESO carga_rec;
|SI #1x ! 0; |O #1z ! 0;
        Irec = rec1 <-;
        Ibaserec = baserec1 <-;
        Icuotrec = cuotrec1 <-;

    |SI contarec = 0;
            baserec = baserec + #1x;
            rec = #1y;
            cuotrec = cuotrec + #1z;
            contarec = contarec + 1;
            sw_punt = 0;
    |SINO;
            sw_punt = 1;
        |PARA pepe = 1; |SI pepe [ contarec; |HACIENDO pepe = pepe + 1;
            |SI rec = #1y;
                |SI sw_punt = 1;
                        baserec = baserec + #1x;
                        cuotrec = cuotrec + #1z;
                        sw_punt = 0;
                |FINSI;
            |SINO;
                    Ibaserec = Ibaserec + 1;
                    Icuotrec = Icuotrec + 1;
                    Irec = Irec + 1;
            |FINSI;
        |SIGUE;
        |SI sw_punt = 1;
                baserec = baserec + #1x;
                cuotrec = cuotrec + #1z;
                rec = #1y;
                contarec = contarec + 1;
        |FINSI;
    |FINSI;
|FINSI;
|FPRC;

|PROCESO puntero2;        ||SUMA anteriores;
  |MODULO runnom;
  |PARA x =  8; |SI x [ 10; |HACIENDO x = x + 1;
        y = x + 6;
        z = y + 6;
        p = x + 3;
            |SI #1p ! 0;
                tb = tb + #1x;
            |SINO;
                te = te + #1x;          || base exenta (repercutido)
            |FINSI;
            ti = ti + #1y;
        tr = tr + #1z;
  |SIGUE;
  |PARA x = 52; |SI x [ 53; |HACIENDO x = x + 1;
        y = x + 4;
        z = y + 4;
        p = x + 2;
            |SI #1p ! 0;
                tb = tb + #1x;
            |SINO;
                te = te + #1x;          || base exenta (repercutido)
            |FINSI;
            ti = ti + #1y;
        tr = tr + #1z;
  |SIGUE;
  tt = tt + #1#26;
|FPRC;

|PROCESO leecuen;
    #3#0 = #1#4;
    #3#1 = #1#5;
|LEE 001#3=;
|SI FSalida ! 0;
    |DDEFECTO #3;
|FINSI;

   eaSerie = "        " + #1#27;
   |SI swModulo = 1872;
       || eaSerie = "";
       |ABRE #120;
       #120#0 = #1#0;
       #120#1 = #1#2;
       #120#2 = #1#27;
       |LEE 001#120=;
       |SI FSalida = 0;
           alfa1 = #120#3; |QBF alfa1;
           |SI alfa1 ! "";
               eaSerie = #120#3;
           |FINSI;
       |FINSI;
       |CIERRA #120;
   |FINSI;
|FPRC;

|PROCESO borrapunt;
    basenod = 0;
    cuotnod = 0;
    Iiva = iva1 <-;
    Ibaseiva = baseiva1 <-;
    Icuotiva = cuotiva1 <-;
    Irec = rec1 <-;
    Ibaserec = baserec1 <-;
    Icuotrec = cuotrec1 <-;
|PARA x = 1; |SI x [ 10; |HACIENDO x = x + 1;
        iva = 0;
        baseiva = 0;
        cuotiva = 0;
        rec = 0;
        baserec = 0;
        cuotrec = 0;
        Ibaseiva = Ibaseiva + 1;
        Icuotiva = Icuotiva + 1;
        Iiva = Iiva + 1;
        Ibaserec = Ibaserec + 1;
        Icuotrec = Icuotrec + 1;
        Irec = Irec + 1;
|SIGUE;
|FPRC;
