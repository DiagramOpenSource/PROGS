|FICHEROS;
     recupera #0;
     caivarep #1;
     camaasil #2;
|FIN;

|VARIABLES;
     nivel   = 0;
     tipo    = "";
     impor   = 0;
     conta   = 0;
     sw      = 0;
     sw1     = 0;
     &texto  = "";
     informe = "recupera";
     feini = @;
     fefin = @;
|FIN;

||---------------------------------------------------------------------------
|| Lee el fichero recupera y comprueba si el numero de factura del libro
|| diario esta bien, si no esta bien cambia el numero de factura del libro
|| diario por la que se encuentra en la recuperacion de los registros de iva.
||---------------------------------------------------------------------------
|PROCESO encuentra;
     |SI sw1 = 0; |O #0#2 ! #2#19; |O #0#3 ! #2#20;
          sw1 = 1;
          #2#19 = #0#2;  || Tipo acumulador de iva.
          #2#20 = #0#3;  || N§. acumulador de iva.
          #2#14 = #0#6;  || N§. Factura.
          |GRABA 121#2a;
          texto = "Graba en Diario";
          |PRINT;
     |SINO;
          texto = "No grabado Repetido";
          |PRINT;
     |FINSI;
|FPRC;

|DEFBUCLE busca;
     #0#1;
     ;
     #2#13,#2#1," ",0,#2#9,0001;   || Libro,Fecha,Tipo,Numero,Importe,Cont.
     #2#13,#2#1,"z",5,#2#9,9999;
     ;
     NULL,encuentra;
|FIN;


|PROCESO miranumero;
     sw1 = 0;
     |BUCLE busca;
|FPRC;

||---------------------------------------------------------------------------
|| Bucle de lectura del fichero diario.
||---------------------------------------------------------------------------
|DEFBUCLE lee_diario;
     #2#1;
     ;
     00,feini,000001,0001;  || Diario, Fecha, Documento, Apunte.
     99,fefin,999999,9999;
     be;
     NULL,miranumero;
|FIN;

||---------------------------------------------------------------------------
|| Comprueba si existe el registro, si existe aumenta uno al contador y
|| repite la comprobacion, si no existe graba el registro.
||---------------------------------------------------------------------------
|PROCESO graba;
     conta = 1;
     #0#0 = #1#0;        || Libro
     #0#1 = #1#3;        || Fecha
     #0#2 = tipo;        || Tipo (Base, Iva, Recargo)
     #0#3 = nivel;       || Nivel (1,2,3,4,5)
     #0#4 = impor;       || Importe.
     #0#5 = conta;       || Contador de repeticiones.
     |ET leeotravez;
          |LEE 000#0=;
          |SI FSalida ! 0;
               #0#5 = conta;
               #0#6 = #1#2;   || Numero Factura.
               |GRABA 121#0n;
               sw = 1;
          |SINO;
               conta = conta + 1;
               |SI conta > 99; |ACABA; |FINSI;
               sw = 0;
          |FINSI;
     |SI sw = 0; |VETE leeotravez; |FINSI;
|FPRC;

||---------------------------------------------------------------------------
|| Bucle de lectura del fichero de Iva Repercutido.
||---------------------------------------------------------------------------
|PROCESO compru;
     |PINTA 1230,#1#2;

     |SI #1#8  ! 0; nivel = 1; tipo = "B"; impor = #1#8;  |HAZ graba; |FINSI;
     |SI #1#9  ! 0; nivel = 2; tipo = "B"; impor = #1#9;  |HAZ graba; |FINSI;
     |SI #1#10 ! 0; nivel = 3; tipo = "B"; impor = #1#10; |HAZ graba; |FINSI;
     |SI #1#52 ! 0; nivel = 4; tipo = "B"; impor = #1#52; |HAZ graba; |FINSI;
     |SI #1#53 ! 0; nivel = 5; tipo = "B"; impor = #1#53; |HAZ graba; |FINSI;

     |SI #1#14 ! 0; nivel = 1; tipo = "I"; impor = #1#14; |HAZ graba; |FINSI;
     |SI #1#15 ! 0; nivel = 2; tipo = "I"; impor = #1#15; |HAZ graba; |FINSI;
     |SI #1#16 ! 0; nivel = 3; tipo = "I"; impor = #1#16; |HAZ graba; |FINSI;
     |SI #1#56 ! 0; nivel = 4; tipo = "I"; impor = #1#56; |HAZ graba; |FINSI;
     |SI #1#57 ! 0; nivel = 5; tipo = "I"; impor = #1#57; |HAZ graba; |FINSI;

     |SI #1#20 ! 0; nivel = 1; tipo = "R"; impor = #1#20; |HAZ graba; |FINSI;
     |SI #1#21 ! 0; nivel = 2; tipo = "R"; impor = #1#21; |HAZ graba; |FINSI;
     |SI #1#22 ! 0; nivel = 3; tipo = "R"; impor = #1#22; |HAZ graba; |FINSI;
     |SI #1#60 ! 0; nivel = 4; tipo = "R"; impor = #1#60; |HAZ graba; |FINSI;
     |SI #1#61 ! 0; nivel = 5; tipo = "R"; impor = #1#61; |HAZ graba; |FINSI;
|FPRC;

|DEFBUCLE lee_ivare;
     #1#1;
     ;
     00,000001,"  ";     || Libro, N§.Factura, Serie.
     99,999999,"zz";
     be;
     NULL,compru;
|FIN;

||---------------------------------------------------------------------------
|| Rutina principal.
||---------------------------------------------------------------------------
|PROGRAMA;
     |ABRE #0;
     |CIERRA #0;
     |DELINDEX #0;
     |IMPRE 0;
     |SI FSalida = 0;
          |INFOR informe;
          |ABRE #0;
          |BUCLE lee_ivare;   || Lee el fichero de iva repercutido.
          feini = "01.01.0001";
          fefin = "31.12.8999";
          |BUCLE lee_diario;  || Lee el fichero diario.
          |CIERRA #0;
     |FINSI;
|FPRO;
