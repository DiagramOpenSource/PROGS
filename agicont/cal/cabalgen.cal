|FICHEROS;
    cabalgen #0;
    cacuenta #1;         || cuentas
    caexistc #6;         || descarga cabs.
    caexistl #7;         || descarga lins.
    caconimp #8;
|FIN;

|VARIABLES;
  &eaFicbal = "";
  &eaNomSes = "";
  &eaDepSes = "";

  &pathbal  = "";
  &balance  = 1;
  &aparam   = "";
  &entrada  = 1;
  informe  = "cabalgen";
  alfa1     = "";
  nNume1   = 0;
  letrames = "";
  mes    = 0;
  nivel     = 0;
  traraf    = 0;

  &r_emp = 0;
  &r_per = 0;
  &d_mes = 0;
  &h_mes = 0;
  &estru = 0;
  &r_eje = 0;
  &r_nom = "";

  &eldesde  = 0;
  &elhasta  = 0;
  des_exi   = 0;
  has_exi   = 0;
  linea     = 0;
  descar    = 0;
  d_cuen    = "";
  h_cuen    = "";
  act_cuent = "";
  act_nivel = 0;

    &sw_inf = 0;
  &debe     = 0;
  &haber    = 0;
  &activo   = 0;
  &pasivo   = 0;
  &gastos   = 0;
  &ingresos = 0;
  &tdebe     = 0;
  &thaber    = 0;
  &tactivo   = 0;
  &tpasivo   = 0;
  &tgastos   = 0;
  &tingresos = 0;
  &swnocal   = 0;

  impuesto = 0;
  &vari1 = "";
  &vari2 = "";
  &ac1 = 0;
  &pa1 = 0;
  &ga1 = 0;
  &in1 = 0;
  &ac2 = 0;
  &pa2 = 0;
  &ga2 = 0;
  &in2 = 0;
  &ac3 = 0;
  &pa3 = 0;
  &ga3 = 0;
  &in3 = 0;

   x = 0;
   y = 0;
   sw_exp = 0;
   sw = 0;
|FIN;

|INCLUYE acceso_i;
|INCLUYE digito_i;

|PROCESO antesque;
  aparam = PARAMETRO; |QBF aparam;
  |SI aparam = "fech";
      |HAZ AntesSesion;
      |NOME_DAT #1 eaFicbal;
  |FINSI;
|FPRC;

|PROCESO PintoSes; |TIPO 7;
  aparam = PARAMETRO; |QBF aparam;
  |SI aparam = "fech";
      |HAZ AntesSesion;
      |SI eaNomSes > "";
          |ATRI I; |PINTA 0505, "SESION: "; |PINTA 0513,eaNomSes;
          |SI eaDepSes > "";
              |PINTA 0558, "Departamento: "; |PINTA 0572, eaDepSes;
          |FINSI;
          |ATRI 0;
      |FINSI;
  |FINSI;
|FPRC;

|| ========================================================================
||                        CALCULOS PANTALLA
|| ========================================================================

|PROCESO mayor;
  alfa1 = #0Campo > " ";
  #0Campo = alfa1;
  |PINTA #0Campo;
|FPRC;

|PROCESO pintames; |TIPO 0;
  nNume1 = Campo + 2;                || Coge el campo del nombre del mes
  |SI #0Campo ! 0; |Y #0Campo ! 13;
      mes = dig1 + (#0Campo - 1);   || Operacion para saber que mes es
      |SI mes > 12; mes = mes - 12; |FINSI;
  |SINO;
      mes = #0Campo;
  |FINSI;
  |HAZ mesletra;
  #0nNume1 = letrames; |PINTA #0nNume1;

  |SI Campo = 1; |Y  #0#0 > #0#1;
      |MENAV "    Error de Entrada ...";
      |ERROR;
  |FINSI;
|FPRC;

|PROCESO pintames2; |TIPO 0;
  |SI #0Campo ! 0; |Y #0Campo ! 13;
      mes = dig1 + (#0Campo - 1);   || Operacion para saber que mes es
      |SI mes > 12; mes = mes - 12; |FINSI;
  |SINO;
      mes = #0Campo;
  |FINSI;
  |HAZ mesletra;
  |ATRI I;
  |SI Campo = 7;  |PINTA 1424, letrames;  |FINSI;
  |SI Campo = 8;  |PINTA 1460, letrames;  |FINSI;
  |ATRI 0;
  |SI Campo = 8;  |Y #0#7 > #0#8;
      |MENAV "    Error de Entrada ...";
      |ERROR;
  |FINSI;
|FPRC;

|PROCESO mesletra;
  |SI mes = 0;  letrames = "Apertura  "; |FINSI;
  |SI mes = 1;  letrames = "Enero     "; |FINSI;
  |SI mes = 2;  letrames = "Febrero   "; |FINSI;
  |SI mes = 3;  letrames = "Marzo     "; |FINSI;
  |SI mes = 4;  letrames = "Abril     "; |FINSI;
  |SI mes = 5;  letrames = "Mayo      "; |FINSI;
  |SI mes = 6;  letrames = "Junio     "; |FINSI;
  |SI mes = 7;  letrames = "Julio     "; |FINSI;
  |SI mes = 8;  letrames = "Agosto    "; |FINSI;
  |SI mes = 9;  letrames = "Septiembre"; |FINSI;
  |SI mes = 10; letrames = "Octubre   "; |FINSI;
  |SI mes = 11; letrames = "Noviembre "; |FINSI;
  |SI mes = 12; letrames = "Diciembre "; |FINSI;
  |SI mes = 13; letrames = "Cierre    "; |FINSI;
|FPRC;

|PROCESO miraniv; |TIPO 0;
  nivel = #0#5;
  |SI nivel = 1;|Y dig4 = 0;  |AVISO;  |ERROR;  |FINSI;
  |SI nivel = 2;|Y dig5 = 0;  |AVISO;  |ERROR;  |FINSI;
  |SI nivel = 3;|Y dig6 = 0;  |AVISO;  |ERROR;  |FINSI;
  |SI nivel = 4;|Y dig7 = 0;  |AVISO;  |ERROR;  |FINSI;
  |SI nivel = 5;|Y dig8 = 0;  |AVISO;  |ERROR;  |FINSI;
  |SI nivel = 6;|Y dig9 = 0;  |AVISO;  |ERROR;  |FINSI;
|FPRC;

|PROCESO estruct; |TIPO 0;
  |SI #0#6 = 0;  |ACABA;  |FINSI;
  |ABRE #6;
  #6#0 = #0#6;
  |LEE 040#6=;
  |SI FSalida ! 0;
      |MENAV "    No existe la estructura de descarga.";
      |CIERRA #6;
      |ERROR;
      |ACABA;
  |FINSI;
  |CIERRA #6;
|FPRC;

|PROCESO conexi;
  d_mes = #0#7;
  h_mes = #0#8;
  estru = #0#6;
  r_emp = empre;
  r_per = ejerc;
  r_eje = #30#26;
  r_nom = #30#1;
  |SUB_EJECUTA "caexicon.run";
|FPRC;

|| ***************************************************************************
|| *********************  CALCULO DE IMPRESION *******************************
|| ***************************************************************************
|PROCESO acortacuen;
  |SI #1#1 < act_nivel;
      |SI #1#1 = 1;  traraf = 100 + dig4;  |FINSI;
      |SI #1#1 = 2;  traraf = 100 + dig5;  |FINSI;
      |SI #1#1 = 3;  traraf = 100 + dig6;  |FINSI;
      |SI #1#1 = 4;  traraf = 100 + dig7;  |FINSI;
      |SI #1#1 = 5;  traraf = 100 + dig8;  |FINSI;
      act_cuent = act_cuent % traraf;
      act_nivel = #1#1;
      act_cuent = act_cuent + "              ";
      act_cuent = act_cuent % 112;
  |FINSI;
|FPRC;

|PROCESO exis_expl;

  descar = #7des_exi - #7has_exi; || iniciales - finales
  |SI descar = 0; |ACABA; |FINSI;
  |SI descar ] 0;
      act_cuent = #7#5;     || cuenta descarga D
      act_nivel = #7#6;     || digito descarga D
  |SINO;
      act_cuent = #7#8;     || cuenta descarga H
      act_nivel = #7#9;     || digito descarga H
  |FINSI;

  |HAZ acortacuen;
  |SI act_cuent = #1#0; |Y act_nivel = #1#1;
      sw_exp = 1;
      |SI descar ] 0;
          debe  = debe  + descar;
      |SINO;
          haber = haber - descar;
      |FINSI;
  |FINSI;
|FPRC;

|DEFBUCLE exis2;
 #7#1;
 ;
 #0#6, 01;
 #0#6, 99;
 be;
 NULL, exis_expl;
|FIN;

|PROCESO exis_3000;
 sw_exp  = 1;
 debe    = debe  + #7des_exi + #7has_exi;
 haber   = haber + #7des_exi;
|FPRC;

|DEFBUCLE exis1;
 #7#2;
 ;
 #0#6, d_cuen, 0;
 #0#6, h_cuen, 9;
 e;
 NULL,exis_3000;
|FIN;

|PROCESO c_exis1;
 d_cuen = #1#0;
 h_cuen = #1#0;
 |SI #1#1 = 1;  traraf = 100 + dig4;  |FINSI;
 |SI #1#1 = 2;  traraf = 100 + dig5;  |FINSI;
 |SI #1#1 = 3;  traraf = 100 + dig6;  |FINSI;
 |SI #1#1 = 4;  traraf = 100 + dig7;  |FINSI;
 |SI #1#1 = 5;  traraf = 100 + dig8;  |FINSI;
 d_cuen = d_cuen % traraf;
 d_cuen = d_cuen + "              ";
 d_cuen = d_cuen % 112;
 h_cuen = h_cuen % traraf;
 h_cuen = h_cuen + "99999999999999";
 h_cuen = h_cuen % 112;
 |BUCLE exis1;
|FPRC;

|PROCESO impresio;
  |HAZ digitos2;  || ... Comprueba nivel de Cuenta para imprimir
  |SI d_error ! 0;                  |ACABA; |FINSI;
  |SI #1#1 > nivel;                 |ACABA; |FINSI;
  |SI #1#3 = "N";  |Y #1#1 ! nivel; |ACABA; |FINSI;

  debe     = 0; || ... Calcular cantidades
  haber    = 0;
  activo   = 0;
  pasivo   = 0;
  gastos   = 0;
  ingresos = 0;
  sw_exp  = 0;
  alfa1 = #1#0 % 101;
  |SI alfa1 = "3";                 |HAZ c_exis1; |FINSI;
  |SI alfa1 = "6"; |O alfa1 = "7"; |BUCLE exis2; |FINSI;
  |SI sw_exp = 0;
      |PARA x = eldesde; |SI x [ elhasta; |HACIENDO x = x + 3;
            y = x + 1;
            debe  = debe  + #1x;
            haber = haber + #1y;
      |SIGUE;
  |FINSI;

  |SI debe = 0; |Y haber = 0; |ACABA; |FINSI;

  |SI #1#5 = "A"; activo   = debe - haber; |FINSI;
  |SI #1#5 = "P"; pasivo   = haber - debe; |FINSI;
  |SI #1#5 = "D"; gastos   = debe - haber; |FINSI;
  |SI #1#5 = "H"; ingresos = haber - debe; |FINSI;
  sw_inf = 0;
  |PRINT;
  sw = 1;
  tdebe     = tdebe     + debe;
  thaber    = thaber    + haber;
  tactivo   = tactivo   + activo;
  tpasivo   = tpasivo   + pasivo;
  tgastos   = tgastos   + gastos;
  tingresos = tingresos + ingresos;
|FPRC;

|DEFBUCLE cabalgen1;
 #1#1;
 ;
 "            ", 0;
 "zzzzzzzzzzzz", 9;
 be;
 NULL,impresio;
|FIN;

|| ******************************** CALCULO TIPO 3
|PROCESO impre; |TIPO 3;
  |HAZ antesque;
  |HAZ conexi;

  |IMPRE 0;
  |SI FSalida ! 0;  |ACABA;  |FINSI;
  |INFOR informe;
  |SI FSalida ! 0;
      |MENAV "     Error en informe.";
      |FINIMP;
      |ACABA;
  |FINSI;

  eldesde = (#0#0 * 3) + 9;
  elhasta = (#0#1 * 3) + 9;
  des_exi = #0#7 + 11
  has_exi = #0#8 + 11

  |ABRE #8;
  |LEE 111#8p;
  |SI #8#2 = "S";  |Y #8#3 = "N";  |Y aparam = "";
      |DFICE pathbal;
      balance = 1;
      |CIERRA #8;
      |SUB_EJECUTA "careccue.tab";
  |FINSI;
  |CIERRA #8;

||... Modificar Cuentas a balance segun saldo
  r_emp = empre;
  r_per = ejerc;
  eldesde = (#0#0 * 3) + 11;
  elhasta = (#0#1 * 3) + 11;
  |SUB_EJECUTA "carecsig.run";
  eldesde = (#0#0 * 3) + 9;
  elhasta = (#0#1 * 3) + 9;
|| ---------------------------------------------------------------------

  |ABRET #1;
  |ABRET #7;
   |BUCLE cabalgen1;
   |HAZ final;
  |CIERRAT #1;
  |CIERRAT #7;
  |PIEINF;
  |FININF;
  |FINIMP;
|FPRC;

|| ******************************* C lculo Final
|PROCESO final;
 ac1 = 0; pa1 = 0; ga1 = 0; in1 = 0;
 ac2 = 0; pa2 = 0; ga2 = 0; in2 = 0;
 ac3 = 0; pa3 = 0; ga3 = 0; in3 = 0;

 pa1 = tactivo - tpasivo;
 |SI pa1 < 0; ac1 = - pa1; pa1 = 0; |FINSI;

 vari1 = "BENEFICIOS ANTES DE IMPUESTOS";
 vari2 = "BENEFICIOS DESPUES DE IMPUESTOS";
 ga1 = tingresos - tgastos;
 impuesto = (ga1 % 35) ! 0;
 |SI ga1 < 0;
     in1 = - ga1;
     ga1 = 0;
     vari1 = "PERDIDAS ANTES DE IMPUESTOS  ";
     vari2 = "PERDIDAS DESPUES DE IMPUESTOS";
 |FINSI;

 |SI impuesto ] 0;
     ac2 = 0;        ga2 = impuesto;
     pa2 = impuesto; in2 = 0;
 |SINO;
     ac2 = -impuesto; ga2 = 0;
     pa2 = 0;         in2 = -impuesto;
 |FINSI;

|SI #0#14 = "N";  ac2 = 0; pa2= 0; ga2 = 0; in2 = 0; swnocal = 1; |FINSI;
  ac3 = ac1 - ac2;
  pa3 = pa1 - pa2;
  ga3 = ga1 - ga2;
  in3 = in1 - in2;

|FPRC;
