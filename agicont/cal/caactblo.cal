|FICHEROS;
    caactblo #0;    || Pantalla
    camaasic #1;    || Apuntes Cabs
    camaasil #2;    || Apuntes Lins
    catrasi2 #3;    || Trabajo Ivas
    caentasc #4;    || apuntes Rapida cabs.
    caentasl #5;    || apuntes Rapida lins.

    catradia #9;    || Trabajo Cuentas
    camandia #10;   || Diario
    cacuenta #11;   || Cuentas
    caivarep #15;   || IVA Repercutido
    caivasop #16;   || IVA Soportado

    calicont #31;   || Lineas de Contrapartidas Asientos
    caliconb #32;   || Lineas de Contrapartidas Bloques
|FIN;

|VARIABLES;
  &enDiario   = 0;
  &enContador = 0;
  &entrada    = 1;
  &modcon     = 0;
  mensa1   = "    ERROR ESTA FECHA NO CORRESPONDE AL EJERCICIO";
  mensa3   = "    NO EXISTE BLOQUE DE APUNTES ";

  swvic    = 0;
  swvi2    = 0;
  tipoAct  = 0;
  cam      = 0;
  alfa1    = "";
  nume1    = 0;
  &bloque   = "";
  fichero  = "";
  estado   = 0;

  nApunts  = 0;
  nSw9999  = 0;
  cuedebe  = 0;
  cuehaber = 0;
  cuediario = 0;
  cuefecha = @;

  ini = 0;
  fecalf = "";
  mes = "";
  mesfec = 0;
  mesac = 0;
  a = 0;
  b = 0;
  c = 0;

  diario   = 0;
  fecha    = @;
  docume   = 0;
  asient   = 0;
  linea    = 0;

  debe     = 0;
  haber    = 0;
  entdia  = 0;
  entfec  = @;
  entdoc  = 0;
  &antdia  = 0;
  &antfec  = @;
  &antdoc  = 0;
  &numero  = 0;
  &error   = "";

  swnuevo  = 1;
  sw_error = 0;
  swcuadre = 0; || 0 = cuadrado; Descuadres -> 1 = pasar a Asientos
                ||                             2 = Salir de Actualizacion
|FIN;

|INCLUYE acceso_i;
|INCLUYE tempo_i;
|INCLUYE condes_i;

|| *************************************************************************
||                        DESDE CAMPOS DE PANTALLA
|| *************************************************************************

|PROCESO defbloque; |TIPO 7;
 |SI bloque ! "";
     #0#0 = bloque;
     |PINTA #0#0;
     |PTEC 802;
 |FINSI;
|FPRC;

|PROCESO leebloq; |TIPO 6;
 |ABRE #4;
 #4#0 = #0#0;
 |LEE 001#4=;
 |SI FSalida ! 0;
     |MENAV mensa3;
     |ERROR;
 |SINO;
      #0#7 = #4#4; |PINTA #0#7;
      #0#8 = #4#5; |PINTA #0#8;
      #0#9 = #4#6;
 |FINSI;
 |CIERRA #4;
|FPRC;

|PROCESO mayor; |TIPO 0;
  alfa1 = #0Campo > " ";
  #0Campo = alfa1;
  |PINTA #0Campo;
|FPRC;

|PROCESO tipoactual; |TIPO 0;
  |C_M #0#2, 1, "N";
  |C_M #0#3, 1, "N";
  |C_M #0#4, 1, "N";
  |C_M #0#5, 1, "N";

  |SI #0#1 = 2; |O #0#1 = 3;
      |C_M #0#2, 1, "S";
  |FINSI;
  |SI #0#1 = 4;
      |C_M #0#3, 1, "S";
      |C_M #0#4, 1, "S";
      |C_M #0#5, 1, "S";
  |FINSI;
|FPRC;

|PROCESO fecha; | TIPO 0;   || Desde/hasta fecha
  |SI #0#1 ! 4; |ACABA; |FINSI;
  |SI #0Campo < fec1; |O #0Campo > fec2;
      |MENAV mensa1;
      |ERROR;
  |FINSI;
|FPRC;

|PROCESO documento; |TIPO 7;
  enDiario = #0#3;
|SI #0#1 = 2; |O #0#1 = 3;
 |SI swvic = 0;
       |HAZ contador;
       swvic = 1;
       #0Campo = enContador;
 |FINSI;
  |PINTA #0Campo;
  |SI Campo = 5;
      |ABRE #1;
      #1#0 = #0#3;
      #1#1 = #0#4;
      #1#2 = #0#5;
      |LEE 001#1=;
      |SI FSalida = 0;
          |MENAV "    ERROR!. EL ASIENTO YA EXISTE ! ";
          |ERROR;
      |FINSI;
      |CIERRA #1;
  |FINSI;
|FINSI;
|FPRC;
|PROCESO document2; |TIPO 7;
  enDiario = #0#3;
|SI #0#1 = 4;
 |SI swvi2 = 0;
       |HAZ contador;
       swvi2 = 1;
       #0Campo = enContador;
 |FINSI;
  |PINTA #0Campo;
  |SI Campo = 5;
      |ABRE #1;
      #1#0 = #0#3;
      #1#1 = #0#4;
      #1#2 = #0#5;
      |LEE 001#1=;
      |SI FSalida = 0;
          |MENAV "    ERROR!. EL ASIENTO YA EXISTE ! ";
          |ERROR;
      |FINSI;
      |CIERRA #1;
  |FINSI;
|FINSI;
|FPRC;

|| **************************************************************************
|| ............................... Comprobar Cuadre
|PROCESO cbloque;
 |SI cam = 0;
     cam = 1;
     diario = #5#2;
     fecha  = #5#3;
     docume = #5#21;
     debe = 0;
     haber = 0;
     nApunts = -9;
 |FINSI;

 |SI docume ! #5#21; |Y tipoAct = 1;
     |SI debe ! haber;
         swcuadre = 1;
         |ERROR10;
         |ACABA;
     |SINO;
         diario = #5#2;
         fecha  = #5#3;
         docume = #5#21;
         debe = 0;
         haber = 0;
         nApunts = -9;
     |FINSI;
 |FINSI;

 |SI tipoAct ! 4;
     |SI diario ! #5#2; |O fecha ! #5#3;
         |SI debe ! haber;
             swcuadre = 1;
             |ERROR10;
             |ACABA;
         |SINO;
             diario = #5#2;
             fecha  = #5#3;
             docume = #5#21;
             debe = 0;
             haber = 0;
             nApunts = -9;
         |FINSI;
     |FINSI;
 |FINSI;

 |SI #5#8 = "D";
     debe = debe + #5#9;
 |SINO;
    haber = haber + #5#9;
 |FINSI;

 nApunts = nApunts + 10;
 |SI nApunts > 9999;
     nSw9999 = 1;
 |FINSI;

 |LIBERA #5;
|FPRC;

|PROCESO fincomblo;
 |SI debe ! haber;
     swcuadre = 1;
 |FINSI;
|FPRC;

|DEFBUCLE comcuablo;
 #5#4;
 ;
 00, "01.01.1900", 000001, 00001;
 99, "31.12.2100", 999999, 99999;
 e;
 NULL, cbloque, NULL, NULL, fincomblo;
|FIN;

|PROCESO comcuadre;
 |PUSHV 1501 1580;
 |ATRI I;
 |PINTA 1522, "Comprobando Cuadre ...";
 |ATRI 0;
 cam      = 0;
 debe     = 0;
 haber    = 0;
 swcuadre = 0;
 nApunts  = -9;
 nSw9999  = 0;

 |SI #0#7 ! #0#8;
     swcuadre = 1;
     cam = 1;
 |SINO;
     || |SI tipoAct ! 4;
         |BUCLE comcuablo;
     || |SINO;
     ||     cam = 1;
     || |FINSI;
 |FINSI;

 |SI nSw9999 = 1;
     |MENAV "0000Existe mas de 9999 apuntes. No puede actualizar";
     swcuadre = 2;
     |POPV;
     |ACABA;
 |FINSI;

 |SI swcuadre ! 0;
     |CONFI "1510NBloque con Descuadres. ¨ Desea Continuar ? ";
     |SI FSalida ! 0;
         swcuadre = 2;
     |FINSI;
 |FINSI;
 |POPV;
|FPRC;

|| **************************************************************************
|| ............................... Crear Asientos
|PROCESO ivas;
 |SI #5#2 = #2#0; |Y #5#3 = #2#1; |Y #5#21 = #2#2; |ACABA; |FINSI;
 #3#0 = #2#12;
 #3#1 = #2#13;
 #3#2 = #2#14;
 #3#3 = #2#15;
 |LEE 101#3=;
 |SI FSalida = 0; |ACABA; |FINSI;
 #3#0 = #2#12;
 #3#1 = #2#13;
 #3#2 = #2#14;
 #3#3 = #2#15;
 #3#4 = #5#2;
 #3#5 = #5#3;
 #3#6 = #5#21;
 #3#7 = #2#0;
 #3#8 = #2#1;
 #3#9 = #2#2;
 |GRABA 020#3n;
 |LIBERA #3;
|FPRC;

|PROCESO impresora;
  |SI sw_error = 0;
      |IMPRE 0;
      |SI FSalida ! 0;  |ACABA;  |FINSI;
      |INFOR "cadescua";
      |SI FSalida ! 0;  |FINIMP;  |ACABA;  |FINSI;
      sw_error = 1;
  |FINSI;
  numero = numero + 1;
  antdia = #2#0;
  antfec = #2#1;
  antdoc = #2#2;
  |PRINT;
|FPRC;

|PROCESO haycambio;
  |SI debe ! haber;
      error = "Descuadre.";
      |HAZ impresora;
  |FINSI;
  |HAZ nuevoAsi;
|FPRC;

|PROCESO nuevoAsi;
  debe   = 0;
  haber  = 0;
  entdia = #5#2;
  entfec = #5#3;
  entdoc = #5#21;
  swnuevo = 0;
  linea = -9;
  |SI tipoAct = 1;  || misma diario, fecha y documento
      diario = #5#2;
      fecha  = #5#3;
      docume = #5#21;
  |FINSI;
  |SI tipoAct = 2; |O tipoAct = 3; || mismo diario y fecha
      diario = #5#2;
      fecha  = #5#3;
      docume = #0#2;
      #0#2 = #0#2 + 1;
  |FINSI;
  |SI tipoAct = 4;  || Un solo asiento
      diario = #0#3;
      fecha  = #0#4;
      docume = #0#5;
  |FINSI;
|FPRC;

|| -------------------------------------------------------------------------
|PROCESO apuntes;

 |SI #5#9 = 0; |ACABA; |FINSI;
 |SI cam = 0;
     |HAZ nuevoAsi;
     cam = 1;
 |FINSI;

 |SI tipoAct !  4;
     |SI entdoc ! #5#21; |Y tipoAct = 1;
         |HAZ haycambio;
     |FINSI;
     |SI entdia ! #5#2; |O entfec ! #5#3;
         |HAZ haycambio;
     |FINSI;
     |SI tipoAct = 2;
         |SI linea > 1;
             |SI debe = haber; |HAZ nuevoAsi; |FINSI;
         |FINSI;
     |FINSI;
 |FINSI;

|| .............................. PROCESO grabalinea;
  |SI #5#9 = 0; |ACABA; |FINSI;
  |SI swnuevo = 0;
      |HAZ cabecera;
      swnuevo = 1;
  |FINSI;
 |PINTA 1522, #5#1;
 |PINTA 1528, #5#2;
 |PINTA 1531, #5#3;
 |PINTA 1542, #5#21;

    |DDEFECTO #2;
    #2#0 = diario;
    #2#1 = fecha;
    #2#2 = docume;
    linea = linea + 10;
    #2#3 = linea;
    #2#4 = #5#4;        || Cuenta Contable
    #2#5 = #5#5;        || digito control
    #2#6 = #5#6;        || concepto
    #2#7 = #5#7;        || Descripcion
    enNumCon = #2#6; |HAZ VerConcepto; #2#24 = eaDesCon;

    #2#8 = #5#8;        || Signo
    #2#9  = #5#9;       || Importe
    |HAZ adapta;

    #2#12 = #5#10;         || Rep/Sop/Cobro/Pago
    #2#13 = #5#11;         || Libro
    #2#14 = #5#12;         || Factura
    #2#15 = #5#13;         || serie (inhibida)
    #2#22 = #5#14;         || N§ recibo

    #2#19 = #5#15;         || Tipo Acumulardor
    #2#20 = #5#16;         || Numero Acumulador
    #2#21 = #5#17;         || Departamento

    |GRABA 020#2n;
    |LIBERA #2;
    |SI FSalida = 0;
        |SI #2#8 = "D";
            debe = debe + #2#9;
        |SINO;
            haber = haber + #2#9;
        |FINSI;
        |HAZ gratra;
        |HAZ act_cabecera;
        |SI #2#12 = "R"; |O #2#12 = "S"; |HAZ ivas; |FINSI;
        |HAZ a_contrapart;
        |HAZ a_bloque;
        |SI tipoAct = 4;
            |HAZ act_diario;
            |HAZ act_cuentas;
        |FINSI;
        |BORRA 020#5a;
        |LIBERA #5;
    |FINSI;
|FPRC;

|DEFBUCLE contabiliza1;
 #5#4;
 ;
 00, "01.01.1900", 000001, 00001;
 99, "31.12.2100", 999999, 99999;
 be;
 NULL, apuntes;
|FIN;

|DEFBUCLE contabiliza4;
 #5#1;
 ;
 #0#0, 00001;
 #0#0, 99999;
 be;
 NULL, apuntes;
|FIN;

|| **********************************************************************
|| ------------- Comprobar que existen
|PROCESO combloq;
 cam = 1;
 |ERROR10;
|FPRC;

|DEFBUCLE combloque;
 #5#1;
 ;
 #0#0, 00001;
 #0#0, 99999;
 e;
 NULL, combloq;
|FIN;
|| **************************************************************************

|PROCESO actual; |TIPO 3;

 |SI #0#6 = "N"; |ACABA; |FINSI;

 bloque = #0#0;
 fichero = bloque;
 |QBF fichero;
 fichero = fichero + "zzzzzzzz";
 fichero = fichero % 107;
 fichero = fichero + "l";

 |NOME_DAT #5 fichero;

 tipoAct = #0#1;
 cam = 0;
 |HAZ comcuadre;
 |SI swcuadre = 2; |ACABA; |FINSI;
 |SI cam = 0; |ACABA; |FINSI;

|| .................. Abre fichero Auxiliar de Cuentas
  |ABRE #9;
  |LEE 101#9u;
  |SI FSalida ! 0;
      |DDEFECTO #9;
      #9#0 = 1;
  |SINO;
      #9#0 = #9#0 + 1;
  |FINSI;
  |GRABA 101#9n;
  codcon = #9#0;
  |LIBERA #9;
|| ----------------------------------------------------------

 |HAZ nom_usu;
 alfa1 = "p" + nom_tmp;
 |NOME_DAT #3 alfa1;

 |ABRE #3;
 |CIERRA #3;
 |DELINDEX #3;

 |ABRE #1;
 |ABRE #2;
 |ABRE #3;

 |ABRE #32;
 cam = 0;
 |SI tipoAct = 4;
     |BUCLE contabiliza4;
 |SINO;
     |BUCLE contabiliza1;
 |FINSI;

 |SI cam ! 0;
     |SI debe ! haber;
         error = "Descuadre.";
         |HAZ impresora;
     |FINSI;
 |FINSI;

 |SI sw_error = 1;
     |PIEINF;
     |FININF;
     |FINIMP;
 |FINSI;

 |CIERRA #1;
 |CIERRA #2;
 |CIERRA #3;
 |CIERRA #9;

 |CIERRA #32;
|| ............... Terminar
 cam = 0;
 |BUCLE combloque;   || Comprobar si queda algo;
 |HAZ final;

 |SI modcon = 0;
     |ABRET #9;
     |DDEFECTO #9;
     #9#0 = codcon;
     |LEE 040#9=;
     |BORRA 101#9a;
     |CIERRAT #9;
 |SINO;
     |CORRE "camaapua.tab"
 |FINSI;
|FPRC;

|| **************************************************************************

|PROCESO modi_iva;
 |SI #3#0 = "R";
     #15#0 = #3#1;
     #15#2 = #3#2;
     #15#27 = #3#3;
     |LEE 101#15=;
     |SI FSalida ! 0; |ACABA; |FINSI;
     #15#45 = #3#7;
     #15#46 = #3#8;
     #15#47 = #3#9;
     |GRABA 020#15a;
     |LIBERA #15;
 |FINSI;
 |SI #3#0 = "S";
     #16#0 = #3#1;
     #16#2 = #3#2;
     #16#27 = #3#3;
     |LEE 101#16=;
     |SI FSalida ! 0; |ACABA; |FINSI;
     #16#45 = #3#7;
     #16#46 = #3#8;
     #16#47 = #3#9;
     |GRABA 020#16a;
     |LIBERA #16;
 |FINSI;
|FPRC;

|DEFBUCLE catrasi2;
 #3#1;
 ;
 " ", 00, 000000, "  ";
 "z", 99, 999999, "zz";
 be;
 NULL, modi_iva;
|FIN;

|PROCESO final;

|| ... Actualiza cabeceras de Bloques
 |ABRE #4;
 #4#0 = #0#0;
 |LEE 001#4=;
 estado = FSalida;
 |SI cam = 0;
     |BORRA 020#4a;
     |DELINDEX #5;
 |SINO;
     |ABRE #5;
     |DDEFECTO #5;
     |LEE 001#5u;
     |CIERRA #5;
     #4#0 = #0#0;
     #4#1 = #5#2;
     #4#2 = #5#3;
     #4#3 = #5#21;
     #4#4 = #0#7;
     #4#5 = #0#8;
     #4#6 = #0#7 - #0#8;
     |SI estado = 0; |GRABA 020#4a; |FINSI;
     |SI estado ! 0; |GRABA 020#4n; |FINSI;
 |FINSI;
 |CIERRA #4;

|| ... Actualiza el numero de Documento Actual.
   enDiario   = #0#3;
   enContador = docume;
   |HAZ GrabaContador;

|| ... Proceso de IVAS
 |BLIN 24;
 |PINTA 2410,"Relacionando Fichero de Facturas ... ";
   |ABRE #15;
   |ABRE #16;
   |BUCLE  catrasi2;
   |CIERRA #15;
   |CIERRA #16;
   |DELINDEX #3;
|FPRC;

|| **************************************************************************

|| ............ Subrutina de Asientos
|PROCESO adapta;
|SI #2#8 = "D";
        #2#16 = #2#4;
        #2#17 = #2#5;
        #2#10 = "";
        #2#11 = 0;
|SINO;
        #2#16 = "";
        #2#17 = 0;
        #2#10 = #2#4;
        #2#11 = #2#5;
|FINSI;
|FPRC;

|PROCESO cabecera; || lee la cabecera.
  |DDEFECTO #1;
  #1#0 = diario;
  #1#1 = fecha;
  #1#2 = docume;
  #1#3 = docume;
  linea = -9;
  |GRABA 040#1n;      || Puesto el 4 para que puedan regrabar cabeceras....
  |SI FSalida ! 0;
      #2#0 = #1#0;
      #2#1 = #1#1;
      #2#2 = #1#2;
      #2#3 = 9999;
      |LEE 040#2];
      |SI FSalida = 0;
          |LEE 040#2a;
          |SI #2#0 = #1#0; |Y #2#1 = #1#1; |Y #2#2 = #1#2;
              linea = #2#3;
          |FINSI;
      |SINO;
          |LEE 040#2u;
          |SI FSalida = 0; |Y  #2#0 = #1#0; |Y #2#1 = #1#1; |Y #2#2 = #1#2;
              linea = #2#3;
          |FINSI;
      |FINSI;
  |FINSI;
|FPRC;

|PROCESO act_cabecera;
  #1#0 = diario;
  #1#1 = fecha;
  #1#2 = docume;
  |LEE 101#1=;
  |SI #2#8 = "D";
       #1#4 = #1#4 + #2#9;
       |SI #1#09 = 0; #1#08 = #2#4; #1#09 = #2#5; |FINSI;  || contrp.HABER
  |SINO;
       #1#5 = #1#5 + #2#9;
       |SI #1#11 = 0; #1#10 = #2#4; #1#11 = #2#5; |FINSI;  || contrp.DEBE
  |FINSI;
  #1#6 = #1#4 - #1#5;        || saldo
  |GRABA 020#1a;
  |LIBERA #1;
|FPRC;

|PROCESO gratra;
  modcon = 1;               || graba el temporal para la actualiz. de ctas.
  #9#0 = codcon;
  #9#1 = #2#4;
  #9#2 = #2#5;
  #9#3 = #2#1;
  #9#4 = #2#8;
  #9#6 = #2#0;
  |LEE 101#9=;
  |SI FSalida ! 0;
      |DDEFECTO #9;
        #9#0 = codcon;
        #9#1 = #2#4;
        #9#2 = #2#5;
        #9#3 = #2#1;
        #9#4 = #2#8;
        #9#6 = #2#0;
      |GRABA 020#9b;
  |FINSI;
  #9#5 = #9#5 + #2#9;
  |GRABA 020#9a;
  |LIBERA #9;
|FPRC;

|PROCESO a_bloque; || actualiza bloque
  |SI #5#8 = "D";
      #0#7 = #0#7 - #5#9;
  |SINO;
      #0#8 = #0#8 - #5#9;
  |FINSI;
  #0#9 = #0#7 - #0#8;
  |PINTA #0#7;
  |PINTA #0#8;
|FPRC;

|PROCESO a_contrapart;
 #32#0 = #5#0;
 #32#1 = #5#1;
 |LEE 101#32=;
 |SI FSalida = 0;
     |ABRE #31;
     #31#0 = #2#0;
     #31#1 = #2#1;
     #31#2 = #2#2;
     #31#3 = #2#3;
     |LEE 101#31=;
     |SI FSalida ! 0;
         #31#0 = #2#0;
         #31#1 = #2#1;
         #31#2 = #2#2;
         #31#3 = #2#3;
         |GRABA 020#31b;
     |FINSI;
     #31#4 = #32#2;
     #31#5 = #32#3;
     |GRABA 020#31a;
     |LIBERA #31;
     |CIERRA #31;
     |BORRA 020#32a;
 |FINSI;
 |LIBERA #32;
|FPRC;

|| *************************************************************************
||         ACTUALIZA ESPECIALES TIPO 4
|| *************************************************************************
|PROCESO act_cuentas;
 |ABRE #11;
 #11#0 = #2#4;
 #11#1 = #2#5;
 |LEE 011#11=;
 |SI FSalida = 0;
     cuedebe  = 0;
     cuehaber = 0;
     cuediario = #5#2;
     cuefecha  = #5#3;
     |SI #5#8 = "D";
         cuedebe = - #5#9;
     |SINO;
         cuehaber = - #5#9;
     |FINSI;
     |HAZ a_cuenta;

     cuedebe  = 0;
     cuehaber = 0;
     cuediario = #2#0;
     cuefecha  = #2#1;
     |SI #2#8 = "D";
         cuedebe =  #2#9;
     |SINO;
         cuehaber =  #2#9;
     |FINSI;
     |HAZ a_cuenta;
     |GRABA 020#11a;
 |FINSI;
 |LIBERA #11;
 |CIERRA #11;
|FPRC;

|PROCESO a_actualiza;                || rutina 'a_cuentas'
  ini = dig1;
  fecalf = cuefecha;
  mes = fecalf % 402;
  mesfec = mes;
  |SI ini = 1;
      mesac = mesfec;
  |SINO;
      mesac = (mesfec - ini) + 1;
      |SI mesac [ 0;
          mesac = (13 - ini) + mesfec;
      |FINSI;
  |FINSI;
  a = (mesac * 3) + 9;
  b = a + 1;
  c = a + 2;
  #11a   = #11a   + cuedebe;
  #11#51 = #11#51 + cuedebe;
  #11b   = #11b   + cuehaber;
  #11#52 = #11#52 + cuehaber;
  #11c   = #11a   - #11b;
  #11#53 = #11#51 - #11#52;
|FPRC;

|PROCESO a_cuenta;
  |SI cuediario = 0;
      #11#9  = #11#9  + cuedebe;        || diario cero (saldos iniciales)
      #11#51 = #11#51 + cuedebe;
      #11#10 = #11#10 + cuehaber;
      #11#52 = #11#52 + cuehaber;
      #11#11 = #11#9  - #11#10;
      #11#53 = #11#51 - #11#52;
  |SINO;
      |SI cuediario = 99;
          #11#48 = #11#48 + cuedebe;   || diario 99 (saldos finales)
          #11#51 = #11#51 + cuedebe;
          #11#49 = #11#49 + cuehaber;
          #11#52 = #11#52 + cuehaber;
          #11#50 = #11#48 - #11#49;
          #11#53 = #11#51 - #11#52;
      |SINO;
          |HAZ a_actualiza;           || saldos del mes correspondiente
      |FINSI;
  |FINSI;
|FPRC;

|PROCESO act_diario; || actualiza diario
  |SI #2#0 = #5#2; |ACABA; |FINSI;
  |ABRE #10;
  #10#0 = #5#2;
  |LEE 011#10=;
  |SI FSalida = 0;
      |SI #5#8 = "D";
          #10#2 = #10#2 - #5#9;
      |SINO;
          #10#3 = #10#3 - #5#9;
      |FINSI;
      |GRABA 020#10a;
      |LIBERA #10;
 |FINSI;
 #10#0 = #2#0;
 |LEE 011#10=;
 |SI FSalida = 0;
     |SI #2#8 = "D";
         #10#2 = #10#2 + #2#9;
     |SINO;
         #10#3 = #10#3 + #2#9;
     |FINSI;
     |GRABA 020#10a;
     |LIBERA #10;
 |FINSI;
 |CIERRA #10;
|FPRC;
