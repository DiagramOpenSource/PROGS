|FICHEROS;
    camancob #0;
    cacuenta #1;
    caclipro #2;
    calibros #10;
    caremesl #11;
|FIN;

|VARIABLES;
    &entrada = 1;
    long = 0;
    sw = 0;
    nume1 = 0;
    nume2 = 0;
    nume3 = 0;
    nume4 = 0;
    alfa1 = "";
    alfa2 = "";
    alfa3 = "";
    alfa4 = "";
    VtoAnt = "";
    FEstado = 0;
    swf1 = 0;

    SwAct = 0;
|FIN;

|INCLUYE acceso_i;
|INCLUYE puntos_i;

|PROCESO mayus;
  #0Campo = #0Campo > " ";
  |PINTA #0Campo;
|FPRC;

|PROCESO cuenta; |TIPO 6;
    p_alfa1 = #0Campo;
    salidas = FSalida;
|HAZ punto;
|SI p_error ! 0;  |ERROR;  |ACABA;  |FINSI;
    alfa1 = #0#4;
|QBF alfa1;
    alfa1 = alfa1 % 0;
    long = alfa1;
    sw   = 0;
    #0#5 = 0;  || inicializa el digito
|SI long = dig4; |Y dig4 ! 0; sw = 1; #0#5 = 1; |FINSI;
|SI long = dig5; |Y dig5 ! 0; sw = 1; #0#5 = 2; |FINSI;
|SI long = dig6; |Y dig6 ! 0; sw = 1; #0#5 = 3; |FINSI;
|SI long = dig7; |Y dig7 ! 0; sw = 1; #0#5 = 4; |FINSI;
|SI long = dig8; |Y dig8 ! 0; sw = 1; #0#5 = 5; |FINSI;
|SI long = dig9; |Y dig9 ! 0; sw = 1; #0#5 = 6; |FINSI;
|SI sw = 0;|Y long ! 0;
    |MENAV "    Longitud de Cuenta Fuera de Nivel !";
    |ERROR;
|FINSI;
|FPRC;

|PROCESO cuenta2; |TIPO 0;
|ABRE #1;
|SELECT #1#1;
    #1#0 = #0#4;
    #1#1 = #0#5;
|LEE 001#1=;
    FEstado = FSalida;
|SI FEstado ! 0;  |DDEFECTO #1;  |FINSI;
|ATRI I;
|PINTA 1036, #1#2;
|ATRI 0;
|SI FEstado = 0; |Y #1#3 = "N";
    |MENAV "    Cuenta sin pase directo ...";
    |ERROR;
|FINSI;
|CIERRA #1;
|FPRC;

|PROCESO defecto; |TIPO 0;
  |SI #0#4 ! Contenido;
     SwAct = 1;
  |SINO;
     SwAct = 0;
  |FINSI;

  |ABRE #2;
  #2#23 = "C";
  #2#10 = #0#4;  || cuenta
  #2#11 = #0#5;  || digito
  |LEE 001#2=;
  |SI FSalida = 0; |Y SwAct = 1;
      #0#15 = #2#13;   || banco
      #0#16 = #2#14;   || direccion
      #0#17 = #2#15;   || cod.postal 1
      #0#18 = #2#16;   || cod.postal 2
      #0#19 = #2#17;   || cta.cte.
      #0#20 = #2#18;   || ccc
      #0#21 = #2#19;   || poblacion
  |FINSI;
  |PINDA #0#0;
  |CIERRA #2;
|FPRC;

|PROCESO pintadas; |TIPO 13;
|ABRE #1;
    #1#0 = #0#4;
    #1#1 = #0#5;
|LEE 000#1=;
|SI FSalida ! 0;
    |DDEFECTO #1;
        #1#2 = "<CUENTA INEXISTENTE>";
|FINSI;
|ATRI I;
|PINTA 1036, #1#2;
|ATRI 0;
|CIERRA #1;
|FPRC;

|PROCESO serieSi;
 |SI swf1 = 0;
     |C_V #0#2,1,"S";
     |C_M #0#2,1,"S";
   ||  |PINTA 0821, "Serie: ";
     |PINTA 0821, "S:   ";
     swf1 = 1;
 |SINO;
     |C_V #0#2,1,"N";
     |C_M #0#2,1,"N";
   ||  |PINTA 0821, "       ";
     |PINTA 0821, "     ";
     swf1 = 0;
 |FINSI;
|FPRC;

|PROCESO funcio1; |TIPO 0;
|SI FSalida = 9;
    |HAZ serieSi;
    |ERROR;
    |ACABA;
|FINSI;
|FPRC;

|PROCESO cheq_libro; |TIPO 0;
|SI FSalida = 9;
    |HAZ serieSi;
    |ERROR;
    |ACABA;
|FINSI;
|SI #10#2 ! "R";
    |MENAV "    ATENCION!!! Libro de I.V.A. Soportado ...";
    |ERROR;
|FINSI;
|FPRC;

|PROCESO lee_libro1;
|ERROR10;
|FPRC;

|DEFBUCLE lee_libro;
 #10#1;
 2;
  1, "R";
 99, "R";
 e;
 NULL, lee_libro1;
|FIN;

|PROCESO deflib; |TIPO 7;
|DDEFECTO #10;
|BUCLE lee_libro;
    #0#0 = #10#0;
|PINTA #0#0;
|FPRC;

|PROCESO AntesVto; |TIPO 7;
   VtoAnt = #camancob#7;
|FPRC;

|PROCESO TrasVto;
   |SI VtoAnt ! #camancob#7;
      |SI #camancob#12 ! 0;
         |ABRE #caremesl;
         |SELECT #2#caremesl;
         #caremesl#2 = #camancob#0;
         #caremesl#3 = #camancob#1;
         #caremesl#4 = #camancob#2;
         #caremesl#5 = #camancob#3;
         |LEE 000#caremesl.=;
         |SI FSalida = 0;
            |SI #camancob#7 ! #caremesl#6;
               |CONFI "    NLa fecha de vencimiento del efecto de la remesa sera distinta a la del recibo. ¨ Continuar ?";
               |SI FSalida ! 0;
                  #camancob#7 = VtoAnt; |PINTA #camancob#7;
                  |ERROR;
               |FINSI;
            |FINSI;
         |FINSI;
         |SELECT #1#caremesl;
         |CIERRA #caremesl;
      |FINSI;
   |FINSI;
|FPRC;
