|FICHEROS;
  cacieex3 #00;   || def de proceso
  cacuenta #01;   || cuentas
  camaasic #02;   || apuntes cabs.
  camaasil #03;   || apuntes lins.
  caexistc #06;   || cabs. descarga exist.
  caconcep #09;   || conceptos automaticos
  camandia #10;   || diarios
  canempre #30;

  ca8m0011 #431;
  ca8m0012 #432;
|FIN;

|VARIABLES;
    &enDiario   = 0;
    &enContador = 0;
    &entrada = 1;
    fecalf = "";
    ini = 0;
    mes = "";
    mesfec = 0;
    mesac = 0;
    a = 0;
    b = 0;
    c = 0;
    importe = 0;
    opera = "";
    &modcon = 0;

    FEstado = 0;
    nume1 = 0;
    nume2 = 0;
    nume3 = 0;
    nume4 = 0;
    alfa1 = "";
    alfa2 = "";
    alfa3 = "";
    alfa4 = "";

    posici = 0;
    linea = 0;
    asie = 0;
    line = 0;
    impo = 0;
    nada = " ";
    zeta = "z";
    abre = 0;
    antes = 0;
    desbloquea = 0;
    descar  = 0;
    nInkey  = 0;
    aBlancs = "";
    swMovi  = 0;

    &enErrorW17 = 0;
    nSwError    = 0;
    LaCuenta    = "";
    ElDigito    = 0;
    nImport0    = 0;
    lee         = 0;
    aDCuenta    = "";
    aHCuenta    = "";
    nSwOp       = 0;
    nDebe       = 0;
    nHaber      = 0;
|FIN;


|INCLUYE inici1_i;
|INCLUYE protec_i;
|INCLUYE condes_i;
|INCLUYE vardep_i;

========================================================================
                        CALCULOS PANTALLA
========================================================================
|PROCESO elTipo8; |TIPO 8;
 |HAZ comienzo;
 |HAZ VerCaconimp;
 |C_V #0#14,1,eaConDes;
 |C_V #0#19,1,eaConDes;
 |SI eaConDes = "N";
     |C_P #0#5,1,1225;
     |C_P #0#17,1,1325;
 |FINSI;
|FPRC;

|PROCESO defecto; |TIPO 7;
  #0Campo = fec2;
  |PINTA #0Campo;
|FPRC;

|PROCESO topes; |TIPO 0;
  |SI #0Campo < fec1; |O #0Campo > fec2;
      |MENAV "    ESTA FECHA NO CORRESPONDE AL EJERCICIO";
      |ERROR;
  |FINSI;
|FPRC;

|PROCESO estruct; |TIPO 0;
  |SI #0#6 = 0;  |ACABA;  |FINSI;
  |ABRE #6;
  #6#0 = #0#6;
  |LEE 001#6=;
  |SI FSalida ! 0;
      |MENAV "    No existe la estructura de descarga.";
      |ERROR;
  |FINSI;
  |CIERRA #6;
|FPRC;

|PROCESO AntesDep; |TIPO 7;
 alfa1 = #30#24;
 |C_M #0#16,1,alfa1;
|FPRC;

|CALCULO LosDep; |TIPO 0;
   nInkey = FSalida;
   |SI eaDepar = "N"; |ACABA; |FINSI;

   aBlancs = " "* 30; |PINTA 1731, aBlancs;
   eOpDep = 0; || 1 = Consulta pantalla
   |SI nInkey = 10; eOpDep = 1;  |FINSI;
   eaCodDep    = #0Campo;
   |HAZ Actividad;
   |SI eaCodDep = ""; eswLect = 0; |FINSI;
   |SI eswLect = 1;
       |MENAV "    Error.! No Existe Departamento";
       |ERROR;
   |SINO;
       #0Campo = eaCodDep; |PINTA #0Campo;
       |ATRI I; |PINTA 1731, eaNombreAct; |ATRI 0;
   |FINSI;
|FCAL;

|PROCESO concepto; |TIPO 0;
  |ABRE #9;
  #9#0 = #0Campo;
  |LEE 021#9=;
  |SI FSalida ! 0;
      |DDEFECTO #9;
  |FINSI;
  |SI Campo = 10; #0#14 = #9#1; |FINSI;
  |SI Campo = 18; #0#19 = #9#1; |FINSI;
  |SI eaConDes = "N";
      |SI Campo = 10; #0#5  = #9#1; |PINTA #0#5; |FINSI;
      |SI Campo = 18; #0#17 = #9#1; |PINTA #0#17; |FINSI;
  |SINO;
      |SI Campo = 10; |PINTA #0#14; |FINSI;
      |SI Campo = 18; |PINTA #0#19; |FINSI;
  |FINSI;
  |CIERRA #9;
|FPRC;

|CALCULO Ptec816; |TIPO 7;
 |SI eaConDes ! "N";
     #0Campo = "";
 |FINSI;
 |PTEC 816;
|FCAL;

|PROCESO diario_expl; |TIPO 0;
  |ABRE #10;
  #10#0 = #0#11;
  |LEE 021#10=;
  |SI FSalida ! 0;
      |DDEFECTO #10;
  |FINSI;
  #0#13 = #10#1;
  |PINTA #0#13;
  |CIERRA #10;
|FPRC;

========================================================================
                        PROCESO PADRE
========================================================================

|PROCESO proceso; |TIPO 3;

 || ... Primer proceso para Imputacion al Patrimonio Neto
 nSwError = 0;
 |HAZ AstoNuevo;
 |SI nSwError = 1;
    |MENAV "    Asiento de Imputacion al Patrimonio no realizado. Proceso Interrumpido";
    |ACABA;
 |FINSI;

  |ABRE #2;
  |ABRE #3;
  |HAZ expl0;     || asiento explotacion y variaciones de existencias
  |BLIN 4;
  |HAZ final;     || actualiza niveles
  |CIERRA #2;
  |CIERRA #3;
  |SI swMovi = 0;
      |MENAV "    Atencion. No hay movimientos en las Cuentas 6 y 7. No se ha realizado el Asiento de Explotacion";
  |FINSI;
|FPRC;

|PROCESO carga;
  |SI #3#08 = "H";
      #3#10 = #3#4;
      #3#11 = #3#5;
      #3#16 = "";
      #3#17 = 0;
  |SINO;
      #3#10 = "";
      #3#11 = 0;
      #3#16 = #3#4;
      #3#17 = #3#5;
  |FINSI;
|FPRC;

========================================================================
                     ASIENTO EXPLOTACION
========================================================================

|PROCESO expl5;         || perdidas y ganacias
  impo = #2#4 - #2#5;   || saldo de la cabecera del asiento de explotacion
  |SI impo = 0;  |ACABA;  |FINSI;
  line = line + 3;
  |DDEFECTO #3;
  #3#00 = #0#11; ||  diario
  #3#01 = #0#0;  ||  fecha
  #3#02 = asie;  ||  documento
  #3#03 = line;  ||  linea
  |LEE 101#3=;
  FEstado = FSalida;
  #3#04 = #6#2;  ||  cuenta
  #3#05 = #6#3;  ||  digito
  #3#06 = #0#10; ||  concepto (explotacion)
  #3#07 = #0#5;  ||  descripcion (explotacion)
  #3#24 = #0#14;
  #3#21 = #0#16;
  |SI impo > 0;  #3#08 = "H";  |FINSI;
  |SI impo < 0;  #3#08 = "D";  impo = impo * -1;  |FINSI;
  |HAZ carga;
  #3#09 = impo;  ||  importe
  |SI FEstado = 0;
      |GRABA 020#3a;
  |SINO;
      |GRABA 020#3n;
  |FINSI;
  |LIBERA #3;
  |HAZ cabecera;
  |HAZ actualiz1;   || actualiza cuentas
  opera = "A";
  |HAZ altatra;     || actualiza niveles
  swMovi = 1;
|FPRC;

|PROCESO Asto917;
        |SI lee = 1;
            #1#0 = LaCuenta;
            #1#1 = ElDigito;
            |LEE 101#1=;
            |SI FSalida ! 0;
                nSwError = 2;
                |ACABA;
            |FINSI;
        |FINSI;

        |DDEFECTO #3;
        #3#00 = #0#11;     ||  diario
        #3#01 = #0#0;      ||  fecha
        #3#02 = asie;      ||  documento
        #3#03 = line;      ||  linea
        |LEE 101#3=;
        FEstado = FSalida;
        #3#04 = LaCuenta;  ||  cuenta
        #3#05 = ElDigito;  ||  digito
        #3#06 = #0#18; ||  concepto
        #3#07 = #0#17; ||  descripcion
        #3#24 = #0#19;
        #3#21 = #0#16;
        |SI impo > 0;  #3#08 = "H";  |FINSI;
        |SI impo < 0;  #3#08 = "D";  impo = impo * -1;  |FINSI;
        |HAZ carga;
        #3#09 = impo;  ||  importe
        |SI FEstado = 0;  |GRABA 020#3a;  |FINSI;
        |SI FEstado ! 0;  |GRABA 020#3n;  |FINSI;
        |LIBERA #3;
        |HAZ cabecera;
        |HAZ actualiz1;   || actualiza cuentas
        opera = "A";
        |HAZ altatra;     || actualiza niveles
        line = line + 3;
        swMovi = 1;
        |SI #3#8 = "D";
            nDebe  = (nDebe + #3#9) ! EURODB;
        |SINO;
            nHaber = (nHaber + #3#9) ! EURODB;
        |FINSI;
|FPRC;

|PROCESO LasLineas;
 LaCuenta  = #432#2;
 ElDigito  = #432#5;
 impo      = #432#4;
 |SI nImport0 > 0; impo = - impo; |FINSI;
 |HAZ Asto917;
|FPRC;

|DEFBUCLE LasLineas;
 #432#1;
 ;
 #431#0, 01;
 #431#0, 99;
 e;
 NULL, LasLineas;
|FIN;

|PROCESO CreaApunte;

    #431#0 = LaCuenta;
    #431#5 = ElDigito;
    |LEE 001#431=;
    |SI FSalida ! 0;
        nSwError = 2;
        |ACABA;
    |FINSI;

    |SI line = 1;
        enDiario = #0#11;
        |HAZ contador;
        asie = enContador;
    |FINSI;

    impo = nImport0;
    |HAZ Asto917;

    |SALVA_DATOS #1;
    LaCuenta = #431#2; |QBF LaCuenta;
    ElDigito = #431#6;
    lee = 1;
    |SI LaCuenta ! "";
        impo = -nImport0;
        |HAZ Asto917;
    |SINO;
        |BUCLE LasLineas;
    |FINSI;
    lee = 0;
    |REPON_DATOS #1;
|FPRC;

|PROCESO expl4;   || apuntes para saldar explotacion
  |SI #1#3 ! "S";  |ACABA;  |FINSI;
  |SI #1#53 = 0;   |ACABA;  |FINSI;
  |PINTA 0436, #1#0;
  LaCuenta = #1#0;
  ElDigito = #1#1;
  impo     = #1#53;
  nImport0 = #1#53;

  |SI nSwOp = 1; |HAZ CreaApunte; |FINSI;

  |SI nSwOp = 0;
      |SI line = 1;
          enDiario = #0#11;
          |HAZ contador;
          asie = enContador;
      |FINSI;
      |DDEFECTO #3;
      #3#00 = #0#11; ||  diario
      #3#01 = #0#0;  ||  fecha
      #3#02 = asie;  ||  documento
      #3#03 = line;  ||  linea
      |LEE 101#3=;
      FEstado = FSalida;
      #3#04 = LaCuenta;  ||  cuenta
      #3#05 = ElDigito;  ||  digito
      #3#06 = #0#10; ||  concepto (explotacion)
      #3#07 = #0#5;  ||  descripcion (explotacion)
      #3#24 = #0#14;
      #3#21 = #0#16;
      |SI impo > 0;  #3#08 = "H";  |FINSI;
      |SI impo < 0;  #3#08 = "D";  impo = impo * -1;  |FINSI;
      |HAZ carga;
      #3#09 = impo;  ||  importe
      |SI FEstado = 0;  |GRABA 020#3a;  |FINSI;
      |SI FEstado ! 0;  |GRABA 020#3n;  |FINSI;
      |LIBERA #3;
      |HAZ cabecera;
      |HAZ actualiz1;   || actualiza cuentas
      opera = "A";
      |HAZ altatra;     || actualiza niveles
      line = line + 3;
      swMovi = 1;
  |FINSI;
  |LIBERA #1;
|FPRC;

|DEFBUCLE cacieexp2;
 #1#1;
 ;
 aDCuenta, 0;
 aHCuenta, 9;
 be;
 NULL,expl4;
|FIN;

|PROCESO expl0;
   |ATRI R;
   |PINTA 0401,"       Proceso en Explotacion ...:              ";
   |ATRI 0;
   abre = 0;
   line = 1;
   nSwOp    = 0;
   aDCuenta = "6           ";
   aHCuenta = "799999999999";
   |BUCLE cacieexp2;
   abre = 1;
   |HAZ expl5;
|FPRC;

|PROCESO AstoNuevo;
  |SUB_EJECUTA "ca8z0011;CIERRE";
  |SI enErrorW17 = 1;
      nSwError = 1;   ||no esta bien el reparto
      |ACABA;
  |FINSI;

  |ATRI S;
  |PINTA 2301,"  Proceso en Imputacion P. Neto .:              ";
  |ATRI 0;

  nDebe  = 0;
  nHaber = 0;
  abre = 0;
  line = 1;
  nSwError = 0;
  nSwOp    = 1;
  aDCuenta = "8           ";
  aHCuenta = "999999999999";
  |ABRE #2;
  |ABRE #3;
  |ABRE #431;
  |BUCLE cacieexp2;
  |CIERRA #431;
  |CIERRA #3;
  |CIERRA #2;

  |SI nSwError = 2;
      |MENAV "    Repase el Asto de Imputacion al Patrimonio, puede estar descuadrado";
      nSwError = 0;
  |FINSI;
|FPRC;

**********************************************************************
    ACTUALIZACION DE NIVELES INFERIORES DE CUENTAS
**********************************************************************

|PROCESO altatra;
  modcon = 1;
  |ABRE #8;
  #8#0 = codcon;
  #8#1 = #3#4;
  #8#2 = #3#5;
  #8#3 = #3#1;
  #8#4 = #3#8;
  #8#6 = #3#0;
  |LEE 101#8=;
  |SI FSalida ! 0;
      |DDEFECTO #8;
      #8#0 = codcon;
      #8#1 = #3#4;
      #8#2 = #3#5;
      #8#3 = #3#1;
      #8#4 = #3#8;
      #8#6 = #3#0;
      |GRABA 101#8b;
  |FINSI;
  importe = #3#9;  || aqui si que se usa el importe real
  |SI opera = "B";
      importe = -importe;
  |FINSI;
  #8#5 = #8#5 + importe;
  |GRABA 101#8a;
  |LIBERA #8;
  |CIERRA #8;
|FPRC;

|PROCESO final;
  |SI modcon ! 0;
      |SUB_EJECUTA "camaapua.run";
  |SINO;
      |ABRE #8;
      |DDEFECTO #8;
      #8#0 = codcon;
      |LEE 001#8=;
      |SI FSalida = 0;
          |BORRA 020#8a;
      |FINSI;
      |CIERRA #8;
  |FINSI;
|FPRC;

****************************************************************************
   ACTUALIZACION DE CUENTAS Y CABECERAS DE APUNTES Y LECTURA ULT.ASIENTO
****************************************************************************

|PROCESO cabecera;
  |SI #3#9 = 0;  |ACABA;  |FINSI;
  |DDEFECTO #2;
  #2#00 = #3#0; ||  diario
  #2#01 = #3#1; ||  fecha
  #2#02 = #3#2; ||  documento
  #2#03 = #3#2; ||  asiento
  |LEE 101#2=;
  FEstado = FSalida;
  |SI #3#08 = "D";
      #2#04 = #2#04 + #3#09;
      |SI #2#09 = 0; #2#08 = #3#4; #2#09 = #3#5; |FINSI;  || contrp.HABER
  |FINSI;
  |SI #3#08 = "H";
      #2#05 = #2#05 + #3#09;
      |SI #2#11 = 0; #2#10 = #3#4; #2#11 = #3#5; |FINSI;  || contrp.DEBE
  |FINSI;
  |SI FEstado = 0;
      |GRABA 020#2a;
  |SINO;
      |GRABA 020#2n;
  |FINSI;
  |LIBERA #2;
|FPRC;

|PROCESO actualiz1;
  |SI abre = 1;  || en 'expl' ya viene abierto y leido del 'defbucle'
      |ABRE #1;
      #1#0 = #3#4;
      #1#1 = #3#5;
      |LEE 101#1=;
  |FINSI;
  |SI #3#0 = 0;
      |SI #3#8 = "D";
          #1#9 = #1#9 + #3#9;
          #1#51 = #1#51 + #3#9;
      |SINO;
          #1#10 = #1#10 + #3#9;
          #1#52 = #1#52 + #3#9;
      |FINSI;
      #1#11 = #1#9 - #1#10;
      #1#53 = #1#51 - #1#52;
  |SINO;
      |SI #3#0 = 99;
          |SI #3#8 = "D";
              #1#48 = #1#48 + #3#9;
              #1#51 = #1#51 + #3#9;
          |SINO;
              #1#49 = #1#49 + #3#9;
              #1#52 = #1#52 + #3#9;
          |FINSI;
          #1#50 = #1#48 - #1#49;
          #1#53 = #1#51 - #1#52;
      |SINO;
          |HAZ actualiz2;
      |FINSI;
  |FINSI;
  |SI #1#58 < #0#0;
      #1#58 = #0#0;
  |FINSI;
  |GRABA 020#1a;
  |SI desbloquea = 0;  ||  en las ctas. de variaciones hace dos grabaciones
      |LIBERA #1;      || /por lo tanto, en la primera no desbloquea
  |FINSI;
  |SI abre = 1;
      |CIERRA #1;
  |FINSI;
  |HAZ actualiz3;  || diario
|FPRC;

|PROCESO actualiz2;
    ini = dig1;      || numero de niveles
    fecalf = #0#0;
    mes = fecalf % 402;
    mesfec = mes;
|SI ini = 1;
        mesac = mesfec;
|SINO;
        mesac = (mesfec - ini) + 1;
    |SI mesac [ 0;
            mesac = (13 - ini) + mesfec;
    |FINSI;
|FINSI;
    a = (mesac * 3) + 9;
    b = a + 1;
    c = a + 2;
|SI #3#8 = "D";
        #1a = #1a + #3#9;
        #1#51 = #1#51 + #3#9;
|SINO;
        #1b = #1b + #3#9;
        #1#52 = #1#52 + #3#9;
|FINSI;
    #1c = #1a - #1b;
    #1#53 = #1#51 - #1#52;
|FPRC;

|PROCESO actualiz3;
|ABRE #10;
    #10#0 = #3#0;
|LEE 121#10=;
|SI FSalida = 0;
    |SI #3#8 = "D";  #10#2 = #10#2 + #3#9;  |FINSI;
    |SI #3#8 = "H";  #10#3 = #10#3 + #3#9;  |FINSI;
    |GRABA 020#10a;
|FINSI;
|LIBERA #10;
|CIERRA #10;
|FPRC;
