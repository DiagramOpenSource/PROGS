|FICHEROS;
     caemi130;
     caportad;
     caman130;

     t1513001;
|FIN;

|VARIABLES;
     nPeriodo     = 0;
     nCampoD      = 0;
     aLLeno40     = "zzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzz";
     aVacio40     = "                                        ";
     aAlfa        = "";
     aAlfa2       = "";
     aLong        = "";
     aModelo      = "";
     aFichero     = "";
     aDirOri      = "";
     aDirDes      = "";
     aOrigen      = "";
     aDestino     = "";
     aCaracter    = "";

     nLong        = 0;
     nComa        = 0;
     nCaracter    = 0;
     nPos         = 0;
     nConver      = 0;
     nCampo       = 0;
     nHandlePortal= 0;
     nEl27        = 0;
     nAnyo2D      = 0;
     nDEmpre      = 0;
     nHEmpre      = 0;

     &eaIBAN      = "";
     &enEmpresa   = 0;
     &enEjercic   = 0;
     &dia         = "";
     &mes         = "";
     &anyo        = "";

     &EURODB    = 0;

     {-1}aMoneda   = "";
         aMoneda1  = "LEER";
         aMoneda2  = "";

     Comodin = "";

     aAbre        = "";
     aModelos     = "";
     aEnlace      = "";
     aHID         = "";
     aNDC         = "";
     aIDI         = "";
     aEJF         = "";
     aMOD         = "";
     aPRG         = "";
     aTIA         = "";
     aINI         = "";
     aFIN         = "";
     aAbrePortal  = "";
     aTipo        = "";

     nHandle1     = 0;
     nHandle2     = 0;
     nTCampo      = 0;

     aAlfa1       = "";
     aUnidad      = "";

  {1}aContiene    = "";

     &eaFOrigen   = "";
     &eaFDestino  = "";
     &eaInternet  = "";
     &eaImpresora = "";
     &enNume      = 0;
     &eaAlfa      = "";
     &eaDiaB      = "";
     &eaMesB      = "";
     &eaAnyoB     = "";
     &eaModelos   = "";
     &eaVersMod   = "    ";
     &eaNIFDs     = "         ";

     &eaEjer             = 0;
     &eaPathEmision      = "";
     &eaPathLanza        = "";
     &eaPathExplorador   = "";
     &eaPathInternet     = "";
     &eaPathAEAT         = "";
     &eaFicheroPlano     = "";
     &eaFicheroSalid     = "";
     &eaFicheroImpre     = "";
     &eaFicheroError     = "";
     &eaOpcion           = "";
     &eaNomEmpresa       = "";
     &enPresentar        = 0;
     &enErrorPortal      = 0;
     &enTecla            = 0;
     &enPer              = 0;
     &enPeriodo          = 0;
     &enHandlePortal     = 0;
     &eaPeriodo          = "";
     &eaUnidad           = "";
     &eaForzExplorador   = "";
     &eaNIF              = "";
     &enImprtMd          = 0;
|FIN;

|INCLUYE seguim_i;

|PROCESO PresentaModelo_02;
     |SI eaOpcion = "1";
         eaNIF     = #t1513001#7;
         enImprtMd = #caman130#46;
         |HAZ PortalPDFPeriodicos;
     |FINSI;

     |SI eaOpcion = "3";
         eaFOrigen  = eaPathEmision  + eaFicheroPlano;
         eaFDestino = eaPathInternet + eaFicheroPlano;
         |HAZ CopiaFicheroPtl;
     |FINSI;
|FPRC;

|PROCESO AbrePagina;
     eaPathAEAT = eaUnidad + "\AEAT";        |RMKDIR eaPathAEAT;
     eaPathAEAT = eaPathAEAT + "\" + eaModelos;  |RMKDIR eaPathAEAT;
     eaPathAEAT = eaPathAEAT + "\" + eaEjer;  |RMKDIR eaPathAEAT;
     eaPathAEAT = eaPathAEAT + "\" + eaPeriodo;  |RMKDIR eaPathAEAT;
     eaPathAEAT = eaPathAEAT + "\";

     eaFOrigen  = eaPathEmision  + eaFicheroPlano;
     eaFDestino = eaPathAEAT + eaFicheroPlano;
     |HAZ CopiaFicheroPtl;

     |DBASE aAlfa;  |QBT aAlfa;
     eaFOrigen  = aAlfa + "hacienda/wait.exe";
     eaFDestino = eaPathAEAT + "wait.exe";
     |HAZ CopiaFicheroPtl;

     eaForzExplorador = "N";
     |HAZ Explorador;

     aAbre = eaPathAEAT + "imp" + Usuario + ".bat";
     |XABRE "WBC", aAbre, enHandlePortal;

     eaAlfa = eaPathAEAT + "wait ";
     |HAZ GrabaPortalSin;

     eaAlfa = eaPathExplorador + " " + &34;
     |HAZ GrabaPortalSin;

     eaAlfa = "https://www1.agenciatributaria.gob.es/es13/h/ie51150a.html";
     |HAZ GrabaPortalSin;

     |XCIERRA enHandlePortal;

     |RSYSTEM aAbre;

     aAlfa = eaPathExplorador > "";
     aAlfa = aAlfa - "CHROME.EXE";
     |SI FSalida ! 0;  |SLEEP 30;  |FINSI;

     aAlfa = eaPathAEAT + aFichero;
     |RFBORRA aAlfa;
     |RFBORRA aAbre;
|FPRC;

|PROCESO TDeclaracion2015;
     |SI #caman130#46 > 0;
         enNume  = #caman130#46;  |HAZ Conver17;  #t1513001#30 = eaAlfa;

         aAlfa = #caportad#117;  |QBF aAlfa;

         #t1513001#6 = "I";
         |SI  aAlfa ! "";  |Y #caemi130#6 = "D";
              #t1513001#6 = "U";
         |FINSI;

         |SI #caemi130#6 ! "N";
             eaIBAN = #caportad#117;
         |FINSI;

     |SINO;
         enNume  = 0;  |HAZ Conver17;  #t1513001#30 = eaAlfa;
         #t1513001#6 = "N";
         |SI #caman130#46 ! 0; #t1513001#6 = "B"; |FINSI;
     |FINSI;
|FPRC;

|PROCESO BuscaNombre_02;
     |QBF eaAlfa;
     aAlfa1 = "";
     aAlfa2 = "";
     aLong  = eaAlfa % 0;
     nLong  = aLong;
     nComa  = 0;

     |PARA nCaracter = 1;  |SI nCaracter [ nLong;  |HACIENDO nCaracter = nCaracter + 1;
           nPos      = (nCaracter * 100) + 1;
           aCaracter = eaAlfa % nPos;

           |SI aCaracter = ",";
               nComa = 1;
           |SINO;
               |SI nComa = 0;
                   aAlfa1 = aAlfa1 + aCaracter;
               |SINO;
                   |SI aCaracter = " ";  |Y aAlfa2 = "";
                   |SINO;
                       aAlfa2 = aAlfa2 + aCaracter;
                   |FINSI;
               |FINSI;
           |FINSI;
      |SIGUE;
|FPRC;

|PROCESO Carga130_02;
     |DDEFECTO #t1513001;

     aAlfa       = #caportad#13;  |QBF aAlfa;
     #t1513001#7 = ("000000000" + aAlfa) % -109;

     aAlfa = #caportad#13 % 101;
     |SI aAlfa = "0"; |O aAlfa = "1"; |O aAlfa = "2"; |O aAlfa = "3";
      |O aAlfa = "4"; |O aAlfa = "5"; |O aAlfa = "6"; |O aAlfa = "7";
      |O aAlfa = "8"; |O aAlfa = "9"; |O aAlfa = "X"; |O aAlfa = "Y";
      |O aAlfa = "Z";
         eaAlfa = #caportad#2;      |HAZ BuscaNombre_02;
         eaAlfa = aAlfa1;    |HAZ QuitaRaros;   #t1513001#8   = eaAlfa;
         eaAlfa = aAlfa2;    |HAZ QuitaRaros;   #t1513001#9   = eaAlfa;
     |SINO;
         eaAlfa   = #caportad#2;  |HAZ QuitaRaros;  #t1513001#8 = eaAlfa;
                                                    #t1513001#9 = "";
     |FINSI;

     #t1513001#10  = #caemi130#0;
     #t1513001#11  = eaPeriodo;

     enNume  = #caman130#8;           |HAZ Conver17;  #t1513001#12 = eaAlfa;
     enNume  = #caman130#8 - #caman130#22;   |HAZ Conver17;  #t1513001#13 = eaAlfa;
     enNume  = #caman130#22;          |HAZ Conver17;  #t1513001#14 = eaAlfa;

     nEl27   = 0;
     |SI #caman130#27 > 0;
         enNume  = #caman130#27;      |HAZ Conver17;  #t1513001#15 = eaAlfa;
         nEl27   = #caman130#27;
     |FINSI;

     enNume  = #caman130#28;                    |HAZ Conver17;  #t1513001#16 = eaAlfa;
     enNume  = #caman130#25;                    |HAZ Conver17;  #t1513001#17 = eaAlfa;
     enNume  = nEl27 - #caman130#28 - #caman130#25;    |HAZ Conver17;  #t1513001#18 = eaAlfa;

     enNume  = #caman130#30;                    |HAZ Conver17;  #t1513001#19 = eaAlfa;
     enNume  = #caman130#31;                    |HAZ Conver17;  #t1513001#20 = eaAlfa;
     enNume  = #caman130#36;                    |HAZ Conver17;  #t1513001#21 = eaAlfa;

     enNume  = #caman130#31 - #caman130#36;            |HAZ Conver17;  #t1513001#22 = eaAlfa;

     enNume  = #caman130#29;                    |HAZ Conver17;  #t1513001#23 = eaAlfa;

     enNume  = #caman130#41;                    |HAZ Conver17;  #t1513001#24 = eaAlfa;
     enNume  = #caman130#42;                    |HAZ Conver17;  #t1513001#25 = eaAlfa;
     enNume  = #caman130#43;                    |HAZ Conver17;  #t1513001#26 = eaAlfa;
     enNume  = #caman130#47;                    |HAZ Conver17;  #t1513001#27 = eaAlfa;
     enNume  = #caman130#44;                    |HAZ Conver17;  #t1513001#28 = eaAlfa;
     enNume  = #caman130#45;                    |HAZ Conver17;  #t1513001#29 = eaAlfa;

     |HAZ TDeclaracion2015;

     |SI #caman130#38 = "S";
         #t1513001#31 = "X";
         #t1513001#32 = #caman130#39;
     |FINSI;

     #t1513001#33 = eaIBAN;

     #t1513001#37 = &13 + &10;

     aAlfa = "<T1300" + #t1513001#10 + #t1513001#11 + "0000>";  |XGRABA nHandle2, aAlfa;
     aAlfa = "<AUX>";                                           |XGRABA nHandle2, aAlfa;
     aAlfa = " " * 70;                                          |XGRABA nHandle2, aAlfa;
     aAlfa = eaVersMod;                                         |XGRABA nHandle2, aAlfa;
     aAlfa = " " * 4;                                           |XGRABA nHandle2, aAlfa;
     aAlfa = eaNIFDs;                                           |XGRABA nHandle2, aAlfa;
     aAlfa = " " * 213;                                         |XGRABA nHandle2, aAlfa;
     aAlfa = "</AUX>";                                          |XGRABA nHandle2, aAlfa;

     |PARA nCampo = 1;  |SI nCampo [ 36;  |HACIENDO nCampo = nCampo + 1;
           aAlfa = #t1513001#nCampo#;
           |XGRABA nHandle2, aAlfa;
     |SIGUE;

     aAlfa = "</T1300" + #t1513001#10 + #t1513001#11 + "0000>";  |XGRABA nHandle2, aAlfa;
|FPRC;

|PROCESO caman130_02;
     |SI #caemi130#5 = "N";
         |SI #caman130#5 = "S";  |ACABA;  |FINSI;
     |FINSI;

     eaFicheroPlano = "130" + (("00000" + #caman130#0) % -105) + ".txt";
     eaFicheroImpre = "130" + (("00000" + #caman130#0) % -105) + ".pdf";
     eaFicheroError = "err" + (("00000" + #caman130#0) % -105) + ".txt";

     aAlfa = eaPathLanza + eaFicheroPlano;  |RFBORRA aAlfa;
     aAlfa = eaPathLanza + eaFicheroImpre;  |RFBORRA aAlfa;
     aAlfa = eaPathLanza + eaFicheroError;  |RFBORRA aAlfa;

     aAbre = eaPathEmision + eaFicheroPlano;
     |XABRE "BW", aAbre, nHandle2;

     |HAZ Carga130_02;

     |XCIERRA nHandle2;

     |HAZ PresentaModelo_02;

     #caman130#4 = #caemi130#3;
     #caman130#5 = "S";
     |GRABA 020#caman130.a;
     |LIBERA #caman130;

     se_empre  = #caman130#0;
     se_ejerc  = #caman130#3;
     se_modelo = "130";
     se_impor  = #caman130#29;
     se_fecha  = #caman130#4;
     |SI SwAgicli = 1;
         |HAZ AlAgicl2;
     |FINSI;
|FPRC;

|DEFBUCLE caman130_02;
     #caman130#1;
     ;
     00001, 0, #caemi130#1 , nAnyo2D;
     99999, 9, #caemi130#1 , nAnyo2D;
     be;
     NULL , caman130_02;
|FIN;

|PROCESO moy13002;
     |HAZ LeeUnidad;

     eaModelos  = "130";
     eaEjer     = #caemi130#0
     enPeriodo  = #caemi130#1;
     nAnyo2D    = #caemi130#0 - 2000;

     |SI #caemi130#1 = 1; eaPeriodo = "1T"; |FINSI;
     |SI #caemi130#1 = 2; eaPeriodo = "2T"; |FINSI;
     |SI #caemi130#1 = 3; eaPeriodo = "3T"; |FINSI;
     |SI #caemi130#1 = 4; eaPeriodo = "4T"; |FINSI;

     eaPathLanza = eaUnidad + "\MODELOS";            |RMKDIR eaPathLanza;
     eaPathLanza = eaPathLanza + "\" + eaModelos;    |RMKDIR eaPathLanza;
     eaPathLanza = eaPathLanza + "\" + eaEjer;       |RMKDIR eaPathLanza;
     eaPathLanza = eaPathLanza + "\" + eaPeriodo;    |RMKDIR eaPathLanza;
     eaPathLanza = eaPathLanza + "\";

     |DBASE eaPathEmision;
     eaPathEmision = eaPathEmision + "emisiones";       |MKDIR eaPathEmision;
     eaPathEmision = eaPathEmision + "/" + eaModelos;   |MKDIR eaPathEmision;
     eaPathEmision = eaPathEmision + "/" + eaEjer;      |MKDIR eaPathEmision;
     eaPathEmision = eaPathEmision + "/" + eaPeriodo;   |MKDIR eaPathEmision;
     eaPathEmision = eaPathEmision + "/";

     eaPathInternet = eaUnidad + "/AEAT";                |RMKDIR eaPathInternet;
     eaPathInternet = eaPathInternet + "/INTERNET";      |RMKDIR eaPathInternet;
     eaPathInternet = eaPathInternet + "/" + eaModelos;  |RMKDIR eaPathInternet;
     eaPathInternet = eaPathInternet + "/" + eaEjer;     |RMKDIR eaPathInternet;
     eaPathInternet = eaPathInternet + "/" + eaPeriodo;  |RMKDIR eaPathInternet;
     eaPathInternet = eaPathInternet + "/";

     |HAZ Botones2015;
     |SI enTecla = 0;  |ACABA;  |FINSI;

     |SI eaOpcion = "1";
         |MENAV "0000La opcion de validacion no esta activada por hacienda.";
         |ACABA;
     |FINSI;

     |ABRE #caportad;
     #caportad#0 = #caemi130#7;
     #caportad#1 = nAnyo2D;
     |LEE 000#caportad.=;
     |SI FSalida = 0;
         |HAZ AlAgicl1; || leer Modulo Integrador. ....
         |BUCLE caman130_02;
     |SINO;
         |MENAV "     ATENCION! NO existen datos en PORTADAS MOD. OFICIALES";
     |FINSI;
     |CIERRA #caportad;

     |SI eaOpcion = "1";
         |HAZ SacaErrorPtl;
     |FINSI;

     |SI eaOpcion = "3";
         aAlfa =  "      Los ficheros generados estan: " + eaPathInternet + " de su PC";
         |MENAV aAlfa;

/*
         |CONFI "0000SQuiere cargar la pagina de hacienda del modelo";
         |SI FSalida = 0;
             |HAZ AbrePagina;
         |FINSI;
*/
     |FINSI;
|FPRC;
