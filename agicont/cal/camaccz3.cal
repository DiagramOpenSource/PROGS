|FICHEROS;
   camaccz3 #0;

   caconimp #8;
   caivasop #2;
   caivarep #12;

   caivaasi #200;

   camacc01 #501;
   camacc02 #502;
   camacc1@ #901;
   camacc2@ #902;

   camaccw1 #991;
   camaccw4 #999;
   canempre #30;
|FIN;

|VARIABLES;
    &entrada   = 1;
    aParam     = "";
    aParam1    = "";

    nEmpresa   = 0;
    nPeriodoO  = 0;
    nPeriodoD  = 0;
    &enPeriodoO = 0;
    &enPeriodoD = 0;
    aPathOrigen  = "";
    aPathDestino = "";

    aAlfa1   = "";
    aAlfa2   = "";
    nNume1   = 0;
    nNume2   = 0;
    nPara1   = 0;
    aFichero = "";
    nCampo   = 0;
    aMas     = "";
    aDef     = "";
    fDFecha  = @;
    aDTipo   = "";
    aHTipo   = "";
    nSwAlgun = 0;
    aMulDep  = "";
    nDdepar  = 0;
    nHdepar  = 0;
    nSwSel   = 0;
    nSw      = 0;

    &aCif      = "";
    &enSwLinea = 0;
    &enTotal   = 0;
    &enTBases  = 0;
    &enTCuotas = 0;
    &eaTexto   = "";
    nOp        = 0;
    nError     = 0;
    dfec       = @;
    hfec       = @;
    &eaError   = "";
    nImpre     = 0;
    informe    = "";
    nSwPrint   = 0;
    nLibro     = 0;

    nEjer      = 0;
    nPeriodo   = 0;
    nEstado    = 0;
|FIN;

|INCLUYE acceso_i;
|INCLUYE puntos_i;
|INCLUYE varcaj_i;
|INCLUYE varnue_i;

|| ***************** Calculos de Pantalla
|CALCULO Primero;
  |HAZ inicio;
  |HAZ eVarEmpresa;

  |ABRE #200;
  |LEE 000#200p;
  |SI FSalida ! 0;
      |DDEFECTO #200;
      |GRABA 040#200n;
  |FINSI;
  |CIERRA #200;
  aAlfa1  = #200#44;
  |C_M #0#7,1,aAlfa1;
  |C_M #0#8,1,aAlfa1;
  #0#4 = fec1;
  #0#5 = fec2;
|FCAL;

|CALCULO QueIva; |TIPO 0;
   |SI #0#13 = "R"; eaTexto = "Cobrados"; |FINSI;
   |SI #0#13 = "S"; eaTexto = "Pagados "; |FINSI;
   |SI #0#13 = "T"; eaTexto = "Operacion"; |FINSI;
   aTipoIVA = #0#13;
|FCAL;

|CALCULO defectos; |TIPO 7;
    #0#4 = dfec; |PINTA #0#4;
    #0#5 = hfec; |PINTA #0#5;
|FCAL;

|CALCULO fecha; |TIPO 0;
   |SI Campo = 5; |Y #0#5 < #0#4;
       |MENAV "    FECHA ERRONEA ";
       |ERROR;
   |FINSI;
|FCAL;

|CALCULO nodepar; |TIPO 7;
   |C_M #0#9, 1, eaDepar;
   |C_M #0#10, 1, eaDepar;
|FCAL;

|PROCESO cuenta; |TIPO 0;
    p_alfa1 = #0Campo;
    salidas = FSalida;
    |HAZ punto;
    |SI p_error ! 0;  |ERROR;  |FINSI;
|FPRC;

|| *************************************************************************
|PROCESO Pasa502;
    |SI #0#6 = "I";
        #502#31 = #0#16;
        #502#33 = #0#16;
    |FINSI;
    |SI #0#6 = "D";
        #502#31 = -1;
        #502#33 = -1;
    |FINSI;

    |GRABA 020#502a;
    |LIBERA #502;

    |SI #0#6 = "I";
        |PARA nPara1 = 0; |SI nPara1 [ 30; |HACIENDO nPara1 = nPara1 + 1;
              #902nPara1 = #502nPara1;
        |SIGUE;
        #902#31 = #0#16;
        #902#32 = #0#17;
        #902#33 = #0#16;
        |GRABA 020#902n;
    |FINSI;
|FPRC;

|DEFBUCLE Pasa502;
    #502#1;
    ;
    #501#13, #501#17, #501#0, #501#14, #501#15, 01;
    #501#13, #501#17, #501#0, #501#14, #501#15, 99;
    be;
    NULL, Pasa502;
|FIN;

|| *************************************************************************
|PROCESO Borra902;
    |BORRA 020#902a;
    |LIBERA #902;
|FPRC;

|DEFBUCLE Borra902;
    #902#1006;
    ;
    #901#13, #901#17, #901#0, #901#14, #901#15, 01;
    #901#13, #901#17, #901#0, #901#14, #901#15, 99;
    be;
    NULL, Borra902;
|FIN;

|PROCESO Borra901;
    |BUCLE Borra902;
    |BORRA 020#901a;
    |LIBERA #901;
|FPRC;

|| *************************************************************************
|PROCESO OpImportar;

   |SI #501#5 = "S"; |ACABA; |FINSI; || esta toda pagada/cobrada
   |SI #501#6 = #999#8; |Y #501#7 = #999#9;
       |ACABA;
   |FINSI;

   |SI #501#18 = -1; ||aun no estaba importada

       |DDEFECTO #901;
       #901#13 = #501#13;
       #901#17 = #0#17;
       #901#0  = #501#0;
       #901#14 = #501#14;
       #901#15 = #501#15;
       |LEE 141#901=;
       |SI FSalida = 0;
           nSwSel = 4;
           eaError = "Ya existe Registro en Ejer. Destino";
           |ACABA;
           || |HAZ  Borra901;
       |FINSI;

       |PARA nPara1 = 0; |SI nPara1 [ 16; |HACIENDO nPara1 = nPara1 + 1;
             #901nPara1 = #501nPara1;
       |SIGUE;

       #901#17 = #0#17;
       #901#18 = #0#16;

       |ABRE #902;
       |BUCLE Pasa502;
       |CIERRA #902;

       |GRABA 040#901n;
       |LIBERA #901;

       #501#18 = #0#16;
       |GRABA 020#501a;

       |SI nLibro ! 0; |Y nLibro ! #501#0;
           |SI nSwPrint = 1; |Y #0#20 = "S";
               |PIEINF;
           |FINSI;
           nSwPrint = 0;
       |FINSI;

       |SI #501#13 = "R";
           eaTexto = "Cobrados";
       |SINO;
           eaTexto = "Pagados";
       |FINSI;

       nLibro = #501#0;
       |SI #0#20 = "S";
           |PRINT;
           nSwPrint = 1;
       |FINSI;
       nSwAlgun = 1;
   |FINSI;

   |LIBERA #501;
|FPRC;

|PROCESO Comprobar902;
    #502#0  = #902#0;
    #502#32 = 0;
    #502#1  = #902#1;
    #502#2  = #902#2;
    #502#3  = #902#3;
    #502#4  = #902#4;
    |LEE 041#502=;
    |SI FSalida ! 0;
        nSw = 1;
        |ERROR10;
    |FINSI;
|FPRC;

|DEFBUCLE Comprobar902;
    #902#1006;
    ;
    #901#13, #901#17, #901#0, #901#14, #901#15, 01;
    #901#13, #901#17, #901#0, #901#14, #901#15, 99;
    e;
    NULL, Comprobar902;
|FIN;

|PROCESO OpDesImpor;

   |SI #501#18 ! -1; || Ya estaba importada
        nSw = 0;
       |DDEFECTO #901;
       #901#13 = #501#13;
       #901#17 = #0#17;
       #901#0  = #501#0;
       #901#14 = #501#14;
       #901#15 = #501#15;
       |LEE 141#901=;
       |SI FSalida = 0;
           |ABRE #502;
           |BUCLE Comprobar902;
           |CIERRA #502;
           |SI nSw = 1;
               eaError = "Existen movimientos en el Ejer.posterior";
               nSwSel = 3;
           |SINO;
               |HAZ  Borra901;
           |FINSI;
       |FINSI;

       |SI nSw = 0;
           |BUCLE Pasa502;

           #501#18 = -1;
           |GRABA 020#501a;

           |SI nLibro ! 0; |Y nLibro ! #501#0;
               |SI nSwPrint = 1; |Y #0#20 = "S";
                   |PIEINF;
               |FINSI;
               nSwPrint = 0;
           |FINSI;

           |SI #501#13 = "R";
               eaTexto = "Cobrados";
           |SINO;
               eaTexto = "Pagados";
           |FINSI;

           nLibro = #501#0;
           |SI #0#20 = "S";
               |PRINT;
               nSwPrint = 1;
           |FINSI;

           nSwAlgun = 1;
       |FINSI;
   |FINSI;
   |LIBERA #501;
|FPRC;
|| *************************************************************************

|PROCESO IgualoCampos;
     #999#13 = aTipoIVA;
     #999#0  = enLibroCaja;
     #999#3  = eaSerieCaja;
     #999#2  = enRegisCaja;
     |SI aTipoIVA = "S";
         #999#4  = #caivasop#3;
         #999#5  = #caivasop#4;
         #999#6  = #caivasop#6;
         #999#7  = #caivasop#7;

         #999#8  = #caivasop#8  + #caivasop#9  + #caivasop#10 + #caivasop#52 + #caivasop#53;
         #999#9  = #caivasop#14 + #caivasop#15 + #caivasop#16 + #caivasop#56 + #caivasop#57;
         #999#10 = #caivasop#26;
         #999#11 = #caivasop#50;
         #999#12 = #caivasop#51;
         #999#14 = #caivasop#6;

         #999#21 = #caivasop#45;
         #999#22 = #caivasop#46;
         #999#23 = #caivasop#47;

         #999#26 = #caivasop#8;
         #999#27 = #caivasop#14;

         #999#28 = #caivasop#9;
         #999#29 = #caivasop#15;

         #999#30 = #caivasop#10;
         #999#31 = #caivasop#16;

         #999#32 = #caivasop#52;
         #999#33 = #caivasop#56;

         #999#34 = #caivasop#53;
         #999#35 = #caivasop#57;
     |FINSI;

     |SI aTipoIVA = "R";
         #999#4  = #caivarep#3;
         #999#5  = #caivarep#4;
         #999#6  = #caivarep#6;
         #999#7  = #caivarep#7;

         #999#8  = #caivarep#8  + #caivarep#9  + #caivarep#10 + #caivarep#52 + #caivarep#53;
         #999#9  = #caivarep#14 + #caivarep#15 + #caivarep#16 + #caivarep#56 + #caivarep#57;
         #999#10 = #caivarep#26;
         #999#11 = #caivarep#50;
         #999#12 = #caivarep#51;
         #999#14 = #caivarep#6;

         #999#21 = #caivarep#45;
         #999#22 = #caivarep#46;
         #999#23 = #caivarep#47;

         #999#26 = #caivarep#8;
         #999#27 = #caivarep#14;

         #999#28 = #caivarep#9;
         #999#29 = #caivarep#15;

         #999#30 = #caivarep#10;
         #999#31 = #caivarep#16;

         #999#32 = #caivarep#52;
         #999#33 = #caivarep#56;

         #999#34 = #caivarep#53;
         #999#35 = #caivarep#57;
     |FINSI;
|FPRC;

|PROCESO GrabaAuxiliar;

     #991#13 = #501#13;
     #991#36 = #501#17;
     #991#0  = #501#0;
     #991#3  = #501#14;
     #991#2  = #501#15;
     |LEE 041#991=;
     |SI FSalida ! 0;
         |DDEFECTO #991;
         #991#13 = #501#13;
         #991#36 = #501#17;
         #991#0  = #501#0;
         #991#3  = #501#14;
         #991#2  = #501#15;
         |GRABA 020#991c;
     |FINSI;

     |SI nSwSel ! 2;
         |PARA nPara1 = 2; |SI nPara1 [ 37; |HACIENDO nPara1 = nPara1 + 1;
               #991nPara1 = #999nPara1;
         |SIGUE;
     |FINSI;
     #991#11 = eaError;
     |GRABA 020#991a;
     |LIBERA #991;
      nError = 1;
|FPRC;

|| ////////////////////////////////////////

|PROCESO Facturas;
  |SI aTipoIVA = "S";
      |SI #2#4  < #0#11; |O #2#4  > #0#12; |ACABA; |FINSI;
      |SI #2#27 < #0#7;  |O #2#27 > #0#8;  |ACABA; |FINSI;
      |SI #2#2  < #0#2;  |O #2#3  > #0#3;  |ACABA; |FINSI;
      |SI #2#3  < #0#4;  |O #2#3  > #0#5;  |ACABA; |FINSI;
      |SI #2#49 < #0#9;  |O #2#49 > #0#10; |ACABA; |FINSI;
  |SINO;
      |SI #12#4  < #0#11; |O #12#4  > #0#12; |ACABA; |FINSI;
      |SI #12#27 < #0#7;  |O #12#27 > #0#8;  |ACABA; |FINSI;
      |SI #12#2  < #0#2;  |O #12#3  > #0#3;  |ACABA; |FINSI;
      |SI #12#3  < #0#4;  |O #12#3  > #0#5;  |ACABA; |FINSI;
      |SI #12#49 < #0#9;  |O #12#49 > #0#10; |ACABA; |FINSI;
  |FINSI;

  enSwCaja = 0;
  |SI aTipoIVA = "R";
      enConcepto = #12#6;
  |SINO;
      enConcepto = #2#6;
  |FINSI;
  |HAZ EsCajaIva;
  |SI enSwCaja = 0; |ACABA; |FINSI;

  nSwSel = 1;
  |SI aTipoIVA= "S";
      enLibroCaja = #2#0;
      eaSerieCaja = #2#27;
      enRegisCaja = #2#2;
  |SINO;
      enLibroCaja = #12#0;
      eaSerieCaja = #12#27;
      enRegisCaja = #12#2;
  |FINSI;
  |HAZ IgualoCampos;

    #999#16 = #501#12;
    #999#17 = #501#8;
    #999#18 = #501#16;
    #999#19 = #501#6;
    #999#20 = #501#7;

    #999#36 = #501#17;
    #999#37 = #501#18;
    ||  |GRABA 020#999n;  no graba
|FPRC;

|PROCESO RegCajaOrigen;

   nSwSel = 0;
   aTipoIVA   = #501#13;
   eDevenCaja = #501#17;

   |SI aTipoIVA = "S";
       #2#0  = #501#0;
       #2#27 = #501#14;
       #2#2  = #501#15;
       |LEE 041#2=;
       nEstado = FSalida;
   |SINO;
       #12#0  = #501#0;
       #12#27 = #501#14;
       #12#2  = #501#15;
       |LEE 041#12=;
       nEstado = FSalida;
   |FINSI;

   |SI nEstado = 0;
       |HAZ Facturas;
   |SINO;
       nSwSel = 2;
       eaError = "No existe Factura";
       |DDEFECTO #999;
   |FINSI;
   |SI nSwSel = 1;
       |SI #0#6 = "I"; |HAZ OpImportar; |FINSI;
       |SI #0#6 = "D"; |HAZ OpDesImpor; |FINSI;
   |FINSI;
   |SI nSwSel ] 2;
       |HAZ GrabaAuxiliar;
   |FINSI;
|FPRC;

|DEFBUCLE RegCajaOrigen;
   #501#1;
   ;
   aDTipo, 0000, #0#0, "  ", 000001;
   aHTipo, 0000, #0#1, "zz", 999999;
   e;
   NULL, RegCajaOrigen;
|FIN;

|| ******************************************************
|PROCESO MiraErrores;
   |PRINT;
   nSwPrint = 1;
|FPRC;

|DEFBUCLE MiraErrores;
   #999#1;
   ;
   INICIO;
   FINAL;
   ;
   MiraErrores;
|FIN;
|| ******************************************************
|CALCULO impre; |TIPO 3;

   aFichero = "x" + Usuario; |NOME_DAT #999 aFichero; |DELINDEX #999;
   aFichero = "y" + Usuario; |NOME_DAT #991 aFichero; |DELINDEX #991;

    enEmpresa  = #0#14;
    ePerioCaja = #0#16; |HAZ ePathCaja; aPathOrigen = ePathCaja;
    ePerioCaja = #0#18; |HAZ ePathCaja; aPathDestino= ePathCaja;

    |DBASE aMas;
    aDef = aMas + "def/camacc01";
    |CARGA_DEF aDef, camacc1@;
    |SI FSalida ! 0;
        |MENAV "    Error cargando def camacc01";
        |ACABA;
    |FINSI;

    aDef = aMas + "def/camacc02";
    |CARGA_DEF aDef, camacc2@;
    |SI FSalida ! 0;
        |DESCARGA_DEF #camacc1@;
        |MENAV "    Error cargando def camacc02";
        |ACABA;
    |FINSI;

    |PATH_DAT #901 aPathDestino;
    |PATH_DAT #902 aPathDestino;

    |PATH_DAT #501 aPathOrigen;
    |PATH_DAT #502 aPathOrigen;

    |PATH_DAT #2  aPathOrigen;
    |PATH_DAT #12 aPathOrigen;

    nSwAlgun = 0;
    aDTipo = #0#13;
    aHTipo = #0#13;
    |SI #0#13 = "T";
        aDTipo = "R";
        aHTipo = "S";
    |FINSI;

    |ABRE #8;
    |DDEFECTO #8;
    |LEE 001#8=;
    |CIERRA #8;

    |SI #0#20 = "S";

        |IMPRE 0;
        |SI FSalida ! 0;
            |MENAV "    Error en Impresora. Proceso abortado";
            |ACABA;
        |FINSI;
        nImpre = 1;

        |INFOR "camaccz3";
        |SI FSalida ! 0;
            |MENAV "    Error en Informe. Proceso abortado";
            |FINIMP;
            |ACABA;
        |FINSI;
    |FINSI;

    |SI #0#6 = "D";
        #0#21 = "INFORME DE FACTURAS EN RECC ANULADAS EL TRASPASO ";
    |FINSI;

    |ABRE #991;
    |ABRE #901;

    |ABRE #2;
    |ABRE #12;
    nLibro   = 0;
    nError   = 0;
    nSwPrint = 0;
    |BUCLE RegCajaOrigen;
    |SI nImpre = 1;
        |SI nSwPrint = 1;  |PIEINF;  |FINSI;
        |FININF;
        nSwPrint = 0;
    |FINSI;

    |SI nSwAlgun = 0; |Y aParam1 = "";
        aAlfa1 = "0000No hay Facturas RECC para traspasar";
        |SI #0#6 = "D";
            aAlfa1 = "0000No hay Facturas traspasadas al ejercicio posterior";
        |FINSI;
        |MENAV aAlfa1;
    |FINSI;

    |CIERRA #12;
    |CIERRA #2;

    |CIERRA #901;
    |CIERRA #991;

    |SI nError ! 0;
        |SI nImpre = 0;
            |IMPRE 0;
            |SI FSalida ! 0;
                |MENAV "    Error en impresora. Proceso abortado";
                |VETE Salir;
            |FINSI;
            nImpre = 1;
        |FINSI;

        |INFOR "camaccy3";
        |SI FSalida ! 0;
            |MENAV "    Error en informe. Proceso abortado";
            |VETE Salir;
        |FINSI;

        nSwPrint = 0;
        |BUCLE MiraErrores;
        |PIEINF;
        |FININF;
    |FINSI;

|ET Salir;
    |SI nImpre = 1;
        |FINIMP;
        nImpre = 0;
    |FINSI;

    |DESCARGA_DEF #902;
    |DESCARGA_DEF #901;
|FCAL;

|| *********************************************************************

|PROCESO BusEmpresa;
   nPeriodo = #30#3;
   |ERROR10;
|FPRC;

|DEFBUCLE BusEmpresa;
   #30#1;
   26;
   #0#14, 0, nEjer;
   #0#14, 9, nEjer;
   e;
   NULL, BusEmpresa;
|FIN;

|PROCESO BuscaEmpresas;
   nError = 0;
   |SI #0#16 = -1;  aAlfa1 = #0#17;  |FINSI;
   |SI #0#18 = -1;  aAlfa1 = #0#19;  |FINSI;
   aAlfa1 = aAlfa1 % -102;
   nEjer  = aAlfa1;
   nPeriodo = -1;
   |BUCLE BusEmpresa;
   |SI nPeriodo = -1;
       nError = 1;
   |SINO;
       |SI #0#16 = -1; #0#16 = nPeriodo; |FINSI;
       |SI #0#18 = -1; #0#18 = nPeriodo; |FINSI;
   |FINSI;
|FPRC;

|PROGRAMA;
   aParam = PARAMETRO; |QBF aParam;

   |SI aParam = ""; aParam = "I"; |FINSI;

   |HAZ Primero;
   |SI enEjercicio < 2014;
       |MENAV "    Fras en Regimen Especial de Cr. de Caja a partir del Ejercicio 2014";
       |ACABA;
   |FINSI;

   aParam1 = aParam % 204;
   aParam  = aParam % 101;
   #0#6    = aParam;

   |SI aParam1 = "";
       #0#14 = enEmpresa;
       #0#15 = eaNombreEmp;
       |SI aParam = "I";
           #0#16 = -1;
           #0#17 = enEjercicio - 1;
           #0#18 = enPeriodo;
           #0#19 = enEjercicio;
           aAlfa2 = "Origen";
       |SINO;
           #0#16 = enPeriodo;
           #0#17 = enEjercicio;
           #0#18 = -1;
           #0#19 = enEjercicio + 1;
           aAlfa2 = "Destino";
       |FINSI;
       |HAZ BuscaEmpresas;

       |SI nError = 1;
           aAlfa2 = "0000Error no Existe Empresa " + aAlfa2;
           |MENAV aAlfa2;
           |ACABA;
       |FINSI;
       |SI #0#17 < 2014;
           aAlfa2 = "0000Error Empresa Origen con Ejercicio inferior a 2014";
           |MENAV aAlfa2;
           |ACABA;
       |FINSI;

       |SI #0#6 = "I";
           aAlfa2 = #0#17;
           aAlfa1 = fec1 % 106; aAlfa1 = aAlfa1 + aAlfa2; dfec = aAlfa1;
           aAlfa1 = fec2 % 106;
           |SI dig1 ! 1; aAlfa2 = #0#19; |FINSI;
           aAlfa1 = aAlfa1 + aAlfa2; hfec = aAlfa1;
       |SINO;
           dfec = fec1;
           hfec = fec2;
       |FINSI;

       |CLS;
       |PINPA #0#0;
       |SI aParam = "D";
           |PINTA 0606, "                                                                  ";
           |ATRI R; |PINTA 0606, "Anular Traspaso de Facturas RECC pendientes"; |ATRI 0;
           |PINTA 1254, "Anuladas t.: ";
           |PINTA 2207, "Modificaremos Cobros/Pagos de las Fras en Ejercicio Origen.         ";
       |FINSI;
       |PINDA #0#0;

       |ENDATOS #1#0;
   |FINSI;

   |SI aParam1 ! "";
       enEjercicio = aParam1;
       |DDEFECTO #0;
       #0#6  = aParam;
       #0#14 = enEmpresa;
       #0#15 = eaNombreEmp;
       #0#16 = enPeriodoO;
       #0#17 = enEjercicio;
       #0#18 = enPeriodoD;
       #0#19 = enEjercicio + 1;
       #0#4  = "01.01.2000";
       #0#5  = "01.01.2099";
       #0#20 = "N";
       |HAZ impre;
   |FINSI;
|FPRO;
