|FICHEROS;
  cafupz01 #0;

  cafupm01 #1,MANTE;
  cafupm02 #2,MANTE;
  cafupm03 #3,MANTE;
  cafupm04 #4,MANTE;
  cafupm05 #5,MANTE;

  cacuenta #15;
  camaasic #10;
  camaasil #11;

  cafupw03 #903,MANTE;
  caextrp4 #904;
  canempre #30;
  &cafupm1@ #900;
  &cacuent@ #915;
|FIN;

|VARIABLES;
 seleccio      = 0;
 &enEjercicio  = 0;
 &enAny        = 0;
 &eaNombreEmp  = "";
 &eaDirDestino = "";
 &eaDirOrigen  = "";
 &eaCuenta     = "";
 &enNivel      = 0;
 &enDef        = 0;
 &enSwLinea    = 0;
 &eaAper       = "";
 &eaExc        = "";

 nEstado  = 0;
 nInkey   = 0;
 nModo    = 0;
 aFichero = "";

  nBoton2 = 0;
  nBoton3 = 0;
  nBoton4 = 0;
  nBoton5 = 0;
  nBoton6 = 0;
  nBoton7 = 0;
  nBoton8 = 0;
  nBoton9 = 0;
  nBoton10 = 0;
  nBoton12 = 0;
  sw       = 0;
  nSalgo   = 0;
  &enSalgo = 0;
  &sw1     = 0;
  &nPlaEmp = 0;
  nDefin   = 0;

  nSwBoton2 = 0;
  nSwBoton3 = 0;
  nSwBoton4 = 0;
  nSwBoton5 = 0;
  nSwBoton6 = 0;
  nSwBoton7 = 0;
  nSwBoton8 = 0;
  nSwBoton9 = 0;
  nSwBoton10 = 0;
  nSwBoton12 = 0;
  nSwAlta    = 0;

  &swVarios  = 0;
  &enDDiario = 0;
  &enHDiario = 0;
  &efDFechas = @;
  &efHFechas = @;
  &enDDocume = 0;
  &enHDocume = 0;
  &enBorraAu = 0;

  &nMaxDes = 0;
 {-1}punt1 = 0;

  informe  = "cafupz01";
  &nContar = 0;
  aBase    = "";
  aDef     = "";
  aAlfa1   = "";
|FIN;

|INCLUYE acceso_i;
|INCLUYE condes_i;

|| /////////////////////// PROCESOS DE TRABAJO \\\\\\\\\\\\\\\\\\\\
|| Botones

|PROCESO PonBotones;

 |CONTROL_SIMPLE 0, "LBOTON,F2 Modificacion   ", 1010, 1022, Boton2;
 nBoton2 = FSalida;
 |CONTROL_SIMPLE 0, "LBOTON,F3 Alta           ", 1024, 1036, Boton3;
 nBoton3 = FSalida;
 |CONTROL_SIMPLE 0, "LBOTON,F4 Baja           ", 1038, 1050, Boton4;
 nBoton4 = FSalida;
 |CONTROL_SIMPLE 0, "LBOTON,F5 Imprimir       ", 1052, 1064, Boton5;
 nBoton5 = FSalida;
 |CONTROL_SIMPLE 0, "LBOTON,F6 Consolidar     ", 1066, 1078, Boton6;
 nBoton6 = FSalida;

 |CONTROL_SIMPLE 0, "LBOTON,F7 Intro. Empresas ", 1110, 1122, Boton7;
 nBoton7 = FSalida;
 |CONTROL_SIMPLE 0, "LBOTON,F8 Intro. Cuentas  ", 1124, 1136, Boton8;
 nBoton8 = FSalida;
 |CONTROL_SIMPLE 0, "LBOTON,F9 Consulta Asto.  ", 1138, 1150, Boton9;
 nBoton9 = FSalida;
 |CONTROL_SIMPLE 0, "LBOTON,F10 Baja Asiento   ", 1152, 1164, Boton10;
 nBoton10 = FSalida;
 |CONTROL_SIMPLE 0, "LBOTON,F12 Imprimir Asto. ", 1166, 1178, Boton12;
 nBoton12 = FSalida;

 |ESTADO_CONTROL nBoton2,  "DISABLE";  nSwBoton2  = 0;
 |ESTADO_CONTROL nBoton3,  "DISABLE";  nSwBoton3  = 0;
 |ESTADO_CONTROL nBoton4,  "DISABLE";  nSwBoton4  = 0;
 |ESTADO_CONTROL nBoton5,  "DISABLE";  nSwBoton5  = 0;
 |ESTADO_CONTROL nBoton6,  "DISABLE";  nSwBoton6  = 0;
 |ESTADO_CONTROL nBoton7,  "DISABLE";  nSwBoton7  = 0;
 |ESTADO_CONTROL nBoton8,  "DISABLE";  nSwBoton8  = 0;
 |ESTADO_CONTROL nBoton9,  "DISABLE";  nSwBoton9  = 0;
 |ESTADO_CONTROL nBoton10, "DISABLE";  nSwBoton10  = 0;
 |ESTADO_CONTROL nBoton12, "DISABLE";  nSwBoton12  = 0;

 |SI Campo = 0;
    |ESTADO_CONTROL nBoton2,  "ENABLE";  nSwBoton2 = 1;
    |ESTADO_CONTROL nBoton3,  "ENABLE";  nSwBoton3 = 1;
    |ESTADO_CONTROL nBoton4,  "ENABLE";  nSwBoton4 = 1;
    |ESTADO_CONTROL nBoton5,  "ENABLE";  nSwBoton5 = 1;
 |FINSI;

 |SI nSwAlta = 0;
     |ESTADO_CONTROL nBoton6,  "ENABLE";  nSwBoton6 = 1;
     |ESTADO_CONTROL nBoton7,  "ENABLE";  nSwBoton7 = 1;
     |SI #1#12 = "G";
         |ESTADO_CONTROL nBoton8,  "ENABLE";  nSwBoton8 = 1;
     |FINSI;
 |FINSI;

 |SI #1#3 > "01.01.0000";
    |ESTADO_CONTROL nBoton9,  "ENABLE";  nSwBoton9 = 1;
    |ESTADO_CONTROL nBoton10, "ENABLE";  nSwBoton10 = 1;
    |ESTADO_CONTROL nBoton12, "ENABLE";  nSwBoton12 = 1;
 |FINSI;
|FPRC;

|PROCESO QuitaBoton;
 |FIN_CONTROL_WINDOWS nBoton2;
 |FIN_CONTROL_WINDOWS nBoton3;
 |FIN_CONTROL_WINDOWS nBoton4;
 |FIN_CONTROL_WINDOWS nBoton5;
 |FIN_CONTROL_WINDOWS nBoton6;
 |FIN_CONTROL_WINDOWS nBoton7;
 |FIN_CONTROL_WINDOWS nBoton8;
 |FIN_CONTROL_WINDOWS nBoton9;
 |FIN_CONTROL_WINDOWS nBoton10;
 |FIN_CONTROL_WINDOWS nBoton12;
|FPRC;

|PROCESO Boton2;
  |PTEC 824;
|FPRC;

|PROCESO Boton3;
  |PTEC 825;
|FPRC;

|PROCESO Boton4;
  |PTEC 826;
|FPRC;

|PROCESO Boton5;
  |PTEC 827;
|FPRC;

|PROCESO Boton6;
  |PTEC 828;
|FPRC;

|PROCESO Boton7;
  |PTEC 829;
|FPRC;

|PROCESO Boton8;
  |PTEC 830;
|FPRC;

|PROCESO Boton9;
  |PTEC 831;
|FPRC;

|PROCESO Boton10;
  |PTEC 832;
|FPRC;

|PROCESO Boton12;
  |PTEC 823;
|FPRC;

|| ------------------------------------------------------------------------

|PROCESO ClaveBaja903;
 #903#0 = #1#0;
 #903#1 = "            ";
|FPRC;

|PROCESO ClaveAlta903;
 #903#0 = #1#0;
 #903#1 = "zzzzzzzzzzzz";
|FPRC;

|PROCESO ClaveBaja2;
 #2#0 = #1#0;
 #2#1 = 00001;
|FPRC;

|PROCESO ClaveAlta2;
 #2#0 = #1#0;
 #2#1 = 99999;
|FPRC;

|| ---------------------------------------------------------------------------
|PROCESO Tipo7_m1;  |TIPO 7;
     |HAZ PonBotones;
     |HAZ VeoTabla;
|FPRC;

|PROCESO VeoTabla;
     nModo = 7;
     |ENTLINEAL #1#2, -2, 7, 17, 0, ClaveBaja2, ClaveAlta2;

     |SI #1#12 = "G";
         |PINPA #0#903;
         |ENTLINEAL #1#903, -2, 7, 22, 0, ClaveBaja903, ClaveAlta903;
         |ABRE #903;
         #903#0 = #1#0;
         #903#1 = "            ";
         |LEE 001#903];
         |SI FSalida ! 0; |O #903#0 ! #1#0;
             #903#0 = #1#0;
             #903#1 = "            ";
         |FINSI;
         |ENTLINEAL #1#3, -3, 7, 22, 0, ClaveBaja3, ClaveAlta3;
         |CIERRA #903;
     |SINO;
         |ABRE #5;
         nPlaEmp = 1;
         #5#0 = #1#0;
         #5#1 = 00001;
         #5#2 ="            ";
         #5#3 = 1;
         |LEE 001#5];
         |SI FSalida = 0; |Y #5#0 = #1#0;
             nPlaEmp = #5#1;
         |FINSI;
         |CIERRA #5;
         |PINPA #0#5;
         |ENTLINEAL #1#5, -3, 7, 22, 0, ClaveBaja5, ClaveAlta5;
     |FINSI;
|FPRC;

|PROCESO AntesDes;  |TIPO 7;
     |HAZ PonBotones;
|FPRC;

|PROCESO DespDes; |TIPO 0;
     |HAZ Tipo0_m1;
|FPRC;

|PROCESO Tipo12_m1; |TIPO 12;
|FPRC;

|PROCESO Tipo11_m1;  |TIPO 11;
  |SI sw = 0;
      |SI FSalida = 1; |O FSalida = 7;
          nSalgo = 1;
      |FINSI;
  |FINSI;
  |HAZ Tipo0_m1;
|FPRC;

|PROCESO Tipo0_m1; |TIPO 0;

  nInkey = FSalida;
  |HAZ QuitaBoton;
  |PULSATECLA;

  |SI Campo = 0;
      |SI nContar = 0;
          |SI nInkey ! 11; |ACABA; |FINSI;
      |FINSI;
  |FINSI;
  sw = 0;
  |SI nInkey = 10; |Y nSwBoton2 = 1;  || <F2>
      |HAZ ProcModi;
  |FINSI;

  |SI nInkey = 11; |Y nSwBoton3 = 1;  || <F3>
      nSwAlta = 1;
      |HAZ ProcAlta;
      nSwAlta = 0;
  |FINSI;

  |SI nInkey = 12; |Y nSwBoton4 = 1;  || <F4>
      |HAZ ProcBaja;
  |FINSI;

  |SI nInkey = 13; |Y nSwBoton5 = 1;  || <F5>
      |HAZ ProcImpr;
  |FINSI;

  |SI nInkey = 14; |Y nSwBoton6 = 1;  || <F6>
      |HAZ ProcConso;
      |PTEC 802;
      |ERROR;
  |FINSI;

  |SI nInkey = 15; |Y nSwBoton7 = 1;  || <F7>
      |HAZ ProcEmpr;
  |FINSI;

  |SI nInkey = 16; |Y nSwBoton8 = 1;  || <F8>
      |HAZ ProcCtas;
  |FINSI;

  |SI nInkey = 17; |Y nSwBoton9 = 1;  || <F9>
      |HAZ ProcConsAsto;
  |FINSI;

  |SI nInkey = 18; |Y nSwBoton10 = 1;  || <F10>
      |LEE 001#1=;
      |HAZ ProcBajaAsto;
      |GRABA 020#1a;
      |LIBERA #1;
  |FINSI;

  |SI nInkey = 9; |Y nSwBoton12 = 1;  || <F12>
      |HAZ ProcImprAsto;
  |FINSI;
|FPRC;

|PROCESO ProcAlta;

      |HAZ LeeUltim;
      |LIBERA #1;

      |PUSHV 0501 2380;
      |PARA;  |SI;  |HACIENDO;
              nDefin = nContar + 1;
              |CUADRO 0723 1151;
              |BLANCO 0824 1050;
              |PINTA 0824, "Introduzca el Codigo";
              E_inf = 0001;
              E_sup = 9999;
              nDefin = 1036 ? 1;

              #1#0 = nDefin;
              |LEE 040#1=;
              |SI FSalida = 0;
                  |MENAV "     El Codigo ya Existe";
              |SINO;
                  |SAL;
              |FINSI;
         |SIGUE;
         |POPV;
||         |PINTA 0658, "                      ";
         |DDEFECTO #1;
         #1#0 = nDefin;
         |GRABA 020#1b;

         |HAZ VeoTabla;

         |C_M #1#12,1,"S";
         |PINTA #1#0;
         |ENDATOS #1#1;
         |SI FSalida ! 0;
             |BORRA 020#1a;
             |LIBERA #1;
         |SINO;
             |GRABA 020#1a;
             |LIBERA #1;
             |HAZ ProcCtas;
             |HAZ ProcEmpr;
         |FINSI;
         |C_M #1#12,1,"N";

         |PONCONTROL 23, "SI";
         |ERROR;
         |PTEC 806;
         sw = 1;
|FPRC;

|PROCESO ProcModi;
      |LEE 001#1=;

      |ENDATOS #1#1;
      |GRABA 020#1a;
      |LIBERA #1;
|FPRC;

|| --- Proceso borrar
|PROCESO Borram903;
 |BORRA 020#903a;
 |LIBERA #903;
|FPRC;

|DEFBUCLE Borram903;
 #903#1;
 ;
 #1#0, "            ";
 #1#0, "zzzzzzzzzzzz";
 be;
 NULL, Borram903;
|FIN;

|PROCESO Borram3;
 |BORRA 020#3a;
 |LIBERA #3;
|FPRC;

|DEFBUCLE Borram3;
 #3#1;
 ;
 #1#0, "            ", 001;
 #1#0, "zzzzzzzzzzzz", 999;
 be;
 NULL, Borram3;
|FIN;

|PROCESO Borram5_1;
 |BORRA 020#5a;
 |LIBERA #5;
|FPRC;

|DEFBUCLE Borram5_1;
 #5#1;
 ;
 #2#0, #2#1, "            ", 0;
 #2#0, #2#1, "zzzzzzzzzzzz", 9;
 be;
 NULL, Borram5_1;
|FIN;

|PROCESO Borram4_1;
 |BORRA 020#4a;
 |LIBERA #4;
|FPRC;

|DEFBUCLE Borram4_1;
 #4#1;
 ;
 #2#0, #2#1, 001;
 #2#0, #2#1, 999;
 be;
 NULL, Borram4_1;
|FIN;


|PROCESO Borram2;
 |BUCLE Borram4_1;
 |BUCLE Borram5_1;
 |BORRA 020#2a;
 |LIBERA #2;
|FPRC;

|DEFBUCLE Borram2;
 #2#1;
 ;
 #1#0, 00001;
 #1#0, 99999;
 be;
 NULL, Borram2;
|FIN;

|PROCESO ProcBaja;
  |CONFI "    NDesea Borrar esta Definicion ";
  |SI FSalida ! 0;
      |ACABA;
  |FINSI;
  |SI #1#3 > "01.01.0000";
      |HAZ ProcBajaAsto;
  |FINSI;

  |BUCLE Borram2;
  |BUCLE Borram3;
  |BUCLE Borram903;

  |BORRA 020#1a;
  |LIBERA #1;
  |ERROR;
  |PTEC 806;
  sw = 1;
|FPRC;

|| ..... .... ..... Imprimir registros

|PROCESO Imprem3;
 |PRINT;
 enSwLinea = 5;
|FPRC;

|DEFBUCLE Imprem3;
 #3#1;
 ;
 #903#0, #903#1, 001;
 #903#0, #903#1, 999;
 e;
 NULL, Imprem3;
|FIN;

|PROCESO Imprem903;
 |BUCLE Imprem3;
 enSwLinea = 4;
|FPRC;

|DEFBUCLE Imprem903;
 #903#1;
 ;
 #1#0, "            ";
 #1#0, "zzzzzzzzzzzz";
 ;
 NULL, Imprem903;
|FIN;


|PROCESO Imprem5;
 |PRINT;
|FPRC;

|DEFBUCLE Imprem5;
 #5#1;
 ;
 #2#0, #2#1, "            ", 0;
 #2#0, #2#1, "zzzzzzzzzzzz", 9;
 ;
 NULL, Imprem5;
|FIN;

|PROCESO Imprem4;
 |PRINT;
|FPRC;

|DEFBUCLE Imprem4;
 #4#1;
 ;
 #2#0, #2#1, 001;
 #2#0, #2#1, 999;
 ;
 NULL, Imprem4;
|FIN;

|PROCESO Imprem2;
  eaAper = "SI"; |SI #2#3 = "N"; eaAper = "NO"; |FINSI;
  eaExc  = "SI"; |SI #2#4 = "N"; eaExc  = "NO"; |FINSI;
  |PRINT;
  |SI #2#4 = "S";
      enSwLinea = 2;
      |BUCLE Imprem4;
  |FINSI;
  |SI #1#12 ! "G";
      enSwLinea = 7;
      |BUCLE Imprem5;
  |FINSI;
  enSwLinea = 1;
|FPRC;

|DEFBUCLE Imprem2;
 #2#1;
 ;
 #1#0, 00001;
 #1#0, 99999;
 ;
 NULL, Imprem2;
|FIN;

|PROCESO ProcImpr;
 |IMPRE 0;
 |SI FSalida ! 0; |ACABA; |FINSI;

 |INFOR informe;
 |SI FSalida ! 0; |FINIMP; |ACABA; |FINSI;

 enSwLinea = 0;
 |BUCLE Imprem2;
 |SI enSwLinea ! 0;
     enSwLinea = 6; |PRINT;
 |FINSI;
 |SI #1#12 = "G";
     enSwLinea = 3;
     |BUCLE Imprem903;
 |FINSI;
 |PIEINF;
 |FININF;
 |FINIMP;
|FPRC;

|PROCESO ProcEmpr;
     |ENTLINEAL #1#2, -2, 2, 17, 0, ClaveBaja2, ClaveAlta2;
|FPRC;

|PROCESO ProcCtas;
 |SI #1#12 ! "G"; |ACABA; |FINSI;
 enSalgo = 0;
 |PARA; |SI enSalgo = 0; |HACIENDO;
        sw1 = 0;
        |PINPA #0#903;
        |ENTLINEAL #1#903, -2, 2, 22, 0, ClaveBaja903, ClaveAlta903;
        |SI enSalgo = 1; |SAL; |FINSI;
 |SIGUE;
|FPRC;

|PROCESO ProcConso;

   |SI #1#3 > "01.01.0000";
       |CONFI "    SDesea realizar otra vez la consolidacion, borrando el anterior";
       |SI FSalida ! 0;
           |ERROR;
           |ACABA;
       |FINSI;
   |FINSI;

   |LEE 001#1=;
   |GRABA 020#1a;
   |LIBERA #1;

   enDef = #1#0;
   |SUB_EJECUTA_NP "cafupz02";

   |LEE 001#1=;
   |ERROR;
|FPRC;

1| /////////////////////////////////////////////
|PROCESO Lee_Asiento1;
   seleccio = 1;
   |DDEFECTO #904;
   #904#00 = #11#3;        || apunte
   #904#01 = #11#16;       || cuenta DEBE
   #904#02 = #11#17;       || digito DEBE
   #904#03 = #11#10;       || cuenta HABER
   #904#04 = #11#11;       || digito HABER
   #904#05 = #11#6;        || concepto
   #904#06 = #11#7;        || descripcion
   |SI eaConDes ! "N";
       aAlfa1 = #11#24; |QBF aAlfa1;
       #904#6 = aAlfa1 + " " + #11#7;
   |FINSI;
   eaCuenta = #11#4;
   |HAZ LeeLaCta;

   #904#07 = #11#21;       || departamento
   #904#08 = #11#9;        || importe
   #904#09 = #15#2;         || descripcion
   #904#10 = #15#51;        || debe
   #904#11 = #15#52;        || haber
   #904#12 = #15#53;        || saldo
   #904#13 = #11#0;        || diario
   #904#14 = #11#1;        || fecha
   #904#15 = #11#2;        || documento
   |GRABA 020#904n;
|FPRC;

|DEFBUCLE Lee_Asiento;
   #11#1;
   ;
   #10#0, #10#1, #10#2,    1;
   #10#0, #10#1, #10#2, 9999;
   e;
   NULL, Lee_Asiento1;
|FIN;

|PROCESO Mini904;
   #904#0 = 1;
|FPRC;

|PROCESO Maxi904;
   #904#0 = 9999;
|FPRC;

|PROCESO ProcConsAsto;

   aFichero = "q" + Usuario;
   |NOME_DAT #904 aFichero;
   |DELINDEX #904;

   |ABRE #10;
   #10#0 = #1#2;
   #10#1 = #1#3;
   #10#2 = #1#4;
   |LEE 001#10=;
   |SI FSalida ! 0;
       |MENAV "    ERROR. NO EXISTE ASIENTO CONTABLE";
       |CIERRA #10;
       |ACABA;
   |FINSI;
   |CIERRA #10;

   seleccio = 0;
   |ABRE #904;
   |BUCLE Lee_Asiento;
   |CIERRA #904;

   |SI seleccio ! 0;
      |PUSHV 0701 2380;
      |PINPA #0#904;
      |ATRI I; |PINTA 0759, " DEBE  "; |ATRI P; |PINTA 0766, #10#4; |ATRI 0;
      |ATRI I; |PINTA 0859, " HABER "; |ATRI P; |PINTA 0866, #10#5; |ATRI 0;
      |ENTLINEAL #1#904, -1, 4, 20, 0, Mini904,Maxi904;
      |POPV;
   |SINO;
       |MENAV "    Error. NO EXISTE ASIENTO ";
   |FINSI;
   |DELINDEX #904;
|FPRC;

|| //////////////////////////////////////////////////////////////

|PROCESO ProcBajaAsto;
     nEstado = 0;
     |SI #1#3 > "01.01.0000";
         |CONFI "    NDesea Borrar el Asiento de Consolidacion";
         |SI FSalida = 0;
             swVarios = 0;
             enDDiario = #1#2;         || diario
             enHDiario = #1#2;
             efDFechas  = #1#3;
             efHFechas  = #1#3;
             enDDocume  = #1#4;
             enHDocume  = #1#4;
             enBorraAu  = 2;
             |SUB_EJECUTA_NP "caborasi;NOMEQUITESQUETECORTOLOSGUEVOS";
             #1#2 = "";
             #1#3 = "01.01.0000";
             #1#4 = "";
             |CONFI "    NDesea Borrar otros Asientos";
             |SI FSalida = 0;
                 swVarios = 0;
                 enBorraAu  = 0;
                 |SUB_EJECUTA_NP "caborasi;NOMEQUITESQUETECORTOLOSGUEVOS";
            |FINSI;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO ProcImprAsto;
     nEstado = 0;
     |SI #1#3 > "01.01.0000";
             enDDiario = #1#2;
             enHDiario = #1#2;
             efDFechas  = #1#3;
             efHFechas  = #1#3;
             enDDocume  = #1#4;
             enHDocume  = #1#4;
             enBorraAu  = 2;
             |SUB_EJECUTA_NP "caditra0;NOMEQUITESQUETECORTOLOSGUEVOS";
     |FINSI;
|FPRC;

|| //////////////////////////////////////////////////////////////////
|RUTINA ClaveBaja1;
 #1#0 = 0001;
|FRUT;

|RUTINA ClaveAlta1;
 #1#0 = 9999;
|FRUT;

|PROGRAMA;

 |HAZ inicio;
 |SI swerror = 1; |ACABA; |FINSI;
 Ipunt1 = dig3<-;
 Ipunt1 = Ipunt1 + dig3;
 nMaxDes = punt1;

 |DFICE eaDirDestino;
 #0#0 = enEmpresa;
 #0#1 = enPeriodo;
 #0#2 = #30#1;
 #0#4 = #30#26;
 |SI #0#4 < 80;
     #0#3 = 2000 + #0#4;
 |SINO;
     #0#3 = 1900 + #0#4;
 |FINSI;

 |DBASE aBase;
 aDef = aBase + "def/cafupm01.mas";
 |CARGA_DEF aDef, cafupm1@;
 |SI FSalida ! 0;
     |MENAV "    Error en Cafupm01";
     |ACABA;
 |FINSI;
 aDef = aBase + "def/cacuenta.mas";
 |CARGA_DEF aDef, cacuent@;
 |SI FSalida ! 0;
     |MENAV "    Error en Cacuenta";
     |DESCARGA_DEF #cafupm1@;
     |ACABA;
 |FINSI;

 |CLS;
 |CABEZA "Consolidar Empresas";
 |PINPA #0#0;
 |PINDA #0#0;

 enEmpresa   = #0#0;
 enPeriodo   = #0#1;
 eaNombreEmp = #0#2;
 enEjercicio = #0#3;
 enAny       = #0#4;


 |PARA; |SI nSalgo = 0; |HACIENDO;
        |HAZ LeeUltim;
        sw = 0;
        |ENTLINEAL #1#1, -1, 4, 22, 0, ClaveBaja1, ClaveAlta1;
        |SI nSalgo = 1; |SAL; |FINSI;
 |SIGUE;

 |DESCARGA_DEF #915;
 |DESCARGA_DEF #900;

 |PULSATECLA;
|FPRO;

|| ///////////////////////////////////////////////////////////////////////
||--- Procesos Auxiliares
|PROCESO LeeLaCta;
     |HAZ Sacanivel;
     |ABRE #15;
     |DDEFECTO #15;
     #15#0 = eaCuenta;
     #15#1 = enNivel;
     |LEE 001#15=;
     |SI FSalida ! 0;
         #15#2 = "CUENTA NO EXISTENTE EN EMPRESA DESTINO";
     |FINSI;
     |CIERRA #15;
|FPRC;
