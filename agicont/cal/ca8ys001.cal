|FICHEROS;
    ca8ys001 #0;

    cacuenta #1;         || cuentas
    ca8wb001 #3;         || temporal Totales Texto
    cabaexp1 #4;         || temporal existencias
    ca8wb002 #5;         || temporal Cuentas
    ca8wb003 #2;         || temporal Cuentas 8 y 9

    caexistc #6;         || descarga cabs.
    caexistl #7;         || descarga lins.

    canempre #30;        || empresas
    caconimp #8;

    caconcep #22;
    camaasil #23;
    caseslin #110;
    cuentas@ #901;

    ca8cumay #410;  || Control de Balances
    ca8m0000 #420;  || Descripcion Numero de Balance
    ca8m0001 #421;  || Def Balance Textos
    ca8m0002 #422;  || Def Balance Linea Cuentas
    ca8m0011 #431;  || Reparto 8 y 9 cabera
    ca8m0012 #432;  || Reparto 8 y 9 lineas
    ca8m0021 #451;
    ca8m0022 #452;
|FIN;

|VARIABLES;
   &eaValeCero = "N";
   &aparam =  "";
   aFichero = "";
   nBalance = 0;
   nBalNum  = 0;
   &aLucrat = "";
   nImpoOri = 0;
   nImporte = 0;
   nLong    = 0;
   nLongT   = 0;
   nLongE   = 0;
   aTitulo  = "";
   aMas     = "";
   aDef     = "";
   aAPR     = "";

   aCtaExis = "";
   aCuenta  = "";
   aAlfa1   = "";
   acumula = 0;
   e1 = 0;
   e2 = 0;
   e3 = 0;
   e4 = 0;
   nCampo  = 0;
   nCampo2 = 0;
   aCampo3 = "";
   nCampo4 = 0;
   nCampo5 = 0;
   nCampo6 = 0;
   nCampo7 = 0;
   nCampo8 = 0;
   nSI     = 0;
   aCtaM   = "";
   aDCta   = "";
   aHCta   = "";
   nNivel  = 0;

   NumMes  = 0;
   NombreMes = "";
   d_cuen    = "";
   h_cuen    = "";
   d_digi    = 0;
   h_digi    = 0;

   &pathbal = "";
   &balance = 1;
    &entrada = 1;
    nNume1 = 0;
    mes = 0;
    nivel = 0;
    linea = 0;
    FEstado = 0;
    activo = 0;
    pasivo = 0;
    activ1 = 0;
    pasiv1 = 0;
    explo = 0;
    expl1 = 0;
    debe1 = 0;
    haber1 = 0;
    x = 0;
    &eldesde = 0;
    &elhasta = 0;
    sw = 0;
    agrupaci = 0;
    subagrup = 0;
    epigrafe = 0;
    sw_epi = 0;
    d = "A";
    h = "P";
    tipo = "";
    tipo1 = "";
    cue_perdi = "";
    dig_perdi = 0;
    &sw_inf = 0;
    &cantidad = 0;
    &variable = "";
    &canti = 0;
    &doh = "";
    mes_fin = 0;
    descar = 0;
    traraf = 0;
    informe = "";
    alfa1 = "";
   &r_emp = 0;
   &r_per = 0;
   &anu1 = 0;
   &anu  =0;
   dos = 0;
   &cta = "";
   &nom = "";
   &canti2 = 0;
   &cantidad2 = 0;
   act_cuent = "";
   alfa2 = "";
   alfa3 = "";
   tracan5 = 0;
   &d_mes = 0;
   &h_mes = 0;
   &estru = 0;
   &r_eje = 0;
   &r_nom = "";
   nom1 = "";
   sw_exp = 0;
   descn = 0;
   swexis = 0;

  &eaFicbal = "";

  &eaNomSes = "";
  &enNumSes = 0;
  &eaDepSes = "";
  &eaDesDep = "";
  &enMasDep = 0;

  nDEstru = 0;
  nHEstru = 0;
   &enCam0 = 0;
   &enCam1 = 0;
   &enCam2 = 0;
   &enCam6 = 0;
   &enCam7 = 0;
   &enCam8 = 0;
   &enActivo   = 0;
   &enPasivo   = 0;
   &enActiv2   = 0;
   &enPasiv2   = 0;
   &enGastos   = 0;
   &enIngresos = 0;
   &MaximoDigitos = 0;
   nImprimir    = 0;
   aSigno       = "";

   CodigoEmpresa    = 0;
   EjercicioEmpresa = 0;
   NombreEmpresa    = "";
   nSwMen           = 0;
   &enNumPag        = 0;
   nSwLInv          = 0;
   &efFecLeg        = @;
   &eaDatLeg        = "";
   Comodin          = "";
   Comodin1         = "";
   swNoExiste       = 0;
   Calc             = 0;
   Compara          = 0;
   Per1             = 0;
   Per2             = 0;
   &eaLegEmp        = "";
   &eaLegPer        = "";
   &enBasImp   = 0;
   &eaNomImp   = "";
   &dirfich0   = "";
   &eaUnidadBasico = "";
   &eaUnidadBTmp   = "";
   nOpera   = 0;
   nHay     = 0;
   nDCampo5 = 0;
   nDCampo6 = 0;
   nDCampo7 = 0;
   nDCampo8 = 0;
   nHCampo5 = 0;
   nHCampo6 = 0;
   nHCampo7 = 0;
   nHCampo8 = 0;
   nPara1   = 0;
   nSwErrorBal = 0;

   &enSwComp = 0;
   aAlfa     = "";
|FIN;

|INCLUYE varnue_i;
|INCLUYE acceso_i;
|INCLUYE digito_i;
|INCLUYE dsmone_i;
|INCLUYE dscoto_i;
|INCLUYE lospat_i;

|| **********************************************************
|PROCESO antesque;
  aparam = PARAMETRO; |QBF aparam;
  |SI aparam = "fech";
      |HAZ AntesSesion;
      |NOME_DAT #1 eaFicbal;
  |FINSI;
|FPRC;

|PROCESO PintoSes; |TIPO 7;
  aparam = PARAMETRO; |QBF aparam;
  |SI aparam = "fech";
      |HAZ AntesSesion;
      |SI eaNomSes > "";
          |ATRI I; |PINTA 0505, "SESION: "; |PINTA 0513,eaNomSes;
          |SI eaDepSes > "";
              |PINTA 0558, "Departamento: "; |PINTA 0572, eaDepSes;
          |FINSI;
          |ATRI 0;
      |FINSI;
  |FINSI;
|FPRC;

|| ========================================================================
||                        CALCULOS PANTALLA
|| ========================================================================
|PROCESO deentrada;  |TIPO 7;
  |SI #0#14 = 0;  |Y #0#15 = 0;
      #0#14 = empre;
      #0#15 = ejerc - 1;
      |SI #0#15 < 0; #0#15 = 9; |FINSI;
      |PINTA #0#14;
      |PINTA #0#15;
  |FINSI;
|FPRC;

|PROCESO mayor1;
  |SI #0Campo = "N";
      #0#14 = empre; |C_M #0#14,1,"N"; |PINTA #0#14;
      #0#15 = ejerc; |C_M #0#15,1,"N"; |PINTA #0#15;
  |SINO;
      |C_M #0#14,1,"S";
      |C_M #0#15,1,"S";
  |FINSI;
|FPRC;

|PROCESO esbuena;
  |SI FSalida = 9;  |ACABA;  |FINSI;
  |ABRE #30;
  #30#2 = #0#14;
  #30#3 = 0;
  |LEE 011#30];
  |SI FSalida ! 0;  |O #30#2 ! #0#14;
      |SI nSwMen = 0;
          |MENAV "     NO Existe la empresa Contable";
          |ERROR;
      |FINSI;
  |FINSI;
  |CIERRA #30;
|FPRC;

|PROCESO consulta;
  |SI FSalida ! 9;  |ACABA;  |FINSI;
  |CIERRAT #30;
  r_emp = 0;
  |SUB_EJECUTA "caconemp.run";
  |ABRET #30;
  |ERROR;
      #0#14 = r_emp;
      #0#15 = r_per;
      |PTEC 802;
      |PTEC 802;
|FPRC;

|PROCESO esbuena1;

  |SI FSalida = 2; |ACABA; |FINSI;

  swNoExiste = 0;
  |ABRE #30;
  #30#2 = #0#14;
  #30#3 = #0#15;
  |LEE 011#30=;
  |SI FSalida ! 0;
      |SI nSwMen = 0;
          |MENAV "     NO Existe Periodo en la empresa Contable";
          |ERROR;
      |FINSI;
      swNoExiste = 1;
      |VETE LeoActual;
  |FINSI;
  |SI #30#13 ! dig3;  |O #30#14 ! dig4;  |O #30#15 ! dig5;
                      |O #30#16 ! dig6;  |O #30#17 ! dig7;
                      |O #30#18 ! dig8;  |O #30#19 ! dig9;
      |SI nSwMen = 0;
          |MENAV "     NO CORRESPONDEN LOS NIVELES";
          |ERROR;
      |FINSI;
      swNoExiste = 1;
      |VETE LeoActual;
  |FINSI;

  #0#16 = #30#1; |PINTA #0#16;
  anu1 = 1900 + #30#26; |SI anu1 [ 1980; anu1 = anu1 + 100; |FINSI;
  nom1 = #30#1;
  |SI anu1 < 2008;
      |SI nSwMen = 0;
          |MENAV "    Periodo Seleccionado anterior al Ejercicio 2008.   NO COMPARABLE";
          |ERROR;
      |FINSI;
      swNoExiste = 1;
  |FINSI;

|ET LeoActual;
  #30#2 = empre;
  #30#3 = ejerc;
  |LEE 011#30=;
  anu = 1900 + #30#26; |SI anu [ 1980; anu = anu + 100; |FINSI;
  |CIERRA #30;
|FPRC;

|PROCESO pintames; |TIPO 0;
  nNume1 = Campo + 2;
  |SI #0Campo ! 0; |Y #0Campo ! 13;
      NumMes = dig1 + (#0Campo - 1);
      |SI NumMes > 12;
          NumMes = NumMes - 12;
      |FINSI;
  |SINO;
      NumMes = #0Campo;
  |FINSI;
  |HAZ NombreMes;
  #0nNume1 = NombreMes; |PINTA #0nNume1;

  |SI Campo = 1; |Y  #0#0 > #0#1;
      |SI nSwMen = 0;
          |MENAV "    Error de Entrada ...";
          |ERROR;
      |FINSI;
  |FINSI;
|FPRC;

|PROCESO pintames2; |TIPO 0;
  |SI #0Campo ! 0; |Y #0Campo ! 13;
      NumMes = dig1 + (#0Campo - 1);
      |SI NumMes > 12;
          NumMes = NumMes - 12;
      |FINSI;
  |SINO;
      NumMes = #0Campo;
  |FINSI;
  |HAZ NombreMes;

  |ATRI I;
  |SI Campo = 7;  |PINTA 1419, NombreMes;  |FINSI;
  |SI Campo = 8;  |PINTA 1454, NombreMes;  |FINSI;
  |ATRI 0;
  |SI Campo = 8;  |Y #0#7 > #0#8;
      |SI nSwMen = 0;
          |MENAV "    Error de Entrada ...";
          |ERROR;
      |FINSI;
  |FINSI;
|FPRC;

|PROCESO NombreMes;
  |SI NumMes = 0;  NombreMes = "Apertura  "; |FINSI;
  |SI NumMes = 1;  NombreMes = "Enero     "; |FINSI;
  |SI NumMes = 2;  NombreMes = "Febrero   "; |FINSI;
  |SI NumMes = 3;  NombreMes = "Marzo     "; |FINSI;
  |SI NumMes = 4;  NombreMes = "Abril     "; |FINSI;
  |SI NumMes = 5;  NombreMes = "Mayo      "; |FINSI;
  |SI NumMes = 6;  NombreMes = "Junio     "; |FINSI;
  |SI NumMes = 7;  NombreMes = "Julio     "; |FINSI;
  |SI NumMes = 8;  NombreMes = "Agosto    "; |FINSI;
  |SI NumMes = 9;  NombreMes = "Septiembre"; |FINSI;
  |SI NumMes = 10; NombreMes = "Octubre   "; |FINSI;
  |SI NumMes = 11; NombreMes = "Noviembre "; |FINSI;
  |SI NumMes = 12; NombreMes = "Diciembre "; |FINSI;
  |SI NumMes = 13; NombreMes = "Cierre    "; |FINSI;
|FPRC;

|PROCESO nivel; |TIPO 7;
 aAlfa1 = #0#4;
 |C_M #0#5, 1,aAlfa1;
 |SI aAlfa1 = "N";
     |SI dig3 = 1; #0#5 = dig4; |FINSI;
     |SI dig3 = 2; #0#5 = dig5; |FINSI;
     |SI dig3 = 3; #0#5 = dig6; |FINSI;
     |SI dig3 = 4; #0#5 = dig7; |FINSI;
     |SI dig3 = 5; #0#5 = dig8; |FINSI;
     |SI dig3 = 6; #0#5 = dig9; |FINSI;
     |PINTA #0#5;
 |FINSI;
|FPRC;

|PROCESO miraniv; |TIPO 0;
  nLongT = #0#5;
  |SI nLongT > 4;
      nivel = 0;
      |SI nLongT = dig4; nivel = 1;  |FINSI;
      |SI nLongT = dig5; nivel = 2;  |FINSI;
      |SI nLongT = dig6; nivel = 3;  |FINSI;
      |SI nLongT = dig7; nivel = 4;  |FINSI;
      |SI nLongT = dig8; nivel = 5;  |FINSI;
      |SI nLongT = dig9; nivel = 6;  |FINSI;
      |SI nivel = 0;
          |SI nSwMen = 0;
              |MENAV "    Error. Longitud incorrecta";
              |ERROR;
          |FINSI;
      |FINSI;
  |FINSI;
|FPRC;

|PROCESO estruc7; |TIPO 7;
   |SI aparam = "";
       aAlfa1 = "    01-99 Codigo de la Estructura de Existencias";
   |SINO;
       aAlfa1 = "    Si selecciona la estructura 00 debe existir el Asiento de Explotacion";
   |FINSI;
   |MENSA aAlfa1;
|FPRC;

|PROCESO estruct; |TIPO 0;
  |SI #0#6 = 0; |Y aparam = "";
      #0#6 = 1; |PINTA #0#6;
      |ERROR;
      |ACABA;
  |FINSI;

  |SI #0#6 = 0;
      |SI #0#0 ! 0; |O #0#1 ! 12;
          |SI nSwMen = 0;
              |MENAV "    Para informar Estructura 00, seleccione ejercicio completo";
              |ERROR;
          |FINSI;
          |ACABA;
      |FINSI;
      |CUADRO 2003 2332;
      |ATRI S; |PINTA 2105, "Concepto Explotacion "; |ATRI 0;
      |C_M #0#30,1,"S";
      |C_V #0#30,1,"S";
      #0#30 = 1; |PINTA #0#30;
  |SINO;
      |PINTA 2003, "                              ";
      |PINTA 2103, "                              ";
      |PINTA 2203, "                              ";
      |PINTA 2303, "                              ";
      |C_M #0#30,1,"N";
      |C_V #0#30,1,"N";
      #0#30 = 1;
  |FINSI;

  |ABRE #6;
  |SI #0#6 = 0;
      |LEE 040#6p;
  |SINO;
      #6#0 = #0#6;
      |LEE 040#6=;
  |FINSI;
  |SI FSalida ! 0;
      |CIERRA #6;
      |SI nSwMen = 0;
          |MENAV "    No existe la estructura de descarga.";
          |ERROR;
      |FINSI;
      |ACABA;
  |FINSI;
  |CIERRA #6;
  |HAZ cue_per;
|FPRC;

|PROCESO cue_per; || calcula la cuenta de perdidas y ganancias
  cue_perdi = #6#2;
  dig_perdi = #6#3;
  |SI #0#4 = "N";
      aAlfa1 = #6#2; |QBF aAlfa1;
      aAlfa1 = aAlfa1 % 0;
      nLong  = aAlfa1;
  |SINO;
      traraf = 100 + #0#5;
      cue_perdi = #6#2 % traraf;
      dig_perdi = nivel;
  |FINSI;
  cue_perdi = cue_perdi + "            ";
  cue_perdi = cue_perdi % 112;
|FPRC;

|PROCESO LeoConcep;
 |C_M #0#30,0,alfa1;
 |SI alfa1 = "N"; |ACABA; |FINSI;

 |ABRE #22;
 #22#0 = #0#30;
 |LEE 001#22=;
 |SI FSalida ! 0;
     |SI nSwMen = 0;
         |MENAV "    Error. No existe Concepto";
         |ERROR;
     |FINSI;
 |SINO;
     alfa1 = #22#1; alfa1 = alfa1 % 123;
     |ATRI I; |PINTA 2209, alfa1; |ATRI 0;
 |FINSI;
 |CIERRA #22;
|FPRC;

|PROCESO descri; |TIPO 6;
 aLucrat     = "";
 nSwErrorBal = 0;
 |ABRE #420;
 |DDEFECTO #420;
 #420#0 = #0#26;
 |LEE 001#420=;
 |SI FSalida ! 0;
     |SI nSwMen = 0;
         |MENAV "   Error. No existe Numero de Balance";
         |ERROR;
     |FINSI;
     nSwErrorBal = 1;
 |FINSI;
 |CIERRA #420;
 #0#27 = #420#1; |SI nSwMen = 0; |PINTA #0#27; |FINSI;
 aLucrat = #420#3;
|FPRC;
|| ****************************************************************
|| ****************************************************************
|PROCESO MiroSiHay;
  |SI nHay = -1; nHay = 0; |FINSI;
  nNume1 = 0;
  |PARA nPara1 = 9; |SI nPara1 [ 47; |HACIENDO nPara1 = nPara1 + 1;
        |SI #1nPara1 ! 0; nNume1 = 1; |SAL; |FINSI;
  |SIGUE;
  |SI nNume1 ! 0; nHay = 1; |ERROR10; |FINSI;
|FPRC;

|DEFBUCLE MiroSiHay;
   #1#1;
   3;
   "8           ", 0, "S";
   "999999999999", 9, "S";
   ;
   NULL, MiroSiHay;
|FIN;


|PROCESO conexi8y9;
  nHay = -1;
  |BUCLE MiroSiHay;
  |SI nHay = 1;
      d_mes = #0#0;
      h_mes = #0#1;
      |SI #0#32 = "N"; |SUB_EJECUTA "ca8z0011;BALANCEN"; |FINSI;
      |SI #0#32 = "S"; |SUB_EJECUTA "ca8z0011;BALANCES"; |FINSI;
  |FINSI;
|FPRC;

|PROCESO conexi;

  |SI #0#6 = 0; |ACABA; |FINSI;
  nCam1 = empre;
  nCam2 = ejerc;
  nCam3 = enEjercicio;
  d_mes = #0#7;
  h_mes = #0#8;
  estru = #0#6;
  r_emp = empre;
  r_per = ejerc;
  r_eje = #30#26;
  r_nom = #30#1;
  |SUB_EJECUTA "caexicon";
  |SI #0#13 = "N"; |ACABA; |FINSI;

  r_emp = #0#14;
  r_per = #0#15;
  r_eje = anu1 - 2000; |SI r_eje < 0; r_eje = anu1 - 1900; |FINSI;
  r_nom = nom1;
  |SUB_EJECUTA "caexicon";

  empre = nCam1;
  ejerc = nCam2;
  enEjercicio = nCam3;
|FPRC;

|| ***************************************************************************
|| *********************  GRABACION DE TEMPORALES Y CALCULOS *****************
|| ***************************************************************************
|PROCESO MSigno_sLeo;
  #410#12 = enEjerBal;
  #410#0  = aCtaM;
  #410#10 = eNum2008;
  |LEE 001#410=;
  |SI FSalida = 0;
      nSI  = 1;
      aAPR = #410#3;
  |FINSI;
|FPRC;

|PROCESO MSigno08_s;
  nSI = 0;
  aCtaM = aCuenta % 105;
  |HAZ MSigno_sLeo;
  |SI nSI = 1; |ACABA; |FINSI;

  aCtaM = aCuenta % 104;
  |HAZ MSigno_sLeo;
  |SI nSI = 1; |ACABA; |FINSI;

  aCtaM = aCuenta % 103;
  |HAZ MSigno_sLeo;
  |SI nSI = 1; |ACABA; |FINSI;

  aCtaM = aCuenta % 102;
  |HAZ MSigno_sLeo;
|FPRC;

|PROCESO BuscaCuenta;
 nNume1  = 100 + nLongT;
 aCuenta = aCuenta % nNume1;

 #901#0 = aCuenta;
 #901#1 = nivel;
 |LEE 001#901=;
 |SI FSalida = 0;
     aTitulo = #901#2;
     aAPR    = #901#5; |HAZ MSigno08_s;
 |SINO;
     |SI nLongT [ 4;
         #410#12 = enEjerBal;
         #410#0  = aCuenta;
         #410#10 = eNum2008;
         |LEE 001#410=;
         |SI FSalida = 0;
             aTitulo = #410#1;
             aAPR    = #410#3;
         |FINSI;
     |FINSI;
 |FINSI;
|FPRC;

|PROCESO CambioCta;
  aAlfa1 = aCuenta; |QBF aAlfa1;
  aAlfa1 = aAlfa1 % 0;
  nLong  = aAlfa1;
  |SI nLong ! nLongT;
      |HAZ BuscaCuenta;
  |FINSI;
|FPRC;
|| .............................................................

|PROCESO cargatra_3;  ||Carga Trabajo de Cuentas
  #3#0 = nBalNum;
  #3#1 = nBalance;
  #3#2 = nCampo2;
  #3#3 = aCampo3;
  #3#4 = nCampo4;
  #3#5 = nCampo5;
  #3#6 = nCampo6;
  #3#7 = nCampo7;
  #3#8 = nCampo8;
  |LEE 101#3=;
  |SI FSalida = 0; |Y #3#9 ! "A";
      #3acumula = #3acumula + nImporte;
      |GRABA 020#3a;
  |FINSI;
  |LIBERA #3;
|FPRC;

|PROCESO LosTotales;
  nCampo2 = #5#2;
  aCampo3 = #5#3;
  nCampo4 = #5#4;
  nCampo5 = #5#5;
  nCampo6 = #5#6;
  nCampo7 = #5#7;
  nCampo8 = #5#8;

  |HAZ cargatra_3;

  |SI nCampo8 ! 0;    ||Subpartida (A/P = 0)
      nCampo8 = 0;
      |HAZ cargatra_3;
  |FINSI;

  |SI nCampo7 ! 0;   ||Partida
      nCampo8 = 0;
      nCampo7 = 0;
      |HAZ cargatra_3;
  |FINSI;

  |SI nCampo6 ! 0;   ||Epigrafe
      nCampo8 = 0;
      nCampo7 = 0;
      nCampo6 = 0;
      |HAZ cargatra_3;
  |FINSI;

  |SI nCampo5 ! 0;  ||Subagrupacion
      nCampo8 = 0;
      nCampo7 = 0;
      nCampo6 = 0;
      nCampo5 = 0;
      |HAZ cargatra_3;
  |FINSI;

  |SI nCampo4 ! 0;  ||Agrupacion
      nCampo8 = 0;
      nCampo7 = 0;
      nCampo6 = 0;
      nCampo5 = 0;
      nCampo4 = 0;
      |HAZ cargatra_3;
  |FINSI;

  |SI aCampo3 = "A";
      activo = (activo + nImporte) ! EURODB;
  |SINO;
      pasivo = (pasivo + nImporte) ! EURODB;
  |FINSI;
|FPRC;

|PROCESO cargatra_5;  ||Carga Trabajo de Cuentas
  #5#0 = nBalNum;
  #5#1 = nBalance;
  #5#2 = #422#2;
  #5#3 = #422#3;
  #5#4 = #422#4;
  #5#5 = #422#5;
  #5#6 = #422#6;
  #5#7 = #422#7;
  #5#8 = #422#12;
  #5#9 = #422#8;
  #5#10 = aCuenta;
  |LEE 001#5=;
  |SI FSalida ! 0;
      |DDEFECTO #5;
      #5#0 = nBalNum;
      #5#1 = nBalance;
      #5#2 = #422#2;
      #5#3 = #422#3;
      #5#4 = #422#4;
      #5#5 = #422#5;
      #5#6 = #422#6;
      #5#7 = #422#7;
      #5#8 = #422#12;
      #5#9 = #422#8;
      #5#10 = aCuenta;
      |GRABA 020#5b;
  |FINSI;

  #5#11 = aTitulo;
  #5#12 = nLong;
  #5acumula = #5acumula + nImporte;
  |GRABA 020#5a;
  |LIBERA #5;

  |HAZ LosTotales;
|FPRC;

|PROCESO Busco;
  nImpoOri = nImporte;
  nSI = 1;
  |SI #422#11 ! " ";
      |SI #422#11 = "P"; |Y nImporte < 0; |ACABA; |FINSI;
      |SI #422#11 = "N"; |Y nImporte > 0; |ACABA; |FINSI;
  |FINSI;
  |SI #422#10 = "-"; nImporte = - nImporte; |FINSI;
  |HAZ cargatra_5;
  nImporte = nImpoOri;
|FPRC;

|DEFBUCLE Busco;
 #422#2;
 ;
 enEjerBal, nBalNum, nBalance, aDCta;
 enEjerBal, nBalNum, nBalance, aHCta;
 ;
 NULL, Busco;
|FIN;

|PROCESO CargaVarios;
  aAlfa1 = aCtaExis; |QBF aAlfa1;
  aAlfa1 = aAlfa1 % 0;
  nLongE = aAlfa1;

  nSI = 0;
  |SI nLongE ] 5;
      aDCta = aCtaExis % 105;
      aHCta = aCtaExis % 105;
      |BUCLE Busco;
||  |SI nSI = 1; |ACABA; |FINSI;
  |FINSI;

  |SI nLongE ] 4;
      aDCta = aCtaExis % 104;
      aHCta = aCtaExis % 104;
      |BUCLE Busco;
||  |SI nSI = 1; |ACABA; |FINSI;
  |FINSI;

   aDCta = aCtaExis % 103;
   aHCta = aCtaExis % 103;
   |BUCLE Busco;
||  |SI nSI = 1; |ACABA; |FINSI;

   aDCta = aCtaExis % 102;
   aHCta = aCtaExis % 102;
   |BUCLE Busco;
|FPRC;

||......................................................
|PROCESO descarga;
  |HAZ digitos2;
  |SI d_error ! 0;  |ACABA;  |FINSI;

  linea = linea + 1;
  #4#0 = linea;
  #4#8 = #0#6;
  |LEE 001#4=;
  |SI FSalida = 0;  |BORRA 020#4a;  |FINSI;

  aCtaExis = #1#0;
  aCuenta = #1#0;
  nNivel  = #1#1;
  aTitulo = #1#2;
  aAPR    = #1#5; |HAZ MSigno08_s;
  |SI #0#4 = "S"; |HAZ CambioCta; |FINSI;

  |DDEFECTO #4;
  #4#0 = linea;    || linea
  #4#8 = #0#6;     || estructura
  #4#1 = #1#0;     || cuenta
  #4#2 = #1#1;     || digito
  #4#3 = #1#2;     || descripcion

  mes_fin  = #0#7 + 11;  || mes iniciales
  #4e1     = #7mes_fin;    || exist. iniciales
  mes_fin  = #0#8 + 11;  || mes finales
  #4e2     = #7mes_fin;    || exist. finales
  tracan5  = #7mes_fin;
||  |SI aAPR = "P"; tracan5 = -tracan5; |FINSI; || (cambio signo)

  descar = (#4e1 - #4e2) ! EURODB; || ... iniciales - finales
  |SI descar ] 0;
      #4e3 = #7#5;     || cuenta descarga D
      #4e4 = #7#6;     || digito descarga D
  |SINO;
      #4e3 = #7#8;     || cuenta descarga H
      #4e4 = #7#9;     || digito descarga H
  |FINSI;
  |GRABA 020#4n;

  nImporte = tracan5;
  |SI nImporte ! 0; |HAZ CargaVarios; |FINSI;
|FPRC;

|PROCESO lineades;
  #1#0 = #7#2;
  #1#1 = #7#3;
  |LEE 000#1=;
  |SI FSalida = 0;
      |HAZ descarga;
  |FINSI;
|FPRC;

|DEFBUCLE LeeDescarga;
 #7#1;
 ;
 nDEstru, 01;
 nHEstru, 99;
 ;
 NULL, lineades;
|FIN;


|PROCESO pideexis;
  |SI #0#6 ! 0;
     |ABRE #6;
      #6#0 = #0#6;
      |LEE 001#6=;
      |HAZ cue_per;
      |BUCLELIN 2lineades#6;
      |CIERRA #6;
  |SINO;
      || cue_perdi = "129";
      |BUCLE LeeDescarga;
  |FINSI;
|FPRC;

|PROCESO Grabaw3;
 |DDEFECTO #2;
 #2#0 = aCuenta;
 |LEE 001#2=;
 |SI FSalida ! 0;
     #2#0 = aCuenta;
     |GRABA 020#2c;
 |FINSI;
 |SI aSigno = "D";
     #2#1 = #2#1 + nImporte;
 |SINO;
     #2#2 = #2#2 + nImporte;
 |FINSI;
 |GRABA 020#2a;
|FPRC;

|PROCESO reparto_lin;
 aAlfa1 = #432#2; |QBF aAlfa1;
 |SI aAlfa1 ! "";
     aCuenta  = #432#2;
     aSigno   = #431#8;
     nImporte = #432#4;
     |HAZ Grabaw3;
 |FINSI;
|FPRC;

|DEFBUCLE reparto_lin;
 #432#1;
 ;
 #431#0, 01;
 #431#0, 99;
 e;
 NULL, reparto_lin;
|FIN;

|PROCESO reparto;
 |DDEFECTO #1;
 #1#0 = #431#0;
 #1#1 = #431#5;
 |LEE 001#1=;

 aAlfa1 = #431#2; |QBF aAlfa1;
 |SI aAlfa1 ! "";
     aCuenta  = #431#2;
     nImporte = #431#4;
     aSigno   = #431#8;
     |HAZ Grabaw3;
 |SINO;
     |BUCLE reparto_lin;
 |FINSI;
|FPRC;

|DEFBUCLE reparto;
 #431#1;
 ;
 "            ";
 "zzzzzzzzzzzz";
 e;
 NULL, reparto;
|FIN;

|PROCESO pide8y9;
  |ABRE #2;
  |ABRE #1;
  |BUCLE reparto;
  |CIERRA #1;
  |CIERRA #2;
|FPRC;

|| Explotacion
|| *************************************************************************
|PROCESO AstoExplota;
 |SI #23#8 = "D";
     tracan5 = tracan5 + #23#9;
 |SINO;
     tracan5 = tracan5 - #23#9;
 |FINSI;
|FPRC;

|DEFBUCLE AstoExplota;
 #23#3;
 6;
 #1#0, #1#1, "01.01.0000", 000001, #0#30;
 #1#0, #1#1, "31.12.2999", 999999, #0#30;
 e;
 NULL, AstoExplota;
|FIN;

|PROCESO grabar129;
 tracan5 = 0;
 |BUCLE AstoExplota;
 |SI tracan5 ! 0;
     aCtaExis= #1#0;
     aCuenta = #1#0;
     nNivel  = #1#1;
     aAPR    = #1#5; |HAZ MSigno08_s;
     aTitulo = #1#2;
     nImporte = tracan5;  ||Auque sea 0, la graba
     |HAZ CargaVarios;
  |FINSI;
|FPRC;

|DEFBUCLE grabar129;
 #1#1;
 3;
 d_cuen, d_digi, "S";
 h_cuen, h_digi, "S";
 e;
 NULL, grabar129;
|FIN;

|PROCESO TodasLas129;
 d_cuen  = "129         "; d_digi = 0;
 h_cuen  = "129zzzzzzzzz"; h_digi = 9;
 |BUCLE grabar129;
|FPRC;

|PROCESO ver_exp;
 |SI #4e3 = #1#0; |Y #4e4 = #1#1;
     sw_exp = 1;
     descar = (#4e1-#4e2);
     |SI aAPR = "H"; descar = -descar; |FINSI;
     descn = descn + descar;
 |FINSI;
|FPRC;

|DEFBUCLE cabasitu9;
 #4#1;
 ;
 nDEstru, 001;
 nHEstru, 999;
 e;
 NULL, ver_exp;
|FIN;

|PROCESO calexpl01;
  aCuenta = #1#0;
  nNivel  = #1#1;
  aAPR    = #1#5; |HAZ MSigno08_s;
  |SI aAPR ! "D"; |Y aAPR ! "H"; |ACABA; |FINSI;

  |HAZ digitos2;
  |SI d_error ! 0;  |ACABA;  |FINSI;

  sw_exp = 0;
  descn  = 0;
  |BUCLE cabasitu9;
  |SI sw_exp = 1
      |SI aAPR = "D";
          debe1 = debe1 + descn;
      |SINO;
          haber1 = haber1 + descn;
     |FINSI;
  |SINO;
     |SI aAPR = "D";
         |PARA x = eldesde; |SI x [ elhasta; |HACIENDO x = x + 3;
               debe1  = (debe1  + #1x) ! EURODB;
         |SIGUE;
     |SINO;
         |PARA x = eldesde; |SI x [ elhasta; |HACIENDO x = x + 3;
               haber1 = (haber1 - #1x) ! EURODB;
         |SIGUE;
     |FINSI;
 |FINSI;
|FPRC;

|DEFBUCLE calexpl0;
 #1#1;
 3;
 "            ", 0, "S";  || "D", 1, 00, 00, 00,"S";
 "zzzzzzzzzzzz", 9, "S";  || "H", 4, 99, 99, 99,"S";
 e;
 NULL,calexpl01;
|FIN;

|PROCESO explota;
  debe1  = 0;
  haber1 = 0;

  |BUCLE calexpl0;

  canti = (haber1 - debe1) ! EURODB;
  explo = canti;

  |SI #0#6 ! 0;
     |ABRE #1;
     |DDEFECTO #1;
     #1#0 = #6#2;
     #1#1 = #6#3;
     |LEE 000#1=;
     |CIERRA #1;
     aCtaExis=#6#2;
     aCuenta = cue_perdi;
     nNivel  = dig_perdi;
     aAPR    = #1#5; |HAZ MSigno08_s;
     aTitulo = #1#2;

     tracan5 = - canti;
     |PARA x = eldesde; |SI x [ elhasta; |HACIENDO x = x + 3;
           tracan5 = tracan5 + #1x;
     |SIGUE;
     nImporte = tracan5;  ||Auque sea 0, la graba
     |HAZ CargaVarios;
  |SINO;
     |HAZ TodasLas129;
  |FINSI;
|FPRC;

|PROCESO grabatra; || grabo todas
   nImporte = 0;
   |PARA x = eldesde; |SI x [ elhasta; |HACIENDO x = x + 3;
         nImporte = nImporte + #1x;
   |SIGUE;

||      |SI nImporte ! 0; |HAZ CargaVarios; |FINSI;

  |SI act_cuent = "1";   || nuevo
      #2#0 = #1#0;
      |LEE 001#2=;
      |SI FSalida = 0;
          nImporte = nImporte + #2#1 - #2#2; ||debe menos haber
      |FINSI;
  |FINSI;

    |SI #0#4 = "S"; |HAZ CambioCta; |FINSI;

    |SI nImporte ! 0;
    |SI #422#11 ! " ";
        |SI #422#11 = "P"; |Y nImporte < 0; |ACABA; |FINSI;
        |SI #422#11 = "N"; |Y nImporte > 0; |ACABA; |FINSI;
    |FINSI;
    |SI #422#10 = "-"; nImporte = - nImporte; |FINSI;
    |HAZ cargatra_5;
  |FINSI;
|FPRC;

|PROCESO miracuen;
  |SI #4#1 = #1#0; |Y #4#2 = #1#1;        || existencias
      swexis = 1;
      cantidad = cantidad + #4#5;
  |FINSI;
|FPRC;

|DEFBUCLE cabasitu3;
 #4#1;
 ;
 #0#6, 001;
 #0#6, 999;
 e;
 NULL, miracuen;
|FIN;

|PROCESO calculit;

  aCuenta = #1#0;
  nNivel  = #1#1;
  aTitulo = #1#2;
  aAPR    = #1#5; |HAZ MSigno08_s;
  |SI aAPR ! "A"; |Y aAPR ! "P"; |ACABA; |FINSI;

  |HAZ digitos2;
  |SI d_error ! 0;  |ACABA;  |FINSI;

  aAlfa1 = #1#0; |QBF aAlfa1;
  aAlfa1 = aAlfa1 % 0;
  nLong  = aAlfa1;
  aCuenta = #1#0;
  nNivel  = #1#1;
  aTitulo = #1#2;
  nImporte = 0;

  |SI #1#0 = #6#2;  |ACABA;  |FINSI;  || es la cuenta de perdidas y ganancias
  act_cuent = #1#0 % 101;
  swexis = 0;
  |SI act_cuent = "3"; |BUCLE cabasitu3; |FINSI;
  |SI swexis = 0;
      |HAZ grabatra;
  |FINSI;
|FPRC;

|DEFBUCLE cabasitu0;
 #1#1;
 3;
 aDCta, 0, "S";
 aHCta, 9, "S";
 e;
 NULL,calculit;
|FIN;

|PROCESO calculit_4;
 aAlfa1 = #422#9; |QBF aAlfa1;
 aDCta = (aAlfa1 + "            ") % 112;
 aHCta = (aAlfa1 + "zzzzzzzzzzzz") % 112;
 |BUCLE cabasitu0;
|FPRC;

|DEFBUCLE cabasitu4;
 #422#1;
 ;
 enEjerBal, nBalNum, nBalance, 0, "A", 00, 0, 00, 00, 0, 001;
 enEjerBal, nBalNum, nBalance, 0, "P", 99, 9, 99, 99, 9, 999;
 e;
 NULL,calculit_4;
|FIN;

|| ***************************************************************************
|| ***************************************************************************
|| ***************************************************************************
|| *********************  CALCULO DE IMPRESION *******************************
|| ***************************************************************************
|PROCESO HaySaldoSI;
  |SI #5#13 ! 0; |O #5#14 ! 0;
      nHay = 1;
      |ERROR10;
  |FINSI;
|FPRC;

|DEFBUCLE HaySaldoSI;
 #5#1;
 ;
 #3#0, #3#1, #3#2, #3#3, #3#4, nDCampo5, nDCampo6, nDCampo7, nDCampo8, "            ";
 #3#0, #3#1, #3#2, #3#3, #3#4, nHCampo5, nHCampo6, nHCampo7, nHCampo8, "zzzzzzzzzzzz";
 e;
 NULL, HaySaldoSI;
|FIN;

|PROCESO HaySaldo;
   nDCampo5 = #3#5; nHCampo5 = #3#5;
   |SI #3#5 = 0;
       nDCampo5 = 0;
       nHCampo5 = 9;
   |FINSI;
   nDCampo6 = #3#6; nHCampo6 = #3#6;
   |SI #3#6 = 0;
       nDCampo6 = 00;
       nHCampo6 = 99;
   |FINSI;
   nDCampo7 = #3#7; nHCampo7 = #3#7;
   |SI #3#7 = 0;
       nDCampo7 = 00;
       nHCampo7 = 99;
   |FINSI;
   nDCampo8 = #3#8; nHCampo8 = #3#8;
   |SI #3#8 = 0;
       nDCampo8 = 0;
       nHCampo8 = 9;
   |FINSI;
   nHay = 0;
   |BUCLE HaySaldoSI;
|FPRC;

|PROCESO cuentas;
  cantidad  = #5#13;
  cantidad2 = #5#14;
  |SI cantidad ! 0; |O cantidad2 ! 0;
      |PRINT;
  |FINSI;
  sw_inf = 4;
|FPRC;


|DEFBUCLE cabasitu2;
 #5#1;
 ;
 #3#0, #3#1, #3#2, #3#3, #3#4, #3#5, #3#6, #3#7, #3#8, "            ";
 #3#0, #3#1, #3#2, #3#3, #3#4, #3#5, #3#6, #3#7, #3#8, "zzzzzzzzzzzz";
 e;
 NULL, cuentas;
|FIN;

|PROCESO agrupacion;
  sw_epi = 1;                || fuerza la impresion del epigrafe
|FPRC;

|PROCESO impresio;

  |SI #3#9 ! "A"; nImprimir = 0; |FINSI;

  |SI #3#13 = 0; |Y #3#14 = 0; |Y #3#9 ! "A";
      nOpera = 0;
      |SI eaValeCero = "N"; nOpera = 1; |FINSI;
      |SI #3#0 = "A";
          |SI activo ! 0; |O activ1 ! 0;
              nOpera = 1;
          |FINSI;
      |FINSI;
      |SI #3#0 = "P";
          |SI pasivo ! 0; |O pasiv1 ! 0;
              nOpera = 1;
          |FINSI;
      |FINSI;
      |SI #3#0 = tipo;               nOpera = 1; |FINSI;
      |SI nOpera = 1;
          nHay = 0;
          |SI #0#4 = "S"; |HAZ HaySaldo; |FINSI;
          |SI nHay = 0; |ACABA; |FINSI;
      |FINSI;
  |FINSI;

  |SI sw = 0;
      tipo     = #3#3;  ||Activo o Pasivo
      agrupaci = #3#4;
      subagrup = #3#5;
      |SI #3#3 = "A"; doh = "ACTIVO"; |FINSI;
      |SI #3#3 = "P"; doh = "PASIVO"; |FINSI;
      |HAZ agrupacion;
      sw = 1;
  |FINSI;

  |SI tipo ! #3#3;
      sw_inf = 5;  |PRINT;  |PRINT;
      sw_inf = 1;
      |SI tipo = "A";
          variable = "                        T O T A L   A C T I V O:";
          canti  = activ1;
          canti2 = activo;
      |SINO;
          variable = "                        T O T A L   P A S I V O:";
          canti = pasiv1;
          canti2 = pasivo;
      |FINSI;
      |PRINT;
      doh = "PASIVO";
      |PIEINF;
      tipo     = #3#3;
      agrupaci = #3#4;
      subagrup = #3#5;
      |HAZ agrupacion;
  |FINSI;

  |SI agrupaci ! #3#4; |O subagrup ! #3#5;
      sw_inf = 5;  |PRINT;       || linea en blanco
      |HAZ agrupacion;
      agrupaci = #3#4;
      subagrup = #3#5;
  |FINSI;

  |SI #3#9 = "C"; |O #3#9 = "S"; |O #3#9 = "T";
      |SI #3#7 ! 0; sw_inf = 2; |FINSI;
      |SI #3#7 = 0; sw_inf = 0; |FINSI;
  |FINSI;
  |SI #3#9 = "A"; sw_inf = 7; |FINSI;
  |SI #3#9 ! "A"; |O nImprimir = 1;
      |PRINT;
      nImprimir = 1;
  |FINSI;

  |SI #3#9 = "C"; |Y #0#4 = "S";
      sw_inf=3;
      |BUCLE cabasitu2;
   |FINSI;
|FPRC;


|DEFBUCLE cabasitu1;
 #3#1;
 ;
 nBalNum, nBalance, 0, "A", 00, 0, 00, 00, 0;
 nBalNum, nBalance, 0, "P", 99, 9, 99, 99, 9;
 e;
 NULL,impresio;
|FIN;

|| ---------------------------------------------
|PROCESO GrabaAuxiliar;
 |SI #421#3 = "A"; |O #421#3 = "P";
     |DDEFECTO #3;
     #3#0  = #421#0;
     #3#1  = #421#1;
     #3#2  = #421#2;
     #3#3  = #421#3;
     #3#4  = #421#4;
     #3#5  = #421#5;
     #3#6  = #421#6;
     #3#7  = #421#7;
     #3#8  = #421#10;
     #3#9  = #421#8;
     #3#10 = #421#9;
     |GRABA 000#3n;
 |FINSI;
 |LIBERA #421;
|FPRC;

|DEFBUCLE GrabaAuxiliar;
 #421#1;
 ;
 enEjerBal, nBalNum, nBalance, 0, "A", 00, 0, 00, 00, 0;
 enEjerBal, nBalNum, nBalance, 0, "P", 99, 9, 99, 99, 9;
 e;
 NULL, GrabaAuxiliar;
|FIN;

|| *****************************************************************
|PROCESO HagoTodo;
  |HAZ Borrar;
  nDEstru = #0#6; nHEstru = #0#6;
  |SI #0#6 = 0; nDEstru = 01; nHEstru = 99; |FINSI;


  |ABRE #3;
  |BUCLE GrabaAuxiliar;
  |CIERRA #3;

  |DBASE aMas;
  aDef = aMas + "def/cacuenta";
  |CARGA_DEF aDef, cuentas@;
  |SI FSalida ! 0;
      |MENAV "    Error al Carga fichero de Cuentas";
      |ACABA;
  |FINSI;
  |PATH_DAT #901 dirfich0;

  eldesde = (#0#0 * 3) + 11;
  elhasta = (#0#1 * 3) + 11;

  |PARA dos = 1; |SI dos [ 2; |HACIENDO dos = dos + 1;

       |DELINDEX #2;
       acumula = 13;
       e1 = 4;
       e2 = 5;
       e3 = 6;
       e4 = 7;

       |SI dos = 2; |Y #0#13 = "N";
           activ1 = activo;
           pasiv1 = pasivo;
           expl1 = explo;
           |SAL;
       |FINSI;

       |SI dos = 2;
           activ1 = activo;
           pasiv1 = pasivo;
           expl1 = explo;
           activo = 0;
           pasivo = 0;
           explo = 0;

           debe1 = 0;
           haber1 = 0;
           acumula = 14;
           e1 =  9;
           e2 = 10;
           e3 = 11;
           e4 = 12;

          |DBASE dir_fich;
          dir_fich = dir_fich + "fich/";
          alfa1 = #0#14;   alfa1 = "00000" + alfa1;   alfa1 = alfa1 % -105;
          alfa2 = #0#15;   alfa2 = "0"   + alfa2;   alfa2 = alfa2 % -101;
          alfa3 = dir_fich + alfa1 + alfa2 + "/";

          |PATH_DAT #1   alfa3;
          |PATH_DAT #901 alfa3;
          |PATH_DAT #6 alfa3;
          |PATH_DAT #7 alfa3;
          |PATH_DAT #8 alfa3;
          |PATH_DAT #23 alfa3;
          |PATH_DAT #431 alfa3;
          |PATH_DAT #432 alfa3;

          |ABRE #30;
          #30#2 = #0#14;
          #30#3 = #0#15;
          |LEE 011#30=;
          |CIERRA #30;
  |FINSI;

  |SI dos = 1;
      r_emp = empre;
      r_per = ejerc;
  |SINO;
      r_emp = #0#14;
      r_per = #0#15;
  |FINSI;

  |ABRE #8;
  |DDEFECTO #8;
  #8#0 = 1;
  |LEE 000#8=;
  |CIERRA #8;
  |SI #8#2 = "S";  |Y #8#3 = "N";  |Y aparam = "";
      |SI dos = 2;
          pathbal = alfa3;
      |SINO;
          |DFICE pathbal;
      |FINSI;
      balance = 1;
      |SUB_EJECUTA "careccue.tab";
 |FINSI;
|| ---------------------------------------------------------------------

  |ABRE #1;
  |ABRE #901;
  |ABRE #410;
  |ABRE #3;
  |ABRE #4;
  |ABRE #5;
  |HAZ pideexis;        || calcula existencias y graba
  |HAZ pide8y9;        || calcula existencias y graba
  |CIERRA #1;

  |HAZ explota;         || calcula la explotacion

  |ABRE #2;
  |BUCLE cabasitu4;     || calcula resto de cuentas
  |CIERRA #2;

  |CIERRA #5;
  |CIERRA #4;
  |CIERRA #3;
  |CIERRA #410;
  |CIERRA #901;
|SIGUE; || ==================================================================
  |DESCARGA_DEF #901;

 |SI #0#13 = "S";
   |ABRE #30;
   #30#2 = empre;
   #30#3 = ejerc;
   |LEE 011#30=;
   |CIERRA #30;
 |FINSI;

 |SI aparam = "Chequeo"; |O aparam = "PDF";
     enActivo = activ1; enActiv2 = activo;
     enPasivo = pasiv1; enPasiv2 = pasivo;
 |SINO;
   |BUCLE cabasitu1;
   |HAZ final;
   |PIEINF;
 |FINSI;

 |SI aparam ! "PDF";
   |DELINDEX #2;
   |DELINDEX #3;
   |DELINDEX #4;
   |DELINDEX #5;
 |FINSI;
|FPRC;

|PROCESO Borrar;
    activo = 0;
    pasivo = 0;
    activ1 = 0;
    pasiv1 = 0;
    explo = 0;
    expl1 = 0;
    debe1 = 0;
    haber1 = 0;
    sw = 0;
    agrupaci = 0;
    epigrafe = 0;
    sw_epi = 0;
|FPRC;

|PROCESO PorSesiones;
   eaDepSes = #110#1;
   eaDesDep = #110#7;

   alfa1 = #110#6; |QBF alfa1;
   |SI alfa1 ! "";
       eaFicbal  = alfa1;
       |NOME_DAT #1 eaFicbal;
       |HAZ HagoTodo;
   |FINSI;
|FPRC;

|DEFBUCLE PorSesiones;
 #110#1;
 ;
 enNumSes, "  ";
 enNumSes, "zz";
 ;
 NULL, PorSesiones;
|FIN;

|PROCESO impre; |TIPO 3;

  |HAZ antesque;
  |SI #0#17 = "S"; |HAZ conexi; |FINSI;
  |SI aparam ! "PDF"; |HAZ conexi8y9; |FINSI;

  nLongT = #0#5;
  |SI #0#4 = "N";
      |SI dig3 = 1; nLongT = dig4; nivel = 1; |FINSI;
      |SI dig3 = 2; nLongT = dig5; nivel = 2; |FINSI;
      |SI dig3 = 3; nLongT = dig6; nivel = 3; |FINSI;
      |SI dig3 = 4; nLongT = dig7; nivel = 4; |FINSI;
      |SI dig3 = 5; nLongT = dig8; nivel = 5; |FINSI;
      |SI dig3 = 6; nLongT = dig9; nivel = 6; |FINSI;
  |SINO;
      nivel = 0;
      |SI nLongT = dig4; nivel = 1; |FINSI;
      |SI nLongT = dig5; nivel = 2; |FINSI;
      |SI nLongT = dig6; nivel = 3; |FINSI;
      |SI nLongT = dig7; nivel = 4; |FINSI;
      |SI nLongT = dig8; nivel = 5; |FINSI;
      |SI nLongT = dig9; nivel = 6; |FINSI;
  |FINSI;

  nBalNum = #0#26;
  aFichero = ("8t" + Usuario) % 108;
  |NOME_DAT #3 aFichero; |DELINDEX #3;

  aFichero = ("8e" + Usuario) % 108;
  |NOME_DAT #4 aFichero; |DELINDEX #4;

  aFichero = ("8c" + Usuario) % 108;
  |NOME_DAT #5 aFichero; |DELINDEX #5;

  aFichero = ("8f" + Usuario) % 108;
  |NOME_DAT #2 aFichero;|DELINDEX #2;

  |SI aparam ! "Chequeo"; |Y aparam ! "PDF";
     |SI aparam ! ""; |Y aparam ! "fech";
         Impresora = eaNomImp + "?"+ eaUnidadBTmp + "\tmp1,,,?";
         |SI Compara = 1;
             Impresora = Impresora + "Bal.Sit.Comp";
          |SINO;
             Impresora = Impresora + "Bal.Sit.";
          |FINSI;
          |SI enBasImp ! 0;
              |IMPRE -1;
              |SI FSalida ! 0;
                  Impresora = Impresora - eaNomImp;
                  |IMPRE 0;
              |FINSI;
          |SINO;
              |IMPRE 0;
          |FINSI;
      |SINO;
          Impresora = "Crystal Reports";
          |IMPRE 0;
      |FINSI;
      |SI FSalida ! 0;  |ACABA;  |FINSI;

      |HAZ los_informes;
      |SI #0#13 = "N";
          informe = que_i1;
      |SINO;
          informe = que_i2;
      |FINSI;

      |SI Impresora = "CrystalReport";
         enSwComp = 0;
         informe = "cc" + (informe % 0306);
         aAlfa = informe % -301;
         |SI aAlfa = "c";
            enSwComp = 1;
            informe = (informe % 0105) + "0" + (informe % 702);
         |FINSI;
      |FINSI;

      |INFOR informe;
      |SI FSalida ! 0;
          |SI nSwMen = 0; |MENAV "     Error en informe."; |FINSI;
          |FINIMP;
          |ACABA;
      |FINSI;
  |FINSI;

  |SI enMasDep = 0; |O aparam = "Chequeo"; |O aparam = "PDF";
      |HAZ HagoTodo;
  |SINO;
      |BUCLE PorSesiones;
       eaDepSes = "TODOS";
  |FINSI;

  |SI aparam ! "Chequeo"; |Y aparam ! "PDF";
      |FININF;
      |FINIMP;
  |FINSI;

|SI aparam ! "PDF";
  |DELINDEX #2;
  |DELINDEX #3;
  |DELINDEX #4;
  |DELINDEX #5;
|FINSI;
|FPRC;


|PROCESO final;
  sw_inf = 5; |PRINT; |PRINT;
  sw_inf = 1;
  |SI tipo = "A";
      variable = "                        T O T A L   A C T I V O:";
      canti  = activ1;
      canti2 = activo;
      doh = "PASIVO";
  |SINO;
      variable = "                        T O T A L   P A S I V O:";
      canti  = pasiv1;
      canti2 = pasivo;
  |FINSI;
  |PRINT;
|FPRC;

|PROGRAMA;
   aparam = PARAMETRO; |QBF aparam;
   nSwMen = 1;
   |SI aparam = ""; |O aparam = "fech";
       nSwMen = 0;
       |HAZ inicio;
       |HAZ eVarEmpresa;
       |SI enEjercicio < 2008;
           |SI aparam ! "Chequeo"; |MENAV "    Informe para la Estructuras del 2008, no factible en este Ejercicio"; |FINSI;
           |ACABA;
       |FINSI;
   |FINSI;

   anu = enEjercicio;
   nBalance = 0; || Balance de Situacion
                 || eNum2008 = Codigo de las Cuentas de Mayor

   |HAZ LosPatrones;

   |C_M #0#32, 1, "S";
   |ABRE #1;
   #1#0 = "8           ";
   #1#1 = 1;
   |LEE 041#1];
   |SI FSalida ! 0;
       |C_M #0#32, 1, "N";
       #0#32 = "N";
   |FINSI;
   |CIERRA #1;

   |SI aparam = "Chequeo";
       |DDEFECTO #0;
       #0#0 = enCam0;
       #0#1 = enCam1;
       #0#5 = MaximoDigitos;
       #0#6 = enCam6;
       #0#7 = enCam7;
       #0#8 = enCam8;
       #0#18 = "N";
       #0#26 = enCam2;
       |HAZ impre;
       |ACABA;
   |FINSI;

   |SI aparam = "PDF";
       |DDEFECTO #0;
       #0#0  = nCTodo0;
       #0#1  = nCTodo1;
       #0#2  = aCTodo2;
       #0#3  = aCTodo3;
       #0#4  = aCTodo4;
       #0#5  = nCTodo5;
       #0#6  = nCTodo6;
       #0#7  = nCTodo7;
       #0#8  = nCTodo8;
       #0#13 = aCTodo13;
       #0#14 = nCTodo14;
       #0#15 = nCTodo15;
       #0#17 = "N";
       #0#18 = aCTodo18;
       #0#26 = nCTodo26;
       |HAZ descri;
       |SI nSwErrorBal = 1; |ACABA; |FINSI;

       #0#32 = "N";
       |PATH_DAT #1  dirfich0;
       |PATH_DAT #6  dirfich0;
       |PATH_DAT #7  dirfich0;
       |PATH_DAT #8  dirfich0;
       |PATH_DAT #431 dirfich0;
       |PATH_DAT #432 dirfich0;
       |HAZ impre;
       |ACABA;
   |FINSI;

   |SI aparam ! ""; |Y aparam ! "fech";
       |HAZ Legalia;
   |SINO;
      |CLS;
      |CABEZA "Balance de Situacion";

      |DFICE dirfich0;

      |ABRE #0;
      #0#28 = "A";
      |LEE 041#0=;
      |SI FSalida ! 0;
          |DDEFECTO #0;
          |GRABA 001#0c;
      |FINSI;
      #0#12 = ~;

      |PINPA #0#0;
      |PINDA #0#0;

      |ENDATOS #1#0;
      |SI FSalida = 0;
          |GRABA 001#0a;
      |FINSI;
      |CIERRA #0;
   |FINSI;
|FPRO;

|PROCESO Legalia;
    enMasDep = 0;
    nSwMen = 1;
    Comodin = PARAMETRO;
    Comodin = Comodin - ",C";
    |SI FSalida ! 0;
        Compara = 1;
    |SINO;
        Compara = 0;
    |FINSI;
    Comodin = Comodin - ",L";
    |SI FSalida ! 0;
        nSwLInv = 1;
    |SINO;
        nSwLInv = 0;
    |FINSI;
    Comodin = Comodin - ",";
    |SI FSalida ! 0;
        Calc = ((FSalida / 100) ! 0) + 99;
        Comodin1 = Comodin % 0101;
        Per1 = Comodin1; Comodin = Comodin - Comodin1;
        Per2 = Comodin;
    |FINSI;

    cod1 = eaLegEmp;
    cod2 = eaLegPer;
    emEmp = 1;  || Sin mensaje
    |HAZ leelo;
    |SI swerror ! 0;  |ACABA;   |FINSI;
    empre = eaLegEmp;
    ejerc = eaLegPer;

    |DBASE fich_alta; |QBF fich_alta;
    dirfich0 = fich_alta + "fich/" + eaLegEmp + eaLegPer + "/";

    |PATH_DAT #0  dirfich0;
    |PATH_DAT #1  dirfich0;
    |PATH_DAT #6  dirfich0;
    |PATH_DAT #7  dirfich0;
    |PATH_DAT #8  dirfich0;
    |PATH_DAT #22  dirfich0;
    |PATH_DAT #23  dirfich0;
    |PATH_DAT #110 dirfich0;
    |PATH_DAT #431 dirfich0;
    |PATH_DAT #432 dirfich0;

    |ABRE #8;
    |DDEFECTO #8;
    #8#0 = 1;
    |LEE 000#8=;
    |CIERRA #8;

    |ABRE #0;
    |DDEFECTO #0;
    #0#28 = "A";
    |LEE 041#0=;
    |SI FSalida ! 0;
        #0#6 = 1;
        |SI eNum2008 =  1; #0#26 =  1; |FINSI;
        |SI eNum2008 = 20; #0#26 = 20; |FINSI;
        |SI eNum2008 = 21; #0#26 = 21; |FINSI;
        |GRABA 001#0n;
    |FINSI;
    |CIERRA #0;
    |SI #0#6 = 0; #0#6 = 1; |FINSI;

    |HAZ descri;
    |SI nSwErrorBal = 1; |ACABA; |FINSI;

    |SI Compara = 1;
         #0#13 = "S";
         #0#14 = eaLegEmp;
         |SI eaLegPer > 0;
             #0#15 = eaLegPer - 1;
         |SINO;
             #0#15 = 9;
         |FINSI;
         |HAZ esbuena1;
         |SI swNoExiste = 1;
             Compara = 0;
             #0#13 = "N";
         |FINSI;
     |SINO;
         #0#13 = "N";
     |FINSI;

     #0#0  = Per1; #0#7 = Per1;
     #0#1  = Per2; #0#8 = Per2;
     Campo = 0; |HAZ pintames;
     Campo = 1; |HAZ pintames;
     Campo = 7; |HAZ pintames;
     Campo = 8; |HAZ pintames;
     #0#4   = "S";
     |SI dig4 [ 4; |Y dig4 ! 0; #0#5   = 1; |FINSI;
     |SI dig5 [ 4; |Y dig5 ! 0; #0#5   = 2; |FINSI;
     |SI dig6 [ 4; |Y dig6 ! 0; #0#5   = 3; |FINSI;
     |SI dig7 [ 4; |Y dig7 ! 0; #0#5   = 4; |FINSI;
     |SI dig8 [ 4; |Y dig8 ! 0; #0#5   = 5; |FINSI;
     |SI dig9 [ 4; |Y dig9 ! 0; #0#5   = 6; |FINSI;

     #0#17  = "N";
     #0#12  = efFecLeg;
     #0#18  = eaDatLeg;
     #0#32  = "N";
     aMonedaA = eaMoneda;
     |SI #0#13 = "N"; aMonedaC = aMonedaA; |FINSI;
     |HAZ impre;
|FPRC;
