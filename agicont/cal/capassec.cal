|FICHEROS;
   capassec #0;
|FIN;

|VARIABLES;
   &eaOrigen  = "";
   &eaNombre1 = "";
   &eaDSMAC   = "";
   &eaDirect  = "";

   aDestino = "";
   Comodin  = "";
   ip       = "";
   aResp    = "";

   aAlfa    = "";
|FIN;

|PROGRAMA;
   |ABRE #capassec;
   |LEE 101#capassec.p;
   |SI FSalida ! 0;
      |DDEFECTO #capassec;
      |GRABA 020#capassec.b;
   |FINSI;

   |PUSHV 0501 2380;
   |BLANCO 0915 1365;
   |CUADRO 1016 1264;
   |PINTA 1117, " ¨ Grabar en Disquete, Carpeta o Ninguna ? ";
   E_sup = 1;
   E_inf = "DCNdcn";
   aResp = 1160 ? 0;
   |POPV;

   ip = ""; |IP_REMOTO ip;

   |SI aResp = "N"; |O aResp = "n"; |ACABA; |FINSI;
   |SI aResp = "D"; |O aResp = "d";
      |PUSHV 0501 2380;
      |BLANCO 0815 1465;
      |CUADRO 0915 1364;
      |IP_REMOTO ip; |QBF ip;
      |PINTA 1015, "      Introduzca un disquete en su PC             ";
      |PINTA 1115, "               y pulse una tecla                  ";
      |PAUSA;

      aDestino = "/normas/"; |RMKDIR aDestino;
      aDestino = aDestino + eaNombre1;
      Comodin = eaOrigen; |QBF Comodin; Comodin = Comodin + eaNombre1;
      |SI ip ! "";
         |ENVIA_FICHERO Comodin, aDestino;
      |SINO;
         |COPIA_FICHERO Comodin, aDestino;
      |FINSI;
      Comodin = "COPY " + aDestino + " A:"; |RSYSTEM Comodin;
      Comodin = "DEL " + aDestino;          |RSYSTEM Comodin;
      |POPV;
   |FINSI;
   |SI aResp = "C"; |O aResp = "c";
      |PUSHV 0501 2380;
      #capassec#1 = eaNombre1;
      |PINPA #0#capassec; |PINDA #0#capassec;
      |ENDATOS #1#capassec;
      |SI FSalida = 0;
         |GRABA 020#capassec.a;
         |POPV;
         aDestino = #capassec#0; |QBF aDestino;
         aAlfa = aDestino % -101;
         |SI aAlfa ! "/"; |Y aAlfa ! "\";
            aAlfa = aDestino - "/";
            |SI FSalida ! 0;
               aDestino = aDestino - "/";
            |SINO;
               aAlfa = aDestino - "\";
               |SI FSalida ! 0;
                  aDestino = aDestino - "\";
               |SINO;
                  aDestino = aDestino - "/";
               |FINSI;
            |FINSI;
         |FINSI;
      |SINO;
         |POPV;
         |MENAV "    Proceso cancelado"; |ACABA;
      |FINSI;

      aDestino = aDestino + #capassec#1; |QBF aDestino;
      Comodin = eaOrigen; |QBF Comodin; Comodin = Comodin + eaNombre1;
      |SI ip ! "";
         |ENVIA_FICHERO Comodin, aDestino;
      |SINO;
         |COPIA_FICHERO Comodin, aDestino;
      |FINSI;
      |SI FSalida = 0;
         |MENAV "    Fichero correctamente ubicado en la carpeta seleccionada";
      |SINO;
         |MENAV "    Ha ocurrido un error al copiar el fichero al directorio destino";
      |FINSI;
   |FINSI;

   |LIBERA #capassec; |CIERRA #capassec;
|FPRO;
