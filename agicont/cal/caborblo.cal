|FICHEROS;
  caborblo #0;
  caentasl #1;    || apuntes Rapida lins.
  caentasc #2;    || apuntes Rapida cabs.
  cacuenta #3;    || plan contable

  camandia #10;   || diarios
  caivasop #6;
  caivarep #7;
  camancob #8;
  camanpag #9;

  caivarec #11;
  caivaref #12;
  caivasof #13;
  caivarea #14;

  caliconb #31;
|FIN;

|VARIABLES;
  &entrada = 1;
  mensa1 = "    ERROR ESTA FECHA NO CORRESPONDE AL EJERCICIO";
  mensa3 = "    NO EXISTE BLOQUE DE APUNTES ";

  cam = 0;
  alfa1 = "";
  nume1 = 0;
  bloque = "";
  fichero = "";
  estado = 0;

  desli = 0;
  hasli = 0;
  desdi = 0;
  hasdi = 0;
  desfe = @;
  hasfe = @;
  desdo = 0;
  hasdo = 0;

  ini = 0;
  fecalf = "";
  mes = "";
  mesfec = 0;
  mesac = 0;
  a = 0;
  b = 0;
  c = 0;
|FIN;

|INCLUYE acceso_i;

|| *************************************************************************
||                        DESDE CAMPOS DE PANTALLA
|| *************************************************************************

|PROCESO defecto; |TIPO 7;
 #0#4 = fec1; |PINTA #0#4;
 #0#8 = fec2; |PINTA #0#8;
 |PARA nume1 = 2; |SI nume1 [ 9; |HACIENDO nume1 = nume1 + 1;
       |C_M #0nume1,1,"N";
 |SIGUE;
|FPRC;

|PROCESO leebloq; |TIPO 6;
 |ABRE #2;
 #2#0 = #0#0;
 |LEE 001#2=;
 |SI FSalida ! 0;
     |MENAV mensa3;
     |ERROR;
 |SINO;
      #0#11 = #2#4; |PINTA #0#11;
      #0#12 = #2#5; |PINTA #0#12;
      #0#13 = #2#6; |PINTA #0#13;
 |FINSI;
 |CIERRA #2;
|FPRC;

|PROCESO mayor; |TIPO 0;
  alfa1 = #0Campo > " ";
  #0Campo = alfa1;
  |PINTA #0Campo;
|FPRC;

|PROCESO mayor1; |TIPO 0;
  |HAZ mayor;
  |SI #0#1 = "S";
      |PDEFECTO #0,2,10;
      #0#4 = fec1; #0#8 = fec2;
      |PARA nume1 = 2; |SI nume1 [ 9; |HACIENDO nume1 = nume1 + 1;
            |C_M #0nume1,1,"N"; |PINTA #0nume1;
      |SIGUE;
  |SINO;
      |PARA nume1 = 2; |SI nume1 [ 9; |HACIENDO nume1 = nume1 + 1;
            |C_M #0nume1,1,"S";
      |SIGUE;
  |FINSI;
|FPRC;

|PROCESO lineah; | TIPO 0;   ||  hasta Linea
  |SI #0#6 < #0#2;  |ERROR;  |FINSI;
|FPRC;

|PROCESO diarh; | TIPO 0;   ||  hasta diario
  |SI #0#7 < #0#3;  |ERROR;  |FINSI;
|FPRC;

|PROCESO fecha; | TIPO 0;   || Desde/hasta fecha
  |SI #0Campo < fec1; |O #0Campo > fec2;
      |MENAV mensa1;
      |ERROR;
  |FINSI;
  |SI Campo = 8;
      |SI #0#8 < #0#4; |ERROR; |FINSI;
  |FINSI;
|FPRC;

|PROCESO documh; | TIPO 0;   ||  hasta documento
  |SI #0#9 < #0#5;  |ERROR;  |FINSI;
|FPRC;

|| *************************************************************************
||                  ACTUALIZA LAS CUENTAS DE TRABAJO
|| *************************************************************************

|PROCESO a_actualiza;                || rutina 'a_cuentas'
  ini = dig1;
  fecalf = #1#3;
  mes = fecalf % 402;
  mesfec = mes;
  |SI ini = 1;
      mesac = mesfec;
  |SINO;
      mesac = (mesfec - ini) + 1;
      |SI mesac [ 0;
          mesac = (13 - ini) + mesfec;
      |FINSI;
  |FINSI;
  a = (mesac * 3) + 9;
  b = a + 1;
  c = a + 2;
  |SI #1#8 = "D";
      #3a = #3a - #1#9;
      #3#51 = #3#51 - #1#9;
  |SINO;
      #3b = #3b - #1#9;
      #3#52 = #3#52 - #1#9;
  |FINSI;
  #3c = #3a - #3b;
  #3#53 = #3#51 - #3#52;
|FPRC;

|PROCESO actua_cuen;
  |SI #1#2 = 0;
      |SI #1#8 = "D";
          #3#9 = #3#9 - #1#9;        || diario cero (saldos iniciales)
          #3#51 = #3#51 - #1#9;
      |SINO;
          #3#10 = #3#10 - #1#9;
          #3#52 = #3#52 - #1#9;
      |FINSI;
      #3#11 = #3#9 - #3#10;
      #3#53 = #3#51 - #3#52;
  |SINO;
      |SI #1#2 = 99;
          |SI #1#8 = "D";
              #3#48 = #3#48 - #1#9;       || diario 99 (saldos finales)
              #3#51 = #3#51 - #1#9;
          |SINO;
              #3#49 = #3#49 - #1#9;
              #3#52 = #3#52 - #1#9;
          |FINSI;
          #3#50 = #3#48 - #3#49;
          #3#53 = #3#51 - #3#52;
      |SINO;
          |HAZ a_actualiza;           || saldos del mes correspondiente
      |FINSI;
  |FINSI;
  |GRABA 020#3a;
  |LIBERA #3;
|FPRC;

|PROCESO actua_dia; || actualiza diario
  #10#0 = #1#2;
  |LEE 011#10=;
  |SI FSalida ! 0;  |ACABA;  |FINSI;
  |SI #1#8 = "D";
      #10#2 = #10#2 - #1#9;
  |SINO;
      #10#3 = #10#3 - #1#9;
  |FINSI;
  |GRABA 020#10a;
  |LIBERA #10;
|FPRC;

|PROCESO actua_cab; || actualiza diario
  |SI #1#8 = "D";
      #0#11 = #0#11 - #1#9;
  |SINO;
      #0#12 = #0#12 - #1#9;
  |FINSI;
  #0#13 = #0#11 - #0#12;
  |PINTA #0#11;
  |PINTA #0#12;
  |PINTA #0#13;
|FPRC;

|PROCESO fuera_cob;
|BORRA 020#8a;
|LIBERA #8;
|FPRC;

|DEFBUCLE cobros;
#8#1;
;
#7#0, #7#27, #7#2,   1;
#7#0, #7#27, #7#2, 999;
e;
NULL, fuera_cob;
|FIN;

|PROCESO fuera_pag;
|BORRA 020#9a;
|LIBERA #9;
|FPRC;

|DEFBUCLE pagos;
#9#1;
;
#6#0, #6#27, #6#2,   1;
#6#0, #6#27, #6#2, 999;
e;
NULL, fuera_pag;
|FIN;

|PROCESO a_ivar;
    #7#0  = #1#11;      || libro
    #7#2  = #1#12;      || factura
    #7#27 = #1#13;      || serie
    |LEE 101#7=;
    |SI FSalida = 0;

        #11#0 = #7#0;
        #11#1 = #7#27;
        #11#2 = #7#2;
        |LEE 101#11=;
        |SI FSalida = 0;
            |BORRA 020#11a;
        |FINSI;
        |LIBERA #11;

        #12#0 = #7#0;
        #12#1 = #7#27;
        #12#2 = #7#2;
        |LEE 101#12=;
        |SI FSalida = 0;
            |BORRA 020#12a;
        |FINSI;
        |LIBERA #12;

        #14#0 = #7#0;
        #14#1 = #7#2;
        #14#2 = #7#27;
        |LEE 101#14=;
        |SI FSalida = 0;
            |BORRA 020#14a;
        |FINSI;
        |LIBERA #14;


        |BUCLE cobros;
        |BORRA 020#7a;
    |FINSI;
    |LIBERA #7;
|FPRC;

|PROCESO a_ivas;
    #6#0  = #1#11;      || libro
    #6#2  = #1#12;      || factura
    #6#27 = #1#13;      || Serie
    |LEE 101#6=;
    |SI FSalida = 0;
        #13#0 = #6#0;
        #13#1 = #6#27;
        #13#2 = #6#2;
        |LEE 101#13=;
        |SI FSalida = 0;
            |BORRA 020#13a;
        |FINSI;
        |LIBERA #13;
        |BUCLE pagos;
        |BORRA 020#6a;
   |FINSI;
   |LIBERA #6;
|FPRC;

|PROCESO facturas;
 |SI #1#10 = "R";
     |HAZ a_ivar;
 |FINSI;
 |SI #1#10 = "S";
     |HAZ a_ivas;
 |FINSI;
|FPRC;

|| *************************************************************************

|PROCESO borrobloq;  || trabajo por cada lineas
  #3#0 = #1#4;
  #3#1 = #1#5;
  |LEE 011#3=;
  |SI FSalida = 0; |HAZ actua_cuen; |FINSI;
  |HAZ actua_dia;
  |HAZ actua_cab;
  |HAZ facturas;

  |BORRA 020#1a;  ||   borra l¡nea
|FPRC;

|DEFBUCLE borbloque;
 #1#1;
 2,3,21;
 #0#0, desli, desdi, desfe, desdo;
 #0#0, hasli, hasdi, hasfe, hasdo;
 e;
 NULL, borrobloq;
|FIN;

|PROCESO borrocont;     || contrapartidas
 |BORRA 020#31a;        || borra contrap.
|FPRC;

|DEFBUCLE borcontra;
 #31#1;
 ;
 #0#0, desli;
 #0#0, hasli;
 e;
 NULL, borrocont;
|FIN;

|| **************************************************************************

|PROCESO finbloque;
 |ABRE #2;
 #2#0 = #0#0;
 |LEE 001#2=;
 estado = FSalida;
 |SI cam = 0;
     |BORRA 020#2a;
     |DELINDEX #1;
 |SINO;
     |ABRE #1;
     |DDEFECTO #1;
     |LEE 001#1u;
     |CIERRA #1;
     #2#0 = #0#0;
     #2#1 = #1#2;
     #2#2 = #1#3;
     #2#3 = #1#21;
     #2#4 = #0#11;
     #2#5 = #0#12;
     #2#6 = #0#13;
     |SI estado = 0; |GRABA 020#2a; |FINSI;
     |SI estado ! 0; |GRABA 020#2n; |FINSI;
 |FINSI;
 |CIERRA #2;
|FPRC;

|PROCESO combloq;
 cam = 1;
 |ERROR10;
|FPRC;

|DEFBUCLE combloque;
 #1#1;
 ;
 #0#0,     1;
 #0#0, 99999;
 e;
 NULL, combloq;
|FIN;

|| **************************************************************************

|| ------------- Anulando los asientos --------------------

|PROCESO borrado; |TIPO 3;
 |SI #0#10 = "SI";
     bloque = #0#0;
     fichero = bloque;
     |QBF fichero;
     fichero = fichero + "zzzzzzzz";
     fichero = fichero % 107;
     fichero = fichero + "l";

     |NOME_DAT #1 fichero;

     |ABRE #3;
     |ABRE #6;
     |ABRE #7;
     |ABRE #10;
     |ABRE #11;
     |ABRE #12;
     |ABRE #13;
     |ABRE #14;
     desli = #0#2; hasli = #0#6;
     desdi = #0#3; hasdi = #0#7;
     desfe = #0#4; hasfe = #0#8;
     desdo = #0#5; hasdo = #0#9;

     |BUCLE borbloque;   || borra lineas
     |BUCLE borcontra;   || Borro contrapartidas
     cam = 0;
     |BUCLE combloque;   || Comprobar si queda algo;
     |HAZ finbloque;

     |CIERRA #14;
     |CIERRA #13;
     |CIERRA #12;
     |CIERRA #11;
     |CIERRA #10;
     |CIERRA #7;
     |CIERRA #6;
     |CIERRA #3;
 |FINSI;
|FPRC;

|| **************************************************************************
