ç  ||  el #5 y el #8 estan reservados para los 'INCLUYE'
  || /para 'cacuenta' y 'catradia' respect.

|FICHEROS;
    cactvpds #00;
    cactvctm #01,MANTE;  || temporal de seleccion
    camaasic #02;   || apuntes cabs.
    camaasil #03;   || apuntes lins.
    camanpdc #06;   || Confirmes cabs.
    camanpdl #07;   || Confirmes lins.
    cacuadre #09;   || control pagares y talones
    camandia #10;   || diarios
    cacuebal #11;   || cuentas balance
    camanpag #12;   || pagos
    caconcep #13;   || conceptos automaticos
    ca8m0002 #422;
|FIN;


|VARIABLES;
    &enDiario   = 0;
    &enContador = 0;

    nNivel = 0;
    nLong  = 0;
    aLong  = "";
    aAlfa  = "";
    import = 0;
    aMarca = "";

    CtaAnt = "";
    LinDirAnt = "";
    VtoAnt = "";
    aCuenta = "";
    i     = 0;
    swmar = 0;
    &entrada = 1;
    alfa1 = "";
    alfa2 = "";
    alfa3 = "";
    alfa4 = "";
    nume1 = 0;
    nume2 = 0;
    nume3 = 0;
    nume4 = 0;
    fech1 = @;
    fech2 = @;
    fech3 = @;
    fech4 = @;
    arteris = 0;
    swarteris = 0;
    mes_anyo = "";
    muestra = "";
    digito = 0;
    longi = 0;
    asie = 0;
    apun = 0;
    cuen = "";
    digi = 0;
    sign = "";
    ptas = 0;
    desc = "";
    punt = "";
    descrip = "";
    impo = 0;
    sw = 0;
    FEstado = 0;

    seleccio = 0;
    sw_tipo = 0;
    opcion = 0;
{-1}menu = "";
    menu1 = "2400";
    menu2 = "1";
    menu3 = "";
    menu4 = "RCS";
    menu5 = " Revisar ";
    menu6 = " Contabilizar ";
    menu7 = " Salir ";

     aAlfa1 = "";
     aAlfa2 = "";
     nSwConci = 0;
     aOtro = "";
     aEmisi = "";
     aVenci = "";

    alfa5 = "";
    alfa6 = "";
    nume5 = 0;
    nume6 = 0;
    nSwLaC = 0;
    aMuestraO = "";
    nPara1 = 0;
    &enEjerBal = 0;
|FIN;

|INCLUYE varnue_i;
|INCLUYE inicio_i;
|INCLUYE actcta_i;
|INCLUYE defec1_i;

|PROCESO Mayus; |TIPO 0;
    #0Campo = #0Campo > #0Campo;
    |PINTA #0Campo;
|FPRC;

|PROCESO Limite; |TIPO 0;
|SI #0#7 < fec1;|O #0#7 > fec2;
    |MENAV "    ATENCION!!! La fecha no corresponde al ejercicio contable ...";
    |ERROR;
|FINSI;
|FPRC;

|PROCESO Final816; |TIPO 7;
 |PTEC 816;
|FPRC;

**************************************************************************
          CALCULOS DESDE CAMPOS SELECCION
**************************************************************************

|PROCESO Tipo11; |TIPO 11;
  |SI FSalida = 9;
      |SI #1#9 = "*";
          #1#9 = " ";
      |SINO;
          #1#9 = "*";
      |FINSI;
      |PINTA #1#9;
      |GRABA 020#1a;
  |FINSI;
|FPRC;

|PROCESO Limite1; |TIPO 0;
|SI #1#10 < fec1;|O #1#10 > fec2;
    |MENAV "    ATENCION!!! La fecha no corresponde al ejercicio contable ...";
    |ERROR;
|FINSI;
|FPRC;

|PROCESO GrabaTemporal;
     |SI #6#8 ! "S"; |Y #6#8 ! "D";  |ACABA;  |FINSI;
     |SI #7#6 < #0#2;  |O #7#6 > #0#3;  |ACABA;  |FINSI;
     |SI #7#11 = "S";                   |ACABA;  |FINSI;

     |DDEFECTO #1;
     #1#0 = #7#0;         || nro.confirme
     #1#1 = #7#1;         || nro.confirme linea
     #1#2 = #7#8;         || cuenta proveedor
     #1#3 = #7#4;         || Serie
     #1#4 = #7#3;         || Numero de Orden
     #1#5 = #7#5;         || Numero de Pago
     #1#6 = #6#1;         || fecha emision
     #1#7 = #7#6;         || fecha vto
     #1#8 = #7#7;         || importe
     |SI swmar = 0;       || Javier 03-05-99
         #1#9 = " ";    || marcado NO
     |SINO;
         #1#9 = "*";
     |FINSI;
     #1#10 = #6#14;
     #1#11 = #6#7;
     #1#12 = #0#7;
     || #1#13 = #7#16;

     |GRABA 020#1n;
     |LIBERA #1;
     seleccio = 1;
|FPRC;

|DEFBUCLE camanpdc;
     #6#1;
     12, 14, 7;
     #0#0, "S", #0#13, #0#11;
     #0#1, "S", #0#14, #0#12;
     ;
     NULL, NULL, GrabaTemporal;
|FIN;

|PROCESO ClaveBaja;
    #1#0 = 1;
    #1#1 = 1;
    #1#2 = "            ";
|FPRC;

|PROCESO ClaveAlta;
    #1#0 = 99999;
    #1#1 = 999;
    #1#2 = "zzzzzzzzzzzz";
|FPRC;

|PROCESO Temporal;
     alfa1 = Usuario; |QBF alfa1;    |NOME_DAT #1 alfa1; |DELINDEX #1;

     |ABRE #1;       || temporal seleccion
     |BUCLE camanpdc;
     |CIERRA #1;

     opcion = 2;
     |SI seleccio = 0;  |ACABA;  |FINSI;

     |PUSHV 0501 2380;
     |PARA; |SI; |HACIENDO;
            |ENTLINEAL #1#1, 2, 4, 21, 1, ClaveBaja, ClaveAlta;
            |MENU menu;
            opcion = FSalida;
            |SI opcion ! 1;  |SAL;  |FINSI;
     |SIGUE;
     |POPV;
|FPRC;

|PROCESO Descrip;
     swmar = 0;
     |CONFI "2400N¨Desea Marcado Automatico de Relaciones de pagos domiciliados ? ";
     |SI FSalida = 0; swmar = 1; |FINSI;
|FPRC;

|PROCESO Descripcion;
     descrip = #0#6;   |QBF descrip;

     alfa1 = "";

     |SI #0#16 = "S";  alfa1 = ("00000" + #6#0) % -105;  |FINSI;                                      || Codigo Pagare Banco
     |SI #0#17 = "S";  alfa1 = (alfa1 +  " " + #7#6);    |FINSI;

     descrip = (descrip + " " + alfa1);
|FPRC;

|PROCESO Cabecera;       || actualiza cabeceras
     |SI #3#09 = 0;  |ACABA;  |FINSI;

     |DDEFECTO #2;
     #2#00 = #3#0; ||  diario
     #2#01 = #3#1; ||  fecha
     #2#02 = #3#2; ||  documento
     #2#03 = #3#2; ||  asiento
     |LEE 101#2=;
     FEstado = FSalida;
     |SI #3#08 = "D";
         #2#04 = #2#04 + #3#09;
         |SI #2#09 = 0; #2#08 = #3#4; #2#09 = #3#5; |FINSI;  || contrp.HABER
     |FINSI;
     |SI #3#08 = "H";
         #2#05 = #2#05 + #3#09;
         |SI #2#11 = 0; #2#10 = #3#4; #2#11 = #3#5; |FINSI;  || contrp.DEBE
     |FINSI;
     #2#6 = #2#4 - #2#5;      || saldo
     |SI FEstado = 0;  |GRABA 020#2a;  |FINSI;
     |SI FEstado ! 0;  |GRABA 020#2n;  |FINSI;
     |LIBERA #2;
|FPRC;

|PROCESO actualiz3;
     |ABRE #10;
     #10#0 = #3#0;
     |LEE 121#10=;
     |SI FSalida = 0;
         |SI #3#8 = "D";  #10#2 = #10#2 + #3#9;  |FINSI;
         |SI #3#8 = "H";  #10#3 = #10#3 + #3#9;  |FINSI;
         |GRABA 020#10a;
     |FINSI;
     |LIBERA #10;
     |CIERRA #10;
|FPRC;

|PROCESO GrabaApunt2;
     #0#08 = #6#0;   |PINTA #0#08;
     #0#09 = asie;   |PINTA #0#09;
     #0#10 = apun;   |PINTA #0#10;

     |DDEFECTO #3;
     #3#00 = #0#4;   || diario
     #3#01 = #1#12;  || fecha
     #3#02 = asie;   || asiento
     #3#03 = apun;   || apunte
     #3#04 = cuen;   || cuenta
     #3#05 = digi;   || digito
     #3#06 = #0#5;   || concepto
     #3#07 = desc;   || descripcion
     #3#08 = sign;   || signo
     #3#09 = ptas;   || importe
     #3#18 = punt;   || punteado S/N
     |SI #3#8 = "D";
         #3#16 = #3#4;
         #3#17 = #3#5;
         #3#10 = "";
         #3#11 = 0;
     |SINO;
         #3#16 = "";
         #3#17 = 0;
         #3#10 = #3#4;
         #3#11 = #3#5;
     |FINSI;
     |GRABA 020#3n;

     |HAZ Cabecera;           || cabecera apunte

     ressum = #3#9;
     |HAZ actualiz1;          || cuenta
     |HAZ actualiz3;          || diario
     opera = "A";
     |HAZ altatra;            || subniveles
|FPRC;

|PROCESO filtro;         || construye la contrapartida individual
  cuen = #7#8; |HAZ SacaNivel;
  |SI nNivel = 1;  nume1 = dig4;  |FINSI;
  |SI nNivel = 2;  nume1 = dig5;  |FINSI;
  |SI nNivel = 3;  nume1 = dig6;  |FINSI;
  |SI nNivel = 4;  nume1 = dig7;  |FINSI;
  |SI nNivel = 5;  nume1 = dig8;  |FINSI;
  |SI nNivel = 6;  nume1 = dig9;  |FINSI;

  aMuestraO = muestra;

|SI nSwLaC = 1;
    alfa5 = muestra % 0;
    nume5 = alfa5;
    alfa5 = "";
    |PARA nPara1 = 1; |SI nPara1 [ nume5; |HACIENDO nPara1 = nPara1 + 1;
          nume6 = (nPara1 * 100) + 1;
          alfa6 = muestra % nume6;
          |SI alfa6 = "C";
              alfa6 = #7#8 % nume6;
          |FINSI;
          alfa5 = alfa5 + alfa6;
    |SIGUE;

    muestra = alfa5;
|FINSI;

  nume2 = nume1 - (longi - 1);
  |SI swarteris = 0;
      nume2 = (100 * longi) + nume2;
      alfa1 = #7#8 % nume2;
      alfa2 = muestra + alfa1;             || si es "*"
  |SINO;
      nume2 = nume2 - 4;
      |SI nume2 < 0;
          |MENAV "    Cuenta 'Confirmes' del Control Pagares con longitud fuera de nivel ...";
          nume2 = nume2 + 4;
          alfa1 = "0" * nume2;
          alfa2 = muestra + alfa1;
      |SINO;
          |SI nume2 = 0;  alfa1 = "";              |FINSI;   || cuenta ajustada
          |SI nume2 > 0;  alfa1 = "0" * nume2;     |FINSI;   || rellena de ceros
           mes_anyo = (#6#1 % 402) + (#6#1 % -102);
          alfa2 = muestra + alfa1 + mes_anyo;  || si es "%"
      |FINSI;
  |FINSI;
  muestra = aMuestraO;
|FPRC;


|PROCESO GrabaApunte;   || controla la grabacion de apuntes
     seleccio = 1;

     |HAZ Descripcion;
     |SI apun = 1;
         enDiario = #0#4;
         |HAZ contador;
         asie = enContador;
     |FINSI;

     apun = apun + 10;

     |HAZ filtro;
     cuen = alfa2; |HAZ SacaNivel
     digi = nNivel;
     || sign = "H";
     sign = "D";
     ptas = #7#7;
     #camanpag#0 = #camanpdl#2;
     #camanpag#2 = #camanpdl#4;
     #camanpag#1 = #camanpdl#3;
     #camanpag#3 = #camanpdl#5;
     |LEE 010#camanpag.=;
     |SI FSalida = 0;
        desc = descrip; |QBF desc;
        desc = desc + " " + #camanpag#28; |QBF desc;
     |SINO;
        desc = descrip;
     |FINSI;
     |HAZ GrabaApunt2;
     impo = impo + #7#7;

     #7#11 = "S";
     |GRABA 020#7a;
     |LIBERA #7;
|FPRC;

|PROCESO Contabiliza;
     |SI #1#10 ! aCuenta;  |Y apun ! 0;
         |HAZ Final;
         impo = 0;
         apun = 1;
     |FINSI;

     aCuenta = #1#10;
     |SI apun = 0;  apun = 1;  |FINSI;

     #7#0 = #1#0;
     #7#1 = #1#1;
     |LEE 121#7=;
     |SI FSalida = 0;
         #6#0 = #7#0;
         |LEE 040#6=;
         |SI FSalida ! 0;  |DDEFECTO #6;  |FINSI;
         |HAZ GrabaApunte;
         #6#0 = #7#0;
         |LEE 101#6=;
         |SI FSalida = 0;
            #6#13 = "S";
            |GRABA 020#6a; |LIBERA #6;
         |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE cactvctm;
     #1#2;
     9;
     "            ",     1, 000, "*";      || ordena por proveedor+pagare
     "zzzzzzzzzzzz", 99999, 999, "*";
     ;
     NULL, Contabiliza;
|FIN;

|PROCESO GrabaApunt3;
     seleccio = 1;

     |HAZ Descripcion;
     |SI apun = 1;
         enDiario = #0#4;
         |HAZ contador;
         asie = enContador;
     |FINSI;
     |HAZ filtro;
     cuen = alfa2; |HAZ SacaNivel;
     digi = nNivel;
     || sign = "H";
     sign = "D";
     ptas = #7#7;
     #camanpag#0 = #camanpdl#2;
     #camanpag#2 = #camanpdl#4;
     #camanpag#1 = #camanpdl#3;
     #camanpag#3 = #camanpdl#5;
     |LEE 010#camanpag.=;
     |SI FSalida = 0;
        desc = descrip; |QBF desc;
        desc = desc + " " + #camanpag#28; |QBF desc;
     |SINO;
        desc = descrip;
     |FINSI;

     #0#08 = #6#0;   |PINTA #0#08;
     #0#09 = asie;   |PINTA #0#09;
     #0#10 = apun;   |PINTA #0#10;
     apun = apun + 10;

     |DDEFECTO #3;
     #3#00 = #0#4;   || diario
     #3#01 = #1#12;  || fecha
     #3#02 = asie;   || asiento
     #3#03 = apun;   || apunte
     #3#04 = cuen;   || cuenta
     #3#05 = digi;   || digito
     #3#06 = #0#5;   || concepto
     #3#07 = desc;   || descripcion
     #3#08 = sign;   || signo
     #3#09 = import; || importe
     #3#18 = punt;   || punteado S/N
     |SI #3#8 = "D";
         #3#16 = #3#4;
         #3#17 = #3#5;
         #3#10 = "";
         #3#11 = 0;
     |SINO;
         #3#16 = "";
         #3#17 = 0;
         #3#10 = #3#4;
         #3#11 = #3#5;
     |FINSI;
     |GRABA 020#3n;

     |HAZ Cabecera;           || cabecera apunte

     ressum = #3#9;
     |HAZ actualiz1;          || cuenta
     |HAZ actualiz3;          || diario
     opera = "A";
     |HAZ altatra;            || subniveles

|FPRC;

|PROCESO Contabiliza1;
     |SI #1#2 ! CtaAnt; |O #1#7 ! VtoAnt; |O #1#13 ! LinDirAnt;
        |SI import ! 0;
           |HAZ GrabaApunt3;
        |FINSI;
        import = 0;
        CtaAnt = #1#2;
        VtoAnt = #1#7;
        LinDirAnt = #1#13;
     |FINSI;

     #7#0 = #1#0;
     #7#1 = #1#1;
     |LEE 121#7=;
     |SI FSalida = 0;
         #6#0 = #7#0;
         |LEE 040#6=;
         |SI FSalida ! 0;  |DDEFECTO #6;  |FINSI;
         import = import + #7#7;
         impo = impo + #7#7;
         #7#11 = "S";
         |GRABA 020#7a;
         |LIBERA #7;
     |FINSI;
|FPRC;

|DEFBUCLE cactvctm1;
     #1#3;
     9;
     "            ", "01.01.1900",     1, 000, "*";      || ordena por proveedor+Fecha Vto+Pagare
     "zzzzzzzzzzzz", "31.12.2999", 99999, 999, "*";
     ;
     NULL, Contabiliza1;
|FIN;

|PROCESO Digito;         || saca el digito de la contrapartida del control
     sw   = 0;
     digito = 0;  || inicializa el digito
     |SI longi = dig4; |Y dig4 ! 0; sw = 1; digito = 1; |FINSI;
     |SI longi = dig5; |Y dig5 ! 0; sw = 1; digito = 2; |FINSI;
     |SI longi = dig6; |Y dig6 ! 0; sw = 1; digito = 3; |FINSI;
     |SI longi = dig7; |Y dig7 ! 0; sw = 1; digito = 4; |FINSI;
     |SI longi = dig8; |Y dig8 ! 0; sw = 1; digito = 5; |FINSI;
     |SI longi = dig9; |Y dig9 ! 0; sw = 1; digito = 6; |FINSI;
     |SI sw = 0;
         |MENAV "    Contrapartida de Pagos Fuera de Nivel!!! Proceso Abortado ...";
     |FINSI;
|FPRC;

|PROCESO Arteris;     || desglosa la contrapartida del control
     alfa1 = #9#21;  |QBF alfa1;
     alfa2 = alfa1 % 0;
     longi = alfa2;            || 'longi' sirve despues (no tocar)
     nume3 = (longi * 100) + 1;
     alfa2 = alfa1 % nume3;
     |SI alfa2 = "*";|O alfa2 = "%";
         nume3 = 99 + longi;
         muestra = alfa1 % nume3;
         arteris = 1;
         swarteris = 0;
         |SI alfa2 = "%";  swarteris = 1;  |FINSI;
     |SINO;
         |HAZ Digito;
         |SI sw = 0;   arteris = 2;  |FINSI; || contrapartida fuera de nivel!!!
         |SI sw ! 0;   arteris = 0;  |FINSI;
          muestra = alfa1;
     |FINSI;

  nSwLaC = 0;
  |PARA nume4 = 1; |SI nume4 [ longi; |HACIENDO nume4 = nume4 + 1;
        nume1 = (nume4 * 100) + 1;
        alfa3 = alfa1 % nume1;
        |SI alfa3 = "C";  nSwLaC = 1; |FINSI;
  |SIGUE;

|FPRC;

|PROCESO Cuenta;    || comprueba si la cuenta esta dada de alta
     |ABRE #5;
     #5#0 = cuen;
     #5#1 = digi;
     |LEE 001#5=;
     |SI FSalida ! 0;
         |DDEFECTO #5;
         #5#0 = cuen;
         #5#1 = digi;
         |HAZ defect1;
    |PUSHV 0501 2380;
    |PINPA #0#5;
    |PINDA #0#5;
    |ENDATOS #1#5;
    |SI FSalida = 0;
        |GRABA 020#5n;
    |FINSI;
    |POPV;
|FINSI;
|CIERRA #5;
|FPRC;

|PROCESO camaasil;
     |DDEFECTO #13; #13#0 = #3#6;  |LEE 000#13=;
     aAlfa1 = #3#7;      |QBF aAlfa1;
     aAlfa2 = #13#1;     |QBF aAlfa2;

     aEmisi = aAlfa1 - aAlfa2;     || descripcion emision
     |QBT aEmisi;

     aAlfa1 = desc;      |QBF aAlfa1;
     aAlfa2 = #0#6;      |QBF aAlfa2;

     aVenci = aAlfa1 - aAlfa2;     || descripcion vencimiento
     |QBT aVenci;

     |SI aEmisi = aVenci;
          #3#18 = "û";
          |GRABA 020#3a;
          nSwConci = 1;
          |ERROR10;
     |FINSI;
     |LIBERA #3;
|FPRC;

|DEFBUCLE camaasil;
     #3#3;
     8, 9, 18;
     cuen, digi, fec1,      0, aOtro, ptas, " ";
     cuen, digi, fec2, 999999, aOtro, ptas, " ";
     be;
     NULL, camaasil;
|FIN;

|PROCESO BuscaOtra;
     nSwConci = 0;
     |SI sign = "D";     aOtro = "H";   |FINSI;
     |SI sign = "H";     aOtro = "D";   |FINSI;
     |BUCLE camaasil;
     |SI nSwConci = 0;
          aAlfa1 = "    Cuenta [" + cuen + "] sin correspondencia para conciliar ...";
          |MENAV aAlfa1;
     |FINSI;
|FPRC;

|PROCESO Final;     || graba apunte cuando la contrapartida es unica
     apun = apun + 10;
     |SI arteris = 0; cuen = #9#21;    |FINSI;
     |SI arteris ! 0; cuen = aCuenta;  |FINSI;

     alfa1 = cuen;  |QBF alfa1;
     alfa2 = alfa1 % 0;
     longi = alfa2;            || 'longi' sirve despues (no tocar)
     |HAZ Digito;

     digi = digito;
  ||   sign = "D";
     sign = "H";
     ptas = impo;
     desc = #0#6;
     punt = " ";
     |SI #0#15 = "S";     punt = "û";    |HAZ BuscaOtra;   |FINSI;
     |QBF desc;
     alfa1 = "";
     |SI #0#16 = "S";  alfa1 = ("00000" + #6#0) % -105;  |FINSI;                                      || Codigo Pagare Banco
     desc = desc + " " + alfa1;
     desc = desc + " " + #0#7;
     |HAZ Cuenta;
     |HAZ GrabaApunt2;
|FPRC;

|PROCESO SubNiveles;     || actualiza subniveles de cuentas
     |SI modcon = 0;
         |ABRE #8;
         |DDEFECTO #8;
             #8#0 = codcon;
         |LEE 001#8=;
         |SI FSalida = 0;
             |BORRA 020#8a;
         |FINSI;
         |CIERRA #8;
     |SINO;
         |SUB_EJECUTA "camaapua.run";
     |FINSI;
|FPRC;

|PROCESO MiraLineas;
   |SI #camanpdl#11 = "N";
      aMarca = "N"; |ERROR10;
   |FINSI;
|FPRC;

|DEFBUCLE MiraLineas;
#camanpdl#1;
;
#camanpdc#0, 001;
#camanpdc#0, 999;
;
NULL,MiraLineas;
|FIN;

|PROCESO MarcaCont;
   aMarca = "S";
   |BUCLE MiraLineas;
   #camanpdc#9 = aMarca;
   |GRABA 020#camanpdc.a;
|FPRC;

|DEFBUCLE MarcaCont;
#camanpdc#1;
;
#cactvpds#0;
#cactvpds#1;
be;
NULL,MarcaCont;
|FIN;

|PROCESO Tipo3; |TIPO 3;
     |SI #30#26 < 80;
         enEjercicio = 2000 + #30#26;
     |SINO;
         enEjercicio = 1900 + #30#26;
     |FINSI;

     |ABRE #9;
     |LEE 000#9p;
     |SI FSalida ! 0;
         |MENAV "    ATENCION!!! Control de Pageres y Talones sin definir ...";
         |CIERRA #9;
         |ACABA;
     |FINSI;
     |CIERRA #9;

     |HAZ Descrip;                            || Javier 03-05-99

     |HAZ Temporal;

     |SI opcion ! 2;
         |ABRE #1;  |CIERRA #1;  |DELINDEX #1;
         |ACABA;
     |FINSI;

     |SI seleccio = 0;
         |ABRE #1;  |CIERRA #1;  |DELINDEX #1;
         |MENAV "    Seleccion vacia ...";
         |ACABA;
     |FINSI;

     |ABRE #2;       || apuntes cabs.
     |ABRE #3;       || apuntes lins.
     |ABRE #5;       || cuentas
     |ABRE #12;      || pagos
     |ABRE #13;      || conceptos

     seleccio = 0;
     apun     = 0;

     |HAZ Arteris;

     |SI arteris ! 2;          || error en el calculo 'digito'
         |ABRE #6;
         |ABRE #7;
         |SI #cactvpds#19 = "N";
            |BUCLE cactvctm;
         |SINO;
            apun = 1;
            |BUCLE cactvctm1;
            |HAZ GrabaApunt3;
            aCuenta = #camanpdc#14;
         |FINSI;
         |CIERRA #6;
         |CIERRA #7;
         |SI impo ! 0;
             |HAZ Final;  || contrapartida englobada
             impo = 0;
         |FINSI;
    |FINSI;

    |SI seleccio = 0;
        |MENAV "    Seleccion sin marcas ...";
    |FINSI;

    |CIERRA #2;
    |CIERRA #3;
    |CIERRA #5;
    |CIERRA #12;
    |CIERRA #13;
    |DELINDEX #1;

    |HAZ SubNiveles;
    |BUCLE MarcaCont;
|FPRC;

|PROCESO SacaNivel;
   aAlfa = cuen; |QBF aAlfa;
   aLong = aAlfa % 0; nLong = aLong;
   |SI #canempre#14 = nLong; nNivel = 1; |FINSI;
   |SI #canempre#15 = nLong; nNivel = 2; |FINSI;
   |SI #canempre#16 = nLong; nNivel = 3; |FINSI;
   |SI #canempre#17 = nLong; nNivel = 4; |FINSI;
   |SI #canempre#18 = nLong; nNivel = 5; |FINSI;
   |SI #canempre#19 = nLong; nNivel = 6; |FINSI;
|FPRC;
