|FICHEROS;

  camaccw1 #599;

  camacc01 #501;
  camacc02 #502,MANTE;
  camacc12 #512,MANTE;

  caivaasi #200;
|FIN;

|VARIABLES;
  nLinea  = 0;
  nDLinea = 0;
  nHLinea = 0;
  nNumero = 0;
  aAlfa   = "";
  aAlfa1  = "";
  aAlfa2  = "";
  fFecha1 = @;
  nNume1  = 0;
  nNume2  = 0;
  nNume3  = 0;
  nTp     = 0;
  nModo   = 0;

  nSuma1    = 0;
  nSuma2    = 0;
  nSuma3    = 0;
  nNumBase  = 0;
  nLineaUlt = 0;
  nSwOp     = 0;
  &ePrograma = 0;

  nCampo1 = 0;
  nCampo2 = 0;
  nCampo3 = 0;
  nBase   = 0;
  nCuota  = 0;
  varmes  = 0;
|FIN;

|INCLUYE varcaj_i;
|INCLUYE varnue_i;

|PROCESO PrimeraSuma;
  #599#16 = #599#16 + #512#5;
  #599#18 = #599#18 + #512#8;
  #599#19 = #599#19 + #512#6;
  #599#20 = #599#20 + #512#7;
  |LIBERA #512;
|FPRC;

|DEFBUCLE PrimeraSuma;
  #512#1;
  ;
  #599#13, #599#0, nNumero, 01;
  #599#13, #599#0, nNumero, 99;
  be;
  NULL, PrimeraSuma;
|FIN;
|| ///////////////////////////

|CALCULO Tipo12CC12; |TIPO 12;
  || Para que no pregunte conforme
|FCAL;

|CALCULO Tipo11CC12; |TIPO 11;
|FCAL;

|CALCULO Tipo0CC12; |TIPO 0;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 1; |Y #599#16 = #599#10;
         aAlfa = "    NFactura cobrada. Desea dar de alta otra linea";
         |SI #599#13 = "S";
             aAlfa = "    NFactura pagada. Desea dar de alta otra linea";
         |FINSI;
         |CONFI aAlfa;
         |SI FSalida ! 0;
             |ERROR;
         |FINSI;
     |FINSI;

     |SI nModo = 1; |Y #512#2 = 1;
         #512#3 = #599#4; |PINTA #512#3;
         |HAZ CalPeriodo12;
     |FINSI;
|FCAL;

|CALCULO Tipo2CC12; |TIPO 2;
      #599#16 = #599#16 +. #512#5;
      #599#18 = #599#18 +. #512#8;
      #599#19 = #599#19 +. #512#6;
      #599#20 = #599#20 +. #512#7;
      |PINTA 2221, #599#16;
      |PINTA 2237, #599#18;
|FCAL;

|CALCULO Tipo0CC1203; |TIPO 0;   || Fecha

   |C_M #512Campo,0,aAlfa;
   |SI aAlfa = "N"; |ACABA; |FINSI;

   |SI #512#3 < #599#4;
       aAlfa = "    NError. Fecha del cobro inferior a la Fecha de la Factura. Continuar";
       |SI #599#13 = "S";
           aAlfa = "    NError. Fecha del pago inferior a la Fecha de la Factura. Continuar";
       |FINSI;
       |CONFI aAlfa;
       |SI FSalida ! 0;
           |ERROR;
           |ACABA;
       |FINSI;
   |FINSI;

   fFecha1 = Contenido;
   |SI fFecha1 ! #512#3;
       |HAZ CalPeriodo12;
   |FINSI;
|FCAL;

|PROCESO CalPeriodo12;
   |ABRE #200;
   |DDEFECTO #200;
   |LEE 041#200p;
   |CIERRA #200;

   aAlfa1 = #512#3;
   aAlfa1 = aAlfa1 % 0402; varmes = aAlfa1;
   |SI #200#46 = "T";
       |SI varmes =  1;|O varmes =  2;|O varmes =  3; #512#4 = 01; |FINSI;
       |SI varmes =  4;|O varmes =  5;|O varmes =  6; #512#4 = 02; |FINSI;
       |SI varmes =  7;|O varmes =  8;|O varmes =  9; #512#4 = 03; |FINSI;
       |SI varmes = 10;|O varmes = 11;|O varmes = 12; #512#4 = 04; |FINSI;
   |SINO;
       #512#4 = varmes;
   |FINSI;
   |PINTA #512#4;
|FPRC;

|CALCULO Tipo7CC1205; |TIPO 7;
   aAlfa = "    Pulse <F2> para cuadrar Importe Total ";
   |SI #599#13 = "S";
       aAlfa = aAlfa + "pagado";
   |SINO;
       aAlfa = aAlfa + "cobrado";
   |FINSI;
   |MENSA aAlfa;

   #512#11 = #599#3;
   #512#12 = #599#2;
|FCAL;

|CALCULO Tipo0CC1205; |TIPO 0;   || Total Cobrado.
   |SI FSalida = 10;
       #512#5 = #599#10 - #599#16; |PINTA #512#5;
   |FINSI;

   nNume1 = (#599#16 + #512#5) ! 2;
   |SI nNume1 > #599#10; |Y #599#10 > 0;
       |MENAV "1234Error, Importe superior al Total Factura";
       |ERROR;
       |ACABA;
   |FINSI;

   |SI nNume1 < #599#10; |Y #599#10 < 0;
       |MENAV "1234Error, Importe superior al Total Factura";
       |ERROR;
       |ACABA;
   |FINSI;

   nNume3 = #599#8 + #599#9;
   |SI #599#10 = #512#5;
       nTp = 100;
       #512#6 = #599#8;
       #512#7 = #599#9;
   |SINO;
       nTp   = ((#512#5 * 100) / #599#10) ! 4;
       #512#6 = (#599#8 % nTp) ! 2;
       #512#7 = (#599#9 % nTp) ! 2;
       |SI nNume1 = #599#10;
           nTp = (100 - #599#18) ! 4;
           #512#6 = #599#8 - #599#19;
           #512#7 = #599#9 - #599#20;
       |FINSI;
       nNume2 = #512#6 + #512#7;
       |SI nNume2 ! #512#5;     ||para cuadrar lo mas posible
           #512#7 = (#512#7 + (#512#5 - nNume2)) ! 2;
       |FINSI;
   |FINSI;
   #512#8 = nTp;
|FCAL;

|| ///////////////////////////
|PROCESO ClaveAlta12;
  #512#13 = #599#13;
  #512#0  = #599#0;
  #512#1  = nNumero;
  #512#2  = nDLinea;
|FPRC;

|PROCESO ClaveBaja12;
  #512#13 = #599#13;
  #512#0  = #599#0;
  #512#1  = nNumero;
  #512#2  = nHLinea;
|FPRC;

|| ////////////////////// proceso para Cainliva (Introduccion Facturas)
|PROCESO TrataCainliva;

  nNumero = #599#1;
  #599#16 = 0;
  #599#18 = 0;
  #599#19 = 0;
  #599#20 = 0;
  nDLinea =  1;
  nHLinea = 99;

  |BUCLE PrimeraSuma;

  |SI eModoCaja ! 14; |Y nHLinea > 1;
      aAlfa1 =  "2400NFra. de Critero de Caja. Desea introducir los datos de Cobro/Pago";
      |SI eModoCaja = 12;
          aAlfa1 =  "2400NFra. de Critero de Caja. Desea modificar los datos de Cobro/Pago";
      |FINSI;
      |AVISO;
      |CONFI aAlfa1;
      |SI FSalida ! 0; |ACABA; |FINSI;
  |FINSI;

  |PINPA #0#512;
  |SI #599#13 = "S";
      |ATRI R; |PINTA 1413, "Pago ";       |ATRI 0;
      |ATRI P; |PINTA 2210, "Pagado ..: "; |ATRI 0;
               |PINTA 1525, "Pagado  ";
               |PINTA 1543, "Pago  ";
  |FINSI;

  |PINDA #0#599;
  |PINTA #599#16;
  |PINTA #599#18;
  |SI eModoCaja = 14;
      |ENTLINEAL #1#512, -4, 5, 20, 0, ClaveAlta12, ClaveBaja12;
      |ACABA;
  |FINSI;

  |ENTLINEAL #1#512, -4, 2, 20, 0, ClaveAlta12, ClaveBaja12;
|FPRC;

|| /////////////////////////////////////////////////////////////////////
|PROCESO Campos502;
|| .. asi el calcul
     nCampo1 = 13 + (nNumBase * 3);
     nCampo2 = nCampo1 + 1;
     nCampo3 = nCampo1 + 2;

     #502nCampo1 = ((nBase + nCuota) % #502#13) ! 2;
     #502nCampo2 = (nBase  % #502#13) ! 2;
     #502nCampo3 = (nCuota % #502#13) ! 2;

     nSuma1 = (nSuma1 + #502nCampo1) ! 2;
     nSuma2 = (nSuma2 + #502nCampo2) ! 2;
     nSuma3 = (nSuma3 + #502nCampo3) ! 2;
     nLineaUlt = nNumBase;
|FPRC;

|PROCESO LeoLineaIVA;
     nSuma1 = 0;
     nSuma2 = 0;
     nSuma3 = 0;
     nLineaUlt = 0;
     |PDEFECTO #502,16,31;

     |SI #599#26 ! 0; |O #599#27 ! 0;
         nBase = #599#26;  nCuota = #599#27;
         nNumBase = 1; |HAZ Campos502;
     |FINSI;

     |SI #599#28 ! 0; |O #599#29 ! 0;
         nBase = #599#28;  nCuota = #599#29;
         nNumBase = 2; |HAZ Campos502;
     |FINSI;

     |SI #599#30 ! 0; |O #599#31 ! 0;
         nBase = #599#30;  nCuota = #599#31;
         nNumBase = 3; |HAZ Campos502;
     |FINSI;

     |SI #599#32 ! 0; |O #599#33 ! 0;
         nBase = #599#32;  nCuota = #599#33;
         nNumBase = 4; |HAZ Campos502;
     |FINSI;

     |SI #599#34 ! 0; |O #599#35 ! 0;
         nBase = #599#34;  nCuota = #599#35;
         nNumBase = 5; |HAZ Campos502;
     |FINSI;

     |SI nSuma1 ! #502#10; |O nSuma2 ! #502#11; |O nSuma3 ! #502#12;
         |SI nLineaUlt > 0;
             nCampo1 = 13 + nLineaUlt * 3;
             nCampo2 = nCampo1 + 1;
             nCampo3 = nCampo1 + 2;
             #502nCampo1 = #502nCampo1 + (#502#10 - nSuma1);
             #502nCampo2 = #502nCampo2 + (#502#11 - nSuma2);
             #502nCampo3 = #502nCampo3 + (#502#12 - nSuma3);
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO PrimeraSuma2;
      #599#16 = #599#16 + #502#10;
      #599#18 = #599#18 + #502#13;
      #599#19 = #599#19 + #502#11;
      #599#20 = #599#20 + #502#12;
      nLinea = #502#4;
      #501#6  = #501#6  + #502#11;
      #501#7  = #501#7  + #502#12;
      #501#12 = #501#12 + #502#10;
      #501#16 = #501#16 + #502#13;

      |SI nSwOp = 2; |Y eSwCambio = 1;
          |HAZ LeoLineaIVA;
          |GRABA 020#502a;
      |FINSI;
      |LIBERA #502;
|FPRC;

|DEFBUCLE PrimeraSuma2;
  #502#1;
  ;
  #501#13, #501#17, #501#0, #501#14, #501#15, 01;
  #501#13, #501#17, #501#0, #501#14, #501#15, 99;
  be;
  NULL, PrimeraSuma2;
|FIN;

|PROCESO ClaveAlta02;
  #502#0  = #599#13;
  #502#32 = #599#36;
  #502#1  = #599#0;
  #502#2  = #599#3;
  #502#3  = #599#2;
  #502#4  = nDLinea;
|FPRC;

|PROCESO ClaveBaja02;
  #502#0  = #599#13;
  #502#32 = #599#36;
  #502#1  = #599#0;
  #502#2  = #599#3;
  #502#3  = #599#2;
  #502#4  = nHLinea;
|FPRC;
|| ////////////////////// proceso para Camaniva

|PROCESO TrataCamaniva;

  |ABRE #501;
  #501#13 = #599#13;   ||Rep/Sop
  #501#17 = #599#36;   ||Ejer.Devengo
  #501#0 =  #599#0;    ||Libro
  #501#14 = #599#3;    ||Serie
  #501#15 = #599#2;    ||Factura
  |LEE 141#501=;
  |SI FSalida ! 0;
      |DDEFECTO #501;
      #501#13 = #599#13;
      #501#17 = #599#36;
      #501#0  = #599#0;
      #501#14 = #599#3;
      #501#15 = #599#2;
      #501#18 = #599#37;
      |GRABA 020#501b;
  |FINSI;

  #501#2 = #599#21;
  #501#3 = #599#22;
  #501#4 = #599#23;

  #501#8  = #599#17;
  #501#13 = #599#13;

  nNumero = #599#2;
  #599#16 = 0; #501#12 = 0;
  #599#18 = 0; #501#16 = 0;
  #599#19 = 0; #501#6 = 0;
  #599#20 = 0; #501#7 = 0;
  nDLinea =  1;
  nHLinea = 99;
  nLinea  = 0;
  nSwOp   = 1;
  |BUCLE PrimeraSuma2;

  |SI nLinea = 0;
      |SI eModoCaja = 4;
          |MENAV "    No existen Cobros/Pagos de esta Factura. ";
          |VETE ParaSalir;
      |FINSI;
  |FINSI;

  |SI eModoCaja ! 4; |Y nForzCaja ! 1;
      aAlfa1 =  "2400NFra. de Critero de Caja. Desea introducir los datos de Cobro/Pago";
      |SI eModoCaja = 2;
          aAlfa1 =  "2400NFra. de Critero de Caja. Desea modificar los datos de Cobro/Pago";
      |FINSI;
      |AVISO;
      |CONFI aAlfa1;
      |SI FSalida ! 0;
          |VETE ParaSalir;
      |FINSI;
  |FINSI;
  |SI nForzCaja = 1; |Y eModoCaja = 4; ePrograma = 1; |FINSI;

  |PINPA #0#599;
  |PINPA #0#502;
  |SI #599#13 = "S";
      |ATRI R; |PINTA 1411, "Pago para IVA Criterio de Caja  "; |ATRI 0;
               |PINTA 2203, "       Total Pagado ..:                (         %)";
      |ATRI P; |PINTA 2203, "       Total Pagado ..: "; |ATRI 0;
               |PINTA 1531, "Pagado  ";
               |PINTA 1549, "Pago ";
  |SINO;
               |PINTA 2203, "       Total Cobrado .:                (         %)";
      |ATRI P; |PINTA 2203, "       Total Cobrado .: "; |ATRI 0;
  |FINSI;
  |C_P #599#16,1,2227;
  |C_P #599#18,1,2243;

  |PINDA #0#599;

  |SI #501#17 = 0; |Y #501#18 ! -1;
      |SI eModoCaja ! 4;
          |MENAV "    Exportada al ejercicio posterior. Solo Consulta";
      |FINSI;
      eModoCaja = 4;
  |FINSI;

  |PINTA #599#16;
  |PINTA #599#18
  |SI eModoCaja = 4;
      |ENTLINEAL #1#502, -6, 5, 20, 0, ClaveAlta02, ClaveBaja02;
  |SINO;
      |ENTLINEAL #1#502, -6, 2, 20, 0, ClaveAlta02, ClaveBaja02;
  |FINSI;

|ET ParaSalir;

  #599#16 = 0; #501#12 = 0;
  #599#18 = 0; #501#16 = 0;
  #599#19 = 0; #501#6 = 0;
  #599#20 = 0; #501#7 = 0;
  nDLinea =  1;
  nHLinea = 99;
  nLinea  = 0;
  nSwOp   = 2;
  |BUCLE PrimeraSuma2;
  #501#5 = "N";
  |SI #501#6 = #599#8; |Y #501#7 = #599#9;
      #501#5 = "S";
  |FINSI;

  |GRABA 020#501a;
  |LIBERA #501;
  |CIERRA #501;

|FPRC;
|| ///////////////////////////////////////////////////////////////////////

|PROGRAMA;
  |NOME_DAT #599 eaFicCaja;

  |ABRE #599;
  |LEE 020#599p;
  |SI FSalida ! 0;
      |CIERRA #599;
      |ACABA;
  |FINSI;

  |SI eModoCaja > 10;
      |HAZ TrataCainliva;
  |SINO;
      |HAZ TrataCamaniva;
  |FINSI;

  |BORRA 020#599a;
  |CIERRA #599;
  eSwCambio = 0;
|FPRO;
