|FICHEROS;
    cabaresu #0;
    cacuenta #1;         || cuentas
    cabaexp3 #3;         || Auxiliar de Cuentas
    caexistc #6;         || descarga cabs.
    caexistl #7;         || descarga lins.
    caconimp #8;
|FIN;

|VARIABLES;
  &eaFicbal = "";
  &eaNomSes = "";
  &eaDepSes = "";

  &pathbal  = "";
  &balance  = 1;
  &aparam   = "";

  linea     = 0;
  informe   = "cabaresu";
  alfa1     = "";
  nume1     = 0;

  mes       = 0;
  letrames  = "";
  nivel     = 0;
  traraf    = 0;
  x         = 0;
  y         = 0;
  sw_exp    = 0;
  sw        = 0;
  sw_error  = 0;

  &r_emp = 0;
  &r_per = 0;
  &d_mes = 0;
  &h_mes = 0;
  &estru = 0;
  &r_eje = 0;
  &r_nom = "";

  &eldesde  = 0;
  &elhasta  = 0;
  des_exi   = 0;
  has_exi   = 0;
  d_cuen    = "";
  h_cuen    = "";
  act_cuent = "";
  act_nivel = 0;
  cue_perdi = "";
  dig_perdi = 0;
  acor_c    = "";
  acor_d    = 0;
  alar_c    = "";
  alar_d    = 0;

  tipo    = "";
  debe    = 0;
  haber   = 0;
  saldo   = 0;
  explota = 0;
  descar  = 0;
  tipoAD  = "";
  contaAD = 0;

  lin     = 0;
  swfinal = 0;
  importe = 0;

  &titulo = "";
  &sw_inf = 0;
  &var1   = "";
  &var2   = "";
  &tot1   = 0;
  &tot2   = 0;
  {-1}menu  = "";           || Variables menu impresora o pantalla
      menu1 = "2400";
      menu2 = "1";
      menu3 = "Elija opcion: ";
      menu4 = "PI";
      menu5 = " Pantalla ";
      menu6 = " Impresora ";
|FIN;

|INCLUYE acceso_i;
|INCLUYE digito_i;

|| **********************************************************************

|PROCESO antesque;
  aparam = PARAMETRO; |QBF aparam;
  |SI aparam = "fech";
      |HAZ AntesSesion;
      |NOME_DAT #1 eaFicbal;
  |FINSI;
|FPRC;

|PROCESO PintoSes; |TIPO 7;
  aparam = PARAMETRO; |QBF aparam;
  |SI aparam = "fech";
      |HAZ AntesSesion;
      |SI eaNomSes > "";
          |ATRI I; |PINTA 0505, "SESION: "; |PINTA 0513,eaNomSes;
          |SI eaDepSes > "";
              |PINTA 0558, "Departamento: "; |PINTA 0572, eaDepSes;
          |FINSI;
          |ATRI 0;
      |FINSI;
  |FINSI;
|FPRC;

|| ========================================================================
||                        CALCULOS PANTALLA
|| ========================================================================

|PROCESO mayor;
  alfa1 = #0Campo > " ";
  #0Campo = alfa1;
  |PINTA #0Campo;
|FPRC;

|PROCESO pintames; |TIPO 0;
  nume1 = Campo + 2;                || Coge el campo del nombre del mes
  |SI #0Campo ! 0; |Y #0Campo ! 13;
      mes = #30#11 + (#0Campo - 1);   || Operacion para saber que mes es
      |SI mes > 12; mes = mes - 12; |FINSI;
  |SINO;
      mes = #0Campo;
  |FINSI;
  |HAZ mesletra;                          || Recoge el mes en letra
  #0nume1 = letrames;
  |PINTA #0nume1;                         || Pinta el nombre del mes

  |SI Campo = 1; |Y  #0#0 > #0#1;
      |MENAV "    Error de Entrada ...";
      |ERROR;
  |FINSI;
|FPRC;

|PROCESO pintames2; |TIPO 0;
  |SI #0Campo ! 0; |Y #0Campo ! 13;
      mes = #30#11 + (#0Campo - 1);   || Operacion para saber que mes es
      |SI mes > 12; mes = mes - 12; |FINSI;
  |SINO;
      mes = #0Campo;
  |FINSI;
  |HAZ mesletra;
  |ATRI I;
  |SI Campo = 7;  |PINTA 1424, letrames;  |FINSI;
  |SI Campo = 8;  |PINTA 1460, letrames;  |FINSI;
  |ATRI 0;
  |SI Campo = 8;  |Y #0#7 > #0#8;
      |MENAV "    Error de Entrada ...";
      |ERROR;
  |FINSI;
|FPRC;

|PROCESO mesletra;
  |SI mes = 0;  letrames = "Apertura  "; |FINSI;
  |SI mes = 1;  letrames = "Enero     "; |FINSI;
  |SI mes = 2;  letrames = "Febrero   "; |FINSI;
  |SI mes = 3;  letrames = "Marzo     "; |FINSI;
  |SI mes = 4;  letrames = "Abril     "; |FINSI;
  |SI mes = 5;  letrames = "Mayo      "; |FINSI;
  |SI mes = 6;  letrames = "Junio     "; |FINSI;
  |SI mes = 7;  letrames = "Julio     "; |FINSI;
  |SI mes = 8;  letrames = "Agosto    "; |FINSI;
  |SI mes = 9;  letrames = "Septiembre"; |FINSI;
  |SI mes = 10; letrames = "Octubre   "; |FINSI;
  |SI mes = 11; letrames = "Noviembre "; |FINSI;
  |SI mes = 12; letrames = "Diciembre "; |FINSI;
  |SI mes = 13; letrames = "Cierre    "; |FINSI;
|FPRC;

|PROCESO miraniv; |TIPO 0;
  nivel = #0#5;
  |SI nivel = 1;|Y dig4 = 0;  |AVISO;  |ERROR;  |FINSI;
  |SI nivel = 2;|Y dig5 = 0;  |AVISO;  |ERROR;  |FINSI;
  |SI nivel = 3;|Y dig6 = 0;  |AVISO;  |ERROR;  |FINSI;
  |SI nivel = 4;|Y dig7 = 0;  |AVISO;  |ERROR;  |FINSI;
  |SI nivel = 5;|Y dig8 = 0;  |AVISO;  |ERROR;  |FINSI;
  |SI nivel = 6;|Y dig9 = 0;  |AVISO;  |ERROR;  |FINSI;
|FPRC;

|PROCESO estruct; |TIPO 0;
  |SI #0#6 = 0;  |ACABA;  |FINSI;
  |HAZ cue_per;
  |SI sw_error ! 0;
      |MENAV "    No existe la estructura de descarga.";
      |ERROR;
  |FINSI;
|FPRC;

|PROCESO cue_per; || calcula la cuenta de perdidas y ganancias
  sw_error = 0;
  |ABRE #6;
  |DDEFECTO #6;
  #6#0 = #0#6;
  |LEE 040#6=;
  |SI FSalida ! 0;
      sw_error = 1;
      |CIERRA #6;
      |ACABA;
  |FINSI;
  cue_perdi = #6#2;
  dig_perdi = #6#3;
  |SI #0#4 = "S";
      acor_c = cue_perdi;
      acor_d = dig_perdi;
      |HAZ acortacuen;
      cue_perdi = acor_c;
      dig_perdi = acor_d;
  |FINSI;
  |CIERRA #6;
|FPRC;

|PROCESO conexi;
  d_mes = #0#7;
  h_mes = #0#8;
  estru = #0#6;
  r_emp = empre;
  r_per = ejerc;
  r_eje = #30#26;
  r_nom = #30#1;
  |SUB_EJECUTA "caexicon.run";
|FPRC;

|| ========================================================================
||                     CALCULOS de FICHEROS AUXILIARES
|| ========================================================================
|PROCESO grabatra;  || ... Graba temporal de Agrupaciones Exp0;

 |SI #1#5 = "A"; |O #1#5 = "P";
     tipoAD = "A"; contaAD = 13;
     |SI #1#5 = "P"; contaAD = 14; |FINSI;
 |FINSI;                             || Situacion
 |SI #1#5 = "D"; |O #1#5 = "H";
     tipoAD = "D"; contaAD = 15;
     |SI #1#5 = "H"; contaAD = 16; |FINSI;
 |FINSI;                             || Explotacion

  #3#0 = tipoAD;
  #3#1 = #0contaAD + 1;
  |LEE 101#3=;
  |SI FSalida ! 0;
      |DDEFECTO #3;
      #3#0 = tipoAD;
      #3#1 = #0contaAD + 1;
      |GRABA 020#3c;
  |FINSI;

  |SI #1#5 = "A"; |O #1#5 = "D";
      #3#2  = 1;
      #3#3  = #1#0;  || Cuenta
      #3#4  = #1#1;  || Digito
      #3#5  = #1#2;  || Descripcion
      #3#6  = saldo; || Cantidad
  |SINO;
      #3#8  = 1;
      #3#9  = #1#0;  || Cuenta
      #3#10 = #1#1;  || Digito
      #3#11 = #1#2;  || Descripcion
      #3#12 = saldo; || Cantidad
  |FINSI;

  |GRABA 020#3a;
  |LIBERA #3;

  || ***  sumasaldo;

  #0contaAD = #0contaAD + 1;     || Suma 1 al contador
  contaAD = contaAD + 4;
  #0contaAD = #0contaAD + saldo; || Suma el Saldo al total A. P. D. o H.
|FPRC;

|PROCESO exis_expl;
  descar = #7des_exi - #7has_exi; || iniciales - finales
||  |SI descar = 0; |ACABA; |FINSI;
  |SI descar ] 0;
      act_cuent = #7#5;     || cuenta descarga D
      act_nivel = #7#6;     || digito descarga D
  |SINO;
      act_cuent = #7#8;     || cuenta descarga H
      act_nivel = #7#9;     || digito descarga H
  |FINSI;

  acor_c = act_cuent; acor_d = act_nivel;
  |HAZ acortacuen;
  act_cuent = acor_c; act_nivel = acor_d;

  |SI act_cuent = #1#0; |Y act_nivel = #1#1;
      sw_exp = 1;
      |SI descar ] 0;
          debe = debe + descar;
      |SINO;
          haber = haber - descar;
      |FINSI;
       saldo = debe - haber;
  |FINSI;
|FPRC;

|DEFBUCLE exis2;
 #7#1;
 ;
 #0#6, 01;
 #0#6, 99;
 be;
 NULL, exis_expl;
|FIN;

|PROCESO exis_3000;
  sw_exp  = 1;
  saldo   = saldo + #7has_exi;
|FPRC;

|DEFBUCLE exis1;
 #7#2;
 ;
 #0#6, #1#0, #1#1;
 #0#6, alar_c, 9;
 e;
 NULL,exis_3000;
|FIN;

|PROCESO calcular;
 saldo   = 0;
 debe    = 0;
 haber   = 0;
 sw_exp  = 0;
 alfa1   = #1#0 % 101;
 |SI alfa1 = "3";                 |BUCLE exis1; |FINSI;
 |SI alfa1 = "6"; |O alfa1 = "7"; |BUCLE exis2; |FINSI;

 |SI sw_exp = 1; |ACABA; |FINSI;

 |PARA x = eldesde; |SI x [ elhasta; |HACIENDO x = x + 3;
       y = x + 1;
       debe  = debe  + #1x;
       haber = haber + #1y;
       saldo = debe - haber;
 |SIGUE;
|FPRC;

|PROCESO grabar;
  |HAZ digitos2;
  |SI d_error ! 0;  |ACABA;  |FINSI;
  |SI #0#4 = "N";  |Y #1#3 = "N";                     |ACABA;  |FINSI;
  |SI #0#4 = "S";  |Y #1#55 > nivel;                  |ACABA;  |FINSI;
  |SI #0#4 = "S";  |Y #1#3 = "N";  |Y #1#55 ! nivel;  |ACABA;  |FINSI;

  alar_c = #1#0;
  |HAZ alargacuen;

||   |SI #1#0 ! cue_perdi; || no es perdidas y ganancias
      |HAZ calcular;
      |SI saldo ! 0;
          |SI #1#5 = "H"; |O #1#5 = "P";
              saldo = - saldo;
          |FINSI;
          |HAZ grabatra;
      |FINSI;
||   |FINSI;
|FPRC;

|DEFBUCLE cabaresu0;
 #1#1;
 ;
 "            ", 0;
 "zzzzzzzzzzzz", 9;
 be;
 NULL, grabar;
|FIN;

|| ***********************************************************************
|PROCESO gr_final; || Grabar totales
 debe = 0;
 haber = 0;
|| #1#0 = cue_perdi;
|| #1#1 = dig_perdi;
|| |LEE 001#1=;
|| |SI FSalida ! 0;
||     sw_error = 1;
||     |MENAV "    ATENCION !!!. NO existe la cuenta de Perdidas y Ganancias ";
||     |ACABA;
|| |FINSI;
|| |PARA x = eldesde; |SI x [ elhasta; |HACIENDO x = x + 3;
||       y = x + 1;
||       debe  = debe  + #1x;
||       haber = haber + #1y;
||       saldo = debe - haber;
|| |SIGUE;

|| ...  saldo   = saldo + ( #0#17 - #0#18);  || saldo129 + activo - pasivo
                                             || explota = saldo;

  saldo   = #0#17 - #0#18;  || activo - pasivo
|| Grabar total ACTIVO Y PASIVO
 #1#0    = "";
 |SI saldo ] 0; #1#2 = "BENEFICIO"; #1#5 = "P";                 |FINSI;
 |SI saldo < 0; #1#2 = "PERDIDA  "; #1#5 = "A"; saldo = - saldo;|FINSI;
 |SI saldo = 0; #1#2 = "RESULTADO CERO "; #1#5 = "A";           |FINSI;
 |HAZ grabatra; || ....................... Graba la cuenta 129. en blanco

|| Grabar total DEBE Y HABER
                                         || saldo = explota;
 saldo   = #0#19 - #0#20;             || debe - haber
 #1#0  = "";
 |SI saldo > 0; #1#2 = "PERDIDA  "; #1#5 = "H";                 |FINSI;
 |SI saldo < 0; #1#2 = "BENEFICIO"; #1#5 = "D"; saldo = - saldo;|FINSI;
 |SI saldo = 0; #1#2 = "RESULTADO CERO "; #1#5 = "D";           |FINSI;
 |HAZ grabatra; || ....................... Graba la cuenta 129. en blanco
|FPRC;

|| ***************************************************************************
|| *********************  CALCULO DE IMPRESION *******************************
|| ***************************************************************************
|PROCESO impresio;
  |SI sw = 0;
      |SI menu2 = 1; |HAZ panta; |FINSI;
      sw = 1;
  |FINSI;
  sw_inf = 0;
  #3#7  = 0;
  #3#13 = 0;
  |SI #3#0 = "A";
      |SI #0#17 ! 0; #3#7  = ( #3#6  * 100  / #0#17 ) ! 2; |FINSI;
      |SI #0#18 ! 0; #3#13 = ( #3#12 * 100  / #0#18 ) ! 2; |FINSI;
  |SINO;
      |SI #0#19 ! 0; #3#7  = ( #3#6  * 100  / #0#19 ) ! 2; |FINSI;
      |SI #0#20 ! 0; #3#13 = ( #3#12 * 100  / #0#20 ) ! 2; |FINSI;
  |FINSI;
  |SI #3#2 = 1; |Y #3#3 = "            "; sw_inf = 1; |FINSI;
  |SI #3#8 = 1; |Y #3#9 = "            "; sw_inf = 2; |FINSI;

  |SI menu2 = 1;
      |HAZ operapanta;
      |SI sw_error = 1; |ERROR10; |FINSI;
  |FINSI;
  |SI menu2 = 2; |PRINT; |FINSI;
  sw = 1;
|FPRC;

|DEFBUCLE cabaresu2;
 #3#1;
 ;
 tipo, 00000;
 tipo, 99999;
 be;
 NULL, impresio;
|FIN;

|PROCESO cabaresu1;
  |SI #0#21 = "S";  |O #0#21 = "A";
      titulo = "     < BALANCE DE SITUACION >     ";
      var1   = "TOTAL ACTIVO .................";
      var2   = "TOTAL PASIVO .................";
      tipo = "A";
      sw = 0;
      |BUCLE cabaresu2;
      |SI sw_error = 1; |ACABA; |FINSI;
      |SI sw = 1;
          tot1   = #0#17;
          tot2   = #0#18;
          |SI menu2 = 2;
              |PIEINF;
          |SINO;
              swfinal = 1;
              |HAZ cambiapag;
              |SI sw_error = 1; |ACABA; |FINSI;
              swfinal = 0;
          |FINSI;
      |FINSI;
  |FINSI;
  |SI #0#21 = "P";  |O #0#21 = "A";
      titulo = "< CUENTA DE PERDIDAS Y GANANCIAS >";
      var1   = "TOTAL DEBE ...................";
      var2   = "TOTAL HABER ...................";
      tipo = "D";
      sw = 0;
      |BUCLE cabaresu2;
      |SI sw_error = 1; |ACABA; |FINSI;
      |SI sw = 1;
          tot1   = #0#19;
          tot2   = #0#20;
          |SI menu2 = 2;
              |PIEINF;
          |SINO;
              swfinal = 1;
              |HAZ cambiapag;
              |SI sw_error = 1; |ACABA; |FINSI;
              swfinal = 0;
          |FINSI;
      |FINSI;
  |FINSI;
|FPRC;

|| ******************************** CALCULO TIPO 3

|PROCESO impre; |TIPO 3;

  |MENU menu;
  menu2 = FSalida;
  |SI menu2 [ 0; |ACABA; |FINSI;

  |HAZ antesque;
  |HAZ conexi;

  |SI menu2 = 2;
      |IMPRE 0;
      |SI FSalida ! 0;  |ACABA;  |FINSI;

      |INFOR informe;
      |SI FSalida ! 0;
          |MENAV "     Error en informe.";
          |FINIMP;
          |ACABA;
      |FINSI;
  |FINSI;

  eldesde = (#0#0 * 3) + 9;
  elhasta = (#0#1 * 3) + 9;
  des_exi = #0#7 + 11
  has_exi = #0#8 + 11

  |ABRE #8;
  |LEE 111#8p;
  |SI #8#2 = "S";  |Y #8#3 = "N";  |Y aparam = "";
      |DFICE pathbal;
      balance = 1;
      |CIERRA #8;
      |SUB_EJECUTA "careccue.tab";
  |FINSI;
  |CIERRA #8;

||... Modificar Cuentas a balance segun saldo
  r_emp = empre;
  r_per = ejerc;
  eldesde = (#0#0 * 3) + 11;
  elhasta = (#0#1 * 3) + 11;
  |SUB_EJECUTA "carecsig.run";
  eldesde = (#0#0 * 3) + 9;
  elhasta = (#0#1 * 3) + 9;
|| ---------------------------------------------------------------------

  nivel = #0#5;
  |HAZ cue_per;

  |ABRE #3;
  |CIERRA #3;
  |DELINDEX #3;

  |ABRET #1;
  |ABRE #3;
  |BUCLE cabaresu0;
  |HAZ gr_final;
  |CIERRA #3;
  |CIERRAT #1;

  |HAZ cabaresu1;

  |SI menu2 = 2;
      |FININF;
      |FINIMP;
  |FINSI;

  |DELINDEX #3;
|FPRC;


|| *** Rutinas Varias

|PROCESO alargacuen;
  |QBF alar_c;
  alar_c = alar_c + "99999999999999";
  alar_c = alar_c % 112;
|FPRC;

|PROCESO acortacuen;
  |SI nivel < acor_d;
      |SI nivel = 1;  traraf = 100 + dig4;  |FINSI;
      |SI nivel = 2;  traraf = 100 + dig5;  |FINSI;
      |SI nivel = 3;  traraf = 100 + dig6;  |FINSI;
      |SI nivel = 4;  traraf = 100 + dig7;  |FINSI;
      |SI nivel = 5;  traraf = 100 + dig8;  |FINSI;
      acor_c = acor_c % traraf;
      acor_d = nivel;
      acor_c = acor_c + "              ";
      acor_c = acor_c % 112;
  |FINSI;
|FPRC;

|PROCESO operapanta;   || Operaciones por pantalla;
 |SI lin > 1300;
     swfinal = 0;
     |HAZ cambiapag;  || Cambia de pagina
     |SI sw_error = 1; |ACABA; |FINSI;
 |FINSI;

 |C_V #3#3, 1,"N"; |C_V #3#6, 1,"N";
 |C_V #3#7, 1,"N"; |C_V #3#9, 1,"N";
 |C_V #3#12,1,"N"; |C_V #3#13,1,"N";
 |C_V #3#5,1,"N";  |C_V #3#11,1,"N";

 |SI #3#2 = 1;
     |C_V #3#3, 1,"S"; |C_V #3#6, 1,"S"; |C_V #3#7, 1,"S";
 |FINSI;
 |SI #3#8 = 1;
     |C_V #3#9, 1,"S"; |C_V #3#12, 1,"S"; |C_V #3#13, 1,"S";
 |FINSI;
 |SI #3#2 = 1; |Y #3#3 = "            ";
     |C_V #3#3, 1,"N"; |C_V #3#5, 1,"S";
 |FINSI;
 |SI #3#8 = 1; |Y #3#9 = "            ";
     |C_V #3#9, 1,"N"; |C_V #3#11, 1,"S";
 |FINSI;

 Pos = lin;
 |PINDA #0#3;         || Pinta las lineas
 Pos = -1;
 lin = lin + 100;     || Posicionar una linea mas abajo
|FPRC;

|PROCESO cambiapag;
 sw_error = 0;
 lin = 100;
 |PAUSA;
 |SI 1 = FSalida;|O 7 = FSalida;
     sw_error = 1;
 |SINO;
     |SI swfinal = 0; |HAZ panta; |FINSI;
 |FINSI;
|FPRC;

|PROCESO panta;
    lin = 100;
    |PINPA #0#3;
    |ATRI I;
    |PINTA 0514, #0#2;  || mes Inicio
    |PINTA 0525, #0#3;  || mes Final
    |PINTA 0564, #0#12; || Fecha
    |ATRI 0;
    var1 = var1 % 120;
    var2 = var2 % 120;
    |ATRI R;
    |PINTA 0725, titulo;
    |PINTA 2203, "                                    ";
    |PINTA 2204, var1;
    |PINTA 2243, "                                    ";
    |PINTA 2244, var2;
    |ATRI 0;
    |SI tipo = "A";
        |C_V #0#17,1,"S"; |PINTA #0#17;
        |C_V #0#18,1,"S"; |PINTA #0#18;
    |SINO;
        |C_V #0#19,1,"S"; |PINTA #0#19;
        |C_V #0#20,1,"S"; |PINTA #0#20;
    |FINSI;
|FPRC;
