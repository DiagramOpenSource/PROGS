|FICHEROS;
  ca8m0000 #0;
  ca8m0001 #1;
  ca8m0002 #2;
  ca8y0001 #10;
  ca8cumay #410;
|FIN;

|VARIABLES;
 aParam    = "";
 &nBalance = 0;
 &nNumero  = 0;
 &aLucrat  = "";

 aAlfa1    = "";
 nError    = 0;
 sw        = 0;
 &swLinea  = 0;
 inform1   = "ca8y0001";
 inform2   = "ca8y1001";
 informe   = "";

 nValo1    = 0;
 nValo2    = 0;
 aValor    = "";
 nSi       = 0;
 &eaText   = "";
|FIN;

|INCLUYE varnue_i;
|INCLUYE acceso_i;
|| ******************************************** Calculos Pantalla
|CALCULO descri;  |TIPO 6;
  |ABRE #0;
  #0#0 = #10#2;
  |LEE 001#0=;
  |SI FSalida ! 0;
      |MENAV "    No existe Definicion de Balance";
      |ERROR;
  |SINO;
      #10#3 = #0#1; |PINTA #10#3;
  |FINSI;
  aLucrat = #0#3;
|FCAL;

|CALCULO bloque;

 |C_M #10Campo, 0, aAlfa1;
 |SI aAlfa1 = "N"; |ACABA; |FINSI;

 nError = 0;
 |SI nBalance = 0; |Y #10Campo ! 0; nError = 1; |FINSI;
 |SI nBalance = 1;
    |SI #10Campo < 1; |O #10Campo > 5; nError = 1; |FINSI;
 |FINSI;
 |SI nBalance = 2; |Y #10Campo ! 6; nError = 1; |FINSI;
 |SI nBalance = 4;
     |SI #10Campo < 7; |O #10Campo > 9; nError = 1; |FINSI;
 |FINSI;

 |SI nError = 1;
     |MENAV "    Bloque incorrecto";
     |ERROR;
 |FINSI;
|FCAL;

|CALCULO tipo;
 |C_M #10Campo, 0, aAlfa1;
 |SI aAlfa1 = "N"; |ACABA; |FINSI;

 nError = 0;
 |SI nBalance = 0; |Y #10Campo ! "A"; |Y #10Campo ! "P"; nError = 1; |FINSI;
 |SI nBalance = 1; |Y #10Campo ! "R"; nError = 1; |FINSI;
 |SI nBalance = 2; |Y #10Campo ! "E"; nError = 1; |FINSI;

 |SI nError = 1;
     |MENAV "    Tipo incorrecto";
     |ERROR;
 |FINSI;
|FCAL;

|CALCULO SiCta;
 aAlfa1 = #10Campo;
 |C_M #10#15,1,aAlfa1;
 |SI aAlfa1 = "N";
     #10#15 = "N"; |PINTA #10#15;
 |FINSI;
|FCAL;

|| /////////////////////// PROCESOS DE TRABAJO \\\\\\\\\\\\\\\\\\\\
|PROCESO BuscoTexto;
  || nCampo2 = #2#2;  ||Bloque
  || aCampo3 = #2#3;  ||Tipo
  || nCampo4 = #2#4;  ||Agrupacion
  || nCampo5 = #2#5;  ||Subagrupacion
  || nCampo6 = #2#6;  ||Epigrafe
  || nCampo7 = #2#7;  ||Partida
  || nCampo8 = #2#8;  ||subpartida

  eaText = "";
  |DDEFECTO #1;
  #1#14 = #2#14;
  #1#0  = #2#0;
  #1#1  = #2#1;
  #1#2  = #2#2;
  #1#3  = #2#3;
  #1#4  = #2#4;
  #1#5  = #2#5;
  #1#6  = #2#6;
  #1#7  = #2#7;
  #1#10 = #2#12;
  |LEE 040#1=;
  |SI FSalida = 0;
      eaText = #1#9; |QBF eaText;
  |FINSI;

  |SI #2#7 ! 0;
    aAlfa1 = "";
    |DDEFECTO #1;
    #1#14 = #2#14;
    #1#0  = #2#0;
    #1#1  = #2#1;
    #1#2  = #2#2;
    #1#3  = #2#3;
    #1#4  = #2#4;
    #1#5  = #2#5;
    #1#6  = #2#6;
    #1#7  = 0;
    #1#10 = 0;
    |LEE 040#1=;
    |SI FSalida = 0;
        aAlfa1 = #1#9; |QBF aAlfa1;
    |FINSI;
    |SI aAlfa1 ! "";
        eaText = aAlfa1 + " " + eaText;
    |FINSI;
 |FINSI;
  |SI #2#6 ! 0;
    aAlfa1 = "";
    |DDEFECTO #1;
    #1#14 = #2#14;
    #1#0  = #2#0;
    #1#1  = #2#1;
    #1#2  = #2#2;
    #1#3  = #2#3;
    #1#4  = #2#4;
    #1#5  = #2#5;
    #1#6  = 0;
    #1#7  = 0;
    #1#10 = 0;
    |LEE 040#1=;
    |SI FSalida = 0;
        aAlfa1 = #1#9; |QBF aAlfa1;
    |FINSI;
    |SI aAlfa1 ! "";
        eaText = aAlfa1 + " " + eaText;
    |FINSI;
 |FINSI;

 aAlfa1 = "";
 |SI #2#3 = "A"; aAlfa1 = "ACTIVO: "; |FINSI;
 |SI #2#3 = "P"; aAlfa1 = "PASIVO: "; |FINSI;
 |SI #2#3 = "R"; aAlfa1 = "RESULTADO: "; |FINSI;
 |SI #2#3 = "E"; aAlfa1 = "ESTADO: "; |FINSI;
 |SI aAlfa1 ! "";
     eaText = aAlfa1 + eaText;
 |FINSI;
|FPRC;

|PROCESO LeoDefCta;
 nSi = 0;
 |SI #2#2 < #10#5;  |O #2#2 > #10#6;  nSi = 1; |FINSI;
 |SI #2#3 < #10#7;  |O #2#3 > #10#8;  nSi = 1; |FINSI;
 |SI #2#4 < #10#9;  |O #2#4 > #10#10; nSi = 1; |FINSI;
 |SI #2#6 < #10#11; |O #2#6 > #10#12; nSi = 1; |FINSI;
 |SI #2#7 < #10#13; |O #2#7 > #10#14; nSi = 1; |FINSI;
 |SI nSi = 1; |ACABA; |FINSI;

 sw = 1;
 |DDEFECTO #410;
 #410#12 = enEjerBal;
 #410#0  = #2#9;
 #410#10 = eNum2008;
 |LEE 040#410=;

 eaText = "";
 |HAZ BuscoTexto;

 |PRINT;
|FPRC;

|DEFBUCLE LeoDefCta;
 #2#2;
 ;
 enEjerBal, nNumero, #10#0, "     ";
 enEjerBal, nNumero, #10#0, "zzzzz";
 e;
 NULL, LeoDefCta;
|FIN;


|PROCESO LeoCtas;
  swLinea = 2;
  |PRINT;
|FPRC;

|DEFBUCLE LeoCtas;
 #2#1;
 ;
 #1#14, #1#0, #1#1, #1#2, #1#3, #1#4, #1#5, #1#6, #1#7, #1#10, 001;
 #1#14, #1#0, #1#1, #1#2, #1#3, #1#4, #1#5, #1#6, #1#7, #1#10, 999;
 e;
 NULL, LeoCtas;
|FIN;

|PROCESO LeoDefinicion;
 |SI sw = 0;
     nValo1 = #1#2;
     aValor = #1#3;
     nValo2 = #1#4;
 |FINSI;
 sw = 1;

 |SI nBalance = 0; |Y aValor ! #1#3;
     swLinea = 3; |PRINT;
     aValor = #1#3;
 |FINSI;

 |SI nBalance = 1; |Y nValo1 ! #1#2;
     swLinea = 3; |PRINT;
     nValo1 = #1#2;
 |FINSI;

 |SI nBalance = 2; |Y nValo2 ! #1#4;
     swLinea = 3; |PRINT;
     nValo2 = #1#4;
 |FINSI;

 |SI nBalance = 4; |Y aValor ! #1#3;
     swLinea = 3; |PRINT;
     aValor = #1#3;
 |FINSI;

 swLinea = 0;
 |SI #10#4 = "N";
     |PRINT;
 |SINO;
     |SI #1#8 = "C"; swLinea = 1; |FINSI;
     |PRINT;
     |SI #1#8 = "C";
         |BUCLE LeoCtas;
         || |SI swLinea = 2; swLinea=3; |PRINT; |FINSI;
     |FINSI;
 |FINSI;
|FPRC;

|DEFBUCLE LeoDefinicion;
 #1#1;
 ;
 enEjerBal, nNumero, #10#0, #10#5, #10#7, #10#9,  0, #10#11, #10#13, 0;
 enEjerBal, nNumero, #10#0, #10#6, #10#8, #10#10, 9, #10#12, #10#14, 9;
 e;
 NULL, LeoDefinicion;
|FIN;


|PROCESO Tipo3;  |TIPO 3;

 nNumero = #10#2;

 |IMPRE 0;
 |SI FSalida ! 0; |ACABA; |FINSI;

 informe = inform1;
 |SI #10#15 = "S"; informe = inform2; |FINSI;
 |INFOR informe;
 |SI FSalida ! 0; |FINIMP; |ACABA; |FINSI;

 sw      = 0;
 |SI #10#15 = "N";
     nValo1  = 0;
     nValo2  = 0;
     aValor  = "";
     swLinea = 0;
     |BUCLE LeoDefinicion;
     |SI sw = 1;
         |PIEINF;
     |FINSI;
  |SINO;
     swLinea = 0;
     |ABRE #1;
     |ABRE #410;
     |BUCLE LeoDefCta;
     |CIERRA #410;
     |CIERRA #1;
     |SI sw = 1;
         |PIEINF;
     |FINSI;
 |FINSI;
 |FININF;
 |FINIMP;
|FPRC;

|| **************************************************************************

|PROGRAMA;
  aParam = PARAMETRO; |QBF aParam;
  aParam = aParam % 204;

  |SI aParam = "";
      |HAZ inicio;
      |HAZ eVarEmpresa;
      |SI enEjercicio < 2008;
          |MENAV "    Plan Contable de 2008. No factible en este Ejercicio";
         |ACABA;
      |FINSI;
   |SINO;
       enEjerBal = aParam; |SI enEjerBal < 2008; enEjerBal = 2008; |FINSI;
   |FINSI;

   aParam = PARAMETRO; |QBF aParam;
   aParam = aParam % 101;
   |SI aParam = ""; aParam = "0"; |FINSI;
   nBalance   = aParam;

   |DDEFECTO #10;
   #10#0  = nBalance;
   #10#16 = enEjerBal;
   |SI #10#0 = 0;
       #10#1 = "BALANCE DE SITUACION AL CIERRE DEL EJERCICIO";
       |C_M #10#5,  1, "N"; |C_M #10#6, 1, "N";
       #10#5 = 0;   #10#6 = 0;
       #10#7 = "A"; #10#8 = "P";
   |FINSI;
   |SI #10#0 = 1;
       #10#1 = "CUENTA DE PERDIDAS Y GANACIAS CORRESPONDEINTE AL EJERCICIO ";
       |C_M #10#7,  1, "N"; |C_M #10#8, 1, "N";
       #10#5 = 1;   #10#6 = 5;
       #10#7 = "R"; #10#8 = "R";
   |FINSI;
   |SI #10#0 = 2;
       #10#1 = "A)ESTADO DE INGRESOS Y GASTOS RECONOCIDOS";
       |C_M #10#5,  1, "N"; |C_M #10#6, 1, "N";
       |C_M #10#7,  1, "N"; |C_M #10#8, 1, "N";
       #10#5 = 6; #10#6 = 6;
       #10#7 = "E"; #10#8 = "E";
   |FINSI;
   |SI #10#0 = 4;
       #10#1 = "ESTADO DE FLUJOS DE EFECTIVO";
       |C_M #10#5,  1, "N"; |C_M #10#6, 1, "N";
       |C_M #10#7,  1, "N"; |C_M #10#8, 1, "N";
       #10#5 = 7; #10#6 = 9;
       #10#7 = "A"; #10#8 = "Z";
   |FINSI;

   |CLS;
   |CABEZA "Inf. Defincion de Balances";
   |PINPA #0#10;
   |PINDA #0#10;

   |ENDATOS #1#10;
|FPRO;
