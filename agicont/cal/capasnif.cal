|FICHEROS;
   capasnif #0;
   caclipro #1;

   canempre #30;
   cabucem1 #998;

   regisnif@ #709;

   agipobla@ #703;
   agiprovi@ #704;
   agim0016@ #718;
|FIN;

|VARIABLES;
 &entrada  = 1;
 &eaCuenta = "";
 &eaCodNif = "";
 &ewNif    = 0;
 &eaFichsel = "";

 dirfich0  = "";
 aDirFich  = "";
 aAlfa1    = "";
 aAlfa2    = "";
 aZDClPr   = "";
 aZHClPr   = "";
 aDClPr    = "";
 aHClPr    = "";
 aParam    = "";
 aDirec    = "";
 aDirec0   = "";
 aDirec1   = "";
 aDirec2   = "";
 aAlfa     = "";
 nSw       = 0;
 nEstado   = 0;

 nMaxDig   = 0;
 nCont     = 0;
 nCalc     = 0;
 nCalc1    = 0;
|FIN;

|INCLUYE acceso_i;

|PROCESO FiltraPuntos; |TIPO 6;
   nMaxDig = 0;
   |PARA nCont = 14; |SI nCont [ 19; |HACIENDO nCont = nCont + 1;
      |SI #48nCont > nMaxDig; nMaxDig = #48nCont; |FINSI;
   |SIGUE;

   aAlfa = eaCuenta; |QBF aAlfa;

   aAlfa = aAlfa - ".";
   |PARA; |SI FSalida ! 0; |HACIENDO;
      nCalc = FSalida + 98;               aAlfa2 = aAlfa % nCalc;
      nCalc = ((FSalida / 100) ! 0) + 99; aAlfa1 = aAlfa % nCalc;
      aAlfa = aAlfa1 + aAlfa2; |QBF aAlfa; aAlfa = aAlfa % 0; nCalc1 = aAlfa;
      nCalc1 = nMaxDig - nCalc1;
      aAlfa = aAlfa1 + ("0" * nCalc1) + aAlfa2;

      aAlfa = aAlfa - ".";
   |SIGUE;

   eaCuenta = aAlfa;
|FPRC;

|CALCULO cuenta; |TIPO 0;
     eaCuenta = #0Campo;
     salidas  = FSalida;
     |HAZ FiltraPuntos;
     #0Campo = eaCuenta;
     |PINTA #0Campo;
|FCAL;
|| --------------------------------------------------
|PROCESO LaDivision;
     aDirec0 = "";
     aDirec1 = aDirec;
     aDirec2 = "";

     nSw    = 0;
     aAlfa1 = aDirec % 103;
     |SI aAlfa1 = "CL ";  nSw = 1; |FINSI;
     |SI aAlfa1 = "AV ";  nSw = 1; |FINSI;
     |SI aAlfa1 = "PA ";  nSw = 1; |FINSI;
     |SI aAlfa1 = "PZ ";  nSw = 1; |FINSI;
     |SI aAlfa1 = "PL ";  nSw = 1; |FINSI;
     |SI aAlfa1 = "CR ";  nSw = 1; |FINSI;
     |SI nSw = 1;
         aDirec0 = aAlfa1;
         aDirec1 = aDirec % 440;
     |FINSI;
|FPRC;

|PROCESO CreaFicNif;
    eaCodNif = #1#9; |QBF eaCodNif;
    |SI eaCodNif = ""; |ACABA; |FINSI;

    #709#0 = eaCodNif;
    |LEE 141#709=;
    nEstado = FSalida;
    |SI nEstado ! 0; |O #0#5 = "S";

        |SI nEstado ! 0;
            |DDEFECTO #709;
        |FINSI;


        #709#0  = eaCodNif;   || Nif
        #709#1  = #1#1;       || Nombre y Apellidos

        aDirec  = #1#2;
        |HAZ LaDivision;
        #709#2  = aDirec1;    || Direccion
        #709#3  = aDirec2;    || No tengo el numero

        #709#4  = #1#3;       || Codigo Provincia
        #709#5  = #1#4;       || Codigo Poblacion
        #709#6  = #1#6;       || Poblacion
        #709#7  = #1#5;       || Provincia
        #709#8  = #1#7;       || Telefono
        #709#17 = #1#8;       || Fax
        #709#9  = aDirec0;    || SG
                              || #709#10 =  Piso
                              || #709#11 =  Puerta
                              || #709#16 =  Escalera
        |SI #1#24 = "   "; #1#24 = "ES "; |FINSI;
        #709#12 = #1#24;      || Codigo Pais
        #agim0016@#0 = #1#24;
        |LEE 000#agim0016@.=;
        |SI FSalida ! 0; |DDEFECTO #agim0016@; |FINSI;
        #709#13 = #agim0016@#1; || Nombre Pais
                               || #709#14 = Codigo Comunidad
                               || #709#15 = Nombre Comunidad
        #709#14 = "";
        #709#15 = "";

        #agipobla@#0 = #1#3;
        #agipobla@#1 = #1#4;
        |LEE 000#agipobla@.=;
        |SI FSalida ! 0; |DDEFECTO #agipobla@; |FINSI;

        #agiprovi@#0 = #1#3;
        |LEE 000#agiprovi@.=;
        |SI FSalida ! 0; |DDEFECTO #agiprovi@; |FINSI;

        #709#6 = #agipobla@#2;
        #709#7 = #agiprovi@#1;

        |SI nEstado ! 0; |GRABA 020#709n; |FINSI;
        |SI nEstado = 0; |GRABA 020#709a; |FINSI;
    |FINSI;

    |LIBERA #709;
|FPRC;

|DEFBUCLE LeeClieProv;
 #1#1;
 ;
 aZDClPr, aDClPr, 0;
 aZHClPr, aHClPr, 9;
 e;
 NULL, CreaFicNif;
|FIN;


|PROCESO HazTodo;

   aZDClPr = #0#0;
   aZHClPr = #0#0;
   |SI #0#0 = "T"; aZDClPr = "C"; aZHClPr = "P"; |FINSI;

   aDClPr = #0#3;
   aHClPr = #0#4;


   |ABRE #709;
   |BUCLE LeeClieProv;
   |CIERRA #709;

|FPRC;

|CALCULO Tipo3; |TIPO 3;
       |SI #capasnif#6 = "S";
           |HAZ MultiEmpresa;
       |SINO;
           |DFICE dirfich0; |QBF dirfich0;
           |HAZ HazTodo;
       |FINSI;
|FCAL;

|| **************************************************************************
|PROGRAMA;

   aParam = PARAMETRO; |QBF aParam;
   |SI aParam ! "";

       #0#0 = "T";
       #0#1 = enEmpresa;
       #0#2 = enPeriodo;
       #0#3 = "            ";
       #0#4 = "zzzzzzzzzzzz";
       #0#5 = "N";

       |DBASE aDirFich; |QBF aDirFich;
       aAlfa1 = ("00000" + enEmpresa) % -105;
       aAlfa2 = ("0" + enPeriodo)     % -101;

       aDirFich = aDirFich + "fich/"+ aAlfa1 + aAlfa2 + "/";

       |ABRE #30;
       #30#2 = enEmpresa;
       #30#3 = enPeriodo;
       |LEE 101#30=;
       |SI FSalida ! 0;
           |CIERRA #30;
           |ACABA;                  || Si no existe no puedo seguir
       |FINSI;
       |CIERRA #30;
       |PATH_DAT #1  aDirFich;
   |SINO;
       |HAZ inicio;
       #0#1 = enEmpresa;
       #0#2 = enPeriodo;
   |FINSI;

   ewNif = 0;
   |HAZ DirAplicSt;

   |DBASS aAlfa; |QBF aAlfa;
   aAlfa = aAlfa + "integral/def/dsam0103.mas";
   |CARGA_DEF aAlfa, regisnif@;
   |SI FSalida ! 0;
      |MENAV "    No se encuentra el fichero de nifs de la integrada";
      |ACABA;
   |FINSI;

   |DBASS aAlfa; |QBF aAlfa;
   aAlfa = aAlfa + "agicli/def/agipobla.mas";
   |CARGA_DEF aAlfa, agipobla@;
   |SI FSalida ! 0;
      |DESCARGA_DEF #regisnif@;
      |MENAV "    No se encuentra el fichero de poblaciones de agicli";
      |ACABA;
   |FINSI;

   |DBASS aAlfa; |QBF aAlfa;
   aAlfa = aAlfa + "agicli/def/agiprovi.mas";
   |CARGA_DEF aAlfa, agiprovi@;
   |SI FSalida ! 0;
      |DESCARGA_DEF #agipobla@; |DESCARGA_DEF #regisnif@;
   |FINSI;

   |DBASS aAlfa; |QBF aAlfa;
   aAlfa = aAlfa + "agicli/def/agim0016.mas";
   |CARGA_DEF aAlfa, agim0016@;
   |SI FSalida ! 0;
      |DESCARGA_DEF #agiprovi@;
      |DESCARGA_DEF #agipobla@; |DESCARGA_DEF #regisnif@;
      |MENAV "    No se encuentra el fichero de paises de agicli";
      |ACABA;
   |FINSI;

   |DBASS aAlfa; |QBF aAlfa;
   aAlfa = aAlfa + "agicli/fich/";
   |PATH_DAT #agim0016@#aAlfa#; |ABRE #agim0016@;
   |PATH_DAT #agipobla@#aAlfa#; |ABRE #agipobla@;
   |PATH_DAT #agiprovi@#aAlfa#; |ABRE #agiprovi@;

   |SI aParam = "";
       |CLS;
       |PINPA #0#0;
       |PINDA #0#0;
       |ENDATOS #1#0;
   |SINO;
       dirfich0 = aDirFich;
       |HAZ HazTodo;
   |FINSI;

   |CIERRA #agim0016@; |DESCARGA_DEF #agim0016@;
   |CIERRA #agiprovi@; |DESCARGA_DEF #agiprovi@;
   |CIERRA #agipobla@; |DESCARGA_DEF #agipobla@;
   |DESCARGA_DEF #regisnif@;
|FPRO;

|| *************************************
|PROCESO MultiEmp1;
   cod1 = #cabucem1#0;
   cod2 = #cabucem1#1;
   emEmp = 1;  || Sin mensaje
   |HAZ leelo;
   |SI swerror ! 0;  |ACABA;   |FINSI;

   |DFICB dirfich0; |QBF dirfich0;
   dirfich0 = dirfich0 + (("00000" + #cabucem1#0) % -105) + #cabucem1#1 + "/";
   |PATH_DAT #1  dirfich0;

   |ATRI N; |CUADRO 1508 1850;
   |ATRI R; |PINTA 1609, "Empresa: ";      |ATRI 0;
   |PINTA 1618, #30#2; |PINTA 1709, #30#1; |ATRI 0;
   |HAZ HazTodo;
|FPRC;

|DEFBUCLE MultiEmp0;
 #cabucem1#1;
 ;
 00000, 0;
 99999, 9;
 ;
 NULL,MultiEmp1;
|FIN;

|PROCESO MultiEmpresa;
  |SUB_EJECUTA "cabucemp";
  ||NOME_DAT #998 eaFichsel;
  |BUCLE MultiEmp0;
  |DELINDEX #998;
|FPRC;
