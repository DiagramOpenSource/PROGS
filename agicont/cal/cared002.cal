|FICHEROS;
 cared002 #0;
 cared001 #1;  || Control Pase Secuencial
 caremesc #2;  || Remesas Cabeceras
 caremesl #3;  || Remesas Lineas
 caclipro #4;  || Clientes
 camancob #5;  || Cobros
 caivarep #6;  || Iva repercutido

 camoneda #7;  || Moneda
|FIN;

|VARIABLES;
 alfa1 = "";
 alfa2 = "";
 alfa3 = "";
 nume1 = 0;
 nume2 = 0;
 nCalc = 0;
 Cantidad = 0;
 MonBase = 0;

 mens     = "";
 swErrorR = 0;
 swErrorP = 0;
 menR1    = "    No Existe Remesa !!!";
 menR2    = "    Remesa YA contabilizada ";
 menR3    = "2400SRemesa YA Emitida en Papel. Aceptar S/N ";
 menR4    = "2400NRemesa YA lanzada en Diskette. Aceptar S/N ";

 NombreP = "                                        ";
 NifP    = "000000000000";
 sufiP   = "000";
 EntP    = "0000";
 SucP    = "0000";
 banAlfa = "           ";
 NombreO = "                                        ";
 NifO    = "000000000";
 sufiO   = "000";
 EntO    = "0000";
 SucO    = "0000";
 dcO     = "00";
 CtaO    = "0000000000";
 LocO    = "                                      ";
 ProP    = "00";
 ConCli  = "";
 sino     = "";
 ceros    = "000000000000000";
 fechapre = "      ";
 fechacar = "      ";
 dd       = "";
 mm       = "";
 aa       = "";
 tiProced = "06";

 cabe1p    = "0170";  || Registro Presentador pesetas
 cabe1e    = "5170";  || Registro Presentador euros
 cabe1     = "";
 cabe2p    = "0370";  || Registro Ordenante pesetas
 cabe2e    = "5370";  || Registro Ordenante euros
 cabe2     = "";
 cabe3p    = "0670";  || Registro Individual pesetas
 cabe3e    = "5670";  || Registro Individual euros
 cabe3     = "";
 cabe4p    = "0676";  || Registro Individual NO domiciliado pesetas
 cabe4e    = "5676";  || Registro Individual NO domiciliado euros
 cabe4     = "";
 tord1p    = "0870";  || Total Ordenante pesetas
 tord1e    = "5870";  || Total Ordenante euros
 tord1     = "";
 tgen1p    = "0970";  || Total General pesetas
 tgen1e    = "5970";  || Total General euros
 tgen1     = "";

 libre6   = "      ";
 libre8   = "        ";
 libre10  = "          ";
 libre12  = "            ";
 libre16  = "                ";
 libre20  = "                    ";
 libre38  = "                                      ";
 libre43  = "                                           ";
 libre52  = "                                                    ";
 libre72  = "                                                                        ";

 ca1  = "";
 ca2  = "";
 ca3  = "";
 ca4  = "";
 ca5  = "";
 ca6  = "";
 ca7  = "";
 ca8  = "";
 ca9  = "";
 ca10 = "";
 l          = 0;
 trecibos   = 0;
 timporte   = 0;

 handle     = "";
 fiche      = "";
 varalfa1   = "";
 contenido  = "";
 swERROR    = 0;
 CampoDni   = "";
 Letra1     = "";
 Letra2     = "";
 Letra3     = "";
 Letra4     = "";
 Letra5     = "";
 Letra6     = "";
 Letra7     = "";
 Letra8     = "";
 Caracter   = "";
 Carac1     = 0;
 Carac2     = 0;
 Carac3     = 0;
 Carac4     = 0;
 Carac5     = 0;
 XPara      = 0;

 ccc        = "";
 c_alfa1    = "";
 c_alfa2    = "";
 c_nume1    = 0;
 c_nume2    = 0;
 c_nume3    = 0;

 swdomi = 0;
 traba  = "";
 nume   = 0;
 &programa = "";
 &ficcopia = "";

 Moneda = "";
 Calc = 0;

    &eaVarDni  = "";
    &enCalcCif = 0;
|FIN;

|INCLUYE acces2_i;

|| *************************************************************************

|PROCESO mayus1;
 alfa1 = #0Campo > " ";
 #0Campo = alfa1;
 |PINTA #0Campo;
|FPRC;

|PROCESO ver_remesa; |TIPO 6;
 |SI FSalida ! 0; |ACABA; |FINSI;
 |ABRE #2;
 |HAZ leerem;
 |CIERRA #2;
 |SI swErrorR ! 0;
     |SI swErrorR < 3;
         |MENAV mens;
         |ERROR;
         |ACABA;
     |SINO;
         |CONFI mens;
         |SI FSalida ! 0;
             |ERROR;
             |ACABA;
         |FINSI;
     |FINSI;
 |FINSI;
 #0#2 = #2#2; |PINTA #0#2;
 #0#3 = #2#1; |PINTA #0#3;
 |PINTA 0935, #2#1;
 |PINTA 1035, #2#2;
 |PINTA 0958, #2#5;
 |PINTA 1049, #2#10;
 |HAZ lee_pase;
|FPRC;

|PROCESO leerem;
 swErrorR = 0;
 |DDEFECTO #2;
 #2#0 = #0#1;
 |LEE 001#2=;
 |SI FSalida ! 0;
     swErrorR = 1; mens = menR1;
 |SINO;
     |SI #2#9 = "S";
         swErrorR = 2; mens = menR2;
     |SINO;
         |SI #2#8 = "S"; swErrorR = 3; mens = menR3; |FINSI;
         |SI #2#8 = "D"; swErrorR = 4; mens = menR4; |FINSI;
     |FINSI;
 |FINSI;
|FPRC;

|PROCESO lee_pase; |TIPO 6;
 |HAZ leepas;
 |ATRI I;
 |PINTA 1220, #1#1;
 |ATRI 0;
|FPRC;

|PROCESO leepas;
 swErrorP = 0;
 |ABRE #1;
 |DDEFECTO #1;
 #1#0 = #0#2;
 |LEE 001#1=;
 |SI FSalida ! 0; swErrorP = 1; |FINSI;
 |CIERRA #1;
|FPRC;

||...................................................................................
||***********************************************************
||************ REGISTRO DE CABECERA DE PRESENTADOR **********
||***********************************************************
|PROCESO cabecera_p;

  |SI Moneda = "P";
     cabe1 = cabe1p;
     cabe2 = cabe2p;
     cabe3 = cabe3p;
     cabe4 = cabe4p;
     tord1 = tord1p;
     tgen1 = tgen1p;
  |SINO;
     cabe1 = cabe1e;
     cabe2 = cabe2e;
     cabe3 = cabe3e;
     cabe4 = cabe4e;
     tord1 = tord1e;
     tgen1 = tgen1e;
  |FINSI;

  varalfa1 = "";
  varalfa1 =  cabe1 + NifP + fechapre + libre6 + NombreP;
  varalfa1 =  varalfa1 + libre20 + EntP + SucP;
  varalfa1 =  varalfa1 + libre12 + banAlfa + libre43;

  contenido = handle + " " + varalfa1 + &#0#7 + &#0#8;
  |FGRABA contenido;

  ||   l = l + 1; .. este registro no suma hasta el final
|FPRC;

||***********************************************************
||************ REGISTRO DE CABECERA DE ORDENANTE ************
||***********************************************************

|PROCESO cab_ordenante;
  varalfa1 = "";
  varalfa1 =  cabe2 + NifO + fechapre + fechacar + NombreO;
  varalfa1 =  varalfa1 + EntO + SucO + dcO + CtaO;
  varalfa1 =  varalfa1 + libre8 + "06";
  varalfa1 =  varalfa1 + libre10 + banAlfa + libre43;

  contenido = handle + " " + varalfa1 + &#0#7 + &#0#8;
  |FGRABA contenido;

  l = l + 1;
|FPRC;

||***********************************************************
||******************* REGISTRO INDIVIDUAL *******************
||***********************************************************

|PROCESO leecl_y_cobro;
 || .............. Lee la ficha del Cliente para obtener los datos necesarios
 |DDEFECTO #4;
 #4#23 = "C";
 #4#10 = #3#11;
 #4#11 = #3#12;
 |LEE 001#4=;
 |SI FSalida ! 0;
     #4#23 = "P";
     #4#10 = #3#11;
     #4#11 = #3#12;
     |LEE 001#4=;
     |SI FSalida ! 0; swERROR = 1; |FINSI;
 |FINSI;

 |DDEFECTO #5;
 #5#0 = #3#2;
 #5#1 = #3#3;
 #5#2 = #3#4;
 #5#3 = #3#5;
 |LEE 001#5=;

 |DDEFECTO #6;
 #6#0  = #3#2;  ||libro
 #6#2  = #3#3;
 #6#27 = #3#4;
 |LEE 001#6=;
|FPRC;

|PROCESO grabarecib; ||Calculo registros individuales;
  swERROR = 0;

  |HAZ leecl_y_cobro;
  |SI swERROR = 1; |ACABA; |FINSI;   || Salta este Cobro. NO el resto

  ca1 = "";                 || Referencia Cliente
  |SI #0#4 = "S";
      ca1 = #4#9;
      CampoDni = ca1; |HAZ LetraDni; ca1 = CampoDni;
  |SINO;
      ca1 = #4#0;
  |FINSI;

  |QBF ca1;
  |SI ca1 = ""; swERROR = 1; |FINSI; || ..... |ERROR10; |ACABA; |FINSI;
  ca1 = ceros + ca1; ca1 = ca1 % -112;

  ca2 = #4#1 + libre43;                                     || Nombre Cliente
  ca2 = ca2 % 140;

  ccc = #5#20;  |QBF ccc;  |HAZ cccBanco;         || Datos del banco del Cobro
  swdomi = 0;
  |SI ccc = ""; swdomi = 1; |FINSI; || ....... |ERROR10; |ACABA; |FINSI;
  traba = ccc % 0;
  nume = traba;
  |SI nume ! 10;  swdomi = 1;  |FINSI;
  ca3 = ceros + ccc; ca3 = ca3 % -110;            || C.C.C.

  ccc = #5#19;  |QBF ccc;  |HAZ cccBanco;
  |SI ccc = ""; swdomi = 1; |FINSI; || ....... |ERROR10; |ACABA; |FINSI;
  traba = ccc % 0;
  nume = traba;
  |SI nume ! 10;  swdomi = 1;  |FINSI;
  ca4 = ceros + ccc; ca4 = ca4 % -110;            || Cuenta corriente  Cliente

  Cantidad = #3#7; |HAZ Conversion;               || Importe en pesetas
  ca5 = Cantidad; |QBF ca5;
  ca5 = ceros + ca5; ca5 = ca5 % -110;

  |SI #1#24 = "S";
      ca6 = #6#7;
  |SINO;
      ca6 = ConCli + " ";
      |SI #1#17 = "S";
          alfa1 = #3#4; |QBF alfa1;
          ca6 = ca6 + alfa1;
          alfa1 = #3#3; |QBF alfa1;
          ca6 = ca6 + alfa1 + " ";
      |FINSI;
      |SI #1#18 = "S";
          alfa1 = #3#5; |QBF alfa1;
          ca6 = ca6 + alfa1 + " ";
      |FINSI;
      |SI #1#19 = "S";
          alfa1 = #5#6; |QBF alfa1;
          ca6 = ca6 + alfa1 + " ";
      |FINSI;
  |FINSI;
|| ..........  |SI #1#20 = "S"; alfa1 = #5#28; |QBF alfa1; ca6 = ca6 + alfa1; |FINSI; Referencia de la 6.00
  ca6 = ca6 + libre43; ca6 = ca6 % 140;            || Concepto

  alfa2 = #3#6;
  ca7   = alfa2 % 102;
  ca7   = ca7 + (alfa2 % 402);
  ca7   = ca7 + (alfa2 % 902);

  ca8   = #4#2; ca8 = ca8 + libre43; ca8 = ca8 % 140;  || Domicilio
  ca9   = #4#6; ca9 = ca9 + libre43; ca9 = ca9 % 135;  || Poblacion

  alfa2 = #4#3; |QBF alfa2; alfa2 = (ceros + alfa2) % -102;
  alfa3 = #4#4; |QBF alfa3; alfa3 = (ceros + alfa3) % -103;
  ca10  = alfa2 + alfa3;                                     || Codigo

  || ... Registro Individual Obligatorio (0670) ........................
  |SI swdomi = 1;
      ca3 = "          ";
      ca4 = "          ";
  |FINSI;
  varalfa1 = "";

  varalfa1 = cabe3 + NifO + ca1 + ca2 + ca3 + ca4 + ca5;
  varalfa1 = varalfa1 + #0#5 + #0#6 + ca6 + ca7 + "  ";

  contenido = handle + " " + varalfa1 + &#0#7 + &#0#8;
  |FGRABA contenido;

  trecibos = trecibos + 1;
  l = l + 1;
  timporte = timporte + Cantidad;

  || ... Registro Individual Obligatorio (0676) ........................
  |SI swdomi = 1;  |O #1#23 = "S";
      varalfa1 = "";
      varalfa1 = cabe4 + NifO + ca1 + ca8 + ca9 + ca10 + LocO + ProP;
      varalfa1 = varalfa1 + fechapre + libre8;
      contenido = handle + " " + varalfa1 + &#0#7 + &#0#8;
      |FGRABA contenido;
      l = l + 1;
      swdomi = 0;
  |FINSI;
|FPRC;

|PROCESO regindiv;
  trecibos = 0;
  timporte = 0;
  |BUCLELIN 0grabarecib#2;
  |SI swERROR = 1;
      |MENAV "    ATENCION !!!. DATOS DE CLIENTES INCOMPLETOS. ";
||       |MENAV "    ATENCION !!!. DATOS DE CLIENTES INCOMPLETOS. OPERACION ABORTADA ";
  |FINSI;
  |SI trecibos ! 0;
      #2#8 = "D";
      |GRABA 020#2a;
      |LIBERA #2;
  |FINSI;
|FPRC;

||***********************************************************
||********** REGISTRO TOTAL ORDENANTE Y GENERAL *************
||***********************************************************
|PROCESO campo_totales;

|| **** REGISTRO TOTAL ORDENANTE

  ca1 = timporte; |QBF ca1;
  ca1 = ceros + ca1; ca1 = ca1 % -110;

  ca2 = trecibos; |QBF ca2;
  ca2 = ceros + ca2; ca2 = ca2 % -110;

  l = l + 1;

  ca3 = l; |QBF ca3;
  ca3 = ceros + ca3; ca3 = ca3 % -110;

  varalfa1 = "";
  varalfa1 = tord1 + NifO + libre72 + ca1 + libre6;
  varalfa1 = varalfa1 + ca2 + ca3 + libre38;

  contenido = handle + " " + varalfa1 + &#0#7 + &#0#8;
  |FGRABA contenido;

|| **** REGISTRO TOTAL GENERAL

  l = l + 2; || Sumamos este ultimo y el primero.

  ca3 = l; |QBF ca3;
  ca3 = ceros + ca3; ca3 = ca3 % -110;   || Numero total de Registros

  ca4 = "0001";                          || Numero de Ordenantes

  varalfa1 = tgen1 + NifP + libre52 + ca4 + libre16;
  varalfa1 = varalfa1 + ca1 + libre6 + ca2 + ca3 + libre38;

  contenido = handle + " " + varalfa1 + &#0#7 + &#0#8;
  |FGRABA contenido;

|FPRC;

|| **************************************************************************

|PROCESO pasecuencial; |TIPO 3;

  |ABRE #2;
  |HAZ leerem;
  |SI swErrorR = 1; |O swErrorR = 2;
      |MENAV "    ERROR !!! REMESA NO CORRECTA ";
      |CIERRA #2;
      |ACABA;
  |FINSI;

  |HAZ leepas;
  |SI swErrorP ! 0;
      |MENAV "    ERROR !!! CONTROL DE PASE NO CORRECTO ";
      |CIERRA #2;
      |ACABA;
  |FINSI;

  |HAZ varia_fich;

  |CAMPO_VISUAL #0#10,1,"S"; #0#10 = 0;
  |CAMPO_VISUAL #0#11,1,"S"; #0#11 = 0;
  |CAMPO_VISUAL #0#12,1,"S"; #0#12 = 0;

  |ABRE #camoneda;
  #camoneda#0 = #48#2;
  #camoneda#1 = #48#3;
  |LEE 000#camoneda.=;
  |SI FSalida = 0;
     Moneda = #camoneda#2;
     |SI #camoneda#2 = "P";
        MonBase = 1;
     |SINO;
        MonBase = 2;
     |FINSI;
  |SINO;
     Moneda = "P";
     MonBase = 1;
  |FINSI;
  |CIERRA #camoneda;

  |HAZ PideMoneda;

  |SI MonBase = 1;
     EURO = 1 / 166.386;
     EURODB = 0;
     EURODV = 2;
  |SINO;
     EURO = 166.386;
     EURODB = 2;
     EURODV = 0;
  |FINSI;

|| ...................................................
  |ABRE #4;                || Abre Clientes / Proveedores
  |ABRE #5;                || Cobros
  |ABRE #6;                || IVA Repercutido
|| Abrir Fichero Secuencial

  |DFICE empresa;
  fiche = "wb  " + empresa + "casecrec.seq";  || "nomfiche.ext";
  |QBF fiche;
  |FABRE fiche;
  handle = FSalida;
  |SI FSalida ] 0;
      |HAZ cabecera_p;
      |HAZ cab_ordenante;
      |HAZ regindiv;
      ||   |SI swERROR = 0;
               |HAZ campo_totales;
      ||   |FINSI;
  |FINSI;
  contenido = handle + " " + &#0#9;
  |FGRABA contenido;

  FSalida = handle;
  |FCIERRA FSalida;

  Pos = -1;
  |CIERRA #6;
  |CIERRA #5;
  |CIERRA #4;
  |CIERRA #2;

|| ...   |SI swERROR = 1; |ACABA; |FINSI;

  |D_CUADRO 1261, 2280;
  |ATRI R;
  |PINTA 1362, " Numero Recibos   ";
  |PINTA 1562, " Numero Registros ";
  |PINTA 1762, " Importe Total    ";
  |ATRI 0;
  |PINTA 1462, "                  ";
  |PINTA 1662, "                  ";
  |PINTA 1862, "                  ";
  |D_CUADRO 1961, 2280;
  |ATRI R;
  |PINTA 2062, " Desea grabar el  ";
  |PINTA 2162, " diskete ahora    ";
  |ATRI 0;

  #0#10 = trecibos;  |PINTA #0#10;
  #0#11 = l;         |PINTA #0#11;
  |SI Moneda = "E";
     Calc = EURODB;
     EURODB = 2;
     #0#12 = timporte / 100;
  |SINO;
     Calc = EURODB;
     EURODB = 0;
     #0#12 = timporte;
  |FINSI;
  |PINTA #0#12;
  EURODB = Calc;

  programa = " REMESAS ";
  ficcopia = "casecrec.seq";
  E_sup = 1;
  E_inf = "SNsn";
  sino  = "N";
  sino  = 2177 ? 1;
  |SI sino = "S"; |O sino = "s"; |SUB_EJECUTA "cared003.run"; |FINSI;
|FPRC;


|| ********** Rutinas Varias

|PROCESO LetraDni;
     eaVarDni   = CampoDni;
     enCalcCif  = 3;
     |HAZ CalculoDNI;
     CampoDni = eaVarDni;
     CampoDni = ("000000000" + CampoDni) % -109;
|FPRC;

|PROCESO cccBanco;
 |QBT ccc;
 c_alfa1 = ccc % 0;
 c_nume1 = c_alfa1;
 c_alfa1 = "";
 c_alfa2 = "";
 |PARA c_nume2 = 1; |SI c_nume2 [ c_nume1; |HACIENDO c_nume2 = c_nume2 + 1;
       c_nume3 = c_nume2 * 100 + 1;
       c_alfa2 = ccc % c_nume3;
       |SI c_alfa2 ] "0"; |Y c_alfa2 [ "9";
           c_alfa1 = c_alfa1 + c_alfa2;
       |FINSI;
 |SIGUE;
 ccc = c_alfa1;
|FPRC;

|PROCESO varia_fich;
|| .......... Variables del Presentador, del Ordenante y comunes
  NombreP = #1#4 + libre43;     NombreP = NombreP % 140;

  CampoDni = #1#2; |HAZ LetraDni;
  NifP    = CampoDni;
  sufiP   = #1#3; |QBT sufiP;
  sufiP   = ceros + sufiP;      sufiP = sufiP % -103;
  NifP    = NifP + sufiP;

  EntP    = #1#5; |QBT EntP;
  EntP    = ceros + EntP;       EntP    = EntP % -104;

  SucP    = #1#6; |QBT SucP;
  SucP    = ceros + SucP;       SucP    = SucP % -104;

  banAlfa = #1#7 + libre43;     banAlfa = banAlfa % 111;

  NombreO = #1#10 + libre43;    NombreO = NombreO % 140;

  CampoDni = #1#8; |HAZ LetraDni;
  NifO    = CampoDni;
  sufiO   = #1#9; |QBT sufiO;
  sufiO   = ceros + sufiO;      sufiO = sufiO % -103;
  NifO    = NifO + sufiO;

  EntO    = #1#11; |QBT EntO;
  EntO    = ceros + EntO;       EntO    = EntO % -104;

  SucO    = #1#12; |QBT SucO;
  SucO    = ceros + SucO;       SucO    = SucO % -104;

  dcO     = #1#13; |QBT dcO;
  dcO     = ceros + dcO;        dcO     = dcO % -102;

  CtaO    = #1#14; |QBT CtaO;
  CtaO    = ceros + CtaO;       CtaO    = CtaO % -110;

  CtaO    = CtaO + #1#14;       CtaO    = CtaO % -110;

  LocO    = #1#21 + libre43;    LocO    = LocO % 138;

  ProP    = #1#22; |QBT ProP;
  ProP    = ceros + ProP;       ProP    = ProP % -102;

  ConCli = #1#16; |QBF ConCli;

  alfa1 = #0#3;
  dd = alfa1 % A102;
  mm = alfa1 % A402;
  aa = alfa1 % A902;
  fechapre = dd + mm + aa;   || Fecha Formato dia + mes + a¤o

  fechacar = libre6; || Fecha Cargo = en Blanco
  |SI #0#14 = "S";
      alfa1 = #0#15;
      dd = alfa1 % A102;
      mm = alfa1 % A402;
      aa = alfa1 % A902;
      fechacar = dd + mm + aa;   || Fecha Cargo Formato dia + mes + a¤o
  |FINSI;

  tiProced = #0#13; || Tipo Procedimiento

|FPRC;

|PROCESO PideMoneda;
   |PUSHV 1943 2177;
   |BLANCO 1943 2177;
   |CUADRO 1945 2175;
   |ATRI R; |PINTA 1946 , " Seleccione moneda de emision "; |ATRI N;
   |PINTA 2053, "(Pesetas/Euros)";
   |ATRI I; |PINTA 2054, "P"; |PINTA 2062, "E"; |ATRI N;
   E_sup  = 1;
   E_inf  = "EP";
   |SI MonBase = 1; Moneda = "P"; |SINO; Moneda = "E"; |FINSI;
   Moneda = 2069 ? 0;
   |POPV;
|FPRC;

|PROCESO Conversion;
   |SI MonBase = 1;
      |SI Moneda = "E";
         Cantidad = ((Cantidad / 166.386) ! 2) * 100;
      |FINSI;
   |SINO;
      |SI Moneda = "P";
         Cantidad = (Cantidad * 166.386) ! 0;
      |SINO;
         Cantidad = Cantidad * 100;
      |FINSI;
   |FINSI;
|FPRC;
