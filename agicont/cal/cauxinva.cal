|FICHEROS;
     cauxinva #0;
     cauxinv2 #1;

     cabucem1 #100;
|FIN;

|VARIABLES;
     informe = "cauxinva";
     &doh = "";
     &aux_letra = 0;
     &aux_desc  = "";
     &aux_imp   = 0;
     &variable  = "";
     &cantidad  = 0;
     &canti     = 0;

     num_linea = 0;

     swElPie = 0;

     aAlfa    = "";
     Comodin  = "";
     Comodin1 = "";
     Numeracion = 0;
     Handle    = 0;
     &enBasImp = 0;
     &eaNomImp = "";
     &eaLegEmp = "";
     &eaLegPer = "";
     &eaUnidadBasico = "";
     &eaUnidadBTmp   = "";
|FIN;

|INCLUYE acceso_i;

|PROCESO Empresas;

   #cabucem1#0 = eaLegEmp;
   #cabucem1#1 = eaLegPer;

   cod1 = eaLegEmp;
   cod2 = eaLegPer;
   emEmp = 1;
   |HAZ leelo;
   |SI swerror ! 0;  |ACABA;   |FINSI;

   |DBASE fich_alta; |QBF fich_alta;
   fich_alta = fich_alta + "fich/" + (("00000" + #cabucem1#0) % -105) + #cabucem1#1 + "/";
   |PATH_DAT #0 fich_alta; |PATH_DAT #1 fich_alta;
   |HAZ impresion;

|FPRC;

/*
|DEFBUCLE Empresas;
#cabucem1#1;
;
INICIO;
FINAL;
;
NULL,Empresas;
|FIN;
*/

|PROGRAMA;
  |SI PARAMETRO = "";
     |HAZ inicio;
     |CLS;
     |CABEZA "Listado Inventario";
     |PINPA #0#0;
     |ENTLINEAL #1#0 , 1 , 2 , 20 , 1 , pro1 , pro2;
     |AVISO;
     |CONFI "2400NDesea Imprimir el Inventario : ";
     |SI FSalida = 0;
          |CIERRA #0;
          |HAZ impresion;
     |FINSI;
  |SINO;
     |HAZ Empresas;
  |FINSI;
|FPRO;

|PROCESO pro1;
     #0#0 = 1;
|FPRC;

|PROCESO pro2;
     #0#0 = 999999;
|FPRC;

|PROCESO linea11; |TIPO 11;
     |HAZ linea;
|FPRC;

|PROCESO linea;
  |SI FSalida ! 9;  |ACABA;  |FINSI;
  |PUSHV 0815 1845;
  |ATRI N;
  |CUADRO 8 15 18 45;
  |PINTA 0916 ,  "1-LINEA SUBCABECERA          ";
  |PINTA 1016 ,  "2-LINEA NORMAL SIN IMPORTE   ";
  |PINTA 1116 ,  "3-LINEA NORMAL CON IMPORTE   ";
  |PINTA 1216 ,  "4-LINEA EN BLANCO            ";
  |PINTA 1316 ,  "5-LINEA AMORTIZACION         ";
  |PINTA 1416 ,  "6-ROTURA NIVEL GENERAL       ";
  |PINTA 1516 ,  "7-SALTO DE PAGINA            ";
  |PINTA 1616 ,  "8-ROTURA NIVEL CUENTAS       ";
  |PINTA 1716 ,  "9-LINEA CABECERA             ";
  |ATRI 0;
  |PAUSA;
  |POPV;
  |ERROR;
|FPRC;

|PROCESO impr;
  |SI #0#1 = 0;
       doh = #0#2;
       |ACABA;
  |FINSI;

  aux_letra = #0#1;
  aux_desc  = #0#2;
  aux_imp   = #0#3;

  |SI #0#1 = 8; |O #0#1 = 7;
      |ACABA;
  |FINSI;

  |SI #0#1 = 6;
      variable  = aux_desc;
      canti     = aux_imp;
      |PIEINF;
      swElPie = 1;
      |ACABA;
  |FINSI;

  |PRINT;
  swElPie = 0;
|FPRC;

|DEFBUCLE imprime;
#0#1;
;
     0;
999999;
e;
NULL , impr;
|FIN;

|PROCESO impresion;
  |SI PARAMETRO ! "";
      Impresora = eaNomImp + "?" + eaUnidadBTmp + "\tmp1,,,?Inventario";

      |SI enBasImp ! 0;
          |IMPRE -1;
          |SI FSalida ! 0;
              Impresora = Impresora - eaNomImp;
              |IMPRE 0;
          |FINSI;
      |SINO;
          |IMPRE 0;
      |FINSI;
  |SINO;
      |IMPRE 0;
  |FINSI;

  |SI FSalida ! 0;  |ACABA;  |FINSI;
  |INFOR informe;
  |SI FSalida ! 0;  |FINIMP;  |ACABA;  |FINSI;

  num_linea = 0;
  |ABRE #1;
  |DDEFECTO #1;
  |LEE 001#1=;

  |BUCLE imprime;

  |SI swElPie = 0; |PIEINF; |FINSI;
  |FININF;
  |FINIMP;
  |CIERRA #1;
|FPRC;
