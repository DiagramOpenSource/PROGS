|FICHEROS;
    n43datos #04;         || Fichero Recepcion.
    n43auxil #44;         || Fichero Recepcion. Auxiliar.
    camanban #01;         || Bancos Contabilidad.
    n43recep #02;         || Este Def.(Fichero Secuencial).
    n43saldo #05;         || Saldos Iniciales.
    n43incid #10;         || Pantalla Incidencias.
    n43infor #11,MANTE;   || Informacion Datos Diskette....
    camaasil #21;         || Lineas Apuntes....

    n43temp@ #900;
|FIN;

|VARIABLES;
    nNume1   = 0;
    aFichero = "";
    fiche    = "";
    handle   = 0;

    aEl10    = "";
    aEl13    = "";
    aAlfa  = "";
    aAlfa1 = "";
    aAlfa2 = "";

    nCalc  = 0;
    n_Conta = 0;
    n_salan = 0;
    n_sigsa = "";
    dfec = "01.01.0000";
    dia  = 0;
    hfec = @;
    sali = 0;
    salf = 0;
    nSal = 0;
    descon  = "";
    sistema = "";
    fich    = "";
    alfa = "";
    tipo = "";
    vic  = 0;
    salida = 0;
    unida = "";
    banco = "";            || clave 11
    oficina = "";
    cuenta = "";
    fecini = "";
    fecfin = "";
    dhcta = "";
    a_saldo = "";
    descrip = "";

    fecope = "";           || clave 22
    d_abajo = 0;
    h_abajo = 0;
    fecval = "";
    concep = "";
    dhmov = "";
    a_importe = "";
    documen = "";
    n_Regd  = 0;
    n_Regr  = 0;
    concep1 = "";          || clave 23
    concep2 = "";

    dhfinal = "";
    a_slfinal = "";


    banco_t = "";
    ofici_t = "";
    cuent_t = "";
    dhfin_t = "";
    contable = "";
    digitoco = 0;

    tranum1 = 0;
    tranum2 = 0;
    traalf1 = "";
    traalf2 = "";
    traalf3 = "";
    fectra = "";
    jn = 0;
   f = "";
   espacio = " ";
   menos = -1;
   reg = 0;
   saldo_ini1 = "";
   saldo_ini2 = 0;
       valida = "0000NContinuar S/N "
        swini = 0;
      salcued = 0;
      salcueh = 0;
       salcue = 0;
   dhsald_ini = "";
      importe = 0;
     importe2 = 0;
     ultimo = 0;
     sw_eof = 0;
     swcuenta = 0;
     totreg_t = 0;
     saldo = 0;
     apund_t = 0;
     impod_t = 0;
     apunh_t = 0;
     impoh_t = 0;
     slfin_t = 0;
     apunh = 0;
     impoh = 0;
     apund = 0;
     impod = 0;
     slfinal = 0;
     totreg = 0;
     aAn    = "";
     nVic   = 0;

     aMas    = "";
     aDef    = "";
     nSwDigo = 0;
     sw      = 0;

     aBase   = "";
     nCopio  = 0;
     &eaOrigen  = "";
     &eaDestino = "";
     &enErrPas  = 0;
|FIN;

|INCLUYE acceso_i;

|CALCULO PonBarra; |TIPO 0;
     aAlfa1 = #2Campo;
     |QBF aAlfa1;
     aAlfa2 = aAlfa1 % -101;
     |SI aAlfa2 ! "/";|Y aAlfa2 ! "\";
          #2Campo = aAlfa1 + "/";
          |PINTA #2Campo;
     |FINSI;
|FCAL;

|CALCULO mayusc; |TIPO 0;
     #2Campo = #2Campo > "";
     |PINTA #2Campo;
|FCAL;

|PROCESO n43auxil;
       ultimo = ultimo + 1;
        #4#00 = ultimo;
        |LEE 101#4=;
        nSal  = FSalida;
        #4#00 = ultimo;
        #4#01 = #44#1;
        #4#12 = #44#12;
        #4#02 = #44#2;
        #4#03 = #44#3;
        #4#04 = #44#4;
        #4#05 = #44#5;
        #4#06 = #44#6;
        #4#07 = #44#7;
        #4#08 = #44#8;
        #4#09 = "N";
        #4#10 = #44#10;
        #4#11 = "N";
        |SI nSal ! 0; |GRABA 020#4n; |FINSI;
        |SI nSal = 0; |GRABA 020#4a; |FINSI;
        |LIBERA #4;
|FPRC;

|DEFBUCLE n43auxil;
     #44#2;
     ;
     #11#3,#11#20,1
     #11#3,#11#20,999999;
     ;
     NULL,n43auxil;
|FIN;

|PROCESO n43aux;
     |BUCLE n43auxil;
     #5#0 = #11#0;
     |LEE 101#5=;
     nSal = FSalida;
     #5#1 = #11#10;
     #5#2 = #11#9;
     |SI nSal = 0; |GRABA 020#5a; |FINSI;
     |SI nSal ! 0; |GRABA 020#5n; |FINSI;
     |LIBERA #5;
|FPRC;

|DEFBUCLE n43infor;
     #11#1;
     12;
     "          ",  1,"*";
     "zzzzzzzzzz",999,"*";
     ;
     NULL,n43aux;
|FIN;

|PROCESO carga;
     |ABRE #4;
     ultimo = 0;
     |LEE 040#4u;
     |SI FSalida ! 0;
          ultimo = 1;
     |SINO;
          ultimo = #4#0;
     |FINSI;
     |BUCLE n43infor;
     |CIERRA #4;
|FPRC;


|CALCULO cambia_fec;
    traalf1 = fectra % 102;
    traalf2 = fectra % 302;
    traalf3 = fectra % 502;
    nVic    = traalf3;
    |SI nVic ] 90;
         aAn = ".19";
    |SINO;
         aAn = ".20";
    |FINSI;
    fectra  = traalf3 + "." + traalf2 + aAn + traalf1;
|FCAL;

/*
|PROCESO marca; |TIPO 11;
 |SI FSalida = 9;
     |SI #11#12 ! "*";
          #11#12 = "*";
     |SINO;
          #11#12 = "";
     |FINSI;
     |PINTA #11#12;
     |GRABA 020#11a;
     |LIBERA #11;
 |FINSI;
|FPRC;
*/

|PROCESO n43datos;
  n_Regr = n_Regr + 1;
  |SI #4#6 = "D";
      apund_t = apund_t + #4#7;
  |FINSI;
  |SI #4#6 = "H";
      apunh_t = apunh_t + #4#7;
  |FINSI;
|FPRC;

|DEFBUCLE n43datos;
     #4#2;
     2;
     #11#3,000001,#11#5;
     #11#3,999999,#11#6;
     ;
     NULL,n43datos;
|FIN;

|PROCESO importado;
     apund_t = 0;
     apunh_t = 0;
     n_Regr  = 0;
     |BUCLE n43datos;
|FPRC;

|PROCESO buclepan;
  |SI #21#8 = "D";
      d_abajo = d_abajo + #21#9;       || ACUMULADO DEBE FINAL
  |SINO;
      h_abajo = h_abajo + #21#9;       || ACUMULADO HABER FINAL
  |FINSI;
|FPRC;


|DEFBUCLE caextrp;
  #21#3;
  ;
  #11#3,#11#4,dfec,      1;
  #11#3,#11#4,hfec, 999999;
  ;
  NULL,buclepan;
|FIN;

|PROCESO extracto;
     d_abajo = 0;
     h_abajo = 0;
     dia = #11#5;
     dia = dia -1;
     hfec = dia;
     |BUCLE caextrp;
|FPRC;

|PROCESO sacasal;
     #5#0 = cuenta;
     |LEE 040#5=;
     |SI FSalida = 0;
          n_salan = #5#1;
          n_sigsa = #5#2;
     |SINO;
          n_salan = 0;
          n_sigsa = "";
     |FINSI;
|FPRC;


|CALCULO tipo_11;
    banco   = alfa % 0304;
    oficina = alfa % 0704;
    cuenta  = alfa % 1110;
    aAlfa   = banco + oficina;
    #1#9 = aAlfa; #1#8 = "";

    |SELECT #3#1;
    |LEE 040#1];
    |PARA; |SI FSalida = 0; |HACIENDO;
           aAlfa1 = #1#9; |QBF aAlfa1; aAlfa1 = aAlfa1 % 0108;
           aAlfa2 = #1#8; |QBF aAlfa2;
           |SI aAlfa1 = aAlfa; |Y cuenta = aAlfa2;
               |SAL;
           |FINSI;
           |LEE 040#1s;
    |SIGUE;
    |SI FSalida = 0;
        contable = #1#10;   ||Cuenta Contable.............................
        digitoco = #1#11;   ||Digito Contable.............................
    |SINO;
        contable = "";      || no ha encontrado la cta contable de esta
        digitoco = 0;       || cuenta corriente del banco
    |FINSI;
    fecini  = alfa % 2106; fectra = fecini; |HAZ cambia_fec; fecini=fectra;
    fecfin  = alfa % 2706; fectra = fecfin; |HAZ cambia_fec; fecfin=fectra;
    dhcta   = alfa % 3301;
    a_saldo = alfa % 3414; saldo = a_saldo; saldo = saldo / 100;
    descrip = alfa % 5226;
    |SI dhcta = "1";  dhcta = "H";  |FINSI;
    |SI dhcta = "2";  dhcta = "D";  |FINSI;
    ||____________________ Comprobacion Saldo.

    nSwDigo = 0;
    nCalc   = 0;
    |DDEFECTO #5;
    #5#0 = cuenta;
    |LEE 000#5=;
    |SI FSalida ! 0; nSwDigo = 2; |FINSI;

    nCalc  = #5#1 ! EURODB;
    |ABRE #900;
    #900#0 = cuenta;
    |LEE 001#900=;
    |SI FSalida ! 0;
        #900#0 = cuenta;
        #900#1 = #5#1;
        #900#2 = #5#2;
        |GRABA 020#900n;

        |SI dhcta ! #5#2; |O saldo ! nCalc;
            |SI nSwDigo = 0; nSwDigo = 1; |FINSI;
        |FINSI;
    |SINO;
        nSwDigo = 0;
    |FINSI;
    |CIERRA #900;

   |SI nSwDigo ! 0;  || ... |SI FSalida ! 0; |O dhcta ! #5#2; |O saldo ! nCalc;

          #10#0 = cuenta; #10#5 = cuenta;
          #10#1 = #5#2;
          #10#2 = nCalc;
          #10#3 = dhcta;  #10#6 = dhcta;
          #10#4 = saldo;  #10#4 = saldo;
          |PUSHV 0501 2380;
          |SI nSwDigo = 1;
              |PINPA #0#10;
              |PINDA #0#10;
          |SINO;
              |PINPA #1#10;
              |PINDA #1#10;
          |FINSI;
          |PAUSA;
          |CONFI "0000NContinuar con la recepcion de esta Cuenta";
          |SI FSalida = 0;
               sw_eof = 0;
          |SINO;
               sw_eof = 1;
          |FINSI;
          |POPV;
    |FINSI;
    impod_t = 0;
    impoh_t = 0;
    n_Regd  = 0;
    n_Conta = n_Conta + 1;
    |SELECT #1#1;
    |SI sw_eof = 0; sw = 1; |FINSI;
|FCAL;

|CALCULO tipo_22;
    fecope  = alfa % 1106;  fectra = fecope; |HAZ cambia_fec; fecope = fectra;
    fecval  = alfa % 1706;  fectra = fecval; |HAZ cambia_fec; fecval = fectra;
    concep  = alfa % 2503;
    descon  = alfa % 5328;  || Descripcion Posible Banco.
    dhmov   = alfa % 2801;
    a_importe=alfa % 2914;  importe = a_importe; importe2 = importe / 100;
    documen = alfa % 4310;
    |SI dhmov = "1";  dhmov = "H";  |FINSI;
    |SI dhmov = "2";  dhmov = "D";  |FINSI;

    |SI dhmov = "D";
       impod_t = impod_t + importe;
       |SI dhcta = "D";
           slfin_t = slfin_t + importe;
       |SINO;
           slfin_t = slfin_t - importe;
       |FINSI;
   |FINSI;
   |SI dhmov = "H";
       impoh_t = impoh_t + importe;
       |SI dhcta = "H";
           slfin_t = slfin_t + importe;
       |SINO;
           slfin_t = slfin_t - importe;
       |FINSI;
   |FINSI;

  |SI dhmov = "D"; |O dhmov = "H";
       n_Regd = n_Regd + 1;
       ultimo = ultimo + 1;
       #44#00 = ultimo;
       #44#01 = contable;
       #44#12 = digitoco;
       #44#02 = fecope;
       #44#03 = fecval;
       #44#04 = concep;
       #44#05 = descon;
       #44#06 = dhmov;
       #44#07 = importe2;
       #44#08 = documen;
       #44#09 = "N";
       #44#10 = descrip;
       #44#13 = n_Conta;
       |GRABA 020#44n;
   |FINSI;
|FCAL;

|CALCULO tipo_23;
    concep1 = alfa % 0538; |QBF concep1;
    concep2 = alfa % 4338;
    |SI ultimo > 0;
       #44#00 = ultimo;
       |LEE 001#44=;
       |SI FSalida = 0;
           #44#05 = concep1 + " " + concep2;
           |GRABA 020#44a;
       |FINSI;
   |FINSI;
|FCAL;

|CALCULO tipo_33;
    dhfinal = alfa % 5901;
    a_slfinal=alfa % 6014;  slfinal = a_slfinal;  slfinal = slfinal / 100;

    |SI dhfinal = "1";  dhfinal = "H";  |FINSI;
    |SI dhfinal = "2";  dhfinal = "D";  |FINSI;

    #11#0 = cuenta;
    #11#20 = n_Conta;
    |LEE 101#11=;
    nSal = FSalida;
    #11#1 = banco;
    #11#2 = oficina;
    #11#3 = contable;
    #11#4 = digitoco;
    #11#5 = fecini;
    #11#6 = fecfin;
    #11#7 = dhcta;
    #11#8 = saldo;
    #11#9 = dhfinal;
    #11#10 = slfinal;
    #11#12 = "";
    #11#14 = n_Regd;
    #11#13 = impod_t - impoh_t;
    #11#13 = #11#13 / 100;
    |HAZ importado;
    #11#15 = n_Regr;
    #11#11 = apund_t - apunh_t;
    |HAZ extracto;
    #11#16 = d_abajo - h_abajo;
    |SI #11#16 < 0;
          #11#16 = #11#16 * -1;
          #11#17 = "H";
    |SINO;
          #11#17 = "D";
    |FINSI;
    |HAZ sacasal;
    #11#18 = n_salan;
    #11#19 = n_sigsa;
    |SI nSal = 0; |GRABA 020#11a; |FINSI;
    |SI nSal ! 0; |GRABA 020#11n; |FINSI;
    |LIBERA #11;
|FCAL;

|PROCESO PasoRegistro;
     nNume1 = nNume1 + 1;
     |ATRI P; |PINTA 2314, "Pasando registros     ";
              |PINTA 2333, nNume1; |ATRI 0;
     tipo = alfa % 102;
     |SI tipo = "11"; |HAZ tipo_11;  |FINSI;
     |SI tipo = "22"; |HAZ tipo_22;  |FINSI;
     |SI tipo = "23"; |HAZ tipo_23;  |FINSI;
     |SI tipo = "33"; |HAZ tipo_33;  |FINSI;
|FPRC;

|PROCESO nombre;  || nombre del fichero
     fiche   = unida + traalf1;

     Pos   = -1;
     nCopio = 0;
     |XABRE "UB", fiche, handle;
     |SI FSalida < 0;
         |XABRE "CUB", fiche, handle;
         |SI FSalida = 0; nCopio = 1; |FINSI;
     |FINSI;
     |XCIERRA handle;

     |SI nCopio = 1;
         |DFICE aBase;
         eaOrigen  = fiche;
         eaDestino = aBase + traalf1;
         |HAZ eReciboFich;
         |SI enErrPas = 0; fiche = eaDestino; |FINSI;
     |FINSI;
|FPRC;

|PROCESO proce;

     unida = #2#0; |QBF unida;

     aFichero = "44z" + Usuario;
     |NOME_DAT #44 aFichero;
     |DELINDEX #44;

     |DBASE aMas;
     aDef = aMas + "def/n43saldo";
     |CARGA_DEF aDef, n43temp@;
     |SI FSalida ! 0;
         |ACABA;
     |FINSI;

     aFichero = "nt" + Usuario;
     |NOME_DAT #900 aFichero;
     |DELINDEX #900;

     aEl13 = &13;
     aEl10 = &10;
     |ABRE #1;
     |ABRE #44;
     |ABRE #11;

     |PARA vic = 1; |SI vic [ 20; |HACIENDO vic = vic + 1;
           jn = vic + 20;
           |SI #2jn = "S";
               traalf1 = #2vic; |QBF traalf1;
               |SI traalf1 ! "";
                   |HAZ nombre;
                               || fiche = unida + traalf1;
                   |XABRE "UB", fiche, handle;
                   |SI FSalida < 0;
                       |XABRE "CUB", fiche, handle;
                   |FINSI;
                   |SI FSalida ] 0;
                       sw_eof = 0;
                       alfa   = "";
                       |PARA; |SI sw_eof = 0; |HACIENDO;

                              |XLEE handle, 1, aAlfa1;
                              |SI FSalida > 0;
                                  |SI aAlfa1 = aEl13; |O aAlfa1 = aEl10;
                                     |SI alfa ! "";
                                         |HAZ PasoRegistro;
                                         alfa = "";
                                     |FINSI;
                                 |SINO;
                                      alfa = alfa + aAlfa1;
                                 |FINSI;
                              |SINO;
                                  sw_eof = 1;
                              |FINSI;
                       |SIGUE;
                       |SI alfa ! ""; |HAZ PasoRegistro; |FINSI;
                       |XCIERRA handle;
                       Pos = menos;
                    |FINSI;
                |FINSI;
           |FINSI;
     |SIGUE;
     |CIERRA #11;
     |CIERRA #44;
     |CIERRA #1;
|FPRC;

|RUTINA inf;
     #11#0  = "          ";
     #11#20 = 1;
|FRUT;

|RUTINA sup;
     #11#0  = "zzzzzzzzzz";
     #11#20 = 999;
|FRUT;

|PROGRAMA;
     |HAZ inicio;
     |CLS;
     |CABEZA "Recepcion Datos";

     |ABRE #2;
     #2#41 = 1;
     |LEE 000#2=;
     salida = FSalida;
     |SI salida ! 0;
          |DDEFECTO #2;
     |FINSI;

     |PINPA #0#2;
     |PINDA #0#2;
     |ENDATOS #1#2;
     |SI FSalida = 0;
          |SI salida = 0; |GRABA 020#2a; |FINSI;
          |SI salida ! 0; |GRABA 020#2n; |FINSI;
          sw = 0;
          |ABRE #5;
          |HAZ proce;
          |SI sw = 1;
               |HAZ PonBoton;
               |ENTLINEAL #1#11, -1 , 4, 19 , 1 , inf, sup;
               |HAZ QuitaBoton;
               |HAZ carga;
          |FINSI;
          |CIERRA #5;

          |DELINDEX #900;
          |DELINDEX #44;
          |DELINDEX #11;
     |FINSI;
|FPRO;
