|FICHEROS;
    caivexpl #0;
    cacuenta #1;         || cuentas
    caparexp #2;         || partidas de explotacion
    cabaexp2 #5;         || temporal situacion
    caexistc #6;         || descarga cabs.
    caexistl #7;         || descarga lins.
    caconimp #8;

    caseslin #110;
|FIN;

|VARIABLES;

  aFichero = "";
  &Ejercicio = 0;

  &pathbal = "";
  &balance = 1;
  varcamp = 0;
  mes = 0;
  alfa1 = "";
  alfa2 = "";
  alfa3 = "";
  traraf = 0;
  &sw_inf = 0;
  &cantidad = 0;
  &cantidad1 = 0;
  &variable = "";
  &canti = 0;
  &canti1 = 0;
  &doh = "";
  &anu = 0;
  &anu1 = 0;
  &raya1 = "";
  &raya11 = "";
  ray1 = "            -------------------------------------------------------------------"
  ray2 = "                                              ---------------------------------"
  ray3 = "                                              ================================="
  letrames = "";
  x = 0;
  nivelet = 0;
  informe = "";
  sw = 0;
  eldesde = 0;
  elhasta = 0;
  acdesde = 0;
  achasta = 0;
  acumula = 0;
  dir_ficha = "";
  dir_fichb = "";
  saldo1 = 0;
  saldo2 = 0;
  saldo3 = 0;
  saldo4 = 0;
  saldo11 = 0;
  saldo12 = 0;
  saldo13 = 0;
  saldo14 = 0;
  importe = 0;
  descar = 0;
  ult_cuent = "";
  ult_nivel = 0;
  nue_cuent = "";
  nue_nivel = 0;
  act_cuent = "";
  act_nivel = 0;
  carac = 0;
  guarda_can = 0;
  guarda_can1 = 0;
  suma1 = 0;
  suma2 = 0;
  suma3 = 0;
  suma4 = 0;
  suma5 = 0;
  suma6 = 0;
  suma11 = 0;
  suma12 = 0;
  suma13 = 0;
  suma14 = 0;
  suma15 = 0;
  suma16 = 0;
  llegoa = 0;
  nume1 = 0;
  tipo = "";
  &ant_agru = "";
  &ant_epig = "";
  &ant_impo = 0;
  &ant_impo1 = 0;
  t_agru = 0;
  t_agru1 = 0;
  t_epig = 0;
  t_epig1 = 0;
  grupo = 0;
  gru_nou = 0;
  agrupaci = 0;
  epigrafe = 0;
  partida = 0;
  antes = "";
  &r_emp = 0;
  &r_per = 0;
  &d_mes = 0;
  &h_mes = 0;
  &estru = 0;
  &r_eje = 0;
  &r_nom = "";
  mesini = 0;
  aparam = "";
  &eaFicbal = "";
  &eaNomSes = "";
  &enNumSes = 0;
  &eaDepSes = "";
  &eaDesDep = "";
  &enMasDep = 0;
  nswPrim  = 0;
  nSwError = 0;
  nSwPrint = 0;
|FIN;

|INCLUYE acceso_i;
|INCLUYE digito_i;

|PROCESO antesque;
  aparam = PARAMETRO;
  |QBF aparam;
  |SI aparam = "fech";
      |HAZ AntesSesion;
      |NOME_DAT #1 eaFicbal;
  |FINSI;
|FPRC;

|PROCESO PintoSes; |TIPO 7;
  aparam = PARAMETRO; |QBF aparam;
  |SI aparam = "fech";
      |HAZ AntesSesion;
      |SI eaNomSes > "";
          |ATRI I; |PINTA 0505, "SESION: "; |PINTA 0513,eaNomSes;
          |SI eaDepSes > "";
              |PINTA 0558, "Departamento: "; |PINTA 0572, eaDepSes;
          |FINSI;
          |ATRI 0;
      |FINSI;
  |FINSI;
|FPRC;

____________________________________/ CALCULOS DE PANTALLA

|PROCESO pintames; |TIPO 0;
  |SI #0Campo ! 0; |Y #0Campo ! 13;
      mes = #30#11 + (#0Campo - 1);   || Operacion para saber que mes es
      |SI mes > 12; mes = mes - 12; |FINSI;
  |SINO;
      mes = #0Campo;
  |FINSI;
  |HAZ mesletra;                          || Recoge el mes en letra
  |SI Campo > 5;
      |ATRI I;
      |SI Campo =  9;|PINTA 1523, letrames; |FINSI;
      |SI Campo = 10;|PINTA 1623, letrames; |FINSI;
      |SI Campo = 15;|PINTA 1547, letrames; |FINSI;
      |SI Campo = 16;|PINTA 1647, letrames; |FINSI;
      |ATRI 0;
  |SINO;
      varcamp = Campo + 2;
      #0varcamp = letrames;
      |PINTA #0varcamp;                       || Pinta el nombre del mes
  |FINSI;

  |SI Campo = 0; |O Campo = 4; |O Campo = 9; |O Campo = 15;
      |ACABA;
  |SINO;
      varcamp = Campo - 1;
      |SI #0varcamp > #0Campo;
          |MENAV "    Error de Entrada ...";
          |ERROR;
      |FINSI;
  |FINSI;
|FPRC;

|PROCESO mesletra;
  |SI mes = 0;  letrames = "Apertura  "; |FINSI;
  |SI mes = 1;  letrames = "Enero     "; |FINSI;
  |SI mes = 2;  letrames = "Febrero   "; |FINSI;
  |SI mes = 3;  letrames = "Marzo     "; |FINSI;
  |SI mes = 4;  letrames = "Abril     "; |FINSI;
  |SI mes = 5;  letrames = "Mayo      "; |FINSI;
  |SI mes = 6;  letrames = "Junio     "; |FINSI;
  |SI mes = 7;  letrames = "Julio     "; |FINSI;
  |SI mes = 8;  letrames = "Agosto    "; |FINSI;
  |SI mes = 9;  letrames = "Septiembre"; |FINSI;
  |SI mes = 10; letrames = "Octubre   "; |FINSI;
  |SI mes = 11; letrames = "Noviembre "; |FINSI;
  |SI mes = 12; letrames = "Diciembre "; |FINSI;
  |SI mes = 13; letrames = "Cierre    "; |FINSI;
|FPRC;

|PROCESO estruct; |TIPO 0;
  |ABRE #6;
  #6#0 = #0#8;
  |LEE 000#6=;
  |SI FSalida ! 0;
      |MENAV "    No existe la estructura de descarga ...";
      |ERROR;
  |FINSI;
  |CIERRA #6;
|FPRC;

|PROCESO numnivel; |TIPO 0;
  |SI #0Campo > #30#13;
      |MENAV "    El numero de niveles no coincide con el definido en la Empresa ...";
      |ERROR;
  |FINSI;
  nivelet = 0;
  |CAMPO_MODIFICA #0#19,1,"S";
  |CAMPO_MODIFICA #0#20,1,"S";
  |CAMPO_MODIFICA #0#21,1,"S";
  |CAMPO_MODIFICA #0#22,1,"S";
  |CAMPO_MODIFICA #0#23,1,"S";
|FPRC;

|PROCESO nivelet; |TIPO 7;
  |SI #0#17 < 6; #0#23 = 0; |PINTA #0#23; |CAMPO_MODIFICA #0#23,1,"N"; |FINSI;
  |SI #0#17 < 5; #0#22 = 0; |PINTA #0#22; |CAMPO_MODIFICA #0#22,1,"N"; |FINSI;
  |SI #0#17 < 4; #0#21 = 0; |PINTA #0#21; |CAMPO_MODIFICA #0#21,1,"N"; |FINSI;
  |SI #0#17 < 3; #0#20 = 0; |PINTA #0#20; |CAMPO_MODIFICA #0#20,1,"N"; |FINSI;
  |SI #0#17 < 2; #0#19 = 0; |PINTA #0#19; |CAMPO_MODIFICA #0#19,1,"N"; |FINSI;
|FPRC;

||
|PROCESO sacacambio;
  alfa1 = alfa1 + "            ";
  #1#0 = alfa1 % 112;
  #1#1 = x;
  |LEE 000#1=;
  |SI FSalida ! 0;
      #1#0 = alfa1 % 112;
      #1#2 = "NO EXISTE CUENTA EN MAESTRO";
  |FINSI;
  guarda_can = cantidad;
  guarda_can1 = cantidad1;
  |SI x = 1; cantidad = suma1; cantidad1 = suma11; suma1 = 0; suma11 = 0; |FINSI;
  |SI x = 2; cantidad = suma2; cantidad1 = suma12; suma2 = 0; suma12 = 0; |FINSI;
  |SI x = 3; cantidad = suma3; cantidad1 = suma13; suma3 = 0; suma13 = 0; |FINSI;
  |SI x = 4; cantidad = suma4; cantidad1 = suma14; suma4 = 0; suma14 = 0; |FINSI;
  |SI x = 5; cantidad = suma5; cantidad1 = suma15; suma5 = 0; suma15 = 0; |FINSI;
  |SI x = 6; cantidad = suma6; cantidad1 = suma16; suma6 = 0; suma16 = 0; |FINSI;
  sw_inf = 6;
  |PRINT; nSwPrint = 1;
  cantidad = guarda_can;
  cantidad1 = guarda_can1;
  llegoa = x;
|FPRC;


|PROCESO haycambio;
  x = act_nivel - 1;
|ET repetimos;
  |SI x = #0#18;  |O x = #0#19;  |O x = #0#20;  |O x = #0#21;
                  |O x = #0#22;  |O x = #0#23;
      carac = 0;
      |SI x = 6;   carac = dig9;  |FINSI;
      |SI x = 5;   carac = dig8;  |FINSI;
      |SI x = 4;   carac = dig7;  |FINSI;
      |SI x = 3;   carac = dig6;  |FINSI;
      |SI x = 2;   carac = dig5;  |FINSI;
      |SI x = 1;   carac = dig4;  |FINSI;
      |SI carac ! 0;
          nume1 = carac + 100;
          alfa1 = act_cuent % nume1;
          alfa2 = nue_cuent % nume1;
          |SI alfa1 ! alfa2;
              |HAZ sacacambio;
          |FINSI;
      |FINSI;
  |FINSI;
  x = x - 1;
  |SI x ! 0;  |VETE repetimos;  |FINSI;
|FPRC;

|PROCESO cambio1;
  x = llegoa - 1;
  |SI x [ 0;  |ACABA;  |FINSI;
|ET repeti1;
  |SI x = #0#18;  |O x = #0#19;  |O x = #0#20;  |O x = #0#21;
                  |O x = #0#22;  |O x = #0#23;
      carac = 0;
      |SI x = 6;   carac = dig9;  |FINSI;
      |SI x = 5;   carac = dig8;  |FINSI;
      |SI x = 4;   carac = dig7;  |FINSI;
      |SI x = 3;   carac = dig6;  |FINSI;
      |SI x = 2;   carac = dig5;  |FINSI;
      |SI x = 1;   carac = dig4;  |FINSI;
      |SI carac ! 0;
          nume1 = carac + 100;
          alfa1 = act_cuent % nume1;
          |HAZ sacacambio;
      |FINSI;
  |FINSI;
  x = x - 1;
  |SI x ! 0;  |VETE repeti1;  |FINSI;
  llegoa = nivelet;
|FPRC;

|PROCESO rup_tipo;
  |SI #5#0 = "H";
      doh = " H A B E R ";
  |FINSI;
  |SI nSwPrint = 1; |PIEINF; nSwPrint = 0; |FINSI;
  tipo = #5#0;
|FPRC;

|PROCESO rup_agru;
  |SI ant_agru ! "";
      ant_impo = t_agru;
      ant_impo1  = t_agru1;
      raya1 = ray1;
      sw_inf = 10; |PRINT; nSwPrint = 1;  || raya
      sw_inf = 7;  |PRINT; nSwPrint = 1;  || agrupacion (con importe)
      sw_inf = 5;  |PRINT; nSwPrint = 1;  || linea blanco
  |FINSI;
  agrupaci = #5#2;

  t_agru = 0;
  t_agru1 = 0;
|FPRC;

|PROCESO rup_epig;
  |SI ult_nivel ! 1;
      nue_cuent = "";
      nue_nivel = 0;
  |FINSI;

  |SI ant_epig ! "";
      ant_impo = t_epig;
      ant_impo1 = t_epig1;
      raya1 = ray2;
      sw_inf = 10; |PRINT; nSwPrint = 1;  || raya
      sw_inf = 2;  |PRINT; nSwPrint = 1;  || epigrafe
      sw_inf = 5;  |PRINT; nSwPrint = 1;  || linea blanco
  |FINSI;
  epigrafe = #5#3;
  partida = #5#4;

  t_epig = 0;
  t_epig1 = 0;
  ult_cuent = "";
  ult_nivel = 0;
|FPRC;

|PROCESO imprires;
  |SI tipo = "D";
      |SI ant_impo [ 0;
          ant_impo = -ant_impo;
      |SINO;
          ant_impo = 0;
      |FINSI;
      |SI ant_impo1 [ 0;
          ant_impo1 = -ant_impo1;
      |SINO;
          ant_impo1 = 0;
      |FINSI;
  |SINO;
      |SI ant_impo [ 0;
          ant_impo = 0;
      |FINSI;
      |SI ant_impo1 [ 0;
          ant_impo1 = 0;
      |FINSI;
  |FINSI;
  |SI ant_impo = 0;  |Y ant_impo1 = 0;
      |ACABA;
  |FINSI;
  raya1 = ray3;
  sw_inf = 10; |PRINT; nSwPrint = 1;  || raya
  sw_inf = 7;  |PRINT; nSwPrint = 1;  || agrupacion (con importe)
  sw_inf = 5;  |PRINT; nSwPrint = 1;  || linea blanco
  ant_agru = antes;
|FPRC;

|PROCESO beneperdi;
   gru_nou = #5#1;
   |SI #5#0 ! tipo;  || ... para que salte todos
       gru_nou = 9999;
   |FINSI;
  |SI tipo = "D";
      |SI grupo = 1;
          antes = ant_agru;
          ant_impo = saldo1;
          ant_impo1 = saldo11;
          ant_agru = "BENEFICIOS DE LA EXPLOTACION";
          |HAZ imprires;
          grupo = grupo + 1;
          |SI gru_nou = grupo;
              |ACABA;
          |FINSI;
      |FINSI;
      |SI grupo = 2;
          antes = ant_agru;
          ant_impo = saldo2;
          ant_impo1 = saldo12;
          ant_agru = "RESULTADOS FINANCIEROS POSITIVOS";
          |HAZ imprires;
          antes = ant_agru;
          ant_impo = saldo1 + saldo2;
          ant_impo1 = saldo11 + saldo12;
          ant_agru = "BENEFICIOS DE LAS ACTIVIDADES ORDINARIAS";
          |HAZ imprires;
          grupo = grupo + 1;
          |SI gru_nou = grupo;
              |ACABA;
          |FINSI;
      |FINSI;
      |SI grupo = 3;
          antes = ant_agru;
          ant_impo = saldo3;
          ant_impo1 = saldo13;
          ant_agru = "RESULTADOS EXTRAORDINARIOS POSITIVOS";
          |HAZ imprires;
          antes = ant_agru;
          ant_impo = saldo1 + saldo2 + saldo3;
          ant_impo1 = saldo11 + saldo12 + saldo13;
          ant_agru = "BENEFICIOS ANTES DE IMPUESTOS";
          |HAZ imprires;
          grupo = grupo + 1;
          |SI gru_nou = grupo;
              |ACABA;
          |FINSI;
      |FINSI;
      |SI grupo = 4;
          antes = ant_agru;
          ant_impo = saldo1 + saldo2 + saldo3 + saldo4;
          ant_impo1 = saldo11 + saldo12 + saldo13 + saldo14;
          ant_agru = "RESULTADO DEL EJERCICIO (BENEFICIOS)";
          |HAZ imprires;
          grupo = grupo + 1;
      |FINSI;
  |SINO;
      |SI grupo = 1;
          antes = ant_agru;
          ant_impo = saldo1;
          ant_impo1 = saldo11;
          ant_agru = "PERDIDAS DE LA EXPLOTACION";
          |HAZ imprires;
          grupo = grupo + 1;
          |SI gru_nou = grupo;
              |ACABA;
          |FINSI;
      |FINSI;
      |SI grupo = 2;
          antes = ant_agru;
          ant_impo = saldo2;
          ant_impo1 = saldo12;
          ant_agru = "RESULTADOS FINANCIEROS NEGATIVOS";
          |HAZ imprires;
          antes = ant_agru;
          ant_impo = saldo1 + saldo2;
          ant_impo1 = saldo11 + saldo12;
          ant_agru = "PERDIDAS DE LAS ACTIVIDADES ORDINARIAS";
          |HAZ imprires;
          grupo = grupo + 1;
          |SI gru_nou = grupo;
              |ACABA;
          |FINSI;
      |FINSI;
      |SI grupo = 3;
          antes = ant_agru;
          ant_impo = saldo3;
          ant_impo1 = saldo13;
          ant_agru = "RESULTADOS EXTRAORDINARIOS NEGATIVOS";
          |HAZ imprires;
          antes = ant_agru;
          ant_impo = saldo1 + saldo2 + saldo3;
          ant_impo1 = saldo11 + saldo12 + saldo13;
          ant_agru = "PERDIDAS ANTES DE IMPUESTOS";
          |HAZ imprires;
          grupo = grupo + 1;
          |SI gru_nou = grupo;
              |ACABA;
          |FINSI;
      |FINSI;
      |SI grupo = 4;
          antes = ant_agru;
          ant_impo = saldo1 + saldo2 + saldo3 + saldo4;
          ant_impo1 = saldo11 + saldo12 + saldo13 + saldo14;
          ant_agru = "RESULTADO DEL EJERCICIO (PERDIDAS)";
          |HAZ imprires;
          grupo = grupo + 1;
      |FINSI;
  |FINSI;
  grupo = #5#1;
|FPRC;

|PROCESO titulillo;
  |SI #5#0 = "D";
      |SI #5#1 = 1;
          raya11 = "GASTOS DE EXPLOTACION";
      |SINO;
          |SI #5#1 = 2;
              raya11 = "GASTOS FINANCIEROS";
          |SINO;
              |SI #5#1 = 3;
                  raya11 = "GASTOS EXTRAORDINARIOS";
              |SINO;
                  raya11 = "";
              |FINSI;
          |FINSI;
      |FINSI;
  |SINO;
      |SI #5#1 = 1;
          raya11 = "INGRESOS DE EXPLOTACION";
      |SINO;
          |SI #5#1 = 2;
              raya11 = "INGRESOS FINANCIEROS";
          |SINO;
              |SI #5#1 = 3;
                  raya11 = "INGRESOS EXTRAORDINARIOS";
              |SINO;
                  raya11 = "";
              |FINSI;
          |FINSI;
      |FINSI;
  |FINSI;
  sw_inf = 11;
  |PRINT; nSwPrint = 1;
|FPRC;

|PROCESO impresio;
  |SI #5#11 = 0; |Y #5#12 = 0;  |ACABA;  |FINSI;
  cantidad = #5#11;
  cantidad1 = #5#12;
  |SI sw = 0;
      tipo = #5#0;
      grupo = #5#1;
      agrupaci = #5#2;
      epigrafe = #5#3;
      partida = #5#4;
      |SI #5#0 = "D";
          doh = "  D E B E";
      |SINO;
          doh = " H A B E R ";
      |FINSI;
      act_cuent = #5#5;
      act_nivel = #5#6;
      sw = 1;
      |HAZ titulillo;
  |FINSI;

  nue_cuent = #5#5;
  nue_nivel = #5#6;

  |HAZ haycambio;


  |SI tipo ! #5#0;
      |HAZ cambio1;
      |HAZ rup_epig;
      |HAZ rup_agru;
      |HAZ beneperdi;
      |HAZ rup_tipo;
      |HAZ titulillo;
  |SINO;
      |SI grupo ! #5#1;
          |HAZ cambio1;
          |HAZ rup_epig;
          |HAZ rup_agru;
          |HAZ beneperdi;
          |HAZ titulillo;
      |SINO;
          |SI agrupaci ! #5#2;
              |HAZ cambio1;
              |HAZ rup_epig;
              |HAZ rup_agru;
          |SINO;
              |SI epigrafe ! #5#3;
                  |HAZ cambio1;
                  |HAZ rup_epig;
              |FINSI;
          |FINSI;
       |FINSI;
  |FINSI;
  act_cuent = #5#5;
  act_nivel = #5#6;
  tipo = #5#0;
  grupo = #5#1;
  agrupaci = #5#2;
  epigrafe = #5#3;
  partida = #5#4;

  #1#0 = #5#5;
  #1#2 = #5#7;
  sw_inf = 6;
  |PRINT; nSwPrint = 1;
  llegoa = nivelet;
  suma1 = suma1 + cantidad;
  suma2 = suma2 + cantidad;
  suma3 = suma3 + cantidad;
  suma4 = suma4 + cantidad;
  suma5 = suma5 + cantidad;
  suma6 = suma6 + cantidad;
  suma11 = suma11 + cantidad1;
  suma12 = suma12 + cantidad1;
  suma13 = suma13 + cantidad1;
  suma14 = suma14 + cantidad1;
  suma15 = suma15 + cantidad1;
  suma16 = suma16 + cantidad1;
  nue_cuent = #5#5;
  nue_nivel = #5#6;
  ant_agru = #5#8;
  |QBF ant_agru;

  ant_epig = #5#9;
  |QBF ant_epig;

  t_agru = t_agru + #5#11;
  t_agru1 = t_agru1 + #5#12;
  t_epig = t_epig + #5#11;
  t_epig1 = t_epig1 + #5#12;
|FPRC;

|DEFBUCLE imprimiendo;
 #5#1;
 ;
"D", 1, 01, 00, 00, "            ", 0;
"H", 4, 99, 99, 99, "zzzzzzzzzzzz", 9;
 ;
 NULL, impresio;
|FIN;

|PROCESO nivelrep; |TIPO 0;
  |SI nivelet = 0;
      nivelet = #0#18;
  |FINSI;
  |PARA x = Campo - 1;  |SI x ] 18;  |HACIENDO x = x - 1;
        |SI #0Campo = #0x;  |Y #0Campo ! 0;
            |MENAV "     Nivel repetido ...";
            |ERROR;
            |ACABA;
            |SAL;
        |FINSI;
        |SI nivelet < #0x;
            nivelet = #0x;
        |FINSI;
  |SIGUE;
|FPRC;

|PROCESO let_nivel;
  |SI nivelet = 0;
      nivelet = #0#18;
  |FINSI;
  |PARA x = 23;  |SI x ] 18;  |HACIENDO x = x - 1;
        |SI nivelet < #0x;
            nivelet = #0x;
        |FINSI;
  |SIGUE;
|FPRC;

|PROCESO final;
  |HAZ cambio1;
  |HAZ rup_epig;
  |HAZ rup_agru;
  #5#1 = 9999;
  |HAZ beneperdi;
  |HAZ rup_tipo;
|FPRC;

|PROCESO acortacuen;
  |SI nivelet < #1#1;
      |SI nivelet = 1;  traraf = 100 + dig4;  |FINSI;
      |SI nivelet = 2;  traraf = 100 + dig5;  |FINSI;
      |SI nivelet = 3;  traraf = 100 + dig6;  |FINSI;
      |SI nivelet = 4;  traraf = 100 + dig7;  |FINSI;
      |SI nivelet = 5;  traraf = 100 + dig8;  |FINSI;
      #1#0 = #1#0 % traraf;
      #1#1 = nivelet;
      #1#0 = #1#0 + "              ";
      #1#0 = #1#0 % 112;
  |FINSI;
|FPRC;

|PROCESO cargatra;
  #5#0 = #1#5;
  #5#1 = #1#4;
  #5#2 = #1#6;
  #5#3 = #1#7;
  #5#4 = #1#8;
  |HAZ acortacuen;
  #5#5 = #1#0;
  #5#6 = #1#1;
  #5#7 = #1#2;
  |LEE 000#5=;
  |SI FSalida ! 0;
      |DDEFECTO #5;
      #5#0 = #1#5;
      #5#1 = #1#4;
      #5#2 = #1#6;
      #5#3 = #1#7;
      #5#4 = #1#8;
      |HAZ acortacuen;
      #5#5 = #1#0;
      #5#6 = #1#1;
      #5#7 = #1#2;
      #5#11 = 0;
      #5#12 = 0;
      |GRABA 020#5c;
  |FINSI;
  |DDEFECTO #2;
  #2#0 = #1#4;
  #2#1 = #1#5;
  #2#2 = #1#6;
  #2#4 = #1#7;
  #2#6 = #1#8;
  |LEE 000#2=;
  |SI FSalida ! 0;
      #2#3 = "";
      #2#5 = "";
      #2#7 = "";
  |FINSI;
  #5#8 = #2#3;
  #5#9 = #2#5;
  #5#10 = #2#7;
|FPRC;

|PROCESO sumadeha;
  |SI acumula = 11;
      |SI #1#4 = 1;
          saldo1 = saldo1 + importe;
      |SINO;
          |SI #1#4 = 2;
              saldo2 = saldo2 + importe;
          |SINO;
              |SI #1#4 = 3;
                  saldo3 = saldo3 + importe;
              |SINO;
                  saldo4 = saldo4 + importe;
              |FINSI;
          |FINSI;
      |FINSI;
  |SINO;
      |SI #1#4 = 1;
          saldo11 = saldo11 + importe;
      |SINO;
          |SI #1#4 = 2;
              saldo12 = saldo12 + importe;
          |SINO;
              |SI #1#4 = 3;
                  saldo13 = saldo13 + importe;
              |SINO;
                  saldo14 = saldo14 + importe;
              |FINSI;
          |FINSI;
      |FINSI;
  |FINSI;
|FPRC;

|PROCESO calculit;
  |HAZ digitos2;
  |SI d_error ! 0;     |ACABA;  |FINSI;
  |SI #1#1 > nivelet;  |ACABA;  |FINSI;
  |SI #1#3 = "S";  |O #1#1 = nivelet;
      |HAZ cargatra;
|| .. Suma ---------- PERIODO
      acumula = 11;
      |SI #1#5 = "D";
          |PARA x = eldesde; |SI x [ elhasta; |HACIENDO x = x + 3;
                #5acumula = #5acumula + #1x;
                importe = #1x;
                |HAZ sumadeha;
          |SIGUE;
      |SINO;
          |PARA x = eldesde; |SI x [ elhasta; |HACIENDO x = x + 3;
                #5acumula = #5acumula + (#1x * -1);
                importe = #1x;
                |HAZ sumadeha;
           |SIGUE;
      |FINSI;
|| .. Suma ---------- ACUMULADO
      acumula = 12;
      |SI #1#5 = "D";
          |PARA x = acdesde; |SI x [ achasta; |HACIENDO x = x + 3;
                #5acumula = #5acumula + #1x;
                importe = #1x;
                |HAZ sumadeha;
          |SIGUE;
      |SINO;
          |PARA x = acdesde; |SI x [ achasta; |HACIENDO x = x + 3;
                #5acumula = #5acumula + (#1x * -1);
                importe = #1x;
                |HAZ sumadeha;
           |SIGUE;
      |FINSI;
      |GRABA 020#5a;
      |LIBERA #5;
  |FINSI;
|FPRC;

|DEFBUCLE carga_ctas;
 #1#1;
5,4,6,7,8;
"            ", 0, "D", 1, 01, 00, 00;
"zzzzzzzzzzzz", 9, "H", 4, 99, 99, 99;
 e;
 NULL, calculit;
|FIN;

|PROCESO conexi; |TIPO 0;
  d_mes = #0#9;
  h_mes = #0#10;
  estru = #0#8;
  r_emp = empre;
  r_per = ejerc;
  r_eje = #30#26;
  r_nom = "PERIODO " + #30#1;
  |SUB_EJECUTA "caexicon.run";
  |SI #0#9 ! #0#15;  |O #0#10 ! #0#16;
      d_mes = #0#15;
      h_mes = #0#16;
      estru = #0#8;
      r_emp = empre;
      r_per = ejerc;
      r_eje = #30#26;
      r_nom = "ACUMULADOS " + #30#1;
     |SUB_EJECUTA "caexicon.run";
  |FINSI;
|FPRC;

|PROCESO Borrar;
  sw = 0;
  saldo1  = 0;  saldo2  = 0;  saldo3  = 0;  saldo4  = 0;
  saldo11 = 0;  saldo12 = 0;  saldo13 = 0;  saldo14 = 0;
  suma1 = 0;
  suma2 = 0;
  suma3 = 0;
  suma4 = 0;
  suma5 = 0;
  suma6 = 0;
  suma11 = 0;
  suma12 = 0;
  suma13 = 0;
  suma14 = 0;
  suma15 = 0;
  suma16 = 0;
  t_agru = 0;
  t_agru1 = 0;
  t_epig = 0;
  t_epig1 = 0;
  grupo = 0;
  gru_nou = 0;
  agrupaci = 0;
  epigrafe = 0;
  partida = 0;
|FPRC;

|PROCESO HagoTodo;
  |HAZ Borrar;

  |ABRE #5;
  |ABRE #2;
  |BUCLE carga_ctas;
  |CIERRA #2;

  |HAZ borractas;
  |HAZ cargades;
  |ABRE #1;
  |BUCLE imprimiendo;
  |HAZ final;
  |CIERRA #1;
  |CIERRA #5;

  |DELINDEX #5;
|FPRC;

|PROCESO PorSesiones;
   eaDepSes = #110#1;
   eaDesDep = #110#7;

   alfa1 = #110#6; |QBF alfa1;
   |SI alfa1 ! "";
       eaFicbal  = alfa1;
       |NOME_DAT #1 eaFicbal;
       |HAZ HagoTodo;
   |FINSI;
|FPRC;

|DEFBUCLE PorSesiones;
 #110#1;
 ;
 enNumSes, "  ";
 enNumSes, "zz";
 ;
 NULL, PorSesiones;
|FIN;

|PROCESO impre; |TIPO 3;

  Ejercicio = #30#26 + 2000;
  |SI #30#26 > 80; Ejercicio = #30#26 + 1900; |FINSI;

  |HAZ let_nivel;
  |HAZ antesque;
  |HAZ conexi;

  |ABRE #8;
  |LEE 111#8p;
  |SI #8#2 = "S";  |Y #8#3 = "N";  |Y aparam = "";
      |DFICE pathbal;
      balance = 1;
      |CIERRA #8;
      |SUB_EJECUTA "careccue.tab";
  |FINSI;
  |CIERRA #8;

  eldesde = (#0#4 * 3 ) + 11;
  elhasta = (#0#5 * 3 ) + 11;
  |SI aparam = ""; |SUB_EJECUTA "carecsig.tab"; |FINSI;

  aFichero = ("5i" + Usuario) % 108;
  |NOME_DAT #5 aFichero;
  |ABRE #5; |CIERRA #5; |DELINDEX #5;

  |IMPRE 0;
  |SI FSalida ! 0;  |ACABA;  |FINSI;

  |HAZ los_informes;
  informe = que_i1;

  |INFOR informe;
  |SI FSalida ! 0;  |FINIMP;  |ACABA;  |FINSI;

  eldesde = (#0#0 * 3 ) + 11;
  elhasta = (#0#1 * 3 ) + 11;
  acdesde = (#0#4 * 3 ) + 11;
  achasta = (#0#5 * 3 ) + 11;

  |SI enMasDep = 0;
      |HAZ HagoTodo;
  |SINO;
      |BUCLE PorSesiones;
      eaDepSes = "TODOS";
  |FINSI;
  |FININF;
  |FINIMP;
|FPRC;

|| ------------------------ cuentas de descarga

|PROCESO borrades;
  #5#0 = #1#5;
  #5#1 = #1#4;
  #5#2 = #1#6;
  #5#3 = #1#7;
  #5#4 = #1#8;
  |HAZ acortacuen;
  #5#5 = #1#0;
  #5#6 = #1#1;
  #5#7 = #1#2;
  |LEE 000#5=;
  |SI FSalida = 0;
      importe = -#5#11;
      |SI #1#5 = "H"; importe = -importe; |FINSI;
      acumula = 11;
      |HAZ sumadeha;
      importe = -#5#12;
      |SI #1#5 = "H"; importe = -importe; |FINSI;
      acumula = 12;
      |HAZ sumadeha;
      |BORRA 000#5a;
  |FINSI;
|FPRC;

|PROCESO lineabor;
  #1#0 = #7#5;
  #1#1 = #7#6;
  |LEE 000#1=;
  |SI FSalida = 0;
      |HAZ borrades;
  |FINSI;
  #1#0 = #7#8;
  #1#1 = #7#9;
  |LEE 000#1=;
  |SI FSalida = 0;
      |HAZ borrades;
  |FINSI;
|FPRC;


|PROCESO descarg;
  |HAZ digitos2;
  |SI d_error ! 0;  |ACABA;  |FINSI;

|| --- periodo
  acumula = 11;
  mesini = #0#9 + 11;              || iniciales
  descar = #7mesini;
  mesini = #0#10 + 11;              || Finales
  descar = descar - #7mesini;
  |SI descar ] 0;
      #1#0 = #7#5;     || cuenta descarga D
      #1#1 = #7#6;     || digito descarga D
  |SINO;
      #1#0 = #7#8;     || cuenta descarga H
      #1#1 = #7#9;     || digito descarga H
  |FINSI;
  |LEE 000#1=;             || lee cta. descarga
  |SI FSalida = 0;
      |HAZ cargatra;
      |SI #1#5 = "D";
          #5acumula = #5acumula + descar;
      |SINO;
          #5acumula = #5acumula - descar;
      |FINSI;
      importe = descar;
      |HAZ sumadeha;
      |GRABA 020#5a;
      |LIBERA #5;
  |FINSI;

|| --- acumulado
  acumula = 12;
  mesini = #0#15 + 11;              || Iniciales
  descar = #7mesini;
  mesini = #0#16 + 11;              || Finales
  descar = descar - #7mesini;
  |SI descar ] 0;
      #1#0 = #7#5;     || cuenta descarga D
      #1#1 = #7#6;     || digito descarga D
  |SINO;
      #1#0 = #7#8;     || cuenta descarga H
      #1#1 = #7#9;     || digito descarga H
  |FINSI;
  |LEE 000#1=;             || lee cta. descarga
  |SI FSalida = 0;
      |HAZ cargatra;
      |SI #1#5 = "D";
          #5acumula = #5acumula + descar;
      |SINO;
          #5acumula = #5acumula - descar;
      |FINSI;
      importe = descar;
      |HAZ sumadeha;
      |GRABA 020#5a;
      |LIBERA #5;
  |FINSI;
|FPRC;

|PROCESO lineades;
  #1#0 = #7#2;
  #1#1 = #7#3;
  |LEE 000#1=;
  |SI FSalida = 0;
      |HAZ descarg;
  |FINSI;
|FPRC;

|PROCESO borractas;
  ||----------------- ESTE PROCESO BORRA LAS CUENTAS CARGADAS de DESCARGA
  |ABRE #1;
  |ABRE #2;
  |ABRE #6;
  #6#0 = #0#8;
  |LEE 000#6=;
  |BUCLELIN 2lineabor#6;
  |CIERRA #1;
  |CIERRA #2;
  |CIERRA #6;
|FPRC;

|PROCESO cargades;
  ||----------------- ESTE PROCESO GRABA LAS CUENTAS de DESCARGA
  |ABRE #1;
  |ABRE #2;
  |ABRE #6;
  #6#0 = #0#8;
  |LEE 000#6=;
  |BUCLELIN 2lineades#6;
  |CIERRA #1;
  |CIERRA #2;
  |CIERRA #6;
|FPRC;
