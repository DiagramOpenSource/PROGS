|FICHEROS;
    cadiablo #0;
    caentasc #1;
    caentasl #2;
    caditra1 #3;
    cacuenta #4;
|FIN;

|VARIABLES;
    &entrada = 1;
    bloque   = "";
    fichero  = "";

    lin     = 0;
    sw_desc = 0;
    opcion  = 0;
    fecalf  = "";

    diario  = 0;
    fecha   = @;
    docume  = 0;

    men1    = "    NO EXISTE EMPRESA";
    mensa1  = "    ERROR ESTA FECHA NO CORRESPONDE AL EJERCICIO";
    mensa3  = "    NO EXISTE BLOQUE DE APUNTES ";
    traba1  = "";

    &swprim  = 0;
    &a_debe  = 0;
    &a_haber = 0;
    &t_debe  = 0;
    &t_haber = 0;

    inform1 = "cadiablo";
    inform2 = "cadiabl1";
    informe = "";

    {-1}menu = "";
        menu1 = "2420";
        menu2 = "1";
        menu3 = " Impresion por : ";
        menu4 = "PI";
        menu5 = "  Pantalla  ";
        menu6 = "  Impresora  ";
|FIN;

|INCLUYE acceso_i;

|| *******************************************************************
|| **************** Campos Pantalla **********************************
|| *******************************************************************

|PROCESO leebloq; |TIPO 6;
 |ABRE #1;
 #1#0 = #0#0;
 |LEE 001#1=;
 |SI FSalida ! 0;
     |MENAV mensa3;
     |ERROR;
 |SINO;
      #0#10 = #1#4; |PINTA #0#10;
      #0#11 = #1#5; |PINTA #0#11;
 |FINSI;
 |CIERRA #1;
|FPRC;

|PROCESO diarh; | TIPO 0;   ||  hasta diario
  |SI #0#8 < #0#7;  |ERROR;  |FINSI;
|FPRC;

|PROCESO fecha; | TIPO 0;   || Desde/hasta fecha
  |SI #0Campo < fec1; |O #0Campo > fec2;
      |MENAV mensa1;
      |ERROR;
  |FINSI;
  |SI Campo = 5;
      |SI #0#5 < #0#4; |ERROR; |FINSI;
  |FINSI;
|FPRC;

|PROCESO documh; | TIPO 0;   ||  hasta documento
  |SI #0#3 < #0#2;  |ERROR;  |FINSI;
|FPRC;

|| *************************************************************************
||                          MEOLLO
|| *************************************************************************

|PROCESO buclepan;
 |SI swprim = 0;
     diario = #2#2;
     fecha  = #2#3;
     docume = #2#21;
     #3#10  = 0;
     #3#11  = 0;
     #3#12  = 0;
     swprim = 1;
     |HAZ pantalla;
 |FINSI;
 |SI #0#9 = 1;
     |SI diario ! #2#2; |O fecha ! #2#3; |O docume ! #2#21;
         sw_desc = 0;
         |SI #3#12 ! 0; sw_desc = 1; |FINSI;
         |HAZ cambiapa;
         diario = #2#2;
         fecha  = #2#3;
         docume = #2#21;
         #3#10  = 0;
         #3#11  = 0;
         #3#12  = 0;
         |HAZ pantalla;
     |FINSI;
 |FINSI;

 |SI lin > 1000;
     sw_desc = 0;
     |HAZ cambiapa;
     |HAZ pantalla;
 |FINSI;

  #3#0 = #2#3;
  #3#1 = #2#2;
  #3#3 = #2#21;
  #3#4 = #2#4;
  #3#5 = #2#6;
  #3#6 = #2#7;
  #3#7 = #2#8;
  |SI #3#7 = "D";
      #3#8 = #2#9;          || debe
      #3#16 = 0;            || haber
      #3#10 = #3#10 + #2#9;
      #3#13 = #3#13 + #2#9;
  |SINO;
      #3#8 = 0;
      #3#16 = #2#9;
      #3#11 = #3#11 + #2#9;
      #3#14 = #3#14 + #2#9;
  |FINSI;
  #3#12 = #3#10 - #3#11;
  #3#15 = #3#13 - #3#14;
  |PINTA #3#0;
  |PINTA #3#1;
  |PINTA #3#3;
  Pos = lin;
  |PINDA #0#3;
  Pos = -1;
  |PINTA #3#10;
  |PINTA #3#11;
  |PINTA #3#12;
  |PINTA #3#13;
  |PINTA #3#14;
  |PINTA #3#15;
  lin = lin + 100;
|FPRC;

|PROCESO cambiapa;
 |SI sw_desc = 1;
     |AVISO;
     |AVISO;
     |ATRI P;
     |PINTA 0430,"===  DESCUADRE ===";
     |ATRI 0;
 |FINSI;
 |PAUSA;
 |SI FSalida = 1; |O FSalida = 7;
     |ERROR10;
 |FINSI;
 |ATRI R;
 |BLIN 04;
 |ATRI 0;
|FPRC;

|PROCESO pantalla;
 lin = 100;
 |C_V #3#2,1,"N";
 |PINPA #0#3;
 |PINTA 0659, "Bloque .: ";
 |ATRI I;
 |PINTA 0669, #0#0;
 |ATRI 0;
|FPRC;

|PROCESO finpan;
  sw_desc = 0;
  |SI #3#12 ! 0; sw_desc = 1; |FINSI;
  |HAZ cambiapa;
|FPRC;

|DEFBUCLE cadiablo3;
 #2#4;
 ;
 #0#7, #0#4, #0#2, 00001;
 #0#8, #0#5, #0#3, 99999;
 e;
 NULL, buclepan, NULL, NULL, finpan;
|FIN;

|DEFBUCLE cadiablo4;
 #2#1;
 2,3,21;
 #0#0, 00001, #0#7, #0#4, #0#2;
 #0#0, 99999, #0#8, #0#5, #0#3;
 e;
 NULL, buclepan, NULL, NULL, finpan;
|FIN;

|| *************************************************************************
|PROCESO bucleimp;
  |DDEFECTO #4;
  #4#0 = #2#4;
  #4#1 = #2#5;
  |LEE 040#4=;

  |SI #2#8 = "D";
      a_debe = #2#9;
      a_haber = 0;
  |SINO;
      a_debe = 0;
      a_haber = #2#9;
  |FINSI;
  |PRINT;
  swprim = 1;
  t_debe  = t_debe  + a_debe;
  t_haber = t_haber + a_haber;
|FPRC;

|DEFBUCLE cadiablo1;
 #2#4;
 ;
 #0#7, #0#4, #0#2, 00001;
 #0#8, #0#5, #0#3, 99999;
 e;
 NULL, bucleimp;
|FIN;

|DEFBUCLE cadiablo2;
 #2#1;
 2,3,21;
 #0#0, 00001, #0#7, #0#4, #0#2;
 #0#0, 99999, #0#8, #0#5, #0#3;
 e;
 NULL, bucleimp;
|FIN;

|PROCESO impresora;
  |IMPRE 0;
  |SI FSalida ! 0;  |ACABA;  |FINSI;

  |SI #0#9 = 1; informe = inform1; |FINSI;
  |SI #0#9 = 2; informe = inform2; |FINSI;

  |INFOR informe;
  |SI FSalida ! 0;  |FINIMP;  |ACABA;  |FINSI;

  |SI #0#9 = 1; |BUCLE cadiablo1; |FINSI;
  |SI #0#9 = 2; |BUCLE cadiablo2; |FINSI;
  |SI swprim = 1;
      |PIEINF;
  |FINSI;
  |FININF;
  |FINIMP;
|FPRC;
|| ----------------------------------------------------------------------

|PROCESO impre;
  bloque = #0#0;
  fichero = bloque;
  |QBF fichero;
  fichero = fichero + "zzzzzzzz";
  fichero = fichero % 107;
  fichero = fichero + "l";

  |NOME_DAT #2 fichero;

  |ABRET #2;
  |ABRE #4;

  |MENU menu;
  opcion = FSalida;

  |SI opcion = 1;
      |SI #0#9 = 1;
          |C_V #3#17,1,"N";
          |BUCLE cadiablo3;
      |FINSI;
      |SI #0#9 = 2; |BUCLE cadiablo4; |FINSI;
  |FINSI;
  |SI opcion = 2;  |HAZ impresora;   |FINSI;

  |CIERRAT #2;
  |CIERRA #4;
|FPRC;

|| *********************************************************************
||                             COMIENZO
|| *********************************************************************

|PROGRAMA;
  |CLS;
  |CABEZA "Diario Bloque";
  |HAZ inicio;
  #0#4 = fec1;
  #0#5 = fec2;
  FSalida = 1;
  |PARA; |SI FSalida = 1; |HACIENDO;
         |PINPA #0#0;
         |PINDA #0#0;                    || Pinta los datos por defecto
         |ENDATOS #1#0;                  || Seleccion informe
         |SI FSalida ! 0;  |SAL;  |FINSI;

         |DDEFECTO #3;
         swprim = 0;
         t_debe = 0;
         t_haber = 0;
         |HAZ impre;                     || Calculo impresion
         FSalida = 1;                || Vuelve al bucle
  |SIGUE;
|FPRO;
