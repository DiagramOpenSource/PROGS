|FICHEROS;
     c3ges347 #0;
     camac347 #1;
     camal347 #2;
     camic347 #3;
     camil347 #4;
     caportad #7;
     canempre #30;
     catrasop #104;
|FIN;

|VARIABLES;
     Vacio20    = "                    ";
     Vacio40    = "                                        ";
     Vacio45    = "                                             ";
     Salir      = 0;
     Opcion     = 0;

     VAlfa      = "";
     VAlfa1     = "";
     VAlfa2     = "";
     VAlfa3     = "";
     VAlfa4     = "";

     Letras     = "";
     Compl      = "";
     Refer      = "";

     Campos     = "";
     Campos1    = "";
     Campos2    = "";
     Campos3    = "";
     Campos4    = "";
     Campos5    = "";
     Campos6    = "";
     Campos7    = "";
     Campos8    = "";
     Campos9    = "";
     Campos10   = "";
     Campos11   = "";
     Campos12   = "";
     Campos13   = "";
     Campos14   = "";
     Campos15   = "";
     Campos16   = "";
     Campos17   = "";
     Campos18   = "";
     Campos19   = "";

     DClave     = "";
     HClave     = "";
     Clave      = "";
     Contador   = 0;
     ModoModi   = "";
     SwLee      = 0;
     Fichero1   = "";
     FicheroD   = "";
     Path       = "";

     CampoDni   = "";
     Letra1     = "";
     Letra2     = "";
     Letra3     = "";
     Letra4     = "";
     Letra5     = "";
     Letra6     = "";
     Letra7     = "";
     Letra8     = "";
     Caracter   = "";
     Carac1     = 0;
     Carac2     = 0;
     Carac3     = 0;
     Carac4     = 0;
     Carac5     = 0;

         alfa1    = "";
         alfa2    = "";
         tip1     = 0;
         tip2     = 0;
         empcon   = 0;
         ejecon   = 0;
         leidos   = 0;
         dirfich0 = "";
         xx       = 0;
         yy       = 0;
         x        = 0;
         y        = 0;
         &r_emp   = 0;
         &r_per   = 0;
         sw_inm   = 0;

         &cargoem = "";
         &any     = "";
         &dia     = "";
         &mes     = "";
         &anyo    = "";
         &preimp  = "";
         &predis  = "";
         &predi1  = "";
         &predi2  = "";
         &represen = "";
         &fecbo   = "";
         &ttes    = 0;
         &tdos    = 0;


     &THojas    = 0;
     &NHojas    = 0;

    &eaVarDni  = "";
    &enCalcCif = 0;

{-1} MGeneral   = "";
     MGeneral1  = "1860";
     MGeneral2  = "1";
     MGeneral3  = "5";
     MGeneral4  = "Creacion  Diskette ";
     MGeneral5  = "Emision   Portada  ";
     MGeneral6  = "Etiqueta  Diskette ";
     MGeneral7  = "Verificacion";
     MGeneral8  = "Grabacion Diskette ";
|FIN;

|INCLUYE acceso_i;
|INCLUYE i_laserc;

|| ***********************************************************************
||                       PROCESO CARGA DE DATOS
|| ***********************************************************************

|PROCESO consulta;
  |SI FSalida ! 9;  |ACABA;  |FINSI;
  |CIERRAT #30;
  |SUB_EJECUTA "caconemp.run";
  |ABRET #30;
  |ERROR;
  |SI Campo = 26;
      #0#26 = r_emp;
      #0#28 = r_per;
      |PTEC 802;
      |PTEC 802;
  |FINSI;
  |SI Campo = 27;
      #0#27 = r_emp;
      #0#29 = r_per;
      |PTEC 802;
  |FINSI;
  |SI Campo ] 30;
      #0Campo = r_emp;
      x = Campo + 20;
      #0x = r_per;
      |PTEC 802;
      |PTEC 802;
  |FINSI;
|FPRC;

|PROCESO Relleno;
  |SI Campo = 6;
      VAlfa = #0Campo; |QBT VAlfa;
      VAlfa = ("00000" + VAlfa) % -105;
  |FINSI;

  |SI Campo = 10;
      VAlfa = #0Campo; |QBT VAlfa;
      VAlfa = ("000" + VAlfa) % -103;
  |FINSI;

  |SI Campo = 11;
      VAlfa = #0Campo; |QBT VAlfa;
      VAlfa = ("0000000" + VAlfa) % -107;
  |FINSI;

  #0Campo = VAlfa;  |PINTA #0Campo;
|FPRC;

|PROCESO mayor1;
  |HAZ Mayus;
  |SI #0#22 = "I"; |ACABA; |FINSI;
  |SI #0Campo = "N";
      |PARA x = 30;  |SI x [ 69;  |HACIENDO x = x + 1;
            |CAMPO_MODIFICA #0x,1,"S";
      |SIGUE;
      #0#26 = 0;
      #0#28 = 0;
      #0#27 = 0;
      #0#29 = 0;
      |CAMPO_MODIFICA #0#26,1,"N";
      |CAMPO_MODIFICA #0#28,1,"N";
      |CAMPO_MODIFICA #0#27,1,"N";
      |CAMPO_MODIFICA #0#29,1,"N";
      |PINTA #0#26;
      |PINTA #0#28;
      |PINTA #0#27;
      |PINTA #0#29;
  |SINO;
      |PARA x = 30;  |SI x [ 69;  |HACIENDO x = x + 1;
            #0x = 0;
            |CAMPO_MODIFICA #0x,1,"N";
      |SIGUE;
      |CAMPO_MODIFICA #0#26,1,"S";
      |CAMPO_MODIFICA #0#28,1,"S";
      |CAMPO_MODIFICA #0#27,1,"S";
      |CAMPO_MODIFICA #0#29,1,"S";
  |FINSI;
|FPRC;

|| --------------- comprueba desde-hasta empresas ---------------------------

|PROCESO buscaem1;

  |SI #30#21 = "I";  |ACABA; |FINSI;
  |SI #30#26 ! #0#2;
      swerror = 3;
      |ACABA;
  |FINSI;

  |DBASE dir_fich;
  dirfich0 = dir_fich + "fich/";
  alfa1 = #30#2;
  alfa2 = #30#3;
  alfa1 = "00000" + alfa1;   alfa1 = alfa1 % -105;
  alfa2 = "0"   + alfa2;   alfa2 = alfa2 % -101;
  dirfich0 = dirfich0 + alfa1 + alfa2 + "/";
  |PATH_DAT #1 dirfich0;
  |PATH_DAT #7 dirfich0;
  |ABRE #1;
  #1#0 = #30#2;
  |LEE 001#1=;
  |SI FSalida ! 0;
      swerror = 1;
      |CIERRA #1;
      |ACABA;
  |FINSI;
  |CIERRA #1;
  |ABRE #7;
  #7#0 = #30#2;
  #7#1 = #30#26;
  |LEE 001#7=;
  |SI FSalida ! 0;
      swerror = 2;
      |CIERRA #7;
      |ACABA;
  |FINSI;
  |CIERRA #7;
  leidos = 1;
|FPRC;

|DEFBUCLE buscaempre;
#30#1;
;
#0#26,#0#28;
#0#27,#0#29;
e;
NULL,buscaem1;
|FIN;

|PROCESO buclempre;
  |SI #0#28 > #0#29;
      |MENAV "     ERROR EN INTRODUCCION";
      |ERROR;
      |ACABA;
  |FINSI;
  |SI #0#26 = 0;  |Y #0#27 = 0;  |ACABA;  |FINSI;
  leidos = 0;
  |BUCLE buscaempre;
  |SI leidos = 0;
      |MENAV "    ATENCION SELECCION VACIA";
      |ERROR;
      |ACABA;
  |FINSI;
|FPRC;

|PROCESO deshas;
  |SI #0#27 < #0#26;
      |MENAV "      Error en Introduccion";
      |ERROR;
  |FINSI;
|FPRC;

|| ------------------------------------------------------------------------

|PROCESO nomodi;
 |PARA x = Campo + 1;  |SI x [ 49;  |HACIENDO x = x + 1;
       #0x = 0;
       |CAMPO_MODIFICA #0x,1,"N";
       y = x + 20;
       #0y = 0;
       |CAMPO_MODIFICA #0y,1,"N";
 |SIGUE;
 y = Campo + 20;
 #0y = 0;
 |CAMPO_MODIFICA #0y,1,"N";
 |PINTA #0y;
|FPRC;

|PROCESO esbuena; || desde campo de empresas seleccion
  |SI #0Campo = 0; |HAZ nomodi;  |ACABA;  |FINSI;
  |ABRE #30;
  #30#2 = #0Campo;
  #30#3 = 0;
  |LEE 011#30];
  |SI FSalida ! 0;  |O #30#2 ! #0Campo;
      |MENAV "     NO Existe la empresa Contable";
      |ERROR;
  |FINSI;
  |CIERRA #30;
|FPRC;

|PROCESO esbuena1;
  |SI FSalida = 2;|ACABA;|FINSI;
  x = Campo - 20;
  |SI #0x = 0;  |ACABA;  |FINSI;
  |ABRE #30;
  x = Campo - 20;
  #30#2 = #0x;
  #30#3 = #0Campo;
  |LEE 011#30=;
  |SI FSalida ! 0;
      |MENAV "     NO Existe Periodo en la empresa Contable";
      |CIERRA #30;
      |ERROR;
      |ACABA;
  |FINSI;
  |CIERRA #30;
  leidos = 0;
  |HAZ buscaem1;
  |SI leidos = 0;
      |SI swerror = 1;
          |MENAV "     No existe Modelo 347";
          |FINSI;
      |SI swerror = 2
          |MENAV "0000No existen datos el PORTADAS MOD. OFICIALES ";
          |FINSI;
      |SI swerror = 3;
          |MENAV "     El a¤o contable NO corresponde ";
          |FINSI;
      |ERROR;
      |ACABA;
  |FINSI;
  x = Campo + 20;
  #0x = #30#1;
  |PINTA #0x;
  x = Campo - 20;
  alfa1 = #0x;
  alfa2 = #0Campo;
|FPRC;

|| -------------------------------------------------------------------------

|PROGRAMA;
  |HAZ inicio;

  aTipoSec = "347";

  |CLS;
  |CABEZA "Soporte Magn. 347 Gral.";
  |DDEFECTO #0;
  |PINPA #0#0;
  |ABRE #0;
  |LEE 101#0p;
  |SI FSalida ! 0;
      |GRABA 020#0b;
  |FINSI;
  |PINDA #0#0;
  |ENDATOS #1#0;
  |SI FSalida = 0;
      |GRABA 020#0a;
  |FINSI;
  |LIBERA #0;
  |CIERRA #0;
|FPRO;

|| -----------------------------------------------------------------------

|PROCESO Mayus; |TIPO 0;
  #0Campo = #0Campo > #0Campo;
  |PINTA #0Campo;
|FPRC;

|PROCESO PintaAnyo; |TIPO 7;
  #0#2 = cod2;
  |PINTA #0#2;
|FPRC;

|PROCESO Individual; |TIPO 0;

  |PARA x = 25; |SI x [ 69; |HACIENDO x = x + 1;
        |C_M #0x, 1,"S";
  |SIGUE;

  |SI #0#22 = "I";
      #0#25 = "S";
      #0#26 = empre; #0#27 = empre;
      #0#29 = ejerc; #0#29 = ejerc;
      ModoModi = "N";
      |PARA x = 25; |SI x [ 69; |HACIENDO x = x + 1;
            |C_M #0x, 1,"N";
      |SIGUE;
  |FINSI;

  |SI #0#22 = "C"; ModoModi = "S"; |FINSI;
  |SI #0#22 = "L"; ModoModi = "N"; |FINSI;

  |C_M #0#03, 1, ModoModi;
  |C_M #0#04, 1, ModoModi;
  |C_M #0#05, 1, ModoModi;
  |C_M #0#06, 1, ModoModi;
  |C_M #0#07, 1, ModoModi;
  |C_M #0#08, 1, ModoModi;
  |C_M #0#09, 1, ModoModi;
  |C_M #0#10, 1, ModoModi;
  |C_M #0#11, 1, ModoModi;
  |C_M #0#12, 1, ModoModi;
  |C_M #0#16, 1, ModoModi;
  |C_M #0#17, 1, ModoModi;
  |C_M #0#18, 1, ModoModi;
  |C_M #0#91, 1, ModoModi;
  |C_M #0#92, 1, ModoModi;
|FPRC;

|PROCESO PonIgual;
  |SI #0#22 = "C";  |ACABA;  |FINSI;
  |SI #0#22 = "L";
      #0#9  = "";    |PINTA #0#9;              || NIF
      #0#3  = "";    |PINTA #0#3;              || Apellidos y Nombre
      #0#4  = "";    |PINTA #0#4;              || SG
      #0#5  = "";    |PINTA #0#5;              || Calle
      #0#6  = "";    |PINTA #0#6;              || Numero
      #0#11 = "";    |PINTA #0#11;             || Telefono
      #0#7  = "";    |PINTA #0#7;              || Municipio
      #0#8  = "";    |PINTA #0#8;              || Provincia
      #0#17 = 0;     |PINTA #0#17;             || Codigo Postal 1
      #0#18 = 0;     |PINTA #0#18;             || Codigo Postal 2
      #0#12 = 0;     |PINTA #0#12;             || Delegacion de Hacienda
      #0#16 = 0;     |PINTA #0#16;             || Administracion de Hacienda
      #0#91 = "";    |PINTA #0#91;             || Delegacion de Hacienda
      #0#92 = "";    |PINTA #0#92;             || Administracion de Hacienda
  |FINSI;

  |SI #0#22 ! "I";  |ACABA;  |FINSI;

  || .......... #0#1 = #0#0; |PINTA #0#1;
  |ABRE #1;
  #1#0 = #30#2;
  #1#1 = #0#2;
  |LEE 040#1=;
  |SI FSalida ! 0;
      |CIERRA #1;
      |MENAV "     No Existe Movimientos en el Modelo 347 con esta Empresa";
      |ERROR;
      |ACABA;
  |FINSI;
  |CIERRA #1;

  |ABRE #7;
  #7#0 = empre;
  #7#1 = #0#2;
  |LEE 040#7=;
  |SI FSalida ! 0;
      |CIERRA #7;
      |MENAV "    No existen datos el PORTADAS MOD. OFICIALES ";
      |ERROR;
      |ACABA;
  |FINSI;
  |CIERRA #7;

  |HAZ representante;
  #0#90 = represen; |PINTA #0#90;             || Fdo.
  CampoDni = #7#13;  |HAZ LetraDni;
  #0#9  = CampoDni; |PINTA #0#9;              || NIF
  #0#3  = #7#2;     |PINTA #0#3;              || Apellidos y Nombre
  #0#4  = #7#3;     |PINTA #0#4;              || SG
  #0#5  = #7#4;     |PINTA #0#5;              || Calle
  #0#6  = #7#5;     |PINTA #0#6;              || Numero
  #0#11 = #7#14;    |PINTA #0#11;             || Telefono
  #0#7  = #7#11;    |PINTA #0#7;              || Municipio
  #0#8  = #7#12;    |PINTA #0#8;              || Provincia
  #0#17 = #7#09;    |PINTA #0#17;             || Codigo Postal 1
  #0#18 = #7#10;    |PINTA #0#18;             || Codigo Postal 2
  #0#12 = #7#16;    |PINTA #0#12;             || Delegacion de Hacienda
  #0#16 = #7#17;    |PINTA #0#16;             || Administracion de Hacienda
  #0#91 = #7#18;    |PINTA #0#91;             || Delegacion
  #0#92 = #7#19;    |PINTA #0#92;             || Administracion
|FPRC;

|PROCESO representante;
 represen = "";
 |SI #7#39 ! Vacio45;
     represen = #7#39;
 |SINO;
     |SI #7#51 ! Vacio20; |O #7#52 ! Vacio20;
         represen = #7#51;
         |QBF represen;
         represen = represen + ", " + #7#52;
     |SINO;
         |SI #7#56 ! Vacio20; |O #7#57 ! Vacio20;
              represen = #7#56;
              |QBF represen;
              represen = represen + ", " + #7#57;
         |SINO;
              |SI #7#61 ! Vacio20; |O #7#62 ! Vacio20;
                  represen = #7#61;
                  |QBF represen;
                  represen = represen + ", " + #7#62;
              |FINSI;
         |FINSI;
     |FINSI;
 |FINSI;
|FPRC;

|PROCESO LetraDni;
     eaVarDni   = CampoDni;
     enCalcCif  = 3;
     |HAZ CalculoDNI;
     CampoDni = eaVarDni;
     CampoDni = ("000000000" + CampoDni) % -109;
|FPRC;

|| ***********************************************************************
||                       IMPRESION DE LA PORTADA
|| ***********************************************************************

|PROCESO FechaP;
  VAlfa2 = #0#15 % 102;
  VAlfa3 = #0#15 % 402;
  VAlfa4 = #0#15 % 704;

  |SI VAlfa3 = "01";  VAlfa3 = "Enero     ";  |FINSI;
  |SI VAlfa3 = "02";  VAlfa3 = "Febrero   ";  |FINSI;
  |SI VAlfa3 = "03";  VAlfa3 = "Marzo     ";  |FINSI;
  |SI VAlfa3 = "04";  VAlfa3 = "Abril     ";  |FINSI;
  |SI VAlfa3 = "05";  VAlfa3 = "Mayo      ";  |FINSI;
  |SI VAlfa3 = "06";  VAlfa3 = "Junio     ";  |FINSI;
  |SI VAlfa3 = "07";  VAlfa3 = "Julio     ";  |FINSI;
  |SI VAlfa3 = "08";  VAlfa3 = "Agosto    ";  |FINSI;
  |SI VAlfa3 = "09";  VAlfa3 = "Septiembre";  |FINSI;
  |SI VAlfa3 = "10";  VAlfa3 = "Octubre   ";  |FINSI;
  |SI VAlfa3 = "11";  VAlfa3 = "Noviembre ";  |FINSI;
  |SI VAlfa3 = "12";  VAlfa3 = "Diciembre ";  |FINSI;

  fecbo = VAlfa2 + " de " + VAlfa3 + " de " + VAlfa4;
|FPRC;

|PROCESO LeePortadaP;
  |SI SwLee = 0;
      ttes = ttes + 1;
      tdos = tdos + (#1#6 + #1#8 + #1#10 + #1#12 + #1#14 + #3#2);
      |ACABA;
  |FINSI;

  any    = #0#2;                              || A¤o Emision
  preimp = "";                                || Impreso
  predi1 = "";                                || Soporte Colectivo Presentador
  |SI #0#22 = "I";  predis = "X";  |FINSI;    || Soporte Individual
  |SI #0#22 = "C";  predi2 = "X";  |FINSI;    || Soporte Colectivo Declarante

  cargoem  = #0#24;                           || Cargo o Empleo
  |SI #0#22 = "L";
      |HAZ representante;
      #0#90 = represen;
  |FINSI;
  represen = #0#90;                           || Representante;

  |PRINT;
  |PIEINF;

  #1#2 = #0#15;
  #1#3 = "S";
  #1#16 = #0#22; |SI #1#16 = "L"; #1#16 = "I"; |FINSI;

  |GRABA 140#1a;
  |LIBERA #1;
|FPRC;

|PROCESO Portada;
  |HAZ FechaP;

  |IMPRE 0;
  |SI FSalida ! 0;  |ACABA;  |FINSI;

  |INFOR "capor347";
  |SI FSalida ! 0;  |FINIMP;  |ACABA;  |FINSI;

  ttes    = 0;
  tdos    = 0;

  |SI #0#22 = "C";
      SwLee = 0;  |HAZ eosm0009C;

      |DDEFECTO #7;
      |DDEFECTO #1;
      any      = #0#2;                   || A¤o Emision
      preimp   = "";                     || Impreso
      predis   = "";                     || Soporte Individual
      predi1   = "X";                    || Soporte Colectivo Presentador
      predi2   = "";                     || Soporte Colectivo Declarante
      represen = "";                     || Nombre Representante

      #7#0  = 0;                        || Codigo Cliente
      CampoDni = #0#9;  |HAZ LetraDni;
      #7#13 = CampoDni;                 || NIF
      #7#2  = #0#3;                     || Apellidos y Nombre
      #7#3  = #0#4;                     || SG
      #7#4  = #0#5;                     || Calle
      #7#5  = #0#6;                     || Numero
      #7#14 = #0#10 + "-" + #0#11;      || Telefono
      #7#11 = #0#7;                     || Municipio
      #7#12 = #0#8;                     || Provincia
      #7#09 = #0#17;                    || Codigo Postal 1
      #7#10 = #0#18;                    || Codigo Postal 2
      #7#16 = #0#12;                    || Delegacion de Hacienda
      #7#17 = #0#16;                    || Administracion de Hacienda
      #7#18 = #0#91;                    || Delegacion Hacienda
      #7#19 = #0#92;                    || Adminsitracion Hacienda
      |PRINT;
      |PIEINF;
  |FINSI;

  ttes    = 0;
  tdos    = 0;

  SwLee = 1; |HAZ eosm0009C;
  |FININF;
  |FINIMP;
|FPRC;

|| ***********************************************************************
||                       GRABACION SECUENCIAL DISTINTOS TIPOS
|| ***********************************************************************

|PROCESO Tipo2;
  Campos1  = "2";                                || Tipo Registro
  Campos2  = "347";                              || Modelo

  Campos3  = "19";
  |SI #0#2 < 80; Campos3 = "20"; |FINSI;
  VAlfa    = #0#2;   VAlfa = ("00" + VAlfa) % -102;
  Campos3  = Campos3 + VAlfa;                             || Ejercicio

  VAlfa    = #0#12;  VAlfa = ("00" + VAlfa) % -102;
  Campos4  = VAlfa;                                       || Delegacion A.E.A.T.
  CampoDni = #7#13;  |HAZ LetraDni;
  Campos5  = CampoDni;                                    || N.I.F.

  Campos6 = "";
  |SI #2#2 = "1"; Campos6 = "B"; |FINSI;
  |SI #2#2 = "2"; Campos6 = "A"; |FINSI;
  |SI #2#2 = "3"; Campos6 = "C"; |FINSI;
  |SI #2#2 = "4"; Campos6 = "D"; |FINSI;
  |SI #2#2 = "5"; Campos6 = "E"; |FINSI;                   || Clave Operacion

  CampoDni = #2#3;  |HAZ LetraDni;
  Campos7  = CampoDni;                                    || NIF Declarado
  Campos8  = #2#4;                                        || Apellidos y Nombre
  Campos9  = " ";
  |SI #2#15 = "S";  Campos9 = "R";  |FINSI;               || Representante

  VAlfa    = #2#9;  VAlfa = ("00" + VAlfa) % -102;
  Campos10 = VAlfa;                                     || Codigo Postal 1

  VAlfa = "000";
  |SI #2#9 = 99; VAlfa = #2#10; |FINSI;
  VAlfa = ("000" + VAlfa) % -103;
  Campos11 = VAlfa;                                     || Codigo Postal 2

  VAlfa    = #2#5;  VAlfa = ("000000000000000" + VAlfa) % -115;
  Campos12 = VAlfa;                                     || I. Operaciones

  Campos13 = " ";
  |SI #2#13 = "S";  Campos13 = "X";  |FINSI;               || Oper.Seguros

  Campos14 = " ";
  |SI #2#14 = "S";  Campos14 = "X";  |FINSI;               || Arrendamientos

  Campos15 = " " * 88;

  Campos   = Handle   + " " + Campos1 + Campos2 + Campos3 + Campos4;
  Campos   = Campos   + Campos5  + Campos6  + Campos7  + Campos8  + Campos9;
  Campos   = Campos   + Campos10 + Campos11 + Campos12 + Campos13;
  Campos   = Campos   + Campos14 + Campos15;
  Campos   = Campos   + &#0#19 + &#0#20;        || Caracter Final de Linea
  |FGRABA Campos;
  #104#14 = #104#14 + 1;
|FPRC;

|PROCESO Tipo3;
  Campos1  = "3";                                || Tipo Registro
  Campos2  = "347";                              || Modelo

  Campos3  = "19";
  |SI #0#2 < 80; Campos3 = "20"; |FINSI;
  VAlfa    = #0#2;   VAlfa = ("00" + VAlfa) % -102;
  Campos3  = Campos3 + VAlfa;                             || Ejercicio

  VAlfa    = #0#12;  VAlfa = ("00" + VAlfa) % -102;
  Campos4  = VAlfa;                                       || Delegacion A.E.A.T.

  CampoDni = #7#13;  |HAZ LetraDni;
  Campos5  = CampoDni;                                    || N.I.F.

  CampoDni = #4#2;  |HAZ LetraDni;
  Campos6  = CampoDni;                                    || NIF Arrendatario

  Campos7  = #4#3;                                        || Apellidos y Nombre
  VAlfa    = #4#4;  VAlfa = ("000000000000000" + VAlfa) % -115;
  Campos8  = VAlfa;                                       || I. Operaciones

  VAlfa    = #4#11;  VAlfa = ("00" + VAlfa) % -102;
  Campos9  = VAlfa;                                     || Codigo Postal 1
||  VAlfa    = #4#12;  VAlfa = ("000" + VAlfa) % -103;
  Campos10 = "000";                                     || Codigo Postal 2

  Campos11 = #4#14;                                     || Municipio
  Campos12 = #4#15;                                     || Referencia
  Campos13 = #4#5;                                      || SG
  Campos14 = #4#6;                                      || Calle

  VAlfa    = #4#7; |QBT VAlfa;
  VAlfa    = ("00000" + VAlfa) % -105;
  Campos15 = VAlfa;                                     || Numero

  Campos16 = #4#8;                                      || Escalera
  Campos17 = #4#9;                                      || Piso
  Campos18 = #4#10;                                     || Puerta
  Campos19 = " " * 5;
  Campos   = Handle   + " " + Campos1 + Campos2 + Campos3 + Campos4;
  Campos   = Campos   + Campos5  + Campos6  + Campos7  + Campos8  + Campos9;
  Campos   = Campos   + Campos10 + Campos11 + Campos12 + Campos13 + Campos14;
  Campos   = Campos   + Campos15 + Campos16 + Campos17 + Campos18 + Campos19;
  Campos   = Campos   + &#0#19 + &#0#20;        || Caracter Final de Linea
  |FGRABA Campos;
  #104#14 = #104#14 + 1;
|FPRC;

|DEFBUCLE grlin;
 #2#2;
 ;
 tip1, "            ";
 tip2, "zzzzzzzzzzzz";
 be;
 NULL, Tipo2;
|FIN;

|PROCESO eosm0010;
  tip1 = 2;
  tip2 = 2;
  |BUCLE grlin; || las lineas - registro de operaciones
  tip1 = 1;
  tip2 = 1;
  |BUCLE grlin; || las lineas - registro de operaciones
  tip1 = 3;
  tip2 = 9;
  |BUCLE grlin; || las lineas - registro de operaciones
|FPRC;

|PROCESO Tipo1;
  Campos1  = "1";                                || Tipo Registro
  Campos2  = "347";                              || Modelo

  Campos3  = "19";
  |SI #0#2 < 80; Campos3 = "20"; |FINSI;

  VAlfa    = #0#2;   VAlfa = ("00" + VAlfa) % -102;
  Campos3  = Campos3 + VAlfa;                             || Ejercicio

  VAlfa    = #0#12;  VAlfa = ("00" + VAlfa) % -102;

  Campos4  = VAlfa;                                       || Delegacion A.E.A.T.
  CampoDni = #7#13;  |HAZ LetraDni;
  Campos5  = CampoDni;                                    || N.I.F.
  Campos6  = #7#2;                                        || Apellidos y Nombre

  VAlfa    = #1#6; VAlfa = ("000000000" + VAlfa) % -109;
  Campos7  = VAlfa;                       || Total Personas Clave A

  VAlfa    = #1#7;  VAlfa = ("000000000000000" + VAlfa) % -115;
  Campos8  = VAlfa;                       || T. Operaciones Clave A

  VAlfa    = #1#8; VAlfa = ("000000000" + VAlfa) % -109;
  Campos9  = VAlfa;                       || Total Personas Clave B

  VAlfa    = #1#9;  VAlfa = ("000000000000000" + VAlfa) % -115;
  Campos10 = VAlfa;                       || T. Operaciones Clave B

  VAlfa    = #1#10; VAlfa = ("000000000" + VAlfa) % -109;
  Campos11 = VAlfa;                       || Total Personas Clave C

  VAlfa    = #1#11;  VAlfa = ("000000000000000" + VAlfa) % -115;
  Campos12 = VAlfa;                       || T. Operaciones Clave C

  VAlfa    = #1#12; VAlfa = ("000000000" + VAlfa) % -109;
  Campos13 = VAlfa;                       || Total Personas Clave D

  VAlfa    = #1#13;  VAlfa = ("000000000000000" + VAlfa) % -115;
  Campos14 = VAlfa;                       || T. Operaciones Clave D

  VAlfa    = #1#14; VAlfa = ("000000000" + VAlfa) % -109;
  Campos15 = VAlfa;                       || Total Personas Clave E

  VAlfa    = #1#15;  VAlfa = ("000000000000000" + VAlfa) % -115;
  Campos16 = VAlfa;                       || T. Operaciones Clave E

  Campos17 = " ";

  Campos   = Handle   + " " + Campos1 + Campos2 + Campos3 + Campos4;
  Campos   = Campos   + Campos5  + Campos6  + Campos7  + Campos8  + Campos9;
  Campos   = Campos   + Campos10 + Campos11 + Campos12 + Campos13 + Campos14;
  Campos   = Campos   + Campos15 + Campos16 + Campos17;
  Campos   = Campos   + &#0#19 + &#0#20;        || Caracter Final de Linea
  |FGRABA Campos;
  #104#14 = #104#14 + 1;
|FPRC;

|PROCESO LeePortadaC;

  |SI SwLee = 0;
      ttes = ttes + 1;
      tdos = tdos + (#1#6 + #1#8 + #1#10 + #1#12 + #1#14 + #3#2);

      #104#0  = #104#0 + #1#6;
      #104#1  = #104#1 + #1#8;
      #104#2  = #104#2 + #1#10;
      #104#3  = #104#3 + #1#12;
      #104#4  = #104#4 + #1#14;
      #104#5  = #104#5 + #3#2;
      #104#6  = #104#0 + #104#1 + #104#2 + #104#3 + #104#4 + #104#5;

      #104#7  = #104#7  + #1#7;
      #104#8  = #104#8  + #1#9;
      #104#9  = #104#9  + #1#11;
      #104#10 = #104#10 + #1#13;
      #104#11 = #104#11 + #1#15;
      #104#12 = #104#12 + #3#3;
      #104#13 = #104#7  + #104#8 + #104#9 + #104#10 + #104#11 + #104#12;
      #104#15 = #104#15 + 1;

      |ACABA;
  |FINSI;

  |ABRE #3;
  sw_inm = 0;
  #3#0  = #1#0;
  |LEE 040#3=;
  |SI FSalida ! 0; sw_inm = 1; |DDEFECTO #3; |FINSI;
  |CIERRA #3;

  SwLee = 1; |HAZ Tipo1;
  SwLee = 2;  DClave = "A";  HClave = "E"; |HAZ eosm0010;
  |SI sw_inm = 0;
       SwLee = 3;  DClave = "Z";  HClave = "Z";
      |BUCLELIN 0Tipo3#3; || las lineas - Inmuebles.
  |FINSI;
|FPRC;

|PROCESO haztodo;
  enEmpresa = #canempre#2;
  enPeriodo = #canempre#3;
  swOperacion = 0
  |SUB_EJECUTA "camoneda.tab";

  |PATH_DAT #1 dirfich0;
  |PATH_DAT #2 dirfich0;
  |PATH_DAT #3 dirfich0;
  |PATH_DAT #4 dirfich0;
  |PATH_DAT #7 dirfich0;
  empcon = #30#1;
  ejecon = #30#26;

     |ABRE #1;
     |LEE 001#1p;
     |SI FSalida ! 0; |CIERRA #1; |ACABA; |FINSI;

     |ABRE #7;
     #7#0 = #30#2;
     #7#1 = #30#26;
     |LEE 001#7=;
     |SI FSalida ! 0; |CIERRA #1; |CIERRA #7; |ACABA; |FINSI;

     sw_inm = 0;
     |ABRE #3;
     |DDEFECTO #3;
     #3#0 = #30#2;
     |LEE 001#3=;
     |SI #3#2 = 0; sw_inm = 1; |FINSI;

     |CIERRA #1;
     |CIERRA #7;
     |CIERRA #3;
  |SI Opcion = 1;
      |SI #0#22 ! "L"; |HAZ LeePortadaC; |FINSI;
      |SI #0#22 = "L"; |HAZ LeePortadaL; |FINSI;
  |FINSI;
  |SI Opcion = 2;
      |HAZ LeePortadaP;
  |FINSI;
|FPRC;

|| ---------- Seleccion DESDE / HASTA Empresa.

|PROCESO esdesd1;
  |DBASE dir_fich;
  dirfich0 = dir_fich + "fich/";
  alfa1 = #30#2;
  alfa2 = #30#3;
  alfa1 = "00000" + alfa1;   alfa1 = alfa1 % -105;
  alfa2 = "0"   + alfa2;   alfa2 = alfa2 % -101;
  dirfich0 = dirfich0 + alfa1 + alfa2 + "/";
  |SI #30#21 = "I";  |ACABA; |FINSI;
  |SI #30#26 ! #0#2; |ACABA; |FINSI;
  |HAZ haztodo;
|FPRC;

|DEFBUCLE esdesde;
#30#1;
;
#0#26,#0#28;
#0#27,#0#29;
e;
NULL,esdesd1;
|FIN;


|| ---------- Seleccion en parrilla.

|PROCESO esunaauna;
      |PARA xx = 30;  |SI xx [ 49;  |HACIENDO xx = xx + 1;
            |SI #0xx = 0;  |SAL;  |FINSI;
            |DBASE dir_fich;
            dirfich0 = dir_fich + "fich/";
            yy = xx + 20;
            alfa1 = #0xx;
            alfa2 = #0yy;
            alfa1 = "00000" + alfa1;   alfa1 = alfa1 % -105;
            alfa2 = "0"   + alfa2;   alfa2 = alfa2 % -101;
            dirfich0 = dirfich0 + alfa1 + alfa2 + "/";
            || lee empresa
               |ABRE #30;
               #30#2 = #0xx;
               #30#3 = #0yy;
               |LEE 011#30=;
               |CIERRA #30;
               |SI #30#26 ! #0#2;
                   |VETE otramas;
               |FINSI;
               |HAZ haztodo;
      |ET otramas;
      |SIGUE;
|FPRC;

|PROCESO eosm0009C;
  |SI #0#25 = "S";
      |BUCLE esdesde;
  |SINO;
      |HAZ esunaauna;
  |FINSI;
|FPRC;
|| ************************************************************************

|PROCESO Tipo0;
  Campos1  = "0";                                || Tipo Registro
  Campos2  = "347";                              || Modelo

  Campos3  = "19";
  |SI #0#2 < 80; Campos3 = "20"; |FINSI;
  VAlfa    = #0#2;   VAlfa = ("00" + VAlfa) % -102;
  Campos3  = Campos3 + VAlfa;                          || Ejercicio

  VAlfa    = #0#12;  VAlfa = ("00" + VAlfa) % -102;
  Campos4  = VAlfa;                                     || Delegacion A.E.A.T.
  Campos5  = #0#9;                                      || N.I.F.
  Campos6  = #0#3;                                      || Apellidos y Nombre
  Campos7  = #0#4;                                      || SG
  Campos8  = #0#5 % 120;                                || Calle
  Campos9  = #0#6;
  VAlfa    = #0#17;  VAlfa = ("00" + VAlfa) % -102;
  Campos10 = VAlfa;                                     || Codigo Postal 1
  VAlfa    = #0#18;  VAlfa = ("000" + VAlfa) % -103;
  Campos11 = VAlfa;                                     || Codigo Postal 2
  Campos12 = #0#7 % 112;                                || Municipio
  Campos13 = #0#10;                                     || Prefijo
  Campos14 = #0#11;                                     || Telefono
  VAlfa    = ttes; VAlfa = ("00000" + VAlfa) % -105; || Total Declarantes
  Campos15 = VAlfa;
  VAlfa    = tdos; VAlfa = ("000000000" + VAlfa) % -109; || T. Declarados
  Campos16 = VAlfa;
  Campos17 = " " * 53;

  Campos   = Handle   + " " + Campos1 + Campos2 + Campos3 + Campos4;
  Campos   = Campos   + Campos5  + Campos6  + Campos7  + Campos8  + Campos9;
  Campos   = Campos   + Campos10 + Campos11 + Campos12 + Campos13 + Campos14;
  Campos   = Campos   + Campos15 + Campos16 + Campos17;
  Campos   = Campos   + &#0#19 + &#0#20;        || Caracter Final de Linea
  |FGRABA Campos;
  #104#14 = #104#14 + 1;
|FPRC;

|| ************************************************************************
||                  CREACION DISKETTE SOPORTE COLECTIVO E INDIVIDUAL
|| ************************************************************************

|PROCESO Creacion;
  |HAZ FechaP;

  |DDEFECTO #104;

  ttes    = 0;
  tdos    = 0;
  SwLee   = 0;

  |HAZ eosm0009C;

  Raiz       = "";  |DIRECTORIO Raiz;
  Directorio = "";  |DBASE Directorio;
  Directorio = Directorio + "hacienda/" + aTipoSec;  |MKDIR Directorio;
  Directorio = Directorio + "/";
  Valfa = Raiz + "bin/agichmod 777 " + Directorio;
  |SYSTEM Valfa;

  Fichero    = "op" + (("00" + #0#2) % -102);
  NFichero   = Directorio + Fichero;          |QBF NFichero;

  Fichero  = "wb  "+ NFichero;

  Pos = -1;
  |FABRE Fichero;
  Handle = FSalida;
  |SI FSalida ] 0;
      |SI #0#22 = "C";  |HAZ Tipo0;  |FINSI;  || Registro del Presentador
      SwLee = 1;  |HAZ eosm0009C;
  |FINSI;

  FSalida = Handle;
  |FCIERRA FSalida;

  Pos = -1;
  |PUSHV 0501 2380;
  |PINPA #0#104;
  |PINDA #0#104;
  |CONFI "2400SEs Correcto : ";
  |SI FSalida ! 0;
      |FBORRA NFichero;
      Salir = 1;
  |FINSI;
  |POPV;
|FPRC;

|| ************************************************************************
||                  CREACION DISKETTE SOPORTE LASER
|| ************************************************************************

|PROCESO LeePortadaL;

  #0#12 = #7#16;

  Directorio = "";
  |DBASE Directorio;
  Directorio = Directorio + "hacienda/347";
  |MKDIR Directorio;
  Directorio = Directorio + "/";

  Empresa  = #1#0;  Empresa  = ("00000" + Empresa) % -105;

  Fichero    = "347" + Empresa;
  INEmpresas = NEmpresas1 <-;
  INEmpresas = INEmpresas + NACopiar;
  NFichero   = Directorio + Fichero;          |QBF NFichero;
  NEmpresas  = NFichero;
  NACopiar   = NACopiar + 1;

  Fichero  = "wb  "+ NFichero;

  Pos = -1;
  |FABRE Fichero;
  Handle = FSalida;
  |SI FSalida ] 0;
     |HAZ Tipo1;
     SwLee = 2;  DClave = "A";  HClave = "E"; |HAZ eosm0010;
     |SI sw_inm = 0;
         SwLee = 3;  DClave = "Z";  HClave = "Z";
         |BUCLELIN 0Tipo3#3; || las lineas - Inmuebles.
     |FINSI;
  |FINSI;

  FSalida = Handle;
  |FCIERRA FSalida;
|FPRC;

|PROCESO CreacionLaser;
  NACopiar   = 0;
  INEmpresas = NEmpresas1 <-;
  |PARA XPara = 1; |SI XPara [ 500;  |HACIENDO XPara = XPara + 1;
        NEmpresas = "";
        INEmpresas = INEmpresas + 1;
  |SIGUE;

  |HAZ FechaP;
  |HAZ eosm0009C;
|FPRC;

|| ************************************************************************
||                  VERIFICACION
|| ************************************************************************

|PROCESO Verificacion;
     Pos = -1;
     Directorio = "";  |DBASE Directorio;
     Directorio = Directorio + "hacienda/" + aTipoSec;  |MKDIR Directorio;
     Directorio = Directorio + "/";
     Fichero    = "op" + (("00" + #0#2) % -102);
     NFichero = Fichero;          |QBF NFichero;
     Fichero  = "rt  "+ Directorio + NFichero;

     |FABRE Fichero;
     |SI FSalida < 0;
         Valfa = "      No se ha creado el fichero " + NFichero + " Por favor haga la Creacion del Diskette";
         |MENAV  Valfa;
         |ACABA;
     |FINSI;
     |FCIERRA FSalida;

     |HAZ GrabaDisketteV;
|FPRC;

|| ************************************************************************
||                  GRABACION DEL DISKETTE OP
|| ************************************************************************

|PROCESO GrabaDisketteC;
     Pos = -1;
     Directorio = "";  |DBASE Directorio;
     Directorio = Directorio + "hacienda/" + aTipoSec;  |MKDIR Directorio;
     Directorio = Directorio + "/";
     Fichero    = "op" + (("00" + #0#2) % -102);
     NFichero = Fichero;          |QBF NFichero;
     Fichero  = "rt  "+ Directorio + NFichero;

     |FABRE Fichero;
     |SI FSalida < 0;
         Valfa = "      No se ha creado el fichero " + NFichero + " Por favor haga la Creacion del Diskette";
         |MENAV  Valfa;
         |ACABA;
     |FINSI;
     |FCIERRA FSalida;

     Pos = -1;
     |PUSHV 0501 2380;
     |PINPA #1#104;

     E_sup  = 1;
     E_inf  = "ABCDEFGHIJKLMNOPQRSTUVWXYZ";
     Unidad = "A";
     Unidad = 0851 ? 0;
     |ATRI I;
     |PINTA 1616, "Introduzca un diskette formateado en D.O.S. ...";
     |ATRI i;
     |PINTA 2380, ".";
     |PAUSA;

     Raiz = "";
     |DIRECTORIO Raiz; |QBF Raiz;
     Directorio = "";  |DBASE Directorio;
     Directorio = Directorio + "hacienda/" + aTipoSec;  |MKDIR Directorio;
     Directorio = Directorio + "/";
     Fichero    = "op" + (("00" + #0#2) % -102);
     NFichero   = Fichero;          |QBF NFichero;
     Fichero    = Directorio + NFichero;

     |ATRI P;
     |PINTA 1616, "                  Copiando ...                 ";
     |ATRI p;
     |PINTA 2380, ".";

     |QUE_SISTEMA Sistema;
     |SI Sistema ! "ESDOS";
         Valfa = Raiz + "bin/agcopiaa " + Fichero + " " + Unidad + ":";
         |SYSTEM Valfa;

         |ATRI P;
         |PINTA 1616, "           FICHERO COPIADO !!!!                 ";
         |ATRI p;
         |PINTA 2380, ".";
         |PAUSA;
     |SINO;
         Valfa1 = Fichero;
         Valfa2 = Unidad + ":" + NFichero;

         |COPIA_FICHERO Valfa1, Valfa2;
         |SI FSalida ! 0;
             Valfa3 = "Error [" + FSalida + "] en copia de ficheros ...";
             |PINTA 1616, Valfa3;
             |PINTA 2380, ".";
             |PAUSA;
         |SINO;
             |ATRI P;
             |PINTA 1616, "           FICHERO COPIADO !!!!                 ";
             |ATRI p;
             |PINTA 2380, ".";
             |PAUSA;
         |FINSI;
     |FINSI;
     |POPV;
|FPRC;


|| ***********************************************************************
||                       EMISION DE ETIQUETAS
|| ***********************************************************************

|PROCESO Etiquetas;
  |SI #104#14 = 0;
      |MENAV "     Por favor haga la creacion de Diskette.";
      |ACABA;
  |FINSI;

  |PUSHV 0501 2380;

  Campos1  = "19";
  |SI #0#2 < 80; Campos1 = "20"; |FINSI;
  VAlfa    = #0#2;   VAlfa = ("00" + VAlfa) % -102;
  Campos1  = Campos1 + VAlfa;                          || Ejercicio
  VAlfa    = #0#17;  VAlfa = ("00" + VAlfa) % -102;
  Campos2  = VAlfa;                                     || Codigo Postal 1
  VAlfa    = #0#18;  VAlfa = ("000" + VAlfa) % -103;
  Campos3  = VAlfa;                                     || Codigo Postal 2

  #104#16 = "Delegacion .........: " + #7#18;
  #104#17 = "Ejercicio ..........: " + Campos1;
  #104#18 = "Modelo .............: 347";
  #104#19 = "Numero Justificante : ";
  #104#20 = "N.I.F. .............: " + #0#9;
  #104#21 = "Nombre : " + #0#3;
  #104#22 = #0#4 + " " + #0#5 + " " + #0#6 + " " + Campos2 + " " + Campos3 + " " + #0#7;
  #104#23 = "P.Cont.: " + #0#3;
  #104#24 = "Telefono ...........: " + #0#10 + " " + #0#11;
  #104#25 = "Numero total Registros ..: " + #104#14;
  #104#26 = "Densidad soporte : 1,44 MB en Diskettes de 3 1/2";
  #104#27 = "";
  #104#28 = "";
  |C_M #104#27, 1, "N";
  |C_M #104#28, 1, "N";
  |SI #0#22 = "C";
      #104#27 = "Numero total Declarantes : " + #104#15;
      #104#28 = "Numero total Declarados .: " + #104#6;
      |C_M #104#27, 1, "S";
      |C_M #104#28, 1, "S";
  |FINSI;

  |PINPA #2#104;
  |PINDA #2#104;
  |ENDATOS #1#104;
  |SI FSalida = 0;
      |PINPA #2#104;
      |PINDA #2#104;

      |CONFI "2400SRealizar Impresion de la Etiqueta : ";
      |SI FSalida ! 0; |POPV; |ACABA; |FINSI;

      |IMPRE 0;
      |SI FSalida ! 0;  |ACABA;  |FINSI;

      |INFOR "catrasop";
      |SI FSalida ! 0;  |FINIMP;  |ACABA;  |FINSI;

      |PRINT; |PIEINF; |FININF; |FINIMP;
  |FINSI;
  |POPV;
|FPRC;

|| ***********************************************************************
||                       PROCESO DE DISTRIBUCION
|| ***********************************************************************

|PROCESO Distribuye; |TIPO 3;
  |SI #0#2 ] 80;
      enAnyo = 1900 + #0#2;
  |SINO;
      enAnyo = 2000 + #0#2;
  |FINSI;

  |PINPA #0#0;
  |PINDA #0#0;

  |SI #0#22 = "L";
      Opcion = 1; |HAZ CreacionLaser;
      Opcion = 4; |HAZ GrabaDisketteL;
      |ACABA;
  |FINSI;

  |PARA; |SI Salir = 0; |HACIENDO;
         |MENUG MGeneral;
         MGeneral2 = FSalida;
         Opcion    = MGeneral2;
         Salir     = 1;
         |SI Opcion ] 1; |Y Opcion [ 5;
             Salir = 0;
             |SI Opcion = 1; |HAZ Creacion;       |FINSI;
             |SI Opcion = 2; |HAZ Portada;        |FINSI;
             |SI Opcion = 3; |HAZ Etiquetas;      |FINSI;
             |SI Opcion = 4; |HAZ Verificacion;   |FINSI;
             |SI Opcion = 5; |HAZ GrabaDisketteC; |FINSI;
         |FINSI;
  |SIGUE;
|FPRC;
