  ||  el #5 y el #8 estan reservados para los 'INCLUYE'
  || /para 'cacuenta' y 'catradia' respect.
|FICHEROS;
    cactvpag #00;
    cactvptm #01,MANTE;  || temporal de seleccion
    camaasic #02;   || apuntes cabs.
    camaasil #03;   || apuntes lins.
    capagcab #06;   || pagares cabs.
    capaglin #07;   || pagares lins.
    cacuadre #09;   || control pagares y talones
    camandia #10;   || diarios
    cacuebal #11;   || cuentas balance
    camanpag #12;   || pagos
    caconcep #13;   || conceptos automaticos
    caivasop #15;   || conceptos automaticos
    ca8m0002 #422;
|FIN;


|VARIABLES;
    &enDiario   = 0;
    &enContador = 0;

    i     = 0;
    swcod = 0;
    swvto = 0;
    swpag = 0;
    swmar = 0;
    &entrada = 1;
    alfa1 = "";
    alfa2 = "";
    alfa3 = "";
    alfa4 = "";
    nume1 = 0;
    nume2 = 0;
    nume3 = 0;
    nume4 = 0;
    fech1 = @;
    fech2 = @;
    fech3 = @;
    fech4 = @;
    arteris = 0;
    swarteris = 0;
    mes_anyo = "";
    muestra = "";
    digito = 0;
    longi = 0;
    asie = 0;
    apun = 0;
    cuen = "";
    digi = 0;
    sign = "";
    ptas = 0;
    desc = "";
    punt = "";
    descrip = "";
    impo = 0;
    sw = 0;
    FEstado = 0;

    seleccio = 0;
    sw_tipo = 0;
    opcion = 0;
{-1}menu = "";
    menu1 = "2400";
    menu2 = "1";
    menu3 = "";
    menu4 = "RCS";
    menu5 = " Revisar ";
    menu6 = " Contabilizar ";
    menu7 = " Salir ";

     aAlfa1 = "";
     aAlfa2 = "";
     nSwConci = 0;
     aOtro = "";
     aEmisi = "";
     aVenci = "";

     alfa5 = "";
     alfa6 = "";
     nume5 = 0;
     nume6 = 0;
     nSwLaC = 0;
     aMuestraO = "";
     nPara1 = 0;
     aCta   = "";
     nNivel = 0;

     depar = "";
     deparT = "";
     deparL = "";
     swdepar = 0;
     &enEjerBal = 0;
|FIN;

|INCLUYE varnue_i;
|INCLUYE inicio_i;
|INCLUYE actcta_i;
|INCLUYE defec1_i;

|PROCESO mayus; |TIPO 0;
    #0Campo = #0Campo > #0Campo;
    |PINTA #0Campo;
|FPRC;

|| ................... Controla la seleccion de tipos de recibo
|PROCESO tipo_recibo;
    #12#0 = #7#2;        || libro
    #12#1 = #7#3;        || orden
    #12#2 = #7#9;        || serie
    #12#3 = #7#4;        || nro.pago
    |LEE 000#12=;
    |SI FSalida = 0;
        |SI #12#11 < #0#11;|O #12#11 > #0#12;
            sw_tipo = 1;
        |FINSI;
    |SINO;
        alfa1 = "    Pago [" + #12#0 + "." + #12#1 + "." + #12#2 + "." + #12#3 + "] inaccesible ...";
        |MENAV alfa1;
    |FINSI;
    |ERROR;
|FPRC;

|| ................... Controla la grabacion de asientos
|PROCESO contab2;    || graba apunte
    #0#08 = #6#0;   |PINTA #0#08;
    #0#09 = asie;   |PINTA #0#09;
    #0#10 = apun;   |PINTA #0#10;
    |DDEFECTO #3;
    #3#00 = #0#4;   || diario
    #3#01 = #1#10;  || fecha
    #3#02 = asie;   || asiento
    #3#03 = apun;   || apunte
    #3#04 = cuen;   || cuenta
    #3#05 = digi;   || digito
    #3#06 = #0#5;   || concepto
    #3#07 = desc;   || descripcion
    #3#08 = sign;   || signo
    #3#09 = ptas;   || importe
    #3#18 = punt;   || punteado S/N
    |SI #3#8 = "D";
        #3#16 = #3#4;
        #3#17 = #3#5;
        #3#10 = "";
        #3#11 = 0;
    |SINO;
        #3#16 = "";
        #3#17 = 0;
        #3#10 = #3#4;
        #3#11 = #3#5;
   |FINSI;
   #3#21 = depar;
   |GRABA 020#3n;
   |HAZ cabecera;           || cabecera apunte
   ressum = #3#9;
   |HAZ actualiz1;          || cuenta
   |HAZ actualiz3;          || diario
   opera = "A";
   |HAZ altatra;            || subniveles
|FPRC;

|PROCESO contab1;   || controla la grabacion de apuntes
   seleccio = 1;
   |HAZ descripcion;
   |SI apun = 1;
       enDiario = #0#4;
       |HAZ contador;
       asie = enContador;
   |FINSI;
   apun = apun + 10;
   cuen = #6#3;
   digi = #6#4;
   sign = "H";
   ptas = #6#9;
   desc = descrip;
   |HAZ contab2;
   |SI arteris = 1; |O nSwLaC = 1;
       apun = apun + 10;
       |HAZ filtro;
       cuen = alfa2;
       digi = nNivel;
       sign = "D";
       ptas = #6#9;
       desc = descrip;
       punt = " ";
       |SI #0#15 = "S";     punt = "û";    |HAZ BuscaOtra;   |FINSI;
       |HAZ cuenta;
       |HAZ contab2;
       apun = 1;
   |SINO;
       impo = impo + #6#9;
   |FINSI;
   #6#8 = "S";
   |GRABA 020#6a;
   |LIBERA #6;
|FPRC;

|PROCESO BuscoDepar;
 |ABRE #15;
 |DDEFECTO #15;
 #15#0  = #7#2;
 #15#27 = #7#9;
 #15#2 =  #7#3;
 |LEE 001#15=;
 |CIERRA #15;
 |SI swdepar = 0;
     swdepar = 1;
     deparT = #15#49;
 |FINSI;
 |SI deparT ! #15#49; deparT = "00"; |FINSI;
|FPRC;

|PROCESO contabiliza;
    deparT  = "01";
    swdepar = 0;
    |SI eaDepar = "S"; |BUCLELIN 2BuscoDepar#6; |FINSI;
    depar = deparT;
    #6#0 = #1#0;
    |LEE 121#6=;
    |SI FSalida = 0;
        |HAZ contab1;
    |FINSI;
|FPRC;

|DEFBUCLE seleccionados;
 #1#2;
 6;
 "            ",      1, "*";      || ordena por proveedor+pagare
 "zzzzzzzzzzzz", 999999, "*";
 ;
 NULL, contabiliza;
|FIN;

|PROCESO graba_temporal;
||sw_tipo = 0;
||BUCLELIN 2tipo_recibo#6;
||SI sw_tipo ! 0;  |ACABA;   |FINSI;

    |DDEFECTO #1;
    #1#0 = #6#0;         || nro.pagare
    #1#1 = #6#1;         || proveedor
    #1#2 = #6#5;         || f.emision
    #1#3 = #6#6;         || f.vto
    #1#4 = #6#9;         || importe
    #1#5 = #6#11;        || nro.banco
    |SI swmar = 0;       || Javier 03-05-99
          #1#6 = " ";    || marcado NO
    |SINO;
          #1#6 = "*";
    |FINSI;
    #1#7 = #6#3;         || banco
|DDEFECTO #5;  #5#0 = #6#3;   #5#1 = #6#4;   |LEE 000#5=;
    #1#8 = #5#2;         || descrip. banco
    #1#9 = #12#11;       || tipo recibo
    #1#10 = #0#7;        || fecha contable
|GRABA 020#1n;
|SI FSalida = 0;
        seleccio = 1;
|FINSI;
|FPRC;

|DEFBUCLE cactvpag0;
 #6#1;
 6, 12, 7, 8, 3, 14;
 #0#0, #0#2, "S", "S", "N", #0#13, #0#11;      || f.vto., impreso, emision SI,
 #0#1, #0#3, "S", "S", "N", #0#14, #0#12;      ||/vto.NO, cuenta banco
 be;
 NULL, graba_temporal;
|FIN;

|PROCESO min1;
    #1#0 = 1;
|FPRC;

|PROCESO max1;
    #1#0 = 999999;
|FPRC;

|PROCESO temporal;
    alfa1 = Usuario; |QBF alfa1;    |NOME_DAT #1 alfa1; |DELINDEX #1;
    |ABRE #1;       || temporal seleccion
    |BUCLE cactvpag0;
    |CIERRA #1;
    |SI seleccio ! 0;
        |PUSHV 0501 2380;
        |PARA; |SI; |HACIENDO;
               |ENTLINEAL #1#1, -1, 3, 22, 1, min1, max1;
               |MENU menu;
               opcion = FSalida;
               |SI opcion ! 1;
                   |SAL;
               |FINSI;
        |SIGUE;
        |POPV;
    |SINO;
        opcion = 2;
    |FINSI;
|FPRC;

|CALCULO Descrip;
     swcod = 0; swvto = 0; swpag = 0; swmar = 0;
               |SI #0#16 = "S"; swcod = 1; |FINSI;
               |SI #0#17 = "S"; swvto = 1; |FINSI;
               |SI #0#18 = "S"; swpag = 1; |FINSI;
     |CONFI "2400N¨Desea Marcado Automatico de  Pagares? ";
               |SI FSalida = 0; swmar = 1; |FINSI;
|FCAL;


|PROCESO arteris;     || desglosa la contrapartida del control

    |SI #9#80 = "N";
        alfa1 = #9#21;
    |SINO;
        alfa1 = #9#83;
    |FINSI;

    |QBF alfa1;
    alfa2 = alfa1 % 0;
    longi = alfa2;            || 'longi' sirve despues (no tocar)
    nume3 = (longi * 100) + 1;
    alfa2 = alfa1 % nume3;
    |SI alfa2 = "*";|O alfa2 = "%";
        nume3 = 99 + longi;
        muestra = alfa1 % nume3;
        arteris = 1;
        swarteris = 0;
        |SI alfa2 = "%";
            swarteris = 1;
        |FINSI;
    |SINO;
        |HAZ digito;
        |SI sw = 0;   arteris = 2;  |FINSI; || contrapartida fuera de nivel!!!
        |SI sw ! 0;   arteris = 0;  |FINSI;
        muestra = alfa1;
    |FINSI;

    nSwLaC = 0;
    |PARA nume4 = 1; |SI nume4 [ longi; |HACIENDO nume4 = nume4 + 1;
          nume1 = (nume4 * 100) + 1;
          alfa3 = alfa1 % nume1;
          |SI alfa3 = "C";  nSwLaC = 1; |FINSI;
    |SIGUE;

|FPRC;

|PROCESO final;     || graba apunte cuando la contrapartida es unica
    |SI arteris = 0; |Y nSwLaC = 0;
        apun = apun + 10;
        |SI #9#80 = "N";
            cuen = #9#21;
        |SINO;
            cuen = #9#83;
        |FINSI;
        digi = digito;
        sign = "D";
        ptas = impo;
        desc = #0#6;
        punt = " ";
        |SI #0#15 = "S";     punt = "û";    |HAZ BuscaOtra;   |FINSI;
        |QBF desc;
        desc = desc + " " + #0#7;
        |HAZ cuenta;
        |HAZ contab2;
   |FINSI;
|FPRC;

|PROCESO descripcion;
    descrip = #0#6;
    |QBF descrip;

     || Javier 03-05-99
    alfa1 = "";
    |SI swcod = 1;
              alfa1 = #6#0;
    |FINSI;                                      || Codigo Pagare Banco
    |SI swvto = 1;
              alfa1 = (alfa1 + " "+ #6#6);
    |FINSI;                                      || Fecha Vto
    |SI swpag = 1;
              alfa1 = (alfa1 + " "+ #6#11);
    |FINSI;                                      || Nro Pagare Banco

    descrip = (descrip + " " + alfa1);
|FPRC;


|PROCESO subniveles;     || actualiza subniveles de cuentas
|SI modcon = 0;
    |ABRE #8;
    |DDEFECTO #8;
        #8#0 = codcon;
    |LEE 001#8=;
    |SI FSalida = 0;
        |BORRA 020#8a;
    |FINSI;
    |CIERRA #8;
|SINO;
    |SUB_EJECUTA "camaapua.run";
|FINSI;
|FPRC;

|PROCESO cabecera;       || actualiza cabeceras
|SI #3#09 ! 0;
    |DDEFECTO #2;
        #2#00 = #3#0; ||  diario
        #2#01 = #3#1; ||  fecha
        #2#02 = #3#2; ||  documento
        #2#03 = #3#2; ||  asiento
    |LEE 101#2=;
        FEstado = FSalida;
    |SI #3#08 = "D";
            #2#04 = #2#04 + #3#09;
        |SI #2#09 = 0; #2#08 = #3#4; #2#09 = #3#5; |FINSI;  || contrp.HABER
    |FINSI;
    |SI #3#08 = "H";
            #2#05 = #2#05 + #3#09;
        |SI #2#11 = 0; #2#10 = #3#4; #2#11 = #3#5; |FINSI;  || contrp.DEBE
    |FINSI;
        #2#6 = #2#4 - #2#5;      || saldo
    |SI FEstado = 0;  |GRABA 020#2a;  |FINSI;
    |SI FEstado ! 0;  |GRABA 020#2n;  |FINSI;
    |LIBERA #2;
|FINSI;
|FPRC;

|PROCESO actualiz3;
    |ABRE #10;
    #10#0 = #3#0;
    |LEE 121#10=;
    |SI FSalida = 0;
        |SI #3#8 = "D";  #10#2 = #10#2 + #3#9;  |FINSI;
        |SI #3#8 = "H";  #10#3 = #10#3 + #3#9;  |FINSI;
        |GRABA 020#10a;
    |FINSI;
    |LIBERA #10;
    |CIERRA #10;
|FPRC;

|PROCESO digito;         || saca el digito de la contrapartida del control
    sw   = 0;
    digito = 0;  || inicializa el digito
|SI longi = dig4; |Y dig4 ! 0; sw = 1; digito = 1; |FINSI;
|SI longi = dig5; |Y dig5 ! 0; sw = 1; digito = 2; |FINSI;
|SI longi = dig6; |Y dig6 ! 0; sw = 1; digito = 3; |FINSI;
|SI longi = dig7; |Y dig7 ! 0; sw = 1; digito = 4; |FINSI;
|SI longi = dig8; |Y dig8 ! 0; sw = 1; digito = 5; |FINSI;
|SI longi = dig9; |Y dig9 ! 0; sw = 1; digito = 6; |FINSI;
|SI sw = 0;
    |MENAV "    Contrapartida de Pagos Fuera de Nivel!!! Proceso Abortado ...";
|FINSI;
|FPRC;

|PROCESO filtro;         || construye la contrapartida individual
  |SI #9#80 = "N";
      aCta   = #6#1;
      nNivel = #6#2;
  |SINO;
      aCta   = #6#3;
      nNivel = #6#4;
  |FINSI;
  |SI nNivel = 1;  nume1 = dig4;  |FINSI;
  |SI nNivel = 2;  nume1 = dig5;  |FINSI;
  |SI nNivel = 3;  nume1 = dig6;  |FINSI;
  |SI nNivel = 4;  nume1 = dig7;  |FINSI;
  |SI nNivel = 5;  nume1 = dig8;  |FINSI;
  |SI nNivel = 6;  nume1 = dig9;  |FINSI;

  aMuestraO = muestra;
  |SI nSwLaC = 1;
      alfa5 = muestra % 0;
      nume5 = alfa5;
      alfa5 = "";
      |PARA nPara1 = 1; |SI nPara1 [ nume5; |HACIENDO nPara1 = nPara1 + 1;
            nume6 = (nPara1 * 100) + 1;
            alfa6 = muestra % nume6;
            |SI alfa6 = "C";
                alfa6 = aCta % nume6;
            |FINSI;
            alfa5 = alfa5 + alfa6;
      |SIGUE;

      muestra = alfa5;
      alfa2   = muestra;
  |FINSI;

  |SI arteris = 1;
      nume2 = nume1 - (longi - 1);
      |SI swarteris = 0;
          nume2 = (100 * longi) + nume2;
          alfa1 = aCta % nume2;
          alfa2 = muestra + alfa1;             || si es "*"
      |SINO;
          nume2 = nume2 - 4;
          |SI nume2 < 0;
              |MENAV "    Cuenta 'Efec.Pagar' del Control Pagares con longitud fuera de nivel ...";
              nume2 = nume2 + 4;
              alfa1 = "0" * nume2;
              alfa2 = muestra + alfa1;
          |SINO;
              |SI nume2 = 0;  alfa1 = "";              |FINSI;   || cuenta ajustada
              |SI nume2 > 0;  alfa1 = "0" * nume2;     |FINSI;   || rellena de ceros
              mes_anyo = (#6#6 % 402) + (#6#6 % -102);
              alfa2 = muestra + alfa1 + mes_anyo;  || si es "%"
          |FINSI;
      |FINSI;
  |FINSI;
  muestra = aMuestraO;
|FPRC;

|PROCESO cuenta;    || comprueba si la cuenta esta dada de alta
    |ABRE #5;
    #5#0 = cuen;
    #5#1 = digi;
    |LEE 001#5=;
    |SI FSalida ! 0;
        |DDEFECTO #5;
        #5#0 = cuen;
        #5#1 = digi;
        |HAZ defect1;
        |PUSHV 0501 2380;
        |PINPA #0#5;
        |PINDA #0#5;
        |ENDATOS #1#5;
        |SI FSalida = 0;
            |GRABA 020#5n;
        |FINSI;
        |POPV;
    |FINSI;
    |CIERRA #5;
|FPRC;

**************************************************************************
          CALCULOS DESDE CAMPOS
**************************************************************************

|PROCESO limite; |TIPO 0;
|SI #0#7 < fec1;|O #0#7 > fec2;
    |MENAV "    ATENCION!!! La fecha no corresponde al ejercicio contable ...";
    |ERROR;
|FINSI;
|FPRC;

|PROCESO final816; |TIPO 7;
 |PTEC 816;
|FPRC;

**************************************************************************
          CALCULOS DESDE CAMPOS SELECCION
**************************************************************************
|PROCESO tipo11; |TIPO 11;
|SI FSalida = 9;
    |SI #1#6 = "*";
            #1#6 = " ";
    |SINO;
            #1#6 = "*";
    |FINSI;
    |PINTA #1#6;
    |GRABA 020#1a;
|FINSI;
|FPRC;

|PROCESO limite1; |TIPO 0;
|SI #1#10 < fec1;|O #1#10 > fec2;
    |MENAV "    ATENCION!!! La fecha no corresponde al ejercicio contable ...";
    |ERROR;
|FINSI;
|FPRC;

|PROCESO BuscaOtra_1;
     |DDEFECTO #13; #13#0 = #3#6;  |LEE 000#13=;
     aAlfa1 = #3#7;      |QBF aAlfa1;
     aAlfa2 = #13#1;     |QBF aAlfa2;

     aEmisi = aAlfa1 - aAlfa2;     || descripcion emision
     |QBT aEmisi;

     aAlfa1 = desc;      |QBF aAlfa1;
     aAlfa2 = #0#6;      |QBF aAlfa2;

     aVenci = aAlfa1 - aAlfa2;     || descripcion vencimiento
     |QBT aVenci;

     |SI aEmisi = aVenci;
          #3#18 = "û";
          |GRABA 020#3a;
          nSwConci = 1;
          |ERROR10;
     |FINSI;
     |LIBERA #3;
|FPRC;

|DEFBUCLE BuscaOtra;
     #3#3;
     8, 9, 18;
     cuen, digi, fec1,      0, aOtro, ptas, " ";
     cuen, digi, fec2, 999999, aOtro, ptas, " ";
     be;
     NULL, BuscaOtra_1;
|FIN;

|PROCESO BuscaOtra;
     nSwConci = 0;
     |SI sign = "D";     aOtro = "H";   |FINSI;
     |SI sign = "H";     aOtro = "D";   |FINSI;
     |BUCLE BuscaOtra;
     |SI nSwConci = 0;
          aAlfa1 = "    Cuenta [" + cuen + "] sin correspondencia para conciliar ...";
          |MENAV aAlfa1;
     |FINSI;
|FPRC;

|CALCULO  proceso; |TIPO 3;
     |SI #30#26 < 80;
         enEjercicio = 2000 + #30#26;
     |SINO;
         enEjercicio = 1900 + #30#26;
     |FINSI;

    |ABRE #9;
    |LEE 000#9p;
    |SI FSalida = 0;
        |HAZ Descrip;                            || Javier 03-05-99
        |ABRET #2;       || apuntes cabs.
        |ABRET #3;       || apuntes lins.
        |ABRET #5;       || cuentas
        |ABRET #12;      || pagos
        |ABRET #13;      || conceptos

        |HAZ temporal;
        |SI opcion = 2;
            |SI seleccio ! 0;
                    seleccio = 0;
                    apun = 1;
                |HAZ arteris;
                |SI arteris ! 2;          || error en el calculo 'digito'
                    |ABRE #6;

                    |BUCLE seleccionados;
                    |CIERRA #6;
                    |SI impo ! 0;
                        |HAZ final;  || contrapartida englobada
                    |FINSI;
                |FINSI;
                |SI seleccio = 0;
                    |MENAV "    Seleccion sin marcas ...";
                |FINSI;
            |SINO;
                |MENAV "    Seleccion vacia ...";
            |FINSI;
        |FINSI;
        |CIERRAT #2;
        |CIERRAT #3;
        |CIERRAT #5;
        |CIERRAT #12;
        |CIERRAT #13;
        |DELINDEX #1;
    |SINO;
        |MENAV "    ATENCION!!! Control de Pageres y Talones sin definir ...";
    |FINSI;
    |HAZ subniveles;
    |CIERRA #9;
|FCAL;
