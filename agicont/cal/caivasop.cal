|FICHEROS;
   caivasop #0;
   cacuenta #4;
   caclipro #5;
   calibros #10;
   caivaasi #100;
   caivases #101;

   cacon300 #17;

   caivasof #121;
|FIN;

|VARIABLES;
    potra = 0;
    mensa1 = "    ERROR ESTA FECHA NO CORRESPONDE AL EJERCICIO";
    men1  = "     NO EXISTE EMPRESA !";
    men2  = "     Longitud de Cuenta Fuera de Nivel !";
    men3  = "      Esta Cuenta NO tiene Pase directo";
    cont = 0;
    long = "  ";
    cta  = 0;
    num   = 0;
    sw = 0;
    traba1 = "";
    traba2 = "";
    trasum = 0;
    juan = 0;
    jose = 0;
    pepe = 0;
    rafa = 0;
    anter = 0;
    antnum = 0;
    nCalc  = 0;
    nImpCartera = 0;


    alfa1 = "";
    alfa2 = "";
    alfa3 = "";
    alfa4 = "";
    nume1 = 0;
    nume2 = 0;
    nume3 = 0;
    nume4 = 0;
    trim1 = @;
    trim2 = @;
    trim3 = @;
    modo = 0;
    xy = 0;

     letra = "";
     final = "";
     x = "";
     lugar = 0;
     nifnum = 0;
     resto = 0;

  primer = "";
  swf1   = 0;

  Libro = 0;
  Serie = "";
  SerieAnt = "  XX";
  LibroAnt = -1;
  Factura = 0;

  Period = "";
  Comodin = "";
  ComodNum = 0;
  aCuentaA = "";

     aNifQbf1   = "";
     aNifQbf2   = "";

     nInkey     = 0;
     nEstado    = 0;

     aBase               = "",
     aAbre               = "";
     aFic                = "";
     aEmp                = "",
     aLib                = "",
     aSer                = "";
     aFac                = "";
     aAlfa               = "";

     nMod          = 0;
     nOk           = 0;
     nBase         = 0;
     nBtnRPDF      = -1;
     nBtnVPDF      = -1;

 {-1}aUtil  = "";
     aUtil1 = "2400";
     aUtil2 = "1";
     aUtil3 = "";
     aUtil4  = "RE";
     aUtil5  = "Informe registros con o sin PDF";
     aUtil6  = "Empaquetador de PDF";


    &entrada = 1;
    &libro = 0;
    &orden = 0;
    &serie = "";
    &pesetas = 0;
    &cuenta = "";
    &digito = 0;
    &emision = "";
    &nConcep = 0;
    &aDesc   = "";
  &ct1_am = "";
  &ct2_am = 0;
  &fec_am = @;
  &imp_am = 0;
  &prv_am = "";
  &raf_sw = 0;
  &c_am  = "";
     &eaVarDni  = "";
     &enCalcCif = 0;
    &eaMensaCad = "";
    &enErrorCad = 0;

     &eaSiiPro     = "";
     &enSiiLib     = 0;
     &eaSiiDoc     = "";
     &enSiiDoc     = 0;
     &eaSiiSer     = "";
     &enSiiLin     = 0;
     &enSiiIva     = 0;
     &enSiiMod     = 0;
     &eaSiiCta     = "";
     &eaSiiDig     = "";
     &eaSiiCpt     = "";
     &eaSiiDes     = "";
     &enSiiL       = 0;
     &eaSiiEje     = "";
     &enSiiPer     = 0;
     &enSiiRet     = 0;
     &enSiiUlt     = 0;
     &eaSiiCIF     = "";
     fecha         = @;
|FIN;

|INCLUYE acceso_i;
|INCLUYE pagoss_i;
|INCLUYE puntos_i;
|INCLUYE caliva_i;
|INCLUYE vardep_i;
|INCLUYE varcaj_i;
|INCLUYE varnue_i;

|PROCESO Tipo8; |TIPO 8;
     eaMensaCad = "9";
     |HAZ ControlCaducidad;
     |SI enErrorCad = 1;
         |PTEC 807;
         |ACABA;
     |FINSI;

     |HAZ inicio;
     |HAZ eVarEmpresa;
     |HAZ BuscaModulo;
|FPRC;

|PROCESO EnciendeBtn;
       |ESTADO_CONTROL nBtnVPDF, "DISABLE";

       |SI modo ! 0;  |Y modo ! 4;
           |ACABA;
       |FINSI;

       aEmp = (("00000" + #48#2) % -105) + #48#3;
       aLib = ("00" + #caivasop#0) % -102;
       aSer = #caivasop#27;  |QBF aSer;
       aSer = ("__" + aSer) % -102;
       aFac = #caivasop#2;  |QBF aFac;
       aFac = ("000000" + aFac) % -106;
       aFic = aEmp + aLib + aSer + aFac + ".pdf";
       |DBASS aBase;
       aAbre = aBase + "hogareur/hogardoc/CC" + aFic;
       |XABRE "E", aAbre, 0;
       |SI FSalida ] 0;
           |ESTADO_CONTROL nBtnVPDF, "ENABLE";
       |FINSI;
|FPRC;

|PROCESO VerPDF;
     aAlfa = "|hogarz01;CC" + aFic;
     |SUB_EJECUTA aAlfa;
|FPRC;

|PROCESO RecPDF;
     aAlfa = "|hogarz01;CR" + aFic;
     |SUB_EJECUTA aAlfa;

     |HAZ EnciendeBtn;
|FPRC;

|CALCULO Tipo13; |TIPO 13;
   modo = (FEntrada / 100) ! 0;

   |SI swModulo = 3824;
       |SI nBtnVPDF ! -1;
           |ESTADO_CONTROL nBtnRPDF, "ENABLE";

           |HAZ EnciendeBtn;
           |ACABA;
       |FINSI;

       |CONTROL_SIMPLE 0, "BOTON,{{197}}Recoger factura PDF", 1665, 1676, RecPDF;
       nBtnRPDF = FSalida;

       |CONTROL_SIMPLE 0, "BOTON,{{197}}Ver factura PDF", 1765, 1776, VerPDF;
       nBtnVPDF = FSalida;

       |ESTADO_CONTROL nBtnRPDF, "DISABLE";

       |HAZ EnciendeBtn;
   |FINSI;

   |ABRE #121;
   |DDEFECTO #121;
   #121#0 = #0#0;
   #121#1 = #0#27;
   #121#2 = #0#2;
   |LEE 001#121=;
   |PINTA 0967, #121#3;
   |CIERRA #121;
|FCAL;

|PROCESO PideFOperacion;
   |ABRE #121;
   |DDEFECTO #121;
   #121#0 = #0#0;
   #121#1 = #0#27;
   #121#2 = #0#2;
   |LEE 101#121=;
   nEstado = FSalida;
   |SI nEstado ! 0;
       #121#0 = #0#0;
       #121#1 = #0#27;
       #121#2 = #0#2;
       #121#3 = #0#3;
   |FINSI;
   |PINTA 0967, #121#3;
   |ENCAMPO #3#121;
   |SI nEstado = 0; |GRABA 020#121a; |FINSI;
   |SI nEstado ! 0; |GRABA 020#121n; |FINSI;
   |LIBERA #121;
   |CIERRA #121;
|FPRC;

|PROCESO EntraSerie; |TIPO 7;
   |ABRE #caivaasi;
   |LEE 010#caivaasi.p;
   |SI FSalida ! 0;
      |DDEFECTO #caivaasi;
      |GRABA 020#caivaasi.n;
   |FINSI;
   |CIERRA #caivaasi;

   Comodin = #caivaasi#44;
   |C_M #caivasop#27,1,Comodin;
|FPRC;

|PROCESO fecha;
  nInkey = FSalida;
  |SI nInkey = 11;
      |HAZ PideFOperacion;
      |ERROR;
      |ACABA;
  |FINSI;

  |SI #0#3 < fec1; |O #0#3 > fec2;
      |MENAV mensa1;
      |ERROR;
      |ACABA;
  |FINSI;
|FPRC;

|PROCESO serieSi;
 |SI swf1 = 0;
     |C_V #0#27,1,"S";
     |C_M #0#27,1,"S";
   ||  |PINTA 0821, "Serie: ";
     |PINTA 0924, "S:   ";
     swf1 = 1;
 |SINO;
     |C_V #0#27,1,"N";
     |C_M #0#27,1,"N";
   ||  |PINTA 0821, "       ";
     |PINTA 0924, "     ";
     swf1 = 0;
 |FINSI;
|FPRC;

|PROCESO funcio1; |TIPO 0;
|SI FSalida = 9;
    |HAZ serieSi;
    |ERROR;
    |ACABA;
|FINSI;
|FPRC;

|PROCESO cheq_libro; |TIPO 0;
|SI FSalida = 9;
    |HAZ serieSi;
    |ERROR;
    |ACABA;
|FINSI;
|SI #10#2 ! "S";
    |MENAV "    ATENCION!!! Libro de I.V.A. Repercutido ...";
    |ERROR;
|FINSI;
|FPRC;

|PROCESO lee_libro1;
|ERROR10;
|FPRC;

|DEFBUCLE lee_libro;
 #10#1;
 2;
  1, "S";
 99, "S";
 e;
 NULL, lee_libro1;
|FIN;

|PROCESO deflib; |TIPO 7;
|DDEFECTO #10;
|BUCLE lee_libro;
    #0#0 = #10#0;
|PINTA #0#0;
|FPRC;

|PROCESO cuentaAnt; |TIPO 7;
 aCuentaA = #0#4;
|FPRC;

|PROCESO cuefin; |TIPO 0;
    modo = (FEntrada / 100) ! 0;
    |ABRE #4;
    #4#0 = #0#4;
    #4#1 = #0#5;
    |LEE 001#4=;
    |SI FSalida = 0;
        |SI #4#3 = "S";
            |HAZ lee_provee;
            ||SI modo = 1;  |HAZ lee_provee;  |FINSI;     || solo en alta
        |SINO;
            |MENAV men3;
            |ERROR;
        |FINSI;
    |SINO;
        |ERROR;
    |FINSI;
    |CIERRA #4;
|FPRC;

|PROCESO lee_provee;
  |SI aCuentaA = #0#4; |ACABA; |FINSI;
  |ABRE #5;
  #5#23 = "P";
  #5#10 = #0#4;   || cuenta
  #5#11 = #0#5;   || digito
  |LEE 001#5=;
  |SI FSalida ! 0;
      |DDEFECTO #5;
  |FINSI;
  #0#50 = #4#2;        || carga el nombre del plan contable
  #0#51 = #5#9;        || carga el CIF del cliente
  |PINTA #0#50;
  |PINTA #0#51;
  |CIERRA #5;
|FPRC;

|PROCESO cuenta; |TIPO 6;
    modo = (FEntrada / 100) ! 0;

    p_alfa1 = #0Campo;
    salidas = FSalida;
    |HAZ punto;
    |SI p_error ! 0;  |ERROR;  |ACABA;  |FINSI;
    |SI #0#4 = "            ";|Y modo ! 4;  |ERROR;  |ACABA;  |FINSI;
    cta  = #0#4;
    long = cta % A0;
    cont = 0;
    sw = 0;
    |SI long = #30#14; sw = 1; #0#5 = 1; |FINSI;
    |SI long = #30#15; sw = 1; #0#5 = 2; |FINSI;
    |SI long = #30#16; sw = 1; #0#5 = 3; |FINSI;
    |SI long = #30#17; sw = 1; #0#5 = 4; |FINSI;
    |SI long = #30#18; sw = 1; #0#5 = 5; |FINSI;
    |SI long = #30#19; sw = 1; #0#5 = 6; |FINSI;
    |SI sw = 0;
        |MENAV men2;
        |ERROR;
        |ACABA;
    |FINSI;
|FPRC;

|PROCESO T1; |TIPO 1;
  modo = (FEntrada / 100) ! 0;
  |SI modo = 3;
      |ABRE #121;
      |DDEFECTO #121;
      #121#0 = #0#0;
      #121#1 = #0#27;
      #121#2 = #0#2;
      |LEE 101#121=;
      |SI FSalida = 0;
          |BORRA 020#121a;
      |FINSI;
      |LIBERA #121;
      |CIERRA #121;
  |FINSI;
  |SI modo = 3; |Y enEjercicio ] 2014;
      aTipoIVA    = "S";
      eDevenCaja  = 0;
      enLibroCaja = #0#0;
      enRegisCaja = #0#2;
      eaSerieCaja = #0#27;
      enConcepto  = #0#6;
      |HAZ EsCajaIva;
      |SI enSwCaja = 1;
          eModoCaja = 3;
          |HAZ eTrataClaveZ;
     |FINSI;
  |FINSI;
|FPRC;

|PROCESO ParaRECC;
     |SI enEjercicio ] 2014;
         aTipoIVA    = "S";
         eDevenCaja  = 0;
         enLibroCaja = #0#0;
         enRegisCaja = #0#2;
         eaSerieCaja = #0#27;
         enConcepto  = #0#6;
         |HAZ EsCajaIva;
         eModoCaja = xy;
         |SI xy = 1; |O xy = 2;  eSwCambio = 1; |FINSI;
         |HAZ eTrataClaveZ;
     |FINSI;
     enSwCaja  = 0;
     eSwCambio = 0;
|FPRC;

|PROCESO auxili; |TIPO 3;

 xy = (FEntrada / 100) ! 0;
 alfa1 = #0#35; |QBF alfa1;
 |SI alfa1 = "S";
     nImpCartera = #0#8 + #0#9 + #0#10 + #0#52 + #0#53;
 |SINO;
     nImpCartera = #0#26;
 |FINSI;

 |HAZ ParaRECC;

 |HAZ pagos;
 |SI xy = 1;
     |HAZ el_inver;
 |FINSI;
|FPRC;

|PROCESO borra; |TIPO 2;
|HAZ borra_pag;
|FPRC;

|PROCESO nodepar; |TIPO 7;
   |CAMPO_MODIFICA #0#49, 1, eaDepar;
|FPRC;

|CALCULO LosDep; |TIPO 0;
   |SI eaDepar = "N"; |ACABA; |FINSI;

   eOpDep = 0; || 1 = Consulta pantalla
   |SI FSalida = 10; eOpDep = 1;  |FINSI;
   eaCodDep    = #0Campo;
   |HAZ Actividad;
   |SI eaCodDep = ""; eswLect = 0; |FINSI;
   |SI eswLect = 1;
       |MENAV "    Error.! No Existe Departamento";
       |ERROR;
   |SINO;
       #0Campo = eaCodDep; |PINTA #0Campo;
   |FINSI;
|FCAL;

|PROCESO periodo;
  modo = (FEntrada / 100) ! 2;
  fecha = #0#3;
  |SI modo ! 1; |Y #caivaasi#53 = "A";
      fecha = #0#46;
  |FINSI;
  |SI Period = "T";
    alfa1 = fecha;   alfa1 = alfa1 % -104;      || saca a¤o
    alfa2 = "31.03." + alfa1;  trim1 = alfa2;  || primer trimestre
    alfa2 = "30.06." + alfa1;  trim2 = alfa2;  || segundo trimestre
    alfa2 = "30.09." + alfa1;  trim3 = alfa2;  || tercer trimestre
    |SI fecha [ trim1;                   #0#44 = 1;  |FINSI;
    |SI fecha > trim1;|Y fecha [ trim2;  #0#44 = 2;  |FINSI;
    |SI fecha > trim2;|Y fecha [ trim3;  #0#44 = 3;  |FINSI;
    |SI fecha > trim3;                   #0#44 = 4;  |FINSI;
  |SINO;
    alfa1 = fecha;
    alfa1 = alfa1 % 0402;
    #caivasop#44 = alfa1;
  |FINSI;
  |PINTA #0#44;
|FPRC;

|PROCESO letra_0;  |TIPO 6;
     |SI FSalida = 9;
         eaVarDni   = #0Campo;
         enCalcCif  = 1;
         |HAZ CalculoDNI;
         #0Campo = eaVarDni;
         |PINTA #0Campo;
         |ERROR;
         |ACABA;
     |FINSI;

     eaVarDni  = #0Campo;
     enCalcCif = 0;
     |HAZ CalculoDNI;

     aNifQbf1 = #0Campo;   |QBF aNifQbf1;
     aNifQbf2 = eaVarDni;  |QBF aNifQbf2;

     |SI aNifQbf1 ! aNifQbf2; |Y aNifQbf1 ! "";
         |SI #caclipro#24 = "ES "; |O #caclipro#24 = "E  "; |O  #caclipro#24 = "   ";
             |CONFI "0000SEl Nif puede ser Erroneo. Desea Calcularlo.";
             |SI FSalida = 0;
                 #0Campo = eaVarDni;  |PINTA #0Campo;
                 |ERROR;
             |FINSI;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO el_inver;
 |SI #0#48 ! "S";
     |ACABA;
 |FINSI;
  |AVISO;
  |CONFI "2400SFra. de Inversion. Desea crear el Bien Amortizable S/N : ";
  |SI FSalida ! 0;
      |ACABA;
  |FINSI;
  fec_am = #0#3;
  imp_am = #0#8 + #0#9 + #0#10 + #0#52 + #0#53;
  c_am = #0#7;
  ct1_am = "";
  ct2_am = 0;
  prv_am = "";
  primer = #0#4 % 101;
  |SI primer = "2";
      ct1_am = #0#4;
      ct2_am = #0#5;
  |SINO;
      prv_am = #0#50;
  |FINSI;
  |SUB_EJECUTA_NP "camocab1.tab";
|FPRC;

|PROCESO con_digito; |TIPO 7;
    modo = (FEntrada / 100) ! 0;
|SI modo = 4;
        alfa1 = "S";
|SINO;
        alfa1 = "N";
|FINSI;
|C_M #0#5, 1, alfa1;
|C_V #0#5, 1, alfa1;
|FPRC;

|PROCESO TipoFact; |TIPO 7;
   |ABRE #caivaasi;
   |LEE 010#caivaasi.p;
   |SI FSalida ! 0;
      |DDEFECTO #caivaasi;
      |GRABA 020#caivaasi.n;
   |FINSI;
   |CIERRA #caivaasi;
   Period = #caivaasi#46;
|FPRC;

|PROCESO AntesOrden; |TIPO 7;
   modo = (FEntrada / 100) ! 0;
   |SI modo ! 1; |ACABA; |FINSI;

   |SI #caivaasi#44 = "S";
      |ABRE #caivases;
      |SI SerieAnt ! "  XX";
         #caivases#0 = SerieAnt;
         #caivases#1 = "S";
         |LEE 110#caivases.=;
         |SI FSalida = 0;
            nCalc = #caivases#3 - #caivases#4;
            |SI nCalc = #caivasop#2;
               #caivases#3 = nCalc;
               |GRABA 020#caivases.a;
            |FINSI;
            |LIBERA #caivases;
         |FINSI;
      |FINSI;
      SerieAnt = #caivasop#27;
      #caivases#0 = #caivasop#27;
      #caivases#1 = "S";
      |LEE 110#caivases.=;
      |SI FSalida = 0;
         |SALVA_DATOS #caivasop;
         Libro = #caivasop#0;
         Serie = #caivasop#27;
         #caivasop#2 = 1;
         |LEE 010#caivasop.];
         |SI FSalida = 0;
            |SI #caivasop#27 = Serie; |Y #caivasop#0 = Libro;
               |PARA; |SI #caivasop#27 = Serie; |Y #caivasop#0 = Libro; |HACIENDO;
                   Factura = #caivasop#2 + 1;
                   |LEE 010#caivasop.s;
                   |SI FSalida ! 0; |SAL; |FINSI;
               |SIGUE;
            |SINO;
               Factura = #caivases#3;
            |FINSI;
         |SINO;
            Factura = 1;
         |FINSI;
         |REPON_DATOS #caivasop;
         |SI Factura > #caivases#3;
            #caivasop#2 = Factura;
         |SINO;
            #caivasop#2 = #caivases#3;
         |FINSI;
         |PINTA #caivasop#2;
         #caivases#3 = #caivases#3 + #caivases#4;
         |GRABA 020#caivases.a; |LIBERA #caivases;
      |SINO;
         #caivasop#2 = 1;
      |FINSI;
      |CIERRA #caivases;
   |SINO;
      |ABRE #calibros;
      |SI LibroAnt ! -1;
         #calibros#0 = LibroAnt;
         |LEE 101#calibros.=;
         |SI FSalida = 0;
            nCalc = #calibros#4 - #calibros#3;
            |SI nCalc = #caivasop#2;
               #calibros#4 = #calibros#4 - #calibros#3;
               |GRABA 020#calibros.a;
            |FINSI;
            |LIBERA #calibros;
         |FINSI;
      |FINSI;
      LibroAnt = #caivasop#0;
      #calibros#0 = #caivasop#0;
      |LEE 101#calibros.=;
      |SI FSalida = 0;
         Libro = #caivasop#0;
         |SALVA_DATOS #caivasop;
         #caivasop#0 = Libro;
         #caivasop#27 = "  ";
         #caivasop#2 = 1;
         |LEE 010#caivasop.];
         |SI FSalida = 0;
            |SI #caivasop#0 = Libro; |Y #caivasop#27 = "  ";
               |PARA; |SI #caivasop#0 = Libro; |Y #caivasop#27 = "  "; |HACIENDO;
                  Factura = #caivasop#2 + 1;
                  |LEE 010#caivasop.s;
                  |SI FSalida ! 0; |SAL; |FINSI;
               |SIGUE;
            |SINO;
               Factura = #calibros#4;
            |FINSI;
         |SINO;
            Factura = 1;
         |FINSI;
         |REPON_DATOS #caivasop;
         |SI Factura > #calibros#4;
            #caivasop#2 = Factura;
         |SINO;
            #caivasop#2 = #calibros#4;
         |FINSI;
         |PINTA #caivasop#2;
         #calibros#4 = #calibros#4 + #calibros#3;
         |GRABA 020#calibros.a; |LIBERA #calibros;
      |FINSI;
      |LIBERA #calibros;
      |CIERRA #calibros;
   |FINSI;
|FPRC;

|PROCESO TrasOrden;
   modo = (FEntrada / 100) ! 0;
   |SI modo ! 1; |ACABA; |FINSI;

   |SI #caivaasi#44 = "S";
      |ABRE #caivases;
      #caivases#0 = #caivasop#27;
      #caivases#1 = "S";
      |LEE 110#caivases.=;
      |SI FSalida = 0;
         nCalc = #caivases#3 - #caivases#4;
         |SI nCalc ! #caivasop#2;
            #caivases#3 = nCalc;
            |GRABA 020#caivases.a;
         |FINSI;
         |LIBERA #caivases;
      |FINSI;
      |CIERRA #caivases;
   |SINO;
      |ABRE #calibros;
      #calibros#0 = LibroAnt;
      |LEE 101#calibros.=;
      |SI FSalida = 0;
         nCalc = #calibros#4 - #calibros#3;
         |SI nCalc ! #caivasop#2;
            #calibros#4 = #calibros#4 - #calibros#3;
            |GRABA 020#calibros.a;
         |FINSI;
         |LIBERA #calibros;
      |FINSI;
      |CIERRA #calibros;
   |FINSI;
|FPRC;

|PROCESO Tipo11; |TIPO 11;
|HAZ T12Sop;
|FPRC;

|PROCESO T12Sop; |TIPO 12;
     |SI FSalida = 7;
        |SI #caivaasi#44 = "S";
           |ABRE #caivases;
           #caivases#0 = #caivasop#27;
           #caivases#1 = "S";
           |LEE 110#caivases.=;
           |SI FSalida = 0;
              nCalc = #caivases#3 - #caivases#4;
              |SI nCalc = #caivasop#2;
                 #caivases#3 = nCalc;
                 |GRABA 020#caivases.a;
              |FINSI;
              |LIBERA #caivases;
           |FINSI;
           |CIERRA #caivases;
        |SINO;
           |ABRE #calibros;
           #calibros#0 = LibroAnt;
           |LEE 101#calibros.=;
           |SI FSalida = 0;
              nCalc = #calibros#4 - #calibros#3;
              |SI nCalc = #caivasop#2;
                 #calibros#4 = #calibros#4 - #calibros#3;
                 |GRABA 020#calibros.a;
              |FINSI;
              |LIBERA #calibros;
           |FINSI;
           |CIERRA #calibros;
        |FINSI;

        |ACABA;
     |FINSI;

     nOk = 0;
     |SI #caivaasi#51 = "S";  nOk = 1; |FINSI;

     |SI nOk = 0;
         |ACABA;
     |FINSI;

     eaSiiPro = "caivasop";
     enSiiLib = #caivasop#0;
     eaSiiDoc = #caivasop#27 + (("000000" + #caivasop#2) % -106);
     enSiiDoc = #caivasop#2;
     eaSiiSer = #caivasop#27;
     enSiiLin = 0;
     eaSiiCpt = #caivasop#6;
     eaSiiCta = #caivasop#4;
     eaSiiDig = #caivasop#5;
     eaSiiCIF = #caivasop#51;  |QBF eaSiiCIF;
     eaSiiDes = #caivasop#7;
     eaSiiEje = #caivasop#3 % -104;
     enSiiPer = #caivasop#44;
     enSiiUlt = 1;
     enSiiRet = 0;

     |SUB_EJECUTA "casiim01";
|FPRC;

|PROCESO InfPDF;
     aAlfa = "hogarz02;CC";
     |SUB_EJECUTA aAlfa;
|FPRC;

|PROCESO PaqPDF;
     aAlfa = "hogarz03;CC";
     |SUB_EJECUTA aAlfa;
|FPRC;

|PROCESO T20; |TIPO 20;
     |SI swModulo ! 3824;
         |ACABA;
     |FINSI;

     |SI FSalida = "OPCION";
          FSalida = "Utilidades PDF";
          |ACABA;
     |FINSI;

     |MENU aUtil;
     aUtil2 = FSalida;

     |SI aUtil2 = "1";  |HAZ InfPDF;  |FINSI;
     |SI aUtil2 = "2";  |HAZ PaqPDF;  |FINSI;
|FPRC;

|PROCESO T90;  |TIPO 90;
     |SI nBtnVPDF ! -1;
         |FIN_CONTROL_WINDOWS nBtnRPDF;
         |FIN_CONTROL_WINDOWS nBtnVPDF;
         |ACABA;
     |FINSI;
|FPRC;
