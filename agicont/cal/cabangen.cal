|FICHEROS;
   cabangen #0;
   catmpefe #1;

   capagcab #2;
   capaglin #3;

   camantal #4;

   camingca #5;
   camingli #6;

   cacuenta #7;
|FIN;

|VARIABLES;
   Cuenta = "";  Nombre = "";

   aLong = "";   nLong = 0;

   BancoAnt = ""; VtoAnt = "";
   &ContLin = 0;  NumLineas = 68;
   Primero = 0;  ComodNum = 0;

   &TotBanco = 0;  &TotVto   = 0;
   &SwLinBan = 0;  &SwCabVto = 0;  &SwCabDet = 0; &SwLinDet = 0;
   &SwTotVto = 0;  &SwTotBan = 0;  &SwBlanco = 0;
   &SubVto   = ""; &SubBan   = ""; &VtoCont = ""; &BanCont = "";
   &FechaAct = @;  &NumPag = 1;

   &Vto1 = @;  &Vto2 = @;  &Vto3 = @;  &Vto4 = @;
   &TVto1 = 0; &TVto2 = 0; &TVto3 = 0; &TVto4 = 0; &TVto5 = 0;

   DContEm = ""; HContEm = "";
   DContVt = ""; HContVt = "";
   DEmis   = ""; HEmis   = "";
|FIN;

|INCLUYE acceso_i;

|PROCESO TomaNombre;
   |ABRE #cacuenta;
   |QBF Cuenta;
   aLong = Cuenta % 0; nLong = aLong;
   |SI nLong = #48#14; #cacuenta#1 = 1; |FINSI;
   |SI nLong = #48#15; #cacuenta#1 = 2; |FINSI;
   |SI nLong = #48#16; #cacuenta#1 = 3; |FINSI;
   |SI nLong = #48#17; #cacuenta#1 = 4; |FINSI;
   |SI nLong = #48#18; #cacuenta#1 = 5; |FINSI;
   |SI nLong = #48#19; #cacuenta#1 = 6; |FINSI;
   #cacuenta#0 = Cuenta;
   |LEE 010#cacuenta.=;
   |SI FSalida = 0;
      Nombre = #cacuenta#2;
   |SINO;
      Nombre = "";
   |FINSI;
   |CIERRA #cacuenta;
|FPRC;

|PROCESO Pagares;
   |DDEFECTO #catmpefe;
   #catmpefe#0  = "P";  #catmpefe#1 = "Pagare";
   #catmpefe#2  = #capagcab#0;                      || Codigo Pagare
   #catmpefe#3  = 0;                                || Linea Pagare
   #catmpefe#4  = #capagcab#1;                      || Cuenta Proveedor
   Cuenta = #catmpefe#4; |HAZ TomaNombre;
   #catmpefe#5  = Nombre;                           || Nombre Proveedor
   #catmpefe#6  = #capagcab#3;                      || Cuenta Banco
   Cuenta = #catmpefe#6; |HAZ TomaNombre;
   #catmpefe#7  = Nombre;                           || Nombre Banco
   #catmpefe#8  = #capagcab#5;                      || Fecha Emision
   #catmpefe#9  = #capagcab#6;                      || Fecha Vencimiento
   #catmpefe#10 = #capagcab#9;                      || Importe Pagare
   |GRABA 020#catmpefe.n;
|FPRC;

|DEFBUCLE Pagares;
#capagcab#1;
3,5,6,1,7,8,12;
000001,#0#6 ,#0#3,#0#4,#0#5,DContEm,DContVt,DEmis;
999999,#0#10,#0#7,#0#8,#0#9,HContEm,DContVt,HEmis;
;
NULL,Pagares;
|FIN;

|PROCESO Talones;
   |DDEFECTO #catmpefe;
   #catmpefe#0  = "T";  #catmpefe#1 = "Talon";
   #catmpefe#2  = #camantal#0;                      || Codigo Talon
   #catmpefe#3  = 0;                                || Linea Talon
   #catmpefe#4  = #camantal#3;                      || Cuenta Proveedor
   Cuenta = #catmpefe#4; |HAZ TomaNombre;
   #catmpefe#5  = Nombre;                           || Nombre Proveedor
   #catmpefe#6  = #camantal#1;                      || Cuenta Banco
   Cuenta = #catmpefe#6; |HAZ TomaNombre;
   #catmpefe#7  = Nombre;                           || Nombre Banco
   #catmpefe#8  = #capagcab#5;                      || Fecha Emision
   #catmpefe#9  = #capagcab#6;                      || Fecha Vencimiento
   #catmpefe#10 = #capagcab#9;                      || Importe Talon
   |GRABA 020#catmpefe.n;
|FPRC;

|DEFBUCLE Talones;
#camantal#1;
1,5,6,3,7,8,12;
000001,#0#6 ,#0#3,#0#4,#0#5,DContEm,DContVt,DEmis;
999999,#0#10,#0#7,#0#8,#0#9,HContEm,HContVt,HEmis;
;
NULL,Talones;
|FIN;

|PROCESO LineasConf;

   |DDEFECTO #catmpefe;
   #catmpefe#0  = "C";  #catmpefe#1 = "Confirme";
   #catmpefe#2  = #camingli#0;                      || Codigo Confirme
   #catmpefe#3  = #camingli#1;                      || Linea Confirme
   #catmpefe#4  = #camingli#9;                      || Cuenta Proveedor
   #catmpefe#5  = #camingli#13;                     || Nombre Proveedor
   #catmpefe#6  = #camingca#4;                      || Cuenta Banco
   Cuenta = #catmpefe#6; |HAZ TomaNombre;
   #catmpefe#7  = Nombre;                           || Nombre Banco
   #catmpefe#8  = #camingli#6;                      || Fecha Emision
   #catmpefe#9  = #camingli#7;                      || Fecha Vencimiento
   #catmpefe#10 = #camingli#8;                      || Importe Linea Confirme
   |GRABA 020#catmpefe.n;
|FPRC;

|DEFBUCLE LineasConf;
#camingli#1;
9,6,7;
#camingca#0,001,#0#5,#0#3,#0#4;
#camingca#0,999,#0#9,#0#7,#0#8;
;
NULL,LineasConf;
|FIN;

|PROCESO Confirmes;
   |BUCLE LineasConf;
|FPRC;

|DEFBUCLE Confirmes;
#camingca#1;
4,12,11,10;
000001,#0#6 ,DContEm,DContVt,DEmis;
999999,#0#10,HContEm,HContVt,HEmis;
;
NULL,Confirmes;
|FIN;

|PROCESO List1;
   |SI BancoAnt ! #catmpefe#6;
      |SI Primero = 0;
         SwLinBan = 0; SwCabVto = 0; SwCabDet = 0; SwLinDet = 0; SwTotVto = 1;
         SwTotBan = 1; |PRINT; ContLin = ContLin + 4;
         TotBanco = 0; TotVto = 0;
      |SINO;
         Primero = 0;
      |FINSI;
      ComodNum = ContLin + 10;
      |SI ComodNum ] NumLineas;
         |SI TotBanco ! 0;
            SubVto = "SUB"; SubBan = "SUB"; VtoCont = "(CONTINUACION)"; BanCont = "(CONTINUACION)";
            SwLinBan = 0; SwCabVto = 0; SwCabDet = 0; SwLinDet = 0; SwTotVto = 1;
            SwTotBan = 1; |PRINT; ContLin = ContLin + 4;
         |FINSI;
         SubVto = ""; SubBan = ""; VtoCont = ""; BanCont = "";
         |HAZ VeteFinal; ContLin = 5;
      |FINSI;
      SwLinBan = 1; SwCabVto = 1; SwCabDet = 1; SwLinDet = 0; SwTotVto = 0;
      SwTotBan = 0; |PRINT; ContLin = ContLin + 6;
      BancoAnt = #catmpefe#6;
      VtoAnt   = #catmpefe#9;
   |SINO;
      |SI VtoAnt ! #catmpefe#9;
         SubVto = ""; SubBan = "SUB"; VtoCont = ""; BanCont = "";
         SwLinBan = 0; SwCabVto = 0; SwCabDet = 0; SwLinDet = 0; SwTotVto = 1;
         SwTotBan = 0; |PRINT; ContLin = ContLin + 2; TotVto = 0;
         SwLinBan = 0; SwCabVto = 1; SwCabDet = 1; SwLinDet = 0; SwTotVto = 0;
         SwTotBan = 0; |PRINT; ContLin = ContLin + 4;
         SubVto = ""; SubBan = ""; VtoCont = ""; BanCont = "";
         VtoAnt   = #catmpefe#9;
      |FINSI;
   |FINSI;

   ComodNum = ContLin + 4;
   |SI ComodNum ] NumLineas;
      SubVto = "SUB"; SubBan = "SUB"; VtoCont = "(CONTINUACION)"; BanCont = "(CONTINUACION)";
      SwLinBan = 0; SwCabVto = 0; SwCabDet = 0; SwLinDet = 0; SwTotVto = 1;
      SwTotBan = 1; |PRINT; ContLin = ContLin + 4;
      SubVto = ""; SubBan = "";
      |HAZ VeteFinal; ContLin = 5;
      SwLinBan = 1; SwCabVto = 1; SwCabDet = 1; SwLinDet = 0; SwTotVto = 0;
      SwTotBan = 0; |PRINT; ContLin = ContLin + 6;
      VtoCont = ""; BanCont = "";
   |FINSI;

   SwLinBan = 0; SwCabVto = 0; SwCabDet = 0; SwLinDet = 1; SwTotVto = 0;
   SwTotBan = 0; |PRINT; ContLin = ContLin + 1;
   TotBanco = TotBanco + #catmpefe#10;
   TotVto = TotVto + #catmpefe#10;

|FPRC;

|DEFBUCLE Registros1;
#catmpefe#1;
;
INICIO;
FINAL;
;
NULL,List1;
|FIN;

|PROCESO Listado1;
   |INFOR "cabange1";
   |SI FSalida < 0; |ACABA; |FINSI;
   |BUCLE Registros1;
   SwLinBan = 0; SwCabVto = 0; SwCabDet = 0; SwLinDet = 0; SwTotVto = 1;
   SwTotBan = 1; |PRINT;
   |PIEINF;
   |FININF;
|FPRC;

|PROCESO List2;
   |SI BancoAnt ! #catmpefe#6;
      |SI Primero = 0;
         |PRINT;
         TVto1 = 0; TVto2 = 0; TVto3 = 0; TVto4 = 0; TVto5 = 0;
      |SINO;
         Primero = 0;
      |FINSI;
      BancoAnt = #catmpefe#6;
   |FINSI;

   |SI #catmpefe#9 [ Vto1; TVto1 = TVto1 + #catmpefe#10; |FINSI;
   |SI #catmpefe#9 > Vto1; |Y #catmpefe#9 [ Vto2;
      TVto2 = TVto2 + #catmpefe#10;
   |FINSI;
   |SI #catmpefe#9 > Vto2; |Y #catmpefe#9 [ Vto3;
      TVto3 = TVto3 + #catmpefe#10;
   |FINSI;
   |SI #catmpefe#9 > Vto3; |Y #catmpefe#9 [ Vto4;
      TVto4 = TVto4 + #catmpefe#10;
   |FINSI;
   |SI #catmpefe#9 > Vto4; TVto5 = TVto5 + #catmpefe#10; |FINSI;

|FPRC;

|DEFBUCLE List2;
#catmpefe#1;
;
INICIO;
FINAL;
;
NULL,List2;
|FIN;

|PROCESO Listado2;
   |INFOR "cabange2";
   |SI FSalida < 0; |ACABA; |FINSI;
   Vto1 = #cabangen#15; Vto2 = #cabangen#16;
   Vto3 = #cabangen#17; Vto4 = #cabangen#18;

   TVto1 = 0; TVto2 = 0; TVto3 = 0; TVto4 = 0; TVto5 = 0;
   |BUCLE List2;
   |PIEINF;
   |FININF;
|FPRC;

|PROCESO List3;
   |SI VtoAnt ! #catmpefe#9;
      |SI Primero = 0;
         SwCabVto = 0; SwCabDet = 0; SwLinDet = 0; SwTotVto = 1;
         |PRINT; ContLin = ContLin + 2;
         TotVto = 0;
      |SINO;
         Primero = 0;
      |FINSI;
      ComodNum = ContLin + 6;
      |SI ComodNum ] NumLineas;
         |SI TotBanco ! 0;
            SubVto = "SUB"; VtoCont = "(CONTINUACION)";
            SwCabVto = 0; SwCabDet = 0; SwLinDet = 0; SwTotVto = 1;
            |PRINT; ContLin = ContLin + 2;
         |FINSI;
         SubVto = "";
         |HAZ VeteFinal; ContLin = 5;
      |FINSI;
      SwCabVto = 1; SwCabDet = 1; SwLinDet = 0; SwTotVto = 0;
      |PRINT; ContLin = ContLin + 4;
      VtoCont = "";
      VtoAnt   = #catmpefe#9;
   |FINSI;

   ComodNum = ContLin + 2;
   |SI ComodNum ] NumLineas;
      SubVto = "SUB"; VtoCont = "(CONTINUACION)";
      SwCabVto = 0; SwCabDet = 0; SwLinDet = 0; SwTotVto = 1;
      |PRINT; ContLin = ContLin + 2;
      SubVto = "";
      |HAZ VeteFinal; ContLin = 5;
      SwCabVto = 1; SwCabDet = 1; SwLinDet = 0; SwTotVto = 0;
      |PRINT; ContLin = ContLin + 4;
      VtoCont = "";
   |FINSI;

   SwCabVto = 0; SwCabDet = 0; SwLinDet = 1; SwTotVto = 0;
   |PRINT; ContLin = ContLin + 1;
   TotVto = TotVto + #catmpefe#10;

|FPRC;

|DEFBUCLE Registros3;
#catmpefe#2;
;
INICIO;
FINAL;
;
NULL,List3;
|FIN;

|PROCESO Listado3;
   |INFOR "cabange3";
   |SI FSalida < 0; |ACABA; |FINSI;
   |BUCLE Registros3;
   SwCabVto = 0; SwCabDet = 0; SwLinDet = 0; SwTotVto = 1;
   |PRINT;
   |PIEINF;
   |FININF;
|FPRC;

|PROCESO VeteFinal;
   |PARA ContLin; |SI ContLin < NumLineas; |HACIENDO ContLin = ContLin + 1;
      SwLinBan = 0; SwCabVto = 0; SwCabDet = 0; SwLinDet = 0; SwTotVto = 0;
      SwTotBan = 0; SwBlanco = 1;
      |PRINT;
      SwBlanco = 0;
   |SIGUE;
   |PIEINF;
   NumPag = NumPag + 1;
|FPRC;

|PROCESO TipoList;
   |SI #cabangen#14 = 2;
      |C_M #cabangen#15,1,"S"; |C_M #cabangen#16,1,"S";
      |C_M #cabangen#17,1,"S"; |C_M #cabangen#18,1,"S";
      #cabangen#15 = ~;            |PINTA #cabangen#15;
      #cabangen#16 = "31.12.2999"; |PINTA #cabangen#16;
      #cabangen#17 = "31.12.2999"; |PINTA #cabangen#17;
      #cabangen#18 = "31.12.2999"; |PINTA #cabangen#18;
   |SINO;
      |C_M #cabangen#15,1,"N"; |C_M #cabangen#16,1,"N";
      |C_M #cabangen#17,1,"N"; |C_M #cabangen#18,1,"N";
      #cabangen#15 = "          "; |PINTA #cabangen#15;
      #cabangen#16 = "          "; |PINTA #cabangen#16;
      #cabangen#17 = "          "; |PINTA #cabangen#17;
      #cabangen#18 = "          "; |PINTA #cabangen#18;
   |FINSI;
|FPRC;

|PROGRAMA;
   |CLS;
   |CABEZA "Informe bancos general";
   |ABRE #cabangen;
   |LEE 010#cabangen.p;
   |SI FSalida ! 0;
      |DDEFECTO #cabangen;
      #cabangen#15 = "          ";
      #cabangen#16 = "          ";
      #cabangen#17 = "          ";
      #cabangen#18 = "          ";
      |GRABA 020#cabangen.n;
      |LEE 010#cabangen.=;
   |FINSI;
   |PINPA #0#0; |PINDA #0#0;
   |ENDATOS #1#0;
   |SI FSalida = 0;
      |GRABA 020#cabangen.a;
      |IMPRE 0;
      |SI FSalida < 0; |ACABA; |FINSI;

      DContEm = #cabangen#11; HContEm = #cabangen#11;
      |SI #cabangen#11 = "A"; DContEm = "N"; HContEm = "S"; |FINSI;
      DContVt = #cabangen#12; HContVt = #cabangen#12;
      |SI #cabangen#12 = "A"; DContVt = "N"; HContVt = "S"; |FINSI;
      DEmis = #cabangen#13;   HEmis = #cabangen#13;
      |SI #cabangen#13 = "A"; DEmis = "N";   HEmis = "S"; |FINSI;

      |DELINDEX #catmpefe;
      |ABRE #catmpefe;
      |SI #cabangen#0 = "S"; |BUCLE Pagares;   |FINSI;
      |SI #cabangen#1 = "S"; |BUCLE Talones;   |FINSI;
      |SI #cabangen#2 = "S"; |BUCLE Confirmes; |FINSI;
      |CIERRA #catmpefe;

      NumLineas = #cabangen#20;
      Primero = 1; ContLin = 5;
      FechaAct = ~; NumPag = 1;
      |SI #cabangen#14 = "1"; |HAZ Listado1; |FINSI;
      |SI #cabangen#14 = "2"; |HAZ Listado2; |FINSI;
      |SI #cabangen#14 = "3"; |HAZ Listado3; |FINSI;

      |FINIMP;
   |FINSI;
|FPRO;
