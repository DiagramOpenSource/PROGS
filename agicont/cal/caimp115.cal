|FICHEROS;
  caimp115 #0;
  caman115 #2;

  cama115@ #6;
|FIN;

|VARIABLES;
  &entrada = 1;
  &r_emp = 0;
  &r_per = 0;
  nom1   = "";
  aAlfa1  = "";
  aAlfa2  = "";
  nPara1  = 0;
  nEstado = 0;
  fich_copi = "";
  nEjerAnterior = 0;
  nEjerActual   = 0;
  any1        = 0;
  any2        = 0;
  aQueQuiero  = "";
  aNumCampos  = "";
  nNumCampos  = 0;
|FIN;

|INCLUYE acceso_i;
|INCLUYE varnue_i;

|| ********************************************************************
|PROCESO ParaEmpezar; |TIPO 8;
 |HAZ inicio;
 |HAZ eVarEmpresa;
 nEjerActual = enEjercicio;
 aAlfa1 = nEjerActual;
 aAlfa1 = aAlfa1 % -102;
 any1   = aAlfa1;
|FPRC;

|PROCESO deentrada;  |TIPO 7;
 #0#5 = empre;     |PINTA #0#5;
 #0#6 = ejerc - 1; |SI #0#6 < 0; #0#6 = 9; |FINSI;
 |PINTA #0#6;
 #0#7 = #30#1; |PINTA #0#7;
|FPRC;

|PROCESO esbuena;
  |SI FSalida = 9;  |ACABA;  |FINSI;
  |ABRE #30;
  #30#2 = #0#5;
  #30#3 = 0;
  |LEE 011#30];
  |SI FSalida ! 0;  |O #30#2 ! #0#5;
      |MENAV "     NO Existe la empresa Contable";
      |ERROR;
  |FINSI;
  |CIERRA #30;
|FPRC;

|PROCESO consulta;
  |SI FSalida ! 9;  |ACABA;  |FINSI;
  r_emp = 0;
  |SUB_EJECUTA "caconemp.run";
  |ERROR;
  #0#5 = r_emp;
  #0#6 = r_per;
  |PTEC 802;
  |PTEC 802;
|FPRC;

|PROCESO esbuena1;
  |SI FSalida = 2; |ACABA; |FINSI;
  |SI #0#5 = empre; |Y #0#6 = ejerc;
      |MENAV "    ERROR.! NO PUEDE IMPORTAR A LA EMPRESA ACTUAL ";
      |ERROR;
      |ACABA;
  |FINSI;
  |ABRE #30;
  #30#2 = #0#5;
  #30#3 = #0#6;
  |LEE 011#30=;
  |SI FSalida ! 0;
      |MENAV "     NO Existe Periodo en la empresa Contable";
      |CIERRA #30;
      |ERROR;
      |ACABA;
  |FINSI;
  |SI #30#26 < 80;
      nEjerAnterior = #30#26 + 2000;
  |SINO;
      nEjerAnterior = #30#26 + 1900;
  |FINSI;

  #0#7 = #30#1; |PINTA #0#7;
  nom1 = #30#1;
  any2 = #30#26;

  #30#2 = empre;
  #30#3 = ejerc;
  |LEE 011#30=;
  |CIERRA #30;
|FPRC;

|| *********************************************************************
|| ************** Proceso PARA Importar Arrendadores
|| *********************************************************************
|PROCESO pasar_a; || Crea las Cabeceras
 #6#0   = enEmpresa;
 #6#1   = #2#1;
 #6#134 = any1;
 |LEE 101#6=;
 nEstado = FSalida;
 |SI nEstado = 0; |Y #0#9 = "N";
     |LIBERA #6;
     |ACABA;
 |FINSI;

 |DDEFECTO #6;
 |PARA nPara1 = 0; |SI nPara1 [ nNumCampos; |HACIENDO nPara1 = nPara1 + 1;
       #6nPara1 = #2nPara1;
 |SIGUE;
 #6#0   = enEmpresa;
 #6#1   = #2#1;
 #6#134 = any1;
 |SI #0#2 = "N";
     |PDEFECTO #6, 23, 127;
 |FINSI;
 |SI nEstado = 0; |GRABA 020#6a; |FINSI;
 |SI nEstado ! 0; |GRABA 020#6b; |FINSI;
 |LIBERA #6;
|FPRC;

|DEFBUCLE pasar_a;
 #2#1;
 ;
 #0#5, #0#0, any2;
 #0#5, #0#1, any2;
 e;
 NULL, pasar_a;
|FIN;

|| *********************************************************************
|PROCESO pase; |TIPO 3;

  |SI #0#8 = "NO"; |ACABA; |FINSI;

  |DDEF aAlfa1; |QBF aAlfa1;
  aAlfa2 = aAlfa1 + "caman115.mas";
  |CARGA_DEF aAlfa2, cama115@;
  |SI FSalida ! 0;
      |MENAV "    Error cargando def caman115";
      |ACABA;
  |FINSI;

|| .............. Variables
  |DFICE fich_copi;         || Directorio Actual

  |DBASE dir_fich;
  dir_fich = dir_fich + "fich/";
  aAlfa1 = #0#5; aAlfa1 = ("000000" + aAlfa1) % -105;
  aAlfa2 = #0#6; aAlfa2 =      ("0" + aAlfa2) % -101;
  fich_alta = dir_fich + aAlfa1 + aAlfa2 + "/";    || Directorio Empresa Origen

  |PATH_DAT #2 fich_alta;    || Lee las fichas de la Empresa Origen

  aQueQuiero = "|NC";
  aNumCampos = #2#aQueQuiero#;
  nNumCampos = aNumCampos;
  nNumCampos = nNumCampos - 1;

  |ABRE #6;
  |BUCLE pasar_a;
  |CIERRA #6;

  |DESCARGA_DEF #cama115@;
|FPRC;
|| **********************************************************************
