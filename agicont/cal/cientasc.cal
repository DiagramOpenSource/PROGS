|FICHEROS;
   cientasc #0;
   cientasl #1;
   cacuenta #2;
   cisalant #3;
   cientcon #4,MANTE;
|FIN;

|VARIABLES;
  &entrada = 1;
  sw       = 0;
  cta      = "            ";
  ctanum   = 0;
  ctaalf   = " ";
  longalf  = " ";
  longnum  = " ";
  nivel    = 0;
  nada = "";
  modo = 0;
  ini = 0;
  fecalf = "";
  mes = "";
  mesfec = 0;
  mesac = 0;
  a = 0;
  b = 0;
  c = 0;
  signo = "";
  elimporte = 0;
  &total = 0;
  &sumal = 0;
  &difer = 0;
  &fecapu = @;
  &sigapu = "";
  &descrira = "";
  &fecant = @;
  nume1 = 0;
  sw_movim = 0;
|FIN;

|INCLUYE acces4_i;
|INCLUYE punto3_i;

|PROCESO alinicio;  |TIPO 8;
  |HAZ inicio4;
  fecapu = ~;
  nume1 = fecapu;
  nume1 = nume1 - 1;
  fecant = nume1;
|FPRC;

|PROCESO elnivel;  |TIPO 6;
  p_alfa1 = #0Campo;
  |HAZ punto3;
  |SI p_error ! 0;  |ERROR;  |ACABA;  |FINSI;
  cta = #0#0;
  |QBF cta;
  nada = cta % A101;
  |SI nada ! "0";
      ctanum = cta;
      ctaalf = ctanum;
      longnum = ctaalf % A0;
  |SINO;
      ctanum = 1;
      ctaalf = cta;
      longnum = ctaalf % A0;
  |FINSI;
  longalf = cta    % A0;
  |SI longnum ! longalf;|O ctanum = 0;
      |MENAV "     CUENTA ERRONEA !!";
      |PINTA #0#0;
      |ERROR;
      |ACABA;
  |FINSI;
  nivel= 0;
  sw = 0;
  |SI longnum = dig4; sw = 1; nivel = 1; |FINSI;
  |SI longnum = dig5; sw = 1; nivel = 2; |FINSI;
  |SI longnum = dig6; sw = 1; nivel = 3; |FINSI;
  |SI longnum = dig7; sw = 1; nivel = 4; |FINSI;
  |SI longnum = dig8; sw = 1; nivel = 5; |FINSI;
  |SI longnum = dig9; sw = 1; nivel = 6; |FINSI;
  |SI sw = 0;
      |MENAV "     LONGITUD DE CUENTA FUERA DE NIVEL !";
      |PINTA #0#0;
      |ERROR;
      |ACABA;
  |FINSI;
  #0#1 = nivel;
|FPRC;

|PROCESO elnivel0;  |TIPO 0;
  |ABRET #2;
  #2#0 = #0#0;
  #2#1 = #0#1;
  |LEE 101#2=;
  |CIERRAT #2;
  |SI #2#3 ! "S";
      |MENAV "     LA CUENTA NO ES DE PASE DIRECTO";
      |ERROR;
      |ACABA;
  |FINSI;
|FPRC;

|PROCESO lossaldos;
  #0#7 = #0#4 + #0#5 - #0#6;
  |PINTA #0#7;
|FPRC;

|RUTINA a_actualiza;                || rutina 'a_cuentas'
  ini = dig1;
  fecalf = #1#3;
  mes = fecalf % 402;
  mesfec = mes;
  |SI ini = 1;
      mesac = mesfec;
  |SINO;
      mesac = (mesfec - ini) + 1;
      |SI mesac [ 0;
          mesac = (13 - ini) + mesfec;
      |FINSI;
  |FINSI;
  a = (mesac * 3) + 9;
  b = a + 1;
  c = a + 2;
  |SI signo = "D";
      #2a = #2a +. elimporte;
      #2#51 = #2#51 +. elimporte;
  |SINO;
      #2b = #2b +. elimporte;
      #2#52 = #2#52 +. elimporte;
  |FINSI;
  #2c = #2a - #2b;
  #2#53 = #2#51 - #2#52;
|FRUT;

|RUTINA a_contrapa;
  signo = #1#6;
  |ABRET #2;
  #2#0 = #0#0;
  #2#1 = #0#1;                    || actualizacion cuentas  CABECERA;
  |LEE 101#2=;
  |SI FSalida = 0;
      elimporte = #1#7;
      |HAZ a_actualiza;           || saldos del mes correspondiente
      |SI #2#58 < #1#3;  #2#58 = #1#3;  |FINSI;     || fecha ultimo apunte
      |GRABA 020#2a;
  |FINSI;
  |LIBERA #2;
  |CIERRAT #2;
|FRUT;

|RUTINA a_cuentas;                || calculo 'refresca'
  signo = #1#6;
  |ABRET #2;
  #2#0 = #0#0;
  #2#1 = #0#1;                    || actualizacion cuentas  CABECERA;
  |LEE 101#2=;
  |SI FSalida = 0;
      elimporte = #1#7;
      |HAZ a_actualiza;           || saldos del mes correspondiente
      |SI #2#58 < #1#3;  #2#58 = #1#3;  |FINSI;     || fecha ultimo apunte
      |GRABA 020#2a;
  |FINSI;
  |LIBERA #2;
  |SI signo = "D";  signo = "H";  |SINO;  signo = "D";  |FINSI;
  #2#0 = #1#8;
  #2#1 = #1#9;
  |LEE 101#2=;
  |SI FSalida = 0;
      elimporte = #1#7;
      |HAZ a_actualiza;
      |SI #2#58 < #1#3;  #2#58 = #1#3;  |FINSI;
      |GRABA 020#2a;
  |FINSI;
  |LIBERA #2;
  |CIERRAT #2;
|FRUT;

|RUTINA pinta_cta_t;
  |ABRET #2;
  #2#0 = #1#8;
  #2#1 = #1#9;
  |LEE 001#2=;
  |SI FSalida ! 0;  |DDEFECTO #2;  |FINSI;
  |CIERRAT #2;
|FRUT;

|PROCESO pinta_cta;
  |SI #1#8 ! "            ";
      |HAZ pinta_cta_t;
  |SINO;
      |DDEFECTO #2;
  |FINSI;
  |ATRI I;
  |PINTA 2203, #2#2;
  |PINTA 2240, #2#51;
  |PINTA 2254, #2#52;
  |PINTA 2268, #2#53;
  |ATRI 0;
|FPRC;

|PROCESO refresca;  |TIPO 2;
  |SI #1#6 = "D";
      #0#5 = #0#5 +. #1#7;
      |PINTA #0#5;
  |SINO;
      #0#6 = #0#6 +. #1#7;
      |PINTA #0#6;
  |FINSI;
  |HAZ lossaldos;
  |SI #1#8 ! "            ";
      |HAZ a_cuentas;
      |HAZ pinta_cta;
  |SINO;
      |HAZ a_contrapa;
  |FINSI;
|FPRC;

|PROCESO defectos;
  modo = ((FEntrada / 100) ! 0);
  |SI modo ! 1;  |ACABA;  |FINSI;
  |ABRE #3;
  #3#0 = #0#0;
  #3#1 = #0#1;
  |LEE 101#3=;
  #1#8 = #3#5;
  #1#9 = #3#6;
  #1#3 = fecapu;
  |PINTA #1#3;
  |PINTA #1#8;
  |CIERRA #3;
|FPRC;

|PROCESO tipo33; |TIPO 3;
  sw_movim = 1;
|FPRC;

|PROCESO salir; |TIPO 9;
 |SI sw_movim = 0; |ACABA; |FINSI;
 |AVISO;
 |CONFI "2400SDesea actualizar las Introduccion Realizada S/N : ";
 |SI FSalida ! 0; |ACABA; |FINSI;
 |CORRE "ciactasc";
|FPRC;
