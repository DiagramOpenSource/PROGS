|FICHEROS;
  caltacta #0;
  cacuenta #1;    || cuentas

  cactrlal #11;   || control altas en otras empresas.

  casieaca #2;
  casieali #3;
  casieac@ #12;
  casieal@ #13;

  canempre #30;   || Empresas
|FIN;

|VARIABLES;

 men2     = "LONGITUD DE CUENTA FUERA DE NIVEL !. NO PROCEDE ALTA ";
 mascara  = "";
 num      = 0;
 sw       = 0;
 mes      = "                ";
 mesdes   = "";
 meshas   = "";

 cta      = "            ";
 longnum  = "";
 nivel    = 0;

 &raf_cue  = "";
 &raf_dig  = 0;
 &raf_des  = "";
 &raf_pase = "";
 &raf_blo  = 0;
 &raf_tipo = "";
 &raf_agru = 0;
 &raf_epi  = 0;
 &raf_par  = 0;

 &raf_empresa = 0;
 &raf_periodo = 0;
 &enOpAlta    = 0;
 &enPrAlta    = 0;
 &eaPrAlta    = "";

 xx       = 0;
 operac   = 0;
 dir_fich = "";
 dirfich0 = "";
 aAlfa    = "";
 aAlfa1   = "";
 aAlfa2   = "";
 nPara1   = 0;

 nEmpresa = 0;
 nPeriodo = 0;
 nDEmpresa = 0;
 nHEmpresa = 0;
 nDPeriodo = 0;
 nHPeriodo = 0;
 nSwDescarga = 0;

 nEstado = 0;
|FIN;


|| *********************************************************************
|| *********************************************************************
|| *********************************************************************
|PROCESO CopiaLin;
 |PARA nPara1 = 0; |SI nPara1 [ 61; |HACIENDO nPara1 = nPara1 + 1;
       #13nPara1 = #3nPara1;
 |SIGUE;
 |GRABA 020#13n;
|FPRC;

|DEFBUCLE CopiaLin;
 #3#1;
 ;
 #2#0, 01;
 #2#0, 99;
 e;
 NULL, CopiaLin;
|FIN;

|PROCESO BorraLin;
 |BORRA 020#13a;
|FPRC;

|DEFBUCLE BorraLin;
 #13#1002;
 ;
 #12#0, 01;
 #12#0, 99;
 e;
 NULL, BorraLin;
|FIN;

|PROCESO CopiaPred;

 |PATH_DAT #12  dirfich0;
 |PATH_DAT #13  dirfich0;

 |ABRE #12;
 #12#0 = #2#0;
 |LEE 101#12=;
 nEstado = FSalida;

 |SI enOpAlta = 1;
     |PARA nPara1 = 0; |SI nPara1 [ 16; |HACIENDO nPara1 = nPara1 + 1;
           #12nPara1 = #2nPara1;
     |SIGUE;
     |SI nEstado = 0; |GRABA 020#12a; |FINSI;
     |SI nEstado ! 0; |GRABA 020#12n; |FINSI;
 |SINO;
     |SI nEstado = 0; |BORRA 020#12a; |FINSI;
 |FINSI;

 |LIBERA #12;
 |CIERRA #12;

 |BUCLE BorraLin;

 |SI enOpAlta = 1;
     |ABRE #13;
     |BUCLE CopiaLin;
     |CIERRA #13;
 |FINSI;
|FPRC;

|| ////////////////// Copia de las Cuentas
|PROCESO CopiaCta;
  nivel= 0; sw = 0;
  |SI longnum = #30#14; sw = 1; nivel = 1; |FINSI;
  |SI longnum = #30#15; sw = 1; nivel = 2; |FINSI;
  |SI longnum = #30#16; sw = 1; nivel = 3; |FINSI;
  |SI longnum = #30#17; sw = 1; nivel = 4; |FINSI;
  |SI longnum = #30#18; sw = 1; nivel = 5; |FINSI;
  |SI longnum = #30#19; sw = 1; nivel = 6; |FINSI;

  |SI sw = 0;
      |AVISO;
      |ATRI R;
      |PINTA 1410, men2;
      |ATRI 0;
      |PAUSA;
  |SINO;

      num = #30#11; |HAZ pinta; mesdes = mes;
      num = #30#12; |HAZ pinta; meshas = mes;

      |PATH_DAT #1  dirfich0;
      |ABRE #1;
      #1#0 = raf_cue;
      #1#1 = nivel;
      |LEE 000#1=;
      |SI FSalida ! 0;
          |ATRI R; |PINTA 1410, "ALTA CORRECTA ... "; |ATRI 0;
          |PDEFECTO #1,9,60;
          #1#0 = raf_cue;
          #1#1 = nivel;
          #1#2 = raf_des;
          #1#3 = raf_pase;
          #1#4 = raf_blo;
          #1#5 = raf_tipo;
          #1#6 = raf_agru;
          #1#7 = raf_epi;
          #1#8 = raf_par;
  || la 3a clave esta formada por la cta + zzzz y el campo fantasma del nivel;
          mascara = #1#0;
          |QBF mascara;
          mascara = mascara +"zzzzzzzzzzzz";
          mascara = mascara % 112;
          #1#54    = mascara;
          #1#55    = nivel;
          #1#56    = mesdes;
          #1#57    = meshas;
          |GRABA 020#1n;
          |LIBERA #1;
      |FINSI;
      |CIERRA #1;
 |FINSI;

|FPRC;

|PROCESO HazTodo;

  |SI #30#2 = raf_empresa; |Y #30#3 = raf_periodo;
      |ACABA;                                       ||No en la Misma Empresa
  |FINSI;

  nEmpresa = #30#2;
  nPeriodo = #30#3;
  |HAZ CalDirec;

  #0#2 = nEmpresa; |PINTA #0#2;
  #0#3 = #30#1;    |PINTA #0#3;

  |SI enOpAlta = 0;
      |HAZ CopiaCta;
  |SINO;
      |HAZ CopiaPred;
  |FINSI;

|FPRC;


|DEFBUCLE LasEmpresas;
 #30#1;
 ;
 nDEmpresa, nDPeriodo;
 nHEmpresa, nHPeriodo;
 e;
 NULL, HazTodo;
|FIN;


|PROGRAMA;

  |ABRE #11;
  |DDEFECTO #11;
  |LEE 001#11p;
  |CIERRA #11;


  |SI enOpAlta = 0;

      #0#0 = raf_cue;
      #0#1 = raf_des;

      cta = raf_cue; |QBF cta; longnum = cta % A0;

      |PINPA #0#0;

  |SINO;

      #0#5 = enPrAlta;
      #0#6 = eaPrAlta;
      |PINPA #0#0;
      |ATRI R; |PINTA 1215, "Predef."; |ATRI 0;
      |C_V #0#0,1,"N"; |C_V #0#1,1,"N";
      |C_V #0#5,1,"S"; |C_V #0#6,1,"S";

      nEmpresa = raf_empresa;
      nPeriodo = raf_periodo;
      |HAZ CalDirec;
      |PATH_DAT #2 dirfich0;
      |PATH_DAT #3 dirfich0;

      |ABRE #2;
      #2#0 = enPrAlta;
      |LEE 001#2=;
      |CIERRA #2;

      |DBASE aAlfa; |QBF aAlfa;
      nSwDescarga = 0;
      aAlfa1 = aAlfa + "def/casieaca.mas";
      |CARGA_DEF aAlfa1, casieac@;
      |SI FSalida = 0;
          aAlfa1 = aAlfa + "def/casieali.mas";
          |CARGA_DEF aAlfa1, casieal@;
          |SI FSalida = 0;
              nSwDescarga = 1;
          |SINO;
              |MENAV "    Error cargando datos ficheros ";
              |DESCARGA_DEF #casieac@;
              |ACABA;
          |FINSI;
      |SINO;
          |MENAV "    Error cargando datos ficheros ";
          |ACABA;
      |FINSI;


  |FINSI;

  |PINDA #0#0;
  |ATRI R;
  |PINTA 1410, "                                                     ";
  |ATRI 0;

|| .... Lectura de las Empresas

  |SI #11#49 ! 0;
      nDEmpresa = #11#49;
      nHEmpresa = #11#50;
      |HAZ CalPerio;
      |BUCLE LasEmpresas;
  |SINO;
      |PARA xx = 7;  |SI xx [ 26;  |HACIENDO xx = xx + 1;
            |ATRI R; |PINTA 1410, "                                                     "; |ATRI 0;
            |SI #11xx ! 0;
                nDEmpresa = #11xx;
                nHEmpresa = #11xx;
                |HAZ CalPerio;
                |BUCLE LasEmpresas;
            |FINSI;
      |SIGUE;
  |FINSI;

  |SI nSwDescarga = 1;
      |DESCARGA_DEF #casieal@;
      |DESCARGA_DEF #casieac@;
  |FINSI;
|FPRO;


||  ****************************************** Procesos Varios
|PROCESO pinta;
  |SI num =  1; mes = "Enero";     |FINSI;
  |SI num =  2; mes = "Febrero";   |FINSI;
  |SI num =  3; mes = "Marzo";     |FINSI;
  |SI num =  4; mes = "Abril";     |FINSI;
  |SI num =  5; mes = "Mayo";      |FINSI;
  |SI num =  6; mes = "Junio";     |FINSI;
  |SI num =  7; mes = "Julio";     |FINSI;
  |SI num =  8; mes = "Agosto";    |FINSI;
  |SI num =  9; mes = "Septiembre";|FINSI;
  |SI num = 10; mes = "Octubre";   |FINSI;
  |SI num = 11; mes = "Noviembre"; |FINSI;
  |SI num = 12; mes = "Diciembre"; |FINSI;
|FPRC;

|PROCESO CalDirec;
  |DBASE dir_fich;
  dirfich0 = dir_fich + "fich/";

  aAlfa1 = nEmpresa;
  aAlfa1 = "00000" + aAlfa1;   aAlfa1 = aAlfa1 % -105;
  aAlfa2 = nPeriodo;
  aAlfa2 = "0"   + aAlfa2;   aAlfa2 = aAlfa2 % -101;
  dirfich0 = dirfich0 + aAlfa1 + aAlfa2 + "/";
|FPRC;

|PROCESO CalPerio;
   |SI #11#48 = "="; nDPeriodo = raf_periodo;     nHPeriodo = raf_periodo;     |FINSI;
   |SI #11#48 = "T"; nDPeriodo = 0;               nHPeriodo = 9;               |FINSI;
   |SI #11#48 = "]"; nDPeriodo = raf_periodo;     nHPeriodo = 9;               |FINSI;
   |SI #11#48 = "["; nDPeriodo = 0;               nHPeriodo = raf_periodo;     |FINSI;
   |SI #11#48 = ">"; nDPeriodo = raf_periodo + 1; nHPeriodo = 9;               |FINSI;
   |SI #11#48 = "<"; nDPeriodo = 0;               nHPeriodo = raf_periodo - 1; |FINSI;
|FPRC;
