|FICHEROS;
  camaasiw #0;
  caivasop #1;
  caivaasi #2;
  caivases #3,MANTE;
  calibros #10;        || libros iva

  casieali #12;
  casieal@ #912;
|FIN;

|VARIABLES;
    &entrada  = 1;
    &ext1     = "";          || fecha
    &ext2     = "";          || libro
    &ext3     = "";          || FSalida
    &ext4     = "";          || Serie
    &ext5     = "";          || factura
    &extf     = @;           || fecha Operacion
    &ext13    = 0;
    nume1     = 0;

    SerieAnt  = "  XX";
    ComodNum  = 0;
    Comodin   = "";
    FEstado   = 0;
    Calc      = 0;
    inkey     = 0;
    aAlfa1    = "";
    aAlfa2    = "";
    aAlfa4    = "";
    nAntes    = 0;
    nNume1    = 0;
    nNume2    = 0;
    nPara1    = 0;
    nPara2    = 0;
    nError    = 0;
    aParApte  = "";
    nSumaIVA  = 0;
    nHayRec   = 0;
    aMas      = "";
    aDef      = "";

 &EURO = 1;
 &EURODB = 0;
 &EURODV = 0;
 &EUROTEXTO = "Cambio de Moneda";
 &swOperacion = 0;  || 0 = Lectura / 1 = Alta
 &enEmpresa = 0;
 &enPeriodo = 0;
 &eaMoneda  = "";
|FIN;

|INCLUYE varnue_i;

|PROCESO LeoEspejo;
  aAlfa4 = #912#56; aAlfa4 = aAlfa4  > " ";

  |SI aAlfa4 = "I"; |O aAlfa4 = "R";
      nSumaIVA = nSumaIVA + #912#60;
      |SI aAlfa4 = "R"; nHayRec = 1; |FINSI;
  |FINSI;
|FPRC;

|DEFBUCLE LeoEspejo;
 #912#1002;
 ;
 ext13, 01;
 ext13, 99;
 e;
 NULL, LeoEspejo;
|FIN;

|PROCESO MiroEspejo;
  nSumaIVA = 0;
  nHayRec  = 0;
  |BUCLE LeoEspejo;
  |SI nHayRec = 1; |O nSumaIVA = nNume2;
      aAlfa2 = "";
  |FINSI;
|FPRC;

|PROCESO CambioNumero;
      |SI nAntes = 0;
          |SI nNume1 = 18;  |O nNume1 = 1.18; |O nNume1 = 0.18; aAlfa2 = "18%";  nNume2 = 18;  |FINSI;
          |SI nNume1 =  8;  |O nNume1 = 1.08; |O nNume1 = 0.08; aAlfa2 = "8%";   nNume2 = 8;   |FINSI;
          |SI nNume1 = 10;  |O nNume1 = 1.10; |O nNume1 = 0.10; aAlfa2 = "10%";  nNume2 = 10;  |FINSI;
          |SI nNume1 = 8.5; |O nNume1 = 0.85;                   aAlfa2 = "8.5%"; nNume2 = 8.5; |FINSI;
      |SINO;
          |SI nNume1 = 16;  |O nNume1 = 1.16; |O nNume1 = 0.16; aAlfa2 = "16%";  nNume2 = 16;  |FINSI;
          |SI nNume1 =  7;  |O nNume1 = 1.07; |O nNume1 = 0.07; aAlfa2 = "7%";   nNume2 = 7;   |FINSI;
          |SI nNume1 =  9;  |O nNume1 = 1.09; |O nNume1 = 0.09; aAlfa2 = "9%";   nNume2 = 9;   |FINSI;
          |SI nNume1 = 7.5; |O nNume1 = 0.75;                   aAlfa2 = "7.5%"; nNume2 = 7.5; |FINSI;
      |FINSI;
|FPRC;

|PROCESO LeoPredefinidos;
  aParApte = #12#56; aParApte = aParApte > " ";

  |SI #12#60 ! 0; |Y aParApte = "I";
      nNume1 = #12#60;
      |HAZ CambioNumero;
  |FINSI;

  |SI aParApte !  "R"; |Y aParApte ! "P";
      |SI aAlfa2 = "";
          |SI #12#7 = "A";
              |PARA nPara1 = 48; |SI nPara1 [ 55; |HACIENDO nPara1 = nPara1 + 1;
                    nPara2 = nPara1 - 8;
                    |SI #12nPara2 ! " "; |Y #12nPara2 ! "+"; |Y #12nPara2 ! "-";
                         nNume1 = #12nPara1;
                         |HAZ CambioNumero;
                         |SI aAlfa2 ! "";
                             |HAZ MiroEspejo;
                             |SI aAlfa2 ! "";
                                 |SAL;
                             |FINSI;
                         |FINSI;
                     |FINSI;
              |SIGUE;
          |FINSI;
      |FINSI;
  |FINSI;

  |SI aAlfa2 ! "";  |ERROR10;  |FINSI;
|FPRC;

|DEFBUCLE LeoPredefinidos;
   #12#1;
   ;
   ext13, 01;
   ext13, 99;
   e;
   NULL, LeoPredefinidos;
|FIN;

|CALCULO VerFechas;
  |DBASE aMas; |QBF aMas;
  aDef = aMas + "def/casieali";
  |CARGA_DEF aDef, casieal@;
  |SI FSalida ! 0;
      |ACABA;
  |FINSI;

  nError  = 0;
  nAntes  = 0;
  aAlfa2  = "";
  aAlfa1  = "Fecha Factura ANTERIOR a 01.07.2010 y Asto Predefinido con IVA al ";
  |SI #0#2 ] "01.07.2010";
      aAlfa1  = "Fecha Factura POSTERIOR a 01.07.2010 y Asto Predefinido con IVA al ";
      nAntes  = 1;
  |FINSI;

  |BUCLE LeoPredefinidos;
  |SI aAlfa2 ! "";
      aAlfa1 = "    N" + aAlfa1 + aAlfa2 + ". Continuar ";
      |CONFI aAlfa1;
      |SI FSalida ! 0;
          nError = 1;
      |FINSI;
  |FINSI;

  |DESCARGA_DEF #casieal@;
|FCAL;

|PROCESO LaFechaOp;
  inkey = FSalida;

  |SI enEjercicio ] 2010;
      |HAZ VerFechas;
      |SI nError = 1;
          |PTEC 807;
          |ACABA;
      |FINSI;
  |FINSI;

  |C_M #0#4, 1, "N";
  |SI inkey = 11; |O #0#4 ! "01.01.0000";
      |C_M #0#4, 1, "S";
  |FINSI;
|FPRC;

|PROCESO AntesFecha; |TIPO 7;
  |C_M #0#4, 0, aAlfa1;
  |SI aAlfa1 = "N"; |ACABA; |FINSI;

  |SI #0#4 = "01.01.0000";
      #0#4 = #0#2; |PINTA #0#4;
  |FINSI;
|FPRC;

|PROGRAMA;
  |SI PARAMETRO ! "";
      Comodin = PARAMETRO;
      |QBF Comodin;
      ext4 = Comodin;
  |FINSI;

  |ABRE #2;
  |LEE 040#2p;
  |SI FSalida = 0;
      |SI #2#25 = "N";  |Y #2#26 = "N";
          |HAZ nolosaques;
          |SI nError = 1; ext3 = "-1"; |FINSI;
          |CIERRA #2;
          |ACABA;
      |SINO;
          |SI #2#26 = "N"; |Y  enEjercicio ] 2010;
              |HAZ VerFechas;
              |SI nError = 1;
                  ext3 = "-1";
                  |ACABA;
              |FINSI;
          |FINSI;
      |FINSI;
  |FINSI;
  |CIERRA #2;
  |SI #2#25 = "N";
      |CAMPO_MODIFICA #0#0,1,"N";
      |CAMPO_MODIFICA #0#1,1,"N";
  |FINSI;
  |SI #2#26 = "N";
      |CAMPO_MODIFICA #0#2,1,"N";
  |FINSI;

  |CABEZA "Iva Soportado";
  |DDEFECTO #0;

  #camaasiw#1 = ext4;

  |PINPA #0#0;
  #0#2 = ext1;         || fecha del asiento por defecto
  |SI #caivaasi#44 = "N";
     |C_M #camaasiw#1,1,"N";
     |HAZ ultima;
     |ABRE #calibros;
     #calibros#0 = ext2;
     |LEE 110#calibros.=;
     |SI FSalida = 0;
        |SI #camaasiw#0 < #calibros#4;
           #camaasiw#0 = #calibros#4;
           #calibros#4 = #calibros#4 + #calibros#3;
        |SINO;
           #calibros#4 = #camaasiw#0 + #calibros#3;
        |FINSI;
        |GRABA 020#calibros.a;
     |FINSI;
     |LIBERA #calibros;
     |CIERRA #calibros;
  |SINO;
     |SI #caivaasi#25 = "S";
         |C_M #camaasiw#1,1,"S";
     |FINSI;
     |HAZ MiraSerie;
  |FINSI;
  |PINDA #0#0;
  |ENDATOS #1#0;
  ext1 = #0#2;         || fecha
  ext3 = FSalida;
  |SI nError = 1; ext3 = "-1"; |FINSI;
  ext4 = #camaasiw#1;
  ext5 = #0#0;         || orden
  extf = #0#4;         || fecha Operacion

  |SI ext3 ! 0;
     |SI #caivaasi#44 = "N";
||        |HAZ ultima;
        |ABRE #calibros;
        #calibros#0 = ext2;
        |LEE 110#calibros.=;
        |SI FSalida = 0;
           Calc = #camaasiw#0 + #calibros#3;
           |SI #calibros#4 = Calc;
              |SI #camaasiw#0 ! 1;
                 #calibros#4 = #camaasiw#0;
              |FINSI;
           |FINSI;
           |GRABA 020#calibros.a;
        |FINSI;
        |LIBERA #calibros;
        |CIERRA #calibros;
     |SINO;
        |ABRE #caivases;
        #caivases#0 = #camaasiw#1;
        |LEE 101#caivases.=;
        |SI FSalida = 0;
           ComodNum = #camaasiw#0 + #caivases#4;
           |SI ComodNum = #caivases#3;
              #caivases#3 = #camaasiw#0;
              |GRABA 020#caivases.a;
           |FINSI;
        |FINSI;
        |LIBERA #caivases;
        |CIERRA #caivases;
     |FINSI;
  |FINSI;
|FPRO;

|PROCESO nolosaques;
  |DDEFECTO #0;
  ext3 = 0;
  #0#1 = ext4;
  ||   ext4 = #0#1;
  |SI enEjercicio ] 2010;
      |HAZ VerFechas;
      |SI nError = 1; |ACABA; |FINSI;
  |FINSI;

  |SI #caivaasi#44 = "N";
     |C_M #camaasiw#1,1,"N";
     |HAZ ultima;
     |ABRE #calibros;
     #calibros#0 = ext2;
     |LEE 110#calibros.=;
     |SI FSalida = 0;
        |SI #camaasiw#0 < #calibros#4;
           #camaasiw#0 = #calibros#4;
           #calibros#4 = #calibros#4 + #calibros#3;
        |SINO;
           #calibros#4 = #camaasiw#0 + #calibros#3;
        |FINSI;
        |GRABA 020#calibros.a;
     |FINSI;
     |LIBERA #calibros;
     |CIERRA #calibros;
  |SINO;
     |C_M #camaasiw#1,1,"S";
     |HAZ MiraSerie;
  |FINSI;
  ext5 = #0#0;
|FPRC;

|PROCESO ultima; |TIPO 7;
  |ABRE #10;
  |DDEFECTO #10;
  #10#0 = ext2;
  |LEE 001#10=;
  |CIERRA #10;

||  #0#0 = #10#3;
  |DDEFECTO #1;
  |ABRE #1;
  #1#0 = ext2;    || libro
  #1#2 = 999999;  || orden
  |LEE 001#1];
  |SI FSalida ! 0;
      |LEE 001#1u;
      |SI FSalida = 0; |Y #1#0 = ext2;
          #0#0 = #1#2 + #10#3;
      |SINO;
          |HAZ anterior;
      |FINSI;
  |SINO;
      |HAZ anterior;
  |FINSI;
  |CIERRA #1;

|ET sientrada;
  nume1 = #0#0 $ #10#3;
  |SI nume1 ! 0;
||      #0#0 = #0#0 - 1;
||      |VETE sientrada;
  |FINSI;
  ext5 = #0#0;
|FPRC;

|PROCESO anterior;
  |LEE 001#1a;
  |SI FSalida = 0; |Y #1#0 = ext2;
      #0#0 = #1#2 + #10#3;
  |FINSI;
|FPRC;

|PROCESO MiraSerie;

   |SI #caivaasi#44 = "N"; |ACABA; |FINSI;

   |ABRE #caivases;
   #caivases#0 = #camaasiw#1;
   #caivases#1 = "S";
   |LEE 000#caivases.=;
   FEstado = FSalida;
   |CIERRA #caivases;

   |SI FEstado ! 0; |ACABA; |FINSI;

   |SI SerieAnt ! #camaasiw#1;
      |ABRE #caivases;
      |SI SerieAnt ! "  XX";
         #caivases#0 = SerieAnt;
         #caivases#1 = "S";
         |LEE 101#caivases.=;
         |SI FSalida = 0;
            ComodNum = #camaasiw#0 + #caivases#4;
            |SI ComodNum = #caivases#3;
               #caivases#3 = #camaasiw#0;
               |GRABA 020#caivases.a;
            |FINSI;
         |FINSI;
      |FINSI;

      #caivases#0 = #camaasiw#1;
      #caivases#1 = "S";
      |LEE 101#caivases.=;
      |SI FSalida = 0;
         #camaasiw#0 = #caivases#3;
         #caivases#3 = #caivases#3 + #caivases#4;
         |GRABA 020#caivases.a;
      |FINSI;
      |LIBERA #caivases;
      |CIERRA #caivases;
      SerieAnt = #camaasiw#1;
   |FINSI;
|FPRC;

|PROCESO MiraNumero;
   |ABRE #caivasop;
   #caivasop#0 = ext2;
   #caivasop#2 = #camaasiw#0;
   #caivasop#27 = #camaasiw#1;
   |LEE 010#caivasop.=;
   |SI FSalida = 0;
      |MENAV "0400La factura ya existe";
      |ERROR;
   |SINO;
      |SI #camaasiw#0 ! Contenido;
         |ABRE #caivases;
         #caivases#0 = #camaasiw#1;
         #caivases#1 = "R";
         |LEE 110#caivases.=;
         |SI FSalida = 0;
            #caivases#3 = #camaasiw#0 + 1;
            |GRABA 020#caivases.a;
         |FINSI;
         |LIBERA #caivases;
         |CIERRA #caivases;
      |FINSI;
   |FINSI;
   |CIERRA #caivasop;
|FPRC;
