|FICHEROS;
    cacuadre #0;
    cacuenta #5;
    cacuebal #11;
    ca8m0002 #422;
|FIN;

|VARIABLES;
    nEstado = 0;
    impreso = 0;
    nombre = "";
    nomcuadre = "";
    numero = "";
    path = "";
    l = "";
    temporal1 = "";
    temporal2 = "";
    nPara1 = 0;
    alfa1 = "";
    alfa2 = "";
    alfa3 = "";
    alfa4 = "";
    nSwLaC = 0;
    nSwError = 0;
    nume1 = 0;
    long  = 0;
    sw = 0;
    swdig = 0;
    salidas = 0;
    cuen = "";
    digi = 0;
    &enEjerBal = 0;
|FIN;

|INCLUYE varnue_i;
|INCLUYE inicio_i;
|INCLUYE puntos_i;
|INCLUYE defec1_i;

|PROCESO minus; |TIPO 0;
  alfa1 = #0Campo < " ";
  #0Campo = alfa1;
  |PINTA #0Campo;
|FPRC;

|PROCESO mayus; |TIPO 0;
  alfa1 = #0Campo > " ";
  #0Campo = alfa1;
  |PINTA #0Campo;
|FPRC;

|CALCULO Especial; |TIPO 0;
 #0#80 = #0#80 > " "; |PINTA #0#80;

 alfa1 = #0#80;
 |C_M #0#81,1, alfa1;
 |C_M #0#83,1, alfa1;
 |SI alfa1 = "N";
     #0#81 = "";
     #0#83 = "";
 |SINO;
      alfa1 = #0#81; |QBF alfa1; |SI alfa1 = ""; #0#81 = "4C1*"; |FINSI;
      alfa1 = #0#83; |QBF alfa1; |SI alfa1 = ""; #0#83 = "524.CCC"; |FINSI;
 |FINSI;
 |PINTA #0#81;
 |PINTA #0#83;
|FCAL;

|CALCULO LCarta;
  alfa2 = "caemital"; nume1 = 79;
  |SI Campo = 6;  alfa2 = "caemipag"; nume1 = 78; |FINSI;
  |SI Campo = 85; alfa2 = "caremesp"; nume1 = 86; |FINSI;
  |SI #0Campo > 0;
      alfa1 = #0Campo; |QBF alfa1;
      alfa1 = ("00" + alfa1) % -102;
      alfa2 = alfa2 % 106;
      alfa2 = alfa2 + alfa1;
  |FINSI;
  |SI #0Campo ! -1;
      #0nume1 = alfa2; |PINTA #0nume1;
  |FINSI;
|FCAL;

|PROCESO Limita;
   alfa1 = #0Campo;
   |C_M #0#88,1,alfa1;
   |SI alfa1 = "N";
       #0#88 = 01; |PINTA #0#88;
   |FINSI;
|FPRC;

|PROGRAMA;
  |HAZ inicio;
  |HAZ eVarEmpresa;
  |CLS;
  |CABEZA "Cuadres y Ctrl Pg./Ta.";
  |ABRE #0;
  |DDEFECTO #0;
  |LEE 001#0p;
  nEstado = FSalida;
  |PINPA #0#0;
  |PINDA #0#0;
  |ENDATOS #1#0;
  |SI FSalida = 0;
      |HAZ configu;
      #0#1  = -1;
      #0#6  = -1;
      #0#37 = -1;
      #0#85 = -1;
      |SI nEstado = 0; |GRABA 020#0a; |FINSI;
      |SI nEstado ! 0; |GRABA 020#0n; |FINSI;
  |FINSI;
  |CIERRA #0;
|FPRO;

|PROCESO configu;
  |SI #0#1 ! -1;
      nombre = "caimpcob.";  numero = #0#1; nomcuadre = "caimpcob"; |HAZ copia;
  |FINSI;
  |SI #0#6 ! -1;
      nombre = "caemipag.";  numero = #0#6; nomcuadre = #0#78; |HAZ copia;
  |FINSI;
  |SI #0#37 ! -1;
      nombre = "caemital.";  numero = #0#37; nomcuadre = #0#79; |HAZ copia;
  |FINSI;
  |SI #0#85 ! -1;
      nombre = "caremesp.";  numero = #0#86; nomcuadre = #0#86; |HAZ copia;
  |FINSI;
|FPRC;

|PROCESO copia;
  |QBF nomcuadre;
  |SI nomcuadre = "";
      |MENAV "    Error. No indicado el nombre del Cuadre actual";
      |ACABA;
  |FINSI;
  nomcuadre = nomcuadre + ".inf";
  |DDEF path;
  numero = "00" + numero;
  numero = numero % -102;
  temporal1 = path + nombre + numero;
  temporal2 = path + nomcuadre;

  |COPIA_FICHERO temporal1,temporal2;
  |PINTA 2301, temporal1;
|FPRC;

|| **************************** PROCESOS PARA LAS CONTRAPARTIDAS

|PROCESO cuenta; |TIPO 0;
  |C_M #0Campo,0,alfa1;
  |SI alfa1 = "N"; |ACABA; |FINSI;

  nume1 = Campo + 1;  #0nume1 = 0;

  alfa1 = #0Campo;  |QBF alfa1;

  alfa2 = alfa1 % 0; long  = alfa2;

  alfa4 = "";
  nSwLaC = 0;
  nSwError = 0;
  |PARA nPara1 = 1; |SI nPara1 [ long; |HACIENDO nPara1 = nPara1 + 1;

        nume1 = (nPara1 * 100) + 1;
        alfa3 = alfa1 % nume1;

        |SI alfa3 = "c"; |O alfa3 = "C";  nSwLaC = 1; alfa3 = "C"; |FINSI;
        alfa4 = alfa4 + alfa3;

        |SI alfa3 = "*"; |O alfa3 = "%";
            |SAL;
        |SINO;
            |SI alfa3 < "0"; |O alfa3 > "9";
                |SI alfa3 ! "C"; |Y alfa3 ! ".";
                    nSwError = 1; |SAL;
                |FINSI;
            |FINSI;
        |FINSI;
  |SIGUE;

  |SI nSwError = 1; |ERROR; |ACABA; |FINSI;
  |SI Campo = 23; |Y nSwLaC = 1; |ERROR; |ACABA; |FINSI;

  #0Campo = alfa4; |PINTA #0Campo;

  nume1 = (long * 100) + 1;
  alfa2 = alfa4 % nume1;

  |SI alfa2 ! "*";|Y alfa2 ! "%";
      || ......................... Calculo de Digito.
      p_alfa1 = #0Campo;
      salidas = FSalida;

      |HAZ punto;
      |SI p_error ! 0;  |ERROR;  |ACABA;  |FINSI;

       nume1 = Campo + 1;
       alfa1 = #0Campo;
       |QBF alfa1;
       alfa1 = alfa1 % 0;
       long  = alfa1;
       swdig = 0;
       #0nume1 = 0;  || inicializa el digito
       |SI long = dig4; |Y dig4 ! 0; swdig = 1; #0nume1 = 1; |FINSI;
       |SI long = dig5; |Y dig5 ! 0; swdig = 1; #0nume1 = 2; |FINSI;
       |SI long = dig6; |Y dig6 ! 0; swdig = 1; #0nume1 = 3; |FINSI;
       |SI long = dig7; |Y dig7 ! 0; swdig = 1; #0nume1 = 4; |FINSI;
       |SI long = dig8; |Y dig8 ! 0; swdig = 1; #0nume1 = 5; |FINSI;
       |SI long = dig9; |Y dig9 ! 0; swdig = 1; #0nume1 = 6; |FINSI;
       |SI swdig = 0;|Y long ! 0;
           |MENAV "    Longitud de Cuenta Fuera de Nivel !";
           |ERROR;
       |SINO;
            cuen = #0Campo;
            digi = #0nume1;
            |SI nSwLaC = 0;
                |HAZ alta_cuenta;
            |FINSI;
       |FINSI;
  |FINSI;

|FPRC;

|PROCESO alta_cuenta;    || comprueba si la cuenta esta dada de alta
|ABRE #5;
    #5#0 = cuen;
    #5#1 = digi;
|LEE 001#5=;
 |SI FSalida ! 0;
    |DDEFECTO #5;
        #5#0 = cuen;
        #5#1 = digi;
    |HAZ defect1;
    |PUSHV 0501 2380;
    |PINPA #0#5;
    |PINDA #0#5;
    |ENDATOS #1#5;
    |SI FSalida = 0;
        |GRABA 020#5n;
    |FINSI;
    |POPV;
 |FINSI;
 |SI #5#3 = "N";
     |MENAV "    La Cuenta NO tiene PASE DIRECTO";
     |ERROR;
 |FINSI;
 |CIERRA #5;
|FPRC;

|PROCESO nolinea1; |TIPO 7;
     |SI Campo > 47;
         nume1 = Campo - 23;
     |SINO;
         nume1 = Campo - 31;
     |FINSI;
     alfa1 = #0nume1;
     |QBF alfa1;
     |SI alfa1 ! "";
          |C_M #0Campo, 1, "S";
     |SINO;
          |C_M #0Campo, 1, "N";
     |FINSI;
|FPRC;

|PROCESO nolinea2; |TIPO 7;
     |SI Campo > 67;
         nume1 = Campo - 43;
     |SINO;
         nume1 = Campo - 51;
     |FINSI;
     alfa1 = #0nume1;
     |QBF alfa1;
     |SI alfa1 ! "";
          |C_M #0Campo, 1, "S";
     |SINO;
          |C_M #0Campo, 1, "N";
     |FINSI;
|FPRC;
