|FICHEROS;
    ca8zcamn #0;
    cacuenta #1;    || plan contable                 ---> puntos_i
    cacuebal #5;    || cuentas de balance
    ca8m0002 #422;

    casieali #21;   || asientos predefinidos lins.
    camanban #24;   || Bancos
    camaasic #25;   || apuntes cabs.
    camaasil #26;   || apuntes lins.
    caiivsol #28;   || intr. fras. soportado
    caivasop #29;   || iva soportado
    caivarep #31;   || iva repercutido
    cacuivli #32;   || ctas. iva lins.
    cacoivli #33;   || ctas. contrapartidas iva lins.

    camancob #34;   || cobros
    camanpag #35;   || pagos
    caremesc #36;   || remesas cabs.
    capagcab #37;   || pagares cabs.

    caconcic #38;   || diferencias cabs.
    cacuepre #39;   || cuentas presupuestarias
    camoauxc #40;   || cuentas inventario
    caconcil #45;   || diferencias lineas.

    camocabe #41;   || amortizaciones
    camogrup #42;   || grupos amortizaciones
    caexistc #43;   || descarga existencias cabs.
    caexistl #44;   || descarga existencias lins.

    camaylin #46;   || lineas mayor

    caiivrel #47;   || intr. fras. repercutido
    cacup300 #49;   || control pase contable 300

    caclipro #50;   || Clientes Proveedores GENERAL
    capasven #51;   || asiento ventas automaticas.
    camoauxl #52;   || cuentas inventario lineas

    cacup130 #61;   || control pase contable 130.
    cacup202 #62;   || control pase contable 202.
    cacupimp #63;   || control pase contable Impuesto.
    caivaasi #64;   || Ctrl Entrada IVA asientos.
    cagruiva #65;   || Grupos facturas Englobadas.
    cavengca #66;
    calicont #67;   || Contrapartidas Apuntes
    camautol #69;   || Asientos Automaticos Lineas
    cagruiva #70;   ||grupo de iva

    ca8m0003 #403;
    ca8m0004 #404;
    ca8m0005 #405;

    espejo1@ #900;
|FIN;

|VARIABLES;
    nNivel  = 0;
    nNivelA = 0;
    nNivelN = 0;
    aLong   = "";
    nLong   = 0;
    fDFecha = @;
    fHFecha = @;
    aAlfa1  = "";
    mens    = "";
    traba   = "";
    x       = 0;

    para3 = 0;
    para4 = 0;
    &aPathPlan = "";
    &num_por    = 0;
    &num_per    = 0;

    nGraba       = 0;
    nCambia      = 0;
    aCtaAnterior = "";
    aCtaNueva    = "";

    aMas      = "";
    aDef      = "";
    nError    = 0;
    aFichero  = "";
    traba1    = "";
    temporal1 = "";
    temporal2 = "";

    nPara1    = 0;
    aQueQuiero = "";
    aNumCampos = "";
    nNumCampos = 0;
    nOp        = 0;
    &nRecalculo = 0;
|FIN;

|INCLUYE varnue_i;
|INCLUYE acceso_i;
|INCLUYE defec1_i;

|| **************************************************************
|PROCESO CargaDefs;
   nError = 0;
   |DBASE aMas;
   aDef = aMas + "def/" + traba1;
   |CARGA_DEF aDef, espejo1@;
   |SI FSalida !  0;
       aDef  = "";
       nError = 1;
   |FINSI;
|FPRC;

|PROCESO DesCargaDefs;
   |SI aDef ! ""; |DESCARGA_DEF #espejo1@; |FINSI;
|FPRC;

|PROCESO copiando;
  temporal1 = fich_alta + traba1   + ".ixx";
  temporal2 = fich_alta + aFichero + ".ixx";
  |COPIA_FICHERO temporal1,temporal2;
  |SI FSalida ! 0;  |ACABA;  |FINSI;
  temporal1 = fich_alta + traba1   + ".dat";
  temporal2 = fich_alta + aFichero + ".dat";
  |COPIA_FICHERO temporal1,temporal2;
  |SI FSalida ! 0;
      temporal2 = fich_alta + aFichero + ".ixx";
      |FBORRA temporal2;
      |ACABA;
  |FINSI;
  temporal1 = fich_alta + traba1   + ".ddx";
  temporal2 = fich_alta + aFichero + ".ddx";
  |COPIA_FICHERO temporal1,temporal2;
|FPRC;

|| **************************************************************
|PROCESO ca8m0004;
   mens = " Cambio de Asiento Apertura" + #ca8m0004#0;   #0#1 = mens;   |PINTA #0#1;
   eaCuenta = #404#1;
   |HAZ LeoTabla_Pr;
   |SI nCambia = 1;
       #404#8 = aCtaNueva;
       #404#14 = "*";
       |GRABA 020#404a;
   |FINSI;
   |LIBERA #404;
|FPRC;

|DEFBUCLE ca8m0004;
   #404#1;
   ;
   00, fDFecha, 000001, 0001;
   00, fHFecha, 999999, 9999;
   be;
   NULL, ca8m0004;
|FIN;

|PROCESO ca8m0003;
   mens = " Cambio de Tabla Equivalencias " + #ca8m0003#0;   #0#1 = mens;   |PINTA #0#1;
   eaCuenta = #403#0;
   |HAZ LeoTabla_Pr;
   |SI nCambia = 1;
       #403#4 = #403#2;
       #403#5 = #403#3;
       #403#2 = aCtaNueva;
       #403#6 = "*";
       |GRABA 020#403a;
   |FINSI;
   |LIBERA #403;
|FPRC;

|DEFBUCLE ca8m0003;
   #403#1;
   ;
   "            ";
   "zzzzzzzzzzzz";
   be;
   NULL, ca8m0003;
|FIN;

|| **************************************************************
|PROCESO Operacion_cta;
 |ABRE #1;
 |DDEFECTO #1;
 #1#0 = #405#4;
 #1#1 = nNivelN;
 |LEE 101#1=;
 |SI FSalida ! 0;
     #1#0 = #405#4;
     #1#1 = nNivelN;
     |GRABA 020#1b;
 |FINSI;
 |PARA nPara1 = 0; |SI nPara1 [ nNumCampos; |HACIENDO nPara1 = nPara1 + 1;
       #1nPara1 = #900nPara1;
 |SIGUE;

 #1#0 = #405#4;
 #1#1 = nNivelN;
 aAlfa1 = #1#0; |QBF aAlfa1;
 #1#54  = (aAlfa1 + "zzzzzzzzzzzzz") % 112;
 |SI nNivelN ! nNivelA;
     |SI nNivelN = dig3; #1#3 = "S"; |FINSI;
 |FINSI;
 |GRABA 020#1a;
 |LIBERA #1;
 |CIERRA #1;
|FPRC;

|PROCESO ca8m0005_Cta;

  |SI #405#2 = #405#4; |ACABA; |FINSI;  ||No ya cambio

  |SI nOp = 0; |Y #405#6 ! "S"; |ACABA; |FINSI;

  emCta    = 0;
  eaCuenta = #405#4;
  |HAZ eSacaNivel;
  nNivelN  = enNivel;

  eaCuenta = #405#2;
  |HAZ eSacaNivel;
  nNivelA  = enNivel;

  |SI nOp = 0;
      |ABRE #1;
      #1#0 = #405#2;
      #1#1 = nNivelA;
      |LEE 101#1=;
      |SI FSalida = 0;
          |BORRA 020#1a;
      |FINSI;
      |LIBERA #1;
      |CIERRA #1;
  |FINSI;

  |SI nOp = 1;
      |ABRE #900;
      #900#0 = #405#2;
      #900#1 = nNivelA;
      |LEE 101#900=;
      |SI FSalida = 0;
          |HAZ Operacion_cta;
      |FINSI;
      |LIBERA #900;
      |CIERRA #900;
  |FINSI;
|FPRC;

|DEFBUCLE ca8m0005_Cta;
 #405#1;
 7;
 0001, "            ", "S";
 9999, "zzzzzzzzzzzz", "S";
 ;
 NULL, ca8m0005_Cta;
|FIN;

|PROCESO cacuenta;
 traba1 = "cacuenta";
 |HAZ CargaDefs;
 |SI nError = 1; |ACABA; |FINSI;

 aFichero = ("d" + Usuario) % 108;
 |NOME_DAT #900 aFichero;
 |PATH_DAT #900 fich_alta;

 |HAZ copiando;

 aQueQuiero = "|NC";
 aNumCampos = #1#aQueQuiero#;
 nNumCampos = aNumCampos;
 nNumCampos = nNumCampos - 1;

 nOp = 0; |BUCLE ca8m0005_Cta;
 nOp = 1; |BUCLE ca8m0005_Cta;

 |DELINDEX #900;
 |HAZ DesCargaDefs;
|FPRC;

|| --------- Clientes y Proveedores
|PROCESO caclipro1;
  mens = "Contrapartida Cli./Prov.> " + #50#10;   #0#1 = mens;   |PINTA #0#1;
  aCtaAnterior = #50#21;
  |HAZ LeoTabla;
  |SI nCambia = 1;
      #50#21 = aCtaNueva;
      #50#22 = nNivel;
      |GRABA 020#50a;
  |FINSI;
  |LIBERA #50;
|FPRC;

|DEFBUCLE caclipro;
 #50#1;
 ;
 " ","            ",0;
 "z","zzzzzzzzzzzz",9;
 be;
 NULL, caclipro1;
|FIN;
|| ------------------------

|PROCESO Operacion;
 |ABRE #50;
 |DDEFECTO #50;
 #50#23 = #900#23;
 #50#10 = #405#4;
 #50#11 = nNivelN;
 |LEE 101#50=;
 |SI FSalida ! 0;
     #50#23 = #900#23;
     #50#10 = #405#4;
     #50#11 = nNivelN;
     |GRABA 020#50b;
 |FINSI;
 |PARA nPara1 = 0; |SI nPara1 [ nNumCampos; |HACIENDO nPara1 = nPara1 + 1;
       #50nPara1 = #900nPara1;
 |SIGUE;
 #50#10 = #405#4;
 #50#11 = nNivelN;
 |GRABA 020#50a;
 |LIBERA #50;
 |CIERRA #50;
|FPRC;

|PROCESO ca8m0005_CP;

  |SI #405#2 = #405#4; |ACABA; |FINSI;  ||No hay cambio

  |SI nOp = 0; |Y #405#6 ! "S"; |ACABA; |FINSI;

  emCta    = 0;
  eaCuenta = #405#4;
  |HAZ eSacaNivel;
  nNivelN = enNivel;

  eaCuenta = #405#2;
  |HAZ eSacaNivel;
  nNivelA = enNivel;

  |SI nOp = 0;
      |ABRE #50;
      #50#23 = "C";
      #50#10 = #405#2;
      #50#11 = nNivelA;
      |LEE 101#50=;
      |SI FSalida = 0;
          |BORRA 020#50a;
      |FINSI;
      |LIBERA #50;

      #50#23 = "P";
      #50#10 = #405#2;
      #50#11 = nNivelA;
      |LEE 101#50=;
      |SI FSalida = 0;
          |BORRA 020#50a;
      |FINSI;
      |LIBERA #50;
      |CIERRA #50;
  |FINSI;

  |SI nOp = 1;
      |ABRE #900;
      #900#23 = "C";
      #900#10 = #405#2;
      #900#11 = nNivelA;
      |LEE 101#900=;
      |SI FSalida = 0;
          |HAZ Operacion;
      |FINSI;
      |LIBERA #900;

      #900#23 = "P";
      #900#10 = #405#2;
      #900#11 = nNivelA;
      |LEE 101#900=;
      |SI FSalida = 0;
          |HAZ Operacion;
      |FINSI;
      |LIBERA #900;
      |CIERRA #900;
  |FINSI;
|FPRC;

|DEFBUCLE ca8m0005_CP;
 #405#1;
 7;
 0001, "            ", "S";
 9999, "zzzzzzzzzzzz", "S";
 ;
 NULL, ca8m0005_CP;
|FIN;

|PROCESO caclipro;
 traba1 = "caclipro";
 |HAZ CargaDefs;
 |SI nError = 1; |ACABA; |FINSI;

 aFichero = ("e" + Usuario) % 108;
 |NOME_DAT #900 aFichero;
 |PATH_DAT #900 fich_alta;

 |HAZ copiando;

 aQueQuiero = "|NC";
 aNumCampos = #50#aQueQuiero#;
 nNumCampos = aNumCampos;
 nNumCampos = nNumCampos - 1;

 nOp = 0; |BUCLE ca8m0005_CP;
 nOp = 1; |BUCLE ca8m0005_CP;

 |DELINDEX #900;
 |HAZ DesCargaDefs;

 |BUCLE caclipro;   ||las contrapartidas
|FPRC;

|PROCESO camogrup1;
   mens = " Grupos Amortizacion> " + #42#0;   #0#1 = mens;   |PINTA #0#1;

   nGraba = 0;
   aCtaAnterior = #42#1; |HAZ LeoTabla; |SI nCambia = 1; #42#1 = aCtaNueva; #42#2 = nNivel; nGraba = 1; |FINSI;
   aCtaAnterior = #42#3; |HAZ LeoTabla; |SI nCambia = 1; #42#3 = aCtaNueva; #42#4 = nNivel; nGraba = 1; |FINSI;
   aCtaAnterior = #42#5; |HAZ LeoTabla; |SI nCambia = 1; #42#5 = aCtaNueva; #42#6 = nNivel; nGraba = 1; |FINSI;
   aCtaAnterior = #42#7; |HAZ LeoTabla; |SI nCambia = 1; #42#7 = aCtaNueva; #42#8 = nNivel; nGraba = 1; |FINSI;
   |SI nGraba = 1;
       |GRABA 020#42a;
   |FINSI;
   |LIBERA #42;
|FPRC;

|DEFBUCLE camogrup;
   #42#1;
   ;
   0001;
   9999;
   be;
   NULL, camogrup1;
|FIN;


|PROCESO camocabe1
   mens = " Elementos Amortizables> " + #41#0+ "-" + #41#1;    #0#1 = mens;   |PINTA #0#1;
   nGraba = 0;
   aCtaAnterior = #41#3;
   |HAZ LeoTabla;
   |SI nCambia = 1;
       #41#3 = aCtaNueva;
       #41#4 = nNivel;
       |GRABA 020#41a;
   |FINSI;
   |LIBERA #41;
|FPRC;

|DEFBUCLE camocabe;
   #41#1;
   ;
   "        ", 00;
   "zzzzzzzz", 99;
   be;
   NULL, camocabe1;
|FIN;

|| ....... Ventas automaticas
|PROCESO cavengca1;
  mens = " Ventas Englobadas > " + #66#0;   #0#1 = mens;   |PINTA #0#1;
  aCtaAnterior = #66#4;
  |HAZ LeoTabla;
  |SI nCambia = 1;
      #66#4 = aCtaNueva;
      #66#5 = nNivel;
      |GRABA 020#66a;
  |FINSI;
  |LIBERA #66;
|FPRC;

|DEFBUCLE cavengca;
 #66#1;
 ;
 fDFecha;
 fHFecha;
 be;
 NULL, cavengca1;
|FIN;

|| .............. COntroles de Pase
|PROCESO cacup300;

  |ABRE #49;
  #49#0 = 1;
  |LEE 000#49=;
  |SI FSalida ! 0;
      |CIERRA #49;
      |ACABA;
  |FINSI;

  nGraba = 0;
  mens = " Control Pase Modelo 300> "; #0#1 = mens; |PINTA #0#1;
  |PARA para4 = 1; |SI para4 [ 11; |HACIENDO para4 = para4 + 1;
         para3 = para4 + 11;
         aCtaAnterior = #49para4;
         |HAZ LeoTabla;
         |SI nCambia = 1;
             #49para4 = aCtaNueva;
             #49para3 = nNivel;
             nGraba = 1;
         |FINSI;
  |SIGUE;
  |SI nGraba = 1; |GRABA 020#49a; |FINSI;
  |LIBERA #49;
  |CIERRA #49;
|FPRC;
____________________________________/ cacup130
|PROCESO cacup130;
  |ABRE #61;
  #61#0 = 1;
  |LEE 000#61=;
  |SI FSalida ! 0;
      |CIERRA #61;
      |ACABA;
  |FINSI;
  mens = " Control Pase Modelo 130>"; #0#1 = mens; |PINTA #0#1;
  nGraba = 0;
  |PARA para4 = 1; |SI para4 [ 11; |HACIENDO para4 = para4 + 1;
         para3 = para4 + 11;
         aCtaAnterior = #61para4;
         |HAZ LeoTabla;
         |SI nCambia = 1;
             #61para4 = aCtaNueva;
             #61para3 = nNivel;
             nGraba = 1;
         |FINSI;
  |SIGUE;
  |SI nGraba = 1; |GRABA 020#61a; |FINSI;
  |LIBERA #61;
  |CIERRA #61;
|FPRC;
____________________________________/ cacup202
|PROCESO cacup202;
 |ABRE #62;
  #62#0 = 1;
  |LEE 000#62=;
  |SI FSalida ! 0;
      |CIERRA #62;
      |ACABA;
  |FINSI;
  mens = " Control Pase Modelo 202>"; #0#1 = mens; |PINTA #0#1;
  nGraba = 0;
  |PARA para4 = 1; |SI para4 [ 11; |HACIENDO para4 = para4 + 1;
        para3 = para4 + 11;
        aCtaAnterior = #62para4;
         |HAZ LeoTabla;
         |SI nCambia = 1;
             #62para4 = aCtaNueva;
             #62para3 = nNivel;
             nGraba = 1;
         |FINSI;
  |SIGUE;
  |SI nGraba = 1; |GRABA 020#62a; |FINSI;
  |LIBERA #62;
  |CIERRA #62;
|FPRC;
____________________________________/ cacupimp
|PROCESO cacupimp;
  |ABRE #63;
  #63#0 = 1;
  |LEE 000#63=;
  |SI FSalida ! 0;
      |CIERRA #63;
      |ACABA;
  |FINSI;

  mens = " Control Pase del Impuesto de Sociedades>"; #0#1 = mens; |PINTA #0#1;
  nGraba = 0;
  |PARA para4 = 1; |SI para4 [ 11; |HACIENDO para4 = para4 + 1;
        para3 = para4 + 11;
        aCtaAnterior = #63para4;
         |HAZ LeoTabla;
         |SI nCambia = 1;
             #63para4 = aCtaNueva;
             #63para3 = nNivel;
             nGraba = 1;
         |FINSI;
  |SIGUE;
  aCtaAnterior = #63#58;|HAZ LeoTabla; |SI nCambia = 1; #63#58 = aCtaNueva; #63#59 = nNivel; nGraba = 1; |FINSI;

  |SI nGraba = 1; |GRABA 020#63a; |FINSI;
  |LIBERA #63;
  |CIERRA #63;
|FPRC;

|| ------------------------- capasven1
|PROCESO capasven1;
   mens = " Control Pase Ventas Contado > " + #capasven#0; #0#1 = mens; |PINTA #0#1;

   nGraba = 0;
   aCtaAnterior = #51#9;  |HAZ LeoTabla; |SI nCambia = 1; #51#9  = aCtaNueva; #51#12 = nNivel; nGraba = 1; |FINSI;
   aCtaAnterior = #51#10; |HAZ LeoTabla; |SI nCambia = 1; #51#10 = aCtaNueva; #51#13 = nNivel; nGraba = 1; |FINSI;
   aCtaAnterior = #51#11; |HAZ LeoTabla; |SI nCambia = 1; #51#11 = aCtaNueva; #51#14 = nNivel; nGraba = 1; |FINSI;
   |SI nGraba = 1;
       |GRABA 020#51a;
   |FINSI;
   |LIBERA #51;
|FPRC;

|DEFBUCLE capasven;
   #51#1;
   ;
   01;
   99;
   be;
   NULL, capasven1;
|FIN;

____________________________________/ camaylin
|PROCESO camaylin;
   mens = " Diario Mayor-> " + #camaylin#3; #0#1 = mens; |PINTA #0#1;

   nGraba = 0;
   aCtaAnterior = #46#4;   |HAZ LeoTabla; |SI nCambia = 1; #46#4  = aCtaNueva; #46#5  = nNivel; nGraba = 1; |FINSI;
   aCtaAnterior = #46#10;  |HAZ LeoTabla; |SI nCambia = 1; #46#10 = aCtaNueva; #46#11 = nNivel; nGraba = 1; |FINSI;
   |SI nGraba = 1;
      |GRABA 020#46a;
   |FINSI;
   |LIBERA #46;
|FPRC;

|DEFBUCLE camaylin;
   #46#1;
   ;
   00, fDFecha, 000001, 0001;
   99, fHFecha, 999999, 9999;
   be;
   NULL, camaylin;
|FIN;


____________________________________/ cagruiva
|PROCESO cagruiva1;
   mens = " Grupos de IVA > " + #cagruiva#0; #0#1 = mens; |PINTA #0#1;

   nGraba = 0;

   |PARA para4 = 2; |SI para4 [ 16; |HACIENDO para4 = para4 + 1;
         para3 = para4 + 15;
         aCtaAnterior = #70para4;
         |HAZ LeoTabla;
         |SI nCambia = 1;
             #70para4 = aCtaNueva;
             #70para3 = nNivel;
             nGraba = 1;
         |FINSI;
   |SIGUE;
   |SI nGraba = 1;
       |GRABA 020#70a;
   |FINSI;
   |LIBERA #70;
|FPRC;

|DEFBUCLE cagruiva;
   #70#1;
   ;
   00;
   99;
   be;
   NULL, cagruiva1;
|FIN;

____________________________________/ camautol
|PROCESO camautol1;
    mens = " Astos Automaticos > " + #camautol#0 + "-" + #camautol#1;   #0#1 = mens;   |PINTA #0#1;

    aCtaAnterior = #69#2;
    |HAZ LeoTabla;
    |SI nCambia = 1;
        #69#2 = aCtaNueva;
        #69#3 = nNivel;
        |GRABA 020#69a;
    |FINSI;
    |LIBERA #69;
|FPRC;

|DEFBUCLE camautol;
   #69#1;
   ;
   00001,    0;
   99999, 9999;
   be;
   NULL, camautol1;
|FIN;

|| *******************************************************
|| ------------------ caexistl
|PROCESO caexistl1;
    mens = " Descarga Existencias> " + #44#0;   #0#1 = mens;   |PINTA #0#1;

    nGraba = 0;
    aCtaAnterior = #44#2;  |HAZ LeoTabla; |SI nCambia = 1; #44#2 = aCtaNueva; #44#3 = nNivel; nGraba = 1; |FINSI;
    aCtaAnterior = #44#5;  |HAZ LeoTabla; |SI nCambia = 1; #44#5 = aCtaNueva; #44#6 = nNivel; nGraba = 1; |FINSI;
    aCtaAnterior = #44#8;  |HAZ LeoTabla; |SI nCambia = 1; #44#8 = aCtaNueva; #44#9 = nNivel; nGraba = 1; |FINSI;

    |SI nGraba = 1;
        |GRABA 020#44a;
    |FINSI;
    |LIBERA #44;
|FPRC;

|DEFBUCLE caexistl;
 #44#1;
 ;
  0,  0;
 99, 99;
 be;
 NULL, caexistl1;
|FIN;

|| ----------------------------- caexistc
|PROCESO caexistc1;
    mens = " Descarga Existencias> " + #43#0;   #0#1 = mens;   |PINTA #0#1;

    aCtaAnterior = #43#2;
    |HAZ LeoTabla;
    |SI nCambia = 1;
        #43#2 = aCtaNueva;
        #43#3 = nNivel;
        |GRABA 020#43a;
    |FINSI;
    |LIBERA #43;
|FPRC;

|DEFBUCLE caexistc;
 #43#1;
 ;
  0;
 99;
 be;
 NULL, caexistc1;
|FIN;

|| -------------- Pagares
|PROCESO capagcab1;
   mens = " Mantenimiento de Pagares> " + #37#0; #0#1 = mens; |PINTA #0#1;
   nGraba = 0;
   aCtaAnterior = #37#1;  |HAZ LeoTabla; |SI nCambia = 1; #37#1 = aCtaNueva; #37#2 = nNivel; nGraba = 1; |FINSI;
   aCtaAnterior = #37#3;  |HAZ LeoTabla; |SI nCambia = 1; #37#3 = aCtaNueva; #37#4 = nNivel; nGraba = 1; |FINSI;
   |SI nGraba = 1;
       |GRABA 020#37a
   |FINSI;
   |LIBERA #37;
|FPRC;

|DEFBUCLE capagcab;
   #37#1;
   ;
   000001;
   999999;
   be;
   NULL, capagcab1;
|FIN;

|| -------------- Remesas
|PROCESO caremesc1;
   mens = " Mantenimiento de Remesas> " + #36#0; #0#1 = mens; |PINTA #0#1;
   nGraba = 0;
   aCtaAnterior = #36#14;
   |HAZ LeoTabla;
   |SI nCambia = 1; #36#14 = aCtaNueva; #36#15 = nNivel; nGraba = 1; |FINSI;
   |SI nGraba = 1;
       |GRABA 020#36a
   |FINSI;
   |LIBERA #36;
|FPRC;

|DEFBUCLE caremesc;
   #36#1;
   ;
   00001;
   99999;
   be;
   NULL, caremesc1;
|FIN;

|| -------------- Pagos
|PROCESO camanpag1;
   mens = " Mantenimiento de Pagos> " + #35#0 + "-" + #35#2 + "-" + #35#1 + "-" + #35#3; #0#1 = mens; |PINTA #0#1;
   nGraba = 0;
   aCtaAnterior = #35#4;
   |HAZ LeoTabla;
   |SI nCambia = 1; #35#4 = aCtaNueva; #35#5 = nNivel; nGraba = 1; |FINSI;
   |SI nGraba = 1;
       |GRABA 020#35a
   |FINSI;
   |LIBERA #35;
|FPRC;

|DEFBUCLE camanpag;
   #35#1;
   ;
   01, "  ", 000001, 001;
   99, "zz", 999999, 999;
   be;
   NULL, camanpag1;
|FIN;

|| -------------- Cobros
|PROCESO camancob1;
   mens = " Mantenimiento de Cobros> " + #34#0 + "-" + #34#2 + "-" + #34#1 + "-" + #34#3; #0#1 = mens; |PINTA #0#1;
   nGraba = 0;
   aCtaAnterior = #34#4;
   |HAZ LeoTabla;
   |SI nCambia = 1; #34#4 = aCtaNueva; #34#5 = nNivel; nGraba = 1; |FINSI;
   |SI nGraba = 1;
       |GRABA 020#34a
   |FINSI;
   |LIBERA #34;
|FPRC;

|DEFBUCLE camancob;
   #34#1;
   ;
   01, "  ", 000001, 001;
   99, "zz", 999999, 999;
   be;
   NULL, camancob1;
|FIN;

____________________________________/ caivaasi
|PROCESO caivaasi;

   |ABRE #64;
   #caivaasi#0 = 1;
   |LEE 000#64=;
   |SI FSalida ! 0;
      |CIERRA #64;
      |ACABA;
   |FINSI;

   mens = " Control Entrada Asientos IVA > ";  #0#1 = mens;   |PINTA #0#1;
   nGraba = 0;
   aCtaAnterior = #64#39;
   |HAZ LeoTabla;
   |SI nCambia = 1; #64#39 = aCtaNueva; #64#40 = nNivel; nGraba = 1; |FINSI;
   |SI nGraba = 1;
       |GRABA 020#64a;
   |FINSI;
   |LIBERA #64;
   |CIERRA #64;
|FPRC;

|| -------------- Registros de IVA repercutido Introduccion
|PROCESO cacoivli1;
   mens = " Control de Contrapartidas de IVA> " + #33#0 + "-" + #33#1; #0#1 = mens; |PINTA #0#1;
   nGraba = 0;
   aCtaAnterior = #33#2;  |HAZ LeoTabla; |SI nCambia = 1; #33#2  = aCtaNueva; #33#3  = nNivel; nGraba = 1; |FINSI;
   aCtaAnterior = #33#11; |HAZ LeoTabla; |SI nCambia = 1; #33#11 = aCtaNueva; #33#12 = nNivel; nGraba = 1; |FINSI;
   |SI nGraba = 1;
       |GRABA 020#33a
   |FINSI;
   |LIBERA #33;
|FPRC;

|DEFBUCLE cacoivli;
   #33#1;
   ;
   " ", 01;
   "z", 99;
   be;
   NULL, cacoivli1;
|FIN;

|| -------------- Registros de IVA repercutido Introduccion
|PROCESO cacuivli1;
   mens = " Control de Cuentas de IVA> " + #32#0 + "-" + #32#1; #0#1 = mens; |PINTA #0#1;
   nGraba = 0;
   aCtaAnterior = #32#3;  |HAZ LeoTabla; |SI nCambia = 1; #32#3 = aCtaNueva; #32#4 = nNivel; nGraba = 1; |FINSI;
   aCtaAnterior = #32#6;  |HAZ LeoTabla; |SI nCambia = 1; #32#6 = aCtaNueva; #32#7 = nNivel; nGraba = 1; |FINSI;
   |SI nGraba = 1;
       |GRABA 020#32a
   |FINSI;
   |LIBERA #32;
|FPRC;

|DEFBUCLE cacuivli;
   #32#1;
   ;
   " ", 01;
   "z", 99;
   be;
   NULL, cacuivli1;
|FIN;

|| -------------- Registros de IVA repercutido Introduccion
|PROCESO caiivrel1;
   mens = " Introduccion IVA Repercutido> " + #28#0 + "-" + #28#1; #0#1 = mens; |PINTA #0#1;
   nGraba = 0;
   aCtaAnterior = #47#5;  |HAZ LeoTabla; |SI nCambia = 1; #47#5  = aCtaNueva; #47#6  = nNivel; nGraba = 1; |FINSI;
   aCtaAnterior = #47#17; |HAZ LeoTabla; |SI nCambia = 1; #47#17 = aCtaNueva; #47#18 = nNivel; nGraba = 1; |FINSI;
   aCtaAnterior = #47#20; |HAZ LeoTabla; |SI nCambia = 1; #47#20 = aCtaNueva; #47#21 = nNivel; nGraba = 1; |FINSI;
   aCtaAnterior = #47#22; |HAZ LeoTabla; |SI nCambia = 1; #47#22 = aCtaNueva; #47#23 = nNivel; nGraba = 1; |FINSI;
   aCtaAnterior = #47#24; |HAZ LeoTabla; |SI nCambia = 1; #47#24 = aCtaNueva; #47#25 = nNivel; nGraba = 1; |FINSI;
   |SI nGraba = 1;
       |GRABA 020#47a
   |FINSI;
   |LIBERA #47;
|FPRC;

|DEFBUCLE caiivrel;
   #47#1;
   4;
   00, 001, fDFecha;
   99, 999, fHFecha;
   be;
   NULL, caiivrel1;
|FIN;


|| -------------- Registros de IVA soportado Introduccion
|PROCESO caiivsol1;
   mens = " Introduccion IVA Repercutido> " + #28#0 + "-" + #28#1; #0#1 = mens; |PINTA #0#1;
   nGraba = 0;
   aCtaAnterior = #28#5;  |HAZ LeoTabla; |SI nCambia = 1; #28#5 = aCtaNueva;  #28#6  = nNivel; nGraba = 1; |FINSI;
   aCtaAnterior = #28#17; |HAZ LeoTabla; |SI nCambia = 1; #28#17 = aCtaNueva; #28#18 = nNivel; nGraba = 1; |FINSI;
   aCtaAnterior = #28#20; |HAZ LeoTabla; |SI nCambia = 1; #28#20 = aCtaNueva; #28#21 = nNivel; nGraba = 1; |FINSI;
   aCtaAnterior = #28#22; |HAZ LeoTabla; |SI nCambia = 1; #28#22 = aCtaNueva; #28#23 = nNivel; nGraba = 1; |FINSI;
   aCtaAnterior = #28#24; |HAZ LeoTabla; |SI nCambia = 1; #28#24 = aCtaNueva; #28#25 = nNivel; nGraba = 1; |FINSI;
   |SI nGraba = 1;
       |GRABA 020#28a;
   |FINSI;
   |LIBERA #28;
|FPRC;

|DEFBUCLE caiivsol;
   #28#1;
   4;
   00, 001, fDFecha;
   99, 999, fHFecha;
   be;
   NULL, caiivsol1;
|FIN;

|| -------------- Registros de IVA soportado
|PROCESO caivasop1;
   mens = " Registros IVA Repercutido> " + #29#0 + "-" + #29#27 + "-" + #29#2; #0#1 = mens; |PINTA #0#1;
   nGraba = 0;
   aCtaAnterior = #29#4;
   |HAZ LeoTabla;
   |SI nCambia = 1; #29#4 = aCtaNueva; #29#5 = nNivel; nGraba = 1; |FINSI;
   |SI nGraba = 1;
       |GRABA 020#29a;
   |FINSI;
   |LIBERA #29;
|FPRC;

|DEFBUCLE caivasop;
   #29#1;
   3;
   00, "  ", 000001, fDFecha;
   99, "zz", 999999, fHFecha;
   be;
   NULL, caivasop1;
|FIN;

|| -------------- Registros de IVA repercutido
|PROCESO caivarep1;
   mens = " Registros IVA Repercutido> " + #31#0 + "-" + #31#27 + "-" + #31#2; #0#1 = mens; |PINTA #0#1;
   nGraba = 0;
   aCtaAnterior = #31#4;
   |HAZ LeoTabla;
   |SI nCambia = 1; #31#4 = aCtaNueva; #31#5 = nNivel; nGraba = 1; |FINSI;
   |SI nGraba = 1;
       |GRABA 020#31a;
   |FINSI;
   |LIBERA #31;
|FPRC;

|DEFBUCLE caivarep;
   #31#1;
   3;
   00, "  ", 000001, fDFecha;
   99, "zz", 999999, fHFecha;
   be;
   NULL, caivarep1;
|FIN;

|| -------------- Lineas Asientos contrapartida
|PROCESO calicont1;
   mens = " Contrapartidas Apuntes Lins.> " + #67#0 + "-" + #67#1 + "_" + #67#3 + "_" + #67#4; #0#1 = mens; |PINTA #0#1;

   aCtaAnterior = #67#4;
   |HAZ LeoTabla;
   |SI nCambia = 1;
       #67#4  = aCtaNueva;
       #67#5  = nNivel;
       |GRABA 020#67a;
   |FINSI;
   |LIBERA #67;
|FPRC;

|DEFBUCLE calicont;
   #67#1;
   ;
   00, fDFecha,      0, 0001;
   99, fHFecha, 999999, 9999;
   be;
   NULL, calicont1;
|FIN;
|| -------------- Lineas Asientos
|PROCESO camaasil1;
   mens = " Apuntes Lins.> " + #26#0 + "-" + #26#1 + "_" + #26#3 + "_" + #26#4; #0#1 = mens; |PINTA #0#1;

   aCtaAnterior = #26#4;
   |HAZ LeoTabla;
   |SI nCambia = 1;
       #26#4 = aCtaNueva;
       #26#5 = nNivel;
       |SI #26#8 = "H";
           #26#10 = #26#4;
           #26#11 = #26#5;
       |SINO;
           #26#16 = #26#4;
           #26#17 = #26#5;
       |FINSI;
       |GRABA 020#26a;
       nRecalculo = 1;
   |FINSI;
   |LIBERA #26;
|FPRC;

|DEFBUCLE camaasil;
   #26#1;
   ;
   00, fDFecha,      0, 0001;
   99, fHFecha, 999999, 9999;
   be;
   NULL, camaasil1;
|FIN;

|| -------------- Cabecera Asientos
|PROCESO camaasic1;
   mens = " Apuntes Cabs.> " + #25#0 + "-" + #25#1 + "_" + #25#3; #0#1 = mens; |PINTA #0#1;

   nGraba = 0;
   aCtaAnterior = #25#8;
   |HAZ LeoTabla;
   |SI nCambia = 1; #25#8  = aCtaNueva; #25#9  = nNivel; nGraba = 1; |FINSI;

   aCtaAnterior = #25#10;
   |HAZ LeoTabla;
   |SI nCambia = 1; #25#10 = aCtaNueva; #25#11 = nNivel; nGraba = 1; |FINSI;

   |SI nGraba = 1;
       |GRABA 020#25a;
   |FINSI;
   |LIBERA #25;
|FPRC;

|DEFBUCLE camaasic;
   #25#1;
   ;
   00, fDFecha,      0;
   99, fHFecha, 999999;
   be;
   NULL, camaasic1;
|FIN;

|| -------------- Bancos
|PROCESO camanban1;
  mens = " Bancos> " + #24#0;   #0#1 = mens;   |PINTA #0#1;
  aCtaAnterior = #24#10;
  |HAZ LeoTabla;
  |SI nCambia = 1;
      #24#10 = aCtaNueva;
      #24#11 = nNivel;
      |GRABA 020#24a;
  |FINSI;
  |LIBERA #24;
|FPRC;

|DEFBUCLE camanban;
 #24#1;
 ;
   1;
 999;
 be;
 NULL, camanban1;
|FIN;

|| -------------- Linea Asientos Predefinidos
|PROCESO casieali1;
  mens = " Predefinidos> " + #21#0;   #0#1 = mens;   |PINTA #0#1;
  aCtaAnterior = #21#3;
  |HAZ LeoTabla;
  |SI nCambia = 1;
      #21#3 = aCtaNueva;
      #21#4 = nNivel;
      |GRABA 020#21a;
  |FINSI;
  |LIBERA #21;
|FPRC;

|DEFBUCLE casieali;
 #21#1;
 ;
    1,  1;
 9999, 99;
 be;
 NULL, casieali1;
|FIN;

|| ************************************************************************
|PROCESO LeoTabla;
 aCtaNueva = aCtaAnterior;
 nCambia = 0;

 aAlfa1 = aCtaAnterior; |QBF aAlfa1;
 |SI aAlfa1 = ""; |O aAlfa1 = "PDTE"; |ACABA; |FINSI;

 |ABRE #405;
 |DDEFECTO #405;
 |SELECT #2#405;
 #405#2 = aCtaAnterior;
 #405#0 = "";
 #405#8 = 1;
 |LEE 001#405];
 |SI FSalida = 0; |Y #405#2 = aCtaAnterior;
      aCtaNueva = #405#4;
 |FINSI;
 |SELECT #1#405;
 |CIERRA #405;

 |SI aCtaAnterior ! aCtaNueva; |Y #405#7 = "S";
     aAlfa1 = aCtaNueva; |QBF aAlfa1;
     |SI aAlfa1 ! ""; |Y aAlfa1 ! "PDTE";
         nCambia = 1;
         |HAZ SacaNivelN;
     |FINSI;
 |FINSI;
|FPRC;

|PROCESO SacaNivelN;
   aLong = aCtaNueva; |QBF aLong;
   aLong = aLong % 0;
   nLong = aLong;
   nNivel = dig3;
   |SI nLong = dig4; |Y dig4 ! 0; nNivel = 1; |FINSI;
   |SI nLong = dig5; |Y dig5 ! 0; nNivel = 2; |FINSI;
   |SI nLong = dig6; |Y dig6 ! 0; nNivel = 3; |FINSI;
   |SI nLong = dig7; |Y dig7 ! 0; nNivel = 4; |FINSI;
   |SI nLong = dig8; |Y dig8 ! 0; nNivel = 5; |FINSI;
   |SI nLong = dig9; |Y dig9 ! 0; nNivel = 6; |FINSI;
|FPRC;

|PROCESO LeoTabla_Pr;  ||por la Cuenta del A¤o pasado
 nCambia = 0;
 |ABRE #405;
 |DDEFECTO #405;
 #405#0 = eaCuenta;
 #405#8 = 1;
 |LEE 001#405];
 |SI FSalida = 0; |Y #405#0 = eaCuenta;
      |SI #405#7 = "S";
          aCtaNueva = #405#4;
          nCambia   = 1;
      |FINSI;
 |FINSI;
 |CIERRA #405;
 aAlfa1 = aCtaNueva; |QBF aAlfa1;
 |SI aAlfa1 = ""; |O aAlfa1 = "PDTE";
     nCambia = 0;
 |SINO;
     |HAZ SacaNivelN;
 |FINSI;
|FPRC;

|PROCESO tipo3;

   |BUCLE casieali;
   |BUCLE camanban;
   |BUCLE camaasic;
   |BUCLE camaasil;
   |BUCLE calicont;
   |BUCLE caivarep;
   |BUCLE caivasop;
   |BUCLE caiivsol;  || intr. fras. soportado
   |BUCLE caiivrel;  || intr. fras. repercutido
   |BUCLE cacuivli;  || ctas. iva lins.
   |BUCLE cacoivli;  || ctas. contrapartidas iva lins.
   |HAZ caivaasi;    ||
   |BUCLE camancob;
   |BUCLE camanpag;
   |BUCLE caremesc;
   |BUCLE capagcab;
   |BUCLE caexistc;
   |BUCLE caexistl;
   |BUCLE camautol;
   |BUCLE cagruiva;
   |BUCLE camaylin;
   |BUCLE capasven;
   |BUCLE cavengca;

   |HAZ cacup300;
   |HAZ cacup130;
   |HAZ cacup202;
   |HAZ cacupimp;

   |BUCLE camocabe;
   |BUCLE camogrup;

   || ... Falta caconcic y caconcil / camoauxc y camoauxl / cacuepre

   |HAZ caclipro;
   |HAZ cacuenta;

   |BUCLE ca8m0003;
   |BUCLE ca8m0004;

   |DELINDEX #405;
|FPRC;

|| *******************************************************************
|PROGRAMA;

   |PINPA #0#0;

   |QBF fich_alta;
   |SI fich_alta = "";
       |DFICE fich_alta;
   |FINSI;

   fDFecha = fec1;
   fHFecha = fec2;

   |PATH_DAT #1   fich_alta;
   |PATH_DAT #5   fich_alta;
   |PATH_DAT #21  fich_alta;
   |PATH_DAT #24  fich_alta;
   |PATH_DAT #25  fich_alta;
   |PATH_DAT #26  fich_alta;
   |PATH_DAT #28  fich_alta;
   |PATH_DAT #29  fich_alta;
   |PATH_DAT #31  fich_alta;
   |PATH_DAT #32  fich_alta;
   |PATH_DAT #33  fich_alta;
   |PATH_DAT #34  fich_alta;
   |PATH_DAT #35  fich_alta;
   |PATH_DAT #36  fich_alta;
   |PATH_DAT #37  fich_alta;
   |PATH_DAT #38  fich_alta;
   |PATH_DAT #39  fich_alta;
   |PATH_DAT #40  fich_alta;
   |PATH_DAT #41  fich_alta;
   |PATH_DAT #42  fich_alta;
   |PATH_DAT #43  fich_alta;
   |PATH_DAT #44  fich_alta;
   |PATH_DAT #45  fich_alta;
   |PATH_DAT #46  fich_alta;
   |PATH_DAT #47  fich_alta;
   |PATH_DAT #49  fich_alta;

   |PATH_DAT #50  fich_alta;
   |PATH_DAT #51  fich_alta;
   |PATH_DAT #52  fich_alta;

   |PATH_DAT #61  fich_alta;
   |PATH_DAT #62  fich_alta;
   |PATH_DAT #63  fich_alta;

   |PATH_DAT #64  fich_alta;
   |PATH_DAT #65  fich_alta;
   |PATH_DAT #66  fich_alta;
   |PATH_DAT #67  fich_alta;
   |PATH_DAT #69  fich_alta;
   |PATH_DAT #70  fich_alta;

   |PATH_DAT #403  fich_alta;
   |PATH_DAT #404  fich_alta;
   |PATH_DAT #405  fich_alta;

   |ABRE #405;
   |LEE 000#405p;
   |SI FSalida ! 0;
       |CIERRA #405;
       |ACABA;  ||no hay cambios
   |FINSI;
   |CIERRA #405;
   |HAZ tipo3;
|FPRO;
