|FICHEROS;
    carefcon #0;    || def entrada
    canempre #30;   || empresa

    cacuent@ #2;    || cuentas fuentes
    cacuenta #3;    || cuentas destino

    camaasic #4;    || apuntes cabs. fuentes
    camaasil #5;    || apuntes lins. fuentes
    camaasic #6;    || apuntes cabs. destino
    camaasil #7;    || apuntes lins. destino
    careftmp #8;    || temporal seleccion apuntes
|FIN;

|VARIABLES;
     aPath = "";
     aAlfa2 = "";
    &entrada = 1;
    empre_p  = "";
    fich_dir = "";
    x = 0;
    men1 = "     No existe empresa !!";
    cuenta = "";
    empalf = "";
    ejealf = "";
    emprealf = "";
    fantasma = 0;
    swemp = 0;
    swemp2 = 0;
    emp = 0;
    eje = 0;
    num = 0;
    sw1 = 0;
    sw2 = 0;
    valor = 0;
    valor2 = 0;
    tipcam = 0;
    varemp = 0;
{-1}val = 0;
    val1 = 0;
    val2 = 0;
    val3 = 0;
    val4 = 0;
    val5 = 0;
    val6 = 0;
    val7 = 0;
    val8 = 0;
    val9 = 0;
    val10 = 0;
    val11 = 0;
    val12 = 0;
    val13 = 0;
    val14 = 0;
    val15 = 0;
    val16 = 0;
    val17 = 0;
    val18 = 0;
    val19 = 0;
    val20 = 0;
    val21 = 0;
    val22 = 0;
    val23 = 0;
    val24 = 0;
    val25 = 0;
    val26 = 0;
    val27 = 0;
    val28 = 0;
    val29 = 0;
    val30 = 0;
    val31 = 0;
    val32 = 0;
    val33 = 0;
    val34 = 0;
    val35 = 0;
    val36 = 0;
    val37 = 0;
    val38 = 0;
    val39 = 0;
    val40 = 0;
    val41 = 0;
    val42 = 0;
    val43 = 0;
    val44 = 0;
    val45 = 0;
    nume1 = 0;
    nume2 = 0;
    nume3 = 0;
    nume4 = 0;
    alfa1 = "";
    alfa2 = "";
    alfa3 = "";
    alfa4 = "";
    cabs = 0;
    lins = 0;
    diari = 0;
    diari2 = 0;
    fecha = @;
    docum = 0;
    apunt = 0;
    veces = 0;
    emp_act = 0;
    eje_act = 0;
    inkey = 0;
    mensa00 = "    ATENCION!!! Asiento de Apertura ya Existente ...";
    mensa99 = "    ATENCION!!! Asiento de Cierre ya Existente ...";
    &r_emp = 0;
    &r_per = 0;
    &varcamp = 0;
|FIN;

|INCLUYE actudi_i;
|INCLUYE acceso_i;
****************************************************************************
                    CALCULOS DESDE CAMPOS
****************************************************************************
|PROCESO emp_actual; |TIPO 8;
  |HAZ inicio;
  |DFICE alfa1;
  alfa2 = alfa1 % -107;
  alfa2 = alfa2 % 106;
  alfa3 = alfa2 % 105;   emp_act = alfa3;            || empresa
  alfa3 = alfa2 %-101;   eje_act = alfa3;            || ejercicio
|FPRC;

|PROCESO consulta;
  |SI FSalida ! 9;  |ACABA;  |FINSI;
  r_emp = 0;
  |SUB_EJECUTA "caconemp.run";
  |ERROR;
  |SI r_emp = 0; |ACABA; |FINSI;
      #0Campo = r_emp;
      varcamp = Campo + 1;
      #0varcamp = r_per;
      |PTEC 802;
      |PTEC 802;
|FPRC;

|PROCESO empresa;|TIPO 0;
  inkey = FSalida;
  emp = Campo - 1;
  |SI #0emp ! 0;|Y inkey ! 2;
      |SI #0emp ! emp_act;|O #0Campo ! eje_act;
          |ABRE #30;
          #30#2 = #0emp;
          #30#3 = #0Campo;
          |LEE 040#30=;
          |SI FSalida ! 0;
              |MENAV men1;
              |ERROR;
          |SINO;
              alfa1 = #30#1 % 119;
              |ATRI I;
              |SI emp = 0;  |PINTA 1121, alfa1;  |FINSI;
              |SI emp = 2;  |PINTA 1221, alfa1;  |FINSI;
              |SI emp = 4;  |PINTA 1321, alfa1;  |FINSI;
              |SI emp = 6;  |PINTA 1421, alfa1;  |FINSI;
              |SI emp = 8;  |PINTA 1521, alfa1;  |FINSI;
              |SI emp = 19; |PINTA 1621, alfa1;  |FINSI;
              |SI emp = 21; |PINTA 1721, alfa1;  |FINSI;
              |SI emp = 23; |PINTA 1821, alfa1;  |FINSI;
              |SI emp = 25; |PINTA 1921, alfa1;  |FINSI;
              |SI emp = 27; |PINTA 2021, alfa1;  |FINSI;
              |ATRI 0;
          |FINSI;
          |CIERRA #30;
      |SINO;
          |MENAV "    ATENCION!!! Esta es la Empresa Actual ...";
          |ERROR;
      |FINSI;
  |FINSI;
|FPRC;
|| ******************************************************************

|PROCESO caacumul;
 |PDEFECTO #3,9,54;
 #3#58 ="01.01.1900";

 |GRABA 020#3a;
 |LIBERA #3;
|FPRC;

|DEFBUCLE caacumul;
 #3#1;
 ;
 "            ", 0;
 "zzzzzzzzzzzz", 9;
 be;
 NULL, caacumul;
|FIN;

|PROCESO fusion; |TIPO 3;

 |SI #0#18 = "S";
     |BUCLE caacumul;
 |FINSI;

 |HAZ cuentas;

 |SI #0#10 = "S";
     |HAZ apuntes;
 |FINSI;

|FPRC;

****************************************************************************
                    GRABACION DE ASIENTOS
****************************************************************************

|PROCESO apu_selec;
    empalf = "00000" + #0emp;    empalf = empalf % -105;
    ejealf = "0" + #0eje;      ejealf = ejealf % -101;
    emprealf = empalf + ejealf;
|DBASE fich_dir;
    fich_dir = fich_dir + "fich/";
    fich_dir = fich_dir + emprealf + "/";

|PATH_DAT #5 fich_dir;
|ABRE #5;
    #5#0 = diari;
    #5#1 = fecha;
    #5#2 = docum;
|SI sw2 = 0;
        sw2 = 1;
        #5#3 = 0;
    |LEE 001#5];
    |HAZ apu_carga;
|SINO;
        #5#0 = diari;
        #5#1 = fecha;
        #5#2 = docum;
        #5#3 = apunt;
    |LEE 001#5=;
    |LEE 001#5s;
    |HAZ apu_carga;
|FINSI;
|CIERRA #5;
|FPRC;

|PROCESO apu_carga;
|SI FSalida = 0; |Y #5#0 = diari; |Y #5#1 = fecha; |Y #5#2 = docum;
                 |Y #5#6 ] #0#16; |Y #5#6 [ #0#17;
    |DIMENSIONA 50, lins, 0;
    |PARA nume1 = 0; |SI nume1 [ 24; |HACIENDO nume1 = nume1 + 1;
            alfa1 = #5nume1;
            nume2 = nume1 + 1;
        |AL_BUF lins, nume2, alfa1;
    |SIGUE;
        apunt = #5#3;
|SINO;
        swemp2 = 1;
|FINSI;
|FPRC;

|PROCESO apu_alta;
|PARA tipcam = 0;|SI tipcam [ 24;|HACIENDO tipcam = tipcam + 1;
         nume2 = tipcam + 1;
    |DEL_BUF lins, nume2, alfa1;
        #7tipcam = alfa1;
|SIGUE;
    #7#0 = diari2;
    #7#1 = fecha;
    #7#2 = docum;
    #7#3 = apunt;
|GRABA 020#7n;
    d_diari = #7#0;
    d_opera = #7#8;
    d_impor = #7#9;
|HAZ actu_diari;
|FPRC;

|PROCESO asi_selec;
    empalf = "00000" + #0emp;    empalf = empalf % -105;
    ejealf = "0" + #0eje;      ejealf = ejealf % -101;
    emprealf = empalf + ejealf;
|DBASE fich_dir;
    fich_dir = fich_dir + "fich/";
    fich_dir = fich_dir + emprealf + "/";

|PATH_DAT #4 fich_dir;       || empresa seleccionada
|ABRE #4;
|DDEFECTO #4;
|SI sw1 = 0;
        sw1 = 1;
    |LEE 001#4p;
    |HAZ asi_carga;
|SINO;
        #4#0 = diari;
        #4#1 = fecha;
        #4#2 = docum;
    |LEE 001#4=;
    |LEE 001#4s;
    |HAZ asi_carga;
|FINSI;
|CIERRA #4;
|FPRC;

|PROCESO asi_carga;
|SI FSalida = 0;
    |DIMENSIONA 15, cabs, 0;
    |PARA nume1 = 0; |SI nume1 [ 11; |HACIENDO nume1 = nume1 + 1;
            alfa1 = #4nume1;
            nume2 = nume1 + 1;
        |AL_BUF cabs, nume2, alfa1;
    |SIGUE;
        diari = #4#0;
        fecha = #4#1;
        docum = #4#2;
|SINO;
        swemp = 1;
|FINSI;
|FPRC;

|PROCESO asi_alta;
|PARA tipcam = 0;|SI tipcam [ 11;|HACIENDO tipcam = tipcam + 1;
         nume2 = tipcam + 1;
    |DEL_BUF cabs, nume2, alfa1;
        #6tipcam = alfa1;
|SIGUE;
    #6#0 = diari2;
    #6#1 = fecha;
    #6#2 = docum;
|GRABA 020#6n;
|HAZ apu_selec;
|SI swemp2 = 1;
    |BORRA 020#6c;
|FINSI;
|PARA; |SI swemp2 = 0; |HACIENDO;
    |DFICE empre_p;
    |PATH_DAT #7 empre_p;
    |ABRE #7;
        #7#0 = diari2;
        #7#1 = fecha;
        #7#2 = docum;
        #7#3 = apunt;
    |LEE 001#7=;
    |SI FSalida ! 0;
        |HAZ apu_alta;
    |FINSI;
    |CIERRA #7;
    |FINDIM lins;
        #0#12 = diari2;  |PINTA #0#12;
        #0#13 = fecha;   |PINTA #0#13;
        #0#14 = docum;   |PINTA #0#14;
        #0#15 = apunt;   |PINTA #0#15;
    |HAZ apu_selec;
|SIGUE;
|FINDIM cabs;
    sw2 = 0;
    swemp2 = 0;
|FPRC;

|PROCESO busca_libre;
    FSalida = 0;
    veces = 0;
|PARA; |SI FSalida = 0; |HACIENDO;
        veces = veces + 1;
        #6#0 = #6#0 + 1;
    |SI #6#0 = 98;  #6#0 = 1;  |FINSI;
    |LEE 001#6=;
    |SI veces > 98;  |SAL;  |FINSI;
|SIGUE;
|SI veces > 98;
    |MENAV "    ATENCION!!! Todos los diarios ocupados ...";
|FINSI;
|FPRC;

|PROCESO apuntes;
 |PARA eje = 1; |SI eje [ 28; |HACIENDO eje = eje + 2;
       emp = eje - 1;
    |SI #0emp ! 0;
        |HAZ asi_selec;
        |PARA; |SI swemp = 0; |HACIENDO;
            |DFICE empre_p;
            |PATH_DAT #6 empre_p;
            |ABRE #6;
                #6#0 = diari;
                #6#1 = fecha;
                #6#2 = docum;
            |LEE 001#6=;
            |SI FSalida = 0;
                |SI #6#0 ! 99;|Y #6#0 ! 0;
                    |HAZ busca_libre;
                    |SI veces [ 98;              || si hay diarios libres
                            diari2 = #6#0;
                        |HAZ asi_alta;
                    |FINSI;
                |SINO;
                    |SI #6#0 = 00;  |MENAV mensa00;  |FINSI;
                    |SI #6#0 = 99;  |MENAV mensa99;  |FINSI;
                |FINSI;
            |SINO;
                    diari2 = diari;
                |HAZ asi_alta;
            |FINSI;
            |CIERRA #6;
            |HAZ asi_selec;
        |SIGUE;
    |FINSI;
    |SI eje = 9; eje = 18; |FINSI;
        sw1 = 0;
        swemp = 0;
|SIGUE;

|FINDIM cabs;       || por si se han quedado dimensionadas
|FINDIM lins;
|FPRC;

****************************************************************************
                    GRABACION DEL PLAN CONTABLE
****************************************************************************

|PROCESO emp_selec;
  empalf = "00000" + #0emp;
  empalf = empalf % -105;

  ejealf = "0" + #0eje;
  ejealf = ejealf % -101;

  emprealf = empalf + ejealf;

  |DBASE fich_dir;
  fich_dir = fich_dir + "fich/";
  fich_dir = fich_dir + emprealf + "/";

  |PATH_DAT #2 fich_dir;        || empresa seleccionada
|FPRC;

|PROCESO LeeOrigen;
 #3#0 = #2#0;
 #3#1 = #2#1;
 |LEE 101#3=;
 |SI FSalida ! 0;
     |PARA valor2 = 0; |SI valor2 [ 59; |HACIENDO valor2 = valor2 + 1;
           #3valor2 = #2valor2;
     |SIGUE;
     |GRABA 020#3c;
 |SINO;
      |PARA valor2 = 9;|SI valor2 [ 53;|HACIENDO valor2 = valor2 + 1;
           #3valor2 = #3valor2 + #2valor2;
      |SIGUE;
      |SI #2#58 > #3#58; #3#58 = #2#58; |FINSI;
      |GRABA 020#3a;
 |FINSI;
 |LIBERA #3;
|FPRC;

|DEFBUCLE LeeOrigen;
 #2#1002;
 ;
 "            ", 0;
 "zzzzzzzzzzzz", 9;
 e;
 NULL, LeeOrigen;
|FIN;

|PROCESO cuentas;

   |DBASE aPath;

   alfa2 = aPath + "def/cacuenta.mas";
   |CARGA_DEF alfa2, cacuent@;
   |SI FSalida ! 0;
       |MENAV "    Error al cargar Cuentas";
       |ACABA;
   |FINSI;

   |PARA eje = 1; |SI eje [ 28; |HACIENDO eje = eje + 2;
         emp = eje - 1;
         |SI #0emp ! 0;
             |HAZ emp_selec;
             |ABRE #3;
             |BUCLE LeeOrigen;
             |CIERRA #3;
         |FINSI;
         |SI eje = 9; eje = 18; |FINSI;
   |SIGUE;
   |DESCARGA_DEF #cacuent@;
|FPRC;

|PROCESO alta;
  #3#0  = cuenta;
  #3#1  = fantasma;
  #3#2  = #2#2;   #3#3  = #2#3;   #3#4  = #2#4;   #3#5  = #2#5;   #3#6  = #2#6;
  #3#7  = #2#7;   #3#8  = #2#8;   #3#9  = #2#9;   #3#10 = #2#10;  #3#11 = #2#11;
  #3#12 = #2#12;  #3#13 = #2#13;  #3#14 = #2#14;  #3#15 = #2#15;  #3#16 = #2#16;
  #3#17 = #2#17;  #3#18 = #2#18;  #3#19 = #2#19;  #3#20 = #2#20;  #3#21 = #2#21;
  #3#22 = #2#22;  #3#23 = #2#23;  #3#24 = #2#24;  #3#25 = #2#25;  #3#26 = #2#26;
  #3#27 = #2#27;  #3#28 = #2#28;  #3#29 = #2#29;  #3#30 = #2#30;  #3#31 = #2#31;
  #3#32 = #2#32;  #3#33 = #2#33;  #3#34 = #2#34;  #3#35 = #2#35;  #3#36 = #2#36;
  #3#37 = #2#37;  #3#38 = #2#38;  #3#39 = #2#39;  #3#40 = #2#40;  #3#41 = #2#41;
  #3#42 = #2#42;  #3#43 = #2#43;  #3#44 = #2#44;  #3#45 = #2#45;  #3#46 = #2#46;
  #3#47 = #2#47;  #3#48 = #2#48;  #3#49 = #2#49;  #3#50 = #2#50;  #3#51 = #2#51;
  #3#52 = #2#52;  #3#53 = #2#53;  #3#54 = #2#54;  #3#55 = #2#55;  #3#56 = #2#56;
  #3#57 = #2#57;  #3#58 = #2#58;
  |GRABA 020#3n;
  |HAZ cero;
|FPRC;

|| En acumulados utilizamos el puntero porque sino los campos
|| del #2 toman valor cero al leer el fichero #3;

|PROCESO acumulados;
    Ival = val1 <-;
|PARA tipcam = 9;|SI tipcam [ 53;|HACIENDO tipcam = tipcam + 1;
        #3tipcam = #3tipcam + val;
        Ival = Ival + 1;|| Sumar uno al indice del puntero
|SIGUE;
|SI #3#58 < fecha;
        #3#58 = fecha;
|FINSI;
|GRABA 020#3a;
|HAZ cero;
|FPRC;

|PROCESO cero;
    Ival = val1 <-;
|PARA x = 9;|SI x < 53;|HACIENDO x = x + 1;
        val = 0;
        Ival = Ival + 1;
|SIGUE;
    Ival = val1 <-;
|FPRC;
