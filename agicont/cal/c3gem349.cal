|FICHEROS;
  c3gem349 #0;
  camac349 #1;
  camal349 #5;
  caportad #7;
  canempre #30;
|FIN;

|VARIABLES;
    tecla = "";
    t1 = "807";
    sw_imp   = 0;
    x        = 0;
    y        = 0;
    n        = "N";
    informe  = "";
    desde    = 0;
    hasta    = 0;
    contador1= 0;
    contador2= 0;
    contador3= 0;
    contador4= 0;
    entrega  = 0;
    adquisi  = 0;
    rectent  = 0;
    rectadq  = 0;
    linea1   = "";
    linea2   = "";
    linea3   = "";
    linea4   = "";
    resto    = 0;
    pp       = 0;
    tipo     = "";
    hojas    = 0;
    periodo  = 0;
    sw_fisica = 0;
    vacio_20  = "                    ";

    &numhoja = 0;
    &hoja    = 0;
    &dia     = "";
    &mes     = "";
    &anyo    = "";
    &fecbo   = "";
    &tipo_p  = "";
    &nombrep = "";
    nifrep = "";
    &preimp = "";
    &predis = "";
    &predi1 = "";
    &predi2 = "";
    &full = 0;
    &t_clave = "";
    &cargoem = "";
    &enEjercicio = 0;

  swsalir = 0;
  {-1}opcion  = "";
      opcion1 = "1546";
      opcion2 = "1";
      opcion3 = "7";
      opcion4 = "Portada                      ";
      opcion5 = "Hojas de Entregas            ";
      opcion6 = "Hojas de Adquisiciones       ";
      opcion7 = "Hojas de Rect. Entregas      ";
      opcion8 = "Hojas de Rect. Adquisiciones ";
      opcion9 = "-----------------------------";
      opcion10 = "En Papel en Blanco          ";
   paso = 0;
   seleccion = 0;

       any    = 0;
       empcon = 0;
       ejecon = 0;
       leidos = 0;
       dirfich0 = "";
       emp1 = 0;
       per1 = 0;
       xx = 0;
       yy = 0;
       anu = 0;
       varcamp = 0;
       &r_emp = 0;
       &r_per = 0;
       alfa1 = "";
       alfa2 = "";

       men9 = "     Empresa Contable INACTIVA ";
|FIN;

|INCLUYE i_cuadre;
|INCLUYE acceso_i;

|| *************************************************************************
||                        CALCULOS DE PANTALLA
|| *************************************************************************

|PROCESO alentrar;  |TIPO 8;
  |HAZ inicio;
|FPRC;

|PROCESO pinta_anyo; |TIPO 7;
  #0#0 = cod2;
  |PINTA #0#0;
|FPRC;

|PROCESO consulta;
  |SI FSalida ! 9;  |ACABA;  |FINSI;
  |SUB_EJECUTA "caconemp.run";
  |ERROR;
  |SI Campo = 5;
      #0#5 = r_emp;
      #0#7 = r_per;
      |PTEC 802;
      |PTEC 802;
  |FINSI;
  |SI Campo = 6;
      #0#6 = r_emp;
      #0#8 = r_per;
      |PTEC 802;
  |FINSI;
  |SI Campo ] 9;
      #0Campo = r_emp;
      varcamp = Campo + 20;
      #0varcamp = r_per;
      |PTEC 802;
      |PTEC 802;
  |FINSI;
|FPRC;

|PROCESO mayor;
  alfa1 = #0Campo > " ";
  #0Campo = alfa1;
  |PINTA #0Campo;
|FPRC;

|PROCESO mayor1;
  |HAZ mayor;
  |SI #0Campo = "N";
      |PARA x = 9;  |SI x [ 48;  |HACIENDO x = x + 1;
            |CAMPO_MODIFICA #0x,1,"S";
      |SIGUE;
      #0#5 = 0;
      #0#7 = 0;
      #0#6 = 0;
      #0#8 = 0;
      |CAMPO_MODIFICA #0#5,1,"N";
      |CAMPO_MODIFICA #0#7,1,"N";
      |CAMPO_MODIFICA #0#6,1,"N";
      |CAMPO_MODIFICA #0#8,1,"N";
      |PINTA #0#5;
      |PINTA #0#7;
      |PINTA #0#6;
      |PINTA #0#8;
  |SINO;
      |PARA x = 9;  |SI x [ 48;  |HACIENDO x = x + 1;
            #0x = 0;
            |CAMPO_MODIFICA #0x,1,"N";
      |SIGUE;
      |CAMPO_MODIFICA #0#5,1,"S";
      |CAMPO_MODIFICA #0#7,1,"S";
      |CAMPO_MODIFICA #0#6,1,"S";
      |CAMPO_MODIFICA #0#8,1,"S";
  |FINSI;
|FPRC;

|| --------------- comprueba desde-hasta empresas ---------------------------

|PROCESO buscaem1;
  |SI #30#21 = "I"; |ACABA; |FINSI;

  |SI #30#26 ! #0#0;
      swerror = 3;
      |ACABA;
  |FINSI;

  |DBASE dir_fich;
  dirfich0 = dir_fich + "fich/";
  alfa1 = #30#2;
  alfa2 = #30#3;
  emp1 = #30#2;
  per1 = #30#3;
  alfa1 = "00000" + alfa1;   alfa1 = alfa1 % -105;
  alfa2 = "0"   + alfa2;   alfa2 = alfa2 % -101;
  dirfich0 = dirfich0 + alfa1 + alfa2 + "/";
  |PATH_DAT #1 dirfich0;
  |PATH_DAT #7 dirfich0;
  |ABRE #1;
  #1#0 = #30#2;
  #1#2 = #0#0;
  #1#1 = #0#70;
  |LEE 001#1=;
  |SI FSalida ! 0;
      swerror = 1;
      |CIERRA #1;
      |ACABA;
  |FINSI;
  |CIERRA #1;
  |ABRE #7;
  #7#0 = #30#2;
  #7#1 = #30#26;
  |LEE 001#7=;
  |SI FSalida ! 0;
      swerror = 2;
      |CIERRA #7;
      |ACABA;
  |FINSI;
  |CIERRA #7;
  leidos = 1;
|FPRC;

|DEFBUCLE buscaempre;
#30#1;
;
#0#5,#0#7;
#0#6,#0#8;
e;
NULL,buscaem1;
|FIN;

|PROCESO buclempre;
  |SI #0#7 > #0#8;
      |MENAV "     ERROR EN INTRODUCCION";
      |ERROR;
      |ACABA;
  |FINSI;
  |SI #0#5 = 0;  |Y #0#6 = 0;  |ACABA;  |FINSI;
  desde = #0#7;
  hasta = #0#8;
  leidos = 0;
  |BUCLE buscaempre;
  |SI leidos = 0;
      |MENAV "    ATENCION SELECCION VACIA";
      |ERROR;
      |ACABA;
  |FINSI;
|FPRC;

|PROCESO deshas;
  |SI #0#6 < #0#5;
      |MENAV "      Error en Introduccion";
      |ERROR;
  |FINSI;
|FPRC;

|| ------------------------------------------------------------------------

|PROCESO nomodi;
      |PARA x = Campo + 1;  |SI x [ 28;  |HACIENDO x = x + 1;
            #0x = 0;
            |CAMPO_MODIFICA #0x,1,"N";
            y = x + 20;
            #0y = 0;
            |CAMPO_MODIFICA #0y,1,"N";
      |SIGUE;
      y = Campo + 20;
      #0y = 0;
      |CAMPO_MODIFICA #0y,1,"N";
      |PINTA #0y;
|FPRC;

|PROCESO esbuena; || desde campo de empresas seleccion
  |SI FSalida = 2; |ACABA; |FINSI;
  |SI #0Campo = 0; |HAZ nomodi;  |ACABA;  |FINSI;
  |ABRE #30;
  #30#2 = #0Campo;
  #30#3 = 0;
  |LEE 011#30];
  |SI FSalida ! 0;  |O #30#2 ! #0Campo;
      |MENAV "     NO Existe la empresa Contable";
      |ERROR;
  |FINSI;
  |CIERRA #30;
|FPRC;

|PROCESO esbuena1;
  |SI FSalida = 2;|ACABA;|FINSI;
  x = Campo - 20;
  |SI #0x = 0;  |ACABA;  |FINSI;
  |ABRE #30;
  x = Campo - 20;
  #30#2 = #0x;
  #30#3 = #0Campo;
  |LEE 011#30=;
  |SI FSalida ! 0;
      |MENAV "     NO Existe Periodo en la empresa Contable";
      |CIERRA #30;
      |ERROR;
      |ACABA;
  |FINSI;
  |CIERRA #30;
  |SI #30#21 = "I";
      |MENAV men9;
      |ERROR;
      |ACABA;
  |FINSI;
  leidos = 0;
  |HAZ buscaem1;
  |SI leidos = 0;
      |SI swerror = 1;
          |MENAV "     No existe Modelo 347";
          |FINSI;
      |SI swerror = 2
          |MENAV "0000No existen datos el PORTADAS MOD. OFICIALES ";
          |FINSI;
      |SI swerror = 3;
          |MENAV "     El a¤o contable NO corresponde ";
          |FINSI;
      |ERROR;
      |ACABA;
  |FINSI;
  x = Campo + 20;
  #0x = #30#1;
  |PINTA #0x;
  x = Campo - 20;
  alfa1 = #0x;
  alfa2 = #0Campo;
|FPRC;

|| **********************************************************************
||                       CALCULOS DE IMPRESION
|| **********************************************************************

|| ---------- Seleccion DESDE / HASTA Empresa.

|PROCESO esdesd1;
  |DBASE dir_fich;
  dirfich0 = dir_fich + "fich/";
  alfa1 = #30#2;
  alfa2 = #30#3;
  emp1 = #30#2;
  per1 = #30#3;
  alfa1 = "00000" + alfa1;   alfa1 = alfa1 % -105;
  alfa2 = "0"   + alfa2;   alfa2 = alfa2 % -101;
  dirfich0 = dirfich0 + alfa1 + alfa2 + "/";

  |SI #30#26 ! #0#0; |O #30#21 = "I";
      |ACABA;
  |FINSI;

  |HAZ haztodo;
|FPRC;


|DEFBUCLE esdesde;
#30#1;
;
#0#5,#0#7;
#0#6,#0#8;
e;
NULL,esdesd1;
|FIN;


|| ---------- Seleccion en parrilla.

|PROCESO esunaauna;
      |PARA xx = 9;  |SI xx [ 28;  |HACIENDO xx = xx + 1;
            |SI #0xx = 0;  |SAL;  |FINSI;
            |DBASE dir_fich;
            dirfich0 = dir_fich + "fich/";
            yy = xx + 20;
            emp1 = #0xx;
            per1 = #0yy;
            alfa1 = #0xx;
            alfa2 = #0yy;
            alfa1 = "00000" + alfa1;   alfa1 = alfa1 % -105;
            alfa2 = "0"   + alfa2;   alfa2 = alfa2 % -101;
            dirfich0 = dirfich0 + alfa1 + alfa2 + "/";
              || lee empresa
                 |ABRE #30;
                   #30#2 = emp1;
                   #30#3 = per1;
                 |LEE 011#30=;
                 |CIERRA #30;
              |SI #30#26 ! #0#0; |O #30#21 = "I";
                 |VETE otramas;
              |FINSI;
            |HAZ haztodo;
      |ET otramas;
      |SIGUE;
|FPRC;

|| ****************************** Propio de la Emision del 349
|PROCESO borradim;
contador1 = 0;
contador2 = 0;
contador3 = 0;
contador4 = 0;
|PARA x = 0; |SI x [ 999; |HACIENDO x = x + 1;
     linea1 = "";
     linea2 = "";
     linea3 = "";
     linea4 = "";

     |AL_BUF entrega, x, linea1;
     |AL_BUF entrega, x, linea2;
     |AL_BUF entrega, x, linea3;
     |AL_BUF entrega, x, linea4;
|SIGUE;
|FPRC;

|PROCESO cuantia;
|SI #5#4 = "E";
    linea1 = #5#3;
    contador1 = contador1 + 1;
    |AL_BUF entrega, contador1, linea1;
|FINSI;
|SI #5#4 = "A";
    linea2 = #5#3;
    contador2 = contador2 + 1;
    |AL_BUF adquisi, contador2, linea2;
|FINSI;
|SI #5#4 = "R";
    linea3 = #5#3;
    contador3 = contador3 + 1;
    |AL_BUF rectent, contador3, linea3;
|FINSI;
|SI #5#4 = "X";
    linea4 = #5#3;
    contador4 = contador4 + 1;
    |AL_BUF rectadq, contador4, linea4;
|FINSI;
|FPRC;
|| **************************************************************

|PROCESO imprelin;
hasta = pp * 20;
desde = hasta - 19;
|SI tipo = "E";
    |PARA x = desde; |SI x [ hasta; |HACIENDO x = x + 1;
          |DEL_BUF entrega, x, linea1;
          #5#0 = #1#0;
          #5#1 = #1#1;
          #5#2 = #1#2;
          #5#3 = linea1;
          |LEE 040#5=;
          |SI FSalida = 0; |PRINT; |FINSI;
    |SIGUE;
|FINSI;
|SI tipo = "A";
    |PARA x = desde; |SI x [ hasta; |HACIENDO x = x + 1;
          |DEL_BUF adquisi, x, linea2;
          #5#0 = #1#0;
          #5#1 = #1#1;
          #5#2 = #1#2;
          #5#3 = linea2;
          |LEE 040#5=;
          |SI FSalida = 0; |PRINT; |FINSI;
    |SIGUE;
|FINSI;
|SI tipo = "R";
    |PARA x = desde; |SI x [ hasta; |HACIENDO x = x + 1;
          |DEL_BUF rectent, x, linea3;
          #5#0 = #1#0;
          #5#1 = #1#1;
          #5#2 = #1#2;
          #5#3 = linea3;
          |LEE 040#5=;
          |SI FSalida = 0; |PRINT; |FINSI;
    |SIGUE;
|FINSI;
|SI tipo = "X";
    |PARA x = desde; |SI x [ hasta; |HACIENDO x = x + 1;
          |DEL_BUF rectadq, x, linea4;
          #5#0 = #1#0;
          #5#1 = #1#1;
          #5#2 = #1#2;
          #5#3 = linea4;
          |LEE 040#5=;
          |SI FSalida = 0; |PRINT; |FINSI;
    |SIGUE;
|FINSI;
|FPRC;

|PROCESO miratab;
 sw_imp = 1;

 |SI tipo = "E";
     informe = "caho1349";
     |SI seleccion = 7; informe = "caeb1349"; |FINSI;
 |FINSI;
 |SI tipo = "A";
     informe = "caho2349";
     |SI seleccion = 7; informe = "caeb2349"; |FINSI;
 |FINSI;
 |SI tipo = "R";
     informe = "caho3349";
     |SI seleccion = 7; informe = "caeb3349"; |FINSI;
 |FINSI;
 |SI tipo = "X";
     informe = "caho4349";
     |SI seleccion = 7; informe = "caeb4349"; |FINSI;
 |FINSI;

 |INFOR informe;
 |SI FSalida = 0;
     |HAZ imprelin;
     |PIEINF;
     |FININF;
 |FINSI;
|FPRC;

|PROCESO impreh1;
 enEjercicio = 2000 + #0#0;
 |SI #0#0 ] 80;
     enEjercicio = 1900 + #0#0;
 |FINSI;

 anyo = enEjercicio;

 sw_imp = 0;
 informe = "capor349";
 |SI seleccion = 7; informe = "capob349"; |FINSI;
 |INFOR informe;
 |SI FSalida = 0;
     |PRINT;
     |PIEINF;
     |FININF;
 |FINSI;
|FPRC;

|PROCESO impreh2;
 hojas = contador1 / 20;
 resto = contador1 $ 20;
 |SI resto ! 0; hojas = (hojas + 0.5) ! 0; |FINSI;
 tipo = "E";
|PARA pp = 1; |SI pp [ hojas; |HACIENDO pp = pp + 1;
       numhoja = hojas;
       hoja    = pp;
      |HAZ miratab;
|SIGUE;
|FPRC;

|PROCESO impreh3;
hojas = contador2 / 20;
resto = contador2 $ 20;
|SI resto ! 0; hojas = (hojas + 0.5) ! 0; |FINSI;
tipo = "A";
|PARA pp = 1; |SI pp [ hojas; |HACIENDO pp = pp + 1;
       numhoja = hojas;
       hoja    = pp;
      |HAZ miratab;
|SIGUE;
|FPRC;

|PROCESO impreh4;
hojas = contador3 / 20;
resto = contador3 $ 20;
|SI resto ! 0; hojas = (hojas + 0.5) ! 0; |FINSI;
tipo = "R";
|PARA pp = 1; |SI pp [ hojas; |HACIENDO pp = pp + 1;
       numhoja = hojas;
       hoja    = pp;
      |HAZ miratab;
|SIGUE;
|FPRC;

|PROCESO impreh5;
 hojas = contador4 / 20;
 resto = contador4 $ 20;
 |SI resto ! 0; hojas = (hojas + 0.5) ! 0; |FINSI;
 tipo = "X";
 |PARA pp = 1; |SI pp [ hojas; |HACIENDO pp = pp + 1;
       numhoja = hojas;
       hoja    = pp;
       |HAZ miratab;
 |SIGUE;
|FPRC;

|PROCESO hojas;
 |SI seleccion = 1; |O seleccion = 7;
     tipo_p = "T";
     |SI periodo = 0; tipo_p = "A"; |FINSI;
     |SI #1#12 = "N";
         preimp = "X";
         predis = " ";
         predi1 = " ";
         predi2 = " ";
     |SINO;
         preimp = " ";
         predi2 = "X";
         |SI #1#13 = "I";
             predis = "X";
             predi1 = " ";
         |SINO;
             predis = " ";
             predi1 = "X";
         |FINSI;
     |FINSI;
     |HAZ impreh1;
 |FINSI;

 |ABRE #5;
 |SI seleccion = 2; |O seleccion = 7; |HAZ impreh2; |FINSI;
 |SI seleccion = 3; |O seleccion = 7; |HAZ impreh3; |FINSI;
 |SI seleccion = 4; |O seleccion = 7; |HAZ impreh4; |FINSI;
 |SI seleccion = 5; |O seleccion = 7; |HAZ impreh5; |FINSI;
 |CIERRA #5;
 |SI seleccion = 1;
     #1#4 = #0#3;
     |GRABA 020#1a;
     |LIBERA #1;
 |FINSI;
|FPRC;

|PROCESO creaimpresion;
   |HAZ borradim;
   |BUCLELIN 0cuantia#1;
|FPRC;

|| *********************************************************************
||                    COMIENZO con TIPO 3
|| *********************************************************************
|PROCESO impre; |TIPO 3;
 enEjercicio = 2000 + #0#0;
 |SI #0#0 ] 80;
     enEjercicio = 1900 + #0#0;
 |FINSI;

 |DIMENSIONA 1000, entrega, 0;
 |DIMENSIONA 1000, adquisi, 0;
 |DIMENSIONA 1000, rectent, 0;
 |DIMENSIONA 1000, rectadq, 0;
 |HAZ fechapre;
     any = #0#0;
 periodo = #0#70;
 nombrep = "";
 cargoem = #0#69;

|ET elegir;
  |MENUG opcion;
    opcion2 = FSalida;
  seleccion = FSalida;
  |SI seleccion < 1; |O seleccion > 7;
      |VETE fin;
  |FINSI;
  |SI seleccion = 6; |VETE elegir; |FINSI;

  |IMPRE 0;
  |SI FSalida ! 0; |ACABA; |FINSI;

  |SI #0#4 = "S";
      |BUCLE esdesde;
  |SINO;
      |HAZ esunaauna;
  |FINSI;

  |FINIMP;
  |VETE elegir;

|ET fin;
 |FINDIM entrega;
 |FINDIM adquisi;
 |FINDIM rectent;
 |FINDIM rectadq;
|FPRC;

|PROCESO haztodo;
  enEmpresa = #canempre#2;
  enPeriodo = #canempre#3;
  swOperacion = 0
  |SUB_EJECUTA "camoneda.tab";

  |PATH_DAT #1 dirfich0;
  |PATH_DAT #5 dirfich0;
  |PATH_DAT #7 dirfich0;

  anu = 1900 + #30#26;
  |SI anu [ 1970;  anu = anu + 100;  |FINSI;
  empcon = #30#1;
  ejecon = #30#26;

     |ABRE #1;
     #1#0 = #30#2;
     #1#2 = #0#0;
     #1#1 = #0#70;
     |LEE 001#1=;
     |SI FSalida ! 0; |CIERRA #1; |ACABA; |FINSI;

     |ABRE #7;
     #7#0 = #30#2;
     #7#1 = #30#26;
     |LEE 001#7=;
     |SI FSalida ! 0; |CIERRA #1; |CIERRA #7; |ACABA; |FINSI;

  |HAZ Representante;
  |HAZ creaimpresion;
  |HAZ hojas;

  |CIERRA #1;
  |CIERRA #7;
|FPRC;

|| **************************************************************************
|PROCESO fechapre;
 dia  = #0#1 % 102;
 anyo = #0#1 % -102;
 mes  = #0#1 % 402;
 |SI mes = "01"; mes = "Enero     "; |FINSI;
 |SI mes = "02"; mes = "Febrero   "; |FINSI;
 |SI mes = "03"; mes = "Marzo     "; |FINSI;
 |SI mes = "04"; mes = "Abril     "; |FINSI;
 |SI mes = "05"; mes = "Mayo      "; |FINSI;
 |SI mes = "06"; mes = "Junio     "; |FINSI;
 |SI mes = "07"; mes = "Julio     "; |FINSI;
 |SI mes = "08"; mes = "Agosto    "; |FINSI;
 |SI mes = "09"; mes = "Septiembre"; |FINSI;
 |SI mes = "10"; mes = "Octubre   "; |FINSI;
 |SI mes = "11"; mes = "Noviembre "; |FINSI;
 |SI mes = "12"; mes = "Diciembre "; |FINSI;
 fecbo = dia + " de " + mes + " de " + anyo;
|FPRC;

|PROCESO Representante;
 nombrep   = "";
 nifrep = #7#38;
 |QBF nifrep;
 |SI nifrep ! "";
     nombrep = #7#39;
 |SINO;
     |SI #7#51 ! vacio_20; |O #7#52 ! vacio_20;
         nombrep = #7#51 + #7#52;
     |SINO;
         |SI #7#56 ! vacio_20; |O #7#57 ! vacio_20;
             nombrep = #7#56 + #7#57;
         |SINO;
             |SI #7#61 ! vacio_20; |O #7#62 ! vacio_20;
                 nombrep = #7#61 + #7#62;
             |FINSI;
         |FINSI;
     |FINSI;
 |FINSI;
|FPRC;
