|FICHEROS;
  ca8m0005 #0,MANTE;
  cacuenta #1;
  ca8cumay #2;
  &cacuent@ #3;
  cacuebal #102;
  ca8m0002 #422;

  ca8z0005 #10;

  camaasil #20

  ca8comay #401;  ||Control Cuentas de mayor
  ca8eqmay #402;  ||Tabla General
  ca8m0003 #403;  ||Tabla de Esta Empresa
  ca8m0004 #404;  ||Tabla de Esta Empresa

  ca8w0002 #902;  ||Auxiliar de cuentas
|FIN;

|VARIABLES;
 &nSwQueO  = 0;
 nInkey    = 0;
 aAlfa1    = "";
 aAlfa2    = "";
 aAlfa3    = "";
 aAlfa4    = "";
 nNumero   = 0;
 nNume1    = 0;
 nNume2    = 0;
 nNume3    = 0;
 nNume4    = 0;
 nLong1    = 0;
 nLong2    = 0;
 nLongMax  = 0;
 nLongMaxT = 0;
 aDirBase  = "";
 nEstado   = 0;
 nEstado1  = 0;
 nSalir    = 0;
 aMas      = "";
 aDef3     = "";
 fFecha    = @;
 nError    = 0;
 nSwVale   = 0;
 mascara   = "";
 mesdes    = "";
 meshas    = "";
 mes       = "";

 &aPathPlan = "";
 &num_por    = 0;
 &num_per    = 0;

 &aCtaNueva  = "";
 &aNomNueva  = "";

 nSwHay      = 0;
 aParam      = "";
 &aTodos     = "";
 &aDefecto0  = "";
 &aDefecto6  = "";
 &aDefecto7  = "";
 &eDNum2008  = 0;

 aCtaAnterior = "";
 nAnterior    = 0;
 &r_emp = 0;
 &r_per = 0;
 nEstado403 = 0;
 nEjerActual = 0;
 traba = "";
 &nRecalculo = 0;

 &balance = 0;
 &pathbal = "";
 nOp      = 0;
 nYaLo    = 0;
 aFichero = "";

 &ndig3 = 0;
 &ndig4 = 0;
 &ndig5 = 0;
 &ndig6 = 0;
 &ndig7 = 0;
 &ndig8 = 0;
 &ndig9 = 0;
 nLong  = 0;
 aLong  = "";


|FIN;

|INCLUYE varnue_i;
|INCLUYE acceso_i;
|INCLUYE defect_i;
|INCLUYE copia1_i;

|| ****************************************************************
|PROCESO consulta;
   |SI FSalida ! 9;  |ACABA;  |FINSI;
   r_emp = 0;
   |SUB_EJECUTA "caconemp";
   |ERROR;
   |SI r_emp = 0; |ACABA; |FINSI;
   #10#4 = r_emp;
   #10#5 = r_per;
   |PTEC 802;
|FPRC;

|PROCESO ejercicio;
   |C_M #10Campo, 0, aAlfa1;
   |SI aAlfa1 = "N"; |ACABA; |FINSI;

   |SI FSalida = 2; |ACABA; |FINSI;
   |SI #10#4 = #10#0; |Y #10#5 = #10#1;
       |MENAV "    Error, seleccionada Empresa Actual";
       |ERROR;
       |ACABA;
   |FINSI;

   |ABRE #30;
   #30#2 = #10#4;
   #30#3 = #10#5;
   |LEE 001#30=;
   |SI FSalida ! 0;
       |MENAV "    Error. No existe Empresa Contable";
       |ERROR;
       |CIERRA #30;
       |ACABA;
   |FINSI;
   #10#6 = #30#1;
   |SI #30#26 < 80;
       #10#7 = #30#26 + 2000;
   |SINO;
       #10#7 = #30#26 + 1900;
   |FINSI;
   |PINTA #10#6;
   |PINTA #10#7;
|FPRC;

|PROCESO CodCta0; |TIPO 0;
  |ABRE #401;
  #401#0 = #10#8;
  |LEE 001#401=;
  |SI FSalida ! 0;
      |MENAV "    Error, no existe Codigo de Cuentas de Mayor";
      |ERROR;
  |SINO;
      #10#9 = #401#1; |PINTA #10#9;
  |FINSI;
  |CIERRA #401;
|FPRC;

|| ****************************************************************
|PROCESO GrabaAuxi;
 #902#0 = aCtaNueva;
 #902#1 = enNivel;
 #902#2 = #403#2;
 |GRABA 020#902n;
|FPRC;

|PROCESO CalculoNuevo;
  nNume4  = aCtaNueva;
  nSalir = 0;
  |PARA; |SI nSalir = 0; |HACIENDO;
         aCtaNueva = nNume4;
         aAlfa1    = aCtaNueva % nNume2;
         |SI aAlfa1 ! eaPlan8;
             nSalir   = 1;     || no puedo sacarla
             aCtaNueva = "PDTE";
             aNomNueva = "NO SE HA PODIDO CREAR";
             eSwTabla = 1;
         |SINO;
             |DDEFECTO #902;
             #902#0 = aCtaNueva;
             #902#1 = enNivel;
             |LEE 040#902=;
             |SI FSalida ! 0;
                 |HAZ GrabaAuxi;
                 nSalir = 2;
             |FINSI;
         |FINSI;
         nNume4 = aCtaNueva;
         nNume4 = nNume4 + 1;
   |SIGUE;
|FPRC;

|PROCESO PasoNivel;
   enNivel = 0;
   aAlfa1  = eaCuenta; |QBF aAlfa1;
   aAlfa1  = aAlfa1 % 0;
   nNume1  = aAlfa1;
   |SI nNume1 = dig4;  enNivel = 1;  |FINSI;
   |SI nNume1 = dig5;  enNivel = 2;  |FINSI;
   |SI nNume1 = dig6;  enNivel = 3;  |FINSI;
   |SI nNume1 = dig7;  enNivel = 4;  |FINSI;
   |SI nNume1 = dig8;  enNivel = 5;  |FINSI;
   |SI nNume1 = dig9;  enNivel = 6;  |FINSI;
|FPRC;

|PROCESO BuscaEquiv;
   eSwTabla = 0;

   |ABRE #402;
   |DDEFECTO #402;

   uno = eaCuenta; |QBF uno;
   dos = uno + "000000000000";

   uno = dos % A104; nLong1 = 4;
   #402#0 = eNum2008;
   #402#1 = uno;
   |LEE 040#402=;
   |SI FSalida = 0;  |HAZ Datos;  |ACABA;  |FINSI;

   uno = dos % A103; nLong1 = 3;
   #402#0 = eNum2008;
   #402#1 = uno;
   |LEE 040#402=;
   |SI FSalida = 0;  |HAZ Datos;  |ACABA;  |FINSI;

   uno = dos % A102; nLong1 = 2;
   #402#0 = eNum2008;
   #402#1 = uno;
   |LEE 040#402=;
   |SI FSalida = 0;  |HAZ Datos;  |ACABA;  |FINSI;

   uno = dos % A101; nLong1 = 1;
   #402#0 = eNum2008;
   #402#1 = uno;
   |LEE 040#402=;
   |SI FSalida = 0;
       |HAZ Datos;
   |SINO;
       aCtaNueva = "PDTE";
       aNomNueva = "No existe Tabla equivalencia";
       eSwTabla  = 2;
       |CIERRA #402;
   |FINSI;
|FPRC;

|PROCESO Datos;
   eSwTabla = 0;
   eaPlan8  = #402#3;
   |QBF eaPlan8;
   |CIERRA #402;

   aAlfa1 = eaPlan8 % 0;
   nNume1 = aAlfa1;    || Longitud del Cambio
   nNume2 = nNume1 + 100; nLong2 = nNume1;

   |SI eaPlan8 = "PDTE";
       aCtaNueva = eaPlan8;
       aNomNueva = #402#4;
       eSwTabla = 1;
       |ACABA;
   |FINSI;

   aLong = eaCuenta % 0;
   nLong = aLong;

   |SI nLong = nLongMaxT; |O nLong > 4;
       nNume3 = 0 - ((nLongMax - nLong2) + 100)
       aAlfa1 = eaCuenta     % nNume2;
       aAlfa3 = aCtaAnterior % nNume2;
       aAlfa2 = aCtaAnterior % nNume3;
       |SI #403#6 = "B";
           aAlfa3 = eaCuenta % nNume2;
           aAlfa2 = eaCuenta % nNume3;
       |FINSI;
   |SINO;
       nNume3 = 0 - ((nLongMaxT - nLong2) + 100)
       aAlfa1 = eaCuenta     % nNume2;
       aAlfa3 = aCtaAnterior % nNume2;
       aAlfa2 = "000000000000" % nNume3;
       |SI #403#6 = "B";
           aAlfa3 = eaCuenta % nNume2;
       |FINSI;
   |FINSI;

   |SI aAlfa1 = eaPlan8; |Y nLong > 4;
       aCtaNueva = eaCuenta;
       |SI eaPlan8 = aAlfa3; |Y #403#6 ! "B";
           aCtaNueva = aCtaAnterior;
       |FINSI;
   |SINO;
       aCtaNueva = eaPlan8 + aAlfa2;
   |FINSI;

   |HAZ CalculoNuevo;
|FPRC;

|PROCESO ElMes;
   |SI num =  1; mes = "Enero";     |FINSI;
   |SI num = 12; mes = "Diciembre"; |FINSI;
   |SI num =  2; mes = "Febrero";   |FINSI;
   |SI num =  3; mes = "Marzo";     |FINSI;
   |SI num =  4; mes = "Abril";     |FINSI;
   |SI num =  5; mes = "Mayo";      |FINSI;
   |SI num =  6; mes = "Junio";     |FINSI;
   |SI num =  7; mes = "Julio";     |FINSI;
   |SI num =  8; mes = "Agosto";    |FINSI;
   |SI num =  9; mes = "Septiembre";|FINSI;
   |SI num = 10; mes = "Octubre";   |FINSI;
   |SI num = 11; mes = "Noviembre"; |FINSI;
|FPRC;

|| *************************************************************
|PROCESO LeoTabla;

   eaCuenta = #403#0; |QBF eaCuenta;
   aAlfa1   = eaCuenta % 0;
   nLongMax = aAlfa1;    || Longitud de la cuenta

   |HAZ PasoNivel;
   |SI enNivel = 0;
       |SI nYaLo = 0; |MENAV "    Repase la Tabla de Equivalencia, existe Cuentas fuera de nivel"; nYaLo = 1; |FINSI;
       |ACABA;
   |FINSI;    || No la Pasa

   aCtaAnterior = #403#2; |QBF aCtaAnterior;

   |DDEFECTO #0;
   #0#0 = #403#0;
   #0#8 = 1;
   |LEE 101#0=;
   nEstado = FSalida;
   |HAZ BuscaEquiv;

   |SI aCtaNueva ! aCtaAnterior; |O aTodos = "S";
       |SI nEstado ! 0;
           #0#0 = #403#0;
           #0#8 = 1;
           |GRABA 020#0b;
       |FINSI;
       #0#1 = #403#1;
       #0#2 = #403#2;
       #0#3 = #403#3;
       |SI nEstado ! 0;
           #0#4 = aCtaNueva;
       |FINSI;
       #0#5 = aNomNueva;
       #0#6 = aDefecto6;
       #0#7 = aDefecto7;
       #0#9 = aCtaNueva;
       |SI #0#4 = "PDTE        ";
           #0#4 = #0#2;
           #0#5 = #0#3;
           #0#6 = "N";
           #0#7 = "N";
       |FINSI;
       |GRABA 020#0a;
       |LIBERA #0;
       nSwHay = 1;
   |FINSI;
|FPRC;

|DEFBUCLE LeoTabla;
 #403#1;
 ;
 "            ";
 "zzzzzzzzzzzz";
 e;
 NULL, LeoTabla;
|FIN;

|PROCESO HagoTodo;
  eNum2008  = #10#8;
  aDefecto6 = #10#13;
  aDefecto7 = #10#14;
  aTodos    = #10#15;
  |SI #10#4 = 0;
      |SI aParam = "";
          |MENAV "    Sin empresa anterior. Proceso Interrumpido";
      |FINSI;
      |ACABA;
  |FINSI;

  aFichero = "i" + Usuario;
  |NOME_DAT #902 aFichero;
  |DELINDEX #902;

  |HAZ LaEmpresaAnterior;
  |SI nAnterior = 0;
      |SI nEstado403 ! 0; |HAZ SinEquivalencia; |FINSI;
      |SI nEstado403 = 0; |HAZ LosNuevos; |FINSI;
      |ABRE #0;
      |ABRE #1;
      |ABRE #902;
      |BUCLE LeoTabla;
      |CIERRA #902;
      |CIERRA #1;
      |CIERRA #0;
  |SINO;
      |SI aParam = "";
          |MENAV "    No Existe Empresa anterior. Proceso Interrumpido";
      |FINSI;
  |FINSI;
  |DELINDEX #902;
|FPRC;

|| ****************************************************************
|PROCESO LaEmpresaAnterior;
  nAnterior = 1;
  |ABRE #30;
  #30#2  = #10#4;
  #30#3  = #10#5;
  |LEE 001#30=;
  |SI FSalida = 0;
      nAnterior = 0;
      aAlfa1 = #30#2; aAlfa1 = ("00000" + aAlfa1) % -105;
      aAlfa2 = #30#3; aAlfa2 = ("0"     + aAlfa2) % -101;
      |DFICB aPathPlan;
      aPathPlan = aPathPlan + aAlfa1 + aAlfa2 + "/";
      |PATH_DAT #3 aPathPlan;
  |FINSI;
  |CIERRA #30;
   ndig3 = #30#13; ndig4 = #30#14;
   ndig5 = #30#15; ndig6 = #30#16;
   ndig7 = #30#17; ndig8 = #30#18;
   ndig9 = #30#19;
|FPRC;

|PROCESO CargoDefs;
   nError = 0;
   |DBASE aMas;
   aDef3 = aMas + "def/cacuenta";
   |CARGA_DEF aDef3, cacuent@;
   |SI FSalida !  0;
       |MENAV "    Error al Carga fichero de Cuentas";
       aDef3 = "";
       nError = 1;
   |FINSI;
|FPRC;

|PROCESO Comprobar1;
 |SI #0#2 ! #0#4;
     nSwHay = 1;
     |ERROR10;
 |FINSI;
|FPRC;

|DEFBUCLE Comprobar1;
 #0#1;
 ;
 0001, "            ";
 9999, "zzzzzzzzzzzz";
 e;
 NULL, Comprobar1;
|FIN;

|PROCESO inf1;
 #0#0 = "            ";
|FPRC;

|PROCESO sup1;
 #0#0 = "zzzzzzzzzzzz";
|FPRC;

|PROCESO LimpioCampoB;
 |BORRA 020#403a;
 |LIBERA #403;
|FPRC;

|DEFBUCLE LimpioCampoB;
 #403#1;
 6;
 "            ","B";
 "zzzzzzzzzzzz","B";
 be;
 NULL, LimpioCampoB;
|FIN;

|PROCESO Act400;
   |ABRE #400;
   |DDEFECTO #400;
   #400#0 = enEmpresa;
   #400#1 = enPeriodo;
   |LEE 101#400=;
   |SI FSalida ! 0;
       |DDEFECTO #400;
       #400#0 = enEmpresa;
       #400#1 = enPeriodo;
       #400#2 = #10#8;
       #400#3 = #10#4;
       #400#4 = #10#5;
       #400#5 = 0;
       fFecha = ~;
       #400#6 = fFecha;
       aAlfa1 = Usuario % 810;
       #400#7 = aAlfa1;
       |GRABA 040#400n;
   |SINO;
       |SI #400#3 = 0;
           #400#3 = #10#4;
           #400#4 = #10#5;
           #400#5 = 0;
           fFecha = ~;
           #400#6 = fFecha;
           aAlfa1 = Usuario % 810;
           #400#7 = aAlfa1;
           |GRABA 040#400a;
       |FINSI;
   |FINSI;
   |LIBERA #400;
   |CIERRA #400;
|FPRC;

|PROGRAMA;
   aParam = PARAMETRO; |QBF aParam;

   nRecalculo = 0;
   nError  = 0;
   |SI aParam = "";
        |HAZ inicio;
        |HAZ eVarEmpresa;
        |SI enEjercicio ! 2008;
           aAlfa1 = "    Proceso para el Ejercicio 2008. No ejecutable en el " + enEjercicio;
           |MENAV aAlfa1;
           |ACABA;
        |FINSI;
        aTodos    = "N";
        aDefecto0 = "N";
        aDefecto6 = "N";
        aDefecto7 = "S";
   |SINO;
        cod1 = num_por;
        cod2 = num_per;
        emEmp = 1;  || Sin mensaje
        |HAZ leelo;
        |SI swerror ! 0;  |ACABA;   |FINSI;
   |FINSI;

   nEjerActual = enEjercicio;

   num = #canempre#11; |HAZ ElMes; mesdes = mes;
   num = #canempre#12; |HAZ ElMes; meshas = mes;
   |SI dig3 = 1; nLongMaxT = dig4; |FINSI;
   |SI dig3 = 2; nLongMaxT = dig5; |FINSI;
   |SI dig3 = 3; nLongMaxT = dig6; |FINSI;
   |SI dig3 = 4; nLongMaxT = dig7; |FINSI;
   |SI dig3 = 5; nLongMaxT = dig8; |FINSI;
   |SI dig3 = 6; nLongMaxT = dig9; |FINSI;

   |HAZ CargoDefs;
   |SI nError = 1; |VETE Terminar; |FINSI;

   |QBF fich_alta;
   |SI fich_alta = ""; |DFICE fich_alta; |FINSI;

   |PATH_DAT #0 fich_alta;
   |PATH_DAT #1 fich_alta;
   |PATH_DAT #101 fich_alta;
   |PATH_DAT #102 fich_alta;
   |PATH_DAT #103 fich_alta;
   |PATH_DAT #104 fich_alta;
   |PATH_DAT #105 fich_alta;

   |PATH_DAT #403 fich_alta;
   |PATH_DAT #404 fich_alta;

   |ABRE #400;
   |DDEFECTO #400;
   #400#0 = enEmpresa;
   #400#1 = enPeriodo;
   |LEE 101#400=;
   |SI FSalida ! 0;
       |DDEFECTO #400;
       #400#0 = enEmpresa;
       #400#1 = enPeriodo;
       #400#2 = eDNum2008;
       #400#3 = enEmpresa;
       #400#4 = enPeriodo - 1; |SI #400#4 < 0; #400#4 = 9; |FINSI;
   |FINSI;
   |LIBERA #400;
   |CIERRA #400;

   |SI aDefecto0 = "S";
       eNum2008 = eDNum2008;
   |SINO;
       eNum2008 = #400#2;
   |FINSI;

   |ABRE #401;
   #401#0 = eNum2008;
   |LEE 001#401=;
   |SI FSalida ! 0;
       nError = 1;
       |MENAV "    Error, no existe Codigo de Cuentas de Mayor";
       |CIERRA #401;
       |VETE Terminar;
   |FINSI;
   |CIERRA #401;

   |ABRE #403;
   #403#0 = "";
   |LEE 040#403];
   |SI FSalida ! 0;
       nEstado403 = 1;
       |SI aParam = "";
           |CONFI "    SCreacion de la Empresa sin Equivalencia. Continuar";
           |SI FSalida ! 0;
               |CIERRA #403;
               |VETE Terminar;
           |FINSI;
       |SINO;
           |CIERRA #403;
           |VETE Terminar;
       |FINSI;
   |FINSI;
   |CIERRA #403;

   #10#0 = enEmpresa;
   #10#1 = enPeriodo;
   #10#2 = eaNombreEmp;
   #10#3 = enEjercicio;
   #10#4 = #400#3;
   #10#5 = #400#4;
   |SI #400#3 ! 0;
       |HAZ LaEmpresaAnterior;
       |SI nAnterior = 1;
           #10#4 = 0; |C_M #10#4, 1, "S";
           #10#5 = 0; |C_M #10#5, 1, "S";
       |FINSI;
   |FINSI;
   |SI #400#3 ! 0;
        #10#6 = #30#1;
        |SI #30#26 < 80;
            #10#7 = #30#26 + 2000;
        |SINO;
             #10#7 = #30#26 + 1900;
        |FINSI;
   |SINO;
        #10#6 = "SI EMPRESA ANTERIOR";
        #10#7 = enEjercicio - 1;
        |C_M #10#4,1,"S";
        |C_M #10#5,1,"S";
   |FINSI;

   #10#8 = eNum2008;
   #10#9 = #401#1;
   #10#13 = aDefecto6;
   #10#14 = aDefecto7;
   #10#15 = aTodos;

   |CLS;
   |CABEZA "Cambios Plan 2008";
   |PINPA #0#10;
   |PINDA #0#10;
   FSalida = 0;
   |SI aParam = "";
       |ENDATOS #1#10;
   |FINSI;
   |SI FSalida = 0;
       |HAZ HagoTodo;

      |SI nSwHay = 1;
          |ENTLINEAL #1#0, -2, 4, 22, 0, inf1, sup1;
          |CONFI "    SConforme con los Datos Introducidos";
          |SI FSalida = 0;
              |HAZ Act400;
              |SI aParam = "";
                  |CONFI "    SDesea Realizar ahora el Cambio";
                  |SI FSalida ! 0; nSwHay = 0; |FINSI;
              |FINSI;
          |SINO;
              |DELINDEX #0;
              |SI nEstado403 ! 0; |DELINDEX #403; |DELINDEX #404; |FINSI;
              nSwHay = 0;
          |FINSI;
      |SINO;
          aAlfa1 = "    El Plan Contable es correcto segun la tabla de Equivalencia";
          |SI nYaLo = 1; aAlfa1 = "    La Tabla de Equivalencia tiene Errores"; |FINSI;
          |MENAV aAlfa1;
      |FINSI;
   |FINSI;

|ET Terminar;
   |SI aDef3  ! ""; |DESCARGA_DEF #cacuent@; |FINSI;
   |SI nSwHay ! 0;
       |SI nEstado403 ! 0; |HAZ SinEquivalencia_M; |FINSI;
       |SUB_EJECUTA_NP "ca8zcamn";
   |FINSI;

   |SI nRecalculo = 1;
       pathbal = fich_alta;
       |CONFI "2410SDesea Realizar el Recalculo de Saldos: ";
       |SI FSalida = 0;
           balance = 1;
           |SUB_EJECUTA "careccue;apertura";
       |FINSI;
   |FINSI;

   |BUCLE LimpioCampoB;
|FPRO;

|| ************** PROCESO ESPECIAL PARA CUANDO NO HAY TABLA
|PROCESO LosNuevos_A;
 aAlfa1 = #3#0; |QBF aAlfa1;
 aAlfa2 = aAlfa1 % 0;
 nNume1 = aAlfa2;
 |SI nNume1 > 4;
     #403#0 = #3#0;
     |LEE 001#403=;
     |SI FSalida ! 0;
        |DDEFECTO #403;
        #403#0 = #3#0;
        #403#1 = #3#2;
        #403#2 = "";
        #403#3 = "";
        #403#4 = "";
        #403#5 = "";
        #403#6 = "B";
        |GRABA 020#403n;
     |FINSI;
 |FINSI;
|FPRC;

|DEFBUCLE LosNuevos_A;
 #3#1002;
 3;
 "            ", 0, "S";
 "zzzzzzzzzzzz", 9, "S";
 e;
 NULL, LosNuevos_A;
|FIN;

|PROCESO LosNuevos;
  |ABRE #1;
  |ABRE #403;
  |BUCLE LosNuevos_A;
  |CIERRA #403;
  |CIERRA #1;
|FPRC;

|PROCESO LeoCuenta;
 |ABRE #1;
 |DDEFECTO #1;
 #1#0 = eaCuenta;
 #1#1 = enNivel;
 |LEE 001#1=;
 |CIERRA #1;
|FPRC;

|PROCESO LeoPlan;
   eaCuenta = #2#0;
   |HAZ PasoNivel;
   |SI enNivel = 0;  |ACABA;  |FINSI;    || No la Pasa

   |DDEFECTO #1;
   #1#0 = #2#0;    || cuenta aqui
   #1#1 = enNivel;
   |LEE 101#1=;
   nEstado = FSalida;
   #1#0  = #2#0;
   #1#1  = enNivel;
   #1#2  = #2#9;  || Descripcion Abreviada
   #1#3  = "N";   || Pase directo
   #1#4  = #2#2;  || Bloque
   #1#5  = #2#3;  || Tipo
   #1#6  = #2#5;  || Agrupacion
   #1#7  = 0;
   #1#8  = 0;

   || la 3a clave esta formada por la cta + zzzz y el campo fantasma del nivel;
   mascara = #1#0;
   |QBF mascara;
   mascara = mascara +"zzzzzzzzzzzz";
   mascara = mascara % 105;
   #1#54   = mascara;
   #1#55   = enNivel;
   #1#56   = mesdes;
   #1#57   = meshas;

   |SI nEstado = 0;  |GRABA 020#1a;  |FINSI;
   |SI nEstado ! 0;  |GRABA 020#1n;  |FINSI;
   |LIBERA #1;
|FPRC;

|DEFBUCLE LeoPlan;
 #2#1;
 ;
 enEjerBal, #10#8, "     ";
 enEjerBal, #10#8, "zzzzz";
 e;
 NULL, LeoPlan;
|FIN;

|PROCESO AstoApertura;
 #404#0   = #20#3;
 eaCuenta = #20#4;
 enNivel  = #20#5;
 |HAZ LeoCuenta;
 #404#1 = eaCuenta;
 #404#2 = #1#2;
 #404#3 = #20#8;
 |SI #404#3 = "D";
     #404#4 = #20#9;
     #404#5 = 0;
 |SINO;
     #404#4 = 0;
     #404#5 = #20#9;
 |FINSI;
 #404#6  = "";
 #404#7  = #20#5;
 #404#8  = eaCuenta;
 #404#9  = #20#5;
 #404#10 = #20#0;
 #404#11 = #20#1;
 #404#12 = #20#2;
 #404#13 = #20#21;
 #404#14 = "*";
 |GRABA 020#404n;
|FPRC;

|DEFBUCLE AstoApertura;
 #20#1;
 ;
 00, fec1, 000001, 0001;
 00, fec1, 999999, 9999;
 e;
 NULL, AstoApertura;
|FIN;

|PROCESO LaTabla;
 #3#0 = #1#0;
 #3#1 = #1#1;
 |LEE 001#3=;
 |SI FSalida ! 0;
     |ACABA;
 |FINSI;
 aAlfa1 = #1#0; |QBF aAlfa1;
 |SI aAlfa1 = ""; |ACABA; |FINSI;
 |SI #1#1 = 0; |ACABA; |FINSI;

 |DDEFECTO #403;
 #403#0 = #1#0;
 |LEE 041#403=;
 nEstado = FSalida;
 #403#0 = #1#0;
 #403#1 = #1#2;
 #403#2 = #1#0;
 #403#3 = #1#2;
 #403#4 = "";
 #403#5 = "";
 #403#6 = "*";
 |SI nEstado ! 0; |GRABA 020#403n; |FINSI;
 |SI nEstado = 0; |GRABA 020#403a; |FINSI;

|FPRC;

|PROCESO BajaCaCuenta;
 aAlfa1 = #1#0; |QBF aAlfa1;
 aAlfa2 = aAlfa1 % 0;
 nNume1 = aAlfa2;
 |SI nOp = 0;
     |SI nNume1 ] 1; |Y nNume1 [ 4;
         |BORRA 020#1a;
     |FINSI;
 |FINSI;
 |SI nOp = 1;
     |SI nNume1 > 4; |O #1#3 = "S"; |HAZ LaTabla; |FINSI;
 |FINSI;
 |LIBERA #1;
|FPRC;

|DEFBUCLE BajaCaCuenta;
 #1#1;
 ;
 "            ",0;
 "zzzzzzzzzzzz",9;
 e;
 NULL, BajaCaCuenta;
|FIN;

|PROCESO SinEquivalencia;
  |DELINDEX #403;

  |ABRE #3;
  nOp = 1;
  |ABRE #403;
  |BUCLE BajaCaCuenta;
  |CIERRA #403;
  |CIERRA #3;

  |DELINDEX #404;
  |ABRE #404;
  |BUCLE AstoApertura;
  |CIERRA #404;
|FPRC;

|PROCESO SinEquivalencia_M;
  nOp = 0;
  |ABRE #403;
  |BUCLE BajaCaCuenta;
  |CIERRA #403;

  nCamp5 = #10#8;
  |HAZ creacion_m;
  |ABRE #1;
  |BUCLE LeoPlan;
  |CIERRA #1;
  nRecalculo = 1;
|FPRC;
