|FICHEROS;
   n32norma #0;

   caremesc #1;
   caremesl #2;

   caclipro #3;
   camancob #4;

   camoneda #5;
|FIN;

|VARIABLES;
   NOrden = 0; Handle = 0; NumReg = 0; NumRem = 0; NumEfect = 0;
   TotalGen = 0; Calc = 0;
   Comodin = ""; Cadena = ""; CodCedente = ""; Moneda = "";

 &programa = "";
 &ficcopia = "";
|FIN;

|PROCESO RegistroInicial;
   Cadena  = "0265";
   Comodin = "  "; Cadena = Cadena + Comodin;
   Comodin = #n32norma#8; Comodin = (Comodin % 0106) + (Comodin % -102);
   Comodin = Comodin - "."; Comodin = Comodin - "."; Cadena = Cadena + Comodin;
   Comodin = NOrden; NOrden = NOrden + 1; Comodin = ("0000" + Comodin) % -104;
   Cadena = Cadena + Comodin;
   Comodin = " " * 35; Cadena = Cadena + Comodin;
   Comodin = ("    " + #n32norma#6) % -104; Cadena = Cadena + Comodin;
   Comodin = ("    " + #n32norma#7) % -104; Cadena = Cadena + Comodin;
   Cadena = Cadena + &13 + &10;
   Cadena = Handle + " " + Cadena; |FGRABA Cadena;
   NumReg = NumReg + 1;
|FPRC;

|PROCESO RegistroCabRemesa;
   Cadena = "1165";
   Comodin = "  "; Cadena = Cadena + Comodin;
   Comodin = NOrden; Comodin = ("0000" + Comodin) % -104; Cadena = Cadena + Comodin;
   Comodin = " " * 12; Cadena = Cadena + Comodin;
   Comodin = ("000000000000000" + #n32norma#13) % -115; Cadena = Cadena + Comodin;
   Cadena = Cadena + "1";
   Cadena = Cadena + &13 + &10;
   Cadena = Handle + " " + Cadena; |FGRABA Cadena;
   NumReg = NumReg + 1;
|FPRC;

|PROCESO RegistroIndiv;
   CodCedente = #caclipro#9; |QBF CodCedente;
   CodCedente = (CodCedente + (" " * 15)) % 0115;

   Cadena = "2565  ";
   Comodin = CodCedente;                             Cadena = Cadena + Comodin;
   Comodin = #n32norma#8; Comodin = (Comodin % 0106) + (Comodin % -102);
   Comodin = Comodin - "."; Comodin = Comodin - "."; Cadena = Cadena + Comodin;
   Comodin = NOrden; Comodin = ("0000" + Comodin) % -104; Cadena = Cadena + Comodin;
   Comodin = ("00" + #caclipro#6) % -102;            Cadena = Cadena + Comodin;
   Comodin = "         ";                            Cadena = Cadena + Comodin;
   Comodin = #caremesl#14; |QBF Comodin; Comodin = (Comodin + (" " * 20)) % 0120;
                                                     Cadena = Cadena + Comodin;
   Comodin = " " * 25;                               Cadena = Cadena + Comodin;
   Calc = #caremesl#7;
   |SI Moneda = "E";
      Calc = Calc * 100;
   |SINO;
      Calc = ((Calc / 166.386) ! 2) * 100;
   |FINSI;
   Comodin = Calc; Comodin = (("0" * 9) + Comodin) % -109;
                                                     Cadena = Cadena + Comodin;
   Comodin = " " * 15;                               Cadena = Cadena + Comodin;
   Comodin = #caremesl#6; Comodin = (Comodin % 0106) + (Comodin % -102);
   Comodin = Comodin - "."; Comodin = Comodin - "."; Cadena = Cadena + Comodin;
   Cadena = Cadena + &13 + &10;
   Cadena = Handle + " " + Cadena; |FGRABA Cadena;

   Cadena = "2665  ";
   Comodin = CodCedente;                             Cadena = Cadena + Comodin;
   Comodin = "  2";                                  Cadena = Cadena + Comodin;
   Comodin = #camancob#6; Comodin = (Comodin % 0106) + (Comodin % -102);
   Comodin = Comodin - "."; Comodin = Comodin - "."; Cadena = Cadena + Comodin;
   Comodin = "20";                                   Cadena = Cadena + Comodin;
   Comodin = #camancob#20; Comodin = Comodin % 0110; Cadena = Cadena + Comodin;
   Comodin = #camancob#19; Comodin = Comodin % 0110; Cadena = Cadena + Comodin;
   Comodin = #48#1; |QBF Comodin;
   Comodin = (Comodin + (" " * 34)) % 0134;          Cadena = Cadena + Comodin;
   Comodin = (#caremesl#13 + (" " * 34)) % 0134;     Cadena = Cadena + Comodin;
   Comodin = #n32norma#9; |QBF Comodin; Comodin = Comodin + " ";
   |SI #n32norma#10 = "S"; Comodin = Comodin + ((("  " + #caremesl#4) % -102) + "/" + (("000000" + #caremesl#3) % -106)); |QBF Comodin; |FINSI;
   |SI #n32norma#11 = "S"; Comodin = Comodin + "/" + (("000" + #caremesl#5) % -103); |QBF Comodin; |FINSI;
   |SI #n32norma#12 = "S"; Comodin = Comodin + #caremesl#6; |QBF Comodin; |FINSI;
   Cadena = Cadena + Comodin; Cadena = Cadena + &13 + &10;
   Cadena = Handle + " " + Cadena; |FGRABA Cadena;

|| ... Mirar los campos cuando suma a las longitudes 11.07.2005

   Cadena = "2765  ";
   Comodin = CodCedente;                             Cadena = Cadena + Comodin;
   Comodin = "  ";                                   Cadena = Cadena + Comodin;
   Comodin = #caclipro#2;
   Comodin = ( Comodin + (" " * 34)) % 0134;
                                                     Cadena = Cadena + Comodin;
   Comodin = (("00" + #caclipro#15) % -102) + (("000" + #caclipro#16) % -103);
                                                     Cadena = Cadena + Comodin;
   Comodin = #caclipro#6;
   Comodin = (Comodin + (" " * 20)) % 0120;
                                                     Cadena = Cadena + Comodin;
   Comodin = (("00" + #caclipro#15) % -102);         Cadena = Cadena + Comodin;
   Comodin = " " * 7;                                Cadena = Cadena + Comodin;
   Comodin = #caclipro#13;
   Comodin = (Comodin + (" " * 15)) % 0115;
                                                     Cadena = Cadena + Comodin;
   Comodin = #caclipro#14;
   Comodin = (Comodin + (" " * 30)) % 0130;
                                                     Cadena = Cadena + Comodin;
   Comodin = " " * 14;                               Cadena = Cadena + Comodin;
   Cadena = Cadena + &13 + &10;
   Cadena = Handle + " " + Cadena; |FGRABA Cadena;

   NumReg = NumReg + 3;
|FPRC;

|PROCESO RegistroFinRemesa;

   Cadena = "7165  ";
   Comodin = #n32norma#8; Comodin = (Comodin % 0106) + (Comodin % -102);
   Comodin = Comodin - "."; Comodin = Comodin - "."; Cadena = Cadena + Comodin;
   Comodin = NOrden; Comodin = ("0000" + Comodin) % -104;
                                                     Cadena = Cadena + Comodin;
   Comodin = " " * 59;                               Cadena = Cadena + Comodin;
   Calc = #caremesc#10;
   |SI Moneda = "E";
      Calc = Calc * 100;
   |SINO;
      Calc = ((Calc / 166.386) ! 2) * 100;
   |FINSI;
   Comodin = Calc;
   Comodin = (("0" * 10) + Comodin) % -110;
   TotalGen = TotalGen + #caremesc#10;
                                                     Cadena = Cadena + Comodin;
   Comodin = " " * 46;                               Cadena = Cadena + Comodin;
   Comodin = ("0000000" + ((#caremesc#5 * 3) + 2)) % -107;
                                                     Cadena = Cadena + Comodin;
   Comodin = ("000000" + #caremesc#5) % -106;        Cadena = Cadena + Comodin;
   Comodin = " " * 6;                                Cadena = Cadena + Comodin;
   Cadena = Cadena + &13 + &10;
   Cadena = Handle + " " + Cadena; |FGRABA Cadena;
   NumEfect = NumEfect + #caremesc#5;
   NumRem   = NumRem + 1;
   NumReg = NumReg + 1;
|FPRC;

|PROCESO RegistroFinal;
   NumReg = NumReg + 1;
   Cadena = "9865";
   Comodin = " " * 71;                               Cadena = Cadena + Comodin;
   Calc = TotalGen;
   |SI Moneda = "E";
      Calc = Calc * 100;
   |SINO;
      Calc = ((Calc / 166.386) ! 2) * 100;
   |FINSI;
   Comodin = ("0000000000" + Calc) % -110;           Cadena = Cadena + Comodin;
   Comodin = " " * 41;                               Cadena = Cadena + Comodin;
   Comodin = ("00000" + NumRem) % -105;              Cadena = Cadena + Comodin;
   Comodin = ("0000000" + NumEfect) % -107;          Cadena = Cadena + Comodin;
   Comodin = ("000000" + NumEfect) % -106;           Cadena = Cadena + Comodin;
   Comodin = " " * 6;                                Cadena = Cadena + Comodin;
   Cadena = Cadena + &13 + &10;
   Cadena = Handle + " " + Cadena; |FGRABA Cadena;
|FPRC;

|PROCESO LineaRem;
   #caclipro#23 = "C";
   #caclipro#10 = #caremesl#11;
   #caclipro#11 = #caremesl#12;
   |LEE 000#caclipro.=;

   #camancob#0 = #caremesl#2;
   #camancob#2 = #caremesl#4;
   #camancob#1 = #caremesl#3;
   #camancob#3 = #caremesl#5;
   |LEE 000#camancob.=;
   |HAZ RegistroIndiv;
|FPRC;

|DEFBUCLE LineaRem;
#caremesl#1;
;
#caremesc#0,001;
#caremesc#0,999;
;
NULL,LineaRem;
|FIN;

|PROCESO Remesas;
   |HAZ RegistroCabRemesa;
   |BUCLE LineaRem;
   |HAZ RegistroFinRemesa;
|FPRC;

|DEFBUCLE Remesas;
#caremesc#1;
1,2;
#n32norma#0,#n32norma#2,#n32norma#4;
#n32norma#1,#n32norma#3,#n32norma#5;
;
NULL,Remesas;
|FIN;

|PROGRAMA;
   |CLS;
   |CABEZA "Emision diskette norma 32";

   |PINPA #0#0; |PINDA #0#0;

   |ENDATOS #1#0;
   |SI FSalida = 0;
      |ABRE #camoneda;
      #camoneda#0 = #48#2;
      #camoneda#1 = #48#3;
      |LEE 000#camoneda.=;
      |SI FSalida = 0;
         Moneda = #camoneda#2;
      |SINO;
         Moneda = "P";
      |FINSI;
      |CIERRA #camoneda;

      |ABRE #caclipro; |ABRE #camancob;
      |DFICE Comodin;
      Comodin = "wb  " + Comodin + "agifarec.seq";
      |FABRE Comodin;
      |SI FSalida ] 0;
         Handle = FSalida;
         |HAZ RegistroInicial;
         |BUCLE Remesas;
         |HAZ RegistroFinal;
         FSalida = Handle;
         |FCIERRA FSalida;
      |FINSI;
      |ATRI I; |PINTA 1926, NumEfect; |ATRI N;
      |ATRI I; |PINTA 2026, NumReg;   |ATRI N;
      |ATRI I; |PINTA 2126, TotalGen; |ATRI N;

      |AVISO;
      |CONFI "2400SDESEA GRABAR AHORA EL DISKETTE ";
      |SI FSalida = 0;
          programa = " REMESA N32 ";
          ficcopia = "agifarec.seq";
          |SUB_EJECUTA_NP "cared003";
      |FINSI;
      |CIERRA #camancob; |CIERRA #caclipro;
   |FINSI;
|FPRO;
