|FICHEROS;
  cainfamo #0;
  camocabe #1;
  camoline #2;
  camogrup #3;         ||Grupos de Amortizacion
  cainftmp #4;         ||Temporal grabacion cuentas
|FIN;

|VARIABLES;
  &entrada = 1;
  informe = "cainfamo";
  &tipo = "AMORTIZACIONES";
  amo = "A";
  &impo = 0;
  &total = 0;
  &totcue = 0;
  &lintot = 0;
  &linnor = 0;
  &linant = 0;
  &ulti = "";
  &impoc = 0;
  &totalc = 0;
  &totcuec = 0;
  &nof = "";
  &noc = "";
  t1 = "NORMAL";
  t2 = "DEGRESIVA";
  t3 = "ACELERADA";
  t4 = "RESIDUAL";
  are = "a";
  bueno = 0;
  nombre = "";
  alfa1 = "";
|FIN;

|INCLUYE acceso_i;
|INCLUYE puntos_i;
|INCLUYE numamo_i;

|PROCESO numamo; |TIPO 0;
    x_alfa1 = #0Campo;
    |HAZ x_punto;
    #0Campo = x_alfa1;
    |PINTA #0Campo;
|FPRC;

|PROCESO no_dep; |TIPO 7;
   alfa1 = #30#24;
   |C_M #0#9, 1, alfa1;
   |C_M #0#10, 1, alfa1;
|FPRC;

|PROCESO cuenta; |TIPO 0;
  p_alfa1 = #0Campo;
  salidas = FSalida;
  |HAZ punto;
  |SI p_error ! 0;  |ERROR;  |FINSI;
|FPRC;

|PROCESO graba_tmp;
  |SI #0#7 = "N";
      |SI #1#22 = "N"; |ACABA; |FINSI;
  |FINSI;
  #3#0 = #1#15;
  |LEE 001#3=;
  |SI FSalida ! 0;  |ACABA;   |FINSI;
  |SI #3#1 ] #0#0; |Y #3#1 [ #0#1;
      #4#0 = #3#1;
      #4#1 = #3#2;
      #4#2 = #1#0;
      #4#3 = #1#1;
      |GRABA 021#4n;
      |LIBERA #4;
  |FINSI;
|FPRC;

|DEFBUCLE carga_tmp;
 #1#1;
 28;
 #0#2,00, #0#9;
 #0#3,99, #0#10;
 e;
 NULL , graba_tmp;
|FIN;

|PROCESO imprelin;
  |SI #2#4 = "A";
      |SI #2#3 ] #0#4; |Y #2#3 [ #0#5;
          impo = impo + #2#5;     impoc = impoc + #2#9;
          total = total + #2#5;   totalc = totalc + #2#9;
          totcue = totcue + #2#5; totcuec = totcuec + #2#9;
      |FINSI;
  |FINSI;
|FPRC;

|PROCESO fechafac;
  #1#0 = #4#2;
  #1#1 = #4#3;
  |LEE 001#1=;
  |SI FSalida ! 0;  |ACABA;  |FINSI;
  |SI bueno ! 0;
      |SI #4#0 ! ulti;
          lintot = 1;
          |PRINT; || linea del informe
          totcue = 0; totcuec = 0;
          lintot = 0;
          linant = 0;
          linnor = 1;
      |SINO;
          lintot = 0;
          linnor = 0;
          linant = 1;
      |FINSI;
  |SINO;
      lintot = 0;
      linnor = 1;
      linant = 0;
  |FINSI;
  ulti = #4#0;
  nof = t1; noc = t1;
  |SI #1#10 = "D"; nof = t2; |FINSI;
  |SI #1#10 = "A"; nof = t3; |FINSI;
  |SI #1#10 = "R"; nof = t4; |FINSI;
  |SI #1#16 = "D"; noc = t2; |FINSI;
  |SI #1#16 = "A"; noc = t3; |FINSI;
  |SI #1#16 = "R"; noc = t4; |FINSI;
  |BUCLELIN 2imprelin#1; || lee las lineas de esta cabecera
  |SI impo ! 0; |O impoc ! 0;
      |PRINT;
      bueno = 1;
  |SINO;
      |SI #0#8 = "S";
          |PRINT;
          bueno = 1;
      |FINSI;
  |FINSI;
  lintot = 0;
  linnor = 0;
  linant = 0;
  impo = 0; impoc = 0;
|FPRC;

|DEFBUCLE cainfamo0;
#4#1;
;
#0#0 , 0 , #0#2, 00;
#0#1 , 9 , #0#3, 99;
e;
NULL, fechafac;
|FIN;

|PROCESO impre; |TIPO 3;
  nombre = "amo" + Usuario;
  nombre = nombre % 108;
  |QBF nombre;
  |NOME_DAT #4 nombre;

  |IMPRE 0;
  |SI FSalida ! 0;  |ACABA;  |FINSI;
  |INFOR informe;
  |SI FSalida ! 0;  |FINIMP;  |ACABA;  |FINSI;
  |ABRE #4; |CIERRA #4; |DELINDEX #4;

  |ABRE #3;
  |ABRE #4;
  |BUCLE carga_tmp;
  |CIERRA #3;
  |CIERRA #4;
  bueno = 0;
  |ABRE #1;
  |BUCLE cainfamo0;

  |SI bueno = 1;
      lintot = 1;
      |PRINT;
      |PIEINF; || imprime el pie del informe
  |FINSI;
  |FININF;
  |CIERRA #1;
  |DELINDEX #4;
  |FINIMP;
|FPRC;
