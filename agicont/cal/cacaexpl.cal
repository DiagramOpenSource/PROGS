|FICHEROS;
    cacaexpl #0;
    cacuenta #1;         || cuentas
    caparexp #2;         || partidas de explotacion
    cabaexp2 #5;         || temporal situacion
    caexistc #6;         || descarga cabs.
    caexistl #7;         || descarga lins.
    canempre #30;        || empresas
    caconimp #8;
    cabucem1 #100;

    caseslin #110;
|FIN;

|VARIABLES;
   aFichero = "";
   &Ejercicio = 0;
    &eaFicbal = "";
    &eaNomSes = "";
    &enNumSes = 0;
    &eaDepSes = "";
    &eaDesDep = "";
    &enMasDep = 0;
    nswPrim  = 0;
    nSwError = 0;
    nSwPrint = 0;

  dos      = 0;
  &pathbal = "";
  &balance = 1;
  varcamp  = 0;
  mes      = 0;
  alfa1    = "";
  alfa2    = "";
  alfa3    = "";
  traraf   = 0;
  &sw_inf  = 0;
  &cantidad  = 0;
  &cantidad1 = 0;
  &variable  = "";
  &canti     = 0;
  &canti1    = 0;
  &doh = "";
  &anu = 0;
  &anu1 = 0;
  &raya1 = "";
  &raya11 = "";
  ray1 = "            -------------------------------------------------------------------"
  ray2 = "                                              ---------------------------------"
  ray3 = "                                              ================================="
  letrames = "";
  x = 0;
  nivelet = 0;
  informe = "";
  sw = 0;
  &eldesde = 0;
  &elhasta = 0;
  acumula = 0;
  dir_ficha = "";
  dir_fichb = "";
  saldo1 = 0;
  saldo2 = 0;
  saldo3 = 0;
  saldo4 = 0;
  saldo11 = 0;
  saldo12 = 0;
  saldo13 = 0;
  saldo14 = 0;
  importe = 0;
  descar = 0;
  ult_cuent = "";
  ult_nivel = 0;
  nue_cuent = "";
  nue_nivel = 0;
  act_cuent = "";
  act_nivel = 0;
  carac = 0;
  guarda_can = 0;
  guarda_can1 = 0;
  suma1 = 0;
  suma2 = 0;
  suma3 = 0;
  suma4 = 0;
  suma5 = 0;
  suma6 = 0;
  suma11 = 0;
  suma12 = 0;
  suma13 = 0;
  suma14 = 0;
  suma15 = 0;
  suma16 = 0;
  llegoa = 0;
  nume1 = 0;
  tipo = "";
  &ant_agru = "";
  &ant_epig = "";
  &ant_impo = 0;
  &ant_impo1 = 0;
  t_agru = 0;
  t_agru1 = 0;
  t_epig = 0;
  t_epig1 = 0;
  grupo = 0;
  gru_nou = 0;
  agrupaci = 0;
  epigrafe = 0;
  partida = 0;
  antes = "";
  &r_emp = 0;
  &r_per = 0;
  &d_mes = 0;
  &h_mes = 0;
  &estru = 0;
  &r_eje = 0;
  &r_nom = "";
  mesini = 0;
  &aparam = "";

  Empresa  = 0;
  Periodo  = 0;
  Per1     = 0;
  Per2     = 0;
  Handle   = 0;
  Numeracion = 0;
  Calc     = 0;
  Comodin  = "";
  Comodin1 = "";

  swlepigr = 0;
  swlagrup = 0;
   &eaUnidadBasico = "";
   &eaUnidadBTmp   = "";
|FIN;

|INCLUYE acceso_i;
|INCLUYE digito_i;
|INCLUYE dsmone_i;

|PROCESO antesque;
  aparam = PARAMETRO; |QBF aparam;
  |SI aparam = "fech";
      |HAZ AntesSesion;
      |NOME_DAT #1 eaFicbal;
  |FINSI;
|FPRC;

|PROCESO PintoSes; |TIPO 7;
  aparam = PARAMETRO; |QBF aparam;
  |SI aparam = "fech";
      |HAZ AntesSesion;
      |SI eaNomSes > "";
          |ATRI I; |PINTA 0505, "SESION: "; |PINTA 0513,eaNomSes;
          |SI eaDepSes > "";
              |PINTA 0558, "Departamento: "; |PINTA 0572, eaDepSes;
          |FINSI;
          |ATRI 0;
      |FINSI;
  |FINSI;
|FPRC;

____________________________________/ CALCULOS DE PANTALLA

|CALCULO mayor1; |TIPO 0;
  #0Campo = #0Campo > " ";
  |PINTA #0Campo;
|FCAL;

|CALCULO mayor; |TIPO 0;
  alfa1 = #0Campo > " ";
  #0Campo = alfa1;
  |PINTA #0Campo;
  |SI #0Campo = "N";
      #0#24 = empre;
      #0#25 = ejerc;
      |CAMPO_MODIFICA #0#24,1,"N";
      |CAMPO_MODIFICA #0#25,1,"N";
      |PINTA #0#24;
      |PINTA #0#25;
  |SINO;
      |CAMPO_MODIFICA #0#24,1,"S";
      |CAMPO_MODIFICA #0#25,1,"S";
  |FINSI;
|FCAL;

|CALCULO deentrada;  |TIPO 7;
  |SI #0#24 = 0;  |Y #0#25 = 0;
      #0#24 = empre;
      #0#25 = ejerc - 1;
      |SI #0#25 < 0; #0#25 = 9; |FINSI;
      |PINTA #0#24;
      |PINTA #0#25;
  |FINSI;
|FCAL;

|CALCULO esbuena; |TIPO 0;
  |SI FSalida = 9; |ACABA; |FINSI;
  |ABRE #30;
  #30#2 = #0#24;
  #30#3 = 0;
  |LEE 011#30];
  |SI FSalida ! 0;  |O #30#2 ! #0#24;
      |MENAV "     NO Existe la empresa Contable";
      |ERROR;
  |FINSI;
  |CIERRA #30;
|FCAL;

|CALCULO consulta; |TIPO 0;
  |SI FSalida ! 9;  |ACABA;  |FINSI;
  r_emp = 0;
  |SUB_EJECUTA "caconemp.run";
  |ERROR;
  |SI Campo = 24;
      #0#24 = r_emp;
      #0#25 = r_per;
      |PTEC 802;
      |PTEC 802;
  |FINSI;
|FCAL;

|CALCULO esbuena1; |TIPO 0;
  |SI FSalida = 2; |ACABA; |FINSI;
  |ABRE #30;
  #30#2 = #0#24;
  #30#3 = #0#25;
  |LEE 011#30=;
  |SI FSalida ! 0;
      |MENAV "     NO Existe Periodo en la empresa Contable";
      |CIERRA #30;
      |ERROR;
      |ACABA;
  |FINSI;
  |SI #30#13 ! dig3;  |O #30#14 ! dig4;  |O #30#15 ! dig5;
                      |O #30#16 ! dig6;  |O #30#17 ! dig7;
                      |O #30#18 ! dig8;  |O #30#19 ! dig9;
      |MENAV "     NO CORRESPONDEN LOS NIVELES";
      |CIERRA #30;
      |ERROR;
      |ACABA;
  |FINSI;
  #0#26 = #30#1;
  anu1 = 1900 + #30#26;
  |SI anu1 [ 1970;
      anu1 = anu1 + 100;
  |FINSI;
  |PINTA #0#26;
  #30#2 = empre;
  #30#3 = ejerc;
  |LEE 011#30=;
  anu = 1900 + #30#26;
  |SI anu [ 1970;
      anu = anu + 100;
  |FINSI;
  |CIERRA #30;
|FCAL;

|CALCULO pintames; |TIPO 0;
  varcamp = Campo + 2;                || Coge el campo del nombre del mes
  |SI #0Campo ! 0; |Y #0Campo ! 13;
      mes = #30#11 + (#0Campo - 1);   || Operacion para saber que mes es
      |SI mes > 12; mes = mes - 12; |FINSI;
  |SINO;
      mes = #0Campo;
  |FINSI;
  |HAZ mesletra;                          || Recoge el mes en letra
  |SI Campo = 7;  |O Campo = 8;
      |ATRI I;
      |SI Campo = 7;
          |PINTA 1518, letrames;
      |SINO;
          |PINTA 1555, letrames;
      |FINSI;
      |ATRI 0;
  |SINO;
      #0varcamp = letrames;
      |PINTA #0varcamp;                       || Pinta el nombre del mes
  |FINSI;
  |SI Campo = 1;
      |SI #0#0 > #0#1;
          |MENAV "    Error de Entrada ...";
          |ERROR;
      |FINSI;                         || Si el mes hasta es menor al desde
  |FINSI;
  |SI Campo = 8;
      |SI #0#7 > #0#8;
          |MENAV "     Error de Entrada ...";
          |ERROR;
      |FINSI;
  |FINSI;
|FCAL;

|PROCESO mesletra;
  |SI mes = 0;  letrames = "Apertura  "; |FINSI;
  |SI mes = 1;  letrames = "Enero     "; |FINSI;
  |SI mes = 2;  letrames = "Febrero   "; |FINSI;
  |SI mes = 3;  letrames = "Marzo     "; |FINSI;
  |SI mes = 4;  letrames = "Abril     "; |FINSI;
  |SI mes = 5;  letrames = "Mayo      "; |FINSI;
  |SI mes = 6;  letrames = "Junio     "; |FINSI;
  |SI mes = 7;  letrames = "Julio     "; |FINSI;
  |SI mes = 8;  letrames = "Agosto    "; |FINSI;
  |SI mes = 9;  letrames = "Septiembre"; |FINSI;
  |SI mes = 10; letrames = "Octubre   "; |FINSI;
  |SI mes = 11; letrames = "Noviembre "; |FINSI;
  |SI mes = 12; letrames = "Diciembre "; |FINSI;
  |SI mes = 13; letrames = "Cierre    "; |FINSI;
|FPRC;

|CALCULO estruct; |TIPO 0;
  |ABRE #6;
  #6#0 = #0#6;
  |LEE 000#6=;
  |SI FSalida ! 0;
      |MENAV "    No existe la estructura de descarga ...";
      |ERROR;
  |FINSI;
  |CIERRA #6;
|FCAL;

|CALCULO numnivel; |TIPO 0;
  |SI #0Campo > #30#13;
      |MENAV "    El numero de niveles no coincide con el definido en la Empresa ...";
      |ERROR;
  |FINSI;
  nivelet = 0;
  |CAMPO_MODIFICA #0#19,1,"S";
  |CAMPO_MODIFICA #0#20,1,"S";
  |CAMPO_MODIFICA #0#21,1,"S";
  |CAMPO_MODIFICA #0#22,1,"S";
  |CAMPO_MODIFICA #0#23,1,"S";
|FCAL;

|CALCULO nivelet; |TIPO 7;
  |SI #0#17 < 6; #0#23 = 0; |PINTA #0#23; |CAMPO_MODIFICA #0#23,1,"N"; |FINSI;
  |SI #0#17 < 5; #0#22 = 0; |PINTA #0#22; |CAMPO_MODIFICA #0#22,1,"N"; |FINSI;
  |SI #0#17 < 4; #0#21 = 0; |PINTA #0#21; |CAMPO_MODIFICA #0#21,1,"N"; |FINSI;
  |SI #0#17 < 3; #0#20 = 0; |PINTA #0#20; |CAMPO_MODIFICA #0#20,1,"N"; |FINSI;
  |SI #0#17 < 2; #0#19 = 0; |PINTA #0#19; |CAMPO_MODIFICA #0#19,1,"N"; |FINSI;
|FCAL;

|CALCULO nivelrep; |TIPO 0;
  |SI nivelet = 0;
      nivelet = #0#18;
  |FINSI;
  |PARA x = Campo - 1;  |SI x ] 18;  |HACIENDO x = x - 1;
        |SI #0Campo = #0x;  |Y #0Campo ! 0;
            |MENAV "     Nivel repetido ...";
            |ERROR;
            |ACABA;
            |SAL;
        |FINSI;
        |SI nivelet < #0x;
            nivelet = #0x;
        |FINSI;
  |SIGUE;
|FCAL;

|| *************************************************************
|| ... Rutinas para imprimir.

|PROCESO sacacambio;
  alfa1 = alfa1 + "            ";
  #1#0 = alfa1 % 112;
  #1#1 = x;
  |LEE 000#1=;
  |SI FSalida ! 0;
      #1#0 = alfa1 % 112;
      #1#2 = "NO EXISTE CUENTA EN MAESTRO";
  |FINSI;
  guarda_can = cantidad;
  guarda_can1 = cantidad1;
  |SI x = 1; cantidad = suma1; cantidad1 = suma11; suma1 = 0; suma11 = 0; |FINSI;
  |SI x = 2; cantidad = suma2; cantidad1 = suma12; suma2 = 0; suma12 = 0; |FINSI;
  |SI x = 3; cantidad = suma3; cantidad1 = suma13; suma3 = 0; suma13 = 0; |FINSI;
  |SI x = 4; cantidad = suma4; cantidad1 = suma14; suma4 = 0; suma14 = 0; |FINSI;
  |SI x = 5; cantidad = suma5; cantidad1 = suma15; suma5 = 0; suma15 = 0; |FINSI;
  |SI x = 6; cantidad = suma6; cantidad1 = suma16; suma6 = 0; suma16 = 0; |FINSI;
  sw_inf = 6;

  |SI cantidad ! 0; |O cantidad1  ! 0;  ||Gloria
       |PRINT;
  |FINSI;
  cantidad = guarda_can;
  cantidad1 = guarda_can1;
  llegoa = x;
|FPRC;

|PROCESO haycambio;
  x = act_nivel - 1;
|ET repetimos;
  |SI x = #0#18;  |O x = #0#19;  |O x = #0#20;  |O x = #0#21;
                  |O x = #0#22;  |O x = #0#23;
      carac = 0;
      |SI x = 6;   carac = dig9;  |FINSI;
      |SI x = 5;   carac = dig8;  |FINSI;
      |SI x = 4;   carac = dig7;  |FINSI;
      |SI x = 3;   carac = dig6;  |FINSI;
      |SI x = 2;   carac = dig5;  |FINSI;
      |SI x = 1;   carac = dig4;  |FINSI;
      |SI carac ! 0;
          nume1 = carac + 100;
          alfa1 = act_cuent % nume1;
          alfa2 = nue_cuent % nume1;
          |SI alfa1 ! alfa2;
              |HAZ sacacambio;
          |FINSI;
      |FINSI;
  |FINSI;
  x = x - 1;
  |SI x ! 0;  |VETE repetimos;  |FINSI;
|FPRC;

|PROCESO cambio1;
  x = llegoa - 1;
  |SI x [ 0;  |ACABA;  |FINSI;
|ET repeti1;
  |SI x = #0#18;  |O x = #0#19;  |O x = #0#20;  |O x = #0#21;
                  |O x = #0#22;  |O x = #0#23;
      carac = 0;
      |SI x = 6;   carac = dig9;  |FINSI;
      |SI x = 5;   carac = dig8;  |FINSI;
      |SI x = 4;   carac = dig7;  |FINSI;
      |SI x = 3;   carac = dig6;  |FINSI;
      |SI x = 2;   carac = dig5;  |FINSI;
      |SI x = 1;   carac = dig4;  |FINSI;
      |SI carac ! 0;
          nume1 = carac + 100;
          alfa1 = act_cuent % nume1;
          |HAZ sacacambio;
      |FINSI;
  |FINSI;
  x = x - 1;
  |SI x ! 0;  |VETE repeti1;  |FINSI;
  llegoa = nivelet;
|FPRC;

|PROCESO rup_tipo;
  |SI #5#0 = "H";
      doh = " H A B E R ";
  |FINSI;
  |PIEINF;
  tipo = #5#0;
|FPRC;

|PROCESO rup_agru;
  |SI ant_agru ! "";
      ant_impo = t_agru;
      ant_impo1  = t_agru1;
      raya1 = ray1;

   |SI ant_impo ! 0; |O ant_impo1 ! 0; |O swlagrup ! 0; || Gloria
      sw_inf = 10; |PRINT;  || raya
      sw_inf = 7;  |PRINT;  || agrupacion (con importe)
      sw_inf = 5;  |PRINT;  || linea blanco
   |FINSI;

  |FINSI;
  agrupaci = #5#2;

  t_agru = 0;
  t_agru1 = 0;
  swlagrup = 0;
|FPRC;

|PROCESO rup_epig;
  |SI ult_nivel ! 1;
      nue_cuent = "";
      nue_nivel = 0;
  |FINSI;

  |SI ant_epig ! "";
      ant_impo = t_epig;
      ant_impo1 = t_epig1;
      raya1 = ray2;

   |SI ant_impo ! 0; |O ant_impo1 ! 0; |O swlepigr ! 0;
      sw_inf = 10; |PRINT;  || raya
      sw_inf = 2;  |PRINT;  || epigrafe
      sw_inf = 5;  |PRINT;  || linea blanco
   |FINSI;

  |FINSI;
  epigrafe = #5#3;
  partida = #5#4;

  t_epig = 0;
  t_epig1 = 0;
  ult_cuent = "";
  ult_nivel = 0;
  swlepigr  = 0;
|FPRC;

|PROCESO imprires;
  |SI tipo = "D";
      |SI ant_impo [ 0;
          ant_impo = -ant_impo;
      |SINO;
          ant_impo = 0;
      |FINSI;
      |SI ant_impo1 [ 0;
          ant_impo1 = -ant_impo1;
      |SINO;
          ant_impo1 = 0;
      |FINSI;
  |SINO;
      |SI ant_impo [ 0;
          ant_impo = 0;
      |FINSI;
      |SI ant_impo1 [ 0;
          ant_impo1 = 0;
      |FINSI;
  |FINSI;
  |SI ant_impo = 0;  |Y ant_impo1 = 0;
      |ACABA;
  |FINSI;
  raya1 = ray3;
  sw_inf = 10; |PRINT;  || raya
  sw_inf = 7;  |PRINT;  || agrupacion (con importe)
  sw_inf = 5;  |PRINT;  || linea blanco
  ant_agru = antes;
|FPRC;

|PROCESO beneperdi;
  gru_nou = #5#1;
  |SI #5#0 ! tipo;  || ... para que salte todos
      gru_nou = 9999;
  |FINSI;
  |SI tipo = "D";
      |SI grupo = 1;
          antes = ant_agru;
          ant_impo = saldo1;
          ant_impo1 = saldo11;
          ant_agru = "BENEFICIOS DE LA EXPLOTACION";
          |HAZ imprires;
          grupo = grupo + 1;
          |SI gru_nou = grupo;
              |ACABA;
          |FINSI;
      |FINSI;
      |SI grupo = 2;
          antes = ant_agru;
          ant_impo = saldo2;
          ant_impo1 = saldo12;
          ant_agru = "RESULTADOS FINANCIEROS POSITIVOS";
          |HAZ imprires;
          antes = ant_agru;
          ant_impo = saldo1 + saldo2;
          ant_impo1 = saldo11 + saldo12;
          ant_agru = "BENEFICIOS DE LAS ACTIVIDADES ORDINARIAS";
          |HAZ imprires;
          grupo = grupo + 1;
          |SI gru_nou = grupo;
              |ACABA;
          |FINSI;
      |FINSI;
      |SI grupo = 3;
          antes = ant_agru;
          ant_impo = saldo3;
          ant_impo1 = saldo13;
          ant_agru = "RESULTADOS EXTRAORDINARIOS POSITIVOS";
          |HAZ imprires;
          antes = ant_agru;
          ant_impo = saldo1 + saldo2 + saldo3;
          ant_impo1 = saldo11 + saldo12 + saldo13;
          ant_agru = "BENEFICIOS ANTES DE IMPUESTOS";
          |HAZ imprires;
          grupo = grupo + 1;
          |SI gru_nou = grupo;
              |ACABA;
          |FINSI;
      |FINSI;
      |SI grupo = 4;
          antes = ant_agru;
          ant_impo = saldo1 + saldo2 + saldo3 + saldo4;
          ant_impo1 = saldo11 + saldo12 + saldo13 + saldo14;
          ant_agru = "RESULTADO DEL EJERCICIO (BENEFICIOS)";
          |HAZ imprires;
          grupo = grupo + 1;
      |FINSI;
  |SINO;
      |SI grupo = 1;
          antes = ant_agru;
          ant_impo = saldo1;
          ant_impo1 = saldo11;
          ant_agru = "PERDIDAS DE LA EXPLOTACION";
          |HAZ imprires;
          grupo = grupo + 1;
          |SI gru_nou = grupo;
              |ACABA;
          |FINSI;
      |FINSI;
      |SI grupo = 2;
          antes = ant_agru;
          ant_impo = saldo2;
          ant_impo1 = saldo12;
          ant_agru = "RESULTADOS FINANCIEROS NEGATIVOS";
          |HAZ imprires;
          antes = ant_agru;
          ant_impo = saldo1 + saldo2;
          ant_impo1 = saldo11 + saldo12;
          ant_agru = "PERDIDAS DE LAS ACTIVIDADES ORDINARIAS";
          |HAZ imprires;
          grupo = grupo + 1;
          |SI gru_nou = grupo;
              |ACABA;
          |FINSI;
      |FINSI;
      |SI grupo = 3;
          antes = ant_agru;
          ant_impo = saldo3;
          ant_impo1 = saldo13;
          ant_agru = "RESULTADOS EXTRAORDINARIOS NEGATIVOS";
          |HAZ imprires;
          antes = ant_agru;
          ant_impo = saldo1 + saldo2 + saldo3;
          ant_impo1 = saldo11 + saldo12 + saldo13;
          ant_agru = "PERDIDAS ANTES DE IMPUESTOS";
          |HAZ imprires;
          grupo = grupo + 1;
          |SI gru_nou = grupo;
              |ACABA;
          |FINSI;
      |FINSI;
      |SI grupo = 4;
          antes = ant_agru;
          ant_impo = saldo1 + saldo2 + saldo3 + saldo4;
          ant_impo1 = saldo11 + saldo12 + saldo13 + saldo14;
          ant_agru = "RESULTADO DEL EJERCICIO (PERDIDAS)";
          |HAZ imprires;
          grupo = grupo + 1;
      |FINSI;
  |FINSI;
  grupo = #5#1;
|FPRC;

|PROCESO titulillo;
  |SI #5#0 = "D";
      |SI #5#1 = 1;
          raya11 = "GASTOS DE EXPLOTACION";
      |SINO;
          |SI #5#1 = 2;
              raya11 = "GASTOS FINANCIEROS";
          |SINO;
              |SI #5#1 = 3;
                  raya11 = "GASTOS EXTRAORDINARIOS";
              |SINO;
                  raya11 = "";
              |FINSI;
          |FINSI;
      |FINSI;
  |SINO;
      |SI #5#1 = 1;
          raya11 = "INGRESOS DE EXPLOTACION";
      |SINO;
          |SI #5#1 = 2;
              raya11 = "INGRESOS FINANCIEROS";
          |SINO;
              |SI #5#1 = 3;
                  raya11 = "INGRESOS EXTRAORDINARIOS";
              |SINO;
                  raya11 = "";
              |FINSI;
          |FINSI;
      |FINSI;
  |FINSI;
  sw_inf = 11;

  |PRINT;
|FPRC;

|PROCESO impresio;

||  |SI #5#11 = 0; |Y #5#12 = 0;  |ACABA;  |FINSI;

  cantidad = #5#11;
  cantidad1 = #5#12;
  |SI sw = 0;
      tipo = #5#0;
      grupo = #5#1;
      agrupaci = #5#2;
      epigrafe = #5#3;
      partida = #5#4;
      |SI #5#0 = "D";
          doh = "  D E B E";
      |SINO;
          doh = " H A B E R ";
      |FINSI;
      act_cuent = #5#5;
      act_nivel = #5#6;
      sw = 1;
      swlepigr = 0;
      swlagrup = 0;
      |HAZ titulillo;
  |FINSI;

  nue_cuent = #5#5;
  nue_nivel = #5#6;

  |HAZ haycambio;


  |SI tipo ! #5#0;
      |HAZ cambio1;
      |HAZ rup_epig;
      |HAZ rup_agru;
      |HAZ beneperdi;
      |HAZ rup_tipo;
      |HAZ titulillo;

  |SINO;
      |SI grupo ! #5#1;
          |HAZ cambio1;
          |HAZ rup_epig;
          |HAZ rup_agru;
          |HAZ beneperdi;
          |HAZ titulillo;
      |SINO;
          |SI agrupaci ! #5#2;
              |HAZ cambio1;
              |HAZ rup_epig;
              |HAZ rup_agru;
          |SINO;
              |SI epigrafe ! #5#3;
                  |HAZ cambio1;
                  |HAZ rup_epig;
              |FINSI;
          |FINSI;
       |FINSI;
  |FINSI;
  act_cuent = #5#5;
  act_nivel = #5#6;
  tipo = #5#0;
  grupo = #5#1;
  agrupaci = #5#2;
  epigrafe = #5#3;
  partida = #5#4;

  #1#0 = #5#5;
  #1#2 = #5#7;
  sw_inf = 6;

   |SI #5#11 ! 0; |O #5#12 ! 0;  ||Gloria
       |PRINT;
       swlepigr = 1;
       swlagrup = 1;
   |FINSI;

  llegoa = nivelet;
  suma1 = suma1 + cantidad;
  suma2 = suma2 + cantidad;
  suma3 = suma3 + cantidad;
  suma4 = suma4 + cantidad;
  suma5 = suma5 + cantidad;
  suma6 = suma6 + cantidad;
  suma11 = suma11 + cantidad1;
  suma12 = suma12 + cantidad1;
  suma13 = suma13 + cantidad1;
  suma14 = suma14 + cantidad1;
  suma15 = suma15 + cantidad1;
  suma16 = suma16 + cantidad1;
  nue_cuent = #5#5;
  nue_nivel = #5#6;
  ant_agru = #5#8;
  |QBF ant_agru;

  ant_epig = #5#9;
  |QBF ant_epig;

  t_agru = t_agru + #5#11;
  t_agru1 = t_agru1 + #5#12;
  t_epig = t_epig + #5#11;
  t_epig1 = t_epig1 + #5#12;
|FPRC;

|DEFBUCLE imprimiendo;
 #5#1;
 ;
"D", 1, 01, 00, 00, "            ", 0;
"H", 4, 99, 99, 99, "zzzzzzzzzzzz", 9;
 ;
 NULL, impresio;
|FIN;

|PROCESO final;
  |HAZ cambio1;
  |HAZ rup_epig;
  |HAZ rup_agru;
  #5#1 = 9999;
  |HAZ beneperdi;
  |HAZ rup_tipo;
|FPRC;

|| *************************************************************************

|PROCESO let_nivel;
  |SI nivelet = 0;
      nivelet = #0#18;
  |FINSI;
  |PARA x = 23;  |SI x ] 18;  |HACIENDO x = x - 1;
        |SI nivelet < #0x;
            nivelet = #0x;
        |FINSI;
  |SIGUE;
|FPRC;

|PROCESO acortacuen;
  |SI nivelet < #1#1;
      |SI nivelet = 1;  traraf = 100 + dig4;  |FINSI;
      |SI nivelet = 2;  traraf = 100 + dig5;  |FINSI;
      |SI nivelet = 3;  traraf = 100 + dig6;  |FINSI;
      |SI nivelet = 4;  traraf = 100 + dig7;  |FINSI;
      |SI nivelet = 5;  traraf = 100 + dig8;  |FINSI;
      #1#0 = #1#0 % traraf;
      #1#1 = nivelet;
      #1#0 = #1#0 + "              ";
      #1#0 = #1#0 % 112;
  |FINSI;
|FPRC;

|| *************************************************************************
|PROCESO cargatra;
  |HAZ acortacuen;
  #5#0 = #1#5;
  #5#1 = #1#4;
  #5#2 = #1#6;
  #5#3 = #1#7;
  #5#4 = #1#8;
  #5#5 = #1#0;
  #5#6 = #1#1;
  #5#7 = #1#2;
  |LEE 000#5=;
  |SI FSalida ! 0;
      |DDEFECTO #5;
      #5#0 = #1#5;
      #5#1 = #1#4;
      #5#2 = #1#6;
      #5#3 = #1#7;
      #5#4 = #1#8;
      ||  |HAZ acortacuen;
      #5#5 = #1#0;
      #5#6 = #1#1;
      #5#7 = #1#2;
      #5#11 = 0;
      #5#12 = 0;
      |GRABA 020#5c;
  |FINSI;
  |DDEFECTO #2;
  #2#0 = #1#4;
  #2#1 = #1#5;
  #2#2 = #1#6;
  #2#4 = #1#7;
  #2#6 = #1#8;
  |LEE 000#2=;
  |SI FSalida ! 0;
      #2#3 = "";
      #2#5 = "";
      #2#7 = "";
  |FINSI;
  #5#8 = #2#3;
  #5#9 = #2#5;
  #5#10 = #2#7;
|FPRC;

|PROCESO sumadeha;
  |SI acumula = 11;
      |SI #1#4 = 1;
          saldo1 = saldo1 + importe;
      |SINO;
          |SI #1#4 = 2;
              saldo2 = saldo2 + importe;
          |SINO;
              |SI #1#4 = 3;
                  saldo3 = saldo3 + importe;
              |SINO;
                  saldo4 = saldo4 + importe;
              |FINSI;
          |FINSI;
      |FINSI;
  |SINO;
      |SI #1#4 = 1;
          saldo11 = saldo11 + importe;
      |SINO;
          |SI #1#4 = 2;
              saldo12 = saldo12 + importe;
          |SINO;
              |SI #1#4 = 3;
                  saldo13 = saldo13 + importe;
              |SINO;
                  saldo14 = saldo14 + importe;
              |FINSI;
          |FINSI;
      |FINSI;
  |FINSI;
|FPRC;

|PROCESO calculit;
  |HAZ digitos2;
  |SI d_error ! 0;  |ACABA;  |FINSI;
  |SI #1#1 > nivelet;  |ACABA;  |FINSI;
  |SI #1#1 = 0; |ACABA; |FINSI;
  |SI #1#3 = "S";  |O #1#1 = nivelet;
      |HAZ cargatra;
      #5acumula = 0;
      |SI #1#5 = "D";
          |PARA x = eldesde; |SI x [ elhasta; |HACIENDO x = x + 3;
                impMoneda = #1x;
                |SI aMonedaA ! aMonedaC; |Y dos = 2; |HAZ ElCMoneda; |FINSI;
                #5acumula = #5acumula + impMoneda;
                importe   = impMoneda;
                |HAZ sumadeha;
          |SIGUE;
      |SINO;
          |PARA x = eldesde; |SI x [ elhasta; |HACIENDO x = x + 3;
                impMoneda = #1x;
                |SI aMonedaA ! aMonedaC; |Y dos = 2; |HAZ ElCMoneda; |FINSI;
                #5acumula = #5acumula + (impMoneda * -1);
                importe = impMoneda;
                |HAZ sumadeha;
           |SIGUE;
      |FINSI;
      |GRABA 020#5a;
      |LIBERA #5;
  |FINSI;
|FPRC;

|DEFBUCLE carga_ctas;
 #1#1;
5,4,6,7,8;
"            ", 0, "D", 1, 01, 00, 00;
"zzzzzzzzzzzz", 9, "H", 4, 99, 99, 99;
 e;
 NULL, calculit;
|FIN;

|PROCESO conexi; |TIPO 0;
  nCam1 = empre;
  nCam2 = ejerc;

  d_mes = #0#7;
  h_mes = #0#8;
  estru = #0#6;
  r_emp = empre;
  r_per = ejerc;
  r_eje = #30#26;
  r_nom = #30#1;
  |SUB_EJECUTA "caexicon.run";
  |SI #0#27 = "N"; |ACABA; |FINSI;
  r_emp = #0#24;
  r_per = #0#25;
  r_eje = anu1 - 2000; |SI r_eje < 0; r_eje = anu1 - 1900; |FINSI;
  r_nom = #0#26;
  |SUB_EJECUTA "caexicon.run";
  empre = nCam1;
  ejerc = nCam2;
  eaMoneda = aMonedaA; |HAZ MonedasI;
|FPRC;
|| *******************************************************************
|PROCESO borrades;
  #5#0 = #1#5;
  #5#1 = #1#4;
  #5#2 = #1#6;
  #5#3 = #1#7;
  #5#4 = #1#8;
  |HAZ acortacuen;
  #5#5 = #1#0;
  #5#6 = #1#1;
  #5#7 = #1#2;
  |LEE 000#5=;
  |SI FSalida = 0;
      |SI #5acumula ! 0;
          importe = -#5acumula;
          |SI #1#5 = "H"; importe = -importe; |FINSI;
          |HAZ sumadeha;
          #5acumula = 0;
          |GRABA 020#5a;
      |FINSI;
  |FINSI;
|FPRC;

|PROCESO lineabor;
  #1#0 = #7#5;
  #1#1 = #7#6;
  |LEE 000#1=;
  |SI FSalida = 0;
      |HAZ borrades;
  |FINSI;
  #1#0 = #7#8;
  #1#1 = #7#9;
  |LEE 000#1=;
  |SI FSalida = 0;
      |HAZ borrades;
  |FINSI;
|FPRC;

|PROCESO borractas;
  |ABRE #1;
  |ABRE #6;
  #6#0 = #0#6;
  |LEE 000#6=;
  |BUCLELIN 2lineabor#6;
  |CIERRA #1;
  |CIERRA #6;
|FPRC;

|PROCESO descarg;
  mesini = #0#7 + 11;              || Iniciales
  descar = #7mesini;
  mesini = #0#8 + 11;              || Finales
  descar = descar - #7mesini; || Iniciales - Finales

  |SI aMonedaC ! aMonedaA; |Y dos = 2;
      impMoneda = descar;
      |HAZ ElCMoneda;
      descar = impMoneda;
  |FINSI;

  |SI descar ] 0;
      #1#0 = #7#5;     || cuenta descarga D
      #1#1 = #7#6;     || digito descarga D
  |SINO;
      #1#0 = #7#8;     || cuenta descarga H
      #1#1 = #7#9;     || digito descarga H
  |FINSI;

  |SI descar = 0; |ACABA; |FINSI;
  |HAZ digitos2;
  |SI d_error ! 0;  |ACABA;  |FINSI;

  |LEE 000#1=;             || lee cta. descarga
  |SI FSalida = 0;
      |HAZ cargatra;
      |SI #1#5 = "D";
          #5acumula = #5acumula + descar;
      |SINO;
          #5acumula = #5acumula - descar;
      |FINSI;
      importe = descar;

      |HAZ sumadeha;
      |GRABA 020#5a;
      |LIBERA #5;
  |FINSI;
|FPRC;

|PROCESO lineades;
  #1#0 = #7#2;
  #1#1 = #7#3;
  |LEE 000#1=;
  |SI FSalida = 0;
      |HAZ descarg;
  |FINSI;
|FPRC;

|PROCESO cargades;
  |ABRE #1;
  |ABRE #2;
  |ABRE #6;
  #6#0 = #0#6;
  |LEE 000#6=;
  |BUCLELIN 2lineades#6;
  |CIERRA #1;
  |CIERRA #2;
  |CIERRA #6;
|FPRC;

|PROCESO Borrar;
  sw = 0;
  saldo1  = 0;  saldo2  = 0;  saldo3  = 0;  saldo4  = 0;
  saldo11 = 0;  saldo12 = 0;  saldo13 = 0;  saldo14 = 0;
  suma1 = 0;
  suma2 = 0;
  suma3 = 0;
  suma4 = 0;
  suma5 = 0;
  suma6 = 0;
  suma11 = 0;
  suma12 = 0;
  suma13 = 0;
  suma14 = 0;
  suma15 = 0;
  suma16 = 0;
  tipo = "";
  t_agru = 0;
  t_agru1 = 0;
  t_epig = 0;
  t_epig1 = 0;
  grupo = 0;
  gru_nou = 0;
  agrupaci = 0;
  epigrafe = 0;
  partida = 0;
|FPRC;
____________________________________/ CALCULO PRINCIPAL
|PROCESO HagoTodo;
  |HAZ Borrar;
  eldesde = (#0#0 * 3 ) + 11;
  elhasta = (#0#1 * 3 ) + 11;

  |ABRE #8;
  |LEE 111#8p;
  |SI #8#2 = "S";  |Y #8#3 = "N";  |Y aparam = "";
      |DFICE pathbal;
      balance = 1;
      |CIERRA #8;
      |SUB_EJECUTA "careccue";
  |FINSI;
  |CIERRA #8;

  r_emp = empre;
  r_per = ejerc;
  |SI aparam = ""; |SUB_EJECUTA "carecsig"; |FINSI;

  dos     = 1;
  acumula = 11;

  |ABRE #5;
  |ABRE #2;
  |BUCLE carga_ctas;
  |CIERRA #2;
  |HAZ borractas;

  dos     = 1;
  acumula = 11;
  |HAZ cargades;
  |CIERRA #5;

  |SI #0#27 = "S";
       |ABRE #30;
       #canempre#2 = #0#24;
       #canempre#3 = #0#25;
       |LEE 011#30=;
       |CIERRA #30;
       enEmpresa = #0#24;
       enPeriodo = #0#25;
       |HAZ LeeMonedaI;
       aMonedaC = eaMoneda;

       |DBASE dir_fich;
       dir_fich = dir_fich + "fich/";
       alfa1 = #0#24;   alfa1 = "00000" + alfa1;   alfa1 = alfa1 % -105;
       alfa2 = #0#25;   alfa2 = "0"   + alfa2;   alfa2 = alfa2 % -101;
       dir_ficha = dir_fich + alfa1 + alfa2 + "/";

      |PATH_DAT #1 dir_ficha;
      |PATH_DAT #2 dir_ficha;
      |PATH_DAT #6 dir_ficha;
      |PATH_DAT #7 dir_ficha;
      |PATH_DAT #8 dir_ficha;
      |ABRE #8;
      |LEE 111#8p;
      |SI #8#2 = "S";  |Y #8#3 = "N";  |Y aparam = "";
          pathbal = dir_ficha;
          balance = 1;
          |CIERRA #8;
          |SUB_EJECUTA "careccue.tab";
      |FINSI;
      |CIERRA #8;

      r_emp = #0#24;
      r_per = #0#25;
      |SI aparam = ""; |SUB_EJECUTA "carecsig.run"; |FINSI;

      eaMoneda = aMonedaC; |HAZ MonedasI;
      |SI aMonedaA = "E"; EURODB = 2; |FINSI;
      dos = 2;
      acumula = 12;

      |ABRE #5;
      |ABRE #2;
      |BUCLE carga_ctas;
      |CIERRA #2;
      |HAZ borractas;

      dos     = 2;
      acumula = 12;
      |HAZ cargades;
      |CIERRA #5;

      |ABRE #30;
      #canempre#2 = empre;
      #canempre#3 = ejerc;
      |LEE 011#30=;
      |CIERRA #30;
      |SI #0#27 = "S"; eaMoneda = aMonedaA; |HAZ MonedasI; |FINSI;
  |FINSI;

  swlepigr = 0;
  swlagrup = 0;
  |ABRE #1;
  |BUCLE imprimiendo;
  |HAZ final;
  |CIERRA #1;
|FPRC;


|PROCESO PorSesiones;
   eaDepSes = #110#1;
   eaDesDep = #110#7;

   alfa1 = #110#6; |QBF alfa1;
   |SI alfa1 ! "";
       eaFicbal  = alfa1;
       |NOME_DAT #1 eaFicbal;
       |HAZ HagoTodo;
   |FINSI;
|FPRC;

|DEFBUCLE PorSesiones;
 #110#1;
 ;
 enNumSes, "  ";
 enNumSes, "zz";
 ;
 NULL, PorSesiones;
|FIN;

|PROCESO impre; |TIPO 3;

  Ejercicio = #30#26 + 2000;
  |SI #30#26 > 80; Ejercicio = #30#26 + 1900; |FINSI;

  aMonedaA = eaMoneda;
  |SI #0#27 = "N"; aMonedaC = aMonedaA; |FINSI;
  |HAZ let_nivel;
  |HAZ antesque;
  |HAZ conexi;

  aFichero = ("5c" + Usuario) % 108;
  |NOME_DAT #5 aFichero;

  |ABRE #5; |CIERRA #5; |DELINDEX #5;

  |IMPRE 0;
  |SI FSalida ! 0;  |ACABA;  |FINSI;
  |HAZ los_informes;
  |SI #0#27 = "S";
      informe = que_i1;
  |SINO;
      informe = que_i2;
  |FINSI;
  |INFOR informe;
  |SI FSalida ! 0;  |FINIMP;  |ACABA;  |FINSI;

  |SI enMasDep = 0;
      |HAZ HagoTodo;
  |SINO;
      |BUCLE PorSesiones;
      eaDepSes = "TODOS";
  |FINSI;
  |FININF;
  |FINIMP;
|FPRC;


||************************************************************************
|PROCESO Empresas;
     #48#2 = #cabucem1#0;
     #48#3 = #cabucem1#1;
     |LEE 000#48=;
     |SI FSalida = 0;
        |DBASE fich_alta; |QBF fich_alta;
        fich_alta = fich_alta + "fich/" + (("00000" + #cabucem1#0) % -105) + #cabucem1#1 + "/";
        |PATH_DAT #0 fich_alta; |PATH_DAT #1 fich_alta;
        |PATH_DAT #2 fich_alta; |PATH_DAT #8 fich_alta;
        |HAZ inicio;

        |DDEFECTO #cacaexpl;
        |SI Per1 ] 0; |O Per2 ] 0;
           #cacaexpl#0  = Per1;           #cacaexpl#1  = Per2;
        |SINO;
           #cacaexpl#0  = #48#11;         #cacaexpl#1  = #48#12;
        |FINSI;
        #cacaexpl#17 = #48#13;         #cacaexpl#18 = #48#14;
        #cacaexpl#19 = #48#15;         #cacaexpl#20 = #48#16;
        #cacaexpl#21 = #48#17;         #cacaexpl#22 = #48#18;
        #cacaexpl#23 = #48#19;

        |HAZ impre;

        |DBASE Comodin1; |QBF Comodin1;
        Comodin1 = Comodin1 + "hacienda/PresLibr/" + (("00000" + #cabucem1#0) % -105) + #cabucem1#1 + "/";
        Comodin1 = Comodin1 + "INV_CUEN*.xls";
        |_PDIR Comodin1;
        |SI FSalida = 0;
           |PARA; |SI FSalida = 0; |HACIENDO;
              Comodin = Comodin1 % -107; Comodin = Comodin % 0103;
              Numeracion = Comodin; Numeracion = Numeracion + 1;
              |_SDIR Comodin1;
           |SIGUE;
        |SINO;
           Numeracion = 1;
        |FINSI;

        Comodin = eaUnidadBTmp + "/tmp1.xls";
        |XABRE "C", Comodin, Handle;
        |SI FSalida ] 0;
           |XCIERRA Handle;
           |DBASE Comodin1; |QBF Comodin1;
           Comodin1 = Comodin1 + "hacienda/PresLibr/";
           |MKDIR Comodin1;
           Comodin1 = Comodin1 + (("00000" + #cabucem1#0) % -105) + #cabucem1#1 + "/";
           |MKDIR Comodin1;
           Comodin1 = Comodin1 + "INV_CUEN_" + (("000" + Numeracion) % -103) + ".xls";
           |RECIBE_FICHERO Comodin, Comodin1;
           Numeracion = Numeracion + 1;
           FSalida = 1;

           |PARA; |SI FSalida ! 0; |HACIENDO;
              |RFBORRA Comodin;
              |SI FSalida ! 0;
                 |MENAV "    Termine la tarea de Excel que tiene activa";
              |FINSI;
           |SIGUE;
        |SINO;
           |FABRE Comodin;
           |SI FSalida ] 0;
              FSalida = Handle;
              |FCIERRA FSalida;
              |DBASE Comodin1; |QBF Comodin1;
              Comodin1 = Comodin1 + "hacienda/PresLibr/";
              |MKDIR Comodin1;
              Comodin1 = Comodin1 + (("00000" + #cabucem1#0) % -105) + #cabucem1#1 + "/";
              |MKDIR Comodin1;
              Comodin1 = Comodin1 + "INV_CUEN" + (("000" + Numeracion) % -103) + ".xls";
              |COPIA_FICHERO Comodin, Comodin1;
              Numeracion = Numeracion + 1;
              FSalida = 1;

              |PARA; |SI FSalida ! 0; |HACIENDO;
                 |FBORRA Comodin;
                 |SI FSalida ! 0;
                    |MENAV "    Termine la tarea de Excel que tiene activa";
                 |FINSI;
              |SIGUE;
           |FINSI;
        |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE Empresas;
#cabucem1#1;
;
INICIO;
FINAL;
;
NULL,Empresas;
|FIN;

|PROGRAMA;
   aparam = PARAMETRO; |QBF aparam;
   |SI aparam = ""; |O aparam = "fech";
      |HAZ inicio;
      |CLS;
      |CABEZA "Balance de Perdidas y Ganancias II";
      |PINPA #0#0; |PINDA #0#0;
      |ENDATOS #1#0;
      |SI FSalida = 0;
         |HAZ impre;
      |FINSI;
   |SINO;
      Comodin = PARAMETRO;
      Comodin = Comodin - ",";
      |SI FSalida ! 0;
         Calc = ((FSalida / 100) ! 0) + 99;
         Comodin1 = Comodin % 0101;
         Per1 = Comodin1; Comodin = Comodin - Comodin1;
         Per2 = Comodin;
      |FINSI;
      Empresa = #48#2; Periodo = #48#3;
      |ABRE #48; |BUCLE Empresas; |CIERRA #48;
      #48#2 = Empresa;
      #48#3 = Periodo;
      |LEE 000#48=;
      |SI FSalida = 0;
         |DBASE fich_alta; |QBF fich_alta;
         fich_alta = fich_alta + "fich/" + (("00000" + #cabucem1#0) % -105) + #cabucem1#1 + "/";
         |PATH_DAT #0 fich_alta; |PATH_DAT #1 fich_alta;
         |PATH_DAT #2 fich_alta; |PATH_DAT #5 fich_alta;
         |PATH_DAT #6 fich_alta; |PATH_DAT #7 fich_alta;
         |PATH_DAT #8 fich_alta;
      |FINSI;
   |FINSI;
|FPRO;
