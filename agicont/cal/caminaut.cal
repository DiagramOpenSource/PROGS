|FICHEROS;
   caminaut #0;
   camingca #6;
   camingli #1;
   camanban #2;
   camanpag #3;
   camingau #4;
   caclipro #5;
   cacuenta #10;
   calisfra #20;
   caacrval #21;
|FIN;

|VARIABLES;
 &entrada = 1;

 alfa1    = "";
 mens     = "";
 swErrorR = 0;
 menR1    = "    Confirme YA emitido y/o contabilizado ";
 menR2    = "2400SConfirme YA Emitido existe. Aceptar S/N ";

 swpaso   = 0;
 cta      = 0;
 cta_t    = 0;
 dig_t    = 0;
 long     = "  ";
 sw       = 0;
 error    = 0;
 estado   = 0;
 linea    = 0;
 tot      = 0;
 sw_n     = 0;

 &quepant  = 1;
 &totrem   = 0;
 &contador = 0;
 &banco    = 0;
 &noremesa = 0;

 DTRec = 0; HTRec = 0; Cont = 0;
|FIN;

|INCLUYE acces3_i;
|INCLUYE punto2_i;
|INCLUYE i_ficher;

|| **********************************************************************
|PROCESO banco; |TIPO 0;
 #0#22 = #0#2; |PINTA #0#22;
 #0#23 = #0#2; |PINTA #0#23;
|FPRC;

|PROCESO mayus1;
 alfa1 = #0Campo > " ";
 #0Campo = alfa1;
 |PINTA #0Campo;
|FPRC;

|PROCESO contador; |TIPO 7;
 |SI swpaso = 0;
     |DDEFECTO #6;
     |ABRE #6;
     |LEE 040#6u;
     |SI FSalida = 0;
         #0#0 = #6#0 + 1;
     |SINO;
         #0#0 = 1;
     |FINSI;
     swpaso = 1;
     |CIERRA #6;
 |FINSI;
|FPRC;

|PROCESO ver_remesa; |TIPO 0;
 |SI FSalida = 2; |ACABA; |FINSI;
 |C_M #0#1, 1,"S"; |C_M #0#2, 1,"S";
 |C_M #0#4, 1,"S"; |C_M #0#21,1,"S";
 |C_M #0#11,1,"S"; |C_M #0#18,1,"S";

 |HAZ leerem;
 |SI swErrorR ! 0;
     |SI swErrorR = 1;
         |MENAV mens;
         |ERROR;
         |ACABA;
     |SINO;
         |CONFI mens;
         |SI FSalida ! 0;
             |ERROR;
             |ACABA;
         |FINSI;
     |FINSI;
     |C_M #0#1,1,"N"; |C_M #0#2,1,"N";
     |C_M #0#4,1,"N"; |C_M #0#21,1,"N";
     |C_M #0#11,1,"N"; |C_M #0#18,1,"N";
     |C_M #0#24,1,"N"; |C_M #0#25,1,"N";
     #0#1  = #6#2; |PINTA #0#1;   ||Fecha Vto
     #0#21 = #6#1; |PINTA #0#21;  ||Fecha Emision
     #0#2  = #6#3; |PINTA #0#3;   ||Banco
     |DEFECTO #2#0;
     |PINTA #0#3;
     #0#4  = #6#4; |PINTA #0#4;   ||Cuenta Contable
     #0#5  = #6#5;                ||Digito
     #0#11 = "N";  |PINTA #0#11;   ||Seleccion de tipos de recibo
     #0#24 = #6#7; |PINTA #0#24;  ||Desde Tipo de Recibo
     #0#25 = #6#7; |PINTA #0#25;  ||Hasta Tipo de Recibo
     #0#18 = #6#6; |PINTA #0#18;  ||Cod.en Entidad
 |FINSI;
|FPRC;

|PROCESO fecha; |TIPO 0;
 #0#10 = #0#1; |PINTA #0#10;
 #0#17 = #0#1; |PINTA #0#17;
|FPRC;

|PROCESO verfecha; |TIPO 0;
 |SI FSalida = 2; |ACABA; |FINSI;
 |SI Campo = 17;
     |SI #0#10 > #0#17;
         |MENAV "    FECHA ERRONEA!. ";
         |ERROR;
         |ACABA;
     |FINSI;
 |FINSI;

 |SI #0Campo < #0#21;
     |MENAV "    ATENCION!. Fecha Vencimiento menor a Fecha Emision Confirme";
     |ACABA;
 |FINSI;

 |SI #0Campo < #0#1;
     |MENAV "    ATENCION!. Fecha Vencimiento MENOR Fecha Vto. Confirme";
     |ACABA;
 |FINSI;
 |SI #0Campo > #0#1;
     |MENAV "    ANTENCION!. Fecha Vencimiento MAYOR Fecha Vto. Confirme";
     |ACABA;
 |FINSI;
|FPRC;

|PROCESO leerem;
 swErrorR = 0;
 |ABRE #6;
 |DDEFECTO #6;
 #6#0 = #0#0;
 |LEE 001#6=;
 estado = FSalida;
 |SI FSalida = 0;
     |SI #6#10 ! "N"; |O #6#11 ! "N";
         swErrorR = 1; mens = menR1;
     |SINO;
         swErrorR = 2; mens = menR2;
     |FINSI;
 |FINSI;
 |CIERRA #6;
|FPRC;

|PROCESO cuenta1; |TIPO 6;
 p_alfa1 = #0Campo;
 salidas = FSalida;
 |HAZ punto2;
 |SI p_error ! 0;  |ERROR;  |ACABA;  |FINSI;
 cta_t = Campo;
 dig_t = Campo + 1;
 |HAZ cuenta1_t;
|FPRC;

|PROCESO cuenta1_t;
   cta  = #0cta_t;
   long = cta % 0;
   sw   = 0;
|SI long = dig4; sw = 1; #0dig_t = 1; |FINSI;
|SI long = dig5; sw = 1; #0dig_t = 2; |FINSI;
|SI long = dig6; sw = 1; #0dig_t = 3; |FINSI;
|SI long = dig7; sw = 1; #0dig_t = 4; |FINSI;
|SI long = dig8; sw = 1; #0dig_t = 5; |FINSI;
|SI long = dig9; sw = 1; #0dig_t = 6; |FINSI;
|SI sw = 0;
    #0dig_t = 0;
    |MENAV "    Longitud de cuenta fuera de Nivel !";
    |ERROR;
    |ACABA;
|FINSI;
|FPRC;

|PROCESO cuefin1; |TIPO 0;
 error = 0;
 |ABRE #10;
 #10#0 = #0cta_t;
 #10#1 = #0dig_t;
 |LEE 040#10=;
 |SI FSalida ! 0; error = 1; |CIERRA #10; |ERROR; |ACABA; |FINSI;
 |SI #10#3 = "N";
     error = 1;
     |CIERRA #10;
     |MENAV "      La cuenta no tiene pase DIRECTO !";
     |ERROR;
     |ACABA;
 |FINSI;
 |CIERRA #10;
|FPRC;

|| **********************************************************************
|PROCESO grabali;
 linea = linea + 1;
 #1#0  = #0#0;        || Confirming
 #1#1  = linea;       || Linea
 #1#2  = #4#1;        || Libro
 #1#3  = #4#2;        || Orden
 #1#4  = #4#8;        || Serie
 #1#5  = #4#3;        || N§ Pago
 #1#6  = #4#9;        || Fecha Emision
 #1#7  = #4#4;        || Fecha Vto.
 #1#8  = #4#5;        || Importe
 #1#9  = #4#10;       || Cta Proveedor
 #1#10 = #4#11;       || Digito
 #1#11 = #4#15;       || Apdo. Correos
 #1#12 = #4#14;       || Forma de Pago
 #1#13 = #4#13;       || Nompre Proveedor

 || ... Actualiza en Alta
 |ABRE #3;
 #3#0 = #1#2;
 #3#1 = #1#3;
 #3#2 = #1#4;
 #3#3 = #1#5;
 |LEE 110#3=;
 |SI FSalida = 0;
     #3#25 = #1#0;      || N§ Confirming
     #3#22 = "S";       || Liquidado
     #3#23 = #3#23 + #1#8;
     #3#26 = #0#2;
     |GRABA 020#3a;
 |FINSI;
 |LIBERA #3;
 |CIERRA #3;

 #6#8 = #6#8 + 1;
 #6#9 = #6#9 + #1#8;

 || .................................
 |GRABA 020#1n;
 sw_n = 1;
|FPRC;

|DEFBUCLE caremesi;
 #4#1;
 7;
 000000,   "S";
 contador, "S";
 e;
 NULL, grabali;
|FIN;

|PROCESO linealin;  || Para saber cual es la ultima linea
 linea = #1#1;
|FPRC;

|PROCESO introduc;
 linea = 0;
 |SI estado = 0;
     |BUCLELIN 0linealin#6;
 |SINO;
     |DDEFECTO #6;
     #6#0 = #0#0;   || n§
     #6#1 = #0#21;  || Fecha Emision
     #6#2 = #0#1;   || Fecha Vto.
     #6#3 = #0#2;   || Banco
     #6#4 = #0#4;   || Cta Contable
     #6#5 = #0#5;   || Dig. Cuenta
     #6#6 = #0#18;  || Codigo Entidad
     #6#7 = #0#24;  || Tipo Recibo
 |FINSI;

 |ABRE #1;
 |BUCLE caremesi;
 |CIERRA #1;

 |SI sw_n = 1;
     |SALVA_DATOS #6;
     |ABRE #6;
     |DDEFECTO #6;
     #6#0 = #0#0;
     |LEE 001#6=;
     estado = FSalida;
     |REPON_DATOS #6;
     |SI estado = 0; |GRABA 020#6a; |FINSI;
     |SI estado ! 0; |GRABA 020#6n; |FINSI;
     |CIERRA #6;
 |FINSI;
|FPRC;

|| ****************************************************
|PROCESO pasalo;
 contador = contador + 1;
 #4#0  = contador;             || Contador
 #4#1  = #3#0;                 || Libro
 #4#2  = #3#1;                 || Orden
 #4#3  = #3#3;                 || N§ Pago
 #4#4  = #3#7;                 || Fecha Vto.
 #4#5  = #3#8 - #3#23;         || Importe
 #4#6  = #3#11;                || Tipo Recibo
 #4#7  = #0#6;                 || Marcar
 #4#8  = #3#2;                 || Serie
 #4#9  = #3#6;                 || Fecha Emision
 #4#10 = #3#4;                 || Proveedor
 #4#11 = #3#5;                 || Digito
 #4#12 = #3#8;                 || Importe Recibo Original
 #4#16 = #3#26;                || Banco
 |HAZ leecuenta;
 #4#13 = #5#1;                 || Nombre Proveedor
 #4#14 = "C";                  || Forma Pago
 #4#15 = #5#27;                || Apdo Correos

 |ABRE #calisfra;
 #calisfra#0 = #4#1;
 #calisfra#1 = #4#8;
 #calisfra#2 = #4#2;
 |LEE 010#calisfra.=;
 |SI FSalida = 0;
    |ABRE #caacrval;
    #caacrval#0 = #calisfra#3;
    #caacrval#1 = #calisfra#4;
    #caacrval#2 = #calisfra#5;
    |LEE 010#caacrval.=;
    |SI FSalida = 0;
       #4#13 = #caacrval#3;                 || Nombre Proveedor
       #4#15 = #caacrval#10;                || Apdo Correos
    |FINSI;
    |CIERRA #caacrval;
 |FINSI;
 |GRABA 040#4n;
 |CIERRA #calisfra;

|FPRC;

|DEFBUCLE camanpag;
 #3#3;
 0, 2, 1, 11, 4, 22, 26;
 #0#10, #0#9,  #0#7,  #0#36, #0#8,  DTRec, #0#12, "N", #0#22;
 #0#17, #0#16, #0#14, #0#37, #0#15, HTRec, #0#19, "N", #0#23;
 e;
 NULL, pasalo;
|FIN;
|| ***********************************************************************

|PROCESO pase; |TIPO 3;
 sw_n = 0;
 contador = 0;
 comienza = "q";
 |HAZ fichero;

 |NOME_DAT #4 fichero;
 |ABRE #4;
 |CIERRA #4;
 |DELINDEX #4;

 |HAZ leerem;  || Para saber si existe ya el confirming

 |ABRE #4;

 |SI #caminaut#11 = "N";
    DTRec = #caminaut#24; HTRec = #caminaut#25;
    |BUCLE camanpag;
 |SINO;
    |PARA Cont = 26; |SI Cont [ 35; |HACIENDO Cont = Cont + 1;
       DTRec = #caminaut#Cont#; HTRec = #caminaut#Cont#;
       |SI DTRec ! 0; |BUCLE camanpag; |FINSI;
    |SIGUE;
 |FINSI;

 |CIERRA #4;
 |SI contador  ! 0;
     |SUB_EJECUTA "camingau.tab";
     |SI noremesa = 0;
         |HAZ introduc;
     |FINSI;
 |SINO;
    |MENAV "      No hay pagos para el Confirme ";
 |FINSI;
 |DELINDEX #4;
|FPRC;

|| ***********************************************************************
|PROCESO leecuenta;
 |ABRE #5;
 #5#23 = "P";
 #5#10 = #3#4;
 #5#11 = #3#5;
 |LEE 040#5=;
 |SI FSalida ! 0;
     |DDEFECTO #5;
     #5#23 = "C";
     #5#10 = #3#4;
     #5#11 = #3#5;
     |LEE 040#5=;
     |SI FSalida ! 0;
         |DDEFECTO #5;
         |ABRE #10;
         #10#0 = #3#4;
         #10#1 = #3#5;
         |LEE 040#10=;
         |SI FSalida ! 0;
             |DDEFECTO #10;
         |FINSI;
         #5#1 = #10#2;
         |CIERRA #10;
     |FINSI;
 |FINSI;
 |CIERRA #5;
|FPRC;
|| ***********************************************************************

|PROCESO SelecTR;
   |SI #caminaut#11 = "S";
      |C_M #caminaut#24,1,"N"; |C_M #caminaut#25,1,"N";
      |PARA Cont = 26; |SI Cont [ 35; |HACIENDO Cont = Cont + 1;
         |C_M #caminaut#Cont#,1,"S";
      |SIGUE;
   |SINO;
      |C_M #caminaut#24,1,"S"; |C_M #caminaut#25,1,"S";
      |PARA Cont = 26; |SI Cont [ 35; |HACIENDO Cont = Cont + 1;
         |C_M #caminaut#Cont#,1,"N";
      |SIGUE;
   |FINSI;
|FPRC;
