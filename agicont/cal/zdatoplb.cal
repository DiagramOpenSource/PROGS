|FICHEROS;
     refer00@ #0;
|FIN;

|VARIABLES;
     aPrograma    = "";
     aDef         = "";
     aQueQuiero   = "";
     aNombreCampo = "";
     aFichero     = "";

     nCampo       = 0;
     nTCampo      = 0;
     nSwHay       = 0;

     &eaMoneda    = "";
     &enCanti     = 0;
     &eaCanti     = "";

     &EURODB      = 0;

     {-1}aMoneda   = "";
         aMoneda1  = "LEER";
         aMoneda2  = "";
|FIN;

|PROCESO PintaMoneda;  |TIPO 7;
     aPrograma = Control;  |QBF aPrograma;

     |DBASE aDef;  |QBF aDef;
     aDef   = aDef + "def/" + aPrograma + ".mas";
     |CARGA_DEF aDef, refer00@;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     aQueQuiero = "|NC";
     aFichero   = #0#aQueQuiero#;
     nTCampo    = aFichero;
     nTCampo    = nTCampo - 1;
     nSwHay     = 0;
     |PARA nCampo = 0;  |SI nCampo [ nTCampo;  |HACIENDO nCampo = nCampo + 1;
           aQueQuiero   = "|C" + (("000" + nCampo) % -103) + "NOMBRE";
           aNombreCampo = (#0#aQueQuiero# + (" " * 20)) % 120;
           |QBF aNombreCampo;
           |SI aNombreCampo = "***MONEDA***";
               nSwHay = 1;
               |SAL;
           |FINSI;
     |SIGUE;

     |SI nSwHay = 0;
         |DESCARGA_DEF #refer00@;
         |ACABA;
     |FINSI;

     IaMoneda = aMoneda1<-;
     |ESPECIFICA "MODO_VER_EURO", aMoneda;

     |SI EURODB ! 0;
         |SI aMoneda2 ! "1";
             #0nCampo = "EUROS";
         |SINO;
             #0nCampo = "PESETAS";
         |FINSI;
     |SINO;
         |SI aMoneda2 ! "1";
             #0nCampo = "PESETAS";
         |SINO;
             #0nCampo = "EUROS";
         |FINSI;
     |FINSI;

     |PINTA #0nCampo;
     eaMoneda = #0nCampo;

     |DESCARGA_DEF #refer00@;
|FPRC;

|PROCESO PintaMonedaSinCampo;  |TIPO 7;

     IaMoneda = aMoneda1<-;
     |ESPECIFICA "MODO_VER_EURO", aMoneda;

     |SI EURODB ! 0;
         |SI aMoneda2 ! "1";
             eaMoneda = "EUROS";
         |SINO;
             eaMoneda = "PESETAS";
         |FINSI;
     |SINO;
         |SI aMoneda2 ! "1";
             eaMoneda = "PESETAS";
         |SINO;
             eaMoneda = "EUROS";
         |FINSI;
     |FINSI;

     eaMoneda = (eaMoneda + "       ") % 107;
|FPRC;

|PROCESO ConBarras;

     |VARIABLES;
          aAlfa1 = "";
          aAlfa2 = "";
          nNume1 = 0;
          nNume2 = 0;
          nNume3 = 0;
          nPara1 = 0;
          nConDeci = 0;
          aConDeci = "";
          aBarra = "#";
     |FIN;

     enCanti = enCanti ! EURODB;
     |SI EURODB ! 0;
         |SI eaMoneda = "PESETAS";
             enCanti = ((enCanti * 166.386) ! 0);
             nConDeci = 0;
         |SINO;
             nConDeci = 1;
         |FINSI;
     |SINO;
         |SI eaMoneda = "EUROS  ";
             enCanti = ((enCanti / 166.386) ! 2);
             nConDeci = 1;
         |SINO;
             nConDeci = 0;
         |FINSI;
     |FINSI;

     |SI nConDeci ! 0;
          enCanti = enCanti * 100;
          aAlfa1 = enCanti;
          aAlfa1 = ("               " + aAlfa1) % -115;
          aConDeci = "," + (aAlfa1 % -102);
          aAlfa1 = aAlfa1 % 113;
          |QBT aAlfa1;
     |SINO;
          aAlfa1 = enCanti;
          aConDeci = "";
     |FINSI;

     aAlfa2 = aAlfa1 % 0;   nNume1 = aAlfa2;
     nNume2 = 0;
     eaCanti = "";
     |PARA nPara1 = nNume1; |SI nPara1 ] 1; |HACIENDO nPara1 = nPara1 - 1;
          nNume2 = nNume2 + 1;
          nNume3 = (nPara1 * 100) + 1;
          aAlfa2 = aAlfa1 % nNume3;
          |SI nNume2 = 4;
               eaCanti = "." + eaCanti;
               nNume2 = 1;
          |FINSI;
          eaCanti = aAlfa2 + eaCanti;
     |SIGUE;

     eaCanti = aBarra + eaCanti + aConDeci + aBarra;
|FPRC;
