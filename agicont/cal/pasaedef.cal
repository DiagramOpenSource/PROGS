|FICHEROS;
    pasaedef #0;
    caconcep #1;    || conceptos automaticos
    camaasic #2;    || apuntes cabs.
    camaasil #3;    || apuntes lins.
    cacuenta #4;    || plan contable
    cacuebal #15;   || cuentas de balance
    caclipro #16;   || clientes/proveedores

    pasbedef #21;   || tmp. renum. conceptos
    ca8m0002 #422;
    ca8cumay #410;
|FIN;

|VARIABLES;
    &enDiario   = 0;
    &enContador = 0;
    alfa1 = "";
    alfa2 = "";
    alfa3 = "";
    alfa4 = "";
    nume1 = 0;
    nume2 = 0;
    nume3 = 0;
    nume4 = 0;
    para1 = 0;
    para2 = 0;
    para3 = 0;
    para4 = 0;
    fech1 = @;
    fech2 = @;
    fech3 = @;
    fech4 = @;
    elsw1 = 0;
    elsw2 = 0;
    elsw3 = 0;
    elsw4 = 0;
    FEstado = 0;
    seleccio = 0;
    modo = 0;
    sw_rup = 0;

    fabre = "r   ";
    fcanl = "";
    field = "";
    ffich = "";
    fexit = 0;
    fcont = 0;
    sw_fabre = 0;
    digito = 0;
    ult_apunt = 0;
    docum = 0;
    impor = 0;
    conte = "";
    digcu = 0;
    conce = 0;
    fecha = @;
    descr = "";
    signe = "";
    mascara = "";
    sw = 0;
|FIN;

|INCLUYE acceso_i;
|INCLUYE actcta_i;
|INCLUYE actudi_i;
|INCLUYE varnue_i;
|INCLUYE defect_i;
____________________________________/CALCULOS DESDE CAMPOS
|PROCESO mayus; |TIPO 0;
    #0Campo = #0Campo > #0Campo;
|PINTA #0Campo;
|FPRC;

|PROCESO no0; |TIPO 0;
    alfa1 = #0#0;
|CAMPO_MODIFICA #0#3, 1, alfa1;
|FPRC;

|PROCESO no1; |TIPO 0;
    alfa1 = #0#1;
|CAMPO_MODIFICA #0#4, 1, alfa1;
|FPRC;

|PROCESO no2; |TIPO 0;
    alfa1 = #0#2;
|CAMPO_MODIFICA #0#5, 1, alfa1;
|FPRC;

|PROCESO instruc; |TIPO 0;
|SI #0#6 = "S";
    |IMPRE 0;
    |SI FSalida = 0;
        |INFOR "pasaedef";
        |PRINT;
        |PIEINF;
        |FININF;
    |SINO;
        |MENAV "    Proceso abortado ...";
    |FINSI;
    |FINIMP;
    |ERROR;
|FINSI;
|FPRC;
____________________________________/ IMPORTACION APUNTES
|PROCESO cabecera;
|DDEFECTO #2;
    #2#0 = #3#0;
    #2#1 = #3#1;
    #2#2 = #3#2;
|LEE 101#2=;
    FEstado = FSalida;
    #2#0 = #3#0;         || diario
    #2#1 = #3#1;         || fecha
    #2#2 = #3#2;         || documento
    #2#3 = #2#2;         || asiento
|SI #3#8 = "D";
        #2#4 = #2#4 + #3#9;
    |SI #2#09 = 0; #2#08 = #3#4; #2#09 = #3#5; |FINSI;      || contrp.HABER
|FINSI;
|SI #3#8 = "H";
        #2#5 = #2#5 + #3#9;
    |SI #2#11 = 0; #2#10 = #3#4; #2#11 = #3#5; |FINSI;      || contrp.DEBE
|FINSI;
|SI FEstado = 0;  |GRABA 020#2a;  |FINSI;
|SI FEstado ! 0;  |GRABA 020#2n;  |FINSI;
|LIBERA #2;
|FPRC;

|PROCESO apuntes;
|SI impor ! 0;|Y conte ! doce;|Y digcu ! 0;
    |DDEFECTO #3;
        #3#00 = 1;          || diario
        #3#01 = fecha;      || fecha
        #3#02 = docum;      || documento
        #3#03 = ult_apunt;  || apunte
        #3#04 = conte;      || cuenta
        #3#05 = digcu;      || digito cuenta
        #3#06 = #1#0;       || concepto
        #3#07 = descr;      || descripcion
        #3#08 = signe;      || signo
        #3#09 = impor;      || importe
    |SI #3#08 = "D";
            #3#16 = #3#4;
            #3#17 = #3#5;
    |SINO;
            #3#10 = #3#4;
            #3#11 = #3#5;
    |FINSI;
        #3#12 = "";           || cobros/pagos
        #3#13 = 0;            || libro
        #3#14 = 0;            || orden
        #3#15 = "";           || serie
        #3#22 = 0;            || nro.recibo
        #3#19 = "";           || tipo acumulador
        #3#20 = 0;            || numero acumulador
        #3#21 = "";           || departamento
    |GRABA 020#3n;
    |SI FSalida = 0;
        |HAZ cabecera;
            ressum = #3#9;
        |HAZ actualiz1;     || actualiza cuentas
            opera = "A";
        |HAZ altatra;       || actualiza niveles inferiores
            d_diari = #3#0;
            d_opera = #3#8;
            d_impor = #3#9;
        |HAZ actu_diari;    || actualiza diario
            ult_apunt = ult_apunt + 10;
    |FINSI;
|FINSI;
|FPRC;

|PROCESO las_pelas;
    alfa1 = field % 9012;    |QBT alfa1;
    alfa2 = alfa1 % 0;   para2 = alfa2;
    alfa3 = "";

|PARA para1 = 1; |SI para1 [ para2;     |HACIENDO para1 = para1 + 1;
        nume1 = (para1 * 100) + 1;
        alfa2 = alfa1 % nume1;
    |SI alfa2 ! ",";
            alfa3 = alfa3 + alfa2;
    |FINSI;
|SIGUE;

    impor = alfa3;
|FPRC;

|PROCESO el_signo;
    signe = field % 8201;
|SI signe = "+";    signe = "D";   |FINSI;
|SI signe = "-";    signe = "H";   |FINSI;
|FPRC;

|PROCESO la_cta;
    conte = field % 12712;    |QBT conte;
    conte = conte - &13;

    alfa1 = conte;  |QBF alfa1;
    alfa2 = alfa1 % 0;   nume1 = alfa2;
    digito = 0;
|SI nume1 = dig4;   digito = 1;    |FINSI;
|SI nume1 = dig5;   digito = 2;    |FINSI;
|SI nume1 = dig6;   digito = 3;    |FINSI;
|SI nume1 = dig7;   digito = 4;    |FINSI;
|SI nume1 = dig8;   digito = 5;    |FINSI;
|SI nume1 = dig9;   digito = 6;    |FINSI;
    digcu = digito;
|FPRC;

|PROCESO el_cto;
    #1#0 = 999;

    #21#0 = field % 4203;
|LEE 000#21=;
|SI FSalida = 0;
        #1#0 = #21#1;
    |LEE 000#1=;
|FINSI;
|FPRC;

|PROCESO la_fecha;
    alfa1 = field % 1302;
    nume1 = alfa1;  alfa1 = nume1;  alfa1 = ("00" + alfa1) % -102;
    alfa2 = field % 1602;
    nume2 = alfa2;  alfa2 = nume2;  alfa2 = ("00" + alfa2) % -102;
    alfa3 = field % 1904;
    alfa4 = alfa1 + "." + alfa2 + "." + alfa3;

    fecha = alfa4;       || la fecha
|FPRC;

|PROCESO apunte1;
|HAZ pintalo;

    sw = 0;

|HAZ la_fecha;
|HAZ el_cto;
|HAZ la_cta;
|SI digcu ! 0;
    |HAZ el_signo;
        descr = field % 5130;
    |HAZ las_pelas;
    |HAZ apuntes;
|SINO;
    |MENAV "    No corresponden los Niveles; proceso interrumpido ...";
        sw = 1;
|FINSI;
|FPRC;

|PROCESO apunte;
|CAMPO_ATRIBUTO #0#2, P, 0, 0;     |PINTA #0#2;

    ult_apunt = 1;
    enDiario  = 1;
    |HAZ contador;
    docum = enContador;

|ABRE #1;
|ABRE #2;
|ABRE #3;
|ABRE #21;
    ffich = #0#5;   |QBF ffich;
    ffich = fabre + ffich;

|FABRE ffich;
    fcanl = FSalida;
|SI FSalida ] 0;
    |PARA; |SI; |HACIENDO;
            field = fcanl + " 254";
        |FLEE field;
        |SI FSalida > 0;
                alfa1 = field % 803;
            |SI alfa1 = "199";
                |HAZ apunte1;
                |SI sw ! 0;   |SAL;     |FINSI;
            |FINSI;
        |SINO;
            |SAL;
        |FINSI;
    |SIGUE;
    |FCIERRA fcanl;
|FINSI;
|CIERRA #1;
|CIERRA #2;
|CIERRA #3;
|CIERRA #21;

|CAMPO_ATRIBUTO #0#2, D, 0, 0;     |PINTA #0#2;
|FPRC;
____________________________________/ IMPORTACION CUENTAS
|PROCESO cli_prov;
  alfa1 = #4#0 % 103;
  |SI alfa1 ! "430";  |Y alfa1 ! "440";  |Y alfa1 ! "400";  |Y alfa1 ! "410";
      |ACABA;
  |FINSI;
  |DDEFECTO #16;
  |SI alfa1 = "430";|O alfa1 = "440";
      #16#23 = "C";
  |SINO;
      #16#23 = "P";
  |FINSI;
  #16#0 = #4#0;              || codigo (cuenta)
  #16#1 = #4#2;              || nombre
  #16#2 = "";                || direccion
  #16#6 = "";                || poblacion
  #16#9 = field % 5812;      || cif
  #16#10 = #4#0;             || cuenta contable
  #16#11 = #4#1;             || digito
  |GRABA 020#16n;
|FPRC;


|PROCESO digito;
    alfa1 = #4#0;   |QBF alfa1;
    alfa2 = alfa1 % 0;   nume1 = alfa2;
    digito = 0;
|SI nume1 = dig4;   digito = 1;    |FINSI;
|SI nume1 = dig5;   digito = 2;    |FINSI;
|SI nume1 = dig6;   digito = 3;    |FINSI;
|SI nume1 = dig7;   digito = 4;    |FINSI;
|SI nume1 = dig8;   digito = 5;    |FINSI;
|SI nume1 = dig9;   digito = 6;    |FINSI;
|FPRC;

|PROCESO cuenta1;
|HAZ pintalo;

    sw = 0;
|DDEFECTO #4;

    #4#0 = field % 212;     || cuenta
|HAZ digito;
|SI digito ! 0;
        #4#1 = digito;             || digito
        #4#2 = field % 1536;       || descripcion
        #4#3 = field % 5601;       || pase directo

        mascara = #4#0;
    |QBF mascara;
        mascara = mascara +"zzzzzzzzzzzz";
        mascara = mascara % 112;

        #4#54 = mascara;
        #4#55 = #4#1;

    |HAZ defect;

    |GRABA 000#4n;
    |SI FSalida = 0;
        |HAZ cli_prov;
    |FINSI;
|SINO;
    |MENAV "    No corresponden los Niveles; proceso interrumpido ...";
        sw = 1;
|FINSI;
|FPRC;

|PROCESO cuenta;
|CAMPO_ATRIBUTO #0#1, P, 0, 0;     |PINTA #0#1;

    ffich = #0#4;   |QBF ffich;
    ffich = fabre + ffich;
|ABRE #4;
|ABRE #16;
|FABRE ffich;
    fcanl = FSalida;
|SI FSalida ] 0;
    |PARA; |SI; |HACIENDO;
            field = fcanl + " 254";
        |FLEE field;
        |SI FSalida > 0;
                alfa1 = field % 201;
            |SI alfa1 ] "1";|Y alfa1 [ "9";
                |HAZ cuenta1;
                |SI sw ! 0;   |SAL;     |FINSI;
            |FINSI;
        |SINO;
            |SAL;
        |FINSI;
    |SIGUE;
    |FCIERRA fcanl;
|FINSI;
|CIERRA #4;
|CIERRA #16;
    Pos = -1;

|CAMPO_ATRIBUTO #0#1, D, 0, 0;     |PINTA #0#1;
|FPRC;
____________________________________/ IMPORTACION DE CONCEPTOS
|PROCESO concep1;
|HAZ pintalo;

    sw = 0;
    conce = conce + 1;
|DDEFECTO #1;
    #1#0 = conce;
    #1#1 = field % 630;
|GRABA 000#1n;
|SI FSalida = 0;
    |DDEFECTO #21;
        #21#0 = field % 203;
        #21#1 = conce;
    |GRABA 020#21n;
|FINSI;
|FPRC;

|PROCESO concep;
|CAMPO_ATRIBUTO #0#0, P, 0, 0;     |PINTA #0#0;

    ffich = #0#3;   |QBF ffich;
    ffich = fabre + ffich;
|ABRE #1;
|ABRE #16;
|ABRE #21;
|FABRE ffich;
    fcanl = FSalida;
|SI FSalida ] 0;
    |PARA; |SI; |HACIENDO;
            field = fcanl + " 254";
        |FLEE field;
        |SI FSalida > 0;
            |SI FSalida = 38;
                |HAZ concep1;
                |SI sw ! 0;   |SAL;     |FINSI;
            |FINSI;
        |SINO;
            |SAL;
        |FINSI;
    |SIGUE;
    |FCIERRA fcanl;
|FINSI;
|CIERRA #1;
|CIERRA #16;
|CIERRA #21;
    Pos = -1;

|CAMPO_ATRIBUTO #0#0, D, 0, 0;     |PINTA #0#0;
|FPRC;
____________________________________/ CALCULO PRINCIPAL
|PROCESO pintalo;
|ATRI I;
    alfa1 = field % 250;
|PINTA 1919, alfa1;
|ATRI 0;
|FPRC;

|PROCESO tipo3;  |TIPO 3;
 |SI #30#26 < 80;
     enEjercicio = 2000 + #30#26;
 |SINO;
     enEjercicio = 1900 + #30#26;
 |FINSI;

|DELINDEX #21;
|SI #0#0 = "S";     |HAZ concep;   |FINSI;
|SI #0#1 = "S";     |HAZ cuenta;   |FINSI;
|SI #0#2 = "S";     |HAZ apunte;   |FINSI;
|DELINDEX #21;
|FPRC;
