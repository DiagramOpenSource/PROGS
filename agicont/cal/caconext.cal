|FICHEROS;
   caconext #0;
   caconcil #1;
   camaasil #2;
   caconex1 #3;
   caconcic #4;
   cacuenta #10;
|FIN;

|VARIABLES;
    &entrada = 1;
    cta      = 0;
    cta_t    = 0;
    dig_t    = 0;
    long     = "  ";
    sw       = 0;
    error    = 0;
    lin      = 0;
    aAlfa    = "";
    nDDiario = 0;
    nHDiario = 0;
|FIN;

|INCLUYE acceso_i;
|INCLUYE puntos_i;

|| *************************************************************************
||                  PROCESOS PARA SACAR LA CUENTA
|| *************************************************************************

|PROCESO cuenta; |TIPO 6;
     salidas = FSalida;
    p_alfa1 = #0Campo;
|HAZ punto;
|SI p_error ! 0;  |ERROR;  |ACABA;  |FINSI;
   cta_t = Campo;
   dig_t = Campo + 1;
   |HAZ cuenta_t;
|FPRC;

|PROCESO cuenta_t;
  cta  = #0cta_t;
  long = cta % 0;
  sw   = 0;

|SI long = dig4; sw = 1; #0dig_t = 1; |FINSI;
|SI long = dig5; sw = 1; #0dig_t = 2; |FINSI;
|SI long = dig6; sw = 1; #0dig_t = 3; |FINSI;
|SI long = dig7; sw = 1; #0dig_t = 4; |FINSI;
|SI long = dig8; sw = 1; #0dig_t = 5; |FINSI;
|SI long = dig9; sw = 1; #0dig_t = 6; |FINSI;

|SI sw = 0;
    #0dig_t = 0;
    |MENAV "    Longitud de cuenta fuera de Nivel !";
    |ERROR;
    |ACABA;
|FINSI;
|FPRC;

|PROCESO cuefin; |TIPO 0;
error = 0;
|ABRE #10;
#10#0 = #0cta_t;
#10#1 = #0dig_t;
|LEE 040#10=;
|SI FSalida ! 0; error = 1; |CIERRA #10; |ERROR; |ACABA; |FINSI;
|SI #10#3 = "N";
    error = 1;
    |CIERRA #10;
    |MENAV "      La cuenta no tiene pase DIRECTO !";
    |ERROR;
    |ACABA;
|FINSI;
#0#2 = #10#2;
|PINTA #0#2;
|CIERRA #10;
|FPRC;

|PROCESO no_dep; |TIPO 7;
    aAlfa = #canempre#24;
    |C_M #0#4,  1, aAlfa;
    |C_M #0#5,  1, aAlfa;
|FPRC;

|| *************************************************************************
||                       PROCESOS DEL EXTRACTO POR PANTALLA
|| *************************************************************************

|PROCESO diferenp;
   error = 0;
   |SI #1#3  = 1;
       #3#6  = #1#2;
       #3#7  = #1#6;
       #3#8  = #1#7;
       #3#9  = #1#4;
       #3#10 = "";
       #3#11 = 0;
       #3#12 = 0;
       #3#13 = 0;
   |SINO;
       #3#6  = #1#2;
       #3#7  = 0;
       #3#8  = 0;
       #3#9  = #1#4;
       #3#10 = "";
       #3#11 = 0;
       #3#12 = #1#6;
       #3#13 = #1#7;
   |FINSI;

   #3#26 = #1#9;
   #3#27 = 99;
   #3#14 = #3#14 + #3#7;
   #3#15 = #3#15 + #3#8;
   #3#16 = #3#16 + #3#12;
   #3#17 = #3#17 + #3#13;

   Pos = lin;
   |PINDA #0#3;
   Pos = -1;

   |PINTA #3#14;
   |PINTA #3#15;
   |PINTA #3#16;
   |PINTA #3#17;

   lin = lin + 100;
   |SI lin > 900; |HAZ cambipag; |FINSI;
|FPRC;

|PROCESO asientosp;
    error = 0;
    #3#6  = #2#3;
    #3#7  = 0;
    #3#8  = 0;
    #3#9  = #2#1;
    #3#10 = "";
    #3#11 = #2#2;
    #3#26 = #2#21;
    #3#27 = #2#0;
    |SI #2#8 = "D";
        #3#12 = #2#9;
        #3#13 = 0;
    |SINO;
        #3#13 = #2#9;
        #3#12 = 0;
    |FINSI;
    #3#14 = #3#14 + #3#7;
    #3#15 = #3#15 + #3#8;
    #3#16 = #3#16 + #3#12;
    #3#17 = #3#17 + #3#13;

    Pos = lin;
    |PINDA #0#3;
    Pos = -1;

    |PINTA #3#14;
    |PINTA #3#15;
    |PINTA #3#16;
    |PINTA #3#17;

    lin = lin + 100;
    |SI lin > 900; |HAZ cambipag; |FINSI;
|FPRC;

|PROCESO cambipag;
   error = 0;
   |PAUSA;
   |SI FSalida = 1; |O FSalida = 7;
       error = 1;
       |ERROR10;
       |ACABA;
   |FINSI;

   |HAZ PPantalla;
   lin = 100;
|FPRC;

|DEFBUCLE caconcilp;
   #1#1;
   8,9;
   #0#0, #0#1, 0001, " ", #0#4;
   #0#0, #0#1, 9999, " ", #0#5;
   e;
   NULL, diferenp;
|FIN;

|DEFBUCLE camaasilp;
   #2#3;
   18, 21, 0;
   #0#0, #0#1, fec1, 000000, " ", #0#4, nDDiario;
   #0#0, #0#1, fec2, 999999, " ", #0#5, nHDiario;
   e;
   NULL, asientosp;
|FIN;

|PROCESO PPantalla;
  |PINPA #0#3;

  |PINTA #3#0;
  |PINTA #3#1;
  |PINTA #3#2;
  |PINTA #3#3;
  |PINTA #3#4;
  |PINTA #3#5;

  |SI #canempre#24 = "S";
      |PINTA 0665, "Depar:    -   ";
      |PINTA 0672, #0#4;
      |PINTA 0677, #0#5;
  |FINSI;
|FPRC;

|PROCESO pantalla;
  error = 0;
  lin   = 100;

  #3#0 = #0#0;
  #3#1 = #0#2;
  #3#2 = #0#3;

  |HAZ PPantalla;

  |SI nDDiario [ 99; |Y nHDiario ] 99;
      |BUCLE caconcilp;
  |FINSI;
  |SI error = 0; |BUCLE camaasilp; |FINSI;

  |SI error = 0;
      |PAUSA;

      |PINPA #1#3;
      #3#18 = #3#3;
      #3#19 = #3#5;
      #3#20 = #3#14;
      #3#21 = #3#16;
      #3#22 = #3#15;
      #3#23 = #3#17;

      |SI #3#4 = "H"; #3#3 = 0 - #3#3; |FINSI;

      #3#24 = #3#18 - #3#20 + #3#22;
      #3#25 = #3#19 - #3#21 + #3#23;

      |PINDA #1#3;
      |PAUSA;
  |FINSI;

|FPRC;

|PROCESO saldo;
   |SI #2#8 = "D";
       #3#5 = #3#5 + #2#9;
   |SINO;
       #3#5 = #3#5 - #2#9;
   |FINSI;
|FPRC;

|DEFBUCLE camaasil;
  #2#3;
  21,0;
  #0#0, #0#1, fec1, 000000, #0#4, nDDiario;
  #0#0, #0#1, fec2, 999999, #0#5, nHDiario;
  e;
  NULL, saldo;
|FIN;

|PROCESO impre;|TIPO 3;
   nDDiario = #0#6;
   nHDiario = #0#7;

  |DDEFECTO #3;
  |ABRE #4;
  #4#0 = #0#0;
  #4#1 = #0#1;
  |LEE 040#4=;
  |SI FSalida ! 0; |DDEFECTO #4; |FINSI;
  |CIERRA #4;
  #3#3 = #4#4;
  #3#4 = #4#5;

  |BUCLE camaasil;

  |HAZ pantalla;
|FPRC;
