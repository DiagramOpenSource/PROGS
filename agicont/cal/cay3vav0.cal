|FICHEROS;
    cay3vav0 #0;
    caivarep #1;
    cay3vav1 #3;
    cacuenta #4;
|FIN;

|VARIABLES;
    &entrada = 1;
    sw = 0;
    cliente = 0;
    sw_desc = 0;
    sw_error = 0;
    lin = 0;
    opcion = 0;
    fecalf = "";
    men1   = "    NO EXISTE EMPRESA";
    mensa1 = "    ERROR ESTA FECHA NO CORRESPONDE AL EJERCICIO";
    traba1 = "";

    inver1 = "";
    inver2 = "";
    &ba = 0;
    &im = 0;
    &to = 0;
    &swlin = 0;
    informe = "cay3vav0";
    {-1}menu = "";
        menu1 = "2400";
        menu2 = "1";
        menu3 = " Elija opcion: ";
        menu4 = "PI";
        menu5 = " Pantalla ";
        menu6 = " Impresora ";
|FIN;

|INCLUYE acceso_i;
|INCLUYE puntos_i;
____________________________________/ CALCULOS DE PANTALLA
|PROCESO empieza; |TIPO 7;
|HAZ inicio;
    #0#6 = fec1;
    #0#7 = fec2;
|PINTA #0#6;
|PINTA #0#7;
|FPRC;

|PROCESO fechad; |TIPO 0;
|SI #0#6 < fec1; |O #0#6 > fec2;
    |MENAV mensa1;
    |ERROR;
|FINSI;
|FPRC;

|PROCESO fechah; |TIPO 0;
|SI #0#7 < fec1; |O #0#7 > fec2; |O #0#7 < #0#6;
    |MENAV mensa1;
    |ERROR;
|FINSI;
|FPRC;

|PROCESO periodoh;
|SI #0#13 < #0#12;
    |MENAV "0000Periodo hasta inferior a Periodo desde";
    |ERROR;
|FINSI;
|FPRC;

|PROGRAMA;
|CLS;
|CABEZA "Informe Facturas Clientes";
    FSalida = 1;

|PARA; |SI FSalida = 1; |HACIENDO;
    |PINPA #0#0;
    |DDEFECTO #0;
    |PINDA #0#0;                    || Pinta los datos por defecto
    |ENDATOS #1#0;                  || Seleccion informe
    |SI FSalida = 0;                || Si el conforme es igua a S
        |HAZ impre;                 || Calculo impresion
        |HAZ borrar;                || Borra acumulado
            FSalida = 1;            || Vuelve al bucle
    |SINO;
            FSalida = 0;
    |FINSI;
|SIGUE;
|FPRO;

|PROCESO impre;
 inver1 = #0#11;
 inver2 = #0#11;
 |SI #0#11 = "A";
     inver1 = " ";
     inver2 = "z";
 |FINSI;
|MENU menu;
    opcion = FSalida;
|SI opcion = 1;  |HAZ pantalla;   |FINSI;
|SI opcion = 2;  |HAZ impresora;  |FINSI;
|FPRC;

|PROCESO cuenta; |TIPO 0;
    p_alfa1 = #0Campo;
    salidas = FSalida;
|HAZ punto;
|SI p_error ! 0;  |ERROR;  |ACABA;  |FINSI;
|FPRC;
____________________________________/ POR IMPRESORA
|PROCESO bucleimp;
|SI sw = 0;
        cliente = #1#4;
        sw = 1;
|FINSI;

|SI #1#4 ! cliente;
        swlin = 1;
    |PRINT;
        swlin = 0;
        cliente = #1#4;
|FINSI;

|HAZ leecuen;
    swlin = 0;
    ba = #1#08+#1#09+#1#10+#1#52+#1#53;
    im = #1#14+#1#15+#1#16+#1#20+#1#21+#1#22+#1#56+#1#57+#1#60+#1#61;
    to = ba + im;
|PRINT;
|FPRC;

|DEFBUCLE cay3vav00;
 #1#2;
 3,48, 44;
 #0#2, 0, #0#0,  #0#9, #0#4, #0#6, inver1, #0#12;
 #0#3, 9, #0#1, #0#10, #0#5, #0#7, inver2, #0#13;
 e;
 NULL, bucleimp;
|FIN;

|PROCESO impresora;
|IMPRE 0;
|SI FSalida = 0;
    |INFOR informe;
    |BUCLE cay3vav00;
    |SI sw = 1;  |PIEINF;   |FINSI;
    |FININF;
|SINO;
    |MENAV "    No existe impresora";
|FINSI;
|FINIMP;
|FPRC;
____________________________________/ POR PANTALLA
|PROCESO buclepan;
  |SI sw = 0;
      cliente = #1#4;
      sw = 1;
  |FINSI;

  |SI #1#4 ! cliente;
      sw_desc = 1;
      |HAZ cambiapa;
      |SI sw_error = 1; |VETE finbu; |FINSI;
      cliente = #1#4;
  |FINSI;

  |SI lin > 1100;
      sw_desc = 0;
      |HAZ cambiapa;
  |FINSI;

|HAZ leecuen;

    #3#0 = #0#8;
    #3#1 = #1#4;
    #3#2 = #4#2;
    #3#3 = #1#0;
    #3#4 = #1#27;
    #3#5 = #1#2;
    #3#6 = #1#3;
    #3#7 = #1#7;
    #3#8 = #1#08+#1#09+#1#10+#1#52+#1#53;
    #3#9 = #1#14+#1#15+#1#16+#1#20+#1#21+#1#22+#1#56+#1#57+#1#60+#1#61;
    #3#10 = #3#8 + #3#9;
    #3#11 = "TOTAL PARCIAL DEL CLIENTE : " + #3#1;
    #3#12 = #3#12 + #3#8;
    #3#13 = #3#13 + #3#9;
    #3#14 = #3#14 + #3#10;
    #3#15 = #1#48;

|PINTA #3#0;
|PINTA #3#1;
|PINTA #3#2;

    Pos = lin;
|PINDA #0#3;
    Pos = -1;

|PINTA #3#11;
|PINTA #3#12;
|PINTA #3#13;
|PINTA #3#14;

    lin = lin + 100;
|SI lin > 1100;
        sw_desc = 0;
    |HAZ cambiapa;
|FINSI;
||    lin = lin + 100;
|ET finbu;
|FPRC;

|DEFBUCLE cay3vav01;
 #1#2;
 3, 48, 44;
 #0#2, 0, #0#0,  #0#9, #0#4, #0#6, inver1, #0#12;
 #0#3, 9, #0#1, #0#10, #0#5, #0#7, inver2, #0#13;
 e;
 NULL,buclepan;
|FIN;

|PROCESO pantalla;
    lin = 100;
|PINPA #0#3;
|BUCLE cay3vav01;
|SI sw = 1;|Y sw_error = 0;
        #3#11 = "TOTAL DEL CLIENTE : " + cliente;
    |PINTA #3#11;
    |DDEFECTO #3;
    |PAUSA;
|FINSI;
|FPRC;

|PROCESO cambiapa;
    sw_error = 0;
|SI sw_desc = 1;
        #3#11 = "TOTAL DEL CLIENTE : " + cliente;
    |PINTA #3#11;
    |DDEFECTO #3;
        sw_desc = 0;
|FINSI;
|PAUSA;
|SI FSalida = 1; |O FSalida = 7;
    |ERROR10;
        sw_error = 1;
|FINSI;
|PINPA #0#3;
    lin = 100;
|FPRC;
____________________________________/ RUTINAS
|PROCESO leecuen;
|ABRE #4;
    #4#0 = #1#4;
    #4#1 = #1#5;
|LEE 001#4=;
|SI FSalida ! 0;
    |DDEFECTO #4;
|FINSI;
|CIERRA #4;
|FPRC;

|PROCESO borrar;
|DDEFECTO #3;
    sw = 0;
|FPRC;
