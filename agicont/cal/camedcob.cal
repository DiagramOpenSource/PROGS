|FICHEROS;
   camedcob #0;

   camancob #2;
   camaasil #3;
   cacuenta #4;
   carepres #5;

   camedtmp #999;
|FIN;

|VARIABLES;
   Cont    = 0;
   Cont1   = 0;
   SwError = 0;

   Comodin  = "";
   Comodin1 = "";

   DRep     = "";
   HRep     = "";
   DTR      = 0;
   HTR      = 0;

   FechaCobro = @;
   ComodNum   = 0;

   Empres     = 1;
   DEmpre     = 0;
   DPerio     = 0;
   HEmpre     = 0;
   HPerio     = 0;
   aFichero   = "";
   nHay       = 0;
   nSw        = 0;
   &nSwLinea  = 0;
   &aRepres   = "";
   &aNomRep   = "";
   informe    = "";
   &enTotal   = 0;
   &enDias    = 0;
   &enMedia   = 0;
   nNumCob    = 0;
   nNumDia    = 0;
|FIN;

|INCLUYE acceso_i;
|INCLUYE puntos_i;

|CALCULO defectos;  |TIPO 7;
  #0#46 = fec1; |PINTA #0#46;
  #0#47 = fec2; |PINTA #0#47;

  #0#28 = "N";       |PINTA #0#28;
  #0#33 = enEmpresa; |PINTA #0#33;
  #0#34 = enPeriodo; |PINTA #0#34;
|FCAL;

|CALCULO cuenta; |TIPO 6;
    p_alfa1 = #0Campo;
    salidas = FSalida;
    |HAZ punto;
    |SI p_error ! 0;  |ERROR;  |ACABA;  |FINSI;
|FCAL;

|CALCULO Selec; |TIPO 0;
   |SI Campo = 2;
        |SI #0#2 = "S";
            |PARA Cont = 5; |SI Cont [ 14; |HACIENDO Cont = Cont + 1;
                  |C_M #0Cont,1,"N";
                  #0Cont = ""; |PINTA #0Cont;
            |SIGUE;
            |C_M #0#3,1,"S";
            |C_M #0#4,1,"S";
        |SINO;
            |PARA Cont = 5; |SI Cont [ 14; |HACIENDO Cont = Cont + 1;
                  |C_M #0Cont,1,"S";
            |SIGUE;
            |C_M #0#3,1,"N"; #0#3 = "  "; |PINTA #0#3;
            |C_M #0#4,1,"N"; #0#4 = "zz"; |PINTA #0#4;
        |FINSI;
   |FINSI;

   |SI Campo = 15;
       |SI #0#15 = "S";
           |PARA Cont = 18; |SI Cont [ 27; |HACIENDO Cont = Cont + 1;
                 |C_M #0Cont,1,"N";
                 #0Cont = 0; |PINTA #0Cont;
           |SIGUE;
           |C_M #0#16,1,"S";
           |C_M #0#17,1,"S";
       |SINO;
           |PARA Cont = 18; |SI Cont [ 27; |HACIENDO Cont = Cont + 1;
                 |C_M #0Cont,1,"S";
           |SIGUE;
           |C_M #0#16,1,"N"; #0#16 = 00; |PINTA #0#16;
           |C_M #0#17,1,"N"; #0#17 = 99; |PINTA #0#17;
       |FINSI;
   |FINSI;

   |SI Campo = 28
       |SI #0#28 = "S";
           |PARA Cont = 33; |SI Cont [ 44; |HACIENDO Cont = Cont + 1;
                 |C_M #0Cont,1,"N";
                 #0Cont = 0; |PINTA #0Cont;
           |SIGUE;
           |C_M #0#29,1,"S";
           |C_M #0#30,1,"S";
           |C_M #0#31,1,"S";
           |C_M #0#32,1,"S";
       |SINO;
           |PARA Cont = 33; |SI Cont [ 44; |HACIENDO Cont = Cont + 1;
                 |C_M #0Cont,1,"S";
           |SIGUE;
           |C_M #0#29,1,"N";
           |C_M #0#30,1,"N";
           |C_M #0#31,1,"N";
           |C_M #0#32,1,"N";
           #0#29 = 1;     |PINTA #0#29;
           #0#30 = 0;     |PINTA #0#30;
           #0#31 = 99999; |PINTA #0#31;
           #0#32 = 9;     |PINTA #0#32;
       |FINSI;
   |FINSI;
|FCAL;

|CALCULO MiraEmpresa; |TIPO 0;
   |SI FSalida = 2; |ACABA; |FINSI;
   |SI #0#28 = "S"; |ACABA; |FINSI;

   Empres = Campo - 1;
   |SI #0Empres = 0; |Y #0Campo = 0; |ACABA; |FINSI;

   |ABRE #30;
   #30#2 = #0Empres;
   #30#3 = #0Campo;
   |LEE 010#30=;
   |SI FSalida ! 0;
       |MENAV "    Empresa inexistente";
       |ERROR;
   |FINSI;
   |CIERRA #30;
|FCAL;

|| ********************************************************************
|PROCESO Cuentas;
   |SI #999#5 ! 0;
       |PRINT;
   |FINSI;
|FPRC;

|DEFBUCLE Cuentas;
   #999#1;
   ;
   #0#0, "  ";
   #0#1, "zz";
   ;
   NULL, Cuentas;
|FIN;

|PROCESO EmpRepres;
        nNumCob = 0;
        nNumDia = 0;
        enTotal = 0;
        enDias  = 0;
        enMedia = 0;
        aRepres  = #999#3;
        aNomRep  = #999#9;
        nSwLinea = 0;
|FPRC;


|PROCESO PorRepres;
   |SI #999#5 ! 0;
       |SI nSw = 0;
           nSw = 1;
           |HAZ EmpRepres;
       |FINSI;

       |SI aRepres ! #999#3;
           nSwLinea = 2; |PRINT;
           |HAZ EmpRepres;
      |FINSI;

      |PRINT;
      nSwLinea = 1;

      enTotal = enTotal + #999#5;  || Total Importe Cobrado
      nNumCob = nNumCob + #999#4;  || Total numero de Facturas
      nNumDia = nNumDia + #999#6;  || Total Dias
      enDias  = (nNumDia / nNumCob) ! 3;
      enMedia = (enTotal / nNumCob) ! EURODB;
  |FINSI;
|FPRC;

|DEFBUCLE PorRepres;
   #999#2;
   ;
   "  ", #0#0;
   "zz", #0#1;
   ;
   NULL, PorRepres;
|FIN;

|| ///////////////////////////////////////////////////////////////

|PROCESO LeeCta;
       |DDEFECTO #4;
       #4#0 = #2#4;
       #4#1 = #2#5;
       |LEE 010#4=;
       #999#1 = #4#2;
|FPRC;

|PROCESO LeeRep;
       |SI #0#45 = "S";
           |DDEFECTO #5;
           #5#0 = aRepres;
           |LEE 010#5=;
           #999#9 = #5#1;
       |SINO;
           #999#9 = "Todos sus representantes";
       |FINSI;
|FPRC;

|PROCESO FechaCobro;
   |SELECT #4#camaasil;
   #camaasil#13 = #camancob#0;
   #camaasil#15 = #camancob#2;
   #camaasil#14 = #camancob#1;
   |LEE 010#camaasil.];
   |SI FSalida = 0; |Y #camaasil#13 = #camancob#0; |Y #camaasil#15 = #camancob#2;
   |Y #camaasil#14 = #camancob#1;
      |PARA; |SI FSalida = 0; |Y #camaasil#13 = #camancob#0;
      |Y #camaasil#15 = #camancob#2; |Y #camaasil#14 = #camancob#1; |HACIENDO;
         |SI #camaasil#22 = #camancob#3; |Y #camaasil#12 = "C";
            FechaCobro = #camaasil#1;
            SwError = 0;
         |FINSI;
         |LEE 010#camaasil.s;
      |SIGUE;
   |FINSI;
   |SELECT #1#camaasil;
|FPRC;

|PROCESO RellenaTempo;

   |SI #2#24 < 0; |ACABA; |FINSI;

   SwError = 1;
   |SI #0#15 = "N";
       |PARA Cont1 = 18; |SI Cont1 [ 27; |HACIENDO Cont1 = Cont1 + 1;
             |SI #0Cont1 = #2#11; |Y #2#11 ! 0;
                 SwError = 0;
                 |SAL;
             |FINSI;
       |SIGUE;
   |SINO;
       |SI #0#16 [ #2#11; |Y #0#17 ] #2#11;
           SwError = 0;
       |FINSI;
   |FINSI;
   |SI SwError = 1; |ACABA; |FINSI;

   SwError = 1;
   |HAZ FechaCobro;
   |SI SwError = 1; |ACABA; |FINSI;

   |SI FechaCobro < #0#46; |O FechaCobro > #0#47; |ACABA; |FINSI;

   aRepres = #2#28;
   |SI #0#45 = "N"; aRepres = ""; |FINSI;

   #999#0 = #2#4;
   #999#3 = aRepres;
   |LEE 011#999=;
   |SI FSalida ! 0;
       |DDEFECTO #999;
       #999#0 = #2#4;
       #999#3 = aRepres;

       |HAZ LeeCta;
       |HAZ LeeRep;
       |GRABA 020#999c;
   |FINSI;

   #999#2 = #2#11;
   #999#4 = #999#4 + 1;
   #999#5 = #999#5 + #2#24;

   ComodNum = FechaCobro - #2#6;
   #999#6   = #999#6 + ComodNum;

   #999#7   = (#999#6 / #999#4) ! 3;
   #999#8   = (#999#5 / #999#4) ! EURODB;

   |GRABA 020#999a;
   nHay = 1;
   |LIBERA #999;
|FPRC;

|DEFBUCLE RellenaTempo;
    #2#1;
    4,28,23;
    01, "  ", 000001, 001, #0#0, DRep, "S";
    99, "zz", 999999, 999, #0#1, HRep, "S";
    ;
    NULL, RellenaTempo;
|FIN;

|PROCESO Empresas;

   |DBASE Comodin;
   Comodin1 = Comodin + "fich/";
   Comodin1 = Comodin1 + (("00000" + #30#2) % -105);
   Comodin1 = Comodin1 + #30#3 + "/";

   |PATH_DAT #2 Comodin1;
   |PATH_DAT #3 Comodin1;
   |PATH_DAT #4 Comodin1;
   |PATH_DAT #5 Comodin1;

   |ABRE #5;
   |ABRE #4;
   |ABRE #3;

   |SI #0#2 = "N";
       |PARA Cont = 5; |SI Cont [ 14; |HACIENDO Cont = Cont + 1;
             DRep = #0Cont;
             HRep = #0Cont;
            |BUCLE RellenaTempo;
       |SIGUE;
   |SINO;
        DRep = #0#3;
        HRep = #0#4;
        |BUCLE RellenaTempo;
   |FINSI;

   |CIERRA #3;
   |CIERRA #4;
   |CIERRA #5;
|FPRC;

|DEFBUCLE Empresas;
   #30#1;
   ;
   DEmpre, DPerio;
   HEmpre, HPerio;
   ;
   NULL,Empresas;
|FIN;


|PROCESO Listado; |TIPO 3;

   |IMPRE 0;
   |SI FSalida < 0; |ACABA; |FINSI;

   informe = "camedcob";
   |SI #0#45 = "S"; informe = "camedcor"; |FINSI;

   |INFOR informe;
   |SI FSalida < 0; |ACABA; |FINSI;

   aFichero = "1x" + Usuario;
   |NOME_DAT #999 aFichero;
   |DELINDEX #999;

   nHay = 0;
   |ABRE #999;
   |SI #0#28 = "S";
       DEmpre = #0#29; DPerio = #0#30;
       HEmpre = #0#31; HPerio = #0#32;
       |BUCLE Empresas;
   |SINO;
       |PARA Cont = 33; |SI Cont [ 44; |HACIENDO Cont = Cont + 1;
             DEmpre = #0Cont; HEmpre = #0Cont;
             Cont = Cont + 1;
             DPerio = #0Cont; HPerio = #0Cont;

            |BUCLE Empresas;
      |SIGUE;
   |FINSI;
   |CIERRA #999;

   |SI nHay ! 0;
       nSw     = 0;
       aRepres = "";
       |SI #0#45 = "N";
           |BUCLE Cuentas;
       |SINO;
           |BUCLE PorRepres;
           |SI nSw = 1;
               |SI nSwLinea = 1;
                   nSwLinea = 2; |PRINT;
               |FINSI;
           |FINSI;
       |FINSI;
       |PIEINF;
   |FINSI;

   |FININF;
   |FINIMP;
   |DELINDEX #999;
|FPRC;
