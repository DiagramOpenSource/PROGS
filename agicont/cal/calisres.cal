|FICHEROS;
  calisres #0;    || def entrada para informe
  caexistl #1;    || lineas de existencias
  cacuenta #2;    || Plan contable        --> #3
  caplctac #3;    || cabecera Planilla
  caplctal #4;    || Lineas Planilla      --> #7
  catralre #5;    || Trabajo
  caconlib #6;
  caportad #8;    || Portadas
|FIN;

|VARIABLES;
  sw_error = 0;
  sw_def = 0;
  sw_exi = 0;
  puntos = ".........................";
  men1   = "    NO existe Informe ";
  men2   = "    NO existe el Codigo de Columnas de Libros ";
  alfa1  = "";
  alfa2  = "";
  alfa3  = "";
  alfa4  = "";
  xx = 0;
  yy = 0;
  lin = 0;
  &r_emp = 0;
  &r_per = 0;
  &r_eje = 0;
  &estru = 0;
  &d_mes = 0;
  &h_mes = 0;
  &r_nom = "";

  a_plan = 0;
  a_tipo = "";
  a_nume = 0;
  a_desc = "";
  a_imp1 = 0;
  a_imp2 = 0;
  a_imp3 = 0;
  a_imp4 = 0;
  a_imp5 = 0;

  exini1 = 0;
  exini2 = 0;
  exini3 = 0;
  exini4 = 0;
  exfin1 = 0;
  exfin2 = 0;
  exfin3 = 0;
  exfin4 = 0;

  informe = "calisres";

  y = "";
  x = 0;
  uno = "";
  dos = "";
  scta = 0;
  cc3 = 0;
  cc4 = 0;
  cc5 = 0;
  signo = "";
  sw = 0;
  tt1 = 0;
  tt2 = 0;
  tt3 = 0;
  tt4 = 0;
  tt5 = 0;
  ingt1 = 0;
  ingt2 = 0;
  ingt3 = 0;
  ingt4 = 0;
  ingt5 = 0;
  &swlinea = 0;
  &tot1 = 0;
  &tot2 = 0;
  &tot3 = 0;
  &tot4 = 0;
  &tot5 = 0;
  &subt = "";
  {-1}menu  = "";           || Variables menu impresora o pantalla
      menu1 = "2400";
      menu2 = "1";
      menu3 = " Elija opcion: ";
      menu4 = "PI";
      menu5 = " Pantalla ";
      menu6 = " Impresora ";
|FIN;

|INCLUYE acceso_i;

***************************************************************************
               CAMPO DE PANTALLA
***************************************************************************

|PROCESO defectos; |TIPO 7;
  |SI sw_def = 1; |ACABA; |FINSI;
  |DDEFECTO #3;
  |ABRE #3;
  |LEE 000#3p;
    |SI FSalida = 0;
        #0#0 = #3#0;
        |PINTA #0#0;
    |FINSI;
  |CIERRA #3;
  |DDEFECTO #8;
  |ABRE #8;
  #8#0 = #30#2;
  #8#1 = #30#26;
  |LEE 000#8=;
    |SI FSalida = 0;
        |SI #8#83 = "N";
            |CAMPO_MODIFICA #0#1, 1, "S";
            #0#1 = #8#88;
            |PINTA #0#1;
        |SINO;
            |CAMPO_MODIFICA #0#1, 1, "N";
        |FINSI;
        #0#4 = #8#86;
        |PINTA #0#4;
    |FINSI;
  |CIERRA #8;
  sw_def = 1;
|FPRC;

|PROCESO leeconlib; |TIPO 0;
 |SI FSalida = 2; |ACABA; |FINSI;
 |SI #0Campo = 0; |ERROR; |ACABA; |FINSI;
 |ABRE #6;
 #6#0 = #0Campo;
 |LEE 001#6=;
 salidas = FSalida;
 |CIERRA #6;
 |SI salidas ! 0;
     |MENAV men2;
     |ERROR;
 |SINO;
     #0#5  =  #6#1  + #6#2;  #0#6  =  #6#3  + #6#4;
     #0#7  =  #6#5  + #6#6;  #0#8  =  #6#7  + #6#8;
     #0#9  =  #6#9  + #6#10; #0#10 =  #6#11 + #6#12;
     #0#11 =  #6#13 + #6#14; #0#12 =  #6#15 + #6#16;
     #0#13 =  #6#17 + #6#18; #0#14 =  #6#19 + #6#20;
     #0#15 =  #6#21 + #6#22; #0#16 =  #6#23 + #6#24;
     #0#17 =  #6#25 + #6#26; #0#18 =  #6#27 + #6#28;
     #0#19 =  #6#29 + #6#30; #0#20 =  #6#31 + #6#32;
     #0#21 =  #6#33 + #6#34; #0#22 =  #6#35 + #6#36;
     |PARA xx = 5; |SI xx [ 22; |HACIENDO xx = xx + 1;
           alfa1 = #0xx; |QBF alfa1;
           alfa1 = alfa1 + puntos;
           alfa1 = alfa1 % 125;
           #0xx  = alfa1;
           |PINTA #0xx;
     |SIGUE;
     |ATRI I;
     |PINTA 1469, #6#55; |PINTA 1569, #6#56;
     |PINTA 1669, #6#57; |PINTA 1769, #6#58;
     |PINTA 1869, #6#59; |PINTA 1969, #6#60;
     |PINTA 2069, #6#61; |PINTA 2169, #6#62;
     |PINTA 2269, #6#63;
     |ATRI 0;
 |FINSI;
|FPRC;

|PROCESO existenc; |TIPO 0;
  |SI FSalida ! 9; |ACABA; |FINSI;
  |SI #8#83 ! "N";
      |MENAV "    La Actividad de esta Empresa es Agricola ";
      |ACABA;
  |FINSI;
  |PARA x = 1; |SI x [ 4; |HACIENDO x = x + 1;
    d_mes = 0 + (x - 1) * 3;
    h_mes = 3 + (x - 1) * 3;
    estru = #0#1;
    r_emp = #30#2;
    r_per = #30#3;
    r_eje = #30#26;
    r_nom = #30#1;
    |QBF r_nom;
    r_nom = r_nom % A120;
    y = x;
    r_nom = r_nom + " - " + y + "§ trimestre";
    |SUB_EJECUTA "caexicon.run";
  |SIGUE;
|FPRC;

***************************************************************************

|PROCESO graba_reg;
  #5#0 = a_plan;
  #5#1 = a_tipo;
  #5#2 = a_nume;
  |LEE 101#5=;
  salidas = FSalida;
  |SI salidas ! 0;
      |DDEFECTO #5;
      #5#0 = a_plan;
      #5#1 = a_tipo;
      #5#2 = a_nume;
  |FINSI;
  #5#3 = a_desc;
  #5#4 = #5#4 + a_imp1;
  #5#5 = #5#5 + a_imp2;
  #5#6 = #5#6 + a_imp3;
  #5#7 = #5#7 + a_imp4;
  #5#8 = #5#8 + a_imp5;
  |SI salidas = 0; |GRABA 020#5a; |FINSI;
  |SI salidas ! 0; |GRABA 020#5n; |FINSI;
  |LIBERA #5;
|FPRC;

|| ===== Calculo de Existencias

|PROCESO sumaex;
  sw_exi = 1;
  exini1 = exini1 + #1#11; exfin1 = exfin1 + #1#14;
  exini2 = exini2 + #1#14; exfin2 = exfin2 + #1#17;
  exini3 = exini3 + #1#17; exfin3 = exfin3 + #1#20;
  exini4 = exini4 + #1#20; exfin4 = exfin4 + #1#23;
|FPRC;

|DEFBUCLE exist;
 #1#1;
 ;
 #0#1, 01;
 #0#1, 99;
 e;
 NULL, sumaex;
|FIN;

|PROCESO lee_exist;
 sw_exi = 0;
 exini1 = 0; exfin1 = 0;
 exini2 = 0; exfin2 = 0;
 exini3 = 0; exfin3 = 0;
 exini4 = 0; exfin4 = 0;
 |BUCLE exist;
 |SI sw_exi = 1;
     a_tipo = "C";
     a_nume = 0;
     a_desc = "Existencias Iniciales ...";
     a_imp1 = exini1;
     a_imp2 = exini2;
     a_imp3 = exini3;
     a_imp4 = exini4;
     a_imp5 = exini1;
     |HAZ graba_reg;  || Cambiar existencias Finales a Consumo 02.09.1997
     a_tipo = "C";
     a_nume = 10;
     a_desc = "Existencias Finales .....";
     a_imp1 = exfin1;
     a_imp2 = exfin2;
     a_imp3 = exfin3;
     a_imp4 = exfin4;
     a_imp5 = exfin4;
     |HAZ graba_reg;
 |FINSI;
|FPRC;
|| ==================================================

|PROCESO cal_ctas2;
 |PARA xx = 1; |SI xx [ 4; |HACIENDO xx = xx + 1;
      cc3 = 12 + (xx - 1) * 9;
      cc4 = 15 + (xx - 1) * 9;
      cc5 = 18 + (xx - 1) * 9;
      |SI signo = "H";
          cc3 = cc3 + 1;
          cc4 = cc4 + 1;
          cc5 = cc5 + 1;
      |FINSI;
      |SI signo = "S";
          cc3 = cc3 + 2;
          cc4 = cc4 + 2;
          cc5 = cc5 + 2;
      |FINSI;
      scta = #2cc3 + #2cc4 + #2cc5;
      |SI #4#3 = "I"; |Y signo = "S";
          scta = - scta;
      |FINSI;
      |SI xx = 1; a_imp1 = a_imp1 + scta; |FINSI;
      |SI xx = 2; a_imp2 = a_imp2 + scta; |FINSI;
      |SI xx = 3; a_imp3 = a_imp3 + scta; |FINSI;
      |SI xx = 4; a_imp4 = a_imp4 + scta; |FINSI;
 |SIGUE;
|FPRC;

|DEFBUCLE lee_ctas;
 #2#1;
 3;
 #4#2, 0, "S";
 dos,  9, "S";
 e;
 NULL, cal_ctas2;
|FIN;

|PROCESO cal_ctas1;
   a_imp1 = 0;
   a_imp2 = 0;
   a_imp3 = 0;
   a_imp4 = 0;
   a_imp5 = 0;
   uno = #4#2;
   |QBF uno;
   dos = uno + "999999999999";
   dos = dos % A112;
   signo = #4#4;
   |SI signo = "*"; signo = #4#7; |FINSI;
   |SI signo = "*"; |ACABA; |FINSI;

   |BUCLE lee_ctas;

   a_imp5 = a_imp1 + a_imp2 + a_imp3 + a_imp4;
   a_tipo = #4#3;
   |SI a_tipo = "I";
       a_nume = #4#6;
       x = #4#6 + 4;
       a_desc = #0x;
   |FINSI;
   |SI a_tipo = "G";
       a_nume = #4#6;
       x = #4#6 + 13;
       a_desc = #0x;
       x = #4#6 + 54;
       a_tipo = #6x;
   |FINSI;
   |SI a_tipo = "V";
       a_nume = 0;
       a_desc = "Incremento de patrimonio.";
       |SI a_imp5 < 0;
           a_nume = 1;
           a_desc = "Disminuciones patrimonio.";
       |FINSI;
   |FINSI;
   |SI a_tipo = "R";
       a_nume = 0;
       a_desc = "Retenciones practicadas..";
   |FINSI;
   |HAZ graba_reg;
|FPRC;

|DEFBUCLE calisres0;
 #4#1;
 ;
 #0#0, 001;
 #0#0, 999;
 e;
 NULL, cal_ctas1;
|FIN;
|| **************************************************************************

|PROCESO poncero;
  sw   = 0;
  tot1 = 0;
  tot2 = 0;
  tot3 = 0;
  tot4 = 0;
  tot5 = 0;
  subt = "";
|FPRC;

|PROCESO imprimir;
 |SI #5#4 = 0; |Y #5#5 = 0; |Y #5#6 = 0; |Y #5#7 = 0;
     |ACABA;
 |FINSI;
 |SI sw = 0;
     sw = 1;
     |SI a_tipo = "I"; |O a_tipo = "C"; |O a_tipo = "G";
         swlinea = 0;
         |SI menu2 = 1; |HAZ operapanta; |FINSI;
         |SI menu2 = 2; |PRINT; |FINSI;
     |FINSI;
 |FINSI;
 swlinea = 1;
 |SI menu2 = 1; |HAZ operapanta; |FINSI;
 |SI menu2 = 2; |PRINT; |FINSI;
 sw_def = 1;
 |SI #5#1 = "C"; |Y #5#2 = 10; || Si es existencias Finales Resta el total.
      tot1 = tot1 - #5#4;
      tot2 = tot2 - #5#5;
      tot3 = tot3 - #5#6;
      tot4 = tot4 - #5#7;
      tot5 = tot5 - #5#8;
 |SINO;
      tot1 = tot1 + #5#4;
      tot2 = tot2 + #5#5;
      tot3 = tot3 + #5#6;
      tot4 = tot4 + #5#7;
      tot5 = tot5 + #5#8;
 |FINSI;
|FPRC;

|DEFBUCLE calisres2;
 #5#1;
 ;
 #0#0, a_tipo, 00;
 #0#0, a_tipo, 99;
 e;
 NULL, imprimir;
|FIN;

|PROCESO calisres1;
 || ... Ingresos
    |HAZ poncero;
    subt = " INGRESOS ";
    a_tipo = "I";
    |BUCLE calisres2;
    |SI sw = 1;
        subt = "TOTAL INGRESOS .....................";
        swlinea = 2;
        |SI menu2 = 1; |HAZ operapanta; |FINSI;
        |SI menu2 = 2; |PRINT; |FINSI;
        tt1 = tt1 + tot1; ingt1 = tt1;
        tt2 = tt2 + tot2; ingt2 = tt2;
        tt3 = tt3 + tot3; ingt3 = tt3;
        tt4 = tt4 + tot4; ingt4 = tt4;
        tt5 = tt5 + tot5; ingt5 = tt5;
    |FINSI;
 || ... CONSUMO
    |HAZ poncero;
    subt = " CONSUMO ";
    a_tipo = "C";
    |BUCLE calisres2;
    |SI sw = 1;
        subt = "TOTAL CONSUMO ......................";
        swlinea = 2;
        |SI menu2 = 1; |HAZ operapanta; |FINSI;
        |SI menu2 = 2; |PRINT; |FINSI;
        tt1 = tt1 - tot1;
        tt2 = tt2 - tot2;
        tt3 = tt3 - tot3;
        tt4 = tt4 - tot4;
        tt5 = tt5 - tot5;
    |FINSI;
 || ... Gastos
    |HAZ poncero;
    subt = " GASTOS ";
    a_tipo = "G";
    |BUCLE calisres2;
    |SI sw = 1;
        subt = "TOTAL GASTOS .......................";
        swlinea = 2;
        |SI menu2 = 1; |HAZ operapanta; |FINSI;
        |SI menu2 = 2; |PRINT; |FINSI;
        tt1 = tt1 - tot1;
        tt2 = tt2 - tot2;
        tt3 = tt3 - tot3;
        tt4 = tt4 - tot4;
        tt5 = tt5 - tot5;
    |FINSI;
 || ... Totales A;
    |SI #0#4 ! 0;
        subt = "DIFERENCIA .........................";
        swlinea = 3;
        tot1 = tt1;
        tot2 = tt2;
        tot3 = tt3;
        tot4 = tt4;
        tot5 = tt5;
        |SI menu2 = 1; |HAZ operapanta; |FINSI;
        |SI menu2 = 2; |PRINT; |FINSI;
        tot1 = (tt1 % #0#4) ! 0;
        tot2 = (tt2 % #0#4) ! 0;
        tot3 = (tt3 % #0#4) ! 0;
        tot4 = (tt4 % #0#4) ! 0;
        tot5 = (tt5 % #0#4) ! 0;
        subt = "COEFICIENTE GASTOS " + #0#4 + " %.................";
        swlinea = 3;
        |SI menu2 = 1; |HAZ operapanta; |FINSI;
        |SI menu2 = 2; |PRINT; |FINSI;
        tt1 = tt1 - tot1;
        tt2 = tt2 - tot2;
        tt3 = tt3 - tot3;
        tt4 = tt4 - tot4;
        tt5 = tt5 - tot5;
    |FINSI;
    subt = "RENDIMIENTO NETO ORDINARIO .........";
    swlinea = 4;
    tot1 = tt1;
    tot2 = tt2;
    tot3 = tt3;
    tot4 = tt4;
    tot5 = tt5;
    |SI menu2 = 1; |HAZ operapanta; |FINSI;
    |SI menu2 = 2; |PRINT; |FINSI;
 || ... Inversion
    |HAZ poncero;
    a_tipo = "V";
    |BUCLE calisres2;
    |SI sw = 1;
        tt1 = tt1 + tot1;
        tt2 = tt2 + tot2;
        tt3 = tt3 + tot3;
        tt4 = tt4 + tot4;
        tt5 = tt5 + tot5;
    || ... Totales A;
        subt = "RENDIMIENTO NETO ...................";
        swlinea = 4;
        tot1 = tt1;
        tot2 = tt2;
        tot3 = tt3;
        tot4 = tt4;
        tot5 = tt5;
        |SI menu2 = 1; |HAZ operapanta; |FINSI;
        |SI menu2 = 2; |PRINT; |FINSI;
    |FINSI;
 || ... Retenciones
    |HAZ poncero;
    a_tipo = "R";
    |BUCLE calisres2;
|FPRC;

|| **************************************************************************


|PROCESO resumen; |TIPO 3;
 |MENU menu;
 |SI 0 < FSalida;          || Se ha elegido opcion
     menu2 = FSalida;

     |ABRE #5;
     |CIERRA #5;
     |DELINDEX #5;
     |SI menu2 = 2;
         |IMPRE 0;
         |SI FSalida ! 0; |ACABA; |FINSI;
         |INFOR informe;
         |SI FSalida ! 0;
             |MENAV men1;
             |FINIMP;
             |ACABA;
         |FINSI;
     |FINSI;

     a_plan = #0#0;
     |ABRE #5;
     |HAZ lee_exist;

     |ABRET #2;
     |ABRET #4;
     |BUCLE calisres0;

     |CIERRAT #2;
     |CIERRAT #4;
     |CIERRA #5;
     |SI menu2 = 1; |HAZ panta; |FINSI;  || Por pantalla
     |SI menu2 = 2; |HAZ impre; |FINSI;  || Por impresora
     |DELINDEX #5;
 |FINSI;
|FPRC;

|PROCESO impre;
 sw = 0;
 sw_def = 0;
 |HAZ calisres1;
 |SI sw_def = 1;
     |PIEINF;
 |FINSI;
 |FININF;
 |FINIMP;
|FPRC;

|PROCESO panta;
 alfa4  = &45 * 23 + &179 + &45 * 10 + &179 + &45 * 10 + &179 + &45 * 10;
 alfa4  = alfa4 + &179 + &45 * 10 + &179 + &45 * 12;lin = 100;
 |PINPA #0#5;       || Pinta la pantalla del informe
 |PINTA 0511, #30#1;
 |PINTA 0559, #30#26;
 |PINTA 0570, #0#2;
 sw = 0;
 sw_def = 0;
 |HAZ calisres1;
 |SI sw_def = 1;
     |PAUSA;
 |FINSI;
|FPRC;

|| **************************************************************************

|PROCESO operapanta;   || Operaciones por pantalla;
 |SI lin > 1400;
     |HAZ cambiapag;
     |SI sw_error = 1; |ACABA; |FINSI;
 |FINSI;
 |SI swlinea = 0; |O swlinea = 4;
     |SI lin > 1300;
         |HAZ cambiapag;
         |SI sw_error = 1; |ACABA; |FINSI;
     |FINSI;
 |FINSI;
 |SI swlinea = 0;
     yy = lin + 801
     |ATRI R;
     |PINTA yy, subt;
     |ATRI 0;
     lin = lin + 100;
 |FINSI;
 |SI swlinea = 1;
     Pos = lin;
     |PINDA #0#5;         || Pinta las lineas
     Pos = -1;
     lin = lin + 100;     || Posicionar una linea mas abajo
 |FINSI;
 |SI swlinea = 4;
     yy = lin + 801; |PINTA yy, alfa4;
     lin = lin + 100;
 |FINSI;
 |SI swlinea ] 2;
     subt = subt % 123;
     #5#4 = tot1; #5#5 = tot2;
     #5#6 = tot3; #5#7 = tot4;
     #5#8 = tot5;
     |ATRI N;
     yy = lin + 801; |PINTA yy, subt;
     yy = lin + 825; |PINTA yy, #5#4;
     yy = lin + 836; |PINTA yy, #5#5;
     yy = lin + 847; |PINTA yy, #5#6;
     yy = lin + 858; |PINTA yy, #5#7;
     yy = lin + 869; |PINTA yy, #5#8;
     |ATRI 0;
     lin = lin + 200;     || Posicionar una linea mas abajo
 |FINSI;
|FPRC;

|PROCESO cambiapag;
 sw_error = 0;
 lin = 100;
 |PAUSA;
 |SI 1 = FSalida;|O 7 = FSalida;
     |ERROR10;    || Rompe el bucle y acaba
     sw_error = 1;
 |SINO;
     |PINPA #0#5;
     |PINTA 0511, #30#1;
     |PINTA 0559, #30#26;
     |PINTA 0570, #0#2;
     FSalida = 0;
 |FINSI;
|FPRC;
