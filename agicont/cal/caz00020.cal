|FICHEROS;
  caz00020 #0;
  camaasic #2;
  camaasil #3;
  camandia #10
|FIN;

|VARIABLES;
   &entrada = 1;
   informe  = "caz00020";

   nHayDesc = 0;
   opcion   = 0;
   nUltLin  = 0;
   nDebe    = 0;
   nHaber   = 0;
   nSaldo   = 0;
   aElDepar = "";
   aElSubde = "";
   aLaCtaAn = "";
   aAlfa1   = "";
   aAlfa2   = "";
   nNume1   = 0;
   nPaso    = 0;
   nInkey   = 0;

   eaCuenta = "";
   eswCta   = 0;
   doce     = "            ";
   aLong    = "";
   nLong    = 0;
   enNivel  = 0;
|FIN;

|INCLUYE acceso_i;
|INCLUYE puntos_i;
|INCLUYE condes_i;
|INCLUYE actcta_i;

|| **********************************************************************
|| ***** Calculos de Pantalla
|| **********************************************************************
|CALCULO ElTipo8; |TIPO 8;
  |HAZ inicio;
  |HAZ VerCaconimp;
  |SI eaConDes ! "N";
      |C_P #0#9,1,1050;
      |C_A #0#9,1,20;
      |C_V #0#16,1,"S";
  |FINSI;
|FCAL;


|CALCULO defectos; |TIPO 7;
   #0#2 = fec1; |PINTA #0#2;
   #0#3 = fec2; |PINTA #0#3;
|FCAL;

|CALCULO antesCta; |TIPO 7;
  |SI nPaso = 0;
      nPaso = 1;
      #0#12 = #0#6; |PINTA #0#12;
      #0#13 = #0#7; |PINTA #0#13;
  |FINSI;
|FCAL;

|CALCULO cuenta; |TIPO 6;
  p_alfa1 = #0Campo;
  salidas = FSalida;
  |HAZ punto;
  |SI p_error ! 0;
      |MENAV "     ERROR EN CUENTA";
      |ERROR;
      |ACABA;
  |FINSI;
  eaCuenta = #0Campo; |HAZ SacaNivel;
  |SI Campo = 6;  nNume1 = 14; |FINSI;
  |SI Campo = 12; nNume1 = 15; |FINSI;
  #0nNume1 = enNivel;
|FCAL;


|CALCULO cuefin; |TIPO 0;
   nInkey = FSalida;
   |SI nInkey = 15; |Y Campo = 12;
       #0#12 = #0#6;
       |PINTA #0#12;
   |FINSI;

   |SI #0Campo = "            ";
       |ERROR;
       |ACABA;
   |FINSI;

   eaCuenta = #0Campo;
   |HAZ SacaNivel;
   eaCuenta = #0Campo; |HAZ SacaNivel;
   |SI Campo = 6;  nNume1 = 14; |FINSI;
   |SI Campo = 12; nNume1 = 15; |FINSI;
   #0nNume1 = enNivel;
   |SI eswCta ! 0;  |ACABA; |FINSI;  ||Cuenta erronea

   |HAZ LeeCuenta;
   |SI eswCta ! 0;  |ACABA; |FINSI;  ||Cuenta erronea

   nNume1   = Campo + 1;
   #0nNume1 = #cacuenta#2; |PINTA #0nNume1;
|FCAL;


|CALCULO c_descrip; |TIPO 7;
  |SI eaConDes ! "N";  #0Campo = ""; |FINSI;
  |PTEC 816;
|FCAL;

|CALCULO fecha; |TIPO 0;
   |SI #0Campo < fec1; |O #0Campo > fec2;
       |MENAV "    ATENCION!!! Fecha fuera de periodo ...";
       |ERROR;
       |ACABA;
   |FINSI;
   |SI Campo = 3; |Y #0Campo < #0#2;
       |MENAV "    ATENCION!!! Fecha inferior al primer limite ... ";
       |ERROR;
   |FINSI;
|FCAL;

|| ----------------------------------------------------------
|CALCULO descuadre; |TIPO 3;

  || .................. Abre fichero Auxiliar de Cuentas
    |ABRE #8;
    |LEE 101#8u;
    |SI FSalida ! 0;
        |DDEFECTO #8;
        #8#0 = 1;
    |SINO;
        #8#0 = #8#0 + 1;
    |FINSI;
    |GRABA 101#8n;
    codcon = #8#0;
    |LIBERA #8;
    |CIERRA #8;
  || ----------------------------------------------------------

      opcion = 0; |SI #0#10 = "S"; opcion = 1; |FINSI;
      |SI opcion = 1;
          |IMPRE 0;
          |SI FSalida ! 0;  |ACABA;  |FINSI;
          |INFOR informe;
          |SI FSalida ! 0;  |FINIMP;  opcion = 0; |FINSI;
      |FINSI;

      |C_V #0#11,1,"S";
      |CUADRO 2105 2371;
      |HAZ HazTodo;
      |SI opcion = 1;
          |FININF;
          |FINIMP;
      |FINSI;

 || ............... Terminar
 |SI modcon = 0;
      |ABRET #8
      |DDEFECTO #8;
      #8#0 = codcon;
      |LEE 040#8=;
      |BORRA 101#8a;
      |CIERRAT #8;
 |SINO;
      |CORRE "camaapua.tab"
 |FINSI;
|FCAL;

||***********************************************************************

|PROCESO LeeLineas;
   nUltLin = #camaasil#3;

   aElDepar = #camaasil#21;
   |SI #camaasil#8 = "D";
       nDebe  = nDebe  + #camaasil#9;
   |SINO;
       nHaber = nHaber + #camaasil#9;
   |FINSI;
|FPRC;

|PROCESO LeeCabecera;
   |PINTA 2222, #camaasic#0;
   |PINTA 2225, #camaasic#1;
   |PINTA 2236, #camaasic#2;
   |PINTA 2243, "                            ";

   nUltLin = 0;
   nDebe   = 0;
   nHaber  = 0;
   aElDepar = "";
   aElSubde = "";
   aLaCtaAn = "";
   |BUCLELIN 2LeeLineas#camaasic;
   nSaldo = nDebe - nHaber;

   aAlfa1 = nSaldo; |QBF aAlfa1;
   |SI aAlfa1 = "0"; |O aAlfa1 = "-0";
       nSaldo = 0;
   |FINSI;

   |SI nSaldo ! 0;    || ... Crea Linea Nueva
       |ATRI P; |PINTA 2243, "DESCUADRADO"; |ATRI 0;

       |ABRE #camaasil;
       |DDEFECTO #camaasil;

       #camaasil#0  = #camaasic#0;
       #camaasil#1  = #camaasic#1;
       #camaasil#2  = #camaasic#2;
       #camaasil#3  = nUltLin + 10;

       |SI nSaldo < 0;
           #camaasil#4   = #0#6;
           #camaasil#5   = #0#14;
       |SINO;
           #camaasil#4   = #0#12;
           #camaasil#5   = #0#15;
       |FINSI;
       #camaasil#6  = #0#8;
       #camaasil#7  = #0#9;

       |SI nSaldo < 0;
           #camaasil#8   = "D";
           nSaldo = - nSaldo;
           nDebe  = nDebe + nSaldo;
       |SINO;
           #camaasil#8  = "H";
           nHaber = nHaber + nSaldo;
       |FINSI;
       #camaasil#9  = nSaldo;
       |HAZ adapta;

       #camaasil#21 = aElDepar;        || El Departamento
       #camaasil#24 = #0#16;

       |GRABA 020#camaasil.n;
       |LIBERA #camaasil;
       |HAZ LaActualizacion;
       |CIERRA #camaasil;

       #camaasic#4 = nDebe; #camaasic#5 = nHaber; #camaasic#6 = #camaasic#4 - #camaasic#5;
       |GRABA 020#camaasic.a;

       |SI opcion = 1;  |PRINT; |FINSI;
       nHayDesc = nHayDesc + 1;
   |FINSI;
   |LIBERA #camaasic;
|FPRC;

|DEFBUCLE lee_asientos;
   #camaasic#1;
   ;
   #0#0, #0#2, #0#4;
   #0#1, #0#3, #0#5;
   be;
   NULL, LeeCabecera;
|FIN;

|| ----------------------------------------------------------------------
|PROCESO HazTodo;

  |ATRI S; |PINTA 2207, "En proceso ... "; |ATRI 0;

  nHayDesc = 0;
  |BUCLE lee_asientos;

  |SI nHayDesc ! 0;
     aAlfa1 = nHayDesc; |QBF aAlfa1;
     aAlfa2 = "Encontrado";
     |SI nHayDesc > 1; aAlfa2 = aAlfa2 + "s"; |FINSI;
     aAlfa2 = aAlfa2 + " " + aAlfa1 + " " + "descuadre";
     |SI nHayDesc > 1; aAlfa2 = aAlfa2 + "s"; |FINSI;

     #0#11 = aAlfa2; |PINTA #0#11;
     |PAUSA;
     |SI opcion = 1;  |PIEINF; |FINSI;
  |FINSI;
|FPRC;


|| ********************************************************************
|| .... Procesos de la linea de apuntes

|PROCESO LaActualizacion;
  ressum = #3#9;
  |HAZ actualiz1;          || cuenta
  |HAZ actualiz3;          || diario
  opera = "A";
  |HAZ altatra;            || subniveles
|FPRC;

|PROCESO actualiz3;
 |ABRE #camandia;
 #camandia#0 = #camaasil#0;
 |LEE 121#camandia.=;
 |SI FSalida ! 0;
     |DDEFECTO #camandia;
     #camandia#0 = #camaasil#0;
     #camandia#1 = "<DIARIO SIN DEFINIR>";
     |GRABA 020#camandia.b;
 |FINSI;
 |SI #camaasil#8 = "D";  #camandia#2 = #camandia#2 + #camaasil#9;  |FINSI;
 |SI #camaasil#8 = "H";  #camandia#3 = #camandia#3 + #camaasil#9;  |FINSI;
 |GRABA 020#camandia.a;
 |LIBERA #camandia;
 |CIERRA #camandia;
|FPRC;

|PROCESO adapta;
   |SI #camaasil#8 = "D";
       #camaasil#16 = #camaasil#4;
       #camaasil#17 = #camaasil#5;
       #camaasil#10 = "";
       #camaasil#11 = 0;
   |SINO;
       #camaasil#16 = "";
       #camaasil#17 = 0;
       #camaasil#10 = #camaasil#4;
       #camaasil#11 = #camaasil#5;
   |FINSI;
|FPRC;

|| ***********************************************************************

|PROCESO SacaNivel;
   eswCta = 1;     ||Errores en Cuenta
   aLong = eaCuenta;
   |QBF aLong;
   aLong = aLong % 0;
   nLong = aLong;
   enNivel = 0;
   |SI nLong = dig4; |Y dig4 ! 0; eswCta = 0; enNivel = 1; |FINSI;
   |SI nLong = dig5; |Y dig5 ! 0; eswCta = 0; enNivel = 2; |FINSI;
   |SI nLong = dig6; |Y dig6 ! 0; eswCta = 0; enNivel = 3; |FINSI;
   |SI nLong = dig7; |Y dig7 ! 0; eswCta = 0; enNivel = 4; |FINSI;
   |SI nLong = dig8; |Y dig8 ! 0; eswCta = 0; enNivel = 5; |FINSI;
   |SI nLong = dig9; |Y dig9 ! 0; eswCta = 0; enNivel = 6; |FINSI;

   |SI eswCta = 1; |Y  nLong ! 0;
       |MENAV "      Longitud de Cuenta Fuera de Nivel ";
       |ERROR;
   |FINSI;
|FPRC;

|PROCESO LeeCuenta;
   eswCta = 0;
   |ABRE #cacuenta;
   #cacuenta#0 = eaCuenta;
   #cacuenta#1 = enNivel;
   |LEE 001#cacuenta.=;
   |SI FSalida ! 0;
       eswCta = 1;
       |MENAV "    NO EXISTE CUENTA ";
       |CIERRA #cacuenta;
       |ERROR;
       |ACABA;
   |FINSI;
   |CIERRA #cacuenta;

   |SI eaCuenta ! doce;
       |SI #cacuenta#3 = "N";
           eswCta = 2;
           |MENAV "     La Cuenta NO tiene PASE DIRECTO";
           |ERROR;
      |FINSI;
   |SINO;
      |ERROR;
   |FINSI;
|FPRC;
