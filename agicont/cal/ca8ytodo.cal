|FICHEROS;
    ca8ytodo #0;

    cacuenta #1;         || cuentas

    ca8wb001 #3;         || temporal Totales Texto
    cabaexp1 #4;         || temporal existencias
    ca8wb002 #5;         || temporal Cuentas
    ca8wb003 #2;         || temporal Cuentas

    camoauxl #11;
    camoauxc #12;

    ca8m0031 #31;
    ca8m0033 #33;

    caexistc #6;         || descarga cabs.

    canempre #30;        || empresas
    caconimp #8;

    cuentas@ #901;
    ca8yfc02 #902;
    catraaux #904;

    ca8cumay #410;  || Control de Balances
    ca8m0000 #420;  || Descripcion Numero de Balance
    ca8m0001 #421;  || Def Balance Textos
    ca8m0002 #422;  || Def Balance Linea Cuentas
    ca8m0011 #431;  || Reparto 8 y 9 cabera
    ca8m0012 #432;  || Reparto 8 y 9 lineas

    &ficher06@ #442;  || Def Balance Estado
    &ficher07@ #443;  || Lineas
    &ficher12@ #444;  || Columnas
    &ficher13@ #445;  || Lineas Texto
|FIN;

|VARIABLES;
   &enInfTodo = 0;
   &enSwComp = 0;

   &aparam =  "";
   &eaValeCero = "N";

   error1 = "0000Nivel repetido";
   aFichero = "";

   &nPagDup   = 0;
   &nSwPagDup = 0;
   &pagina    = 0;
   sw_pagina  = 0;
   &pathbal   = "";
   &balance   = 1;
   &entrada   = 1;

   &nBalance  = 0;
   &nBalNum   = 0;
   &nCtaNum   = 0;

   traraf     = 0;
   aAlfa      = "";
   aAlfa1     = "";
   aAlfa2     = "";
   aAlfa3     = "";
   nNume1     = 0;
   nNume2     = 0;
   nNume3     = 0;
   nNume4     = 0;
   nPara1     = 0;
   nLong      = 0;
   nLongT     = 0;
   nivel      = 0;
   nivel_bal  = 0;
   cue_perdi  = "";
   dig_perdi  = 0;

   &enActivo   = 0;
   &enActiv2   = 0;
   &enPasivo   = 0;
   &enPasiv2   = 0;

   &eaUnidadBasico = "";
   &eaUnidadBTmp   = "";
   &eaFormato      = "";
   nSwErrorBal     = 0;

   &r_emp = 0;
   &r_per = 0;
   &anu1  = 0;
   &anu   = 0;
   &d_mes = 0;
   &h_mes = 0;
   &estru = 0;
   &r_eje = 0;
   &r_nom = "";
   nom1   = "";
   &cta       = "";
   &nom       = "";
   &canti2    = 0;
   &cantidad2 = 0;
   digito     = 0;
   swdig      = 0;
   long       = 0;

   nSwMen           = 0;
   nSwLInv          = 0;
   &efFecLeg        = @;
   &eaDatLeg        = "";
   &eaLegEmp        = "";
   &eaLegPer        = "";
   &eaUltNiv        = "";
   &enBasImp   = 0;
   &eaNomImp   = "";
   &dirfich0   = "";

   Comodin          = "";
   Comodin1         = "";
   swNoExiste       = 0;

   informe   = "";
   nImprimir = 0;
   sw        = 0;
   tipo      = "";
   &swImp     = 0;
   aCasilla = "";
   nCol       = 0;
   nCol1      = 0;
   nCol2      = 0;
   nCol3      = 0;
   &swImp1    = 0;
   &swImp2    = 0;
   &swImp3    = 0;
   &eaRaya    = "";

    agrupaci   = 0;
    subagrup   = 0;
    epigrafe   = 0;
    sw_epi     = 0;
    &sw_inf    = 0;
    &cantidad  = 0;
    &variable  = "";
    &canti     = 0;
    &doh       = "";
    &eaNomICta = "";

    nHay       = 0;
    nTipBal    = 0;

    CodigoEmpresa    = 0;
    EjercicioEmpresa = 0;
    NombreEmpresa    = "";

    nDCampo5 = 0;
    nDCampo6 = 0;
    nDCampo7 = 0;
    nDCampo8 = 0;
    nHCampo5 = 0;
    nHCampo6 = 0;
    nHCampo7 = 0;
    nHCampo8 = 0;
    nOpera   = 0;

    &enNumPag = 0;
    Compara   = 0;
    Per1      = 0;
    Per2      = 0;

  &aDef06  = "";
  &aDef07  = "";
  &aDef12  = "";
  &aDef13  = "";
  &anyo2   = 0;
  &anyo1   = 0;
  &eaTitol = "";
  &ejer1   = "";
  &ejer2   = "";
  &enejer1 = 0;
  &enejer2 = 0;

   Numeracion = 0;
   Handle     = 0;
   nSwLegalia = 0;
   Calc       = 0;

   &i_debe = 0;       || Total del debe por registro
   &i_haber = 0;      || Total del haber por registro
   &i_deudor = 0;     || Total saldo deudor por registro
   &i_acreedor = 0;   || Total saldo acreedor por registro
   &t_debe = 0;       || Total del debe del informe
   &t_haber = 0;      || Total del haber del informe;
   &t_deudor = 0;     || Total saldo deudor por informe
   &t_acreedor = 0;   || Total saldo acreedor por informe
   &i_saldoap  = 0;
   &t_saldoap  = 0;
   &swlinea = 0;      || Pone o no linea en blanco en informe
   &swcomen = 0;
   swprint = 0;       || Variable para saber si imprimimos o no
   swsuma = 0;        || Variable para saber si sumamos o no
   saldo = 0;         || Resta entre el debe y el haber;
   nivelact = 0;      || Nivel actual
   rellenos = "";     || Variable para rellenar el hasta de la cuenta en zetas
   menor    = 0;
   x        = 0;
   varcamp  = 0;
   op_form  = 0;
   dos_ant  = "xx";
   vermov   = 0;

   &aElDepar = "";
   &a_depar = "";
   &a_cuen = "";
   &a_digi = 0;
   &a_desc = "";
   &a_busc = "";
   &a_impo = 0;
   &total  = 0;
   &descri = "";
   nSwOp   = 0;

   nEsPdf  = 0;
   NumMes  = 0;
   NombreMes = "";
|FIN;

|INCLUYE acceso_i;
|INCLUYE digito_i;
|INCLUYE puntos_i;
|INCLUYE dsmone_i;
|INCLUYE varnue_i;
|INCLUYE dscoto_i;
|INCLUYE tempo_i;
|| **********************************************************
|PROCESO &c_pagina;
   |SI sw_pagina = 0;
      pagina = #0#30;
      sw_pagina = 1;

      nPagDup = #0#30;
      nSwPagDup = 1;
   |SINO;
      pagina = pagina + 1;

      |HAZ PDuplex;
   |FINSI;
|FPRC;

|| ========================================================================
||                        CALCULOS PANTALLA
|| ========================================================================

|PROCESO deentrada;  |TIPO 7;
  |SI #0#14 = 0;  |Y #0#15 = 0;
      #0#14 = empre;
      #0#15 = ejerc - 1;
      |SI #0#15 < 0; #0#15 = 9; |FINSI;
      |PINTA #0#14;
      |PINTA #0#15;
  |FINSI;

  #0#6 = 1;
  |PINTA #0#6;
|FPRC;

|PROCESO mayor1;
  |SI #0Campo = "N";
      #0#14 = empre; |C_M #0#14,1,"N"; |PINTA #0#14;
      #0#15 = ejerc; |C_M #0#15,1,"N"; |PINTA #0#15;
  |SINO;
      |C_M #0#14,1,"S";
      |C_M #0#15,1,"S";
  |FINSI;
|FPRC;

|PROCESO esbuena;
  |SI FSalida = 9;  |ACABA;  |FINSI;
  |ABRE #30;
  #30#2 = #0#14;
  #30#3 = 0;
  |LEE 011#30];
  |SI FSalida ! 0;  |O #30#2 ! #0#14;
      |SI nSwMen = 0;
          |MENAV "     NO Existe la empresa Contable";
          |ERROR;
     |FINSI;
  |FINSI;
  |CIERRA #30;
|FPRC;

|PROCESO consulta;
  |SI FSalida ! 9;  |ACABA;  |FINSI;
  |CIERRAT #30;
  r_emp = 0;
  |SUB_EJECUTA "caconemp.run";
  |ABRET #30;
  |ERROR;
      #0#14 = r_emp;
      #0#15 = r_per;
      |PTEC 802;
      |PTEC 802;
|FPRC;

|PROCESO esbuena1;

  |SI FSalida = 2; |ACABA; |FINSI;
  swNoExiste = 0;
  |ABRE #30;
  #30#2 = #0#14;
  #30#3 = #0#15;
  |LEE 011#30=;
  |SI FSalida ! 0;
      |SI nSwMen = 0;
          |MENAV "     NO Existe Periodo en la empresa Contable";
          |ERROR;
      |FINSI;
      swNoExiste = 1;
      |VETE LeoActual;
  |FINSI;
  |SI #30#13 ! dig3;  |O #30#14 ! dig4;  |O #30#15 ! dig5;
                      |O #30#16 ! dig6;  |O #30#17 ! dig7;
                      |O #30#18 ! dig8;  |O #30#19 ! dig9;
      |SI nSwMen = 0;
          |MENAV "     NO CORRESPONDEN LOS NIVELES";
          |ERROR;
      |FINSI;
      swNoExiste = 1;
      |VETE LeoActual;
  |FINSI;

  #0#16 = #30#1; |PINTA #0#16;
  anu1  = 1900 + #30#26; |SI anu1 [ 1980; anu1 = anu1 + 100; |FINSI;
  nom1  = #30#1;
  |SI anu1 < 2008;
      |SI nSwMen = 0;
          |MENAV "    Periodo Seleccionado anterior al Ejercicio 2008.   NO COMPARABLE";
          |ERROR;
      |FINSI;
      swNoExiste = 1;
  |FINSI;

|ET LeoActual;
  #30#2 = empre;
  #30#3 = ejerc;
  |LEE 011#30=;
  anu  = 1900 + #30#26; |SI anu [ 1980; anu = anu + 100; |FINSI;
  |CIERRA #30;
|FPRC;


|PROCESO pintames; |TIPO 0;
  nNume1 = Campo + 2;
  |SI #0Campo ! 0; |Y #0Campo ! 13;
      NumMes = dig1 + (#0Campo - 1);
      |SI NumMes > 12;
          NumMes = NumMes - 12;
      |FINSI;
  |SINO;
      NumMes = #0Campo;
  |FINSI;
  |HAZ NombreMes;
  #0nNume1 = NombreMes; |PINTA #0nNume1;

  |SI Campo = 1; |Y  #0#0 > #0#1;
      |SI nSwMen = 0;
          |MENAV "    Error de Entrada ...";
          |ERROR;
      |FINSI;
  |FINSI;
|FPRC;

|PROCESO pintames2; |TIPO 0;
  |SI #0Campo ! 0; |Y #0Campo ! 13;
      NumMes = dig1 + (#0Campo - 1);
      |SI NumMes > 12;
          NumMes = NumMes - 12;
      |FINSI;
  |SINO;
      NumMes = #0Campo;
  |FINSI;
  |HAZ NombreMes;

  |ATRI I;
  |SI Campo = 7;  |PINTA 1543, NombreMes;  |FINSI;
  |SI Campo = 8;  |PINTA 1569, NombreMes;  |FINSI;
  |ATRI 0;
  |SI Campo = 8;  |Y #0#7 > #0#8;
      |SI nSwMen = 0;
          |MENAV "    Error de Entrada ...";
          |ERROR;
      |FINSI;
  |FINSI;
|FPRC;

|PROCESO NombreMes;
  |SI NumMes = 0;  NombreMes = "Apertura  "; |FINSI;
  |SI NumMes = 1;  NombreMes = "Enero     "; |FINSI;
  |SI NumMes = 2;  NombreMes = "Febrero   "; |FINSI;
  |SI NumMes = 3;  NombreMes = "Marzo     "; |FINSI;
  |SI NumMes = 4;  NombreMes = "Abril     "; |FINSI;
  |SI NumMes = 5;  NombreMes = "Mayo      "; |FINSI;
  |SI NumMes = 6;  NombreMes = "Junio     "; |FINSI;
  |SI NumMes = 7;  NombreMes = "Julio     "; |FINSI;
  |SI NumMes = 8;  NombreMes = "Agosto    "; |FINSI;
  |SI NumMes = 9;  NombreMes = "Septiembre"; |FINSI;
  |SI NumMes = 10; NombreMes = "Octubre   "; |FINSI;
  |SI NumMes = 11; NombreMes = "Noviembre "; |FINSI;
  |SI NumMes = 12; NombreMes = "Diciembre "; |FINSI;
  |SI NumMes = 13; NombreMes = "Cierre    "; |FINSI;
|FPRC;

|PROCESO nivel; |TIPO 7;
 aAlfa1 = #0#4;
 |C_M #0#5, 1,aAlfa1;
 |SI aAlfa1 = "N";
     |SI dig3 = 1; #0#5 = dig4; |FINSI;
     |SI dig3 = 2; #0#5 = dig5; |FINSI;
     |SI dig3 = 3; #0#5 = dig6; |FINSI;
     |SI dig3 = 4; #0#5 = dig7; |FINSI;
     |SI dig3 = 5; #0#5 = dig8; |FINSI;
     |SI dig3 = 6; #0#5 = dig9; |FINSI;
     |PINTA #0#5;
 |FINSI;
|FPRC;

|PROCESO miraniv; |TIPO 0;
  nLongT = #0#5;
  |SI nLongT > 4;
      nivel_bal = 0;
      |SI nLongT = dig4; nivel_bal = 1;  |FINSI;
      |SI nLongT = dig5; nivel_bal = 2;  |FINSI;
      |SI nLongT = dig6; nivel_bal = 3;  |FINSI;
      |SI nLongT = dig7; nivel_bal = 4;  |FINSI;
      |SI nLongT = dig8; nivel_bal = 5;  |FINSI;
      |SI nLongT = dig9; nivel_bal = 6;  |FINSI;
      |SI nivel_bal = 0;
          |SI nSwMen = 0;
              |MENAV "    Error. Longitud incorrecta";
              |ERROR;
          |FINSI;
      |FINSI;
  |FINSI;
|FPRC;

|PROCESO nosaldo; |TIPO 7;
   aAlfa1 = "S";
   |SI #0#40 = "S";  aAlfa1 = "N";  #0#41 = "S"; |FINSI;
   |C_M #0#41, 1, aAlfa1;
   |PINTA #0#41;
|FPRC;

|PROCESO cuenta; |TIPO 6;
    p_alfa1 = #0Campo;
    salidas = FSalida;
    |HAZ punto;
    |SI p_error ! 0;  |ERROR;  |ACABA;  |FINSI;
    |SI #0Campo = "            ";  |ACABA;  |FINSI;
    cta  = #0Campo;
    long = cta % 0;
    swdig = 0;
|SI long = #30#14; swdig = 1; digito = 1; |FINSI;
|SI long = #30#15; swdig = 1; digito = 2; |FINSI;
|SI long = #30#16; swdig = 1; digito = 3; |FINSI;
|SI long = #30#17; swdig = 1; digito = 4; |FINSI;
|SI long = #30#18; swdig = 1; digito = 5; |FINSI;
|SI long = #30#19; swdig = 1; digito = 6; |FINSI;
|SI swdig = 0;
    |MENAV "     Longitud de Cuenta Fuera de Nivel !";
    |ERROR;
|FINSI;
|FPRC;

|PROCESO numnivel;|TIPO 0;
   |SI #0#42 > #canempre#13;
      |MENAV "    El numero de nivel no coincide con el numero de nivel de la empresa";
      |ERROR;
   |FINSI;
|FPRC;

|PROCESO nivelcos; |TIPO 7;  || Calculo para hacer no modificable
   |C_M #0Campo,1,"S";
   |SI Campo = 20; |Y #0#42 < 2; || Si solo hemos seleccionado
       #0Campo = 0; |PINTA #0Campo;
       |C_M #0Campo,1,"N";
   |FINSI;
   |SI Campo = 21; |Y #0#42 < 3; || Si solo hemos seleccionado
       #0Campo = 0; |PINTA #0Campo;
       |C_M #0Campo,1,"N";
   |FINSI;
   |SI Campo = 22; |Y #0#42 < 4; || Si solo hemos seleccionado
       #0Campo = 0; |PINTA #0Campo;
       |C_M #0Campo,1,"N";
   |FINSI;
   |SI Campo = 23; |Y #0#42 < 5; || Si solo hemos seleccionado
       #0Campo = 0; |PINTA #0Campo;
       |C_M #0Campo,1,"N";
   |FINSI;
   |SI Campo = 24; |Y #0#42 < 6; || Si solo hemos seleccionado
       #0Campo = 0; |PINTA #0Campo;
       |C_M #0Campo,1,"N";
   |FINSI;
|FPRC;

|PROCESO nivelrep;|TIPO 0;  || Para comprobar que no repetimos los niveles

   |SI #0Campo > #canempre#13;
       |MENAV "    El numero de nivel no coincide con el numero de nivel de la empresa";
       |ERROR;
       |ACABA;
   |FINSI;

   |SI nivel = 0;        || Buscamo el nivel mas alto;
       nivel = #0#19;
   |FINSI;

   |SI Campo = 19; |ACABA; |FINSI;
   |SI Campo = 20;
      |SI #0#20 = #0#19;|Y #0#20 ! 0;    || Si hay dos niveles iguales error
          |MENAV error1;
          |ERROR;
          |ACABA;
      |SINO;
         |SI #0#20 > #0#19;  || Busca el nivel mas alto
             nivel = #0#20;
         |SINO;
            nivel = #0#19;
         |FINSI;
      |FINSI;
   |FINSI;

   |SI Campo = 21; |Y #0#21 ! 0;
      |SI #0#21 = #0#19; |O #0#21 = #0#20; || Si hay dos niveles iguales error
          |MENAV error1;
          |ERROR;
          |ACABA;
      |FINSI;

      |SI #0#21 > nivel;  || Busca el nivel mas alto
          nivel = #0#21;
      |FINSI;
   |FINSI;

   |SI Campo = 22; |Y #0#22 ! 0;      || Si hay dos niveles iguales error
      |SI #0#22 = #0#19; |O #0#22 = #0#20; |O #0#22 = #0#21;
         |MENAV error1;
         |ERROR;
         |ACABA;
      |FINSI;

      |SI #0#22 > nivel;  || Busca el nivel mas alto
         nivel = #0#22;
      |FINSI;
   |FINSI;

   |SI Campo = 23; |Y #0#23 ! 0;     || Si hay dos niveles iguales error
      |SI #0#23 = #0#19; |O #0#23 = #0#20; |O #0#23 = #0#21; |O #0#23 = #0#22;
         |MENAV error1;
         |ERROR;
         |ACABA;
      |FINSI;

      |SI #0#23 > nivel;  || Busca el nivel mas alto
         nivel = #0#23;
      |FINSI;
   |FINSI;

   |SI Campo = 24; |Y #0#24 ! 0;    || Si hay dos niveles iguales error
      |SI #0#24 = #0#19; |O #0#24 = #0#20; |O #0#24 = #0#21; |O #0#24 = #0#22; |O #0#24 = #0#23;
         |MENAV error1;
         |ERROR;
         |ACABA;
      |FINSI;

      |SI #0#24 > nivel;  || Busca el nivel mas alto
         nivel = #0#24;
      |FINSI;
   |FINSI;

|FPRC;

|PROCESO estruct; |TIPO 0;
  |SI #0#6 = 0;  |ACABA;  |FINSI;

  |ABRE #6;
  #6#0 = #0#6;
  |LEE 040#6=;
  |SI FSalida ! 0;
      |CIERRA #6;
      |SI nSwMen = 0;
          |MENAV "    No existe la estructura de descarga.";
          |ERROR;
      |FINSI;
      |ACABA;
  |FINSI;
  |HAZ cue_per;
  |CIERRA #6;
|FPRC;

|PROCESO cue_per; || calcula la cuenta de perdidas y ganancias
  cue_perdi = #6#2;
  dig_perdi = #6#3;
  |SI #0#4 = "N";
      aAlfa1 = #6#2; |QBF aAlfa1;
      aAlfa1 = aAlfa1 % 0;
      nLong  = aAlfa1;
  |SINO;
      traraf = 100 + #0#5;
      cue_perdi = #6#2 % traraf;
      dig_perdi = nivel;
  |FINSI;
  cue_perdi = cue_perdi + "            ";
  cue_perdi = cue_perdi % 112;
|FPRC;

|PROCESO descri; |TIPO 6;
 nSwErrorBal = 0;
 |ABRE #420;
 |DDEFECTO #420;
 #420#0 = #0Campo;
 |LEE 001#420=;
 |SI FSalida ! 0;
     |SI nSwMen = 0;
         |MENAV "   Error. No existe Numero de Balance";
         |ERROR;
     |FINSI;
     nSwErrorBal = 1;
 |FINSI;
 |CIERRA #420;
 nNume1 = Campo + 1;
 #0nNume1 = #420#1; |SI nSwMen = 0; |PINTA #0nNume1; |FINSI;
|FPRC;

|CALCULO AntesDepar; |TIPO 7;
   |C_M #0Campo, 0, aAlfa1;
   |SI aAlfa1 = "S";
      |C_M #0#47,1,eaDepar;
      |C_M #0#48,1,eaDepar;
   |FINSI;
|FCAL;

|CALCULO ImpInv; |TIPO 0;
   aAlfa1 = #0#43;
   |C_M #0#44,1,aAlfa1;
   |C_M #0#45,1,aAlfa1;
   |C_M #0#46,1,aAlfa1;
   |C_M #0#47,1,aAlfa1;
   |C_M #0#48,1,aAlfa1;
|FCAL;

|| ****************************************************************
|| ****************************************************************

|PROCESO conexi;
  nCam1 = empre;
  nCam2 = ejerc;
  nCam3 = enEjercicio;
  d_mes = #0#7;
  h_mes = #0#8;
  estru = #0#6;
  r_emp = empre;
  r_per = ejerc;
  r_eje = #30#26;
  r_nom = #30#1;
  |SUB_EJECUTA "caexicon";

  |SI #0#13 = "N"; |ACABA; |FINSI;
  r_emp = #0#14;
  r_per = #0#15;
  r_eje = anu1 - 2000; |SI r_eje < 0; r_eje = anu1 - 1900; |FINSI;
  r_nom = nom1;
  |SUB_EJECUTA "caexicon";

  empre = nCam1;
  ejerc = nCam2;
  enEjercicio = nCam3;
|FPRC;


|PROCESO MiroSiHay;
  |SI nHay = -1; nHay = 0; |FINSI;
  nNume1 = 0;
  |PARA nPara1 = 9; |SI nPara1 [ 47; |HACIENDO nPara1 = nPara1 + 1;
        |SI #1nPara1 ! 0; nNume1 = 1; |SAL; |FINSI;
  |SIGUE;
  |SI nNume1 ! 0; nHay = 1; |ERROR10; |FINSI;
|FPRC;

|DEFBUCLE MiroSiHay;
   #1#1;
   3;
   "8           ", 0, "S";
   "999999999999", 9, "S";
   ;
   NULL, MiroSiHay;
|FIN;


|PROCESO conexi8y9;
  nHay = -1;
  |BUCLE MiroSiHay;
  |SI nHay = 1;
      d_mes = #0#0;
      h_mes = #0#1;
      |SI #0#31 = "N"; |SUB_EJECUTA "ca8z0011;BALANCEN"; |FINSI;
      |SI #0#31 = "S"; |SUB_EJECUTA "ca8z0011;BALANCES"; |FINSI;
  |FINSI;
|FPRC;

|| ***************************************************************************
|| *********************  CALCULO DE IMPRESION *******************************
|| ***************************************************************************

|| ... // Balance de Sumas y Saldos
|PROCESO rellena;
   rellenos = "zzzzzzzzzzzz";
   menor = -1;

   |PARA nPara1 = 19; |SI nPara1 [ 24; |HACIENDO nPara1 = nPara1 + 1;
      |SI #0nPara1 ! 0;
         |SI menor = -1;  menor = #0nPara1;  |FINSI;
         |SI menor > #0nPara1;  menor = #0nPara1;  |FINSI;
      |FINSI;                   || saca el nivel elegido mas peque¤o
   |SIGUE;

   |SI menor ! 0;
      |SI menor = 1;  nNume1 = dig4;  |FINSI;
      |SI menor = 2;  nNume1 = dig5;  |FINSI;   || construye el hasta segun
      |SI menor = 3;  nNume1 = dig6;  |FINSI;   ||/el nivel mas bajo elegido
      |SI menor = 4;  nNume1 = dig7;  |FINSI;
      |SI menor = 5;  nNume1 = dig8;  |FINSI;
      |SI menor = 6;  nNume1 = dig9;  |FINSI;
      nNume1 = 100 + nNume1;
      rellenos = #0#39 % nNume1;
      rellenos = rellenos + "zzzzzzzzzzzz";
      rellenos = rellenos % 112;
   |FINSI;
|FPRC;

|PROCESO borrar;
   sw = 0;
   swcomen = 0;
   t_debe = 0;
   t_haber = 0;
   t_deudor = 0;
   t_acreedor = 0;
   t_saldoap  = 0;
|FPRC;

|PROCESO artenvas;
   aAlfa1 = #1#0;
   aAlfa1 = aAlfa1 % 102;

   |SI dos_ant = "xx"; dos_ant = aAlfa1;    |FINSI;

   |SI aAlfa1 ! dos_ant;
      dos_ant = aAlfa1;
      swlinea = 1;          || cuando solo se ha seleccionado un nivel y
   |SINO;                        ||/cambia en sus dos primeros digitos se
      swlinea = 0;          ||/imprime una linea en blanco!!!
   |FINSI;
|FPRC;

|PROCESO operaimpre;     || impresora
   vermov = 0;
   |PARA x = #0#0; |SI x [ #0#1; |HACIENDO x = x + 1;
      i_debe = i_debe + #1varcamp;                   || acumula debe
      |SI #1varcamp ! 0; vermov = 1; |FINSI;

      varcamp = varcamp + 1;
      i_haber = i_haber + #1varcamp;                 || acumula haber
      |SI #1varcamp ! 0; vermov = 1; |FINSI;

      varcamp = varcamp + 2;
   |SIGUE;

   |SI op_form = 3;                      || acumula saldo inicial
       i_debe    = i_debe  - #1#9;       || debe
       i_haber   = i_haber - #1#10;      || haber
       i_saldoap = #1#11;
   |FINSI;

   i_debe    =  i_debe  ! EURODB;
   i_haber   =  i_haber ! EURODB;
   i_saldoap =  i_saldoap ! EURODB;

   saldo = (i_debe - i_haber + i_saldoap) ! EURODB;   || saldo

   |SI saldo > 0; i_deudor = saldo; i_acreedor = 0;            |FINSI;
   |SI saldo < 0; i_deudor = 0;     i_acreedor = saldo * (-1); |FINSI;
   |SI saldo = 0; i_deudor = 0;     i_acreedor = 0;            |FINSI;

   |SI vermov = 0; |Y #0#40 = "N"; |ACABA; |FINSI;

   |SI saldo = 0; |Y #0#41 = "N";    || cuentas sin saldo
       |ACABA;
   |FINSI;

   |SI nivelact ! #1#55;         || cambio de nivel, linea en blanco
       swlinea = 1;
       nivelact = #1#55;
   |SINO;
      swlinea = 0;
   |FINSI;

   |SI #0#42 = 1;  |HAZ artenvas; |FINSI;   || separacion lineas formato ARTENVAS

   |PRINT;
   swcomen = 1;              || linea suma anterior
   |SI swsuma = 1;     || acumula totales
      t_saldoap  = (t_saldoap + i_saldoap) ! EURODB;      || apertura
      t_debe     = (t_debe + i_debe) ! EURODB;            || debe
      t_haber    = (t_haber + i_haber) ! EURODB;          || haber
      t_deudor   = (t_deudor + i_deudor) ! EURODB;        || saldo deudor
      t_acreedor = (t_acreedor + i_acreedor) ! EURODB;    || saldo acreedor
   |FINSI;
|FPRC;

|PROCESO condicion;      || Calculo condiciones de impresion
   swprint = 0;         || Variable para saber si imprimimos o no
   swsuma = 0;          || Variable para saber si sumamos o no
   i_debe = 0;          || Pone a cero el total debe del registro
   i_haber = 0;         || Pone a cero el total haber del registro
   i_saldoap = 0;

   |SI sw = 0;
      nivelact = #1#55;     || Nivel actual
      sw = 1;
   |FINSI;

   |SI #1#55 ! 0;     || Condicion para saber si el nivel esta seleccionado
      |SI #1#55 = #0#19;|O #1#55 = #0#20;|O #1#55 = #0#21;      || Compara 3 niveles
         swprint = 1;
      |SINO;
         |SI #1#55 = #0#22;|O #1#55 = #0#23;|O #1#55 = #0#24; || Compara 3 niveles
            swprint = 1;
         |SINO;
            swprint = 0;
         |FINSI;
      |FINSI;
   |FINSI;

   |SI swprint = 1; |Y #1#55 = nivel;      || Si es el nivel mas alto entoces
       swsuma = 1;                         || se suma.
   |FINSI;

   |SI #1#3 = "S";|Y #1#55 [ nivel;   || Si el nivel no esta seleccionado
      swsuma = 1;                || pero tiene en el campo pase una S
      swprint = 1;               || entonces se imprime y se suma.
   |FINSI;

   |SI swprint = 1;    || Si se puede imprimir
       varcamp = ((#0#0 * 3) + 9);
       |HAZ operaimpre;
   |FINSI;
|FPRC;

|DEFBUCLE caycosy00;      || plan contable (3ra. clave)
   #1#3;
   ;
   #0#38, 1;
   rellenos, 6;
   e;
   NULL, condicion;
|FIN;

|PROCESO SumasySaldos;

  |HAZ borrar;
  |BUCLE caycosy00;
  |SI sw = 1;
      |PIEINF;
  |FINSI;
|FPRC;

|PROCESO ImpSumasySaldos;
  enInfTodo = 1;

  #0#29 = "BALANCE DE SUMAS Y SALDOS";

  |HAZ rellena;

|SI nEsPdf = 0;
    informe = "caycosy0"; |SI #0#18 = "N"; informe = "caycosy1"; |FINSI;
    |INFOR informe;
    |SI FSalida ! 0;
        |SI nSwMen = 0; |MENAV "     Error en informe."; |FINSI;
         |ACABA;
     |FINSI;
|FINSI;

  |SI nSwLegalia = 1;

      #0#0 = 0; #0#1 = 0;
      Campo = 0; |HAZ pintames;
      Campo = 1; |HAZ pintames;
      |HAZ SumasySaldos;

      #0#0 = Per1; #0#1 = 3;
      Campo = 0; |HAZ pintames;
      Campo = 1; |HAZ pintames;
      |HAZ SumasySaldos;

      #0#0 = Per1; #0#1 = 6;
      Campo = 1; |HAZ pintames;
      |HAZ SumasySaldos;

      #0#0 = Per1; #0#1 = 9;
      Campo = 1; |HAZ pintames;
      |HAZ SumasySaldos;

      #0#0 = Per1; #0#1 = 12;
      Campo = 1; |HAZ pintames;
      |HAZ SumasySaldos;

      #0#0 = 0;  #0#1 = 12;
      Campo = 0; |HAZ pintames;
      Campo = 1; |HAZ pintames;
  |SINO;
       |HAZ SumasySaldos;
  |FINSI;

  |SI nEsPdf = 0;  |FININF;  |FINSI;
|FPRC;
|| ////////////////////////////////////////////////////////////////////////

|| ... ... ... Comunes
|PROCESO HaySaldoSI;
  |SI #5#13 ! 0; |O #5#14 ! 0;
      nHay = 1;
      |ERROR10;
  |FINSI;
|FPRC;

|DEFBUCLE HaySaldoSI;
 #5#1;
 ;
 #3#0, #3#1, #3#2, #3#3, #3#4, nDCampo5, nDCampo6, nDCampo7, nDCampo8, "            ";
 #3#0, #3#1, #3#2, #3#3, #3#4, nHCampo5, nHCampo6, nHCampo7, nHCampo8, "zzzzzzzzzzzz";
 e;
 NULL, HaySaldoSI;
|FIN;

|PROCESO HaySaldo;
   nDCampo5 = #3#5; nHCampo5 = #3#5;
   |SI #3#5 = 0;
       nDCampo5 = 0;
       nHCampo5 = 9;
   |FINSI;
   nDCampo6 = #3#6; nHCampo6 = #3#6;
   |SI #3#6 = 0;
       nDCampo6 = 00;
       nHCampo6 = 99;
   |FINSI;
   nDCampo7 = #3#7; nHCampo7 = #3#7;
   |SI #3#7 = 0;
       nDCampo7 = 00;
       nHCampo7 = 99;
   |FINSI;
   nDCampo8 = #3#8; nHCampo8 = #3#8;
   |SI #3#8 = 0;
       nDCampo8 = 0;
       nHCampo8 = 9;
   |FINSI;
   nHay = 0;
   |BUCLE HaySaldoSI;
|FPRC;

|PROCESO cuentas;
  cantidad  = #5#13;
  cantidad2 = #5#14;
  |SI cantidad ! 0; |O cantidad2 ! 0; |O eaValeCero ! "N";
      |PRINT;
  |FINSI;
  sw_inf = 4;
|FPRC;

|DEFBUCLE cabasitu2;
 #5#1;
 ;
 #3#0, #3#1, #3#2, #3#3, #3#4, #3#5, #3#6, #3#7, #3#8, "            ";
 #3#0, #3#1, #3#2, #3#3, #3#4, #3#5, #3#6, #3#7, #3#8, "zzzzzzzzzzzz";
 e;
 NULL, cuentas;
|FIN;

|PROCESO agrupacion;
  sw_epi = 1;                || fuerza la impresion del epigrafe
|FPRC;
|| ...

|| ... // Balance de Situacion
|PROCESO impresio_S;

  |SI #3#9 ! "A"; nImprimir = 0; |FINSI;

  |SI #3#13 = 0; |Y #3#14 = 0; |Y #3#9 ! "A";
      nOpera = 0;
      |SI eaValeCero = "N"; nOpera = 1; |FINSI;
      |SI #3#0 = "A";
          |SI enActivo ! 0; |O enActiv2 ! 0;
              nOpera = 1;
          |FINSI;
      |FINSI;
      |SI #3#0 = "P";
          |SI enPasivo ! 0; |O enPasiv2 ! 0;
              nOpera = 1;
          |FINSI;
      |FINSI;
      |SI #3#0 = tipo;               nOpera = 1; |FINSI;
      |SI nOpera = 1;
          nHay = 0;
          |SI #0#4 = "S"; |HAZ HaySaldo; |FINSI;
          |SI nHay = 0; |ACABA; |FINSI;
      |FINSI;
  |FINSI;

  |SI sw = 0;
      tipo     = #3#3;  || Activo o Pasivo
      agrupaci = #3#4;
      subagrup = #3#5;
      |SI #3#3 = "A"; doh = "ACTIVO"; |FINSI;
      |SI #3#3 = "P"; doh = "PASIVO"; |FINSI;
      |HAZ agrupacion;
      sw = 1;
  |FINSI;

  |SI tipo ! #3#3;
      sw_inf = 5;  |PRINT;  |PRINT;
      sw_inf = 1;
      |SI tipo = "A";
          variable = "                        T O T A L   A C T I V O:";
          canti  = enActivo;
          canti2 = enActiv2;
      |SINO;
          variable = "                        T O T A L   P A S I V O:";
          canti  = enPasivo;
          canti2 = enPasiv2;
      |FINSI;
      |PRINT;
      doh = "PASIVO";
      |PIEINF;
      tipo     = #3#3;
      agrupaci = #3#4;
      subagrup = #3#5;
      |HAZ agrupacion;
  |FINSI;

  |SI agrupaci ! #3#4; |O subagrup ! #3#5;
      sw_inf = 5;  |PRINT;       || linea en blanco
      |HAZ agrupacion;
      agrupaci = #3#4;
      subagrup = #3#5;
  |FINSI;

  |SI #3#9 = "C"; |O #3#9 = "S"; |O #3#9 = "T";
      |SI #3#7 ! 0; sw_inf = 2; |FINSI;
      |SI #3#7 = 0; sw_inf = 0; |FINSI;
  |FINSI;
  |SI #3#9 = "A"; sw_inf = 7; |FINSI;
  |SI #3#9 ! "A"; |O nImprimir = 1;
      |PRINT;
      nImprimir = 1;
  |FINSI;

  |SI #3#9 = "C"; |Y #0#4 = "S";
      sw_inf=3;
      nOpera = 0; |BUCLE cabasitu2;
  |FINSI;
|FPRC;


|DEFBUCLE cabasitu1;
 #3#1;
 ;
 nBalNum, nBalance, 0, "A", 00, 0, 00, 00, 0;
 nBalNum, nBalance, 0, "P", 99, 9, 99, 99, 9;
 e;
 NULL,impresio_S;
|FIN;

|PROCESO final_S;
  sw_inf = 5; |PRINT; |PRINT;
  sw_inf = 1;
  |SI tipo = "A";
      variable = "                        T O T A L   A C T I V O:";
      canti  = enActivo;
      canti2 = enActiv2;
      doh = "PASIVO";
  |SINO;
      variable = "                        T O T A L   P A S I V O:";
      canti  = enPasivo;
      canti2 = enPasiv2;
  |FINSI;
  |PRINT;
|FPRC;

|PROCESO Situacion;
  enInfTodo = 2;
  nBalance  = 0;
  |SUB_EJECUTA "ca8ys001;PDF";

  aFichero = ("8t" + Usuario) % 108; |NOME_DAT #3 aFichero;
  aFichero = ("8e" + Usuario) % 108; |NOME_DAT #4 aFichero;
  aFichero = ("8c" + Usuario) % 108; |NOME_DAT #5 aFichero;
  aFichero = ("8f" + Usuario) % 108; |NOME_DAT #2 aFichero;

  #0#29 = "BALANCE DE SITUACION";

|SI nEsPdf = 0;
    informe = "cr8ys001"; |SI #0#13 = "S"; informe = "cr8ysc01"; |FINSI;
    |SI #8#4 = "S";  informe = "ca" + (informe % 306); |FINSI;
    |INFOR informe;
    |SI FSalida ! 0;
        |SI nSwMen = 0; |MENAV "     Error en informe."; |FINSI;
         |ACABA;
     |FINSI;
|FINSI;

  sw = 0;
  |BUCLE cabasitu1;
  |HAZ final_S;
  |PIEINF;

  |DELINDEX #2;
  |DELINDEX #3;
  |DELINDEX #4;
  |DELINDEX #5;

  |SI nEsPdf = 0;  |FININF;  |FINSI;

|FPRC;

|| ... /// Cuenta de Resultados

|PROCESO impresio_E;

  |SI #3#9 ! "A"; nImprimir = 0; |FINSI;

  |SI #3#13 = 0; |Y #3#14 = 0; |Y #3#6 ! 0; |Y #3#9 ! "A";
      |SI eaValeCero = "N";
          nHay = 0;
          |SI #0#4 = "S"; |HAZ HaySaldo; |FINSI;
          |SI nHay = 0; |ACABA; |FINSI;
      |FINSI;
  |FINSI;

  |SI sw = 0;
      tipo     = #3#3;  ||Activo o Pasivo
      agrupaci = #3#4;
      subagrup = #3#5;
      |HAZ agrupacion;
      sw = 1;
  |FINSI;

  |SI agrupaci ! #3#4; |O subagrup ! #3#5;
      sw_inf = 5;  |PRINT;       || linea en blanco
      |HAZ agrupacion;
      agrupaci = #3#4;
      subagrup = #3#5;
  |FINSI;

  |SI #3#9 = "T";
      sw_inf = 0;
      |SI #3#5 = 0; |Y #3#6 = 0; sw_inf = 6; |FINSI;
  |FINSI;
  |SI #3#9 = "S"; sw_inf = 0; |FINSI;
  |SI #3#9 = "C";
      sw_inf = 2;
      |SI #3#5 = 0; |Y #3#7 = 0; sw_inf = 0; |FINSI;  ||es un total con Cuentas
  |FINSI;
  |SI #3#9 = "A"; sw_inf = 7; |FINSI;

  |SI #3#9 ! "A"; |O nImprimir = 1;
      |PRINT;
      nImprimir = 1;
  |FINSI;

  |SI #3#9 = "C"; |Y #0#4 = "S";
      sw_inf=3;
      |BUCLE cabasitu2;
   |FINSI;
|FPRC;

|DEFBUCLE cabaexpl1;
 #3#1;
 ;
 nCtaNum, nBalance, 1, "R", 00, 0, 00, 00, 0;
 nCtaNum, nBalance, 5, "R", 99, 9, 99, 99, 9;
 e;
 NULL,impresio_E;
|FIN;

|PROCESO final_E;
  sw_inf = 5;
  sw_inf = 1;
|FPRC;

|PROCESO Resultados;
  enInfTodo = 3;
  nBalance  = 1;
  |SUB_EJECUTA "ca8ye001;PDF";

  aFichero = ("8t" + Usuario) % 108; |NOME_DAT #3 aFichero;
  aFichero = ("8e" + Usuario) % 108; |NOME_DAT #4 aFichero;
  aFichero = ("8c" + Usuario) % 108; |NOME_DAT #5 aFichero;

  #0#29 = "CUENTA DE PERDIDAS Y GANANCIAS";
  aAlfa1 = #0#29; |QBF aAlfa1;
  aAlfa1 = aAlfa1 + " " + eaNomICta;
  #0#29 = aAlfa1;

|SI nEsPdf = 0;
    informe = "cr8ye001"; |SI #0#13 = "S"; informe = "cr8yec01"; |FINSI;
    |SI #8#4 = "S";  informe = "ca" + (informe % 306); |FINSI;
    |INFOR informe;
    |SI FSalida ! 0;
        |SI nSwMen = 0; |MENAV "     Error en informe."; |FINSI;
         |ACABA;
     |FINSI;
|FINSI;

  sw = 0;
  |BUCLE cabaexpl1;
  |HAZ final_E;
  |PIEINF;

  |DELINDEX #3;
  |DELINDEX #4;
  |DELINDEX #5;

  |SI nEsPdf = 0;  |FININF;  |FINSI;

|FPRC;

|| ... /// Ingresos y Gastos

|PROCESO impresio_G;

  |SI #3#9 ! "A"; nImprimir = 0; |FINSI;
  |SI sw = 0;
      tipo     = #3#3;
      agrupaci = #3#4;
      subagrup = #3#5;
      |HAZ agrupacion;
      sw = 1;
  |FINSI;

  |SI agrupaci ! #3#4; |O subagrup ! #3#5;
      sw_inf = 5;  |PRINT;       || linea en blanco
      |HAZ agrupacion;
      agrupaci = #3#4;
      subagrup = #3#5;
  |FINSI;

  |SI #3#9 = "T";
      sw_inf = 0;
      |SI #3#5 = 0; |Y #3#6 = 0; sw_inf = 6; |FINSI;
  |FINSI;
  |SI #3#9 = "S"; sw_inf = 0; |FINSI;
  |SI #3#9 = "C";
      sw_inf = 2;
      |SI #3#5 = 0; |Y #3#7 = 0; sw_inf = 0; |FINSI;  ||es un total con Cuentas
  |FINSI;
  |SI #3#9 = "A"; sw_inf = sw_inf + 7; |FINSI;

  |SI #3#9 ! "A"; |O nImprimir = 1;
      |PRINT;
      nImprimir = 1;
  |FINSI;

  |SI #3#9 = "C"; |Y #0#4 = "S";
      sw_inf=3;
      |BUCLE cabasitu2;
   |FINSI;
|FPRC;

|DEFBUCLE cabaings1;
 #3#1;
 ;
 nBalNum, nBalance, 0, "E", 00, 0, 00, 00, 0;
 nBalNum, nBalance, 9, "E", 99, 9, 99, 99, 9;
 e;
 NULL,impresio_G;
|FIN;

|PROCESO IngYGastos;
  enInfTodo = 4;
  nBalance  = 2;

  |SI nTipBal = 3; |ACABA; |FINSI; || balance PYME, no procede

  nHay = -1;
  |BUCLE MiroSiHay;
  |SI nHay [ 0; |ACABA; |FINSI;  || No procede Balance

  |SUB_EJECUTA "ca8yp001;PDF";

  aFichero = ("8t" + Usuario) % 108; |NOME_DAT #3 aFichero;
  aFichero = ("8e" + Usuario) % 108; |NOME_DAT #4 aFichero;
  aFichero = ("8c" + Usuario) % 108; |NOME_DAT #5 aFichero;

  #0#29 = "ESTADO DE INGRESOS Y GASTOS RECONOCIDOS EN EL PAT. NETO";
  aAlfa1 = #0#29; |QBF aAlfa1;
  aAlfa1 = aAlfa1 + " " + eaNomICta;
  #0#29 = aAlfa1;

|SI nEsPdf = 0;
    informe = "cr8yp001"; |SI #0#13 = "S"; informe = "cr8ypc01"; |FINSI;
    |SI #8#4 = "S";  informe = "ca" + (informe % 306); |FINSI;
    |INFOR informe;
    |SI FSalida ! 0;
        |SI nSwMen = 0; |MENAV "     Error en informe."; |FINSI;
         |ACABA;
     |FINSI;
|FINSI;

  sw = 0;
  |BUCLE cabaings1;
  |PIEINF;

  |DELINDEX #3;
  |DELINDEX #4;
  |DELINDEX #5;

  |SI nEsPdf = 0;  |FININF;  |FINSI;
|FPRC;

|| //////////////////////////////////////////////////////////////////
|| ... /// Estado de Cambios del Patrimonio Neto
|PROCESO Recorta;
 |QBF aAlfa1;
 aAlfa2 = aAlfa1 % 0;
 nNume4 = aAlfa2;
 nNume4 = ((15 - nNume4) / 2) ! 0;
 aAlfa1 =(( " " * nNume4) + aAlfa1) % 115;
|FPRC;

|PROCESO Leem804;

  |PDEFECTO #902,16,21;
  aCasilla = "";
  |SI #31#4 ! 0;
      aCasilla = #31#4;
      aCasilla = ("000" + aCasilla) % -103;
  |FINSI;
  #902#16  = #31#5;
  #902#17 = aCasilla;

  |SI sw = 0;
      tipo = #31#3;
      sw = 1;
      sw_inf = 1;
  |SINO;
      |SI tipo ! #31#3;
          sw_inf = 5; |PRINT;
          tipo = #31#3;
          sw_inf = 1;
      |FINSI;
  |FINSI;

  |SI nCol < 13;
      nCol1 = nCol;     nNume1 = nCol1 + 5;
      nCol2 = nCol + 1; nNume2 = nCol2 + 5;
      nCol3 = nCol + 2; nNume3 = nCol3 + 5;
      #902#18 = #31nNume1;
      #902#19 = #31nNume2;
      #902#20 = #31nNume3;
  |SINO;
      nCol1 = nCol;     nNume1 = nCol1 + 5;
      nCol2 = 0;
      nCol3 = 0;
      #902#18 = #31nNume1;
      #902#19 = 0;
      #902#20 = 0;
  |FINSI;

  swImp1 = 1; swImp2 = 1; swImp3 = 1;
  #442#0 = nBalNum;
  #442#1 = #31#2;
  |LEE 001#442=;
  |SI nCol1 < 13;
      nNume1 = nCol1 + 6; |SI #442nNume1 ! "X";  swImp1 = 0; |FINSI;
      nNume1 = nCol2 + 6; |SI #442nNume1 ! "X";  swImp2 = 0; |FINSI;
      nNume1 = nCol3 + 6; |SI #442nNume1 ! "X";  swImp3 = 0; |FINSI;
  |SINO;
      swImp1 = 0;
  |FINSI;

  |PRINT;
  sw_inf = 0;
  sw = 1;
|FPRC;

|DEFBUCLE Leem804;
 #31#1;
 ;
 enEmpresa, enEjercicio, 001;
 enEmpresa, enEjercicio, 999;
 e;
 NULL,Leem804;
|FIN;

|PROCESO ECPN;

  enInfTodo = 5;
  nBalance  = 3;
  |SI #0#34 = "S";
      |SUB_EJECUTA "ca8yc001;PDF";
  |FINSI;

  |HAZ eCargaECPN;
  |SI aDef12 ! ""; |Y aDef06 ! "";
      #0#29 = "BALANCE DE ESTADO DE CAMBIOS DEL PATRIMONIO NETO";
      aAlfa1 = #0#29; |QBF aAlfa1;
      aAlfa1 = aAlfa1 + " " + eaNomICta;
      #0#29 = aAlfa1;
      ejer1   =  enEjercicio;
      ejer2   =  enEjercicio - 1;
      enejer1 = ejer1;
      enejer2 = ejer2;
      eaTitol = #0#29;

|SI nEsPdf = 0;
    informe = "cr8yc001"; |SI #8#4 = "S"; informe = "ca8yc001"; |FINSI;
    |INFOR informe;
    |SI FSalida ! 0;
        |SI nSwMen = 0; |MENAV "     Error en informe."; |FINSI;
         |ACABA;
     |FINSI;
|FINSI;

      |ABRE #444;
      |ABRE #442;
      |PARA nCol = 1; |SI nCol [ 13; |HACIENDO nCol = nCol + 3;
            sw      = 0;
            |PDEFECTO #902,10,16;
            #902#21 = ""; #902#22 = ""; #902#23 = "";
            |SI nCol < 13;
               eaRaya = "=" * 52;
               |DDEFECTO #444;
               #444#0 = nBalNum;
               #444#1 = nCol;
               |LEE 001#444=;
               #902#10 = #444#3; #902#11 = #444#4;
               #902#21 = ("00" + #444#1) % -102;

               |DDEFECTO #444;
               #444#0 = nBalNum;
               #444#1 = nCol + 1;
               |LEE 001#444=;
               #902#12 = #444#3; #902#13 = #444#4;
               #902#22 = ("00" + #444#1) % -102;

               |DDEFECTO #444;
               #444#0 = nBalNum;
               #444#1 = nCol + 2;
               |LEE 001#444=;
               #902#14 = #444#3; #902#15 = #444#4;
               #902#23 = ("00" + #444#1) % -102;
           |SINO;
               #902#10 = ""; #902#11 = "   T O T A L   ";
               #902#21 = "13";
           |FINSI;
           aAlfa1 = #902#10; |HAZ Recorta; #902#10 = aAlfa1;
           aAlfa1 = #902#11; |HAZ Recorta; #902#11 = aAlfa1;
           aAlfa1 = #902#12; |HAZ Recorta; #902#12 = aAlfa1;
           aAlfa1 = #902#13; |HAZ Recorta; #902#13 = aAlfa1;
           aAlfa1 = #902#14; |HAZ Recorta; #902#14 = aAlfa1;
           aAlfa1 = #902#15; |HAZ Recorta; #902#15 = aAlfa1;
           |BUCLE Leem804;
           |SI sw = 1; |PIEINF; |FINSI;
      |SIGUE;
      |CIERRA #442;
      |CIERRA #444;

  |FINSI;
  |HAZ eDesCargaECPN;

  |SI nEsPdf = 0;  |FININF;  |FINSI;

|FPRC;
|| /////////////////////////////////////////////////////////////////////
|| ... Estado de Flujos de Efectivo
|PROCESO Leem805;

  |PDEFECTO #902,10,21;
  aCasilla = "";
  |SI #33#16 ! 0;
      aCasilla = #33#16;
      aCasilla = ("000000" + aCasilla) % -106;
  |FINSI;
  #902#16 = #33#3;
  #902#17 = aCasilla;
  #902#18 = #33#7;
  #902#19 = #33#6;
  #902#24 = #33#5;
  |SI #33#8 ! "A"; nImprimir = 0; |FINSI;

  |SI #33#7 = 0; |Y #33#6 = 0; |Y #33#8 ! "A";
      |SI eaValeCero = "N"; |ACABA; |FINSI;
  |FINSI;

  |SI sw = 0;
      sw = 1;
  |FINSI;

  sw_inf = 0;
  |SI #33#4 = "G"; sw_inf = 1; |FINSI;
  |SI #33#8 = "A"; sw_inf = 7; |FINSI;
  |SI #33#16 = 0; sw_inf = 7; |FINSI;
  |SI #33#8 ! "A"; |O nImprimir = 1;
      |PRINT;
      nImprimir = 1;
  |FINSI;
|FPRC;

|DEFBUCLE Leem805;
 #33#1;
 ;
 enEmpresa, enEjercicio, 0001;
 enEmpresa, enEjercicio, 9999;
 e;
 NULL,Leem805;
|FIN;


|PROCESO LeoParaImprimir;
  |SI #33#6 ! 0; |O #33#7 ! 0;
      nImprimir = 1;
      |ERROR10;
  |FINSI;
|FPRC;

|DEFBUCLE LeoParaImprimir;
 #33#1;
 ;
 enEmpresa, enEjercicio, 0001;
 enEmpresa, enEjercicio, 9999;
 e;
 NULL, LeoParaImprimir;
|FIN;

|PROCESO EFE;
  enInfTodo = 6;
  nBalance  = 4;
  |SI #0#35 = "S";
      |SUB_EJECUTA "ca8yf001;PDF";
  |FINSI;
  nImprimir = 0;
  |BUCLE LeoParaImprimir;
  |SI nImprimir = 1;
      #0#29 = "BALANCE DE ESTADO DE FLUJO DE EFECTIVOS";
      aAlfa1 = #0#29; |QBF aAlfa1;
      aAlfa1 = aAlfa1 + " " + eaNomICta;
      #0#29 = aAlfa1;
      ejer1   =  enEjercicio;
      ejer2   =  enEjercicio - 1;
      enejer1 = ejer1;
      enejer2 = ejer2;
      eaTitol = #0#29;

|SI nEsPdf = 0;
    informe = "cr8yf001"; |SI #0#13 = "S"; informe = "cr8fsc01"; |FINSI;
    |SI #8#4 = "S"; informe = "ca" + (informe % 306); |FINSI;
    |INFOR informe;
    |SI FSalida ! 0;
        |SI nSwMen = 0; |MENAV "     Error en informe."; |FINSI;
         |ACABA;
     |FINSI;
|FINSI;

      doh = "";
      sw      = 0;
      |BUCLE Leem805;
      |SI sw = 1;
          |PIEINF;
      |FINSI;

  |SI nEsPdf = 0;  |FININF;  |FINSI;

  |FINSI;
|FPRC;
|| ///////////////////////////////////////////////////////////////
|| ... // El Inventario de las Existencias
|PROCESO implineas;
 nHay    = 1;
 swlinea = 2;
 |PRINT;
|FPRC;

|PROCESO Imprimir_Aux;
  |SI nSwOp = 0;
      |SI #904#3 = "S";
          nImprimir = 1;
         |ERROR10;
      |FINSI;
      |ACABA;
  |FINSI;

  sw = 1;
  a_cuen  = #904#0;
  a_digi  = #904#1;
  a_desc  = #904#2;
  a_impo  = #904#4;
  |HAZ descripcion;
  |SI #904#3 = "S";
      nHay = 0;
      #12#0 = #904#0;
      #12#1 = #904#1;
      |LEE 000#12=;
      |SI FSalida = 0;
          |BUCLELIN 2implineas#12;
      |FINSI;
      swlinea = 1;
      |SI nHay = 1; swlinea = 3; |FINSI;
      total = total + #904#4;
  |SINO;
      a_impo = 0;
      swlinea = 1;
      |SI #904#1 = menor; swlinea = 0; |FINSI;
  |FINSI;
  |PRINT;
|FPRC;

|DEFBUCLE cainvaux2;
 #904#1;
 ;
 "            ", 0;
 "zzzzzzzzzzzz", 6;
 e;
 NULL, Imprimir_Aux;
|FIN;

|PROCESO descripcion;
    descri = "";
    |SI a_digi = 1;  nNume1 = dig4; aAlfa2 = ""; |FINSI;
    |SI a_digi = 2;  nNume1 = dig5; aAlfa2 = "  "; |FINSI;   || construye el hasta segun
    |SI a_digi = 3;  nNume1 = dig6; aAlfa2 = "    "; |FINSI;   ||/el nivel mas bajo elegido
    |SI a_digi = 4;  nNume1 = dig7; aAlfa2 = "      "; |FINSI;
    |SI a_digi = 5;  nNume1 = dig8; aAlfa2 = "        "; |FINSI;
    |SI a_digi = 6;  nNume1 = dig9; aAlfa2 = "          "; |FINSI;
    nNume1 = 100 + nNume1;
    descri = a_cuen % nNume1;
    descri = aAlfa2 + descri + "  ";
    descri = descri + a_desc;
|FPRC;

|PROCESO ElInventario;
    enInfTodo = 7;
    |SUB_EJECUTA "cainvaux;PDF";

    |HAZ nom_usu;
    aAlfa1 = "v" + nom_tmp;
    |NOME_DAT #904 aAlfa1;

    nSwOp = 0;
    nImprimir = 0;
    |BUCLE cainvaux2;
    |SI nImprimir = 1;
        #0#29 = "INVENTARIO DE EXISTENCIAS";

|SI nEsPdf = 0;
     informe = "cainvaux"; |SI #0#46 = "S"; informe = "cainvau2"; |FINSI;
     |INFOR informe;
     |SI FSalida ! 0;
         |SI nSwMen = 0; |MENAV "     Error en informe."; |FINSI;
         |ACABA;
     |FINSI;
|FINSI;

        nSwOp = 1;
        sw    = 0;
        |ABRE #12;
        |BUCLE cainvaux2;
        |SI sw = 1; |PIEINF; |FINSI;
        |CIERRA #12;
        |DELINDEX #904;

  |SI nEsPdf = 0;  |FININF;  |FINSI;

  |FINSI;

|FPRC;

|| ///////////////////////////////////////////////////////////////
|| *****************************************************************
|PROCESO HazTodo;

   |SI #0#17 = "S";  |HAZ conexi; |FINSI;
   |HAZ conexi8y9;
   #0#17 = "N";
   #0#31 = "N";

   nBalNum = #0#26;
   nCtaNum = #0#32;

   nCTodo0  = #0#0;
   nCTodo1  = #0#1;
   aCTodo2  = #0#2;
   aCTodo3  = #0#3;
   aCTodo4  = #0#4;
   nCTodo5  = #0#5;
   nCTodo6  = #0#6;
   nCTodo7  = #0#7;
   nCTodo8  = #0#8;
   aCTodo13 = #0#13;
   nCTodo14 = #0#14;
   nCTodo15 = #0#15;

   aCTodo18 = #0#18;
   nCTodo19 = #0#19;
   nCTodo20 = #0#20;
   nCTodo21 = #0#21;
   nCTodo22 = #0#22;
   nCTodo23 = #0#23;
   nCTodo24 = #0#24;
   nCTodo26 = #0#26;
   nCTodo30 = #0#30;
   nCTodo32 = #0#32;
   aCTodo34 = #0#34;
   aCTodo35 = #0#35;
   aCTodo36 = #0#36;
   aCTodo37 = #0#37;
   nCTodo42 = #0#42;
   aCTodo44 = #0#44;
   aCTodo45 = #0#45;
   aCTodo46 = #0#46;
   aCTodo47 = #0#47;
   aCTodo48 = #0#48;

   sw_pagina = 0;
   |ABRE #8;
   |DDEFECTO #8;
   #8#0 = 1;
   |LEE 000#8=;
   |CIERRA #8;

   |HAZ ImpSumasySaldos;
   nivel =  nivel_bal;
   |HAZ Situacion;
   |HAZ Resultados;
   |HAZ IngYGastos;
   |HAZ ECPN;
   |HAZ EFE;
   |SI #0#43 = "S";
       |HAZ ElInventario;
   |FINSI;
|| ---------------------------------------------------------------------

|FPRC;

|PROCESO impre; |TIPO 3;
  |SI #ca8ytodo#13 = "S";
     enSwComp = 1;
  |SINO;
     enSwComp = 0;
  |FINSI;

  |SI aparam ! "";
      |SI nSwLInv = 0;
          Impresora = eaNomImp + ";" + eaUnidadBTmp + "\InvCCAA.pdf";
          |IMPRE -1;
          |SI FSalida ! 0;  |ACABA;  |FINSI;
      |FINSI;
  |SINO;
||      Impresora = "CrystalReport";
||      |IMPRE -1;

      Impresora = "Crystal Reports";
      |IMPRE 0;

      |SI FSalida ! 0;  |ACABA;  |FINSI;
  |FINSI;

  nEsPdf = 0;
  |SI Impresora = "CrystalReport"; |O nSwLegalia = 1;
      nEsPdf = 1;
  |FINSI;

|SI nEsPdf = 1;
  informe = "cc8ytodo";
  |INFOR informe;
  |SI FSalida ! 0;
      |SI nSwMen = 0; |MENAV "     Error en informe."; |FINSI;
      |SI nSwLInv = 0; |FINIMP; |FINSI;
      |ACABA;
  |FINSI;
|FINSI;

  CodigoEmpresa    = enEmpresa;
  EjercicioEmpresa = enEjercicio;
  NombreEmpresa    = eaNombreEmp;

  |SI aparam = "";
      aMonedaA = eaMoneda;
      |DFICE dirfich0;
      |HAZ HazTodo;
  |SINO;
      aMonedaA = eaMoneda;
      |HAZ HazTodo;
   |FINSI;

|SI nEsPdf = 1;
   |FININF;
|FINSI;

   |SI nSwLInv = 0; |FINIMP; |FINSI;
|FPRC;

|PROGRAMA;

   nSwMen = 1;
   aparam = PARAMETRO; |QBF aparam;
   |SI aparam = ""; |O aparam = " N";
       |HAZ inicio;
       |HAZ eVarEmpresa;
       nSwMen = 0;
       aparam = "";
       |SI enEjercicio < 2008;
           |SI nSwMen = 0; |MENAV "    Informe para la Estructuras del 2008, no factible en este Ejercicio"; |FINSI;
          |ACABA;
       |FINSI;
   |FINSI;

   |HAZ DirAplicSt;

   anu = enEjercicio;

   |C_M #0#32, 1, "S";
   |ABRE #1;
   #1#0 = "8           ";
   |LEE 041#1];
   |SI FSalida ! 0;
       |C_M #0#31, 1, "N"; #0#31 = "N";
       |C_M #0#35, 1, "N"; #0#35 = "N";
       |C_M #0#37, 1, "N"; #0#37 = "N";
   |FINSI;
   |CIERRA #1;

   |SI aparam ! "";

       |HAZ Legalia;

   |SINO;
      |CLS;
      |CABEZA "Inventario y CCAA";

      |ABRE #0;
      #0#28 = "A";
      |LEE 041#0=;
      |SI FSalida ! 0;
          |DDEFECTO #0;
          #0#6 = 1;
          |SI eNum2008 = 1;  #0#26 = 1;  #0#32 = 1;  |FINSI;
          |SI eNum2008 = 20; #0#26 = 20; #0#32 = 20; |FINSI;
          |SI eNum2008 = 21; #0#26 = 21; #0#32 = 21; |FINSI;
          |GRABA 001#0c;
      |FINSI;
      #0#12 = ~;

      |PINPA #0#0;
      |PINDA #0#0;

      |ENDATOS #1#0;
      |SI FSalida = 0;
          #0#29 = "INVENTARIO Y CUENTAS ANUALES";
          |GRABA 001#0a;
      |FINSI;
      |CIERRA #0;
   |FINSI;
|FPRO;

|PROCESO Legalia;
    nSwLegalia = 1;
    nSwMen  = 1;

    Comodin = PARAMETRO;
    Comodin = Comodin - ",C";
    |SI FSalida ! 0;
        Compara = 1;
    |SINO;
        Compara = 0;
    |FINSI;
    Comodin = Comodin - ",L";
    |SI FSalida ! 0;
        nSwLInv = 1;
    |SINO;
        nSwLInv = 0;
    |FINSI;

    aAlfa = Comodin % -102;
    eaValeCero = "N";
    |SI aAlfa = ",S";  eaValeCero = "S"; |FINSI;

    Comodin = Comodin - ",";
    |SI FSalida ! 0;
        Calc = ((FSalida / 100) ! 0) + 99;
       Comodin1 = Comodin % 0101;
       Per1 = Comodin1; Comodin = Comodin - Comodin1;
       Per2 = Comodin;
    |FINSI;

    cod1 = eaLegEmp;
    cod2 = eaLegPer;
    emEmp = 1;  || Sin mensaje
    |HAZ leelo;
    |SI swerror ! 0;  |ACABA;   |FINSI;
    empre = eaLegEmp;
    ejerc = eaLegPer;

    |DBASE fich_alta; |QBF fich_alta;
    dirfich0 = fich_alta + "fich/" + eaLegEmp + eaLegPer + "/";

    |PATH_DAT #0  dirfich0;
    |PATH_DAT #1  dirfich0;
    |PATH_DAT #6  dirfich0;
    |PATH_DAT #8  dirfich0;
    |PATH_DAT #31 dirfich0;
    |PATH_DAT #33 dirfich0;
    |PATH_DAT #431 dirfich0;
    |PATH_DAT #432 dirfich0;

    |ABRE #8;
    |DDEFECTO #8;
    #8#0 = 1;
    |LEE 000#8=;
    |CIERRA #8;

    |ABRE #0;
    |DDEFECTO #0;
    #0#28 = "A";
    |LEE 041#0=;
    |SI FSalida ! 0;
        #0#6 = 1;
        |SI eNum2008 =  1; #0#26 =  1; |FINSI;
        |SI eNum2008 = 20; #0#26 = 20; |FINSI;
        |SI eNum2008 = 21; #0#26 = 21; |FINSI;
        |GRABA 001#0n;
    |FINSI;
    |CIERRA #0;

    Campo = 26; |HAZ descri; |SI nSwErrorBal = 1; |ACABA; |FINSI;
    Campo = 32; |HAZ descri; |SI nSwErrorBal = 1; |ACABA; |FINSI;

    |SI Compara = 1;
         #0#13 = "S";
         #0#14 = eaLegEmp;
         |SI eaLegPer > 0;
             #0#15 = eaLegPer - 1;
         |SINO;
             #0#15 = 9;
         |FINSI;
         |HAZ esbuena1;
         |SI swNoExiste = 1;
             Compara = 0;
             #0#13 = "N";
         |FINSI;
     |SINO;
          #0#13 = "N";
     |FINSI;

     #0#0  = Per1; #0#7 = Per1;
     #0#0  = 0;    #0#7 = 0;
     #0#1  = Per2; #0#8 = Per2;
     Campo = 0; |HAZ pintames;
     Campo = 1; |HAZ pintames;
     Campo = 7; |HAZ pintames;
     Campo = 8; |HAZ pintames;
     #0#4   = "S";
     |SI dig4 [ 4; |Y dig4 ! 0; #0#5   = 1; |FINSI;
     |SI dig5 [ 4; |Y dig5 ! 0; #0#5   = 2; |FINSI;
     |SI dig6 [ 4; |Y dig6 ! 0; #0#5   = 3; |FINSI;
     |SI dig7 [ 4; |Y dig7 ! 0; #0#5   = 4; |FINSI;
     |SI dig8 [ 4; |Y dig8 ! 0; #0#5   = 5; |FINSI;
     |SI dig9 [ 4; |Y dig9 ! 0; #0#5   = 6; |FINSI;

     #0#6   = 00;
     #0#17  = "N";
     #0#12  = efFecLeg;
     #0#18  = eaDatLeg;
     #0#31  = "N";
     aMonedaA = eaMoneda;
     |SI #0#13 = "N"; aMonedaC = aMonedaA; |FINSI;
     #0#35 = "N";
     #0#37 = "N";
     #0#34 = "S";
     #0#36 = "N";   ||borrar no

     #0#38 = "            ";
     #0#39 = "zzzzzzzzzzzz";
     #0#42 = #30#13;
     |SI eaUltNiv = "N"; #0#42 = #30#13 - 1; |FINSI;
     nivel = #0#42;
     |SI #0#42 ] 1; #0#19 = 1; |FINSI;
     |SI #0#42 ] 2; #0#20 = 2; |FINSI;
     |SI #0#42 ] 3; #0#21 = 3; |FINSI;
     |SI #0#42 ] 4; #0#22 = 4; |FINSI;
     |SI #0#42 ] 5; #0#23 = 5; |FINSI;
     |SI #0#42 ] 6; #0#24 = 6; |FINSI;
     #0#43 = "S";
     #0#41 = eaValeCero;

     |HAZ impre;

     |DBASE Comodin1; |QBF Comodin1;
     Comodin1 = Comodin1 + "PresLibr/" + eaLegEmp + eaLegPer + "/";
     Comodin1 = Comodin1 + "INV_CUEN_*.pdf";
     Numeracion = 1;
     |_PDIR Comodin1;
     |PARA; |SI FSalida = 0; |HACIENDO;
            Comodin = Comodin1 % -107; Comodin = Comodin % 0103;
            Numeracion = Comodin; Numeracion = Numeracion + 1;
            |_SDIR Comodin1;
     |SIGUE;

     Comodin = eaUnidadBTmp + "\InvCCAA.pdf";
     |XCIERRA Handle;
     |DBASE Comodin1; |QBF Comodin1;
     Comodin1 = Comodin1 + "PresLibr/";
     |MKDIR Comodin1;
     Comodin1 = Comodin1 + eaLegEmp + eaLegPer + "/";
     |MKDIR Comodin1;
     Comodin1 = Comodin1 + "INV_CUEN_" + (("000" + Numeracion) % -103) + ".pdf";
     aAlfa = "";
     |IP_REMOTO aAlfa; |QBF aAlfa;
     |SI aAlfa ! "";
         |RECIBE_FICHERO Comodin, Comodin1;
     |SINO;
         |COPIA_FICHERO Comodin, Comodin1;
     |FINSI;
|FPRC;
