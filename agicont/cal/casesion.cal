|FICHEROS;
  cacreano #0;
  casesion #100;
  caseslin #110,MANTE;
  espejoc@ #900;
  espejol@ #910;

  cacuenta #1;
  imagen@  #101;
|FIN;

|VARIABLES;
 aD1 = "";
 aDD = "";
 aH1 = "";
 aHD = "";
 nAntSes = 0;
 aAntDep = "";
 aAntTod = "";

 nTecla = 0;
 nPara1 = 0;
 nPara2 = 0;
 aBase  = "";
 aDef   = "";
 fich   = "";
 alfa1  = "";
 alfa2  = "";
 temporal1 = "";
 temporal2 = "";
 nSw    = 0;
 &eaFicbal = "";
 aFicbueno = "";
|FIN;

|INCLUYE acceso_i;
|INCLUYE vardep_i;

|| *** Borrar primero la Sesion
|PROCESO Quitofich;
  |DFICE fich;
  |SI alfa1 ! "";
      temporal2 = fich + alfa1 + ".ixx";
      |FBORRA temporal2;
      temporal2 = fich + alfa1 + ".dat";
      |FBORRA temporal2;
      temporal2 = fich + alfa1 + ".ddx";
      |FBORRA temporal2;
  |FINSI;
|FPRC;

|PROCESO BorroSesLin;
  alfa1 = #110#6; |QBF alfa1;
  |HAZ Quitofich;
  |BORRA 020#110a;
  |LIBERA #110;
|FPRC;

|DEFBUCLE BorroSesLin;
 #110#1;
 ;
 #100#0, "  ";
 #100#0, "zz";
 be;
 NULL, BorroSesLin;
|FIN;

|PROCESO BorroSesion;
 alfa1 = #100#32; |QBF alfa1;
 |HAZ Quitofich;
 |BUCLE BorroSesLin;
 |BORRA 020#100a;
 |LIBERA #100;

 |ABRE #0;
 |LEE 101#0p;
 |SI FSalida ! 0;
     |CIERRA #0;
     |ACABA;
 |FINSI;
 |SI #0#26 = #100#0;
     |DDEFECTO #0;
     |GRABA 020#0a;
 |FINSI;
 |LIBERA #0;
 |CIERRA #0;
|FPRC;

|PROCESO DesactLin;
  #110#5 = alfa1;
  |GRABA 020#110a;
  |LIBERA #110;
|FPRC;

|DEFBUCLE DesactLin;
 #110#1;
 ;
 nAntSes, aD1;
 nAntSes, aH1;
 be;
 NULL, DesactLin;
|FIN;

|PROCESO Desactualiza;
 |ABRE #900;
 #900#0 = nAntSes;
 |LEE 101#900=;
 |SI FSalida = 0;
     alfa1 = "N";
     |SI nAntSes ! #100#0;
         #900#5 = "N";
         |GRABA 020#900a;
         aD1 = ""; aH1 = "zz";
         |BUCLE DesactLin;
     |SINO;
         |SI aAntTod = "S";
             aD1 = "  "; aH1 = "zz";
         |SINO;
             aD1 = aAntDep; aH1 = aAntDep;
         |FINSI;
         |BUCLE DesactLin;
     |FINSI;
 |FINSI;

 |LIBERA #900;
 |CIERRA #900;
|FPRC;

|PROCESO LeoActividad; |TIPO 0;
 |C_M #100#1, 0, alfa1;
 |SI alfa1 = "N"; |ACABA; |FINSI;

 |ABRE #110;
 #110#0 = #100#0;
 #110#1 = #100#1;
 |LEE 001#110=;
 |SI FSalida ! 0;
     |MENAV "    Error. No existe Departamentos en esta Sesion";
     |ERROR;
 |FINSI;
 |CIERRA #110;
|FPRC;

|PROCESO ActivarSesion;

  |SI #100#31 = "S"; |Y eaDepar = "S";
      |C_V #100#1, 1, "S";  |C_M #100#1, 1, "S";
      |C_V #100#33, 1, "S"; |C_M #100#33, 1, "S";
      |PUSHV 0540 0857;
      |PINTA 0641, "                 ";
      |PINTA 0741, "                 ";
      |ATRI R; |PINTA 0641, "ACTIVAR TODOS:  "; |ATRI 0;
      |ATRI R; |PINTA 0741, "DEPARTAMENTO:   "; |ATRI 0;
      |CUADRO 0540 0857;
      |ENCAMPO #33#100;
      |SI #100#33 = "N";
          |ENCAMPO #1#100;
      |FINSI;
      |C_V #100#1, 1, "N"; |C_M #100#1, 1, "N";
      |C_V #100#33, 1, "N"; |C_M #100#33, 1, "N";
      |POPV;
  |FINSI;

  |ABRE #0;
  |LEE 101#0p;
  |SI FSalida ! 0;
      |DDEFECTO #0;
      |GRABA 020#0b;
  |FINSI;
  nAntSes = #0#26;
  aAntDep = #0#28;
  aAntTod = #0#34;

  #0#25 = "S";
  #0#26 = #100#0;
  #0#27 = #100#31;
  #0#28 = #100#1;
  #0#29 = #100#4;
  #0#30 = #100#2;
  #0#31 = #100#3;
  |PARA nPara1 = 0; |SI nPara1 [ 24; |HACIENDO nPara1 = nPara1 + 1;
        nPara2 = nPara1 + 6;
        #0nPara1 = #100nPara2;
  |SIGUE;
  |PARA nPara1 = 34; |SI nPara1 [ 45; |HACIENDO nPara1 = nPara1 + 1;
        nPara2 = nPara1 - 1;
        #0nPara1 = #100nPara2;
  |SIGUE;
  |GRABA 020#0a;
  |LIBERA #0;
  |CIERRA #0;

  |SI nAntSes ! 0;
      |HAZ Desactualiza;
  |FINSI;

  aD1 = #100#1; aH1 = #100#1;
  |SI #100#33 = "S";
      aD1 = "  "; aH1 = "zz";
  |FINSI;
  nAntSes = #100#0; nAntSes = #100#0;
  alfa1 = "S"; |BUCLE DesactLin;

  #100#5 = "S";
  |GRABA 020#100a;
  |LIBERA #100;
  |HAZ Tipo7_C;
|FPRC;

|PROCESO NuevaSesion;

  |PUSHV 0601 2380;
  eaCodDep = #100#0;
  |SUB_EJECUTA_NP "casescop;desde";
  |POPV;
  |ERROR;
  |PTEC 816;
|FPRC;


|PROCESO Tipo11_C; |TIPO 11;
 nTecla = FSalida;
 |SI nTecla = 10; |Y eaDepar = "S";
     |ENTLINEAL #1#110, -2, 4, 21, 0, Alta110, Baja110;
 |FINSI;
 |SI nTecla = 11;
     |PUSHV 05012380;
     |PINPA #1#100;
     |PARA nPara1 = 6; |SI nPara1 [ 31; |HACIENDO nPara1 = nPara1 + 1;
           |PINTA #100nPara1;
     |SIGUE;
     |PARA nPara1 = 33; |SI nPara1 [ 44; |HACIENDO nPara1 = nPara1 + 1;
           |PINTA #100nPara1;
     |SIGUE;
     |PAUSA;
     |POPV;
 |FINSI;
 |SI nTecla = 12;
     |HAZ ActivarSesion;
 |FINSI;
 |SI nTecla = 13;
     |CONFI "    NDar de Baja la Sesion. Continuar";
     |SI FSalida = 0;
         |HAZ BorroSesion;
         |PONCONTROL 23, "SI";
     |FINSI;
 |FINSI;
 |SI nTecla = 14; |Y eaDepar = "S"; |Y #100#31 = "S";
     |HAZ NuevaSesion;
 |FINSI;
|FPRC;

|PROCESO Alta110;
 #110#0 = #100#0;
 #110#1 = "  ";
|FPRC;

|PROCESO Baja110;
 #110#0 = #100#0;
 #110#1 = "zz";
|FPRC;

|PROCESO Tipo7_C; |TIPO 7;
 |SI eaDepar = "S";
     |SI #100#31 = "S";
         |MENSA "    F2-Departamentos, F3-Consulta Seleccion, F4-Activar, F5-Borrar, F6-Crear Nueva";
     |SINO;
         |MENSA "    F2-Departamentos, F3-Consulta Seleccion, F4-Activar, F5-Borrar";
     |FINSI;
     |ENTLINEAL #1#110, -2, 7, 21, 0, Alta110, Baja110;
 |SINO;
     |MENSA "    F3-Consulta Seleccion, F4-Activar, F5-Borrar";
 |FINSI;
|FPRC;

|PROCESO Alta100;
 #100#0 = 01;
|FPRC;

|PROCESO Baja100;
 #100#0 = 99;
|FPRC;

|PROGRAMA;

  |CLS;
  |CABEZA "Relacion de Sesion Balances";

  |HAZ inicio;

  |DBASE aBase;
  aDef = aBase + "def/casesion";
  |CARGA_DEF aDef, espejoc@;
  |SI FSalida ! 0;
      |MENAV "    Error en Sesion";
      |ACABA;
  |FINSI;
  aDef = aBase + "def/caseslin";
  |CARGA_DEF aDef, espejol@;
  |SI FSalida ! 0;
      |MENAV "    Error en Lineas de Sesion";
      |DESCARGA_DEF #espejoc@;
      |ACABA;
  |FINSI;

  |SI eaDepar = "N";
      |PINPA #0#100;
  |SINO;
      |C_P #100#0, 1, 0904;
      |C_P #100#2, 1, 0908;
      |C_P #100#3, 1, 0919;
      |C_P #100#4, 1, 0709; |C_L #100#4, 1, "N";
      |C_V #100#5, 1, "N";
      |PINPA #0#110;
 |FINSI;
 |ENTLINEAL #1#100, -1, 5, 22, 0, Alta100, Baja100;

 |DESCARGA_DEF #espejol@;
 |DESCARGA_DEF #espejoc@;
|FPRO;

|PROCESO Tipo5_C;  |TIPO 5;
       |LEE 040#100p;
       #100#5 = "N";
       |LINEAS_BI_ATRI #100#5, D, S, -1;
|FPRC;

|PROCESO Tipo5_L;  |TIPO 5;
       |LEE 040#110p;
       #110#5 = "N";
       |LINEAS_BI_ATRI #110#5, D, S, -1;
|FPRC;
