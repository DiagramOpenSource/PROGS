|FICHEROS;
  c3gediar #0;
  camaasic #1;    || Apuntes cabs.
  camaasil #2;    || Apuntes lins.
  cacuenta #4;    || Plan Contable.
  caconimp #8;
  ca8m0004 #404;
  canempre #30;   || Empresas contable.
|FIN;

|VARIABLES;
  empcon = 0;
  ejecon = 0;
  leidos = 0;
  dirfich0 = "";
  alfa2 = "";
  emp1 = 0;
  per1 = 0;
  xx = 0;
  yy = 0;
  anu = 0;
  desde = 0;
  hasta = 0;
  x = 0;
  y = 0;
  varcamp = 0;
  any = 0;
  &r_emp = 0;
  &r_per = 0;
  &entrada = 1;
  sw = 0;
  fecha = 0;
  fecalf = "";
  mensa1 = "    ERROR ESTA FECHA NO CORRESPONDE AL EJERCICIO";
  traba1 = "";
  varalf = "";
  &swlin = 0;
  &descuadre = 0;
  &a_debe = 0;
  &a_haber = 0;
  &tde = 0;
  &tha = 0;
  &pagina = 0;
  d = "D";
  h = "H";
  informe = "";
  alfa1 = "";
  sw_pagina = 0;
  swalgun = 0;
  men9 = "     Empresa Contable INACTIVA ";
  Primero = 0;

  nDiaAper     = 0;
  nIgual       = 0;
  nConcepto    = 0;
  aDescripcion = "";
  aSubdep      = "";
  nOpe         = 0;
  nLinea       = 0;
  nDia8 = 0;
  fFec8 = @;
  nDoc8 = 0;

  nCa21 = 0;
  nCa23 = 0;
  nCampo = 0;
  aCampo21 = "";
  aCampo23 = "";

  nSwAlgun = 0;
|FIN;

|INCLUYE acceso_i;

|| *************************************************************************
||                        CALCULOS DE PANTALLA
|| *************************************************************************

|PROCESO fechasb; |TIPO 7;
   #0#0 = fec1;
   #0#1 = fec2;
   |PINTA #0#0;
   |PINTA #0#1;
|FPRC;

|PROCESO &c_pagina;
|SI sw_pagina = 0 ;
        pagina = 1;
        sw_pagina = 1;
|SINO;
        pagina = pagina + 1;
|FINSI;
|FPRC;

|PROCESO consulta;
  |SI FSalida ! 9;  |ACABA;  |FINSI;
  |SUB_EJECUTA "caconemp.run";
  |ERROR;
  |SI Campo = 15;
      #0#15 = r_emp;
      #0#17 = r_per;
      |PTEC 802;
      |PTEC 802;
  |FINSI;
  |SI Campo = 16;
      #0#16 = r_emp;
      #0#18 = r_per;
      |PTEC 802;
  |FINSI;
  |SI Campo ] 19;
      #0Campo = r_emp;
      varcamp = Campo + 20;
      #0varcamp = r_per;
      |PTEC 802;
      |PTEC 802;
  |FINSI;
|FPRC;

|PROCESO mayor;
  alfa1 = #0Campo > " ";
  #0Campo = alfa1;
  |PINTA #0Campo;
|FPRC;

|PROCESO AsAper_d; |TIPO 0;
 |C_M #0Campo, 0, alfa1;
 |SI alfa1 = "N"; |ACABA; |FINSI;
 alfa1 = #0Campo; nCampo = Campo + 2;
 |C_M #0nCampo, 1, alfa1;
|FPRC;

|PROCESO mayor1;
  |HAZ mayor;
  |SI #0Campo = "N";
      |PARA x = 19;  |SI x [ 58;  |HACIENDO x = x + 1;
            |CAMPO_MODIFICA #0x,1,"S";
      |SIGUE;
      #0#15 = 0;
      #0#17 = 0;
      #0#16 = 0;
      #0#18 = 0;
      |CAMPO_MODIFICA #0#15,1,"N";
      |CAMPO_MODIFICA #0#17,1,"N";
      |CAMPO_MODIFICA #0#16,1,"N";
      |CAMPO_MODIFICA #0#18,1,"N";
      |PINTA #0#15;
      |PINTA #0#17;
      |PINTA #0#16;
      |PINTA #0#18;
  |SINO;
      |PARA x = 19;  |SI x [ 58;  |HACIENDO x = x + 1;
            #0x = 0;
            |CAMPO_MODIFICA #0x,1,"N";
      |SIGUE;
      |CAMPO_MODIFICA #0#15,1,"S";
      |CAMPO_MODIFICA #0#17,1,"S";
      |CAMPO_MODIFICA #0#16,1,"S";
      |CAMPO_MODIFICA #0#18,1,"S";
  |FINSI;
|FPRC;

|PROCESO fechad;
  |SI #0#0 < fec1; |O #0#0 > fec2;
      |MENAV mensa1;
      |ERROR;
      |ACABA;
  |FINSI;
|FPRC;

|PROCESO fechah;
  |SI #0#1 < fec1; |O #0#1 > fec2; |O #0#1 < #0#0;
      |MENAV mensa1;
      |ERROR;
      |ACABA;
  |FINSI;
|FPRC;

|PROCESO desasien;
  |SI #0#13 < #0#12;
      |MENAV "     Error en Introduccion";
      |ERROR;
  |FINSI;
|FPRC;

|PROCESO desdiari;
  |SI #0#7 < #0#6;
      |MENAV "     Error en Introduccion";
      |ERROR;
  |FINSI;
|FPRC;

|| --------------- comprueba desde-hasta empresas ---------------------------

|PROCESO buscaem1;
  |SI #30#21 = "I"; |ACABA; |FINSI;
  |SI #30#26 ! any; |ACABA; |FINSI;
  leidos = 1;
|FPRC;

|DEFBUCLE buscaempre;
#30#1;
;
#0#15,desde;
#0#16,hasta;
e;
NULL,buscaem1;
|FIN;


|PROCESO buclempre;
  |SI #0#17 > #0#18;
      |MENAV "     ERROR EN INTRODUCCION";
      |ERROR;
      |ACABA;
  |FINSI;
  |SI #0#15 = 0;  |Y #0#16 = 0;  |ACABA;  |FINSI;
  desde = #0#17;
  hasta = #0#18;
  leidos = 0;
  |BUCLE buscaempre;
  |SI leidos = 0;
      |MENAV "    ATENCION SELECCION VACIA";
      |ERROR;
      |ACABA;
  |FINSI;
|FPRC;

|PROCESO deshas;
  |SI #0#16 < #0#15;
      |MENAV "      Error en Introduccion";
      |ERROR;
  |FINSI;
|FPRC;

|| ------------------------------------------------------------------------


|PROCESO nomodi;
      |PARA x = Campo + 1;  |SI x [ 38;  |HACIENDO x = x + 1;
            #0x = 0;
            |CAMPO_MODIFICA #0x,1,"N";
            y = x + 20;
            #0y = 0;
            |CAMPO_MODIFICA #0y,1,"N";
      |SIGUE;
      y = Campo + 20;
      #0y = 0;
      |CAMPO_MODIFICA #0y,1,"N";
      |PINTA #0y;
|FPRC;


|PROCESO esbuena; || desde campo de empresas seleccion
  |SI FSalida = 2; |ACABA; |FINSI;
  |SI #0Campo = 0; |HAZ nomodi;  |ACABA;  |FINSI;
  |ABRE #30;
  #30#2 = #0Campo;
  #30#3 = 0;
  |LEE 011#30];
  |SI FSalida ! 0;  |O #30#2 ! #0Campo;
      |MENAV "     NO Existe la empresa Contable ";
      |ERROR;
  |FINSI;
  |CIERRA #30;
|FPRC;

|PROCESO esbuena1;
  |SI FSalida = 2;|ACABA;|FINSI;
  x = Campo - 20;
  |SI #0x = 0;  |ACABA;  |FINSI;
  |ABRE #30;
  x = Campo - 20;
  #30#2 = #0x;
  #30#3 = #0Campo;
  |LEE 011#30=;
  |SI FSalida ! 0;
      |MENAV "     NO Existe Periodo en la empresa Contable";
      |CIERRA #30;
      |ERROR;
      |ACABA;
  |FINSI;
  |CIERRA #30;
  |SI #30#21 = "I";
      |MENAV men9;
      |ERROR;
      |ACABA;
  |FINSI;
  leidos = 0;
  |HAZ buscaem1;
  |SI leidos = 0;
      |MENAV "     NO Corresponde el a¤o contable";
      |ERROR;
      |ACABA;
  |FINSI;
  x = Campo + 20;
  #0x = #30#1;
  |PINTA #0x;
  x = Campo - 20;
  alfa1 = #0x;
  alfa2 = #0Campo;
|FPRC;

|| **********************************************************************
||                       CALCULOS DE IMPRESION
|| **********************************************************************


|| ---------- Seleccion DESDE / HASTA Empresa.

|PROCESO esdesd1;
  |DBASE dir_fich;
  dirfich0 = dir_fich + "fich/";
  alfa1 = #30#2;
  alfa2 = #30#3;
  emp1 = #30#2;
  per1 = #30#3;
  alfa1 = "00000" + alfa1;   alfa1 = alfa1 % -105;
  alfa2 = "0"   + alfa2;   alfa2 = alfa2 % -101;
  dirfich0 = dirfich0 + alfa1 + alfa2 + "/";

  |SI #30#26 = any; |Y #30#21 ! "I";
      |HAZ haztodo;
  |FINSI;
|FPRC;

|DEFBUCLE esdesde;
#30#1;
;
#0#15,#0#17;
#0#16,#0#18;
e;
NULL,esdesd1;
|FIN;

|| ---------- Seleccion en parrilla.

|PROCESO esunaauna;
      |PARA xx = 19;  |SI xx [ 38;  |HACIENDO xx = xx + 1;
            |SI #0xx = 0;  |SAL;  |FINSI;
            |DBASE dir_fich;
            dirfich0 = dir_fich + "fich/";
            yy = xx + 20;
            emp1 = #0xx;
            per1 = #0yy;
            alfa1 = #0xx;
            alfa2 = #0yy;
            alfa1 = "00000" + alfa1;   alfa1 = alfa1 % -105;
            alfa2 = "0"   + alfa2;   alfa2 = alfa2 % -101;
            dirfich0 = dirfich0 + alfa1 + alfa2 + "/";
              || lee empresa
                 |ABRE #30;
                   #30#2 = emp1;
                   #30#3 = per1;
                 |LEE 011#30=;
                 |CIERRA #30;
                 |SI #30#26 = any; |Y #30#21 ! "I";
                     |HAZ haztodo;
                 |FINSI;
      |SIGUE;
|FPRC;

|| *********************************************************************
||                    COMIENZO con TIPO 3
|| *********************************************************************

|PROCESO impre; |TIPO 3;
   sw = 0;
   |IMPRE 0;
   |SI FSalida ! 0; |ACABA; |FINSI;
   |HAZ los_informes;
        informe = que_i1;
        |INFOR informe;                || Abrimos el informe
        |SI FSalida ! 0; |FINIMP; |ACABA; |FINSI;

  |SI #0#14 = "S";
      |BUCLE esdesde;
  |SINO;
      |HAZ esunaauna;
  |FINSI;

  |FININF;
  |FINIMP;
|FPRC;


|| *************************************************************************
||    - Calculos propios del Diario Oficial ---------
|| *************************************************************************
|PROCESO VerAper08_d;
  nIgual = 0;
  |ABRE #404;
  |LEE 001#404p;
  |SI FSalida = 0;
      |SI #404#10 = #1#0; |Y #404#11 = #1#1; |Y #404#12 = #1#2;
          nIgual = 1;
          nDia8 = #404#10;
          fFec8 = #404#11;
          nDoc8 = #404#12;
      |FINSI;
  |FINSI;
  |CIERRA #404;
|FPRC;

|PROCESO AImprimir;
   |SI nOpe = 3; |Y #camaasil#8 ! "D";
       nLinea = nLinea - 1;
       |ACABA;    || No lo imprime
   |FINSI;
   |SI nOpe = 4; |Y #camaasil#8 ! "H";
       nLinea = nLinea - 1;
       |ACABA;    || No lo imprime
   |FINSI;

  |SI Primero = 0;
     |SI descuadre = 0;
         swlin = 1;
         |PRINT;
         swalgun = 1;
     |SINO;
         swlin = 2;
         |SI descuadre < 0; descuadre = descuadre * (-1); |FINSI;
         |PRINT;
         swalgun = 1;
     |FINSI;
     Primero = 1;
  |FINSI;

   swlin = 0;
   |SI #camaasil#8 = "D";
       a_debe = #camaasil#9;
       a_haber = 0;
   |SINO;
       a_debe = 0;
       a_haber = #camaasil#9;
   |FINSI;

   |PRINT; swalgun = 1;
|FPRC;

|PROCESO Leo8m004;
   |SI nOpe = 2;      || Sumar solo ls diferencias
       |SI #404#1 ! #404#8;
           tde = tde + (#404#4 + #404#5) ! EURODB;
           tha = tha + (#404#4 + #404#5) ! EURODB;
       |FINSI;
       |ACABA;
   |FINSI;

   |SI #404#13 < #0#14;  |O #404#13 > #0#15;  |ACABA;  |FINSI;

   #2#0 = #404#10;
   #2#1 = #404#11;
   #2#2 = #404#12;
   #2#21 = #404#13;
   #2#24 = "";
   |SI nOpe = 0;      ||primer asiento
       #2#3 = #404#0;  || La Linea
       #2#4 = #404#1;  || Cuenta Anterior
       #2#5 = #404#7;
       #2#6 = nConcepto;
       #2#7 = aDescripcion;
       #2#8 = #404#3;
       #2#9 = #404#4 + #404#5;
       #4#2 = #404#2;
       |HAZ AImprimir;
       |SI #404#1 ! #404#8;
           #1#4 = #1#4 + #2#9;
           #1#5 = #1#5 + #2#9;
       |FINSI;
   |FINSI;

   |SI nOpe = 1; |O nOpe = 3; |O nOpe = 4;
                                            ||primer asiento
       |SI #404#1 = #404#8; |ACABA; |FINSI; ||no ya cambio de cuenta

       nLinea = nLinea + 1;
       #2#3 = nLinea;  || La Linea
       #2#4 = #404#1;  || Cuenta Anterior
       #2#5 = #404#7;
       #2#6 = #0#80;
       #2#7 = #0#81;
       |SI #404#3 = "D";
           #2#8 = "H";
       |SINO;
           #2#8 = "D";
       |FINSI;
       #2#9 = #404#4 + #404#5;
       #4#2 = #404#2;
       |HAZ AImprimir;

       nLinea = nLinea + 1;
       #2#3 = nLinea;  || La Linea
       #2#4 = #404#8;  || Cuenta Anterior
       #2#5 = #404#9;
       #2#6 = #0#80;
       #2#7 = #0#81;
       #2#8 = #404#3;
       #2#9 = #404#4 + #404#5;
       #4#0 = #2#4;
       #4#1 = #2#5;
       |LEE 040#4=;
       |SI FSalida ! 0; |DDEFECTO #4; |FINSI;
       |HAZ AImprimir;
   |FINSI;
|FPRC;

|DEFBUCLE Leo8m004;
 #404#1;
 ;
 nDia8, fFec8, nDoc8, 0001;
 nDia8, fFec8, nDoc8, 9999;
 ;
 NULL, Leo8m004;
|FIN;

|DEFBUCLE Leo8m004_2;
 #404#4;
 ;
 nDia8, fFec8, nDoc8, " ", 0001;
 nDia8, fFec8, nDoc8, "z", 9999;
 ;
 NULL, Leo8m004;
|FIN;

|PROCESO Apertura2008;

   |SALVA_DATOS #1;
   #1#3 = #1#3 - 1; |SI #1#3 [ 0; #1#3 = 1; |FINSI;
   descuadre = (#1#4 - #1#5) ! EURODB;
   swlin = 1;
   |SI descuadre ! 0;
      swlin = 2;
      |SI descuadre < 0; descuadre = descuadre * (-1); |FINSI;
   |FINSI;
   |PRINT; nSwAlgun = 1;

   Primero = 0;
   #1#4 = 0; #1#5 = 0; #1#6 = 0;
   nOpe = 0;
   |SI #0#3 = "S";
       |BUCLE Leo8m004_2;
   |SINO;
       |BUCLE Leo8m004;
   |FINSI;

   #1#3 = #1#3 + 1;
   descuadre = (#1#4 - #1#5) ! EURODB;
   swlin = 1;
   |SI descuadre ! 0;
      swlin = 2;
      |SI descuadre < 0; descuadre = descuadre * (-1); |FINSI;
   |FINSI;
   |PRINT; nSwAlgun = 1;

   nLinea = 0;
   nOpe   = 1;
   Primero = 0;
   |SI #0#3 = "S";
       nOpe = 3; |BUCLE Leo8m004_2;  ||Debe
       nOpe = 4; |BUCLE Leo8m004_2;  ||Haber
   |SINO;
       |BUCLE Leo8m004;
   |FINSI;

   |REPON_DATOS #1;
|FPRC;

|PROCESO porimpre;
  #4#0 = #2#4;
  #4#1 = #2#5;
  |LEE 040#4=;
  |SI FSalida ! 0;
      |DDEFECTO #4;
  |FINSI;

  |SI Primero = 0;
     |SI descuadre = 0;
         swlin = 1;
         |PRINT;
         swalgun = 1;
     |SINO;
         swlin = 2;
         |SI descuadre < 0; descuadre = descuadre * (-1); |FINSI;
         |PRINT;
         swalgun = 1;
     |FINSI;
     Primero = 1;
  |FINSI;

  swlin = 0;
  |SI #2#8 = "D";
      a_debe = #2#9;
      a_haber = 0;
  |SINO;
      a_debe = 0;
      a_haber = #2#9;
  |FINSI;

  |PRINT;
  swalgun = 1;
|FPRC;

|DEFBUCLE cadiario2;
#2#2;
3;
#1#0, #1#1, #1#2, d, 0001;
#1#0, #1#1, #1#2, h, 9999;
be;
NULL,porimpre;
|FIN;

|PROCESO bucleimp;
  |SI #0#2 = "S";
      |SI sw = 0; fecha = #1#1; sw = 1; |FINSI;
      |SI fecha ! #1#1; |PIEINF; fecha = #1#1; |FINSI;
  |FINSI;

  nConcepto    = 0;
  aDescripcion = "";
  |SI #0nCa21 = "S";
      |SI #1#0 = 0; |Y nDiaAper = 0;
           |HAZ VerAper08_d;
           |SI nIgual = 1;
               |HAZ Apertura2008;
               nDiaAper = 1;
               |ACABA;           ||este asiento se trata de forma diferente
           |FINSI;
        |FINSI;
  |FINSI;

  descuadre = #1#4 - #1#5;
  Primero = 0;

  |SI #0#3 = "S";
      |BUCLE cadiario2;
  |SINO;
      |BUCLELIN 0porimpre#1;
  |FINSI;
|FPRC;

|DEFBUCLE cadiario1;
#1#2;
;
#0#12, #0#0, #0#6, 000001;
#0#13, #0#1, #0#7, 999999;
be;
NULL,NULL,NULL,NULL,NULL,bucleimp;
#1#1,#1#3,#1#0,#1#2;
|FIN;

|PROCESO contar;
  |SI #1#1 < #0#0;
      tde = tde + #1#4;
      tha = tha + #1#5;
       |SI #0nCa21= "S";
           |SI #1#0 = 0; |Y nDiaAper = 0;
               |HAZ VerAper08_d;
               |SI nIgual = 1;
                   nOpe = 2;|BUCLE Leo8m004;   || Solo Suma el ficticio
                   nDiaAper = 1;
               |FINSI;
           |FINSI;
       |FINSI;
  |FINSI;
|FPRC;


|DEFBUCLE cadiario0;
 #1#2;
 ;
      1, fec1, #0#6, 000001;
 999999, #0#0, #0#7, 999999;
 e;
 NULL,contar;
|FIN;

|PROCESO haztodo;
  enEmpresa = #canempre#2;
  enPeriodo = #canempre#3;
  swOperacion = 0
  |SUB_EJECUTA "camoneda.tab";

  |HAZ borrar;
  |PATH_DAT #1 dirfich0;
  |PATH_DAT #2 dirfich0;
  |PATH_DAT #4 dirfich0;
  |PATH_DAT #8 dirfich0;
  |PATH_DAT #404 dirfich0;

  anu = 1900 + #30#26;
  |SI anu [ 1970;  anu = anu + 100;  |FINSI;
  empcon = #30#1;
  ejecon = #30#26;
  sw = 0; swalgun = 0;

  nDiaAper = 0;
  |ABRE #4;
      |BUCLE cadiario0; || calcula el saldo acumulado.
      |BUCLE cadiario1;
  |CIERRA #4;
  |SI swalgun = 1; |PIEINF; |FINSI;
|FPRC;

|PROCESO borrar;
   swlin     = 0;
   descuadre = 0;
   a_debe    = 0;
   a_haber   = 0;
   tde       = 0;
   tha       = 0;
   pagina    = 0;
   sw_pagina = 0;
   swalgun   = 0;
   nDiaAper  = 0;
|FPRC;

|PROGRAMA;

   |HAZ inicio;
   |HAZ eVarEmpresa;

   any = #30#26
   |DBASE dir_fich;
   dir_fich = dir_fich + "fich/";

   nCa21 = 79;
   nCa23 = 81;
   |ABRE #8;
   |DDEFECTO #8;
   #8#0 = 1;
   |LEE 001#8=;
   |CIERRA #8;

   |CLS;
   |CABEZA "Diario Oficial Gral";
   |PINPA #0#0;
||   |SI enEjercicio ! 2008;
       #0#80 = "N";
       |C_V #0#79,1,"N"; |C_M #0#79,1,"N";
       |C_V #0#81,1,"N"; |C_M #0#81,1,"N";
||  |FINSI;

  |PINDA #0#0;                    || Pinta los datos por defecto
  |ENDATOS #1#0;                  || Seleccion informe
|FPRO;
