|FICHEROS;
   camac347 #0;
   camal347 #1;
   camal348 #44,MANTE;
   eosclipr #2,MANTE;
   cauti347 #3;
   agipobla #4;
   agiprovi #5;
   caclipro #6;
   canempre #30;
   agicontr #23;

   caw00143 #943,MANTE;
   reflejo@ #900;
|FIN;

|VARIABLES;
 mod       = 0;
 men1      = "0000SEs correcto S/N : ";
 men2      = "0000NLo Grabo en el Fichero General : ";
 men6      = "    NConfirma el Alta en Fichero General (S/N): ";
 error1    = "     La empresa no tiene impreso del 347 ";
 verrfec   = "0000ATENCION !! Fecha erronea o formato incorrecto ";
 vdia1     = "";
 vme1      = "";
 vany1     = "";
 vdia      = 0;
 vme       = 0;
 vany      = 0;
 vtope     = 0;
 vsw       = 0;
 vfec      = "";
 vblanco   = "          ";
 vpuntos   = "..........";
 v         = 0;
 vacio     = "                                        ";
 cliente   = "";
 cuarenta  = "                                        ";
 NoHacer = 0;


 &elcampo0 = "";
 &elcampo1 = "";
 &elcampo2 = "";
 &elcampo3 = "";
 &elcampo4 = "";
 &elcampo5 = 0;
 &elcampo6 = 0;
 &elcampo7 = "";
 &elcampo8 = "";
 &elcampo9 = "";
 &swentra  = 0;
 &swmodi   = 0;
 swmod   = 0;
 directo = "";
 direct1 = "";
 traba = "";
 dprocli = "";
 eselbase = "";
 salida = 0;
   &aste = "";
   agi = "";
   &agi1 = "";
   &agi2 = "";
   num = 0;
   longi = "";

   nCampo1   = 0;
   nCampo2   = 0;
   nCampo3   = 0;

     aPathModelo = "";
     aDef        = "";

   alfa1 = "";
     &eaCodNif   = "";
     &eswNif     = 0;
     aNifQbf1   = "";
     aNifQbf2   = "";

     &eaVarDni  = "";
     &enCalcCif = 0;

     aAlfa       = "";
     nEjerCobro  = 0;
     nEjer       = 0;
     nSwSolo     = 0;
     nOpera      = 0;
     nModo       = 0;
     nMax        = 0;
     nMaxCobro   = 0;
     aMenav      = "";
     nNume1      = 0;
     nPara1      = 0;
     nPara2      = 0;
     aAlfa1      = "";
     aAlfa2      = "";
     nNegativo   = 0;
|FIN;

|INCLUYE acceso_i;
|INCLUYE varnue_i;

|PROCESO alentrar;  |TIPO 8;
  |HAZ Recalcula;
  |HAZ inicio;
  |HAZ eVarEmpresa;
  |HAZ tipo_8; || comprueba la existencia de la ficha general
  |SI aste ! "*";
      |PATH_DAT #2 agi1;
      |PATH_DAT #4 agi1;
      |PATH_DAT #5 agi1;

      |PATH_PAN #2 agi2;
      |PATH_PAN #4 agi2;
      |PATH_PAN #5 agi2;
  |FINSI;
  |SI enEjercicio > 2010;
      |C_M #1#5,1,"N";
  |FINSI;
|FPRC;

|PROCESO antesque;  |TIPO 7;
  |ABRE #30;
  #30#2 = cod1;
  #30#3 = cod2;
  |LEE 040#30=;
  #0#0 = #30#2;
  #0#1 = #30#1;
  |PINTA #0#0;
  |PINTA #0#1;
  |CIERRA #30;
|FPRC;

|PROCESO despuesde;
  |SI #0#0 ! cod1;
      |ERROR;
      #0#0 = cod1;
      |PINTA #0#0;
  |FINSI;
|FPRC;

|PROCESO TipoDecl;
   |SI #0#18 ! "N";
      |C_M #0#19,1,"S";
   |SINO;
      #0#19 = ""; |PINTA #0#19;
      |C_M #0#19,1,"N";
   |FINSI;
|FPRC;

|PROCESO mayor11;
  alfa1 = #0Campo > " ";
  #0Campo = alfa1;
  |PINTA #0Campo;

|SI Campo = 3;
    |SI #0Campo = "N";
        #0#16 = " "; |PINTA #0#16;
        |CAMPO_MODIFICA #0#16,1,"N";
    |SINO;
        |CAMPO_MODIFICA #0#16,1,"S";
        |SI #0#16 = " ";
            #0#16 = "I"; |PINTA #0#16;
        |FINSI;
    |FINSI;
|FINSI;

|SI Campo = 16;
    |SI #0#3 ! "N"; |Y #0#16 = " ";
        #0#16 = "I"; |PINTA #0#16;
    |FINSI;
|FINSI;

|FPRC;

|PROCESO fechacon; |TIPO 0;
  vfec = #0Campo;
  vsw = 0;
  |SI vfec = vblanco; |O vfec = vpuntos; |ACABA; |FINSI;

  vdia1 = vfec % A102;
  vme1  = vfec % A402;
  vany1 = vfec % A704;
  vdia  = vdia1;
  vme   = vme1;
  vany  = vany1;

  |SI vdia = 0; |Y vme = 0; |Y vany < 1900;
      |MENAV verrfec;
      |ERROR;
      |ACABA;
  |FINSI;

  |SI vme [ 12; |Y vme ] 1;
      |SI vme = 1; |O vme = 3; |O vme = 5; |O vme = 7; |O vme = 8; |O vme = 10; |O vme = 12;
          vtope = 31;
      |SINO;
          |SI vme = 2;
              mod = vany $ 4;
              |SI mod = 0; vtope = 29; |SINO; vtope = 28; |FINSI;
          |SINO;
              vtope = 30;
          |FINSI;
      |FINSI;
      |SI vdia [ vtope; vsw = 1; |FINSI;
  |FINSI;

  |SI vsw = 0;
      |MENAV verrfec;
      |ERROR;
  |FINSI;
|FPRC;
|| **************************************************************************

|PROCESO conficha;
  |SI aste ! "*";
      |ABRE #2;
      |PUSHV 0501 2380;
      |PINPA #0#2;
      #2#0 = #1#3;
      |LEE 000#2];
      |CONSULTA #0#2;
      #1#3 = #2#0;
      |CIERRA #2;
  |SINO;
      |ABRE #6;
      |SELECT #4#6;
      |PUSHV 0501 2380;
      |PINPA #0#6;
      #6#9 = #1#3;
      |LEE 000#6=;
      |CONSULTA #3#6;
      #1#3 = #6#9;
      |CIERRA #6;
  |FINSI;
      |POPV;
      |PINTA #1#3;
|FPRC;

|PROCESO cliente;|TIPO 0;
  salida = FSalida;
  |SI salida = 9;
      eaVarDni = #1Campo;
      |HAZ CalculoDNI;
      #1Campo = eaVarDni;
      |PINTA #1Campo;
      |ERROR;
      |ACABA;
  |FINSI;
  |SI salida = 10; |HAZ conficha; |FINSI;

  eaVarDni  = #1Campo;
  enCalcCif = 0;
  |HAZ CalculoDNI;

  aNifQbf1 = #1Campo;   |QBF aNifQbf1;
  aNifQbf2 = eaVarDni;  |QBF aNifQbf2;

  |SI aNifQbf1 ! aNifQbf2; |Y aNifQbf1 ! "";
      |CONFI "0000SEl Nif puede ser Erroneo. Desea Calcularlo.";
      |SI FSalida = 0;
          #1Campo = eaVarDni;  |PINTA #1Campo;
          |ERROR;
          |ACABA;
      |FINSI;
  |FINSI;

  |ABRE #3;
  |DDEFECTO #3;
  swmodi = 0;
  swmod  = 0;
|SI aste ! "*"; |Y #1#3 ! "            ";
    |ABRE #2;
    #2#0 = #1#3;
    |LEE 040#2=;
    |SI FSalida = 0;
        #1#4   = #2#1; #1#6  = #2#2; #1#7  = #2#3; #1#8  = #2#4;
        #1#9   = #2#7; #1#10 = #2#8; #1#11 = #2#9; #1#12 = #2#10;
        #1#16  = #2#14;
        |SI #1#9 ! 99;  #1#16 = "";  |FINSI;
        swmod = 1;
    |SINO;
        |HAZ altaclipr;
    |FINSI;
    |CIERRA #2;
|FINSI;
|SI swmod = 0;
    |ABRE #6;
    |SELECT #4#6;
    |DDEFECTO #6;
    #6#9 = #1#3;
    |LEE 040#6=;
    |SI FSalida = 0;
        #1#4 = #6#1; #1#7  = #6#2; #1#9 = #6#3;
        #1#10 = #6#4; #1#11 = #6#5; #1#12 = #6#6;
    |FINSI;
    |CIERRA #6;
|FINSI;
 |HAZ cargavar;
 |SI swentra = 1;
     |HAZ recovar;
     |SI aste ! "*"; |Y swmodi = 0;
         |CONFI men2;
         |SI FSalida = 0; |HAZ grabagen; |FINSI;
     |FINSI;
 |SINO;
     |ERROR;
 |FINSI;
|PINTA #1#4;
|CIERRA #3;
|FPRC;

|PROCESO grabagen;
   |ABRE #2;
   #2#0 = #1#3;
   |LEE 040#2=;
   salida = FSalida;
   |SI salida ! 0; |DDEFECTO #2; |FINSI;
   #2#0  = #1#3;  #2#1 = #1#4;  #2#2  = #1#6;
   #2#3  = #1#7;  #2#4 = #1#8;  #2#7  = #1#9;
   #2#8  = #1#10; #2#9 = #1#11; #2#10 = #1#12;
   |SI #1#9 = 99;  #2#14 = #1#16;  |FINSI;
   |SI salida ! 0;
       |GRABA 040#2n;
   |SINO;
       |GRABA 040#2a;
   |FINSI;
   |LIBERA #2;
   |CIERRA #2;
|FPRC;

|PROCESO cargavar;
  swentra  = 0;
  elcampo0 = #1#3;
  elcampo1 = #1#4;
  elcampo2 = #1#6;
  elcampo3 = #1#7;
  elcampo4 = #1#8;
  elcampo5 = #1#9;
  elcampo6 = #1#10;
  elcampo7 = #1#11;
  elcampo8 = #1#12;
  elcampo9 = #1#16;
  |SUB_EJECUTA_NP "cauti347.tab";
|FPRC;

|PROCESO recovar;
  #1#3  = elcampo0; elcampo0 = "";
  #1#4  = elcampo1; elcampo1 = "";
  #1#6  = elcampo2; elcampo2 = "";
  #1#7  = elcampo3; elcampo3 = "";
  #1#8  = elcampo4; elcampo4 = "";
  #1#9  = elcampo5; elcampo5 = 0;
  #1#10 = elcampo6; elcampo6 = 0;
  #1#11 = elcampo7; elcampo7 = "";
  #1#12 = elcampo8; elcampo8 = "";
  #1#16 = elcampo9; elcampo9 = "";
|FPRC;

|PROCESO TipoOpe;
     |C_M #1Campo, 0, aAlfa1;
     |SI aAlfa1 = "N"; |ACABA; |FINSI;
     |SI FSalida = 2; |ACABA; |FINSI;

     aAlfa = #1Campo;
     |SI aAlfa = "S";
         |SI Campo = 28;
             #1#29 = "N";  |PINTA #1#29;
             #1#13 = "N "; |PINTA #1#13;
             #1#14 = "N";  |PINTA #1#14;
         |FINSI;
         |SI Campo = 29;
             #1#28 = "N";  |PINTA #1#28;
             #1#13 = "N "; |PINTA #1#13;
             #1#14 = "N";  |PINTA #1#14;
         |FINSI;
         |SI Campo = 13;
             #1#28 = "N";  |PINTA #1#28;
             #1#29 = "N"; |PINTA #1#29;
             #1#14 = "N"; |PINTA #1#14;
         |FINSI;
         |SI Campo = 14;
             #1#28 = "N";  |PINTA #1#28;
             #1#29 = "N";  |PINTA #1#29;
             #1#13 = "N "; |PINTA #1#13;
         |FINSI;
     |FINSI;

     |C_M #1#30, 1, "S";
     |SI #1#28 = "S"; |O #1#29 = "S";
        |C_M #1#30, 1, "N";
        #1#30 = "N"; |PINTA #1#30;
     |FINSI;
|FPRC;

|PROCESO vertipo; |TIPO 7;
||    |SI #1#2 ! 1;
||        #1#13 = "N";
||        |PINTA #1#13;
||        |CAMPO_MODIFICA #1#13,1,"N";
||    |SINO;
||        |CAMPO_MODIFICA #1#13,1,"S";
||    |FINSI;
|FPRC;

|| ////////////////////////////////////////
|PROCESO TrasTipo; |TIPO 0;
     |C_M #1#13,1,"S";
     |C_M #1#14,1,"S";
     |C_M #1#28,1,"S";
     |C_M #1#29,1,"S";
     |C_M #1#30,1,"S";

     |SI #1#2 ! 2;
         |C_M #1#28,1,"N"; #1#28 = "N"; |PINTA #1#28;
     |FINSI;
     |SI #1#2 ! 2; |Y #1#2 ! 1;
         |C_M #1#13, 1,"N"; #1#13 = "N"; |PINTA #1#13;
         |C_M #1#14, 1,"N"; #1#14 = "N"; |PINTA #1#14;
         |C_M #1#28, 1,"N"; #1#28 = "N"; |PINTA #1#28;
         |C_M #1#29, 1,"N"; #1#29 = "N"; |PINTA #1#29;
         |C_M #1#30, 1,"N"; #1#30 = "N"; |PINTA #1#30;
     |FINSI;
     |SI enEjercicio < 2014;
         |C_M #1#28,1,"N"; #1#28 = "N"; |PINTA #1#28;
         |C_M #1#29,1,"N"; #1#29 = "N"; |PINTA #1#29;
         |C_M #1#30,1,"N"; #1#30 = "N"; |PINTA #1#30;
     |FINSI;
|FPRC;
|| ////////////////////////////////////////
|RUTINA ClaveBaja44;
     #44#0 = #1#0;
     #44#1 = #1#1;
     #44#2 = nEjerCobro;
|FRUT;

|RUTINA ClaveAlta44;
     #44#0 = #1#0;
     #44#1 = #1#1;
     #44#2 = 2008;
|FRUT;

|PROCESO SumayBorra;
   |SI nOpera = 1;
       |BORRA 020#44a;
       |SI #1#2 = 2; #0#6 = #0#6 - 1; |FINSI;
       |SI #1#2 = 1; #0#8 = #0#8 - 1; |FINSI;
   |FINSI;
   |SI nOpera = 0;
       #1#19 = #1#19 + #44#3;
   |FINSI;
   |LIBERA #44;
|FPRC;

|DEFBUCLE SumayBorra;
   #44#1;
   ;
   #1#0,  #1#1, nEjerCobro;
   #1#0,  #1#1, 2000;
   be;
   NULL, SumayBorra;
|FIN;

|PROCESO Metalico;
    |ENTLINEAL #1#44, -3, 2, 21,0, ClaveBaja44,ClaveAlta44;

    #1#19 = 0; nOpera = 0; |BUCLE SumayBorra;
|FPRC;

|PROCESO Tipo7C410; |TIPO 7;
   nModo = (FEntrada / 100) ! 0;
   |SI nModo = 1;
       |SI #44#2 > 2999;
           #44#2 = nEjer - 1; |PINTA #44#2;
       |FINSI;
   |FINSI;
|FPRC;

|PROCESO Tipo2C410; |TIPO 2;
  |SI #1#2 = 2;
      #0#6 = #0#6 +. 1;
  |FINSI;
  |SI #1#2 = 1;
      #0#8 = #0#8 +. 1;
  |FINSI;
|FPRC;

|PROCESO ImporteCobro; |TIPO 0;
     |SI #44#3 < nMaxCobro;
         aMenav = "     Introduzca Importe igual o superior a " + nMaxCobro;
         |MENAV aMenav;
         |ERROR;
     |FINSI;
     nEjer = #44#2;
|FPRC;


|PROCESO Modi2011;

     aAlfa = "S"; aAlfa1 = "N";
     |SI #1#2  ! 2; |Y #1#2 ! 1; aAlfa = "N"; |FINSI;
     |SI #1#2  = 1; |O #1#2 = 2;
         |SI #1#13 = "N"; |Y #1#14 = "N"; aAlfa1 = "S"; |FINSI;
     |FINSI;
     aAlfa2 = aAlfa;
     |SI enEjercicio ] 2012; |Y #1#2 = 2; aAlfa2 = "N"; |FINSI;

     |C_V #943#1,  1, aAlfa2; |C_M #943#1,  1, aAlfa2;
     |C_V #943#3,  1, aAlfa2; |C_M #943#3,  1, aAlfa2;
     |C_V #943#2,  1, aAlfa;
     |C_V #943#8,  1, aAlfa; |C_M #943#8,  1, aAlfa1;
     |C_V #943#9,  1, aAlfa; |C_M #943#9,  1, aAlfa1;
     |C_V #943#10, 1, aAlfa; |C_M #943#10, 1, aAlfa1;
     |C_V #943#11, 1, aAlfa; |C_M #943#11, 1, aAlfa1;

     |C_M #943#0, 1, "N";
     |C_M #943#4, 1, "S";
     |C_M #943#5, 1, "S";
     |C_M #943#6, 1, "S";
     |C_M #943#7, 1, "S";
     |C_M #943#12, 1, "N";  |C_V #943#12, 1,"N";
     |SI nSwSolo = 1;
         |C_M #943#1, 1, "N";
         |C_M #943#4, 1, "N";
         |C_M #943#5, 1, "N";
         |C_M #943#6, 1, "N";
         |C_M #943#7, 1, "N";
     |SINO;
         |SI #1#30 = "S";
             |C_M #943#0, 1, "S";
             |C_M #943#4, 1, "N";
             |C_M #943#5, 1, "N";
             |C_M #943#6, 1, "N";
             |C_M #943#7, 1, "N";
             |C_M #943#12, 1, "S";  |C_V #943#12, 1, "S";
        |FINSI;
     |FINSI;
|FPRC;

|PROCESO Sacar2011;
     |DDEFECTO #943;

     nNegativo = #1#5;
     |SI nNegativo < 0; nNegativo = - #1#5; |FINSI;
     |SI nNegativo < nMax; #1#17 = 0; |FINSI;
     #943#0 = #1#5;
     |PARA nPara1 = 17; |SI nPara1 [ 27; |HACIENDO nPara1 = nPara1 + 1;
           nPara2 = nPara1 - 16;
           #943nPara2 = #1nPara1;
     |SIGUE;
     #943#12 = #1#31;

     |PINPA #0#943;
     |SI #1#2 ! 2; |Y #1#2 ! 1;
         |BLANCO 1325 2280;
         |CUADRO 0925 1380;
     |FINSI;
     |SI enEjercicio ] 2012;
         |SI #1#2 = 2;
             |BLANCO 1625 2280;
             |CUADRO 0925 1680;
         |FINSI;
     |FINSI;

     |HAZ Modi2011;
     |PINDA #0#943;

     |SI enEjercicio ] 2012;
         |SI #1#2 = 1;
             |PINPA #0#44;
             |ENTLINEAL #1#44, -3, 7, 21,1, ClaveBaja44,ClaveAlta44;
         |FINSI;
     |SINO;
         |SI #1#2 = 2; |O #1#2 = 1;
             |PINPA #0#44;
             |ENTLINEAL #1#44, -3, 7, 21,1, ClaveBaja44,ClaveAlta44;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO ImporteW143; |TIPO 0;
      |SI #1#30 = "N";
          #943#0 = #943#4 + #943#5 + #943#6  + #943#7;  |PINTA #943#0;
      |SINO;
          #943#4 = 0;  #943#5 = 0;
          #943#6 = 0;  #943#7 = 0;
      |FINSI;
      #943#2 = #943#8 + #943#9 + #943#10 + #943#11; |PINTA #943#2;

      |SI FSalida = 2; |ACABA; |FINSI;

      |SI Campo = 7;
          |HAZ Modi2011;

          aMenav = "";
          nNegativo = #943#0;
          |SI nNegativo < 0; nNegativo = - #943#0; |FINSI;
          |SI nNegativo < nMax;
              aMenav = "     Introduzca Importe igual o superior a " + nMax + ".(sin signo)";
          |FINSI;
          |SI enEjercicio ] 2012;
              |SI #1#2 = 1;
                  |SI #943#0 = 0;
                      aMenav=  "    Introducir solo Importes en Metalico Ejercicios Anteriores";
                  |FINSI;
              |FINSI;
          |SINO;
              |SI #1#2 = 2; |O #1#2 = 1;
                  |SI #943#0 = 0;
                      aMenav=  "    Introducir solo Importes en Metalico Ejercicios Anteriores";
                  |FINSI;
              |FINSI;
          |FINSI;
          |SI aMenav ! "";
              |MENAV aMenav;
              |SI #943#0 ! 0;
                  |SI #1#2 ! 2; |Y #1#2 ! 1;
                      |ERROR;
                  |SINO;
                      |CONFI "   Es Correcto";
                      |SI FSalida ! 0;
                          |ERROR;
                      |FINSI;
                  |FINSI;
                  |ACABA;
              |SINO;
                  |C_M #943#1,  1, "N";
                  |C_M #943#8,  1, "N";
                  |C_M #943#9,  1, "N";
                  |C_M #943#10, 1, "N";
                  |C_M #943#11, 1, "N";
                  |SI enEjercicio ] 2012;
                      |SI #1#2 ! 1;
                          |ERROR;
                      |FINSI;
                  |SINO;
                      |SI #1#2 ! 2; |Y #1#2 ! 1;
                          |ERROR;
                      |FINSI;
                  |FINSI;
              |FINSI;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Tipo0C12w143; |TIPO 0;
     |C_M #943#12, 0, aAlfa1;
     |SI aAlfa1 = "N"; |ACABA; |FINSI;
     |SI FSalida = 10;
         #943#12 = #943#0; |PINTA #943#12;
     |FINSI;
|FPRC;

|PROCESO Tipo12w143; |TIPO 12;
|FPRC;

|PROCESO Tipo7C1w143; |TIPO 7;
   |SI #943#0 = 0;
       #943#1 = 0;
       |C_V #943#1,0,aAlfa1;
       |SI aAlfa1 = "S";
           |PINTA #943#1;
       |FINSI;
   |FINSI;
|FPRC;

|PROCESO Tipo0C1w143; |TIPO 0;
   |C_M #943Campo, 0, aAlfa1;
   |SI aAlfa1 = "N"; |ACABA; |FINSI;

   |SI #943#1 < nMaxCobro; |Y #943#1 ! 0;
       aMenav = "    El Importe es inferior a " + nMaxCobro + ". Continuar";
       |MENAV aMenav;
       #943#1 = 0; |PINTA #943#1;
       |ERROR;
       |ACABA;
   |FINSI;
|FPRC;

|PROCESO Tipo7C3w143; |TIPO 7;
   |C_M #943Campo, 0, aAlfa1;
   |SI aAlfa1 = "N"; |ACABA; |FINSI;

   |HAZ Metalico;
   #943#3 = #1#19;  |PINTA #943#3;
|FPRC;

|PROCESO Tipo0C3w143; |TIPO 0;
   #943#3 = #1#19;
   |C_V #943#3,0,aAlfa1;
   |SI aAlfa1 = "S"; |PINTA #943#3; |FINSI;
|FPRC;

|PROCESO Pedir2011;

     nSwSolo = 0;
     |HAZ Sacar2011;

     |SI nSwSolo = 1;
         |ATRI I; |PINTA 0926, "Introduzca Importes en Metalico Ejercicios Anteriores"; |ATRI 0;
     |FINSI;

     |ENDATOS #2#943;
     |SI FSalida = 0;
         |SI #1#30 = "N";
             #943#0 = #943#4 + #943#5 + #943#6  + #943#7;
         |FINSI;
         #943#2 = #943#8 + #943#9 + #943#10 + #943#11;

         #1#5 = #943#0;
         |PARA nPara1 = 17; |SI nPara1 [ 27; |HACIENDO nPara1 = nPara1 + 1;
               nPara2 = nPara1 - 16;
               #1nPara1 = #943nPara2;
         |SIGUE;
         #1#31 = #943#12;
     |FINSI;
|FPRC;

|PROCESO importe; |TIPO 0;

     |SI EURODB = 0;
         nMax = 500000;
         |SI #1#2 = 3;                 nMax = 50000;   |FINSI;
         |SI #1#2 = 1; |Y #1#14 = "S"; nMax = 0;       |FINSI;
         |SI #1#2 = 6;                 nMax = 0;       |FINSI;
         |SI #1#2 = 7;                 nMax = 0;       |FINSI;
         nMaxCobro = 1000000;
     |SINO;
         nMax = 3005.06;
         |SI #1#2 = 3;                 nMax = 300.51;  |FINSI;
         |SI #1#2 = 1; |Y #1#14 = "S"; nMax = 0;       |FINSI;
         |SI #1#2 = 6;                 nMax = 0;       |FINSI;
         |SI #1#2 = 7;                 nMax = 0;       |FINSI;
         nMaxCobro = 6000.01;
     |FINSI;

     |C_M #1#5, 0 , aAlfa1;
     |SI aAlfa1 = "S";
         nNegativo = #1#5;
         |SI nNegativo < 0; |Y enEjercicio > 2010; nNegativo = - #1#5; |FINSI;

         aMenav = "";
         |SI nNegativo < nMax;
             aMenav = "     Introduzca Importe igual o superior a " + nMax;
         |FINSI;
         |SI enEjercicio < 2012;
             |SI #1#2 = 1; |O #1#2 = 2;
                 |SI enEjercicio ] 2010;
                     |SI #1#5 = 0;
                        aMenav=  "    Introducir solo Importes en Metalico Ejercicios Anteriores";
                    |FINSI;
                 |FINSI;
             |FINSI;
         |SINO;
             |SI #1#2 = 1;
                 |SI #1#5 = 0;
                     aMenav=  "    Introducir solo Importes en Metalico Ejercicios Anteriores";
                 |FINSI;
             |FINSI;
         |FINSI;
         |SI aMenav ! "";
             |MENAV aMenav;
             |SI #1#5 ! 0; |O enEjercicio < 2011;
                 |ERROR;
                 |ACABA;
             |FINSI;
         |FINSI;
     |FINSI;
     |SI enEjercicio < 2008; |ACABA; |FINSI;

     |SI #1#2 ! 1;  |Y #1#2 ! 2; |Y enEjercicio < 2011;
         |ACABA;
     |FINSI;

|SI enEjercicio ] 2010;
     nEjer      = enEjercicio;
     nEjerCobro = enEjercicio - 1;

     |PUSHV 0925 2380;
     |SI enEjercicio = 2010;
         |BLANCO 1025 2280;
         |CUADRO 1025 1480;

         nSwSolo = 0;
         |SI #1#5 = 0; nSwSolo = 1; |FINSI;

         |SI nSwSolo = 1;
             |ATRI I; |PINTA 1026, "Introduzca Importes en Metalico Ejercicios Anteriores"; |ATRI 0;
         |FINSI;

         |PINTA 1126, "Percibido en metalico Ejercicio Actual...:            ";
         |PINTA 1226, "Percibido en metalico Ejer.Anteriores....:            ";
         |PINTA 1326, "Percibido por transmisiones sujetas a IVA:            ";

         |PINPA #0#44;

         E_inf = "-999999999.?EURO";
         E_sup = "9999999999";

         |SI #1#5 < nMax; #1#17 = 0; |FINSI;
         nCampo1  = #1#17;   |PINTA 1169, #1#17;
         nCampo3  = #1#19;   |PINTA 1269, #1#19;
         nCampo2  = #1#18;   |PINTA 1369, #1#18;
         |ENTLINEAL #1#44, -3, 7, 21,1, ClaveBaja44,ClaveAlta44;

         |ET nCampo1;
             E_inf = "-999999999.?EURO";
             E_sup = "9999999999";
             #1#19 = nCampo3;  |PINTA 1269, #1#19;
             |SI #1#5 ] nMax; |Y nSwSolo = 0;
                 nCampo1 = 1169 ? 1;
                 |SI FSalida = 1;  |VETE Abajo;    |FINSI;
                 |SI FSalida = 7;  |VETE Abajo;    |FINSI;
                 #1#17 = nCampo1;  |PINTA 1169, #1#17;
                 |SI #1#17 < nMaxCobro; |Y #1#17 ! 0;
                     aMenav = "     Introduzca Importe igual o superior a " + nMaxCobro;
                     |MENAV aMenav;
                     #1#17 = 0; |PINTA #1#17;
                     |VETE nCampo1;
                 |FINSI;
             |FINSI;

         |ET nCampo3;
             #1#18 = nCampo2;  |PINTA 1369, #1#18;
             nCampo3 = 1269 ? 1;
             |SI FSalida = 1;  |VETE Abajo;    |FINSI;
             |SI FSalida = 2;  |VETE nCampo1;  |FINSI;
             |SI FSalida = 7;  |VETE Abajo;    |FINSI;
             nCampo3  = #1#19;   |PINTA 1269, #1#19;
             |SI FSalida = 3;  |VETE nCampo2;    |FINSI;
             |HAZ Metalico;
             nCampo3  = #1#19;   |PINTA 1269, #1#19;

         |ET nCampo2;
             E_inf = "-999999999.?EURO";
             E_sup = "9999999999";
             |SI #1#5 ] nMax; |Y nSwSolo = 0;
                 nCampo2 = 1369 ? 1;
                 |SI FSalida = 2;  |VETE nCampo3;  |FINSI;
                 #1#18 = nCampo2;  |PINTA 1369, #1#18;
             |FINSI;

         |ET Abajo;
             #1#17 = nCampo1;  |PINTA 1169, #1#17;
             #1#19 = nCampo3;  |PINTA 1269, #1#19;
             #1#18 = nCampo2;  |PINTA 1369, #1#18;

    |SINO;
          |HAZ Pedir2011;
    |FINSI;

    |POPV;
    |BLIN 4;
    |SI enEjercicio > 2010; |PINTA #1#5; |FINSI;

|SINO;
     |PUSHV 1325 1680;
     |BLANCO 1325 1680;
     |CUADRO 1325 1680;

     |PINTA 1426, "Percibido en metalico ...................:            ";
     |PINTA 1526, "Percibido por transmisiones sujetas a IVA:            ";

     E_inf = "-999999999.?EURO";
     E_sup = "9999999999";
     nCampo1  = #1#17;   |PINTA 1469, #1#17;
     nCampo2  = #1#18;   |PINTA 1569, #1#18;

     |ET nCampo1_2008;
         E_inf = "-999999999.?EURO";
         E_sup = "9999999999";
         nCampo1 = 1469 ? 1;
         |SI FSalida = 1;  |VETE Abajo_2008;    |FINSI;
         |SI FSalida = 7;  |VETE Abajo_2008;    |FINSI;

     |ET nCampo2_2008;
         E_inf = "-999999999.?EURO";
         E_sup = "9999999999";
         nCampo2 = 1569 ? 1;
         |SI FSalida = 2;  |VETE nCampo1_2008;  |FINSI;

     |ET Abajo_2008;
         #1#17 = nCampo1;  |PINTA 1469, #1#17;
         #1#18 = nCampo2;  |PINTA 1569, #1#18;

     |POPV;
     |BLIN 4;

|FINSI;

|FPRC;

|PROCESO actualiz; |TIPO 2;
  |SI #1#5 = 0;  |ACABA;  |FINSI;

  |SI #1#2 = 2;
      |SI #1#5 ! 0;
          #0#6 = #0#6 +. 1;
      |FINSI;
      #0#7 = #0#7 +. #1#5;
      |PINTA #0#6;
      |PINTA #0#7;
  |FINSI;

  |SI #1#2 = 1;
      |SI #1#5 ! 0;
          #0#8 = #0#8 +. 1;
      |FINSI;
      #0#9 = #0#9 +. #1#5;
      |PINTA #0#8;
      |PINTA #0#9;
  |FINSI;

  |SI #1#2 = 3;
      #0#10 = #0#10 +. 1;
      #0#11 = #0#11 +. #1#5;
      |PINTA #0#10;
      |PINTA #0#11;
  |FINSI;

  |SI #1#2 = 4;
      #0#12 = #0#12 +. 1;
      #0#13 = #0#13 +. #1#5;
      |PINTA #0#12;
      |PINTA #0#13;
  |FINSI;

  |SI #1#2 = 5;
      #0#14 = #0#14 +. 1;
      #0#15 = #0#15 +. #1#5;
      |PINTA #0#14;
      |PINTA #0#15;
  |FINSI;

  |SI #1#2 = 6;
      #0#20 = #0#20 +. 1;
      #0#21 = #0#21 +. #1#5;
      |PINTA #0#20;
      |PINTA #0#21;
  |FINSI;

  |SI #1#2 = 7;
      #0#22 = #0#22 +. 1;
      #0#23 = #0#23 +. #1#5;
      |PINTA #0#22;
      |PINTA #0#23;
  |FINSI;

   nModo = (FEntrada / 100) ! 0;
   |SI nModo = 3;
       nOpera = 1; |BUCLE SumayBorra;
   |FINSI;
|FPRC;

|PROCESO tipo_8; || control de la ficha de clientes general
    |DIRECTORIO agi;
    |QBF agi;
    |PATH_DAT #23 agi;

    |ABRE #23;
    #23#0 = 1;
    |LEE 040#23=;
    |SI FSalida ! 0; aste = "*"; |CIERRA #23; |ACABA; |FINSI;
    |CIERRA #23;
    aste = #23#7;
    |QBF aste;
    |SI aste  = "*"; |ACABA; |FINSI;

    agi1 = #23#8; |QBF agi1;

    longi = agi1 % 0;
    num   = longi;
    num   = 100 + (num - 5);
    longi = num;

    agi2  = (agi1 % longi) + "pan/";
|FPRC;

|PROCESO altaclipr;
        |SI aste = "*"; |ACABA; |FINSI;
        |CONFI men6;
        |SI FSalida = 0;
            |PUSHV 0501 2380;
            |PINPA #0#2;
            |DDEFECTO #2;
            |PINDA #0#2;
            #2#0 = #1#3;
            |PINTA #2#0;
            |ENDATOS #1#2;
            |SI FSalida = 0;
                |POPV;
                #1#4  = #2#1; #1#6  = #2#2; #1#7  = #2#3; #1#8  = #2#4;
                #1#9  = #2#7; #1#10 = #2#8; #1#11 = #2#9; #1#12 = #2#10;
                #1#16 = #2#14;
                |SI #1#9 ! 99;  #1#16 = "";  |FINSI;
                swmodi=1; swmod = 1;
                |GRABA 020#2n;                 || Graba el agicli
            |SINO;
                |POPV;
            |FINSI;
        |FINSI;
        |LIBERA #2;
        |CIERRA #2;
|FPRC;

|PROCESO Registro4;
   |SI #1#2 = 2; #0#6 = #0#6 + 1; |FINSI;
   |SI #1#2 = 1; #0#8 = #0#8 + 1; |FINSI;
|FPRC;

|DEFBUCLE Registro4;
   #44#1;
   ;
   #camal347#0,  #camal347#1, 2999;
   #camal347#0,  #camal347#1, 2000;
   ;
   NULL, Registro4;
|FIN;

|PROCESO Lineas;
   |SI #camal347#2 = 1;
       |SI #camal347#5 ! 0;
           #camac347#8  = #camac347#8  + 1;
       |FINSI;
       |BUCLE Registro4;
       #camac347#9  = #camac347#9  + #camal347#5;
   |FINSI;
   |SI #camal347#2 = 2;
       |SI #camal347#5 ! 0;
           #camac347#6  = #camac347#6  + 1;
       |FINSI;
       |BUCLE Registro4;
       #camac347#7  = #camac347#7  + #camal347#5;
   |FINSI;
   |SI #camal347#2 = 3; #camac347#10 = #camac347#10 + 1; #camac347#11 = #camac347#11 + #camal347#5; |FINSI;
   |SI #camal347#2 = 4; #camac347#12 = #camac347#12 + 1; #camac347#13 = #camac347#13 + #camal347#5; |FINSI;
   |SI #camal347#2 = 5; #camac347#14 = #camac347#14 + 1; #camac347#15 = #camac347#15 + #camal347#5; |FINSI;
   |SI #camal347#2 = 6; #camac347#20 = #camac347#20 + 1; #camac347#21 = #camac347#21 + #camal347#5; |FINSI;
   |SI #camal347#2 = 7; #camac347#22 = #camac347#22 + 1; #camac347#23 = #camac347#23 + #camal347#5; |FINSI;

   |SI #camal347#0 ! #48#2; |Y NoHacer = 0;
      |SALVA_DATOS #camal347; |BORRA 020#camal347.a; |REPON_DATOS #camal347;
      #camal347#0 = #48#2; |GRABA 020#camal347.n;
   |FINSI;
|FPRC;

|DEFBUCLE Lineas;
#camal347#1;
;
#camac347#0,00001;
#camac347#0,99999;
;
NULL,Lineas;
|FIN;

|PROCESO Empres;
   |SI #camac347#0 ! #48#2;
      |SALVA_FICHA #camac347;
      #camac347#0 = #48#2;
      |LEE 000#camac347.=;
      |SI FSalida = 0; NoHacer = 1; |SINO; NoHacer = 0; |FINSI;
      |REPON_FICHA #camac347;
   |FINSI;
   #camac347#6  = 0; #camac347#7  = 0;
   #camac347#8  = 0; #camac347#9  = 0;
   #camac347#10 = 0; #camac347#11 = 0;
   #camac347#12 = 0; #camac347#13 = 0;
   #camac347#14 = 0; #camac347#15 = 0;
   #camac347#20 = 0; #camac347#21 = 0;
   #camac347#22 = 0; #camac347#23 = 0;
   |BUCLE Lineas;
   |SI #camac347#0 ! #48#2;
      |SI NoHacer = 0;
         |SALVA_DATOS #camac347;
         |BORRA 020#camac347.a; |LIBERA #camac347;
         |REPON_DATOS #camac347;
         #camac347#0 = #48#2; |GRABA 020#camac347.b;
      |FINSI;
   |FINSI;
   |GRABA 020#camac347.a;
   |LIBERA #camac347;
|FPRC;

|DEFBUCLE Empres;
   #camac347#1;
   ;
   00001;
   99999;
   be;
   NULL,Empres;
|FIN;

|PROCESO Recalcula;
   |ABRE #camac347;
   |LEE 000#camac347.p;
   |SALVA_FICHA #camac347;
   |BUCLE Empres;
   |REPON_FICHA #camac347;
   |CIERRA #camac347;
|FPRC;

|PROCESO Baja900;
     #900#0 = #1#0;
     #900#1 = 1;
|FPRC;

|PROCESO Alta900;
     #900#0 = #1#0;
     #900#1 = 99999;
|FPRC;

|PROCESO Tipo11;  |TIPO 11;
     nEjerCobro = enEjercicio - 1;
     |SI FSalida = 10;
         |DBASE aPathModelo;  |QBT aPathModelo;
         aDef = aPathModelo + "def/camal347.mas";
         |CARGA_DEF aDef, reflejo@;
         |SI FSalida ! 0;
             aAlfa = "     Error en cargar el Def camal347.mas";
             |MENAV aAlfa;
             |ERROR;
         |FINSI;

         |C_V #900#17, 1, "S";
         |C_V #900#18, 1, "S";
         |C_V #900#19, 1, "S";

         |ABRE #900;
         |CONSULTA_F_CLAVE #1#900, Baja900, Alta900;
         |SI FSalida = 0;
             #1#0 = #900#0;
             #1#1 = #900#1;
             |LEE 000#1=;
         |FINSI;
         |CIERRA #900;

         |DESCARGA_DEF #reflejo@;

         |PTEC 809;
         |PTEC 808;
         |PONCONTROL  23, "SI";
     |FINSI;

     |SI FSalida = 11;
         |SI #1#2 ! 1;  |Y #1#2 ! 2; |Y enEjercicio < 2011;
             |ACABA;
         |FINSI;

         |SI enEjercicio < 2008; |ACABA; |FINSI;

         |SI enEjercicio ] 2010;
             |PUSHV 0925 2480;
             |SI enEjercicio > 2010;
                 |HAZ  Sacar2011;
             |SINO;
                 |BLANCO 1025 2280;
                 |CUADRO 1025 1480;

                 |PINTA 1126, "Percibido en metalico Ejercicio Actual...:            ";
                 |PINTA 1226, "Percibido en metalico Ejer. Anteriores ..:            ";
                 |PINTA 1326, "Percibido por transmisiones sujetas a IVA:            ";

                 |PINTA 1169, #1#17;
                 |PINTA 1269, #1#19;
                 |PINTA 1369, #1#18;

                 |ENTLINEAL #1#44, -3, 7, 21,1, ClaveBaja44,ClaveAlta44;
             |FINSI;
          |SINO;
             |PUSHV 1325 1680;
             |BLANCO 1325 1680;
             |CUADRO 1325 1680;

             |PINTA 1426, "Percibido en metalico ...................:            ";
             |PINTA 1526, "Percibido por transmisiones sujetas a IVA:            ";

             nCampo1  = #1#17;   |PINTA 1469, #1#17;
             nCampo2  = #1#18;   |PINTA 1569, #1#18;

          |FINSI;

          |PAUSA;
          |POPV;
          |ERROR;

     |FINSI;
|FPRC;
