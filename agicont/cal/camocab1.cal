|FICHEROS;
  camocab1 #0;
  camocabe #2;
  camoline #1;
  cacuenta #5;
  camoauxc #6;
|FIN;

|VARIABLES;
  &ct1_am = "";
  &ct2_am = 0;
  &fec_am = @;
  &imp_am = 0;
  &prv_am = "";
  &c_am = "";
  alfa1 = "";
  xx = 0;
  sw_er = 0;
  any = "";
  estado = 0;
  linea = 0;
  impcom = 0;
  p = 0;
    &entrada = 1;
    &sw_lin = 0;
    doce = "            ";
    sw = 0;
    long = 0;
    alfa2 = "";
    alfa3 = "";
    alfa4 = "";
    nume1 = 0;
    nume2 = 0;
    nume3 = 0;
    nume4 = 0;
    salidas = 0;
    inkey   = 0;
|FIN;

|INCLUYE inicio_i;
|INCLUYE puntos_i;
|INCLUYE numamo_i;
|INCLUYE vardep_i;

|| .... Calculos de los campos por pantalla.
|PROCESO cuenta; |TIPO 6;
  p_alfa1 = #0Campo;
  salidas = FSalida;
  |HAZ punto;
  |SI p_error ! 0;  |ERROR;  |ACABA;  |FINSI;
      nume1 = Campo + 1;
      alfa1 = #0Campo;
  |QBF alfa1;
  alfa1 = alfa1 % 0;
  long = alfa1;
  sw   = 0;
  #0nume1 = 0;  || inicializa el digito
  |SI long = dig4; |Y dig4 ! 0; sw = 1; #0nume1 = 1; |FINSI;
  |SI long = dig5; |Y dig5 ! 0; sw = 1; #0nume1 = 2; |FINSI;
  |SI long = dig6; |Y dig6 ! 0; sw = 1; #0nume1 = 3; |FINSI;
  |SI long = dig7; |Y dig7 ! 0; sw = 1; #0nume1 = 4; |FINSI;
  |SI long = dig8; |Y dig8 ! 0; sw = 1; #0nume1 = 5; |FINSI;
  |SI long = dig9; |Y dig9 ! 0; sw = 1; #0nume1 = 6; |FINSI;
  |SI sw = 0;|Y long ! 0;
      |MENAV "    Longitud de Cuenta Fuera de Nivel !";
      |ERROR;
      |ACABA;
  |FINSI;
|FPRC;

|PROCESO refresca; |TIPO 0;
  |SI #0Campo ! doce;
      |SI #5#3 = "N";
          |MENAV "    Esta Cuenta NO tiene Pase Directo ";
          |ERROR;
          |ACABA;
      |FINSI;
  |FINSI;
|FPRC;

|PROCESO comp;
  |ABRE #6;
  #6#0 = #0#3;
  #6#1 = #0#4;
  |LEE 001#6=;
  |SI FSalida = 0;
      |MENAV "0000Esta Cuenta YA existe en el Desglose";
      |ERROR;
  |FINSI;
  |CIERRA #6;
|FPRC;

|PROCESO ATamo; |TIPO 7;
  alfa1 = "0000Entre: (N) normal; (D) Degresiva; (A) Acelerada por Suma de Digitos";
  |SI #0#6 < "01.01.2015";
      alfa1 = "0000Entre: (N) normal; (D) Degresiva; (A) Acelerada; (R) Valor residual ant. 2015";
  |FINSI;
  |MENSA alfa1;
|FPRC;

|PROCESO tipamo;
  |BLIN 4;

  |SI #0#8 = "U"; |Y #0Campo ! "N";
      |MENAV "     En un Bien Usado NO se admite este tipo de Amortizacion";
      |ERROR;
      |ACABA;
  |FINSI;

   |SI #0Campo = "R";
       |SI #0#6 ] "01.01.2015";
           |MENAV "     Fecha de Compra igual o superior a 01.01.2015, Tipo amortizacion Incorrecto";
           |ERROR;
           |ACABA;
       |FINSI;
   |FINSI;

  nume1 = Campo + 4;
  |SI #0Campo = "N";
      #0nume1 = 0;
      |CAMPO_MODIFICA #0nume1, 1, "N";
      |PINTA #0nume1;
  |SINO;
      |CAMPO_MODIFICA #0nume1, 1, "S";
  |FINSI;
|FPRC;

|CALCULO ElsAnys07; |TIPO 7;
 nume1 = Campo - 4;
 |SI #0nume1 = "R";
     |MENSA "0000Introduzca los a¤os que restan de vida util ";
 |FINSI;
|FCAL;

|CALCULO ElsAnys; |TIPO 0;
 |BLIN 4;
 inkey = FSalida;
 |C_M #0Campo,0,alfa1;
 |SI alfa1 = "N"; |ACABA; |FINSI;
 |SI inkey = 2; |ACABA; |FINSI;

 |SI #0Campo = 0;
     |MENAV "    Introduzca el datos de los A¤os ";
     |ERROR;
 |SINO;
     nume1 = Campo - 4;
     |SI #0nume1 ! "R";
         #0Campo = #0Campo ! 0;
         |PINTA #0Campo;
     |FINSI;
 |FINSI;
|FCAL;

|PROCESO tracodi; |TIPO 0;
  |CAMPO_MODIFICA #0#1, 1, "S";
  alfa1 = #0#0;
  alfa2 = alfa1;
  |QBF alfa2;
  |SI alfa2 = ""; |ERROR; |ACABA; |FINSI;
  #2#0 = #0#0;
  #2#1 = 00;
  |LEE 011#2=;
  |SI FSalida ! 0;
      |CAMPO_MODIFICA #0#1, 1, "N";
      #0#1 = 0;
      |PINTA #0#1;
      |ACABA;
  |FINSI;
|FPRC;

|PROCESO numamo; |TIPO 6;
  x_entra = entrada;
  x_alfa1 = #0Campo;
  |HAZ x_punto;
  #0Campo = x_alfa1;
  |PINTA #0Campo;
|FPRC;

|| ======================================================================

|PROGRAMA;
  |HAZ inicio;
  |ABRET #2;
  |DDEFECTO #2;
|ET empieza;
  |PINPA #0#0;
 |HAZ def_campos;
       |SI sw_er = 1;
           |CIERRA #2;
           |ACABA;
       |FINSI;
  |PINDA #0#0;
  |ENDATOS #1#0;
  |SI FSalida = 0;
       |HAZ actualiza;
  |SINO;
       |CONFI "2400SINTRODUCCION NO CONFORME. ¨ Elegir otro codigo de Elemento? ";
       |SI FSalida = 0;
           |HAZ restaura;
           |VETE empieza;
       |FINSI;
  |FINSI;
  |CIERRA #2;
|FPRO;

|PROCESO def_campos;
|ET otro;
  sw_er = 0;
  linea = 0;
  impcom = 0;
  |DDEFECTO #0;

  |ENCAMPO #0#0;
  |SI FSalida ! 0; sw_er = 1; |ACABA; |FINSI;
  |ENCAMPO #1#0;
  |SI FSalida ! 0; sw_er = 1; |ACABA; |FINSI;

  #2#0 = #0#0;
  #2#1 = #0#1;
  |LEE 011#2=;
  estado = FSalida;
  |SI FSalida ! 0;
      linea = 1;
  |SINO;
      |HAZ tracod2;
      |SI sw_er = 1; |ACABA;     |FINSI;
      |SI sw_er = 2; |VETE otro; |FINSI;
      |PARA xx = 2; |SI xx [ 23; |HACIENDO xx = xx + 1;
            #0xx = #2xx;
      |SIGUE;
  |FINSI;


  |SI ct2_am ! 0;
      #0#3 = ct1_am;
      #0#4 = ct2_am;
  |FINSI;
  #0#5 = prv_am;
  #0#6 = fec_am;
  #0#7 = imp_am + impcom;
  #0#13 = #0#13 + imp_am;
  #0#19 = #0#19 + imp_am;
  #0#23 = "00.00.0000";

  #0#24 = linea;
  any   =  #0#6 % -104; #0#25 = any;
  #0#26 = "C";
  #0#27 = imp_am;
  #0#28 = c_am;
  #0#29 = fec_am;
  #0#30 = "S";
  #0#31 = eaCodDep;
|FPRC;

|PROCESO actualiza;
  |DDEFECTO #2;
  |PARA xx = 0; |SI xx [ 23; |HACIENDO xx = xx + 1;
        #2xx = #0xx;
  |SIGUE;
  #2#28 = #0#31;
  |SI estado = 0;
      |GRABA 011#2a;
      |LIBERA #2;
  |SINO;
      |GRABA 011#2n;
      |LIBERA #2;
  |FINSI;

  |ABRE #1;
  #1#0 = #2#0;
  #1#1 = #2#1;
  #1#2 = #0#24;
  |LEE 000#1=;
  estado = FSalida;
    #1#0 = #2#0;  #1#1 = #2#1;  #1#2 = #0#24;
    #1#3 = #0#25; #1#4 = #0#26; #1#5 = #0#27;
    #1#6 = #0#30; #1#7 = #0#29; #1#8 = #0#28;
    #1#9 = #0#27;
  |SI estado = 0;
      |GRABA 011#1a;
      |LIBERA #1;
  |SINO;
      |GRABA 011#1n;
      |LIBERA #1;
  |FINSI;
  |CIERRA #1;
|FPRC;


|PROCESO tracod2;
  |SI #2#23 > "00.00.0000";
      |MENAV "0000El Elemento Ya existe y esta vendido. ";
      sw_er = 2;
      |ACABA;
  |FINSI;
  |SI #2#12 = 0; |O #2#17 = 0;
      |CONFI "2400NEl Elemento ya existe. ¨Desea ampliarlo? ";
      |SI FSalida ! 0;
          sw_er = 1;
          |CONFI "2400S¨Introducir otro codigo de Elemento? ";
          |SI FSalida = 0;
              sw_er = 2;
          |FINSI;
          |ACABA;
      |FINSI;
      |HAZ lineas;
  |SINO;
      |MENAV "0000El Elemento Ya existe y se esta amortizando. ";
      sw_er = 2;
      |ACABA;
  |FINSI;
|FPRC;

|PROCESO vercom;
  linea = #1#2;
  |SI #1#4 ! "C"; |ACABA; |FINSI;
  impcom = impcom + #1#5;
|FPRC;

|PROCESO lineas;
  linea = 0;
  |ABRE #1;
  |BUCLELIN 0vercom#2;
  |CIERRA #1;
  |PINTA 2007, #1#2;
  |PINTA 2012, #1#3;
  |PINTA 2019, #1#7;
  |PINTA 2032, #1#4;
  |PINTA 2036, #1#8;
  |PINTA 2066, #1#5;
  |PINTA 2078, #1#6;

  linea = linea + 1;
|CAMPO_POSICION #0#24,0,p; p=p+100; |CAMPO_POSICION #0#24,1,p;
|CAMPO_POSICION #0#25,0,p; p=p+100; |CAMPO_POSICION #0#25,1,p;
|CAMPO_POSICION #0#26,0,p; p=p+100; |CAMPO_POSICION #0#26,1,p;
|CAMPO_POSICION #0#27,0,p; p=p+100; |CAMPO_POSICION #0#27,1,p;
|CAMPO_POSICION #0#28,0,p; p=p+100; |CAMPO_POSICION #0#28,1,p;
|CAMPO_POSICION #0#29,0,p; p=p+100; |CAMPO_POSICION #0#29,1,p;
|CAMPO_POSICION #0#30,0,p; p=p+100; |CAMPO_POSICION #0#30,1,p;
|FPRC;

|PROCESO restaura;
 |SI linea = 1; |ACABA; |FINSI;

|CAMPO_POSICION #0#24,0,p; p=p-100; |CAMPO_POSICION #0#24,1,p;
|CAMPO_POSICION #0#25,0,p; p=p-100; |CAMPO_POSICION #0#25,1,p;
|CAMPO_POSICION #0#26,0,p; p=p-100; |CAMPO_POSICION #0#26,1,p;
|CAMPO_POSICION #0#27,0,p; p=p-100; |CAMPO_POSICION #0#27,1,p;
|CAMPO_POSICION #0#28,0,p; p=p-100; |CAMPO_POSICION #0#28,1,p;
|CAMPO_POSICION #0#29,0,p; p=p-100; |CAMPO_POSICION #0#29,1,p;
|CAMPO_POSICION #0#30,0,p; p=p-100; |CAMPO_POSICION #0#30,1,p;
|FPRC;
