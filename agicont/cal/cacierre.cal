|FICHEROS;
  cacierre #0;
  camaasil #1;    || apuntes lins.
  camaasic #2;    || apuntes cabs.
  catracie #4;    || trabajo cierre
  catradia #5;    || Trabajo Actualizacion cuentas de mayor
  caconcep #7;    || conceptos
  caexistc #22;   || Cabeceras de Existencias

  caconcic #50;
  caconcil #51;
  camocabe #60;
  camoline #61;
  caw00011 #65;

  Referen1@ #600;
  Referen2@ #601;
  Referen3@ #602;
  Referen4@ #603;

  ca8comay #401;  ||Control Cuentas de mayor
  ca8m0003 #403;
  ca8m0004 #404;
  ca8m0002 #422;
|FIN;

|VARIABLES;
  nSw116 = 0;
  nNume  = 0;
  aAlfa1 = "";
  aAlfa2 = "";
   jaleo = 0;
   &enDiario   = 0;
   &enContador = 0;
   &entrada = 1;

   VAlfa1 = "";
   VAlfa2 = "";
   VAlfa3 = "";
   VAlfa4 = "";
   VAlfa5 = "";
   swexis = 0;

    alfa1 = "";
    alfa2 = "";
    cam = 0;
    linea = 0;
    avisillo = "";
    cuenta = "";
    cuen = "";
    xxxxfiac = "  ";
    xxaponer = "  ";
    xrelleno = "                              ";
    fecalf = "";
    men2   = "    NO EXISTE CONCEPTO AUTOMATICO";

    desde = "            ";
    hasta = "zzzzzzzzzzzz";
    fich = "";
    no_sigas = 0;
    cierre = "";
    apertu = "";
    d_diari = 0;
    d_opera = "";
    d_impor = 0;
    d_tipos = 0;
    FEstado = 0;
    nPOrigen = 0;
    swalg    = 0;

    nume1 = 0;
    nume2 = 0;
    &num_per    = 0;
    &enEmpreOri = 0;
    &enPerioOri = 0;
    nEjerActual = 0;
    aCtazzz     = "";
    nSw         = 0;

    debe   = 0;
    haber  = 0;
    nCta   = 0;
    aCta   = "";
    aCtaN  = "";
    fFecha = @;

  &modcon = 0;
  juan = 0;
  errant = 0;
  cuini = "            ";
  cufin = "999999999998";
  diini = 0;
  difin = 9;
  fecini = @;
  fecfin = @;
  tiini = "D";
  tifin = "H";
  diain = 0;
  diafi = 99;
  prim = 0;
  cuant = "";
  lon = 0;
  aa = 0;
  aar = "";
  errcuent = "aaaaaaaaaaaa";

  nError      = 0;
  aFichero    = "";
   nNumero     = 0;
   nDebe       = 0;
   nHaber      = 0;
   nMovim      = 0;
   nMoviI      = 0;
   nMoviE      = 0;
   nMoviB      = 0;

   &eaPathO  = "";
   &eaPathI  = "";
   &enPeriodoO = 0;
   &enPeriodoD = 0;
|FIN;

|INCLUYE acceso_i;
|INCLUYE varnue_i;
|INCLUYE copian_i;
|INCLUYE copia1_i;
|INCLUYE condes_i;
|INCLUYE dsmone_i;
|INCLUYE vardep_i;
|INCLUYE defec1_i;

|CALCULO BuscaDoc; |TIPO 0;
   |ABRE #2;
   |SELECT #3#2;
   |LEE 010#2u;
   |SI FSalida = 0;
      #0#3 = #2#2 + 1;
      #0#4 = #2#2 + 1;
   |SINO;
      #0#3 = 1;
      #0#4 = 1;
   |FINSI;
   |SELECT #1#2;
   |CIERRA #2;
|FCAL;

|PROGRAMA;
  |CLS;
  |CABEZA "Cierre Ejercicio";

  |HAZ VerCaconimp;
  |C_V #0#16,1,eaConDes; |C_V #0#17,1,eaConDes;
  |PINPA #0#0;
  |DDEFECTO #0;
  |HAZ BuscaDoc;

  |PINDA #0#0;
  |HAZ apertu;
  |ENDATOS #1#0;
  |SI FSalida = 0;
      |HAZ TesteoCierre;
      |SI nError ! 0; |ACABA; |FINSI;

      |HAZ leeempre;
      |SI no_sigas ! 0;  |ACABA;  |FINSI;
      |HAZ cierre;
      |SI nError ! 0;  |ACABA; |FINSI;

      |HAZ apertura;
      |HAZ copiaantes;
  |FINSI;
|FPRO;

|PROCESO apertu;         || calcula el periodo de la empresa nueva
  |HAZ inicio;

  num_por1 = #30#2;
  any_por1 = #30#26;
  |HAZ eVarEmpresa;

  enPeriodoO = enPeriodo;

  cierre   = #30#0;  cierre = "000000" + cierre;  cierre = cierre % -106;
  cod2     = #30#3;
  nPOrigen = cod2;
  #0#10 = cod1;
  #0#11 = cod2 + 1;
  |SI #0#11 = 10;  #0#11 = 0;  |FINSI;
  |PINTA #0#10;
  |PINTA #0#11;
  #0#0 = fec2;
  #0#5 = #0#0 + 1;
  |PINTA #0#0;
  |PINTA #0#5;
  aMonedaC = eaMoneda;
  alfa1=#30#24; |C_M #0#18, 1, alfa1; |C_M #0#19, 1, alfa1;

  #0#20 = eNum2008; |PINTA #0#20;
  nEjerActual = enEjercicio + 1;
  eSwC2008    = 0;
  |SI nEjerActual ] 2008; |Y enEjercicio < 2008;  eSwC2008 = 1; |FINSI;

  |SI nEjerActual ] 2008; |HAZ LeoTabla; |FINSI;
  |C_M #0#20,1,"N";
  |SI eSwC2008 = 1; |C_M #0#20, 1, "S"; |FINSI;

  enPeriodoD = #0#11;
|FPRC;

|PROCESO LeoTabla;
     |ABRE #400;
     |DDEFECTO #400;
     #400#0 = enEmpresa;
     #400#1 = enPeriodo;
     |LEE 001#400=;
     |SI FSalida = 0;
         #0#20 = #400#2; |PINTA #0#20;
     |SINO;
         #400#0 = #0#10;
         #400#1 = #0#11;
         |LEE 001#400=;
         |SI FSalida = 0;
             #0#20 = #400#2; |PINTA #0#20;
         |FINSI;
     |FINSI;
     |CIERRA #400;
|FPRC;


|PROCESO leeempre;       || lee la empresa
  jaleo = 0;
  cod1 = #0#10;
  cod2 = #0#11;
  |HAZ leelo;
  |SI swerror = 1;
      |HAZ creacion;
      swOperacion = 1;
      |SI no_sigas = 1; |ACABA; |FINSI;
  |SINO;
      swOperacion = 0;
      enEmpresa = #0#10;
      enPeriodo = #0#11;
      |SUB_EJECUTA_NP "camoneda";
  |FINSI;
  |BLIN 23;
  |BLIN 24;
  apertu = #30#0;  apertu = "000000" + apertu;  apertu = apertu % -106;
  #0#12 = #30#1;
  |PINTA #0#12;
  aMonedaA = eaMoneda;
  |SI dig3 = 1; nume1 = dig4 + 100; |FINSI;
  |SI dig3 = 2; nume1 = dig5 + 100; |FINSI;
  |SI dig3 = 3; nume1 = dig6 + 100; |FINSI;
  |SI dig3 = 4; nume1 = dig7 + 100; |FINSI;
  |SI dig3 = 5; nume1 = dig8 + 100; |FINSI;
  |SI dig3 = 6; nume1 = dig9 + 100; |FINSI;
  aCta  = ("11600000000000" % nume1);
  aCtaN = "AJUSTES";
  nCta  = dig3;
|FPRC;

*****************************************************************************
               CALCULOS DESDE CAMPOS
*****************************************************************************
|PROCESO mayor;
  #0Campo = #0Campo > "  ";
  |PINTA #0Campo;
|FPRC;

|PROCESO entrada; |TIPO 0;         || desde campo 'fecha de cierre'
  enDiario = 99;
  |HAZ contador;
  #0#3 = enContador;
  #0#4 = #0#3;
  |PINTA #0#3;
  |PINTA #0#4;
|FPRC;

|PROCESO repite; |TIPO 0;          || desde campo 'documento'
  nume1 = Campo + 1;
  #0nume1 = #0Campo;
  |PINTA #0nume1;
|FPRC;

|PROCESO concepto; |TIPO 0;        || desde campo 'concepto'
  |ABRE #7;
  #7#0 = #0Campo;
  |LEE 001#7=;
  |SI FSalida ! 0;
      |MENAV men2;
      |DDEFECTO #7;
      |CIERRA #7;
      |ERROR;
      |ACABA;
  |FINSI;
  |SI Campo = 1; nume1 = 2; nume2 = 16; |FINSI;
  |SI Campo = 6; nume1 = 7; nume2 = 17; |FINSI;

  #0nume2 = #7#1;
  |SI eaConDes = "N";
      #0nume1 = #7#1; |PINTA #0nume1;
  |SINO;
      |PINTA #0nume2;
  |FINSI;
  |CIERRA #7;
|FPRC;

|PROCESO final; |TIPO 7;
 |SI eaConDes ! "N"; #0Campo = ""; |FINSI;
 |PTEC 816;
|FPRC;

|CALCULO LosDep; |TIPO 0;

   |C_M #0Campo,0,alfa1;
   |SI alfa1 = "N"; |ACABA; |FINSI;

   eOpDep = 0; || 1 = Consulta pantalla
   |SI FSalida = 10; eOpDep = 1;  |FINSI;
   eaCodDep    = #0Campo;
   |HAZ Actividad;
   |SI eaCodDep = ""; eswLect = 0; |FINSI;
   |SI eswLect = 1;
       |MENAV "    Error.! No Existe Departamento";
       |ERROR;
   |SINO;
       #0Campo = eaCodDep; |PINTA #0Campo;
   |FINSI;
|FCAL;

|PROCESO CodCta7; |TIPO 7;
   |SI nEjerActual < 2008;
       #0#20 = 0; |PINTA #0#20;
       |ACABA;
   |FINSI;
|FPRC;

|PROCESO CodCta0; |TIPO 6;

  |ABRE #401;
  #401#0 = #0#20;
  |LEE 001#401=;
  |SI FSalida ! 0;
      |MENAV "    Error, no existe Codigo de Cuentas de Mayor";
      |ERROR;
  |SINO;
      |C_A #401#1,1,33; |PINTA 1143, #401#1;
  |FINSI;
  |CIERRA #401;
|FPRC;

*****************************************************************************
               CALCULOS DE CIERRE
*****************************************************************************
|PROCESO altatra;
  modcon = 1;

  #5#0 = codcon;
  #5#1 = #1#4;
  #5#2 = #1#5;
  #5#3 = #1#1;
  #5#4 = #1#8;
  #5#6 = #1#0;
  |LEE 101#5=;
  |SI FSalida ! 0;
      |DDEFECTO #5;
      #5#0 = codcon;
      #5#1 = #1#4;
      #5#2 = #1#5;
      #5#3 = #1#1;
      #5#4 = #1#8;
      #5#6 = #1#0;
      |GRABA 020#5b;
  |FINSI;
  #5#5 = #5#5 + #1#9;
  |GRABA 020#5a;
  |LIBERA #5;
|FPRC;

|PROCESO buscalibre;
  #1#0 = #2#0;
  #1#1 = #2#1;
  #1#2 = #2#2;
  #1#3 = 9999;
  |LEE 001#1];
  |SI FSalida ! 0;
      |LEE 001#1u;
  |SINO;
      |LEE 001#1a;
  |FINSI;
  |SI FSalida ! 0;
      linea = 1;
  |SINO;
      |SI #1#0 = #2#0; |Y #1#1 = #2#1; |Y #1#2 = #2#2;
          linea = #1#3 + 1;
      |SINO;
          linea = 1;
      |FINSI;
  |FINSI;
|FPRC;

|PROCESO grabadia;
  |SI cam = 0;
      cam = 1;
      #2#0 = 99;
      #2#1 = #0#0;
      #2#2 = #0#3;
      |LIBERA #2;
      |LEE 101#2=;
      |SI FSalida ! 0;
          |DDEFECTO #2;
          #2#0 = 99;
          #2#1 = #0#0;
          #2#2 = #0#3;
          #2#3 = #0#3;
          |GRABA 020#2b;
      |FINSI;
      |HAZ buscalibre;
  |FINSI;
  |DDEFECTO #1;
  #1#0 = #2#0;
  #1#1 = #2#1;
  #1#2 = #2#2;
  #1#3 = linea;
  #1#4 = #6#0;
  #1#5 = #6#1;
  #1#6 = #0#1;
  #1#7 = #0#2;
  #1#24 = #0#16;
  |SI #6#53 [ 0;
      #1#8 = "D";
      #1#9 = -#6#53;
  |SINO;
      #1#8 = "H";
      #1#9 = #6#53;
  |FINSI;
  |SI #1#8 = "D";
      #1#16 = #1#4;
      #1#17 = #1#5;
      #1#10 = "";
      #1#11 = 0;
  |SINO;
      #1#16 = "";
      #1#17 = 0;
      #1#10 = #1#4;
      #1#11 = #1#5;
  |FINSI;
  #1#21 = #0#18;
  |GRABA 020#1n;
  linea = linea + 1;

  d_diari = #1#0;
  d_opera = #1#8;
  d_impor = #1#9;
  d_tipos = 1;
  |HAZ actu_diari;
  |HAZ altatra;

  |SI #1#8 = "D";
      #2#4 = #2#4 + #1#9;
      |SI #2#09 = 0; #2#08 = #1#4; #2#09 = #1#5; |FINSI;      || contrp.HABER
  |SINO;
      #2#5 = #2#5 + #1#9;
      |SI #2#11 = 0; #2#10 = #1#4; #2#11 = #1#5; |FINSI;      || contrp.DEBE
  |FINSI;
  #2#6 = #2#4 - #2#5;
  |GRABA 020#2a;
|FPRC;

|PROCESO grabacion;
  #4#0 = #6#0;
  #4#1 = #6#1;
  #4#2 = #6#58;
  |SI #6#53 [ 0;
      #4#3 = "H";
      #4#4 = -#6#53;
  |SINO;
      #4#3 = "D";
      #4#4 = #6#53;
  |FINSI;
  #4#5 = #6#2;
  #4#6 = #6#3;
  #4#7 = #6#4;
  #4#8 = #6#5;
  #4#9 = #6#6;
  #4#10 = #6#7;
  #4#11 = #6#8;
  #4#12 = #6#54;
  #4#13 = #6#55;
  |GRABA 020#4n;
|FPRC;

|PROCESO acerar;
  |SI #6#53 [ 0;
      #6#48 = #6#48 - #6#53;
      #6#51 = #6#51 - #6#53;
  |SINO;
      #6#49 = #6#49 + #6#53;
      #6#52 = #6#52 + #6#53;
  |FINSI;
  #6#50 = #6#48 - #6#49;
  #6#53 = #6#51 - #6#52;
  |GRABA 020#6a;
|FPRC;

|PROCESO aceroplan;
  avisillo = "2301 Asiento de Cierre, Tratando: " + #6#0;
  |MENSA avisillo;
  |HAZ grabacion;
  |SI #6#53 ! 0;
      |HAZ grabadia;
  |FINSI;
  |HAZ acerar;
  |LIBERA #6;
|FPRC;

|DEFBUCLE cacierre0;
 #6#1;
 3;
 desde, 0, "S";
 hasta, 9, "S";
 be;
 NULL, aceroplan;
|FIN;

|PROCESO cierre;
  eaMoneda = aMonedaC;
  |HAZ MoCierre;      ||Carga Variables Moneda

  |DBASE dir_fich;
  dir_fich = dir_fich + "fich/" + cierre + "/";

  aFichero = "b" + Usuario;
  |NOME_DAT #5 aFichero;

  |PATH_DAT #1  dir_fich;
  |PATH_DAT #2  dir_fich;
  |PATH_DAT #6  dir_fich;
  |PATH_DAT #4  dir_fich;
  |PATH_DAT #5  dir_fich;
  |PATH_DAT #18 dir_fich;
  |DELINDEX #4;
  |DELINDEX #5;

  |ABRE #1;
  |ABRE #2;
  |ABRE #4;
  |ABRE #5;
  |ABRE #18;
  cam = 0;
  |BUCLE cacierre0;
  |CIERRA #1;
  |CIERRA #2;
  |CIERRA #4;
  |CIERRA #5;
  |CIERRA #18;
  |SI cam = 0;
      |MENAV "    Atencion. No hay movimientos en las Cuentas. No se ha realizado el Asiento de Cierre";
      nError = 1;
      |ACABA;
  |FINSI;
  |SI #2#4 ! #2#5;
      |MENAV "    AVISO!!! Asiento de Cierre Descuadrado";
  |FINSI;
  |HAZ camaapua;   || act. las cuentas.
|FPRC;

|| ----------------------- PROCESO camaapua -----------------------
|PROCESO actuali;
  |SI #5#2 [ juan; |O #5#2 = 9;
      |SI prim = 1;
           |GRABA 020#6a;
      |FINSI;
      prim = 0;
      |VETE acaba;
  |FINSI;
  aa = 100 + lon;
  aar = aa;
  traba = #5#1 % aar;
|ET iniemp;
  |SI prim = 0;
      cuant = traba;
      prim = 1;
      #6#0 = cuant;
      #6#1 = juan;
      |SI #6#0 = errcuent;
          prim = 0;
          |VETE acaba;
      |FINSI;
      |LIBERA #6;
      |LEE 101#6=;
      |SI FSalida ! 0; || no existe la cuenta
          |LIBERA #6;
          |DDEFECTO #6;
          #6#0 = cuant;
          #6#1 = juan;
          |HAZ defect1; || poner defectos a la cuenta nueva
          |PUSHV 0501 1980;
          |PINPA #0#6;
          |PINDA #0#6;
          |ENDATOS #1#6;
          |SI FSalida ! 0;
              |POPV;
              errant = #6#0;
              prim = 0;
              |VETE acaba;
          |FINSI;
          |GRABA 020#6b;
          |POPV;
      |FINSI;
  |FINSI;
  |SI cuant ! traba;
      |GRABA 020#6a;
      prim = 0;
      |VETE iniemp;
  |FINSI;
  |HAZ suma;
|ET acaba;
  |LIBERA #5;
|FPRC;

|PROCESO borrando;
  |BORRA 020#5a;
  |LIBERA #5;
|FPRC;

|DEFBUCLE camaapua0; || lee el fichero de trabajo para modificar cuentas
 #5#1;
 ;
 codcon,cuini,diini,fecini,tiini,diain;
 codcon,cufin,difin,fecfin,tifin,diafi;
 be;
 NULL,actuali;
|FIN;

|DEFBUCLE camaapua1; || lee fic. trabajo para borrarlo
 #5#1;
 ;
 codcon,cuini,diini,fecini,tiini,diain;
 codcon,cufin,difin,fecfin,tifin,diafi;
 be;
 NULL,borrando;
|FIN;

|PROCESO camaapua;
|| PINTA 2410,"Estoy Actualizando las Cuentas";
  |ABRE #6;
  |PARA juan = dig3 - 1; |SI juan ] 1; |HACIENDO juan = juan - 1;
        prim = 0;
        |SI juan = 1; lon = dig4; |FINSI;
        |SI juan = 2; lon = dig5; |FINSI;
        |SI juan = 3; lon = dig6; |FINSI;
        |SI juan = 4; lon = dig7; |FINSI;
        |SI juan = 5; lon = dig8; |FINSI;
        fecini = "01.01.0000";
        fecfin = "31.12.8999";
        |BUCLE camaapua0;
        |SI prim = 1;
            |GRABA 020#6a;
        |FINSI;
  |SIGUE;
  |CIERRA #6;
  fecini = "01.01.0000";
  fecfin = "31.12.8999";
  |BUCLE camaapua1;
  |ABRE #5;
  |LEE 000#5u;
  |SI FSalida ! 0;
       |CIERRA #5;
       |DELINDEX #5;
  |SINO;
       |CIERRA #5;
  |FINSI;
|FPRC;

|PROCESO suma;
  |SI #5#4 = "D";
       #6#48 = #6#48 + #5#5;
       #6#51 = #6#51 + #5#5;
   |SINO;
       #6#49 = #6#49 + #5#5;
       #6#52 = #6#52 + #5#5;
   |FINSI;
   #6#50 = #6#48 - #6#49;
   #6#53 = #6#51 - #6#52;
|FPRC;
*****************************************************************************
               CALCULOS DE APERTURA
*****************************************************************************
|PROCESO leeape;
  avisillo = "2301 Asiento de Apertura, Tratando: " + #4#0;
  |MENSA avisillo;
  |SI aMonedaC ! aMonedaA;
      impMoneda = #4#4;
      |HAZ ElCambioMoneda;
      #4#4= impMoneda;
  |FINSI;

  eaCuenta = #4#0;
  enNivel  = #4#1;
  aCtazzz  = #4#12;
  eaNomCta = #4#5;
  |SI eSwC2008 = 0;
      |HAZ grabacuen;                    || actualiza cuenta
      |SI #4#4 ! 0; |Y #4#6 = "S";
           |HAZ grabaaper;                || graba apunte
      |FINSI;
  |SINO;
      |SI #4#6 = "S";
          |HAZ grabacuen08;
      |FINSI;
  |FINSI;
|FPRC;

|DEFBUCLE cacierre1;
 #4#1;
 ;
 desde, 0;
 hasta, 9;
 e;
 NULL, leeape;
|FIN;

|PROCESO grabacuen08;

  alfa1     = eaCuenta; |QBF alfa1;
  eaCta2008 = eaCuenta;
  eSwTabla  = 0;
  |SI alfa1 ! "";
      |HAZ CambioCta08;
  |FINSI;

  |SI eSwTabla = 0;
      eaCuenta = eaCta2008;
      alfa1    = eaCuenta; |QBF alfa1;
      aCtazzz  = (alfa1 + "zzzzzzzzzzzz" ) % 112;
      |HAZ grabacuen;
      |SI #4#4 ! 0;
           |HAZ grabaaper;                || graba apunte
           |HAZ GraboAux;
      |FINSI;
  |SINO;
      |SI #4#4 ! 0;          ||si no tenia cantidad desaparece
          eaCuenta = aCta;
          alfa1    = eaCuenta; |QBF alfa1;
          aCtazzz  = (alfa1 + "zzzzzzzzzzzz" ) % 112;
          nSw116   = 1;
          |HAZ grabacuen;
          |HAZ grabaaper;
          |HAZ GraboAux;
          nSw116   = 0;
      |FINSI;
  |FINSI;
|FPRC;

|PROCESO grabacuen;
  |ABRE #6;
  #6#0 = eaCuenta;
  #6#1 = enNivel;
  |LEE 101#6=;
  |SI FSalida ! 0;
      |DDEFECTO #6;
      #6#0 = eaCuenta;
      #6#1 = enNivel;
      #6#2 = #4#5;
      #6#3 = #4#6;
      |HAZ defect1;
      #6#54 = aCtazzz;
      #6#55 = enNivel;
      |GRABA 020#6b;
  |FINSI;

  #6#58 = #4#2;
  |SI #4#3 = "D";
      #6#9 = #4#4;
      #6#51 = #6#51 + #4#4;
  |SINO;
      #6#10 = #4#4;
      #6#52 = #6#52 + #4#4;
  |FINSI;
  #6#11 = #6#9 - #6#10;
  #6#53 = #6#51 - #6#52;
  |GRABA 020#6a;
  |LIBERA #6;
  |CIERRA #6;
|FPRC;

|PROCESO lee_descarga1;
  |SI #1#8 = "D";     #19#11 = #1#9;  |FINSI;
  |SI #1#8 = "H";     #19#11 = -#1#9; |FINSI;

  |GRABAENDEF VAlfa2, VAlfa3, 0,#19#0, 1,#19#1, 2,#19#2, 3,#19#3, 4,#19#4, 5,#19#5, 6,#19#6, 7,#19#7, 8,#19#8, 9,#19#9, 10,#19#10, 11,#19#11;
  |SI swexis = 0;
      |ABRE #22;
      |DDEFECTO #22;
      #22#0 = #19#0;
      |LEE 001#22=;
      |GRABAENDEF VAlfa4, VAlfa5, 0,#22#0, 1,#22#1, 2,#22#2, 3,#22#3;
      swexis = 1;
      |CIERRA #22;
  |FINSI;
||  |GRABA 020#19a;
||  |LIBERA #19;
|FPRC;

|DEFBUCLE lee_descarga;
 #19#2;
 ;
 #0#13, #1#4, #1#5;
 #0#13, #1#4, #1#5;
 ;
 NULL, lee_descarga1;
|FIN;

|PROCESO buscaaper;
  #1#0 = #2#0;
  #1#1 = #2#1;
  #1#2 = #2#2;
  #1#3 = 9999;
  |LEE 001#1];
  |SI FSalida ! 0;
      |LEE 001#1u;
  |SINO;
      |LEE 001#1a;
  |FINSI;
  |SI FSalida ! 0;
      linea = 1;
  |SINO;
      |SI #1#0 = #2#0; |Y #1#1 = #2#1; |Y #1#2 = #2#2;
          linea = #1#3 + 1;
      |SINO;
          linea = 1;
      |FINSI;
  |FINSI;
|FPRC;

|PROCESO grabaaper;
  |SI cam = 0;
      cam = 1;
      #2#0 = 0;
      #2#1 = #0#5;
      #2#2 = #0#8;
      |LIBERA #2;
      |LEE 101#2=;
      |SI FSalida ! 0;
          |DDEFECTO #2;
          #2#0 = 0;
          #2#1 = #0#5;
          #2#2 = #0#8;
          #2#3 = #0#8;
          |GRABA 020#2b;
      |FINSI;
      |HAZ buscaaper;
  |FINSI;
  |DDEFECTO #1;
  #1#0 = #2#0;
  #1#1 = #2#1;
  #1#2 = #2#2;
  #1#3 = linea;
  #1#4 = eaCuenta;
  #1#5 = enNivel;
  #1#6 = #0#6;
  #1#7 = #0#7;
  #1#24 = #0#17;
  #1#8 = #4#3;
  #1#9 = #4#4;
  |SI #1#8 = "D";
      #1#16 = #1#4;
      #1#17 = #1#5;
      #1#10 = "";
      #1#11 = 0;
  |SINO;
      #1#16 = "";
      #1#17 = 0;
      #1#10 = #1#4;
      #1#11 = #1#5;
  |FINSI;
  |SI #0#14 = "S";
      #1#18 = "û";
  |FINSI;
  #1#21 = #0#19;
  |GRABA 020#1n;
  linea = linea + 1;

  |BUCLE lee_descarga;

  d_diari = #1#0;
  d_opera = #1#8;
  d_impor = #1#9;
  d_tipos = 2;
  |HAZ actu_diari;

  |SI #1#8 = "D";
      #2#4 = #2#4 + #1#9;
      |SI #2#09 = 0; #2#08 = #1#4; #2#09 = #1#5; |FINSI;      || contrp.HABER
  |SINO;
      #2#5 = #2#5 + #1#9;
      |SI #2#11 = 0; #2#10 = #1#4; #2#11 = #1#5; |FINSI;      || contrp.DEBE
  |FINSI;
  #2#6 = #2#4 - #2#5;
  |GRABA 020#2a;
  nSw = 1;
|FPRC;

|PROCESO GraboAux;
  debe   = 0;
  haber  = 0;
  |ABRE #404;

  |DDEFECTO #404;
  #404#0  = #1#3;
  #404#10 = #1#0;
  #404#11 = #1#1;
  #404#12 = #1#2;

  #404#1 = #4#0;
  #404#7 = #4#1;
  #404#2 = #4#5;
  #404#3 = #4#3;
  |SI #404#3 = "D";
      #404#4 = #4#4;
      debe   = #4#4;
      #404#5 = 0;
  |SINO;
      #404#4 = 0;
      #404#5 = #4#4;
      haber  = #4#4;
  |FINSI;
  #404#8  = eaCuenta;
  #404#9  = enNivel;
  #404#13 = #1#21;
  |GRABA 020#404n;
  |LIBERA #404;
  |CIERRA #404;

  |SI nSw116 = 1;
      |ABRE #403;
      #403#0 = #4#0;
      |LEE 101#403=;
      |SI FSalida ! 0;
          #403#0 = #4#0;
          |GRABA 020#403b;
      |FINSI;
      #403#1 = #4#5;
      #403#4 = #403#2;
      #403#5 = #403#3;
      #403#2 = eaCuenta;
      #403#3 = #6#2;
      #403#6 = "*";
      |GRABA 020#403a;
      |LIBERA #403;
      |CIERRA #403;
  |FINSI;
|FPRC;

|PROCESO apertura;

  eaMoneda = aMonedaA;
  |HAZ MoCierre;      ||Carga Variables Moneda

  nNume       = enEjercicio;
  enEjercicio = nEjerActual;
  |DBASE dir_fich;
  dir_fich = dir_fich + "fich/" + apertu + "/";
  fich_alta = dir_fich;
  eaPath2008 = fich_alta;

  |PATH_DAT #1 dir_fich;
  |PATH_DAT #2 dir_fich;
  |PATH_DAT #6 dir_fich;
  |PATH_DAT #18 dir_fich;
  |PATH_DAT #50 dir_fich;
  |PATH_DAT #51 dir_fich;
  |PATH_DAT #60 dir_fich;
  |PATH_DAT #61 dir_fich;
  |PATH_DAT #102 dir_fich;
  |PATH_DAT #404 dir_fich; |DELINDEX #404;
  |PATH_DAT #403 dir_fich;

  |DDEF VAlfa1; |QBF VAlfa1;
  VAlfa2 = VAlfa1   + "caexistl";
  VAlfa3 = dir_fich + "caexistl";
  VAlfa4 = VAlfa1   + "caexistc";
  VAlfa5 = dir_fich + "caexistc";
  swexis = 0;
|| ..............................  |PATH_DAT #19 dir_fich;

  |SI enEjercicio ] 2008;
     |ABRE #400;
     |DDEFECTO #400;
     #400#0 = #0#10;
     #400#1 = #0#11;
     |LEE 000#400=;
     |SI FSalida ! 0;
         #400#0 = #0#10;
         #400#1 = #0#11;
         #400#2 = #0#20;
         #400#3 = enEmpresa;
         #400#4 = nPOrigen;
         #400#5 = 1;
         fFecha = ~;             #400#6 = fFecha;
         aAlfa1 = Usuario % 810; #400#7 = aAlfa1;
         |GRABA 020#400b;
     |SINO;
         #400#2 = #0#20;
         |GRABA 020#400a;
     |FINSI;
     |CIERRA #400;
 |FINSI;
|| ******************************************************************

  linea   = 1;
  |ABRE #1;
  |ABRE #2;
  |ABRE #18;
  cam = 0;
  |BUCLE cacierre1;
  |CIERRA #18;
  |CIERRA #2;
  |CIERRA #1;
  |DELINDEX #4;

  |SI #2#4 ! #2#5;
      |MENAV "    AVISO!!! Asiento de Apertura Descuadrado";
  |FINSI;

  |SI nSw = 1; |Y eSwC2008 = 1;
      |PUSHV 0909 1666;
      |ATRI R;
      |PINTA 1010, " ATENCION:                                               ";
      |PINTA 1110, " SE HA REALIZADO EL ASIENTO DE APERTURA EN  BASE  DE LA  ";
      |PINTA 1210, " TABLA DE EQUIVALENCIAS DE CUENTAS EXISTENTE.            ";
      |PINTA 1310, " REVISE QUE EL ASIENTO DE APERTURA CONTIENE LAS CUENTAS  ";
      |PINTA 1410, " CORRECTAS, Y, EN CASO NECESARIO, PROCEDA MANUALMENTE A  ";
      |PINTA 1510, " REALIZAR LOS CAMBIOS QUE CONSIDERE OPORTUNOS.           ";
      |ATRI 0;
      |CUADRO 0909 1666;
      |PAUSA;
      |POPV;
  |FINSI;

  |SI nSw = 1; |Y eSwC2008 = 1;
      aAlfa1 = #0#10; aAlfa1 = ("00000" + aAlfa1) % -105;
      aAlfa2 = #0#11; aAlfa2 = ("0"     + aAlfa2) % -101;
      aAlfa1 = "carecupe canempr1 " + aAlfa1 + aAlfa2 + ";apertura";
      |SUB_EJECUTA_NP aAlfa1;
  |FINSI;
  enEjercicio = nNume;

  |SI #0#21 = "S";
      aAlfa1 = enEjercicio;
      aAlfa1 = "camaccz3;I"+ enEjercicio;
      |SUB_EJECUTA_NP aAlfa1;
  |FINSI;
|FPRC;
*****************************************************************************
               CALCULOS DE CREACION DE LA EMPRESA
*****************************************************************************

|PROCESO creacion;
  no_sigas = 0;
  |CONFI "2400S Realizar Creacion de Empresa de Apertura: ";
  |SI FSalida = 0;
      |HAZ empresa;
      swOperacion = 1;
      enEmpresa = #0#10;
      enPeriodo = #0#11;
      |SUB_EJECUTA_NP "camoneda";
      |HAZ jaleo;
  |SINO;
      no_sigas = 1;
  |FINSI;
|FPRC;

|PROCESO empresa;             || da de alta la empresa y crea el directorio
   |DBASE dir_fich;
   dir_fich=dir_fich + "fich/";
   |PATH_DAT #30 dir_fich;
   |ABRE #30;
   #30#2 = #0#10;
   #30#3 = #0#11;
   |LEE 001#30=;
   |SI FSalida ! 0;
       #30#0 = (#0#10 * 10) + #0#11;
       #30#2 = #0#10;
       #30#3 = #0#11;
       #30#26 = #30#26 + 1;                 || suma un a¤o
       |SI #30#26 = 100;  #30#26 = 0;  |FINSI;  || a¤o cero
       |GRABA 020#30n;
       |SI FSalida = 0;
          |DBASE alfa1;
          alfa2 = #30#0;  alfa2 = "000000" + alfa2;  alfa2 = alfa2 % -106;
          alfa1 = alfa1 + "fich/" + alfa2;
          |MKDIR alfa1;
      |FINSI;
  |FINSI;
  |CIERRA #30;
|FPRC;

|PROCESO copiaantes;
  |DFICE dir_fich;
  |DBASE fich;
  alfa2 = #30#0;
  alfa2 = "000000" + alfa2;
  alfa2 = alfa2 % -106;
  fich = fich + "fich/" + alfa2 + "/";

  fich_copi = dir_fich;
  fich_alta = fich;

  |SI #0#14 = "S";
      traba1 = "caconcic";  |HAZ copiando;    || cabeceras diferencias
      traba1 = "caconcil";  |HAZ copiando;    || lineas diferencias
      |HAZ CambioConc;
  |FINSI;
  |SI #0#15 = "S";
       |SI jaleo = 1;
           |SI eSwC2008 = 0;
               traba1 = "camocabe";  |HAZ copiando;    || cabeceras amortizaciones
               traba1 = "camoline";  |HAZ copiando;    || lineas amortizaciones
           |FINSI;
           |HAZ CambioAmor;
       |SINO;
           |HAZ CopiaAmort;
       |FINSI;
  |FINSI;
|FPRC;

|PROCESO jaleo;          || copia los ficheros
  num_por = #30#2;
  num_per = #30#3;
  any_por = #30#26;
  |DFICE dir_fich;
  |DBASE fich;
  alfa2 = #30#0;
  alfa2 = "000000" + alfa2;
  alfa2 = alfa2 % -106;
  fich = fich + "fich/" + alfa2 + "/";

  fich_copi = dir_fich;
  fich_alta = fich;
  eaPath2008 = fich_alta;

  |HAZ copia_maes;
  |SI eSwC2008 = 1;
      |HAZ creacion_m;   || .... Copia las estructuras
  |FINSI;
  |HAZ acerar4;
  |HAZ pone0_maes;
  jaleo = 1;
   |ABRE #400;
   |DDEFECTO #400;
   #400#0 = #0#10;
   #400#0 = #0#11;
   |LEE 101#400=;
   |SI FSalida ! 0;
       #400#0 = #0#10;
       #400#1 = #0#11;
       #400#2 = #0#20;
       |SI eSwC2008 = 1;
           #400#3 = enEmpresa;
           #400#4 = enPeriodo;
           #400#5 = 1;
       |FINSI;
       fFecha = ~;
       #400#6 = fFecha;
       aAlfa1 = Usuario % 810;
       #400#7 = aAlfa1;
       |GRABA 020#400b;
   |FINSI;
   |LIBERA #400;
   |CIERRA #400;

  |SI eSwC2008 = 1;
      eNum2208 = 1;
      eNum2008   = #0#20;
      enEmpreOri = #0#10;
      enPerioOri = nPOrigen;
      |PINTA 2211, " ADAPTANDO PLAN CONTABLE 2008. Cuenta:                 ";
      eNum2208 = 1;
      |SUB_EJECUTA_NP "ca8zcamb";
   |FINSI;
|FPRC;

|| **************************************************************
|PROCESO actu_diari;
  #18#0 = d_diari;
  |LEE 101#18=;
  |SI FSalida ! 0;
      |DDEFECTO #18;
      #18#0 = d_diari;
      |SI d_tipos = 1;  #18#1 = "DIARIO DE CIERRE  ";  |FINSI;
      |SI d_tipos = 2;  #18#1 = "DIARIO DE APERTURA";  |FINSI;
      |GRABA 020#18n;
      |LEE 121#18=;
  |FINSI;
  |SI d_opera = "D";
      #18#2 = #18#2 + d_impor;        || debe
  |SINO;
      #18#3 = #18#3 + d_impor;        || haber
  |FINSI;
  |GRABA 020#18a;
  |LIBERA #18;
|FPRC;

|| *********************************************************************
|PROCESO borrarlo;
  avisillo = "2301 Actualizando Diferencias, Tratando: " + #caconcil#0;
  |MENSA avisillo;
  |SI #caconcil#8 ! " ";
      |BORRA 020#caconcil.a;
  |SINO;
      |SI aMonedaC ! aMonedaA;
          impMoneda = #caconcil#6; |HAZ ElCambioMoneda; #caconcil#6= impMoneda;
          impMoneda = #caconcil#7; |HAZ ElCambioMoneda; #caconcil#7= impMoneda;
          |GRABA 020#caconcil.a;
      |FINSI;
  |FINSI;
  |LIBERA #caconcil;
|FPRC;

|DEFBUCLE borrar_a;
 #caconcil#1;
 4;
 desde, 0, 001, "01.01.1900";
 hasta, 9, 999, "31.12.2090";
 be;
 NULL, borrarlo;
|FIN;

|| *********************************************************************

|PROCESO leelineas;
 swalg = 1;
 |ERROR10;
|FPRC;

|PROCESO borrar_cab;
 swalg = 0;
 |BUCLELIN 2leelineas#caconcic;
 |SI swalg = 0;
     |BORRA 020#caconcic.a;
 |SINO;
     |SI aMonedaC ! aMonedaA;
         impMoneda = #caconcic#4; |HAZ ElCambioMoneda; #caconcic#4 = impMoneda;
         |GRABA 020#caconcic.a;
     |FINSI;
 |FINSI;
 |LIBERA #caconcic;
|FPRC;

|DEFBUCLE borrar_c;
 #caconcic#1;
 ;
 desde, 0;
 hasta, 9;
 be;
 NULL, borrar_cab;
|FIN;

|PROCESO CambioConc;
  |BUCLE borrar_a;  ||Bucle para repasar Diferencias
  |BUCLE borrar_c;  ||Recalculo de Cabeceras
|FPRC;

|| ********************************************************************
|PROCESO C2Amor0;
  avisillo = "2301 Actualizando Amortizaciones, Tratando: " + #camoline#0;
  |MENSA avisillo;
  impMoneda = #camoline#5; |HAZ ElCambioMoneda; #camoline#5= impMoneda;
  impMoneda = #camoline#9; |HAZ ElCambioMoneda; #camoline#9= impMoneda;
  |GRABA 020#camoline.a;
  |LIBERA #camoline;
|FPRC;

|PROCESO C1Amor0;
  avisillo = "2301 Actualizando Amortizaciones, Tratando: " + #camocabe#0;
  |MENSA avisillo;
  impMoneda = #camocabe#7; |HAZ ElCambioMoneda; #camocabe#7= impMoneda;
  impMoneda = #camocabe#12; |HAZ ElCambioMoneda; #camocabe#12= impMoneda;
  impMoneda = #camocabe#13; |HAZ ElCambioMoneda; #camocabe#13= impMoneda;
  impMoneda = #camocabe#18; |HAZ ElCambioMoneda; #camocabe#18= impMoneda;
  impMoneda = #camocabe#19; |HAZ ElCambioMoneda; #camocabe#19= impMoneda;
  |GRABA 020#camocabe.a;
  |LIBERA #camocabe;
|FPRC;

|DEFBUCLE C2Amor;
 #camoline#1;
 ;
 "        ", 00,000;
 "zzzzzzzz", 99,999;
 be;
 NULL, C2Amor0;
|FIN;

|DEFBUCLE C1Amor;
 #camocabe#1;
 ;
 "        ", 00;
 "zzzzzzzz", 99;
 be;
 NULL, C1Amor0;
|FIN;

|PROCESO CambioAmor;
  |SI aMonedaC ! aMonedaA;
      |BUCLE C1Amor;
      |BUCLE C2Amor;
  |FINSI;
|FPRC;


|| ***************************************
|PROCESO BorraAct;
   |BORRA 020#Referen4@.a; |LIBERA #Referen4@;
|FPRC;

|DEFBUCLE BorraAct;
#Referen4@#1003;
;
#Referen3@#0,#Referen3@#1,001;
#Referen3@#0,#Referen3@#1,999;
be;
NULL,BorraAct;
|FIN;

|PROCESO LineasAmort;
  #Referen4@#0 = #Referen2@#0;
  #Referen4@#1 = #Referen2@#1;
  #Referen4@#2 = #Referen2@#2;
  |LEE 101#Referen4@.=;
  |SI FSalida ! 0;
      Comodin = "|NC";
      Calc = #Referen2@#Comodin# - 1;
      |PARA nCont = 0; |SI nCont [ Calc; |HACIENDO nCont = nCont + 1;
            #Referen4@#nCont# = #Referen2@#nCont#;
      |SIGUE;
      |SI aMonedaC ! aMonedaA;
         impMoneda = #603#5; |HAZ ElCambioMoneda; #603#5 = impMoneda;
         impMoneda = #603#9; |HAZ ElCambioMoneda; #603#9 = impMoneda;
      |FINSI;

      |GRABA 020#Referen4@.b;
   |FINSI;
   |LIBERA #Referen4@;
|FPRC;

|DEFBUCLE LineasAmort;
 #Referen2@#1003;
 ;
 #Referen1@#0,#Referen1@#1,001;
 #Referen1@#0,#Referen1@#1,999;
 be;
 NULL,LineasAmort;
|FIN;

|PROCESO Amortiz;

  #Referen3@#0 = #Referen1@#0;
  #Referen3@#1 = #Referen1@#1;
  |LEE 101#Referen3@.=;
  |SI FSalida ! 0;
      Comodin = "|NC";
      Calc = #Referen1@#Comodin# - 1;
      |PARA nCont = 0; |SI nCont [ Calc; |HACIENDO nCont = nCont + 1;
            #Referen3@#nCont# = #Referen1@#nCont#;
      |SIGUE;
      |SI aMonedaC ! aMonedaA;
         impMoneda = #602#7;  |HAZ ElCambioMoneda; #602#7= impMoneda;
         impMoneda = #602#12; |HAZ ElCambioMoneda; #602#12= impMoneda;
         impMoneda = #602#13; |HAZ ElCambioMoneda; #602#13= impMoneda;
         impMoneda = #602#18; |HAZ ElCambioMoneda; #602#18= impMoneda;
         impMoneda = #602#19; |HAZ ElCambioMoneda; #602#19= impMoneda;
      |FINSI;
      eaCuenta = #Referen1@#3; eaCta2008 = #Referen1@#3;
      enNivel  = #Referen1@#4;
      |SI eSwC2008 = 1; |HAZ CambioCta08; |FINSI;
      #Referen3@#3 = eaCta2008;

      |GRABA 020#Referen3@.b;
   |FINSI;

   |BUCLE LineasAmort;

   |LIBERA #Referen3@;
|FPRC;

|DEFBUCLE Amortiz;
 #Referen1@#1002;
 ;
 "        ",00;
 "zzzzzzzz",99;
 be;
 NULL,Amortiz;
|FIN;

|PROCESO CopiaAmort;
   Comodin = fich_copi; |QBF Comodin;
   Comodin1 = Comodin % -107;
   Comodin = Comodin - Comodin1;  || Le quitamos el directorio de empresa
   Comodin = Comodin - "fich/";
   Comodin1 = Comodin + "def/camocabe.mas";
   Comodin2 = Comodin + "def/camoline.mas";

   |CARGA_DEF Comodin1, Referen1@;
   |SI FSalida = 0;
       |PATH_DAT #600 fich_copi;
       |CARGA_DEF Comodin2, Referen2@;
       |SI FSalida = 0;
           |PATH_DAT #601 fich_copi;
           |CARGA_DEF Comodin1, Referen3@;
           |SI FSalida = 0;
               |PATH_DAT #602 fich_alta;
               |CARGA_DEF Comodin2, Referen4@;
               |SI FSalida = 0;
                   |PATH_DAT #603 fich_alta;
                   |ABRE #602;
                   |ABRE #603;
                   |BUCLE Amortiz;
                   |CIERRA #603;
                   |CIERRA #602;
                   |DESCARGA_DEF #Referen4@;
               |FINSI;
               |DESCARGA_DEF #Referen3@;
           |FINSI;
           |DESCARGA_DEF #Referen2@;
       |FINSI;
       |DESCARGA_DEF #Referen1@;
   |FINSI;
|FPRC;


|| ***************************************************************
|PROCESO TesteoC;
  alfa1 = #cacuenta#0; |QBF alfa1;

  |SI alfa1 = "";
      |SI #cacuenta#53 ! 0; nMoviB = 1; |FINSI;
  |FINSI;

  |SI #cacuenta#3 ! "S"; |ACABA; |FINSI;

  alfa1 = alfa1 % 101;
  |SI alfa1 = "6"; |O alfa1 = "7";
      |SI #cacuenta#53 ! 0; nMoviE = 1; |FINSI;
  |FINSI;
  |SI alfa1 = "8"; |O alfa1 = "9";
      |SI #cacuenta#53 ! 0; nMoviI = 1; |FINSI;
  |FINSI;

  |SI #cacuenta#53 < 0;
      nDebe  = (nDebe - #cacuenta#53) ! EURODB;
      |SI alfa1 ! "6"; |Y alfa1 ! "7"; |Y alfa1 ! "8"; |Y alfa1 ! "9"; |Y alfa1 ! ""; nMovim = 1; |FINSI;
  |FINSI;
  |SI #cacuenta#53 > 0;
      nHaber = (nHaber + #cacuenta#53) ! EURODB;
      |SI alfa1 ! "6"; |Y alfa1 ! "7"; |Y alfa1 ! "8"; |Y alfa1 ! "9"; |Y alfa1 ! ""; nMovim = 1; |FINSI;
  |FINSI;
|FPRC;

|DEFBUCLE TesteoC;
   #6#1;
   ;
   desde,0;
   hasta,9;
   e;
   NULL, TesteoC;
|FIN;

|PROCESO Max65;
 #65#0 = 1;
|FPRC;

|PROCESO Min65;
 #65#0 = 999999;
|FPRC;

|PROCESO TesteoCierre;
  aFichero = "w" + Usuario;
  |NOME_DAT #65 aFichero;
  |DELINDEX #65;
  |C_P #65#0, 1, 1110;
  |C_P #65#1, 1, 1117;
  |C_A #65#1, 1, 53;
  |ABRE #65;
  nNumero = 0;
  nDebe   = 0;
  nHaber  = 0;
  nMovim  = 0;
  nMoviE  = 0;
  nMoviB  = 0;
  nMoviI  = 0;
  |BUCLE TesteoC;
  |SI nMoviI ! 0;     nError = 8; |HAZ Error_Graba; |FINSI;
  |SI nMoviE ! 0;     nError = 1; |HAZ Error_Graba; |FINSI;
  |SI nMovim = 0;     nError = 2; |HAZ Error_Graba; |FINSI;
  |SI nMoviB ! 0;     nError = 3; |HAZ Error_Graba; |FINSI;
  |SI nDebe ! nHaber; nError = 4; |HAZ Error_Graba; |FINSI;
  |CIERRA #65;
  |SI nError ! 0;
      |HAZ ErrorCierre;
  |FINSI;
|FPRC;

|PROCESO Error_Graba;
  aAlfa1 = "";
  nNumero = nNumero + 1;
  #65#0 = nNumero;
  |SI nError = 1; aAlfa1= "HAY CUENTAS DE LOS GRUPOS 6 Y 7 CON SALDO"; |FINSI;
  |SI nError = 2; aAlfa1= "NO EXISTE MOVIMIENTOS DE LAS CUENTAS     "; |FINSI;
  |SI nError = 3; aAlfa1= "LA CUENTA CONTABLE EN BLANCO TIENE SALDO "; |FINSI;
  |SI nError = 4; aAlfa1= "EXISTE UN DESCUADRE EN EL BALANCE DE SUMAS Y SALDOS "; |FINSI;
  |SI nError = 5; aAlfa1= "NO EXISTE CUENTA DE PERDIDAS Y GANANCIAS (1290) "; |FINSI;
  |SI nError = 6; aAlfa1= "NO EXISTE DESCARGA DE EXISTENCIAS        "; |FINSI;
  |SI nError = 7; aAlfa1= "EN BLANCO CTA PERDIDAS Y GANANCIAS EN DESC.EXISTENCIAS"; |FINSI;
  |SI nError = 8; aAlfa1= "HAY CUENTAS DE LOS GRUPOS 8 Y 9 CON SALDO"; |FINSI;
  #65#1 = aAlfa1;
  |GRABA 020#65n;
|FPRC;

|PROCESO ErrorCierre;
      |SI nNumero = 1; |Y nError = 4;
          |CONFI "    NHay un descuadre en el Balance de Sumas y Saldos. Desea continuar. ";
          |SI FSalida = 0; nError = 0; |FINSI;
          |ACABA;
      |FINSI;

      |PUSHV 0809 2270;
      |PINTA 1110, "                                                             ";
      |PINTA 1210, "                                                             ";
      |PINTA 1310, "                                                             ";
      |PINTA 1410, "                                                             ";
      |PINTA 1510, "                                                             ";
      |ATRI R;
      |PINTA 0910, " ATENCION: NO SE VA A REALIZAR EL CIERRE, HAY ERRORES        ";
      |PINTA 1010, " ==========================================================  ";
      |ATRI S;
      |PINTA 1610, " Revise el Asiento de Explotacion y compruebe que no existe  ";
      |PINTA 1710, " otro asiento de Cierre. Para buscar el problema realice:    ";
      |PINTA 1810, " a) Testear Plan Contable                                    ";
      |PINTA 1910, " b) Chequeo General de Asientos                              ";
      |PINTA 2010, " c) Recuperacion Total Asientos                              ";
      |PINTA 2110, " d) Recuperacion de Saldos                                   ";
      |ATRI 0;
      |CUADRO 0809 2270;
      |ENTLINEAL #1#65, -1, 7, 15, 0, Max65, Min65;
      |PAUSA;
      |PAUSA;
      |PAUSA;
      |POPV;
|FPRC;
