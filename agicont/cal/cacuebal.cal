|FICHEROS;
    cacuebal #0;         || cuentas balance
    caagrupa #1;         || agrupaciones
    caepigra #2;         || epigrafes
    caparbal #3;         || partidas balance
    caparexp #4;         || partidas explotacion
|FIN;

|VARIABLES;
    &entrada     = 1;
    uno          = "";
    nNume1       = 0;
    aAlfa        = "";
    nModo        = "";
    &enEjercicio = 0;
|FIN;

|PROCESO Prim_bal;  |TIPO 8;
   nNume1 = #48#26;
   |SI nNume1 < 80;
       enEjercicio = 2000 + nNume1;
   |SINO;
       enEjercicio = 1900 + nNume1;
   |FINSI;
|FPRC;

|PROCESO Antestipo; |TIPO 7;
  aAlfa = "    Entre: (A) Activo; (P) Pasivo; (D) Debe; (H) Haber";
  |SI enEjercicio ] 2008;
      aAlfa = "    Entre: (A)Activo; (P) Pasivo; (D) Debe; (H) Haber; (I) Ingreso; (G) Gasto";
  |FINSI;
  |MENSA aAlfa;
|FPRC;

|PROCESO Antesbloque; |TIPO 7;
  aAlfa = "    Entre: (0) para Activo y Pasivo; (1-4) para Debe y Haber ";
  |SI enEjercicio ] 2008;
      aAlfa = "   (0) para Activo y Pasivo; (1-5) para Debe y Haber; (6) Ingreso y Gasto";
  |FINSI;
  |MENSA aAlfa;
|FPRC;

|PROCESO bloque;
   uno = #0#0 % A101;
   |SI uno = "6"; |O uno = "7";
       |SI #0#1 < 1; |O #0#1 > 5;
           |ERROR;
       |FINSI;
       |SI enEjercicio < 2008;
           |SI #0#1 = 1;  |PINTA 1324, "RESULTADOS EXPLOTACION    "; |FINSI;
           |SI #0#1 = 2;  |PINTA 1324, "RESULTADOS FINANCIEROS    "; |FINSI;
           |SI #0#1 = 3;  |PINTA 1324, "RESULTADOS EXTRAORDINARIOS"; |FINSI;
           |SI #0#1 = 4;  |PINTA 1324, "IMPUESTOS                 "; |FINSI;
       |FINSI;
   |SINO;
       |SI enEjercicio < 2008;
           |SI #0#1 ! 0;
               |ERROR;
           |FINSI;
       |SINO;
           |SI uno = "8"; |O uno = "9";
               |SI #0#1 ! 6; |ERROR; |FINSI;
           |SINO;
               |SI #0#1 ! 0; |ERROR; |FINSI;
           |FINSI;
       |FINSI;
    |FINSI;
|FPRC;

|PROCESO tipo;
   uno = #0#0 % A101;
   |SI uno = "6"; |O uno = "7";
      |SI #0#2 ! "D"; |Y #0#2 ! "H";
         |AVISO;
         |ERROR;
      |FINSI;
      |SI #0#2 = "D";  |PINTA 1424, "DEBE    ";  |FINSI;
      |SI #0#2 = "H";  |PINTA 1424, "HABER   "; |FINSI;
   |SINO;
      |SI uno ] "0"; |Y uno [ "5";
          |SI #0#2 ! "A"; |Y #0#2 ! "P";
              |AVISO;
              |ERROR;
          |FINSI;
          |SI #0#2 = "A";  |PINTA 1424, "ACTIVO  "; |FINSI;
          |SI #0#2 = "P";  |PINTA 1424, "PASIVO  "; |FINSI;
      |SINO;
          |SI enEjercicio < 2008;
              |ERROR;
          |SINO;
              |SI #0#2 = "I";  |PINTA 1424, "INGRESOS"; |FINSI;
              |SI #0#2 = "G";  |PINTA 1424, "GASTOS  "; |FINSI;
          |FINSI;
      |FINSI;
   |FINSI;
|FPRC;

|PROCESO agrupa;
   |ABRE #1;
   #1#0 = #0#2;
   #1#1 = #0#3;
   |LEE 001#1=;
   |SI FSalida = 0;
       |PINTA 1524, #1#2;
   |SINO;
       |ERROR;
   |FINSI;
   |CIERRA #1;
|FPRC;

|PROCESO epigra;
    |ABRE #2;
    #2#0 = #0#2
    #2#1 = #0#3;
    #2#3 = #0#4;
    |LEE 001#2=;
    |SI FSalida = 0;
        |PINTA 1624, #2#4;
    |SINO;
        |ERROR;
    |FINSI;
    |CIERRA #2;
|FPRC;

|PROCESO partida;
    uno = #0#0 % A101;
    |SI uno = "6"; |O uno = "7";
        |ABRE #4;
        #4#0 = #0#1;
        #4#1 = #0#2;
        #4#2 = #0#3;
        #4#4 = #0#4;
        #4#6 = #0#5;
        |LEE 001#4=;
        |SI FSalida = 0;
            |PINTA 1724, #4#7;
        |SINO;
            |AVISO;
            |ERROR;
        |FINSI;
        |CIERRA #4;
     |FINSI;
     |SI uno ] "0"; |Y uno [ "5";
        |ABRE #3;
        #3#0 = #0#2;
        #3#1 = #0#3;
        #3#3 = #0#4;
        #3#5 = #0#5;
        |LEE 001#3=;
        |SI FSalida = 0;
            |PINTA 1724, #3#6;
        |SINO;
            |AVISO;
            |ERROR;
        |FINSI;
        |CIERRA #3;
     |FINSI;
|FPRC;

|PROCESO pintas; |TIPO 13;

   nModo = (FEntrada / 100) ! 0;
   |SI nModo < 1; |ACABA; |FINSI;

   uno = #0#0; |QBF uno; |SI uno = ""; |ACABA; |FINSI;

   |DDEFECTO #caagrupa; |PINTA 1524, #caagrupa#2;
   |DDEFECTO #caepigra; |PINTA 1624, #caepigra#4;
   |DDEFECTO #caparbal; |PINTA 1724, #caparbal#6;

   |HAZ tipo;
   |HAZ agrupa;
   |HAZ epigra;
   |HAZ partida;
|FPRC;
