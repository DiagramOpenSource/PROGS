|FICHEROS;
  ca8comay #0;
  ca8cumay #1;
  ca8y0000 #10;
|FIN;

|VARIABLES;
 &nNumPlan = 0;
 nNume1    = 0;
 aAlfa1    = "";
 aAlfa2    = "";
 sw        = 0;
 informe   = "ca8y0000";
 aDesde    = "";
 aHasta    = "";
 &aTexto   = "";
|FIN;

|INCLUYE acceso_i;

|| ******************************************** Calculos Pantalla
|CALCULO AntesEjer; |TIPO 7;
   #10#9 = enEjerBal;
   |PINTA #10#9;
|FCAL;

|CALCULO descri;  |TIPO 6;
  |ABRE #0;
  #0#0 = #10#0;
  |LEE 001#0=;
  |SI FSalida ! 0;
      |MENAV "    No existe Plan Contable";
      |ERROR;
  |SINO;
      #10#1 = #0#1; |PINTA #10#1;
  |FINSI;
|FCAL;

|| /////////////////////// PROCESOS DE TRABAJO \\\\\\\\\\\\\\\\\\\\
|PROCESO LeoDefinicion;

 aAlfa1 = #1#0; |QBF aAlfa1;
 aAlfa2 = aAlfa1 % 0;
 nNume1 = aAlfa2;
 |SI nNume1 < #10#4; |O nNume1 > #10#5; |ACABA; |FINSI;

 |SI #10#6 ! "T";
     |SI #10#6 = "S";
         |SI #1#3 ! "A"; |Y #1#3 ! "P"; |ACABA; |FINSI;
     |FINSI;
     |SI #10#6 = "R";
         |SI #1#3 ! "D"; |Y #1#3 ! "B"; |ACABA; |FINSI;
     |FINSI;
     |SI #10#6 = "E";
         |SI #1#3 ! "I"; |Y #1#3 ! "G"; |ACABA; |FINSI;
     |FINSI;
 |FINSI;

 sw = 1;
 |PRINT;
|FPRC;

|DEFBUCLE LeoDefin_cod;
 #1#1;
 ;
 enEjerBal, nNumPlan, #10#2;
 enEjerBal, nNumPlan, #10#3;
 e;
 NULL, LeoDefinicion;
|FIN;

|DEFBUCLE LeoDefin_nom;
 #1#1;
 0;
 enEjerBal, nNumPlan, aDesde, #10#2;
 enEjerBal, nNumPlan, aHasta, #10#3;
 e;
 NULL, LeoDefinicion;
|FIN;


|PROCESO Tipo3;  |TIPO 3;

 nNumPlan = #10#0;
 aDesde   = " " * 70;
 aHasta   = "z" * 70;
 aTexto   = "TODAS LAS CUENTAS ";
 |SI #10#6 = "S"; aTexto = "CUENTAS DE ACTIVO Y PASIVO"; |FINSI;
 |SI #10#6 = "R"; aTexto = "CUENTAS DE RESULTADO "; |FINSI;
 |SI #10#6 = "E"; aTexto = "CUENTAS DE ESTADO DEL PATRIMONIO NETO"; |FINSI;

 |IMPRE 0;
 |SI FSalida ! 0; |ACABA; |FINSI;

 |INFOR informe;
 |SI FSalida ! 0; |FINIMP; |ACABA; |FINSI;

 sw      = 0;
 |SI #10#7 = "C";
     |BUCLE LeoDefin_cod;
 |SINO;
     |BUCLE LeoDefin_nom;
 |FINSI;
 |SI sw = 1;
     |PIEINF;
 |FINSI;
 |FININF;
 |FINIMP;
|FPRC;
|| **************************************************************************
