|FICHEROS;
     cayc0001 #0;
     camancob #1;
     camanpag #11;
     caivarep #3;
     caivasop #13;

     caclip00 #2;
     cacuenta #5;

     eosclipr #12;
     agicontr #23;

     caw00008 #900;
|FIN;

|VARIABLES;
    aAlfa1 = "";
    aAlfa2 = "";
    agi    = "";
    &agi1  = "";
    &agi2  = "";
    &aste  = "";
    num    = 0;
    lonagi = "";
    salida = 0;

    sw         = 0;
    nCampo     = 0;
    long       = 0;
    informe    = "cayc0001";
    aNifQbf1   = "";
    aNifQbf2   = "";
    &eaVarDni  = "";
    &enCalcCif = 0;
    nInkey     = 0;

    aFichero   = "";
    novale     = 0;
    aNif       = "";
    aCuenta    = "";
    aNifCta    = "";
    aCP23      = "";
    nNivel     = 0;
    nImporte   = 0;
    nLiquida   = 0;
    aLiquida   = "";
    impor      = 0;
    aTipoIVA   = "";
    aNombre    = "";

    &nSwLinea  = 0;
    doce       = "            ";
|FIN;

|INCLUYE acceso_i;
|INCLUYE puntos_i;

||------------------------------------------------------------------------
|CALCULO Tipo8; |TIPO 8;
    |HAZ inicio;

    |DIRECTORIO agi; |QBF agi;
    |PATH_DAT #23 agi;
    |ABRE #23;
    #23#0 = 1;
    |LEE 040#23=;
    |SI FSalida ! 0;
        aste = "*";
        |CIERRA #23;
        |ACABA;
    |FINSI;
    |CIERRA #23;

    aste = #23#7;
    |QBF aste;
    |SI aste  = "*"; |ACABA; |FINSI;

    agi1   = #23#8; |QBF agi1;
    lonagi = agi1 % 0;
    num    = lonagi;
    num    = 100 + (num - 5);
    lonagi = num;
    agi2  = (agi1 % lonagi) + "pan/";
    |PATH_DAT #12 agi1;
|FCAL;

|CALCULO Tipo7; |TIPO 7;
    |SI aste ! "*";
        aAlfa1 = "    F1-Calcula Letra NIF, F2-Cons. cl/pr. Gral, F3-Cons. cl/pr., en blanco sel.ctas";
    |SINO;
        aAlfa1 = "    F1-Calcula Letra NIF, F3-Consulta Clien/Prov., en blanco sel. cuentas11111";
    |FINSI;
    |MENSA aAlfa1;
|FCAL;

|PROCESO conficha; |TIPO 6;
    |BLIN 4;
    nInkey = FSalida;

    |SI aste ! "*"; |Y nInkey = 10;
        |ABRE #12;
        |PUSHV 0501 2380;

        |PINPA #0#12;
        #12#0 = #0#0;
        |LEE 000#12];
        |CONSULTA #0#12;
        salida = FSalida;
        |CIERRA #12;
        |POPV;
        |SI salida = 0;
            #0#0 = #12#0;
            |PINTA #0#0;
        |SINO;
            |ERROR;
        |FINSI;
        |ACABA;
    |FINSI;

    |SI nInkey = 11;
        #2#9 = #0Campo;
        |ABRE #2;
        |CONSULTA_CLAVE #4#2;
        salida = FSalida;
        |CIERRA #2;
        |SI salida = 0;
            #0Campo = #2#9; |PINTA #0Campo;
        |SINO;
            |ERROR;
        |FINSI;
        |ACABA;
    |FINSI;

    |SI nInkey  = 9;
         eaVarDni   = #0Campo;
         enCalcCif  = 1;
         |HAZ CalculoDNI;
         #0Campo = eaVarDni;
         |PINTA #0Campo;
         |ERROR;
         |ACABA;
     |FINSI;

     eaVarDni  = #0Campo;
     enCalcCif = 0;
     |HAZ CalculoDNI;

     aNifQbf1 = #0Campo;   |QBF aNifQbf1;
     aNifQbf2 = eaVarDni;  |QBF aNifQbf2;

     |SI aNifQbf1 ! aNifQbf2; |Y aNifQbf1 ! "";
         |CONFI "0000SEl Nif puede ser Erroneo. Desea Calcularlo.";
         |SI FSalida = 0;
             #0Campo = eaVarDni;  |PINTA #0Campo;
             |ERROR;
         |FINSI;
     |FINSI;
|FPRC;

|CALCULO Tipo0C0; |TIPO 0;
     aAlfa1 = #0Campo; |QBF aAlfa1;
     aAlfa2 = "N";
     |SI aAlfa1 = "";
         aAlfa2 = "S";
     |FINSI;
     |SI aAlfa2 = "N";
         #0#1 = ""; |PINTA #0#1;
         #0#2 = ""; |PINTA #0#2;
         #0#3 = ""; |PINTA #0#3;
         #0#4 = ""; |PINTA #0#4;
         #0#5 = ""; |PINTA #0#5;
         #0#6 = ""; |PINTA #0#6;
     |FINSI;
     |C_M #0#1, 1, aAlfa2;
     |C_M #0#2, 1, aAlfa2;
     |C_M #0#3, 1, aAlfa2;
     |C_M #0#4, 1, aAlfa2;
     |C_M #0#5, 1, aAlfa2;
     |C_M #0#6, 1, aAlfa2;

     #0#31 = "";
     |SI aAlfa2 = "N";
         |ABRE #2;
         |SELECT #4#2;
         #2#9 = #0#0;
         |LEE 041#2=;
         |SI FSalida = 0;
             #0#31 = #2#1;
         |SINO;
             |SI aste ! "*";
                 |ABRE #12;
                 #12#0 = #0#0;
                 |LEE 041#12=;
                 |SI FSalida = 0;
                     #0#31 = #12#1;
                 |FINSI;
                 |CIERRA #12;
             |FINSI;
         |FINSI;
         |SELECT #1#2;
         |CIERRA #2;
     |FINSI;
     |PINTA #0#31;
|FCAL;

|CALCULO cuenta; |TIPO 6;
   |C_M #0Campo, 0, aAlfa1;
   |SI aAlfa1 = "N"; |ACABA; |FINSI;

   salidas = FSalida;
   p_alfa1 = #0Campo;
   |HAZ punto;
   |SI p_error ! 0;  |ERROR;  |ACABA;  |FINSI;

   aAlfa1 = #0Campo; |QBF aAlfa1;
   aAlfa1 = aAlfa1 % 0;
   long = aAlfa1;
   sw   = 0;
   nCampo = Campo + 6; #0nCampo = 0;
   |SI long = dig4; |Y dig4 ! 0; sw = 1; #0nCampo = 1; |FINSI;
   |SI long = dig5; |Y dig5 ! 0; sw = 1; #0nCampo = 2; |FINSI;
   |SI long = dig6; |Y dig6 ! 0; sw = 1; #0nCampo = 3; |FINSI;
   |SI long = dig7; |Y dig7 ! 0; sw = 1; #0nCampo = 4; |FINSI;
   |SI long = dig8; |Y dig8 ! 0; sw = 1; #0nCampo = 5; |FINSI;
   |SI long = dig9; |Y dig9 ! 0; sw = 1; #0nCampo = 6; |FINSI;

   |SI sw = 0;|Y long ! 0;
       |MENAV "    Longitud de Cuenta Fuera de Nivel !";
       |ERROR;
   |FINSI;
|FCAL;


|RUTINA inf5;
  #5#0 = "40          ";
  #5#1 = dig3;
|FRUT;

|RUTINA sup5;
  #5#0 = "44zzzzzzzzzz";
  #5#1 = dig3;
|FRUT;

|CALCULO CueCons; |TIPO 0;
  |C_M #0Campo, 0, aAlfa1;
  |SI aAlfa1 = "N"; |ACABA; |FINSI;

  |SI FSalida = 10;
     |ABRE #5;
     |CONSULTA_F_CLAVE #1#5, inf5, sup5;
     |CIERRA #5;
     #0Campo = #5#0; |PINTA #0Campo;
 |FINSI;
|FCAL;

|| ------------------------------------------------------------------------
|PROCESO leeiva;
     |SI aTipoIVA = "R";
         |DDEFECTO #3;
         #3#0  = #1#0;
         #3#2  = #1#1;
         #3#27 = #1#2;
         |LEE 001#3=;
     |SINO;
         |DDEFECTO #13;
         #13#0  = #11#0;
         #13#2  = #11#1;
         #13#27 = #11#2;
         |LEE 001#13=;
     |FINSI;
|FPRC;

|PROCESO verbueno;
   novale = 1;
   impor  = 0;
   |SI #0#17 = "T";
       impor = nImporte; || importe total cobro
   |FINSI;
   |SI #0#17 = "S";
       impor = nLiquida; || importe cobrado
       |SI impor = 0;
           novale = 0;
       |FINSI;
   |FINSI;
   |SI #0#17 = "N";
       impor = nImporte - nLiquida; || importe total - liquidado
       |SI impor  = 0; |O aLiquida = "S";
           novale = 0;
           impor  = 0;
       |FINSI;
   |FINSI;
|FPRC;

|PROCESO busca_cuen;
     aNombre = "";
     aNifCta = "";
     |DDEFECTO #2;
     #2#23 = aCP23;
     #2#10 = aCuenta;
     #2#11 = nNivel;
     |LEE 001#2=;
     |SI FSalida = 0;
         aNombre = #2#1;
         aNifCta = #2#9;
     |SINO;
         |DDEFECTO #5;
         #5#0 = aCuenta;
         #5#1 = nNivel;
         |LEE 001#5=;
         aNombre = #5#2;
    |FINSI;
    |QBF aNifCta;
|FPRC;

|PROCESO PrimeraSeleccion;
  aNifCta = "";
  aNombre = "";
  novale = 1;
  |SI aNif = "";   || seleccion por cuentas
      novale = 0;
      |SI #0#1 ! doce; |Y #0#1 = aCuenta; novale = 1; |FINSI;
      |SI #0#2 ! doce; |Y #0#2 = aCuenta; novale = 1; |FINSI;
      |SI #0#3 ! doce; |Y #0#3 = aCuenta; novale = 1; |FINSI;
      |SI #0#4 ! doce; |Y #0#4 = aCuenta; novale = 1; |FINSI;
      |SI #0#5 ! doce; |Y #0#5 = aCuenta; novale = 1; |FINSI;
      |SI #0#6 ! doce; |Y #0#6 = aCuenta; novale = 1; |FINSI;
  |FINSI;
  |SI novale = 0; |ACABA; |FINSI;

  |HAZ busca_cuen;
  |SI aNif ! ""; |Y aNif ! aNifCta; novale = 0; |FINSI;
|FPRC;

|PROCESO GrabaAux;
  |HAZ leeiva;
  #900#0  = aNifCta;
  #900#2  = aNombre;
  #900#7  = aCuenta;
  #900#10 = nImporte;
  #900#12 = nLiquida;

  |SI aTipoIVA = "R";
      #900#1  = #1#7;
      #900#3  = #1#0;
      #900#4  = #1#1;
      #900#5  = #1#2;
      #900#6  = #1#3;
      #900#8  = #3#7;
      #900#9  = #1#6;
      #900#11 = #1#9 + #1#10;
      #900#14 = #1#28;
  |SINO;
      #900#1  = #11#7;
      #900#3  = #11#0;
      #900#4  = #11#1;
      #900#5  = #11#2;
      #900#6  = #11#3;
      #900#8  = #13#7;
      #900#9  = #11#6;
      #900#11 = #11#9;
      #900#14 = "  ";

      #900#10 = -#900#10;
      #900#12 = -#900#12;
  |FINSI;

  #900#13 = #900#10 + #900#11 - #900#12;
  |SI aLiquida = "S"; #900#13 = 0; |FINSI;

  |GRABA 020#900n;
|FPRC;

|PROCESO lee_cobros;
  aTipoIVA = "R";
  aCP23   = "C";
  aCuenta = #1#4;
  nNivel  = #1#5;
  |HAZ PrimeraSeleccion;
  |SI novale = 0; |ACABA; |FINSI;

  nImporte = #1#8;
  nLiquida = #1#24;
  aLiquida = #1#23;
  |HAZ verbueno;
  |SI novale = 0; |ACABA; |FINSI;

  |HAZ GrabaAux;
|FPRC;

|DEFBUCLE lee_cobros;
  #1#1;
  7,28;
  00, "  ", 000001, 001, #0#13, #0#15;
  99, "zz", 999999, 999, #0#14, #0#16;
  e;
  NULL, lee_cobros;
|FIN;

|PROCESO lee_pagos;
  aTipoIVA = "S";
  aCP23   = "P";
  aCuenta = #11#4;
  nNivel  = #11#5;
  |HAZ PrimeraSeleccion;
  |SI novale = 0; |ACABA; |FINSI;

  nImporte = #11#8;
  nLiquida = #11#23;
  aLiquida = #11#22;
  |HAZ verbueno;
  |SI novale = 0; |ACABA; |FINSI;

  |HAZ GrabaAux;
|FPRC;

|DEFBUCLE lee_pagos;
  #11#1;
  7;
  00, "  ", 000001, 001, #0#13;
  99, "zz", 999999, 999, #0#14;
  e;
  NULL, lee_pagos;
|FIN;

|PROCESO LeeAuxiliar;
   |SI sw = 0;
      #0#20 = #900#0;
      #0#21 = #900#2;
      |PDEFECTO #0, 22,31;
      #0#26 = #900#1;
      sw = 1;
      nSwLinea = 0;
  |FINSI;

  |SI #900#0 ! #0#20;
      nSwLinea = 2; |PRINT;
      |PIEINF;
      #0#20 = #900#0;
      #0#21 = #900#2;
      |PDEFECTO #0, 22,31;
      #0#26 = #900#1;
      nSwLinea = 0;
  |FINSI;

  |SI #900#1 ! #0#26;
      nSwLinea = 2; |PRINT;
      #0#26 = #900#1;
      |PDEFECTO #0, 27,31;
      nSwLinea = 1;
  |FINSI;

  |PRINT;
  nSwLinea = 1;
  #0#22 = #0#22 + #900#10;
  #0#23 = #0#23 + #900#11;
  #0#24 = #0#24 + #900#12;
  #0#25 = #0#25 + #900#13;

  #0#27 = #0#27 + #900#10;
  #0#28 = #0#28 + #900#11;
  #0#29 = #0#29 + #900#12;
  #0#30 = #0#30 + #900#13;
|FPRC;

|DEFBUCLE LeeAuxiliar;
  #900#1;
  ;
  "               ", #0#13, 00, "  ", 000001, 001;
  "zzzzzzzzzzzzzzz", #0#14, 99, "zz", 999999, 999;
  ;
  NULL, LeeAuxiliar;
|FIN;

|| ------------------------------------------------------------------------
|CALCULO Tipo3; |TIPO 3;

     aFichero = ("1z" + Usuario) % 108;
     |NOME_DAT #900 aFichero;
     |DELINDEX #900;

     |IMPRE 0;
     |SI FSalida ! 0;
         |MENAV "    Error en Impresora";
         |ACABA;
     |FINSI;

     |INFOR informe;
     |SI FSalida ! 0;
         |MENAV "    Error en Informe";
         |FINIMP;
         |ACABA;
     |FINSI;

     aNif = #0#0; |QBF aNif;

     |ABRE #900;
     |ABRE #3;
     |ABRE #13;
     |ABRE #2;
     |ABRE #5;

     |BUCLE lee_cobros;
     |BUCLE lee_pagos;

     |CIERRA #5;
     |CIERRA #2;
     |CIERRA #13;
     |CIERRA #3;
     |CIERRA #900;

     sw       = 0;
     nSwLinea = 0;
     |BUCLE LeeAuxiliar;
     |SI sw = 1;
         nSwLinea = 2; |PRINT;
         |PIEINF;
     |FINSI;
     |FININF;
     |FINIMP;
|FCAL;

/*
|PROCESO imprim;
     |SI sw = 0;
          sw = 1;
          sw_inf = 0;
          |HAZ busca_cuen;
          |PRINT;
          ult_cta = #1#4;
     |FINSI;
     |SI ult_cta ! #1#4;
          sw_inf = 2;    ||Imprime el primer total
          |PRINT;
          sw_inf = 0;
          |HAZ busca_cuen;
          |PRINT;
          total = 0;
     |FINSI;
     sw_inf = 1;
     |PRINT;
     total = total + impor;
     total1 = total1 + impor;
     ult_cta = #1#4;
|FPRC;
