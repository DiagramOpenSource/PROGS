|FICHEROS;
  caexicon #0;
  caexistl #1;
  cacuenta #2;
|FIN;

|VARIABLES;
  &d_mes = 0;
  &h_mes = 0;
  &estru = 0;
  desmes = 0;
  hasmes = 0;
   &r_emp= 0;
   &r_per= 0;
   &r_eje= 0;
   &r_nom="";
   dir_fich = "";
   dirfich0 = "";
   alfa1 = "";
   alfa2 = "";
   aAlfa = "";
 &EURO = 1;
 &EURODB = 0;
 &EURODV = 0;
 &EUROTEXTO = "Cambio de Moneda";
 &swOperacion = 0;  || 0 = Lectura / 1 = Alta
 &enEmpresa = 0;
 &enPeriodo = 0;
 &eaMoneda  = "";
|FIN;


|PROCESO lee_existenci1;
  |DDEFECTO #0;
  #0#0 = #1#1;
  #0#1 = #1#2;
  #0#2 = #1#3;
  #0#3 = #1#4;
  #0#4 = #1#11;
  #0#5 = #1desmes;
  #0#6 = #1hasmes;
  |GRABA 020#0n;
|FPRC;

|DEFBUCLE lee_existencia;
 #1#1;
 ;
 estru,0;
 estru,99;
 be;
 NULL,lee_existenci1;
|FIN;

|PROCESO gra_existenci1;
  #0#0 = #1#1;
  |LIBERA #0;
  |LEE 140#0=;
  #1#11 = #0#4;
  #1desmes = #0#5;
  #1hasmes = #0#6;
  |GRABA 140#1a;
  |LIBERA #1;
|FPRC;

|DEFBUCLE gra_existencia;
 #1#1;
 ;
 estru,0;
 estru,99;
 be;
 NULL,gra_existenci1;
|FIN;

|PROCESO min;
  #0#0 = 1;
|FPRC;

|PROCESO max;
  #0#0 = 99;
|FPRC;

|PROCESO dirfich0; || calcular el directorio base de los ficheros
  |DBASE dir_fich;
  dirfich0 = dir_fich + "fich/";
  alfa1 = r_emp;
  alfa2 = r_per;
  alfa1 = "00000" + alfa1;   alfa1 = alfa1 % -105;
  alfa2 = "0"   + alfa2;   alfa2 = alfa2 % -101;
  dirfich0 = dirfich0 + alfa1 + alfa2 + "/";

  |PATH_DAT #0 dirfich0;
  |PATH_DAT #1 dirfich0;
  |PATH_DAT #2 dirfich0;

  enEmpresa = r_emp;
  enPeriodo = r_per;
  swOperacion = 0;
  |SUB_EJECUTA "camoneda";
|FPRC;

|PROGRAMA;
  |ATRI P;  |MENSA "    Cargando Opcion ...";  |ATRI 0;
  |HAZ dirfich0;
  |DELINDEX #0;
  |ABRE #0;
  desmes = d_mes + 11;
  hasmes = h_mes + 11;
  |BUCLE lee_existencia;
  |CIERRA #0;
  |BLIN 4;
  |PINPA #0#0;
  |ATRI I;
  aAlfa = ("00" + r_eje) % -102;
  |PINTA 0713, alfa1;
  |PINTA 0719, r_per;
  |PINTA 0721, r_nom;
  |PINTA 0774, aAlfa;
  |ATRI 0;
  |ENTLINEAL #1#0, 1, 4, 20, 0, min, max;
  |ABRE #0;
  |BUCLE gra_existencia;
  |CIERRA #0;
  |DELINDEX #0;
|FPRO;

|CALCULO ImpNegativo; |TIPO 0;
  |SI #1Campo < 0;
      |MENAV "    Error. Importe de Existencias en Negativo";
      |ERROR;
  |FINSI;
|FCAL;

|PROCESO esbaja;  |TIPO 11;
  |SI FSalida ! 9;  |ACABA;  |FINSI;
  |LIBERA #0;
  |LEE 140#0=;
|ET campo1;
  |ENCAMPO #5#0;
  |SI FSalida = 9;
      |ENCAMPO #4#0;
      |SI FSalida = 7;
         |HAZ repinta;
         |ACABA;
      |FINSI;
      |VETE campo1;
  |FINSI;
  |SI FSalida = 10;
      |HAZ planconta;
      |VETE campo1;
  |FINSI;
  |SI FSalida = 7;
      |HAZ repinta;
      |ACABA;
  |FINSI;
  |SI FSalida = 2;  |VETE campo1;  |FINSI;
  |ENCAMPO #6#0;
  |SI FSalida = 7;
      |HAZ repinta;
      |ACABA;
  |FINSI;
  |SI FSalida = 2;  |VETE campo1;  |FINSI;
  |GRABA 140#0a;
|FPRC;

|PROCESO repinta;
  |LIBERA #0;
  |LEE 140#0=;
  |PINTA #0#0;
  |PINTA #0#1;
  |PINTA #0#3;
  |PINTA #0#4;
  |PINTA #0#5;
  |PINTA #0#6;
|FPRC;

|PROCESO planconta;
  |ABRE #2;
  #2#0 = #0#1;
  #2#1 = #0#2;
  |LEE 140#2=;
  #1#11 = #2#9;
  #1#12 = #2#12;
  #1#13 = #2#15;
  #1#14 = #2#18;
  #1#15 = #2#21;
  #1#16 = #2#24;
  #1#17 = #2#27;
  #1#18 = #2#30;
  #1#19 = #2#33;
  #1#20 = #2#36;
  #1#21 = #2#39;
  #1#22 = #2#42;
  #1#23 = #2#45;
  #1#24 = #2#48;
  #0#4 = #1#11;
  #0#5 = #1desmes;
  #0#6 = #1hasmes;
  |PINTA #0#4;
  |PINTA #0#5;
  |PINTA #0#6;
  |LIBERA #2;
  |CIERRA #2;
|FPRC;

|PROCESO SinBarras; |TIPO 5;
  FSalida = "SINBARRA";
|FPRC;
