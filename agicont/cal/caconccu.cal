|FICHEROS;
   caconccu #0;
   camaasil #1;
   zconcili #2;
   caconcil #3;
   cacuenta #10;
|FIN;

|VARIABLES;
    &entrada = 1;
    cta       = 0;
    cta_t     = 0;
    dig_t     = 0;
    long      = "  ";
    sw        = 0;
    error     = 0;
    feini     = @;
    fefin     = @;
    aAlfa     = "";
    nDDiario  = 0;
    nHDiario  = 0;
    aDDepar   = "";
    aHDepar   = "";

    &contador = 0;
    &cuenta   = "";
    &digito   = 0;
    &saldoant = 0;
    &nombre   = "";
    &eaCam5   = "";
    &enCam6   = 0;
    &enCam7   = 0;
    &eaCam8   = "";
    &eaCam9   = "";
    &eaCam10  = "";
|FIN;

|INCLUYE acceso_i;
|INCLUYE puntos_i;
|INCLUYE i_ficher;

|PROCESO cuenta; |TIPO 6;
    salidas = FSalida;
    p_alfa1 = #0Campo;
    |HAZ punto;
    |SI p_error ! 0;  |ERROR;  |ACABA;  |FINSI;
    cta_t = Campo;
    dig_t = Campo + 1;
    |HAZ cuenta_t;
|FPRC;

|PROCESO cuenta_t;
    cta  = #0cta_t;
    long = cta % 0;
    sw   = 0;
    |SI long = dig4; sw = 1; #0dig_t = 1; |FINSI;
    |SI long = dig5; sw = 1; #0dig_t = 2; |FINSI;
    |SI long = dig6; sw = 1; #0dig_t = 3; |FINSI;
    |SI long = dig7; sw = 1; #0dig_t = 4; |FINSI;
    |SI long = dig8; sw = 1; #0dig_t = 5; |FINSI;
    |SI long = dig9; sw = 1; #0dig_t = 6; |FINSI;

    |SI sw = 0;
        #0dig_t = 0;
        |MENAV "    Longitud de cuenta fuera de Nivel !";
        |ERROR;
    |FINSI;
|FPRC;

|PROCESO cuefin; |TIPO 0;
    error = 0;
    |ABRE #10;
    #10#0 = #0cta_t;
    #10#1 = #0dig_t;
    |LEE 040#10=;
    |SI FSalida ! 0;
        error = 1;
        |MENAV "      No existe la cuenta";
        |CIERRA #10;
        |ERROR;
        |ACABA;
    |FINSI;
    |SI #10#3 = "N";
        error = 1;
        |CIERRA #10;
        |MENAV "      La cuenta no tiene pase DIRECTO !";
        |ERROR;
        |ACABA;
    |FINSI;
    #0#4 = #10#2;
    |PINTA #0#4;
    |CIERRA #10;
|FPRC;

|CALCULO PorDiario;
    aAlfa = #caconccu#5;
    |C_M #caconccu#6,1,aAlfa;
    |C_M #caconccu#7,1,aAlfa;
    |SI aAlfa = "N";
       #caconccu#6 = 0;  |PINTA #caconccu#6;
       #caconccu#7 = 99; |PINTA #caconccu#7;
    |FINSI;
|FCAL;

|PROCESO no_dep; |TIPO 7;
    aAlfa = #canempre#24;
    |C_M #caconccu#8,  1, aAlfa;
    |C_M #caconccu#9,  1, aAlfa;
    |C_M #caconccu#10, 1, aAlfa;
|FPRC;

|CALCULO PorDepartamento;
    |C_M #caconccu#8,0,aAlfa;
    |SI aAlfa = "N"; |ACABA; |FINSI;

    aAlfa = #caconccu#8;
    |C_M #caconccu#9, 1,aAlfa;
    |C_M #caconccu#10,1,aAlfa;
    |SI aAlfa = "N";
       #caconccu#9  = "  ";  |PINTA #caconccu#9;
       #caconccu#10 = "zz";  |PINTA #caconccu#10;
    |FINSI;
|FCAL;
|| /////////////////////////////////////////////////////////////////

|PROCESO calcuant;
    |SI #1#8 = "D";
        #0#2 = #0#2 + #1#9;
    |SINO;
        #0#2 = #0#2 - #1#9;
    |FINSI;
|FPRC;

|DEFBUCLE camaasil0;
    #1#3;
    18, 0, 21;
    #0#0, #0#1, feini, 000000, "û", nDDiario, aDDepar;
    #0#0, #0#1, fefin, 999999, "û", nHDiario, aHDepar;
    e;
    NULL, calcuant;
|FIN;

|PROCESO indexa;
    contador = contador + 1;
    #2#0 = contador;
    #2#1 = #1#0;
    #2#2 = #1#1;
    #2#3 = #1#2;
    #2#4 = #1#3;
    #2#5 = #1#7;
    |SI #1#8 = "D";
        #2#6 = #1#9;
        #2#7 = 0;
    |SINO;
        #2#7 = #1#9;
        #2#6 = 0;
    |FINSI;
    #2#8 = #1#18;
    #2#9 = #1#21;

    |GRABA 040#2n;
|FPRC;

|DEFBUCLE camaasil1;
    #1#3;
    18, 0, 21;
    #0#0, #0#1, feini, 000000, " ", nDDiario, aDDepar;
    #0#0, #0#1, fefin, 999998, " ", nHDiario, aHDepar;
    e;
    NULL, indexa;
|FIN;

|PROCESO indexa1;
  contador = contador + 1;
  #2#0 = contador;
  #2#1 = 99;
  #2#2 = #3#4;
  #2#3 = 999999;
  #2#4 = #3#2;
  #2#5 = #3#5;
  #2#6 = #3#6;
  #2#7 = #3#7;
  #2#8 = #3#8;
  #2#9 = #3#9;

  |GRABA 040#2n;
|FPRC;

|DEFBUCLE caconcil;
   #3#1;
   3,8;
   #0#0, #0#1, 001, 2, " ";
   #0#0, #0#1, 999, 2, " ";
   e;
   NULL, indexa1;
|FIN;

|PROCESO grabaapun;
    |SI #2#1 = 99; |Y #2#3 = 999999;
       #3#0 = #0#0;
       #3#1 = #0#1;
       #3#2 = #2#4;
       |LIBERA #3;
       |LEE 110#3=;
       |SI FSalida = 0;
           |BORRA 020#3a;
       |FINSI;
    |SINO;
       #1#0 = #2#1;
       #1#1 = #2#2;
       #1#2 = #2#3;
       #1#3 = #2#4;
       |LIBERA #1;
       |LEE 110#1=;
       |SI FSalida = 0;
           #1#18 = "û";
           |GRABA 020#1a;
       |FINSI;
    |FINSI;
|FPRC;

|DEFBUCLE zconcili;
    #2#1;
    8;
    0001, "*";
    9999, "*";
    e;
    NULL, grabaapun;
|FIN;

|PROCESO concilia; |TIPO 3;

    |SI error = 1; |ACABA; |FINSI;

    nDDiario = 00;
    nHDiario = 99;
    |SI #caconccu#5 = "S";
        nDDiario = #caconccu#6;
        nHDiario = #caconccu#7;
    |FINSI;

    aDDepar  = "  ";
    aHDepar  = "zz";
    |SI #caconccu#8 = "S";
        aDDepar  = #caconccu#9;
        aHDepar  = #caconccu#10;
    |FINSI;

    eaCam5   = #caconccu#5;
    enCam6   = #caconccu#6;
    enCam7   = #caconccu#7;
    eaCam8   = #caconccu#8;
    eaCam9   = #caconccu#9;
    eaCam10  = #caconccu#10;

    comienza = "z";
    |HAZ fichero;
    |NOME_DAT #2 fichero;

    |ABRE #2;
    |CIERRA #2;
    |DELINDEX #2;

    |MENSA "2400 Procesando ......";

    feini = "01.01.1900";
    fefin = "31.12.2999";

    |BUCLE camaasil0;
    |PINTA #0#2;

    |ABRE #2;
    contador = 0;
    feini = "01.01.1900";
    fefin = "31.12.2999";
    |BUCLE camaasil1;
    |SI nDDiario [ 99; |Y nHDiario ] 99;
        |BUCLE caconcil;
    |FINSI;
    |CIERRA #2;

    |BLIN 24;
    cuenta   = #0#0;
    digito   = #0#1;
    saldoant = #0#2;
    nombre   = #0#4;

    |SI contador = 0;
        |MENAV "      No hay nada para Conciliar";
        |DELINDEX #2;
        |ACABA;
    |FINSI;

    |SUB_EJECUTA_NP "zconcili";

    |CONFI "2400SActualizar Conciliacion : ";
    |SI FSalida = 0;
        |ABRE #1;
        |ABRE #3;
        |BUCLE zconcili;
        |CIERRA #1;
        |CIERRA #3;
    |FINSI;
    |DELINDEX #2;
|FPRC;
