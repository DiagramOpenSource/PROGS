|FICHEROS;
   caremesc #0;
   caremesl #1;
   camanban #2;
   camancob #3;
   caremesi #4;
   caclipro #5;
   cacuenta #10;

   caconcyp #210;
|FIN;

|VARIABLES;
    &entrada = 1;
    pepe      = 0;
    linea     = 0;
    antlinea  = 0;
    suma      = 0;
    panta     = 0;
    cont      = 0;
    cta       = 0;
    cta_t     = 0;
    dig_t     = 0;
    long      = "  ";
    sw        = 0;
    error     = 0;
    diasrem   = @;
    &contador = 0;
    &banco    = 0;
    feini     = @;
    feinv     = @;
    fefin     = @;
    nnt       = 0;
    tot       = 0;
    sw_n      = 0;
    aSiVto    = "";
|FIN;

|INCLUYE acces3_i;
|INCLUYE punto2_i;
|INCLUYE i_ficher;

|| *************************************************************************
|| ***************  PROCESO DE CABECERA ************************************
|| *************************************************************************

|CALCULO contador; |TIPO 7;
  cont = (FEntrada / 100) ! 0;
  |SI cont = 1;
      |LEE 040#0u;
      |SI FSalida = 0;
          pepe = #0#0;
          |DDEFECTO #0;
          #0#0 = pepe + 1;
      |SINO;
          |DDEFECTO #0;
          #0#0 = 1;
      |FINSI;
  |FINSI;
  |ABRE #210;
  |DDEFECTO #210;
  |LEE 001#210p;
  aSiVto = #210#12;
  |CIERRA #210;
|FCAL;

|CALCULO siconta; |TIPO 7;
    |SI #0#9 = "S";
        |CONFI "2400NRemesa ya contabilizada, desea continuar : ";
        |SI FSalida = 0;
           |MENAV "     Las variaciones realizadas no tendran efectos sobre el asiento.";
        |SINO;
           |ERROR;
           |PTEC 807;
        |FINSI;
    |FINSI;
|FCAL;

|CALCULO cuenta1; |TIPO 6;
    p_alfa1 = #0Campo;
    salidas = FSalida;
    |HAZ punto2;
    |SI p_error ! 0;  |ERROR;  |ACABA;  |FINSI;
    cta_t = Campo;
    dig_t = Campo + 1;
    |HAZ cuenta1_t;
|FCAL;

|PROCESO cuenta1_t;
   cta  = #0cta_t;
   long = cta % 0;
   sw   = 0;

   |SI long = dig4; sw = 1; #0dig_t = 1; |FINSI;
   |SI long = dig5; sw = 1; #0dig_t = 2; |FINSI;
   |SI long = dig6; sw = 1; #0dig_t = 3; |FINSI;
   |SI long = dig7; sw = 1; #0dig_t = 4; |FINSI;
   |SI long = dig8; sw = 1; #0dig_t = 5; |FINSI;
   |SI long = dig9; sw = 1; #0dig_t = 6; |FINSI;

   |SI sw = 0;
       #0dig_t = 0;
       |MENAV "    Longitud de cuenta fuera de Nivel !";
       |ERROR;
   |FINSI;
|FPRC;

|CALCULO cuefin1; |TIPO 0;
   error = 0;
   |ABRE #10;
   #10#0 = #0cta_t;
   #10#1 = #0dig_t;
   |LEE 040#10=;
   |SI FSalida ! 0; error = 1; |CIERRA #10; |ERROR; |ACABA; |FINSI;
   |SI #10#3 = "N";
       error = 1;
       |MENAV "      La cuenta no tiene pase DIRECTO !";
       |ERROR;
   |FINSI;
   |CIERRA #10;
|FCAL;

|PROCESO grabali;
    linea = linea + 1;
    #1#0 = #0#0;
    #1#1 = linea;
    #1#2 = #4#1;
    #1#3 = #4#2;
    #1#4 = #4#8;
    #1#5 = #4#3;
    #1#6 = #4#4;
    #1#7 = #4#5;
    #1#11 = #4#10;
    #1#12 = #4#11;
    #1#13 = #4#13;
    #1#14 = #4#14;
    |HAZ calculo;
    |GRABA 020#1n;
    sw_n = 1;
|FPRC;

|DEFBUCLE caremesi;
  #4#1;
  7;
  000000,   "*";
  contador, "*";
  e;
  NULL, grabali;
|FIN;

|PROCESO linealin;
  linea    = #1#1;
  antlinea = #1#1;
|FPRC;

|PROCESO introduc;
  linea    = 0;
  antlinea = 0;
  |BUCLELIN 0linealin#0;

  |ABRE #1;
  |BUCLE caremesi;
  |CIERRA #1;
|FPRC;

|PROCESO pasalo;
  contador = contador + 1;
  #4#0  = contador;
  #4#1  = #3#0;
  #4#2  = #3#1;
  #4#3  = #3#3;
  #4#4  = #3#7;
  #4#5  = #3#8 - #3#24;
  #4#6  = #3#11;
  #4#7  = "";
  #4#8  = #3#2;   || serie
  #4#9  = #3#6;
  #4#10 = #3#4
  #4#11 = #3#5
  #4#12 = #3#8

  |ABRE #5;
  #5#23 = "C";
  #5#10 = #3#4;
  #5#11 = #3#5;
  |LEE 040#5=;
  |SI FSalida ! 0; |DDEFECTO #5; |FINSI;
  #4#13 = #5#1;
  #4#14 = #5#6;
  |CIERRA #5;

  |GRABA 040#4n;
|FPRC;

|DEFBUCLE camancob;
  #3#3;
  11, 12, 23;
  feinv, feini, #0#7, 0, "N";
  fefin, fefin, #0#7, 0, "N";
  e;
  NULL, pasalo;
|FIN;

|CALCULO automat; |TIPO 0;
  |SI FSalida = 2; |ACABA; |FINSI;

  |CONFI "2400SRemesas Automaticas : ";
  |SI FSalida ! 0; |ACABA; |FINSI;
  sw_n = 0;
  contador = 0;
  comienza = "q"; |HAZ fichero;
  |NOME_DAT #4 fichero;

  |ABRE #4; |CIERRA #4; |DELINDEX #4;

  feini = "01.01.0001";
  feinv = #0#1;
  fefin = "31.12.2099";
  |SI aSiVto = "S"; feinv = feini; |FINSI;

  |ABRE #4;
  |BUCLE camancob;
  |CIERRA #4;
  |SI contador  ! 0;
      banco  = #0#2;
      |SUB_EJECUTA_NP "caremesi";
      |HAZ introduc;
      |SI sw_n = 1;
          |PTEC 815;
          |PTEC 808;
          |PTEC 815;
          |PTEC 808;
      |FINSI;
  |SINO;
      |MENAV "      No hay cobros a remesar ";
      |ERROR;
  |FINSI;
  |DELINDEX #4;
|FCAL;

|| ---------------------------------------------------------------------
|| ----------DESDE LINEAS ----------------------------------------------
|| ---------------------------------------------------------------------

|CALCULO eltipo5; |TIPO 5;
  FSalida = "SINBARRA";
|FCAL;

|CALCULO actuali; |TIPO 2;
   panta = 0 +.1;

   |ABRE #3;
   |LIBERA #3;
   |DDEFECTO #3;
   #3#0 = #1#2;
   #3#1 = #1#3;
   #3#2 = #1#4;
   #3#3 = #1#5;
   |LEE 110#3=;
   |SI FSalida = 0;
       |SI panta = 1;
           #3#12 = #1#0;
           #3#23 = "S";
       |SINO;
           #3#12 = 0;
           #3#23 = "N";
       |FINSI;
       #3#24 = #3#24 +. #1#7;
       #3#9  = #3#9  +. (#1#10 + #0#6);
       |GRABA 020#3a;
   |FINSI;
   |CIERRA #3;

   |ABRE #2;
   |LIBERA #2;
   #2#0 = #0#2;
   |LEE 110#2=;
   |SI FSalida = 0;
       |SI panta ! 1;
          |SI #1#15 = "N";
              #2#13  = #2#13 - #1#7;
          |FINSI;
       |SINO;
          #2#13  = #2#13 + #1#7;
       |FINSI;
       |GRABA 020#2a;
   |FINSI;
   |CIERRA #2;

   #0#5  = #0#5  +. 1;
   #0#10 = #0#10 +. #1#7;
   #0#11 = #0#11 +. #0#6;
   #0#12 = #0#12 +. #1#10;
   |PINTA #0#5;
   |PINTA #0#10;
   |PINTA #0#11;
   |PINTA #0#12;
|FCAL;


|PROCESO remesado; |TIPO 6;

    |ABRE #3;
    #3#0 = #1#2;
    #3#1 = #1#3;
    #3#2 = #1#4;
    #3#3 = #1#5;
    |LEE 040#3=;
    |SI FSalida = 0;
        |SI #0#7 ! #3#11;
            |CIERRA #3;
            |MENAV "     El tipo recibo del Cobro no coincide con el de la remesa";
            |ERROR;
            |ACABA;
        |FINSI;
        |SI #3#12 ! 0;
            |CIERRA #3;
            |MENAV "     Cobro ya remasado !!! ";
            |ERROR;
            |ACABA;
        |FINSI;
        |SI #3#23 = "S";
            |CIERRA #3;
            |MENAV "     Cobro ya liquidado !!! ";
            |ERROR;
            |ACABA;
        |FINSI;
    |SINO;
        |CIERRA #3;
        |MENAV "     No existe cobro !!! ";
        |ACABA;
    |FINSI;
    |CIERRA #3;

    #1#6 = #3#7;          |PINTA #1#6;
    #1#7 = #3#8 - #3#24;  |PINTA #1#7;

    #1#11 = #3#4;   || los defectos, cuando entras con F11 y validas no se
    #1#12 = #3#5;   ||/cargan!!!
    |PINTA #1#11;
    |PINTA #1#12;
    |ABRE #5;
    #5#23 = "C";
    #5#10 = #1#11;
    #5#11 = #1#12;
    |LEE 040#5=;
    |SI FSalida ! 0; |DDEFECTO #5; |FINSI;

    #1#13 = #5#1; |PINTA #1#13;
    #1#14 = #5#6; |PINTA #1#14;
    |CIERRA #5;

    |ABRE #2;
    #2#0 = #0#2;
    |LEE 040#2=;
    |SI FSalida = 0;
        suma = #2#13 + (#3#8 - #3#24);
        |SI #2#12 < suma;
            |MENAV "     Hemos sobrepasado el riesgo concedido !!! ";
        |FINSI;
    |FINSI;
    |CIERRA #2;
|FPRC;

|PROCESO diasrem; |TIPO 0;
   |SI aSiVto ! "S";
       |SI #1#6 < #0#1;
           |MENAV "     Recibo vencido, no se puede remesar. ";
           |ERROR;
          |ACABA;
       |FINSI;
   |FINSI;
   |HAZ liqu;
   |PINTA #1#7;
   #1#8 = #1#6 - #0#1;
   |SI #1#8 < 0; #1#8 = 0; |FINSI;
   |PINTA #1#8;
|FPRC;

|PROCESO interes; |TIPO 0;
   |ABRE #2;
   #2#0 = #0#2;
   |LEE 040#2=;
   |SI FSalida ! 0; |DDEFECTO #2; |FINSI;
   |CIERRA #2;

   |SI #1#8 ] #2#20; |Y #1#8 [ #2#24;
       #1#9 = #2#28;
   |FINSI;

   |SI #1#8 ] #2#21; |Y #1#8 [ #2#25;
       #1#9 = #2#29;
   |FINSI;

   |SI #1#8 ] #2#22; |Y #1#8 [ #2#26;
       #1#9 = #2#30;
   |FINSI;

   |SI #1#8 ] #2#23; |Y #1#8 [ #2#27;
       #1#9 = #2#31;
   |FINSI;

   #1#10 = 0;
   |SI #1#9 ! 0;
       tot = #1#7 % #1#9;
       #1#10 = (tot * #1#8 ) / 365;
   |FINSI;

   |PINTA #1#9;
   |PINTA #1#10;
|FPRC;

|| ----------------------------------------------------------------------

|PROCESO calculo;

 #1#8 = #1#6 - #0#1; |SI #1#8 < 0; #1#8 = 0; |FINSI;

 |ABRE #2;
 |LIBERA #2;
 #2#0 = #0#2;
 |LEE 110#2=;
 |SI FSalida ! 0;
     |DDEFECTO #2;
     #2#0 = #0#2;
     #2#1 = #0#3;
     #2#8 = #0#4;
     #2#16 = #0#6;
     #2#10 = #0#14;
     #2#11 = #0#15;
     |GRABA 020#2b;
 |FINSI;
 #2#13  = #2#13 + #1#7;
 |GRABA 020#2a;
 |LIBERA #2;
 |CIERRA #2;

 |SI #1#8 ] #2#20; |Y #1#8 [ #2#24;
     #1#9 = #2#28;
 |FINSI;

 |SI #1#8 ] #2#21; |Y #1#8 [ #2#25;
     #1#9 = #2#29;
 |FINSI;

 |SI #1#8 ] #2#22; |Y #1#8 [ #2#26;
     #1#9 = #2#30;
 |FINSI;

 |SI #1#8 ] #2#23; |Y #1#8 [ #2#27;
     #1#9 = #2#31;
 |FINSI;

 #1#10 = 0;
 |SI #1#9 ! 0;
     tot = #1#7 % #1#9;
     #1#10 = (tot * #1#8 ) / 365;
 |FINSI;

 |ABRE #3;
 #3#0 = #1#2;
 #3#1 = #1#3;
 #3#2 = #1#4;
 #3#3 = #1#5;
 |LEE 110#3=;
 |SI FSalida = 0;
     #3#12 = #1#0;
     #3#23 = "S";
     #3#24 = #3#24 + #1#7;
     #3#9  = #3#9 + (#1#10 + #0#6);
     |GRABA 020#3a;
 |SINO;
     |DDEFECTO #3;
 |FINSI;
 |LIBERA #3;
 |CIERRA #3;

 #0#5  = #0#5  + 1;
 #0#10 = #0#10 + #1#7;
 #0#11 = #0#11 + #0#6;
 #0#12 = #0#12 + #1#10;
 |PINTA #0#5;
 |PINTA #0#10;
 |PINTA #0#11;
 |PINTA #0#12;
|FPRC;

|PROCESO liqu;
  nnt = (FEntrada / 100) ! 0;
  |SI nnt = 1;
     #1#7 = #3#8 - #3#24;
  |FINSI;
|FPRC;
