|FICHEROS;
  cafupz01 #0;

  cafupm01 #1,MANTE;
  cafupm02 #2,MANTE;
  cafupm03 #3,MANTE;
  cafupm04 #4,MANTE;

  cafupz03 #50,MANTE;
  cafupw03 #903,MANTE;

  &cafupm1@ #900;
  cafupw0@ #993;
  cacuenta #10;
|FIN;

|VARIABLES;
 &entrada = 1;
 &salidas = 0;
 &enEjercicio = 0;
 &enAny       = 0;
 &eaNombreEmp = "";
 &eaDirDestino = "";
 &eaDirOrigen  = "";
 &eaCuenta = "";
 &enNivel = 0;
 &aFicw3  = "";

 &dig1 = 0;
 &dig3 = 0;
 &dig4 = 0;
 &dig5 = 0;
 &dig6 = 0;
 &dig7 = 0;
 &dig8 = 0;
 &dig9 = 0;

 nConsol = 0;
 nTecla  = 0;
 nModo   = 0;
 nModoEntrada = 0;

 &nMaxDes  = 0;

 aAlfa1 = "";
 aAlfa2 = "";

 aLong  = "";
 nLong  = 0;
 &sw1     = 0;
 &enSalgo = 0;

 aDCuenta = "";
 aHCuenta = "";
 nerror   = 0;
 aBase    = "";
 aDef     = "";

 &nContar = 0;
 nEstoy   = 0;
 nSwOp    = 0;
 nSwError = 0;
|FIN;

|INCLUYE puntos_i;

|| **********************************************************

|PROCESO Borram3_w3;
 |BORRA 020#3a;
 |LIBERA #3;
|FPRC;

|DEFBUCLE Borram3_w3;
 #3#1;
 ;
 #903#0, #903#1, 001;
 #903#0, #903#1, 999;
 be;
 NULL, Borram3_w3;
|FIN;

|CALCULO Tipo1_w3; |TIPO 1;
 nModoEntrada = (FEntrada / 100) ! 0;
 |SI nModoEntrada = 3;
     |BUCLE Borram3_w3;
 |FINSI;
|FCAL;

|PROCESO inf10;
 aAlfa1 = " " * 40;
 #10#0 = aAlfa1;
 #10#1 = 0;
 #10#2 = aAlfa1;
|FPRC;

|PROCESO inf20;
 aAlfa1 = "z" * 40;
 #10#0 = "zzzzzzzzzzzz";
 #10#1 = 9;
 #10#2 = aAlfa1;
|FPRC;

|PROCESO Tipo11_w3;  |TIPO 11;
  |SI sw1 = 0;
      |SI FSalida = 1; |O FSalida = 7;
          enSalgo = 1;
      |FINSI;
  |FINSI;
  |HAZ Tipo0_w3;
|FPRC;

|PROCESO Tipo0_w3;
  nTecla = FSalida;
  |SI nTecla = 10;
      |ABRE #10;
      #10#0 = #903#1;
      #10#1 = #903#3;
      |CONSULTA_F_CLAVE #1#10, inf10, inf20;
      #903#1 = #10#0;
      #903#3 = #10#1;
      |CIERRA #10;
      |PINTA #903#1;
      |PTEC 802;
      |ERROR;
  |FINSI;
  |SI nTecla = 11;
      |HAZ Recojo;
      sw1 = 1;
  |FINSI;
  |SI nTecla = 12;
      |HAZ Copio;
      sw1 = 1;
  |FINSI;
|FPRC;


|PROCESO ClaveBaja3;
 #3#0 = #903#0;
 #3#1 = #903#1;
 #3#2 = 001;
|FPRC;

|PROCESO ClaveAlta3;
 #3#0 = #903#0;
 #3#1 = #903#1;
 #3#2 = 999;
|FPRC;

|PROCESO Tipo7_w3;  |TIPO 7;
     nModo = 7;
     |ENTLINEAL #1#3, -3, 7, 22, 0, ClaveBaja3, ClaveAlta3;
|FPRC;

|PROCESO cuentaw3; |TIPO 6;
  p_alfa1 = #903Campo;
  salidas = FSalida;
  |HAZ punto;
  |SI p_error ! 0;  |ERROR;  |FINSI;
  eaCuenta = #903Campo;
  |HAZ Sacanivel;
  |SI enNivel = 0;
      |MENAV "     Longitud de Cuenta Fuera de Nivel !";
      |ERROR;
  |SINO;
      #903#3 = enNivel;
  |FINSI;
|FPRC;

|PROCESO cuefinw3; |TIPO 0;
  nModoEntrada = (FEntrada / 100) ! 0;

  aAlfa1 = #903#1; |QBF aAlfa1;
  |SI aAlfa1 = ""; |ERROR; |ACABA; |FINSI;
  eaCuenta = #903#1;
  enNivel  = #903#3;

  |ABRE #10;
  #10#0 = eaCuenta;
  #10#1 = enNivel;
  |LEE 001#10=;
  |SI FSalida ! 0;
      |MENAV "    Error. No Existe Cuenta";
      |CIERRA #10;
      |ERROR;
      |ACABA;
  |FINSI;
  |CIERRA #10;

  |SI #10#3 = "N";
      |MENAV "      Esta Cuenta NO tiene Pase directo";
      |ERROR;
  |FINSI;
|FPRC;

|PROCESO Tipo3_w3;  |TIPO 3;
      |HAZ LeePrimero;
      |ENTLINEAL #1#3, -3, 2, 22, 0, ClaveBaja3, ClaveAlta3;
|FPRC;

|PROCESO LeePrimero;
 |ABRE #3;
 #3#0 = #1#0;
 #3#1 = eaCuenta;
 #3#2 = 001;
 |LEE 101#3];
 |SI FSalida ! 0; |O #3#0 ! #1#0; |O #3#1 ! eaCuenta;
     #3#0 = #1#0;
     #3#1 = eaCuenta;
     #3#2 = 001;
     #3#3 = #3#1;
     #3#4 = #3#1;
     |GRABA 020#3b;
 |FINSI;
 |LIBERA #3;
 |CIERRA #3;
|FPRC;

|PROCESO Porcacuenta;
 eaCuenta = #10#0;
 aAlfa1   = #10#0; |QBF aAlfa1; |SI aAlfa1 = ""; |ACABA; |FINSI;
 enNivel  = #10#1;

 #993#0 = #1#0;
 #993#1 = eaCuenta;
 |LEE 001#993=;
 |SI FSalida ! 0;
     #993#0 = #1#0;
     #993#1 = eaCuenta;
     #993#2 = #10#2;
     #993#3 = #10#1;
     |GRABA 020#993n;
 |FINSI;
 |HAZ LeePrimero;
|FPRC;

|DEFBUCLE Porcacuenta;
 #10#1;
 3;
 aDCuenta, 1, "S";
 aHCuenta, 9, "S";
 e;
 NULL, Porcacuenta;
|FIN;

|PROCESO  Recojo;
  |HAZ CargaA;
  |SI nerror = 1; |ACABA; |FINSI;

  nConsol  = 0;
  aDCuenta = "            ";
  aHCuenta = "zzzzzzzzzzzz";
  |HAZ PidoLimites;

  |ABRE #993;
  |BUCLE Porcacuenta;
  |CIERRA #993;
  |HAZ DescargaA;

  sw1 = 1;
  |ERROR;
  |PTEC 806;
|FPRC;

|| ...............................
|PROCESO LinOtro;
 |SI nSwOp = 0;
     |BORRA 020#3a;
 |SINO;
     |SALVA_FICHA #3;
     #3#0 = #1#0;
     |GRABA 020#3n;
     |REPON_FICHA #3;
 |FINSI;
 |LIBERA #3;
|FPRC;

|DEFBUCLE LinOtro;
 #3#1;
 ;
 nEstoy, #993#1, 001;
 nEstoy, #993#1, 999;
 b;
 NULL, LinOtro;
|FIN;

|PROCESO PorOtro;
   |SI nSwOp = 0;
       |BORRA 020#993a;
   |SINO;
       |SALVA_FICHA #993;
       #993#0 = #1#0;
       |GRABA 020#993n;
       |REPON_FICHA #993;
   |FINSI;
   |LIBERA #993;
   |BUCLE LinOtro;
|FPRC;

|DEFBUCLE PorOtro;
 #993#1002;
 ;
 nEstoy, #50#2;
 nEstoy, #50#3;
 ;
 NULL, PorOtro;
|FIN;


|PROCESO Copio;
  |HAZ CargaA;
  |SI nerror = 1; |ACABA; |FINSI;

  nConsol  = -1;
  aDCuenta = "            ";
  aHCuenta = "zzzzzzzzzzzz";
  |HAZ PidoLimites;

  nSwOp = 0; nEstoy = #1#0;
  |BUCLE PorOtro;
  nSwOp = 1; nEstoy = #50#0;
  |BUCLE PorOtro;

  |HAZ DescargaA;

  sw1 = 1;
  |ERROR;
  |PTEC 806;
|FPRC;


|PROCESO PidoLimites;

 |DDEFECTO #50;
 |SI nConsol = -1;
     |HAZ LeeUltim;
     |SI nContar = #903#0; nContar = nContar - 1; |HAZ LeeDefi; |FINSI;
     #50#0 = nContar;
     #50#1 = #900#1;
     |C_M #50#0,1,"S";
 |SINO;
     #50#0 = 0; |C_M #50#0,1,"N";
     #50#1 = "DESDE EL PLAN CONTABLE";
 |FINSI;

 |PUSHV 1431 2180;
 |PINPA #0#50;
 |PINDA #0#50;
 |ENDATOS #1#50;
 |SI FSalida ! 0;
     |POPV;
     |ACABA;
 |FINSI;
 aDCuenta = #50#2;
 aHCuenta = #50#3;

 |POPV;
|FPRC;

|PROCESO CargaA;
 nerror = 0;
 |DBASE aBase;
 aDef = aBase + "def/cafupw03.mas";
 |CARGA_DEF aDef, cafupw0@;
 |SI FSalida ! 0;
     |MENAV "    Error en Cafupw03";
     nerror = 1;
     |ACABA;
 |FINSI;
|FPRC;

|PROCESO DescargaA;
 |DESCARGA_DEF #cafupw0@;
|FPRC;

|| **********************************************************************
|PROCESO cuenta3; |TIPO 6;
  p_alfa1 = #3Campo;
  salidas = FSalida;
  |HAZ punto;
  |SI p_error ! 0;  |ERROR;  |ACABA; |FINSI;

  |SI Campo = 4; |Y #3#3 > #3#4;
      |MENAV "    Error. Cuenta Erronea ... ";
      |ERROR;
  |FINSI;
|FPRC;

|PROCESO cuenta50; |TIPO 6;
  p_alfa1 = #50Campo;
  salidas = FSalida;
  |HAZ punto;
  |SI p_error ! 0;  |ERROR;  |ACABA; |FINSI;

  |SI Campo = 3; |Y #50#2 > #50#3;
      |MENAV "    Error. Cuenta Erronea ... ";
      |ERROR;
  |FINSI;
|FPRC;

|PROCESO Lee50; |TIPO 0;
  |C_M #50#0,0,aAlfa1;
  |SI aAlfa1 = "N"; |ACABA; |FINSI;

  nContar = #50#0;
  |HAZ LeeDefi;
  |SI nSwError ! 0;
      |MENAV "    No Existe Definicion";
      |ERROR;
  |SINO;
      #50#1 = #900#1; |PINTA #50#1;
      |SI #900#12 = "E";
          |MENAV "    No puede recoger de esta Definicion, no es General";
          |ERROR;
      |FINSI;
  |FINSI;
|FPRC;

|| **********************************************************************
|PROCESO Sacanivel;
   aLong = eaCuenta; |QBF aLong;
   aLong = aLong % 0;
   nLong = aLong;
   enNivel = 0;
   |SI nLong = dig4; |Y dig4 ! 0; enNivel = 1; |FINSI;
   |SI nLong = dig5; |Y dig5 ! 0; enNivel = 2; |FINSI;
   |SI nLong = dig6; |Y dig6 ! 0; enNivel = 3; |FINSI;
   |SI nLong = dig7; |Y dig7 ! 0; enNivel = 4; |FINSI;
   |SI nLong = dig8; |Y dig8 ! 0; enNivel = 5; |FINSI;
   |SI nLong = dig9; |Y dig9 ! 0; enNivel = 6; |FINSI;
|FPRC;

|PROCESO LeeUltim;
 nContar = 0;
 |ABRE #900;
 |LEE 001#900u;
 |SI FSalida = 0; nContar = #900#0; |FINSI;
 |CIERRA #900;
|FPRC;

|PROCESO LeeDefi;
 nSwError = 0;
 |ABRE #900;
 #900#0 = nContar;
 |LEE 001#900=;
 |SI FSalida ! 0;
     nSwError = 1;
 |FINSI;
 |CIERRA #900;
|FPRC;
