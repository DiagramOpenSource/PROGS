|FICHEROS;
   ca8m0011 #0,MANTE;    || cuentas
   ca8m0012 #12,MANTE;   ||Varias lineas

   cacuenta #1;
   ca8cumay #2;

   cacuebal #5;
   ca8m0002 #422;

   ca8z0011 #10;
   ca8ctrle #400;  ||Control Empresa
   ca8comay #401;  ||Control Cuentas de mayor
   ca8m0010 #430;
|FIN;

|VARIABLES;
   &entrada = 1;
   &eaDepar = "";
   &eNum2008 = 0;
   nSalir   = 0;
   nModo    = 0;
   cinco    = "     ";
   aCta1    = "";
   aCta2    = "";
   cta      = "     ";
   ctanum   = 0;
   ctaalf   = "";
   longalf  = "";
   longnum  = "";
   nada     = "";
   aAlfa1   = "";
   aAlfa2   = "";
   nNume1   = 0;
   nInkey   = 0;

   mesdes = "";
   meshas = "";
   num    = 0;
   mes    = "";
   nYaExiste = 0;
   aMas      = "";
   aDef      = "";
   nSw       = 0;
   xx        = 0;
   nOper     = 0;
   aCuenta   = "";
   aAntes    = "";
    salidas   = 0;
    &dig1     = 0;
    &dig3     = 0;
    &dig4     = 0;
    &dig5     = 0;
    &dig6     = 0;
    &dig7     = 0;
    &dig8     = 0;
    &dig9     = 0;
    aNomCtaSup ="";
    &enEjerBal = 0;
|FIN;

|INCLUYE vardep_i;
|INCLUYE varnue_i;
|INCLUYE defec1_i;
|INCLUYE puntos_i;

||  ***************************************
|PROCESO BorroLineas;
  #0#4 = #0#4 - #12#4; |PINTA #0#4;
  |SI nOper = 0;
      |BORRA 020#12a;
  |SINO;
       #12#4 = 0;
       |GRABA 020#12a;
  |FINSI;
  |LIBERA #12;
|FPRC;

|DEFBUCLE BorroLineas;
 #12#1;
 ;
 #0#0, 01;
 #0#0, 99;
 be;
 NULL, BorroLineas;
|FIN;

|CALCULO Tipo1; |TIPO 1;
 nModo = (FEntrada / 100) ! 0;
 |SI nModo = 3;
     nOper = 0;
     |BUCLE BorroLineas;
 |FINSI;
|FCAL;

|PROCESO inf0;
 #1#0 = "           ";
 #1#1 = 0;
 #1#3 = "S";
|FPRC;

|PROCESO sup0;
 #1#0 = "zzzzzzzzzzzz";
 #1#1 = 9;
 #1#3 = "S";
|FPRC;

|PROCESO inf430;
 #430#5 = enEjerBal;
 #430#0 = eNum2008;
 #430#1 = "           ";
|FPRC;

|PROCESO sup430;
 #430#5 = enEjerBal;
 #430#0 = eNum2008;
 #430#1 = "zzzzzzzzzzzz";
|FPRC;


|PROCESO inf12;
 #12#0 = #0#0;
 #12#1 = 01;
|FPRC;

|PROCESO sup12;
 #12#0 = #0#0;
 #12#1 = 99;
|FPRC;

|CALCULO tipo7; |TIPO 7;
 |ENTLINEAL #1#12, -2, 7, 22, 0, inf12, sup12;
|FCAL;

|CALCULO Tipo11; |TIPO 11;
 nInkey = FSalida;
 |SI nInkey = 10;
     |ABRE #1;
     #1#0 = #0#0;
     |CONSULTA_F_CLAVE #1#1, inf0, sup0;
     #0#0 = #1#0; |PINTA #0#0;
     #0#1 = #1#2; |PINTA #0#1;
     #0#5 = #1#1;
     |CIERRA #1;
 |FINSI;

 |SI FSalida ! 20;  |ACABA;  |FINSI;
 salidas  = FSalida;
 p_alfa1 = #0Campo;
 |HAZ punto;
 |SI p_error ! 0; |ERROR; |ACABA; |FINSI;
|FCAL;

|CALCULO cuenta; |TIPO 6;
   |C_M #0Campo, 0, aAlfa1;
   |SI aAlfa1 = "N"; |ACABA; |FINSI;

   p_alfa1  = #0Campo
   salidas  = FSalida;
   |HAZ punto;
   |PINTA #0Campo;
   FSalida = salidas;
|FCAL;

|CALCULO cuefin; |TIPO 0;
   nInkey = FSalida;

   |C_M #0Campo, 0, aAlfa1;
   |SI aAlfa1 = "N"; |ACABA; |FINSI;

   |SI nInkey = 10;
      nNume1 = Campo + 1;
      |ABRE #1;
      #1#0 = #0Campo;
      |CONSULTA_F_CLAVE #1#1, inf0, sup0;
      #0Campo  = #1#0; |PINTA #0Campo;
      #0nNume1 = #1#2; |PINTA #0nNume1;
      |CIERRA #1;
   |FINSI;

   |SI nInkey = 11; |Y Campo = 2;
       |ABRE #430;
       #430#5 = enEjerBal;
       #430#0 = eNum2008;
       #430#1 = #0#0;
       |CONSULTA_F_CLAVE #1#430, inf430, sup430;
       |CIERRA #430;
       |ERROR;
       |ACABA;
   |FINSI;

   eaCuenta = #0Campo; |QBF eaCuenta;

   |SI Campo = 0;
       aAlfa1 = eaCuenta % 101;
       |SI aAlfa1 ! "8"; |Y aAlfa1 ! "9";
           |MENAV "    Error. No ha introducido una cuenta del Grupo 8 o 9";
           |ERROR;
           |ACABA;
       |FINSI;
   |FINSI;

   |SI Campo = 2;
       |SI eaCuenta = ""; |O nInkey = 12;
           #0Campo = ""; |PINTA #0#2;
           #0#3    = ""; |PINTA #0#3;
           |SI aAntes ! doce;
               #0#4  = 0; |PINTA #0#4;
           |FINSI;
           nSalir = 1;
           |PARA; |SI nSalir = 1; |HACIENDO;
                  |ENTLINEAL #1#12, -2, 2, 22, 0, inf12, sup12;
                  nSalir = 0;
                  |SI #0#7 ! #0#4;
                      |CONFI "    NReparto no Correcto. Salir ";
                      |SI FSalida ! 0; nSalir = 1; |FINSI;
                 |FINSI;
           |SIGUE;
           |ACABA;
       |FINSI;
   |FINSI;

   emCta    = 1;
   eaCuenta = #0Campo;
   |HAZ eSacaNivel;
   |SI eswCta ! 0;  |ACABA; |FINSI;  ||Cuenta erronea
   |SI Campo = 0; #0#5 = enNivel;|FINSI;
   |SI Campo = 2; #0#6 = enNivel;|FINSI;

   |SI Campo = 0;
       eaCuenta = #0#0;
       |HAZ aContra8y9;
       |SI eSwT8y9 = 1;
           |MENAV "    Atencion, cuenta no definida en Cierre del P.Neto";
       |FINSI;
   |FINSI;

   |SI Campo = 2;
       aAlfa1 = eaRaiz89; |QBF aAlfa1;
       |SI aAlfa1 ! "";
           aAlfa2 = aAlfa1 % 0;
           nNume1 = aAlfa2;
           nNume1 = 100 + nNume1;
           aAlfa2 = #0Campo % nNume1;
           |SI aAlfa2 ! aAlfa1;
               |MENAV "    Atencion. No corresponde definicion de Cierre del P.Neto";
               |ERROR;
               |ACABA;
           |FINSI;
       |FINSI;
  |FINSI;

   emCta = 0;
   |HAZ eLeeCuenta;
   |SI eswCta = 1;
       |CONFI "    SNo Existe Cuenta. Dar de Alta";
       |SI FSalida ! 0;
           |ERROR;
           |ACABA;
       |SINO;
           |HAZ AltaCta;
       |FINSI;
   |FINSI;  ||Cuenta erronea
   nNume1 = Campo + 1;
   #0nNume1= #1#2; |PINTA #0nNume1;
   |SI #1#3 = "N";
       |MENAV "    Cuenta sin pase Directo";
       |ERROR;
       |ACABA;
   |FINSI;

   |SI aAntes = doce; |Y Campo = 2;
       #0#4 = #0#7; |PINTA #0#4;
       nOper = 1;
       |BUCLE BorroLineas;
       #0#4 = #0#7; |PINTA #0#4;
   |FINSI;

   nModo = (FEntrada / 100) ! 0;
   |SI nModo = 1; |Y  Campo = 0;
       #0#2 = eaCon8y9;
       #0#3 = eaTit8y9;
       #0#6 = enNivel;
       |SI #1#53 ] 0;
            #0#7 = #1#53;
            #0#8 = "D";
       |SINO;
            #0#7 = -#1#53;
            #0#8 = "H";
       |FINSI;
       aCuenta = #0#2; |QBF aCuenta;
       |SI aCuenta ! "";
            #0#4 = #0#7;  || Todo Repartido
       |FINSI;
   |FINSI;
   aAntes = #0#2;
|FCAL;


|CALCULO cuenta_l; |TIPO 6;
   |C_M #12Campo, 0, aAlfa1;
   |SI aAlfa1 = "N"; |ACABA; |FINSI;

   p_alfa1  = #12Campo
   salidas  = FSalida;
   |HAZ punto;
   |PINTA #12Campo;
   FSalida = salidas;
|FCAL;

|CALCULO cuefin_l; |TIPO 0;
   nInkey = FSalida;

   |C_M #12Campo, 0, aAlfa1;
   |SI aAlfa1 = "N"; |ACABA; |FINSI;

   |SI nInkey = 10;
      |ABRE #1;
      #1#0 = #12Campo;
      |CONSULTA_F_CLAVE #1#1, inf0, sup0;
      #12#2 = #1#0; |PINTA #12#2;
      #12#3 = #1#2; |PINTA #12#3;
      #12#5 = #1#2; |PINTA #12#5;
      |CIERRA #1;
   |FINSI;

   emCta    = 1;
   eaCuenta = #12#2;
   |QBF eaCuenta; |SI eaCuenta = ""; |ERROR; |ACABA; |FINSI;

   |HAZ eSacaNivel;
   |SI eswCta ! 0;  |ACABA; |FINSI;  ||Cuenta erronea
   #12#5 = enNivel;

   emCta = 0;
   |HAZ eLeeCuenta;
   |SI eswCta = 1;
       |CONFI "    SNo Existe Cuenta. Dar de Alta";
       |SI FSalida ! 0;
           |ERROR;
           |ACABA;
       |SINO;
           |HAZ AltaCta;
       |FINSI;
   |FINSI;  ||Cuenta erronea
   #12#3= #1#2; |PINTA #12#3;

   |SI #1#3 = "N";
       |MENAV "    Cuenta sin pase Directo";
       |ERROR;
   |FINSI;
|FCAL;

|CALCULO Tipo2_l; |TIPO 2;
 #0#4 = #0#4 +. #12#4;
 |PINTA #0#4;
|FCAL;

|CALCULO Tipo0_l; |TIPO 0;
 nInkey = FSalida;
 |SI nInkey = 11;
     #12#4 = #0#7 - #0#4;
     |PINTA #12#4;
     |ERROR;
 |FINSI;
|FCAL;

|PROCESO c_departa; |TIPO 7;
   |C_M #12#6, 1, eaDepar;
|FPRC;

|PROCESO pActividad; |TIPO 0;
   |SI eaDepar = "N"; |ACABA; |FINSI;

   eOpDep = 0; || 1 = Consulta pantalla
   |SI FSalida = 10;  eOpDep = 1; |FINSI;
   eaCodDep    = #12#6;
   |HAZ Actividad;
   |SI eaCodDep = ""; eswLect = 0; |FINSI;
   |SI eswLect = 1;
       |MENAV "    Error.! No Existe Departamento";
       |ERROR;
   |SINO;
       #12#6 = eaCodDep; |PINTA #12#6;
       |HAZ pDepartamento;
   |FINSI;
|FPRC;

|PROCESO AltaCta;
   |ABRE #1;
   #1#0 = eaCuenta;
   #1#1 = enNivel;
   |LEE 001#1=;
   |SI FSalida ! 0;
       num = dig1; |HAZ elmes_m3; mesdes = mes;
       num = dig1 + 11; |SI num > 12; num = num - 12; |FINSI;
       |HAZ elmes_m3; meshas = mes;
       |DDEFECTO #1;
       #1#0 = eaCuenta;
       #1#1 = enNivel;
       |HAZ BuscaSup;
       #1#2 = aNomCtaSup;
       #1#3 = "N"; |SI enNivel = dig3; #1#3 = "S"; |FINSI;
       nMensa   = 1;
       |HAZ defect1;
       #1#56   = mesdes;
       #1#57   = meshas;
       |GRABA 020#1n;
  |FINSI;
  |LIBERA #1;
  |CIERRA #1;
|FPRC;


|PROCESO BuscaSup;
 |ABRE #2;
 aNomCtaSup = "";
 |DDEFECTO #2;
 aAlfa1 = eaCuenta % 105;
 #2#12 = enEjerBal;
 #2#10 = eNum2008;
 #2#0  = aAlfa1;
 |LEE 001#2=;
 |SI FSalida = 0;
     aNomCtaSup = #2#9;
 |SINO;
     aAlfa1 = eaCuenta % 104;
     #2#0  = aAlfa1;
     |LEE 001#2=;
     |SI FSalida = 0;
         aNomCtaSup = #2#9;
     |SINO;
         aAlfa1 = eaCuenta % 103;
         #2#0  = aAlfa1;
         |LEE 001#2=;
         |SI FSalida = 0;
             aNomCtaSup = #2#9;
         |FINSI;
     |FINSI;
 |FINSI;
 |CIERRA #2;
|FPRC;

|PROCESO elmes_m3;
   |SI num =  1; mes = "Enero";     |FINSI;
   |SI num = 12; mes = "Diciembre"; |FINSI;
   |SI num =  2; mes = "Febrero";   |FINSI;
   |SI num =  3; mes = "Marzo";     |FINSI;
   |SI num =  4; mes = "Abril";     |FINSI;
   |SI num =  5; mes = "Mayo";      |FINSI;
   |SI num =  6; mes = "Junio";     |FINSI;
   |SI num =  7; mes = "Julio";     |FINSI;
   |SI num =  8; mes = "Agosto";    |FINSI;
   |SI num =  9; mes = "Septiembre";|FINSI;
   |SI num = 10; mes = "Octubre";   |FINSI;
   |SI num = 11; mes = "Noviembre"; |FINSI;
|FPRC;
