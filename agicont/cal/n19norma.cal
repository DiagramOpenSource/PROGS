         **** PASE DEL HISTORICO DE RECIBOS AL SECUENCIAL ADAPTADO A LA ***
                        ***     NUEVA NORMA    ****
|| Nota:
|| Lo de las lineas opcionales no tiene demasiado sentido en Gestion Comercial.
|| De echo de los comentarios que hemos hecho con los interesados no le ha
|| interesado a nadie.

|FICHEROS;
  n19norma #0;      || Pantalla de pase
  caremesc #4;      || Cabecera de remesas
  caremesl #1;      || Lineas de remesas
  caclipro #2;      || Clientes
  n19idrec #3;      || Indexado de recibos (Temporal)
  n19datos #5;      || Datos secuencial
  camancob #6;      || Recibos
  caivarep #7;      || IVA repercutido

  camoneda #100;
|FIN;

|VARIABLES;
  &EURO   = 0;
  &EURODB = 0;
  &EURODV = 0;
|| Cabecera del presentador;
  cabe1p = "0180";
  cabe1e = "5180";
  cabe1 = "";
  cabe2 = "";
  cabe3 = "      ";
  cabe5 = "";
  cabe6 = "          ";
  libre20 = "                    ";
  libre12 = "            ";
  libre40 = "                                        ";

|| Cabecera de Ordenante;
  orde1p = "0380";
  orde1e = "5380";
  orde1 = "";
  orde2 = "";
  orde3 = "";
  orde5 = "";
  libre8= "        ";
  orde6 = "01";
  libre10 = "          ";
  fechacargo = @;

|| Registro individual Obligatorio
  indi1p = "0680";
  indi1e = "5680";
  indi1 = "";
  indi2 = "";
  indi3 = "";
  indi4 = "";
  indi5 = "";
  indi6 = "";
  indi7 = "";
  indi8 = "";
||  indi9 = "       .";
  indi9 = "        ";
  indi10 = "          ";
  dc="";

  conc1_1p = "0681"; conc1_1e = "5681";
  conc1_2p = "0682"; conc1_2e = "5682";
  conc1_3p = "0683"; conc1_3e = "5683";
  conc1_4p = "0684"; conc1_4e = "5684";
  conc1_5p = "0685"; conc1_5e = "5685";

  conc1_1 = "";
  conc1_2 = "";
  conc1_3 = "";
  conc1_4 = "";
  conc1_5 = "";
  conc1 = "";
  conc2 = "";

  vacio40 = "                                        ";

  conc3 = "                                        ";
  conc4 = "                                        ";
  conc5 = "                                        ";

  conc11="";
  conc12="";
  conc13="";
  conc21="";
  conc22="";


|| Registro Total General Y ordenante
  tgen1p = "0980";
  tgen1e = "5980";
  tgen1 = "";

  tgen2 = "                                                    ";
  tgen3 = "";
  tgen4 = "";
  tgen5 = "      ";
  tgen6 = "";
||  tgen7 = "                                     .";
  tgen7 = "                                      ";
  tgen8 = "0001";
  tgen9 = "                ";
  totgen = 0;

  tord1p = "0880";
  tord1e = "5880";
  tord1 = "";
  tord9 = "                    ";

  MonBase = 0;
  Cantidad = 0;

  handle = "";
  varalfa1 = "";
  varalfa2 = "";
  facalfa = "";
  contenido = "";
  longalf = "";
  fiche = "";
  espacio = " ";
  dd = "";
  mm = "";
  aa = "";
  fechalf = "";
  impalf = "";
  vacio = "          ";
  trecibo = 0;
  timpsuc = 0;
  tnumsuc = 0;
  serie = "";
  factu = 0;
  cont = 0;
  nCalc = 0;
  contreg = 0;
  empresa = "";
  linea_c = "Procesando Factura: Serie:    Factura:";
  linea   = 0;
||  final = "                   .";
  final = "                    ";
||  final14 = "             .";
  final14 = "              ";
  l = 0;
  valo = 0;
  elefe = "";
  ceerre = "";
  traba = "";
  mensaje = "";
  Moneda = "";

  sw_existe = 0;
  iva       = 0;
  c_clie    = "";
  fijo      = "";
  varalf    = "";
  posici    = "";


  ca1    = "";
  ca2    = "";
  ca3    = "";
  ca4    = "";
  ca5    = "";
  ca6    = "";
  ca7    = "";
  ca8    = "";
  ca9    = "";
  ca10   = "";
  ca11   = "";
  ca12   = "";

  ca        = "";

  aconcepto    = "";
  i            = 0;
  vacio7       = "       ";
  aimporte     ="";
  atmp         = "";
  atmp1        = "";
  atmp2        = "";
 &programa = "";
 &ficcopia = "";

  aAlfa1 = "";
  aAlfa2 = "";
|FIN;



|CALCULO secuencial;

  |DDEFECTO #6;
  #6#0 = #1#2;
  #6#1 = #1#3;
  #6#2 = #1#4;
  #6#3 = #1#5;
  |LEE 001#6=;

  #2#23 = "C";              ||** Lee fichero de clientes;
  #2#10 = #1#11;
  #2#11 = #1#12;
  |LEE 040#2=;
  |SI FSalida = 0;
      #3#0 = #1#4;  ||** Serie;
      #3#1 = #1#3;  ||** Factura;
      #3#2 = #2#1;  ||** Nom. Cliente;

      |SI #0#14 = "N";                   || Si no hay que quitar puntos y guiones
                                         || en cif descargo directamente
          #3#3 = #2#9;  ||** Cif/Dni;
      |SINO;
          |HAZ quita_guion;              || Si no los quito
          #3#3 = ca;
      |FINSI;

      aAlfa1 = #6#20; |QBF aAlfa1;
      |SI aAlfa1 = ""; aAlfa1 = #2#18; |SINO; aAlfa1 = #6#20; |FINSI;
      aAlfa2 = #6#19; |QBF aAlfa2;
      |SI aAlfa2 = ""; aAlfa2 = #2#17; |SINO; aAlfa2 = #6#19; |FINSI;

      atmp1 = aAlfa1 % 104;   || Banco
      #3#4 = atmp1;

      atmp2 = aAlfa1 % 504;   || Sucursal
      #3#5 = atmp1 + atmp2;               || Banco + sucursal (se repite)

      atmp1 = aAlfa2 % 110;   || Cuenta

      atmp2 = aAlfa1 % 902;   || Digito control

      #3#6 = atmp1 + atmp2;

      #3#7  = #1#7;           ||** Importe Recibo;
      #3#8  = #1#6;           ||** Fecha Vencimiento
      #3#9  = #1#5;    || Numero de recibo

      |DDEFECTO #7;
      #7#0  = #1#2;
      #7#2  = #1#3;
      #7#27 = #1#4;
      |LEE 001#7=;
      #3#10 = #1#2;   ||Libro
      #3#11 = #7#7;   ||descripcion

      |GRABA 020#3n;||** Graba el fichero indexado;
      |LIBERA #3;
  |SINO;
     mensaje = "0000ATENCION Cliente Inexistente! CODIGO.: " + #1#11;
     |MENAV mensaje;
  |FINSI;
|FCAL;

|PROCESO quita_guion;
ca1  = #2#9 % 101;
ca2  = #2#9 % 201;
ca3  = #2#9 % 301;
ca4  = #2#9 % 401;
ca5  = #2#9 % 501;
ca6  = #2#9 % 601;
ca7  = #2#9 % 701;
ca8  = #2#9 % 801;
ca9  = #2#9 % 901;
ca10 = #2#9 % 1001;
ca11 = #2#9 % 1101;
ca12 = #2#9 % 1201;

|SI ca1  = "."; |O ca1  = "-"; ca1  = ""; |FINSI;
|SI ca2  = "."; |O ca2  = "-"; ca2  = ""; |FINSI;
|SI ca3  = "."; |O ca3  = "-"; ca3  = ""; |FINSI;
|SI ca4  = "."; |O ca4  = "-"; ca4  = ""; |FINSI;
|SI ca5  = "."; |O ca5  = "-"; ca5  = ""; |FINSI;
|SI ca6  = "."; |O ca6  = "-"; ca6  = ""; |FINSI;
|SI ca7  = "."; |O ca7  = "-"; ca7  = ""; |FINSI;
|SI ca8  = "."; |O ca8  = "-"; ca8  = ""; |FINSI;
|SI ca9  = "."; |O ca9  = "-"; ca9  = ""; |FINSI;
|SI ca10 = "."; |O ca10 = "-"; ca10 = ""; |FINSI;
|SI ca11 = "."; |O ca11 = "-"; ca11 = ""; |FINSI;
|SI ca12 = "."; |O ca12 = "-"; ca12 = ""; |FINSI;

ca = ca1 + ca2 + ca3 + ca4 + ca5 + ca6 + ca7 + ca8 + ca9 + ca10 + ca11 + ca12;
|QBF ca;
ca = ("000000000000" + ca) % -112;
|FPRC;


|PROCESO lin_remesa;

   |SI #0#15 = "N"; |Y #caremesc#8 = "D"; |ACABA; |FINSI;
   |SI #0#15 = "D"; |Y #caremesc#8 ! "D"; |ACABA; |FINSI;

   |BUCLELIN 2secuencial#caremesc;

   #caremesc#8 = "D";     || Marco emitida como "D"isquete
                          || Se debera controlar que este valor no se cambie
                          || al intentar imprimir la remesa
   |GRABA 021#caremesc.a;
   |LIBERA #caremesc;
|FPRC;

|| Recibos-> Clave primaria
|DEFBUCLE capasban0;
 #4#3;
 ;           || +-- Antes #0#15 No deja pasa las emitidas a papel
 #0#4, #0#2, " " , "N";
 #0#5, #0#3, "z" , "N";
 be;
 NULL,lin_remesa;
|FIN;


|PROGRAMA;
  |CLS;
  |CABEZA "Pase Secuencial N-19";
  |PINPA #0#3;     || Pantalla de aviso. Utilizo el def INDEXADO DE RECIBOS
                   || para no crear uno nuevo
  |AVISO;
  |CONFI "2400SSi no esta muy seguro, verifique que son correctos los datos. Continuar ?  ";
  |SI FSalida ! 0;
       |ACABA;
  |FINSI;

  |ABRE #camoneda;
  #camoneda#0 = #48#2;
  #camoneda#1 = #48#3;
  |LEE 000#camoneda.=;
  |SI FSalida = 0;
     Moneda = #camoneda#2;
     |SI #camoneda#2 = "P";
        MonBase = 1;
     |SINO;
        MonBase = 2;
     |FINSI;
  |SINO;
     Moneda = "E";
     MonBase = 2;
  |FINSI;
  |CIERRA #camoneda;

  |PINPA #0#0;
  |PINDA #0#0;

  |ENDATOS #1#0;

  |SI FSalida ! 0; |ACABA; |FINSI;

  |HAZ PideMoneda;

  |SI MonBase = 2;                  || euros
     EURO = 166.386;
     EURODB = 2;
     EURODV = 0;
  |SINO;
     EURO = 1 / 166.386;
     EURODB = 0;
     EURODV = 2;
  |FINSI;

  elefe = &010;                 || Cr
  ceerre = &013;                || Lf
  |SI #0#13 = "S";              || A¤adimos al final del registro 13+10 ?
      traba = ceerre + elefe;
      elefe = traba;
  |FINSI;

  |ABRE #2;      || Clientes
  |ABRE #7;      || IVA Repercutido
  |ABRE #6;

  || Temporal
  |ABRE #3;      || Indexado de recibos
  |CIERRA #3;
  |DELINDEX #3;
  |ABRE #3;

  || Abrimos fichero de recibos

  |ATRI P;
  |MENSA "0000Creando temporal...";
  |BUCLE capasban0;
  |BLIN 4;
  |ATRI p;

  |CIERRA #6;
  |CIERRA #7;
  |CIERRA #2;

  |ATRI P;
  |MENSA "0000Creando fichero secuencial ...";
  |ATRI p;

  |ABRE #5;          || Datos secuencial

  |ABRE #camancob;    || Recibos

  #5#0 = #0#9;
  |LEE 040#5=;
  |DFICE empresa;
  fiche = "wb  " + empresa + "agifarec.seq";
  |QBF fiche;
  |FABRE fiche;
  handle = FSalida;
  |SI FSalida ] 0;
      |HAZ cabecera_p;
      |HAZ cab_ordenante;
      |HAZ regindiv;
      |HAZ campo_totales;
  |FINSI;

  |CIERRA #camancob;
  FSalida = handle;
  |FCIERRA FSalida;


  || Temporal
  |CIERRA #3;
  |||DELINDEX #3;   || Lo borraremos al entrar

  |CIERRA #5;
  |AVISO;
  |CONFI "2400SDESEA GRABAR AHORA EL DISKETTE ";
  |SI FSalida = 0;
      programa = " REMESA N19 ";
      ficcopia = "agifarec.seq";
      |CORRE "cared003.run";
  |FINSI;
|FPRO;


||***********************************************************;
||************ REGISTRO DE CABECERA DE PRESENTADOR **********;
||***********************************************************;
|PROCESO cabecera_p;
  fechalf = #0#8;       || Fecha abono
  dd = fechalf % A102;
  mm = fechalf % A402;
  aa = fechalf % A902;
  cabe2 = dd + mm + aa;
  cabe5 =  #5#7 + #5#8;   || Codigo banco + codigo sucursal

  |SI Moneda = "P";
     cabe1 = cabe1p;
  |SINO;
     cabe1 = cabe1e;
  |FINSI;

  varalfa1 = cabe1 + #5#2 + #0#12 + cabe2 + cabe3 + #5#3 + libre20+ cabe5 + libre12 + libre40+ final14;
  || varalfa  = cabe1       Fijo 0180
  ||           +  #5#2      nif
  ||           +  #0#12     Sufijo
  ||           +  cabe2     Fecha ddmmaa
  ||           +  cabe3     LIBRE 6 espacios
  ||           +  #5#3      Nombre del presentador
  ||           +  libre20   LIBRE 20 espacios
  ||           +  cabe5     Codigo Banco + codigo sucursal
  ||           +  libre12   LIBRE 12 espacios
  ||           +  libre40   LIBRE 40 espacios
  ||           +  final14   LIBRE 14 espacios

  contenido = handle + espacio + varalfa1 + elefe;
  |FGRABA contenido;
  l = l + 1;
|FPRC;

||***********************************************************;
||************ REGISTRO DE CABECERA DE ORDENANTE ************;
||***********************************************************;
|PROCESO cab_ordenante;
  fechalf = #0#8;        || Fecha de abono
  dd = fechalf % A102;
  mm = fechalf % A402;
  aa = fechalf % A902;
  orde2 = dd + mm + aa;
  fechacargo = #0#11;    || Fecha de cargo
  dd = fechacargo % A102;
  mm = fechacargo % A402;
  aa = fechacargo % A902;
  orde3 = dd + mm + aa;
  orde5 = #5#4 + #5#5 + #5#11 + #5#6;
  varalfa1="";

  |SI Moneda = "P";
     orde1 = orde1p;
  |SINO;
     orde1 = orde1e;
  |FINSI;

  varalfa1 = orde1 + #5#2 + #0#12 + orde2 + orde3 + #5#3 + orde5 + libre8 + orde6 + libre10 + libre40 + final14;
  ||varalfa1   = orde1   Fijo 0380
  ||           + #5#2    nif
  ||           + #0#12   sufijo
  ||           + orde2   Fecha de abono ddmmaa
  ||           + orde3   Fecha de cargo ddmmaa
  ||           + #5#3    Nombre ordenante = presentador
  ||           + orde5   codigo banco + oficina + digito control + cuenta
  ||           + libre8  LIBRE 8 espacios
  ||           + orde6   FIJO 01
  ||           + libre10 LIBRE 10 espacios
  ||           + libre40 LIBRE 40 espacios
  ||           + final14 LIBRE 14 espacios

  contenido = handle + espacio + varalfa1 + elefe;
  |FGRABA contenido;
  l = l + 1;
|FPRC;


||***********************************************************;
||******************* REGISTRO INDIVIDUAL *******************;
||***********************************************************;
|PROCESO regindiv;
  |SELECT #3#3;          ||Cojo la tercera clave del fichero CCC y Cta.
  |LEE 040#3p;
  |SI FSalida ! 0;  |ACABA;  |FINSI;    || Si no hay ninguna Fuera...

  |||PINTA 2218,linea_c;                  || Mensaje
  |||ATRI I;
  |||PINTA 2245,#3#0;
  |||PINTA 2257,#3#1;
  |||ATRI i;

|ET compara;

  || Leo el recibo
  #camancob#0 = #3#10;  || Libro
  #camancob#1 = #3#1;   || Numero Factura
  #camancob#2 = #3#0;   || Serie
  #camancob#3 = #3#9;   || Recibo
  |LEE 001#camancob.=;
  |HAZ campo_indiv;

  trecibo = trecibo + 1;
  timpsuc = timpsuc + Cantidad;

  || Si hemos dicho que queremos todas las lineas de la factura en el secuencial
  || se compondran los 6 registros opcionales que se necesiten. En cada registro
  || 3 lineas
  |SI #5#10 = "S";
       |HAZ conceptos;
  |FINSI;
  |LEE 040#3s;
  |SI FSalida = 0;

  ||    |ATRI I;              || Si no se descontrola con el FGRABA
  ||    |PINTA 2245,#3#0;
  ||    |PINTA 2257,#3#1;
  ||    |ATRI i;

      |VETE compara;
  |FINSI;
|FPRC;


||***********************************************************;
||******************* REGISTRO CONCEPTOS ********************;
||***********************************************************;
|PROCESO conceptos;
  || Miro todos los conceptos que hay en este recibo para componer los
  || registros individuales opcionales

  || !!! COMPLETAR !!!

  ||     |HAZ graba_opci;

|FPRC;

|| Graba registros opcionales
|PROCESO graba_opci;

      varalfa1 = conc1 + #5#2 + #0#12 + #3#3 + conc3 + conc4 + conc5 + final14;
      || varalfa1   = conc1        Fijo 0681 o 0682
      ||            + #5#2         Nuestro nif
      ||            + #0#12        Sufijo
      ||            + #3#3         Cif cliente
      ||            + conc3        primer concepto
      ||            + conc4        segundo concepto
      ||            + conc5        tercer concepto
      ||            + final14      LIBRE 14 posiciones

      contenido = handle + espacio + varalfa1 + elefe;
      |FGRABA contenido;
      l = l + 1;

      || Se inicializan valores
      conc3 = "                                        ";
      conc4 = "                                        ";
      conc5 = "                                        ";

|FPRC;



||Calculo registros individuales;

|| Individual obligatorio
|PROCESO campo_indiv;

  indi4 = #3#5 % 108;         || entidad + oficina
  indi4 = indi4 + vacio;
  indi4 = indi4 % 108;


  indi5 = #3#6 % 110;         || Numero de Cta.
  |QBT indi5;
  indi5="0000000000" + indi5;
  indi5= indi5 % -110;
  dc = #3#6 % 1102;           || Digito de control
  |QBF dc;
  dc = dc + "**";             || Si esta en blanco se debe rellenar de "*"
  dc = dc % 102;

  Cantidad = #3#7; |HAZ Conversion;
  impalf  = Cantidad;         || Importe recibo rellenado de ceros
  impalf  = "0000000000" + impalf;
  impalf  = impalf % -110;
  indi6   = impalf;
  fechalf = #3#8;             || Fecha de emision del recibo
  dd      = fechalf % A102;
  mm      = fechalf % A402;
  aa      = fechalf % A902;
  indi7   = dd + mm + aa;

  posici  = #5#9 % -101;       || Codigo sucursal nuestra

  facalfa="";

  |SI #5#20 = "S";
      facalfa = #3#11;  ||descripcion de la factura
      indi8   = facalfa + "                                           ";
  |SINO;

     |SI #5#13 = "S";            || Incluir serie en descripcion
        atmp = "00"+#3#0;
        atmp = atmp % "-102";
        facalfa = facalfa + atmp;
     |FINSI;

     |SI #5#14 = "S";            || Incluir numero factura en descripcion
        |SI #5#13 = "S";
             facalfa = facalfa + "/";
        |FINSI;
        atmp = "000000" + #3#1;
        atmp = atmp % "-106";
        facalfa = facalfa + atmp;
     |FINSI;

     |SI #5#15 = "S";            || Incluir numero serie en descripcion
        |SI #5#13 = "S";|O #5#14="S";
             facalfa = facalfa + "/";
        |FINSI;
        atmp = "000" + #3#9;
        atmp = atmp % "-103";
        facalfa = facalfa + atmp;
     |FINSI;

     || Se compone descripcion mas numero de Fra.
     || NUESTRA FRA. NUMERO 000001
     indi8 = #5#9; |QBF indi8;
     indi8   = indi8 + " " + facalfa + "                                           ";
  |FINSI;

  indi8   = indi8 % A140;


  |SI Moneda = "P";
     indi1 = indi1p;
  |SINO;
     indi1 = indi1e;
  |FINSI;

 varalfa1 = indi1 + #5#2 + #0#12 + #3#3 + #3#2 + indi4 + dc + indi5 + indi6 + indi7 + indi10 + indi8 + indi9;
 || varalfa1   =    indi1     Fijo 0680
 ||            +    #5#2      nif
 ||            +    #0#12     Sufijo
 ||            +    #3#3      Cif del cliente
 ||            +    #3#2      Nombre del cliente
 ||            +    indi4     Entidad + oficina
 ||            +    dc        Digito control
 ||            +    indi5     Cuenta
 ||            +    indi6     Importe recibo
 ||            +    indi7     Fecha emision recibo ddmmaa
 ||            +    indi10    LIBRE en blanco
 ||            +    indi8     Descripcion + numero de recibo
 ||            +    indi9     LIBRE 8 posicones

  contenido = handle + espacio + varalfa1 + elefe;
  |FGRABA contenido;
  l = l + 1;
|FPRC;

||***********************************************************;
||********** REGISTRO TOTAL ORDENANTE Y GENERAL *************;
||***********************************************************;
|PROCESO campo_totales;

|| **** REGISTRO TOTAL ORDENANTE

  tgen3 = trecibo;
  tgen3 = "0000000000" + tgen3;
  tgen3 = tgen3 % -110;
  impalf = timpsuc;
  impalf = "0000000000" + impalf;
  impalf = impalf % -110;
  tgen4  = impalf;
  l = l + 1;

  tnumsuc = l - 1;        ||Se quita un registro, porque no se cuenta
                          ||el primero
  tgen6  = tnumsuc;
  tgen6  = "0000000000" + tgen6;
  tgen6  = tgen6 % -110;

  |SI Moneda = "P";
     tord1 = tord1p;
  |SINO;
     tord1 = tord1e;
  |FINSI;

  varalfa1 = tord1 + #5#2 + #0#12 + tgen2 + tord9 + tgen4 + tgen5 + tgen3 + tgen6 + tgen7;
  || varalfa1  =    tord1     Fijo 0880
  ||           +     #5#2     Nif nuestro
  ||           +     #0#12    Sufijo
  ||           +     tgen2    LIBRE 52 posiciones  ( 12 + 40)
  ||           +     tord9    LIBRE 20 posiciones
  totgen = totgen + timpsuc;  ||Suma de importes
  ||           +     tgen5    LIBRE 6 posicones
  ||           +     tgen3    Numero de recibos
  ||           +     tgen6    Total de registros (No se cuenta el primero)
  ||           +     tgen7    LIBRE 30 posiciones (20 + 18)

  contenido = handle + espacio + varalfa1 + elefe;
  |FGRABA contenido;

|| **** REGISTRO TOTAL GENERAL

  tgen3 = trecibo;                ||** Total num recibos;
  tgen3 = "0000000000" + tgen3;
  tgen3 = tgen3 % -110;

  impalf = timpsuc;               ||** Importe del recibo sucursal;
  impalf = "0000000000" + impalf;
  impalf = impalf % -110;
  tgen4  = impalf;
  ||totgen = totgen + timpsuc;  ||Suma de importes

  l = l + 1;
  tnumsuc = l;

  tgen6  = tnumsuc;                 ||** Total reg. sucursales
  tgen6  = "0000000000" + tgen6;
  tgen6  = tgen6 % -110;

  |SI Moneda = "P";
     tgen1 = tgen1p;
  |SINO;
     tgen1 = tgen1e;
  |FINSI;

  || En nuestro caso es igual al TOTAL ORDENANTE con un registro mas
  varalfa1 = tgen1 + #5#2 + #0#12 + tgen2 + tgen8 + tgen9 + tgen4 + tgen5 + tgen3 + tgen6 + tgen7;
  contenido = handle + espacio + varalfa1 + elefe;
  |FGRABA contenido;

  valo = tgen3;
  |PINTA 1926,valo;
  valo = tgen6;
  |PINTA 2026,valo;
  |SI Moneda = "P";
     valo = totgen;
  |SINO;
     valo = totgen / 100;
  |FINSI;
  |PINTA 2126,valo;
  |PAUSA;
|FPRC;

|PROCESO PideMoneda;
   |PUSHV 1943 2177;
   |BLANCO 1943 2177;
   |CUADRO 1945 2175;
   |ATRI R; |PINTA 1946 , " Seleccione moneda de emision "; |ATRI N;
   |PINTA 2053, "(Pesetas/Euros)";
   |ATRI I; |PINTA 2054, "P"; |PINTA 2062, "E"; |ATRI N;
   E_sup  = 1;
   E_inf  = "EP";
   |SI MonBase = 1; Moneda = "P"; |SINO; Moneda = "E"; |FINSI;
   Moneda = 2069 ? 0;
   |POPV;
|FPRC;

|PROCESO Conversion;
   |SI MonBase = 1;
      |SI Moneda = "E";
         Cantidad = ((Cantidad / 166.386) ! 2) * 100;
      |FINSI;
   |SINO;
      |SI Moneda = "P";
         Cantidad = (Cantidad * 166.386) ! 0;
      |SINO;
         Cantidad = Cantidad * 100;
      |FINSI;
   |FINSI;
|FPRC;
