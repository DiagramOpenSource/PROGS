|FICHEROS;
    cajainli #0;
    cajainca #1;
    cafantas #2;
    cajasald #3;
|FIN;

|VARIABLES;
    &enDiario   = 0;
    &enContador = 0;

    &entrada = 1;
    &esalta = 0;
    &adocum = 0;
    &adiari = 0;
    &afecha = @;
    &dpta   = "";
    &cpartida = "";
    &dpartida = 0;
    paso = 0;
    &cuant = "";
    &diant = 0;
    &coant = 1;
    &deant = "";
    &dhant = "C";
    &dpant = "01";
    &imant = 0;
    &avisos = "";
    alfa1 = "";
    nume1 = 0;
    nume2 = 0;
    sw_movim = 0;
    fecnum = 0;
|FIN;

|INCLUYE acceso_i;

|PROCESO comienza; |TIPO 8;
 |HAZ inicio;
 dpta = #30#24;
 |ABRE #3;
 |LEE 101#3p;
 |SI FSalida ! 0;
     |MENAV "    Error, no Existe fichero <Saldo Dia Anterior> ";
     |LIBERA #3;
     |CIERRA #3;
     |PTEC 807;
     |ACABA;
 |FINSI;
 cpartida = #3#2;
 dpartida = #3#3;
 afecha = #3#7;
 |SI afecha < fec1;
     fecnum = fec1 - 1;
     afecha = fecnum;
 |FINSI;
 |LIBERA #3;
 |CIERRA #3;
|| ..... Los ultimos;
 |DDEFECTO #1;
 |LEE 001#1u;
 |SI FSalida = 0;
     afecha = #1#1;
     paso = 1;
 |FINSI;
 adiari = #1#0;
 adocum = #1#4;
|FPRC;

|PROCESO salir; |TIPO 9;
 |SI sw_movim = 0; |ACABA; |FINSI;
 |AVISO;
 |CONFI "2400SDesea actualizar el Diario de Caja S/N : ";
 |SI FSalida ! 0; |ACABA; |FINSI;
 |CORRE "cajaactu.tab";
|FPRC;

|PROCESO def_fecha; |TIPO 7;
 esalta = (FEntrada / 100) ! 0;
 |SI esalta = 1; #1#1 = afecha + 1; |PINTA #1#1; |FINSI;
|FPRC;

|PROCESO saldo; |TIPO 0;
 esalta = (FEntrada / 100) ! 0;
 |SI #1#1 < fec1;|O #1#1 > fec2;
     |MENAV "    ERROR ESTA FECHA NO CORRESPONDE AL EJERCICIO ";
     |ERROR;
 |SINO;
     #2#0 = #1#3 - #1#2;
     |ATRI I;
     |PINTA 0967, #2#0;
     |ATRI 0;
 |FINSI;
|FPRC;

|PROCESO la_fecha; |TIPO 13;
    #2#0 = #1#2 - #1#3;
    |ATRI I;
    |PINTA 0967, #2#0;
    |PINTA 0739, "             ";
    |PINTA 0753, "             ";
    |PINTA 0767, "             ";
    |PINTA 0639, "                                     ";
    |ATRI 0;
|FPRC;

|PROCESO def_dia; |TIPO 7;
 esalta = (FEntrada / 100) ! 0;
 |SI esalta = 1; #1#0 = adiari; |PINTA #1#0; |FINSI;
|FPRC;

|PROCESO leedocum;
  enDiario = #0#0;
  |HAZ contador;
|FPRC;

|PROCESO def_doc; |TIPO 7;
 esalta = (FEntrada / 100) ! 0;
 |SI esalta ! 1; |ACABA; |FINSI;
 |SI paso = 0;
     |HAZ leedocum;
     adocum = enContador;
     paso = 1;
 |FINSI;
 #1#4 = adocum + 1;
 |PINTA #1#4;
|FPRC;

|PROCESO guarda_doc; |TIPO 0;
 |SI FSalida = 9;
     |HAZ leedocum;
     #1#4 = enContador;
     |PINTA #1#4;
     |ERROR;
     |ACABA;
 |FINSI;
|FPRC;

|PROCESO guarda_def; |TIPO 30;
 sw_movim = 1;
 esalta = (FEntrada / 100) ! 0;
 |SI esalta = 1;
     afecha = #1#1;
     adiari = #1#0;
     adocum = #1#4;
 |FINSI;
|FPRC;

|PROCESO modbaj; |TIPO 1;
  esalta = (FEntrada / 100) ! 0;
  |SI esalta= 2;
     |ABRE #0;
     #0#1 = #1#1;
     #0#3 = 9999;
     |LEE 001#0];
     |SI FSalida = 0;
         |LEE 001#0a;
         |SI FSalida ! 0; |O #0#1 ! #1#1;
             |CIERRA #0;
             |ACABA;
         |FINSI;
     |SINO;
         |LEE 001#0u;
         |SI FSalida ! 0; |O #0#1 ! #1#1;
             |CIERRA #0;
             |ACABA;
         |FINSI;
     |FINSI;
      cuant = #0#4;
      diant = #0#5;
      coant = #0#6;
      deant = #0#7;
      dpant = #0#12;
      dhant = #0#8;
      imant = #0#9;
     |CIERRA #0;
 |FINSI;
|FPRC;

|PROCESO aviso5;         || desde calculo 'c_importe' (camaasil)
|PUSHV 09101171;
|HAZ color_f;
|CUADRO 09 10 11 71;
||            123456789012345678901234567890123456789012345678901234567890
|HAZ color_c;

|QBF avisos;
    alfa1 = avisos % 0;  nume1 = alfa1;
    nume2 = ((60 - nume1) / 2) ! 0;     nume1 = 60 - nume1 - nume2;
    avisos = (" " * nume1) + avisos + (" " * nume2);        || lo centra

|PINTA 1011, avisos;
|ATRI 0;
|AVISO;
|FPRC;

|PROCESO aviso9;         || desde calculo 'descuadre', 'modbaj'
|ATRI 0;
|POPV;
|FPRC;

|PROCESO color_c;        || activa el color caracter en avisos
|COLOR 14, 0;
|ATRI 0;
|FPRC;

|PROCESO color_f;        || activa el color graficos en avisos
|COLOR 4, 0;
|ATRI 0;
|FPRC;
