|FICHEROS;
  capas110 #0;
  caman110 #1;
  caportad #2;

  canempre #30;

  labempr@ #50; || labempre #50;
  labtrab@ #51; || labtraba #51;
  nomconc@ #52; || nomconca #52;
  nomconl@ #53; || nomconli #53;

  eosm0030 #54;
  eosm0031 #55;
  eosm0032 #56;

  agicontr #99;
|FIN;

|VARIABLES;
  agi       = "";
  path_fich = "";
  path_pan  = "";
  path_def  = "";

  aDef50    = "";
  aDef51    = "";
  aDef52    = "";
  aDef53    = "";

  aAlfa     = "";
  nInkey    = 0;

  primer    = 0;
  segund    = 0;
  tercer    = 0;
  cuarto    = 0;

  trabajador = 0;
  x      = 0;
  letra  = "";
  irpf   = 0;
  desde  = 0;
  hasta  = 0;
  desde1 = 0;
  hasta1 = 0;
  dnitra = "";
  dnicon = "";
  encontrado = 0;

  para1 = 0;
  aClave = "";
  nMes = 0;
  nume2 = 0;
  nBase = 0;
  nPorc = 0;
  nCuot = 0;
  nGtos = 0;
  aSubClave = "";
  nEspe = 0;
  nPorE = 0;
  nCuoE = 0;
  aRepe = "";
  nTipo = 0;
  nTipoA = 0;
  nAnyo = 0;
  nNume = 0;
  nNume1 = 0;
|FIN;


|INCLUYE acceso_i;

|PROCESO tipo8;  |TIPO 8;

  |HAZ inicio;

  |DBASS agi;
  |PATH_DAT #99 agi;
  |ABRE #99;
  |LEE 040#99p;
  |SI FSalida ! 0;
      |MENAV "      No existe <AGIFIGE>";
      |PTEC 807;
      |CIERRA #99;
      |ACABA;
  |FINSI;
  |CIERRA #99;

  path_fich = #99#2; |QBF path_fich;
  path_pan  = (path_fich - "fich/") + "pan/"
  path_def  = (path_fich - "fich/") + "def/"
  |SI path_fich  = "*";
      |MENAV "      No esta activada la aplicacion de nominas en el <AGIFIGE>";
      |PTEC 807;
      |ACABA;
  |FINSI;

  aDef50 = path_def + "labempre.mas";
  |CARGA_DEF aDef50, labempr@;
  |SI FSalida ! 0;
      aAlfa = "    Error cargando def " + aDef50;
      |MENAV aAlfa;
      aDef50 = "";
      |PTEC 807;
      |ACABA;
  |FINSI;
  aDef51 = path_def + "labtraba.mas";
  |CARGA_DEF aDef51, labtrab@;
  |SI FSalida ! 0;
      aAlfa = "    Error cargando def " + aDef51;
      |MENAV aAlfa;
      aDef51 = "";
      |PTEC 807;
      |ACABA;
  |FINSI;

  aDef52 = path_def + "nomconca.mas";
  |CARGA_DEF aDef52, nomconc@;
  |SI FSalida ! 0;
      aAlfa = "    Error cargando def " + aDef52;
      |MENAV aAlfa;
      aDef52 = "";
      |PTEC 807;
      |ACABA;
  |FINSI;

  aDef53 = path_def + "nomconli.mas";
  |CARGA_DEF aDef53, nomconl@;
  |SI FSalida ! 0;
      aAlfa = "    Error cargando def " + aDef53;
      |MENAV aAlfa;
      aDef53 = "";
      |PTEC 807;
      |ACABA;
  |FINSI;

  |PATH_DAT #50 path_fich; |PATH_PAN #50 path_pan;
  |PATH_DAT #51 path_fich; |PATH_PAN #51 path_pan;
  |PATH_DAT #52 path_fich; |PATH_PAN #52 path_pan;
  |PATH_DAT #53 path_fich; |PATH_PAN #53 path_pan;
  |PATH_DAT #54 path_fich; |PATH_PAN #54 path_pan;
  |PATH_DAT #55 path_fich; |PATH_PAN #55 path_pan;
  |PATH_DAT #56 path_fich; |PATH_PAN #56 path_pan;
|FPRC;

|PROCESO Tipo9; |TIPO 9;
 |SI aDef53 ! ""; |DESCARGA_DEF #53; aDef53 = ""; |FINSI;
 |SI aDef52 ! ""; |DESCARGA_DEF #52; aDef52 = ""; |FINSI;
 |SI aDef51 ! ""; |DESCARGA_DEF #51; aDef51 = ""; |FINSI;
 |SI aDef50 ! ""; |DESCARGA_DEF #50; aDef50 = ""; |FINSI;
|FPRC;

|| ------------------------------------------------------------

|PROCESO sacacampos;  |TIPO 7;
  #0#0 = cod1;
  #0#1 = #30#1;
  #0#2 = #30#26;
  |PINTA #0#0;
  |PINTA #0#1;
  |PINTA #0#2;
|FPRC;

|PROCESO LaEmpresaNom; |TIPO 0;
  nInkey = FSalida;
  |ABRE #50;
  |SI nInkey = 10;
      #50#0 = #0Campo;
      |LEE 000#50];
      |CONSULTA_CLAVE #1#50;
      |SI FSalida = 0;
          #0Campo = #50#0; |PINTA #0Campo;
      |FINSI;
  |FINSI;
  #50#0 = #0Campo;
  |LEE 000#50=;
  |SI FSalida ! 0;
      |MENAV "    Error. No existe Empresa en Nominas";
      |ERROR;
  |SINO;
      #0#4 = #50#2; |PINTA #0#4;
  |FINSI;
  |CIERRA #50;
|FPRC;

|| -----------------------------------------------------------------------
|PROCESO BorroClavesEste;
  nMes = #56#6; |SI nMes > 12; |ACABA; |FINSI;

  |SI nMes ] 1; |Y nMes [ 3;
      |SI primer = 0; |ACABA; |FINSI;
  |FINSI;

  |SI nMes ] 4; |Y nMes [ 6;
      |SI segund = 0; |ACABA; |FINSI;
  |FINSI;

  |SI nMes ] 7; |Y nMes [ 9;
      |SI tercer = 0; |ACABA; |FINSI;
  |FINSI;

  |SI nMes ] 10; |Y nMes [ 12;
      |SI cuarto = 0; |ACABA; |FINSI;
  |FINSI;
  #56#7 = 0;
  #56#9 = 0;
  #56#10 = 0;
  #56#12 = 0;
  #56#13 = 0;
  #56#15 = 0;
  |GRABA 020#56a;
  |LIBERA #56;
|FPRC;

|DEFBUCLE LeeClavesEste;
 #56#1;
 ;
 #0#3, nAnyo, #1#7, " ", "  ", 0, 0000;
 #0#3, nAnyo, #1#7, "z", "zz", 9, 9999;
 e;
 NULL, BorroClavesEste;
|FIN;
|| //////////////////////////////////////////////////////////////



|RUTINA cargatabla;
  hasta = desde1 + 23;
  hasta1 = 0;
  |PARA x = desde1;  |SI x [ hasta;  |HACIENDO x = x + 1;
        hasta1 = hasta1 + 1;
        #51x = #51x + #1desde;
        |SI hasta1 = 12;  desde = desde + 1;  |FINSI;
        desde = desde + 1;
  |SIGUE;
|FRUT;

|RUTINA buscaletra;
  |SI #51#148 = " ";  |O letra = #51#148;
      |SI #51#148 = " ";  #51#148 = letra;  #51#30  = irpf;  |FINSI;
      desde1 = 99;
      |HAZ cargatabla;
  |FINSI;
  |SI #51#228 = " ";  |O letra = #51#228;
      |SI #51#228 = " ";  #51#228 = letra;  #51#174 = irpf;  |FINSI;
      desde1 = 176;
      |HAZ cargatabla;
  |FINSI;
  |SI #51#229 = " ";  |O letra = #51#29;
      |SI #51#229 = " ";  #51#229 = letra;  #51#175 = irpf;  |FINSI;
      desde1 = 200;
      |HAZ cargatabla;
  |FINSI;
|FRUT;

|| \\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\

|PROCESO ClavesCab;

     nNume = 0;
     aClave = #1#12;
     nPorc  = #1#9;
     |HAZ ClaveLin;
     aClave = #1#13;
     nPorc  = #1#10;
     |HAZ ClaveLin;
     aClave = #1#14;
     nPorc  = #1#11;
     |HAZ ClaveLin;
     |PARA para1 = 1; |SI para1 [ 12; |HACIENDO para1 = para1 + 1;
          aClave = "E";
          nMes = para1;
          nume2 = para1 + 100;  nBase = #1nume2;
          |SI nBase ! 0;
              |HAZ ClaveLin;
              |SAL;
          |FINSI;
     |SIGUE;
|FPRC;

|PROCESO ClaveLin;

     aClave = aClave > aClave;

     |SI aClave = " ";             |ACABA;   |FINSI;

     aSubClave = "  ";
     nEspe = 0;
     aRepe = "N";
               |SI aClave = "K";|O aClave = "Y";
                    aClave = "H";
                    aSubClave = "01";
               |SINO;
                    |SI aClave = "L";|O aClave = "Z";
                         aClave = "H";
                         aSubClave = "01";
                         nEspe = 1;
                         |SI nPorc ! 0; aRepe = "S";   |FINSI;
                    |SINO;
                         |SI aClave = "E";
                              aClave = "L";
                              aSubClave = "01";
                         |SINO;
                              |SI aClave = "G";
                                   aSubClave = "01";
                              |SINO;
                                   |SI aClave = "H";
                                        aClave = "G";
                                        aSubClave = "01";
                                        nEspe = 1;
                                        |SI nPorc ! 0; aRepe = "S";   |FINSI;
                                   |FINSI;
                              |FINSI;
                         |FINSI;
                    |FINSI;
               |FINSI;

   nNume  = nNume + 1; |SI nNume > 3; |ACABA; |FINSI;
   nNume1 = nNume + 98;
   #51nNume1 = aClave;
   nNume1 = nNume + 101;
   #51nNume1 = aSubClave;
   nNume1 = nNume + 104;
   #51nNume1 = "N"; |SI nEspe = 1; #51nNume1 = "S"; |FINSI;
   nNume1 = nNume + 107;
   #51nNume1 = aRepe;
|FPRC;

|| \\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\
|PROCESO miratraba;

  dnitra = #51#7; |QBT dnitra;
  dnicon = #1#7;  |QBT dnicon;
  |SI dnitra = dnicon;
      trabajador = #51#1;
      encontrado = 1;
      |ERROR10;
      |ACABA;
  |FINSI;
  |SI #51#1 ] #0#8;  |Y #51#1 [ #0#9;
      trabajador = #51#1;
  |FINSI;
|FPRC;

|DEFBUCLE labtraba;
 #51#1002;
 ;
 #0#3, 00000;
 #0#3, 99999;
 e;
 NULL, miratraba;
|FIN;

|PROCESO  buscatrabajador;
  |BUCLE labtraba;
  |SI encontrado = 1
      #1#136 = trabajador;
  |SINO;
      trabajador = trabajador + 1;
      #1#136 = trabajador;
  |FINSI;
  |GRABA 101#1a;
|FPRC;
|| ////////////////////////////////////////////////////////////

|PROCESO GrabaLin;

     aClave = aClave > aClave;

     |SI aClave = " ";  |ACABA;   |FINSI;
     |SI nBase = 0;|Y nCuot = 0;
         |ACABA;
     |FINSI;

     |SI nMes ] 1; |Y nMes [ 3;
         |SI primer = 0; |ACABA; |FINSI;
     |FINSI;

     |SI nMes ] 4; |Y nMes [ 6;
         |SI segund = 0; |ACABA; |FINSI;
     |FINSI;

     |SI nMes ] 7; |Y nMes [ 9;
         |SI tercer = 0; |ACABA; |FINSI;
     |FINSI;

     |SI nMes ] 10; |Y nMes [ 12;
         |SI cuarto = 0; |ACABA; |FINSI;
     |FINSI;

     aSubClave = "  ";
     nEspe = 0;
     nPorE = 0;
     nCuoE = 0;
     aRepe = "N";
               |SI aClave = "K";|O aClave = "Y";
                    aClave = "H";
                    aSubClave = "01";
               |SINO;
                    |SI aClave = "L";|O aClave = "Z";
                         aClave = "H";
                         aSubClave = "01";
                         nEspe = nBase; nBase = 0;
                         nPorE = nPorc; nPorc = 0;
                         nCuoE = nCuot; nCuot = 0;
                         |SI nPorE ! 0; aRepe = "S";   |FINSI;
                    |SINO;
                         |SI aClave = "E";
                              aClave = "L";
                              aSubClave = "01";
                         |SINO;
                              |SI aClave = "G";
                                   aSubClave = "01";
                              |SINO;
                                   |SI aClave = "H";
                                        aClave = "G";
                                        aSubClave = "01";
                                        nEspe = nBase; nBase = 0;
                                        nPorE = nPorc; nPorc = 0;
                                        nCuoE = nCuot; nCuot = 0;
                                        |SI nPorE ! 0; aRepe = "S";   |FINSI;
                                   |FINSI;
                              |FINSI;
                         |FINSI;
                    |FINSI;
               |FINSI;

     nTipo = 0;
     |SI aClave = "A";
          nTipo = nTipoA;
          |SI nTipo = 0; nTipo = 1;     |FINSI;
     |FINSI;

     |DDEFECTO #eosm0032;
     #eosm0032#0 = #0#3;    || empresa
     #eosm0032#1 = nAnyo;          || a¤o
     #eosm0032#2 = #1#7;    || nif
     #eosm0032#3 = aClave;         || clave
     #eosm0032#4 = aSubClave;      || subclave
     #eosm0032#5 = nTipo;          || tipo relacion
     #eosm0032#6 = nMes;           || mes
     |LEE 101#eosm0032.=;
     |SI FSalida ! 0;
          |GRABA 020#eosm0032.b;
     |FINSI;

     #eosm0032#7 = #eosm0032#7 + nBase;      || base
     #eosm0032#9 = #eosm0032#9 + nCuot;      || cuota
     #eosm0032#8 = (#eosm0032#9 * 100) / #eosm0032#7;       || (%) dinerarias
     #eosm0032#10 = #eosm0032#10 + nEspe;    || en especie
     #eosm0032#12 = #eosm0032#12 + nCuoE;    || cuota en especie
     #eosm0032#11 = (#eosm0032#12 * 100) / #eosm0032#10;    || (%) en especie
     #eosm0032#13 = aRepe;                   || repercute
     #eosm0032#15 = #eosm0032#15 + nGtos;    || gastos 17.3
     |GRABA 020#eosm0032.a;
     |LIBERA #eosm0032;
|FPRC;

|PROCESO GrabaCab;

     |DDEFECTO #eosm0030;
     #eosm0030#0 = #0#3;
     #eosm0030#1 = nAnyo;
     |LEE 101#eosm0030.=;
     |SI FSalida ! 0;
          |DDEFECTO #caportad;
          #caportad#0 = #1#0;
          |LEE 000#caportad.=;

          #eosm0030#2 = #1#2;
          #eosm0030#3 = " ";
          #eosm0030#16 = #caportad#46;
          #eosm0030#17 = #caportad#39;
          ||#eosm0030#18 = #labempre#18;
          |GRABA 020#eosm0030.n;
     |FINSI;
     |LIBERA #eosm0030;

     |DDEFECTO #eosm0031;
     #eosm0031#0 = #0#3;
     #eosm0031#1 = nAnyo;
     #eosm0031#2 = #1#7;
     |LEE 101#eosm0031.=;
     |SI FSalida ! 0;
          #eosm0031#3 = #1#2;
          |GRABA 020#eosm0031.n;
     |FINSI;
     |LIBERA #eosm0031;
|FPRC;

|PROCESO Trabajador;

     |HAZ GrabaCab;

     |SI #0#7 = "S";
         |BUCLE LeeClavesEste;
     |FINSI;

     |PARA para1 = 1; |SI para1 [ 12; |HACIENDO para1 = para1 + 1;
          aClave = #1#12;
          nMes   = para1;

          nume2 = para1 + 22;   nBase = #1nume2;
          nPorc = #1#9;
          nume2 = para1 + 35;   nCuot = #1nume2;
          nume2 = para1 + 113;  nGtos = #1nume2;
          |HAZ GrabaLin;
     |SIGUE;

     |PARA para1 = 1; |SI para1 [ 12; |HACIENDO para1 = para1 + 1;
          aClave = #1#13;
          nMes = para1;

          nume2 = para1 + 48;  nBase = #1nume2;
          nPorc = #1#10;
          nume2 = para1 + 61;  nCuot = #1nume2;
          nGtos = 0;
          |HAZ GrabaLin;
     |SIGUE;

     |PARA para1 = 1; |SI para1 [ 12; |HACIENDO para1 = para1 + 1;
          aClave = #1#14;
          nMes = para1;

          nume2 = para1 + 74;  nBase = #1nume2;
          nPorc = #1#11;
          nume2 = para1 + 87;  nCuot = #1nume2;
          nGtos = 0;
          |HAZ GrabaLin;
     |SIGUE;

     |PARA para1 = 1; |SI para1 [ 12; |HACIENDO para1 = para1 + 1;
          aClave = "E";
          nMes = para1;

          nume2 = para1 + 100;  nBase = #1nume2;
          nPorc = 0;
          nCuot = 0;
          nGtos = 0;
          |HAZ GrabaLin;
     |SIGUE;

|FPRC;

|PROCESO caman110;

  |ABRE #54;
  |ABRE #55;
  |ABRE #56;

  nAnyo = #1#134;

  |SI nAnyo ] 80;
     nAnyo = nAnyo + 1900;
  |SINO;
     nAnyo = nAnyo + 2000;
  |FINSI;

  |HAZ Trabajador;
  |CIERRA #56;
  |CIERRA #55;
  |CIERRA #54;


  |SI #1#136 = 0;
      encontrado = 0;
      trabajador = #0#8;
      |HAZ buscatrabajador;
  |FINSI;

  #50#0 = #0#3;
  |LEE 140#50=;
  |SI FSalida ! 0; |DDEFECTO #50; |FINSI;

  #51#0 = #0#3;
  #51#1 = #1#136;
  |LIBERA #51;
  |LEE 140#51=;
  |SI FSalida ! 0;
      |DDEFECTO #51;
      #51#0   = #0#3;     || Empresa
      #51#1   = #1#136;   || Trabajador
      #51#2   = #1#3;     || Nombre
      #51#3   = #1#127;   || Domicilio
      #51#4   = #1#128;   || Numero
      #51#5   = #1#130;   || Piso
      #51#6   = #1#4;     || Localidad
      #51#7   = #1#7;     || D.N.I.
      #51#8   = #1#5;     || Codigo Postal 1
      #51#9   = #1#6;     || Codigo Postal 2
      #51#10  = #1#132;   || Provincia
      #51#26  = #1#7;     || Numero de Demanda

      #51#44  = "A";      || Situacion Baja
      #51#87  = #50#1;    || Convenio

      #51#30  = #1#9;     || % I.R.P.F. 1
      #51#174 = #1#10;    || % I.R.P.F. 1
      #51#175 = #1#11;    || % I.R.P.F. 1

      #51#99   = #1#12;    || CLAVE 1
      #51#100  = #1#13;    || CLAVE 2
      #51#101  = #1#14;    || CLAVE 3
      |SI #51#99 = "A"; |O #51#100 = "A"; |O #51#101 = "A";
          #51#111 = 2;
      |SINO;
          #51#111 = 0;
      |FINSI;
      |HAZ ClavesCab;
      |GRABA 140#51n;
  |SINO;
      |GRABA 140#51a;
  |FINSI;
|FPRC;

|DEFBUCLE caman110;
 #1#1;
 ;
 #0#0, 00001, #0#2;
 #0#0, 99999, #0#2;
 be;
 NULL, caman110;
|FIN;

|| ************************************************************************
|PROCESO tipo3;  |TIPO 3;
  primer = 0;
  segund = 0;
  tercer = 0;
  cuarto = 0;
  |SI #0#5 = 1;  primer = 1;  segund = 1;  tercer = 1;  cuarto = 1;  |FINSI;
  |SI #0#5 = 2;               segund = 1;  tercer = 1;  cuarto = 1;  |FINSI;
  |SI #0#5 = 3;                            tercer = 1;  cuarto = 1;  |FINSI;
  |SI #0#5 = 4;                                         cuarto = 1;  |FINSI;
  |SI #0#6 = 1;               segund = 0;  tercer = 0;  cuarto = 0;  |FINSI;
  |SI #0#6 = 2;                            tercer = 0;  cuarto = 0;  |FINSI;
  |SI #0#6 = 3;                                         cuarto = 0;  |FINSI;
  |SI #0#6 = 4;                                                      |FINSI;

  |ABRE #50;
  |ABRET #51;
  |ABRE #52;
  |ABRE #53;
  |BUCLE caman110;
  |CIERRA #50;
  |CIERRAT #51;
  |CIERRA #52;
  |CIERRA #53;

|FPRC;
