|FICHEROS;
    c38yp001 #0;
    ca8yp001 #100;

    cacuenta #1;         || cuentas
    ca8wb001 #3;         || temporal Totales Texto
    cabaexp1 #4;         || temporal existencias
    ca8wb002 #5;         || temporal Cuentas

    caexistc #6;         || descarga cabs.
    caexistl #7;         || descarga lins.

    canempre #30;        || empresas
    caconimp #8;

    cuentas@ #901;

    ca8cumay #410;  || Control de Balances
    ca8m0000 #420;  || Descripcion Numero de Balance
    ca8m0001 #421;  || Def Balance Textos
    ca8m0002 #422;  || Def Balance Linea Cuentas
|FIN;

|VARIABLES;
   &eaValeCero = "N";
   &aparam =  "";
   aFichero = "";
   nBalance = 0;
   nBalNum  = 0;
   nImpoOri = 0;
   nImporte = 0;
   nImporte1= 0;
   nImporte2= 0;
   nImporte3= 0;
   nImporte4= 0;
   nImporte5= 0;
   nImporte6= 0;
   nLong    = 0;
   nLongT   = 0;
   aTitulo  = "";
   aMas     = "";
   aDef     = "";
   aAPR     = "";
   aCuenta  = "";
   aAlfa1   = "";
   acumula = 0;
   e1 = 0;
   e2 = 0;
   e3 = 0;
   e4 = 0;
   nCampo  = 0;
   nCampo2 = 0;
   aCampo3 = "";
   nCampo4 = 0;
   nCampo5 = 0;
   nCampo6 = 0;
   nCampo7 = 0;
   nCampo8 = 0;
   nSI     = 0;
   aCtaM   = "";
   aDCta   = "";
   aHCta   = "";
   nNivel  = 0;

   NumMes  = 0;
   NombreMes = "";
   d_cuen    = "";
   h_cuen    = "";
   d_digi    = 0;
   h_digi    = 0;

   &pathbal = "";
   &balance = 1;
    &entrada = 1;
    nNume1 = 0;
    mes = 0;
    nivel = 0;
    linea = 0;
    FEstado = 0;
    activo = 0;
    pasivo = 0;
    activ1 = 0;
    pasiv1 = 0;
    explo = 0;
    expl1 = 0;
    debe1 = 0;
    haber1 = 0;
    saldo  = 0;
    x = 0;
    y = 0;
    &eldesde = 0;
    &elhasta = 0;
    sw = 0;
    agrupaci = 0;
    subagrup = 0;
    epigrafe = 0;
    sw_epi = 0;
    d = "A";
    h = "P";
    tipo = "";
    tipo1 = "";
    cue_perdi = "";
    dig_perdi = 0;
    &sw_inf = 0;
    &cantidad = 0;
    &variable = "";
    &canti = 0;
    &doh = "";
    mes_fin = 0;
    descar = 0;
    traraf = 0;
    informe = "";
    alfa1 = "";
   &r_emp = 0;
   &r_per = 0;
   &anu1 = 0;
   &anu  =0;
   dos = 0;
   &cta = "";
   &nom = "";
   &canti2 = 0;
   &cantidad2 = 0;
   act_cuent = "";
   alfa2 = "";
   alfa3 = "";
   tracan5 = 0;
   &d_mes = 0;
   &h_mes = 0;
   &estru = 0;
   &r_eje = 0;
   &r_nom = "";
   nom1 = "";
   sw_exp = 0;
   descn = 0;
   swexis = 0;

  nDEstru = 0;
  nHEstru = 0;
   &MaximoDigitos = 0;
   nImprimir    = 0;
   aSigno       = "";

   CodigoEmpresa    = 0;
   EjercicioEmpresa = 0;
   NombreEmpresa    = "";
   nSwMen           = 0;
   &enNumPag        = 0;
   nSwLInv          = 0;
   Comodin          = "";
   Comodin1         = "";
   swNoExiste       = 0;
   Calc             = 0;
   Compara          = 0;
   Per1             = 0;
   Per2             = 0;
   &dirfich0   = "";
   nOpera   = 0;
   nHay     = 0;
   nDCampo5 = 0;
   nDCampo6 = 0;
   nDCampo7 = 0;
   nDCampo8 = 0;
   nHCampo5 = 0;
   nHCampo6 = 0;
   nHCampo7 = 0;
   nHCampo8 = 0;

   leidos   = 0;
   primvez  = 0;
   xx       = 0;
   yy       = 0;
   num1     = 0;
   num2     = 0;
   num3     = 0;
   desde    = 0;
   hasta    = 0;
   emp1     = 0;
   per1     = 0;
   men9 = "     Empresa Contable INACTIVA ";
   sw_error = 0;
  nPara1    = 0;
|FIN;

|INCLUYE varnue_i;
|INCLUYE acceso_i;
|INCLUYE digito_i;
|INCLUYE dsmone_i;

|CALCULO coloca; |TIPO 8;
   nSwMen = 0;
   |HAZ inicio;
   |HAZ eVarEmpresa;
   anu = 1900 + #30#26; |SI anu [ 1980; anu = anu + 100; |FINSI;
   anu = 1900 + #30#26; |SI anu [ 1980; anu = anu + 100; |FINSI;
   nBalance = 2; || a) estado de ingresos y gastos P.Neto
                 || eNum2008 = Codigo de las Cuentas de Mayor

   |DFICE dirfich0;
|FCAL;

|| ========================================================================
||                        CALCULOS PANTALLA
|| ========================================================================
|PROCESO consulta;
  |SI FSalida ! 9;  |ACABA;  |FINSI;
  |SUB_EJECUTA "caconemp.run";
  |ERROR;
  |SI Campo = 14;
      #0#14 = r_emp;
      #0#15 = r_per;
      |PTEC 802;
      |PTEC 802;
  |FINSI;
  |SI Campo = 35;
      #0#35 = r_emp;
      #0#37 = r_per;
      |PTEC 802;
  |FINSI;
  |SI Campo = 36;
      #0#36 = r_emp;
      |PTEC 802;
  |FINSI;
  |SI Campo ] 39;
      #0Campo = r_emp;
      nNume1 = Campo + 20;
      #0nNume1 = r_per;
      |PTEC 802;
      |PTEC 802;
  |FINSI;
|FPRC;

|PROCESO mayor1;
  |SI #0Campo = "N";
      |PARA x = 39;  |SI x [ 78;  |HACIENDO x = x + 1;
            |CAMPO_MODIFICA #0x,1,"S";
      |SIGUE;
      #0#35 = 0;
      #0#36 = 0;
      #0#37 = 0;
      #0#38 = 0;
      |CAMPO_MODIFICA #0#35,1,"N";
      |CAMPO_MODIFICA #0#36,1,"N";
      |CAMPO_MODIFICA #0#37,1,"N";
      |CAMPO_MODIFICA #0#38,1,"N";
      |PINTA #0#35;
      |PINTA #0#36;
      |PINTA #0#37;
      |PINTA #0#38;
  |SINO;
      |PARA x = 39;  |SI x [ 78;  |HACIENDO x = x + 1;
            #0x = 0;
            |CAMPO_MODIFICA #0x,1,"N";
      |SIGUE;
      |CAMPO_MODIFICA #0#35,1,"S";
      |CAMPO_MODIFICA #0#36,1,"S";
      |CAMPO_MODIFICA #0#37,1,"S";
      |SI #0#33 = "N";
          |CAMPO_MODIFICA #0#38,1,"S";
      |SINO;
          |CAMPO_MODIFICA #0#38,1,"N";
      |FINSI;
  |FINSI;
|FPRC;

|PROCESO buscaem1;
  |SI #30#21 = "I"; |ACABA; |FINSI;
  |SI #30#26 ] 80;
      enEjercicio = #30#26 + 1900;
  |SINO;
      enEjercicio = #30#26 + 2000;
  |FINSI;
  |SI enEjercicio < 2008; |ACABA; |FINSI;
  leidos = 1;
  |SI primvez = 0; |Y #0#33 = "S";
      num1 = #30#2;
      num2 = #30#3;
      num3 = num2 - 1;
      |SI num3 < 0;  num3 = 9;  |FINSI;
      #30#2 = num1;
      #30#3 = num3;
      |LEE 141#30=;
      |SI FSalida ! 0;
          alfa1 = num1;
          alfa2 = num3;
          aAlfa1 = "     Empresa: " + alfa1 + "  " + alfa2;
          sw_error = 3;
      |FINSI;
      #30#2 = num1;
      #30#3 = num2;
      |LEE 141#30=;
      |SI sw_error = 3;
          |ERROR10;
          |ACABA;
      |FINSI;
  |FINSI;
  |HAZ caldigitos;
  |SI sw_error = 1;
      alfa1 = #30#2;
      alfa2 = #30#3;
      aAlfa1 = "      Empresa: " + alfa1 + " " + alfa2;
      |ERROR10;
      |ACABA;
  |FINSI;
  alfa1 = #30#2;
  alfa2 = #30#3;
  |HAZ estruc;
  |SI sw_error = 1;
      sw_error = 2;
      alfa1 = #30#2;
      alfa2 = #30#3;
      aAlfa1 = "      Empresa: " + alfa1 + " " + alfa2;
      |ERROR10;
  |FINSI;
|FPRC;

|DEFBUCLE buscaempre;
  #30#1;
  ;
  #0#35,desde;
  #0#36,hasta;
  e;
  NULL,buscaem1;
|FIN;

|PROCESO periodes;
  |SI #0#33 = "N";
      #0#38 = #0Campo;
      |SI #0#34 = "N";
          |CAMPO_MODIFICA #0#38,1,"N";
      |SINO;
          |CAMPO_MODIFICA #0#38,1,"S";
      |FINSI;
  |SINO;
      #0#38 = #0Campo;
      |CAMPO_MODIFICA #0#38,1,"N";
      |PINTA #0#38;
  |FINSI;
|FPRC;

|PROCESO buclempre;
  |SI #0#37 > #0#38;
      |MENAV "     ERROR EN INTRODUCCION";
      |ERROR;
      |ACABA;
  |FINSI;
  |SI #0#35 = 0;  |Y #0#36 = 0;  |ACABA;  |FINSI;
  desde = #0#37;
  hasta = #0#38;
  primvez = 0;
|ET segundo;
  sw_error = 0;
  leidos = 0;
  |BUCLE buscaempre;
  |SI leidos = 0;
      |MENAV "    ATENCION SELECCION VACIA";
      |ERROR;
      |ACABA;
  |FINSI;
  |SI sw_error = 1;
      sw_error = 0;
      aAlfa1 = aAlfa1 + " NO SE CORRESPONDEN LOS NIVELES:";
      |MENAV aAlfa1;
      |ERROR;
      |ACABA;
  |FINSI;
  |SI sw_error = 2;
      sw_error = 0;
      aAlfa1 = aAlfa1 + " SIN ESTRUCTURAS DE DESCARGA";
      |MENAV aAlfa1;
      |ERROR;
  |FINSI;
  |SI sw_error = 3;
      sw_error = 0;
      aAlfa1 = aAlfa1 + " NO EXISTE EMPRESA";
      |MENAV aAlfa1;
      |ERROR;
  |FINSI;
  |SI #0#33 ! "S";  |O primvez ! 0;
      |ACABA;
  |FINSI;
  primvez = 1;
  desde = #0#37 - 1;
  |SI desde < 0;  desde = 9;  |FINSI;
  hasta = desde;
  |VETE segundo;
|FPRC;

|PROCESO deshas;
  |SI #0#36 < #0#35;
      |MENAV "      Error en Introduccion";
      |ERROR;
  |FINSI;
|FPRC;

|PROCESO pintanter;
  |SI #0#33 = "N";  |ACABA;  |FINSI;
  x = Campo - 19;
  y = Campo - 20;
  |SI #0y = 0;  |ACABA;  |FINSI;
  #0x = #0y;
  |PINTA #0x;
  x = Campo + 1;
  #0x = #0Campo - 1;
  |SI #0x < 0;  #0x = 9;  |FINSI;
  |PINTA #0x;
|FPRC;

|PROCESO nomodi;
      |PARA x = Campo + 1;  |SI x [ 58;  |HACIENDO x = x + 1;
            #0x = 0;
            |CAMPO_MODIFICA #0x,1,"N";
            y = x + 20;
            #0y = 0;
            |CAMPO_MODIFICA #0y,1,"N";
      |SIGUE;
      y = Campo + 20;
      #0y = 0;
      |CAMPO_MODIFICA #0y,1,"N";
      |PINTA #0y;
|FPRC;

|PROCESO esbuena;
  |SI FSalida = 2; |ACABA; |FINSI;
  |SI #0#33 = "S"; |Y #0Campo = 0;
      |SI Campo = 40; |O Campo = 42;  |O Campo = 44;  |O Campo = 46;
                      |O Campo = 48;  |O Campo = 50;  |O Campo = 52;
                      |O Campo = 54;  |O Campo = 56;  |O Campo = 58;
          x = Campo - 1;
          |SI #0x ! 0;
              |MENAV "     DEBE INTRODUCIR UNA EMPRESA QUE EXISTA";
              |ERROR;
              |ACABA;
          |FINSI;
      |FINSI;
  |FINSI;
  |SI #0Campo = 0; |HAZ nomodi;  |ACABA;  |FINSI;
  |ABRE #30;
  #30#2 = #0Campo;
  #30#3 = 0;
  |LEE 011#30];
  |SI FSalida ! 0;  |O #30#2 ! #0Campo;
      |MENAV "     NO Existe la empresa Contable";
      |ERROR;
  |FINSI;
  |CIERRA #30;
|FPRC;

|PROCESO estruc;
  |DBASE dir_fich;
  dir_fich = dir_fich + "fich/";
  alfa1 = "00000" + alfa1;   alfa1 = alfa1 % -105;
  alfa2 = "0"   + alfa2;   alfa2 = alfa2 % -101;
  alfa3 = dir_fich + alfa1 + alfa2 + "/";
  |PATH_DAT #6 alfa3;
  |ABRE #6;
  #6#0 = #0#6;
  |LEE 000#6=;
  |SI FSalida ! 0;  sw_error = 1;  |FINSI;
  |CIERRA #6;
|FPRC;

|PROCESO caldigitos;
  |SI #0#4 = "S"; |Y #0#5 > 4;
      sw_error = 1;
      |SI #30#14 = #0#5;  sw_error = 0;  |ACABA;  |FINSI;
      |SI #30#15 = #0#5;  sw_error = 0;  |ACABA;  |FINSI;
      |SI #30#16 = #0#5;  sw_error = 0;  |ACABA;  |FINSI;
      |SI #30#17 = #0#5;  sw_error = 0;  |ACABA;  |FINSI;
      |SI #30#18 = #0#5;  sw_error = 0;  |ACABA;  |FINSI;
      |SI #30#19 = #0#5;  sw_error = 0;  |ACABA;  |FINSI;
  |FINSI;
|FPRC;

|PROCESO esbuena1;
  |SI FSalida = 2;|ACABA;|FINSI;
  x = Campo - 20;
  |SI #0x = 0;  |ACABA;  |FINSI;
  |ABRE #30;
  x = Campo - 20;
  #30#2 = #0x;
  #30#3 = #0Campo;
  |LEE 011#30=;
  |SI FSalida ! 0;
      |MENAV "     NO Existe Periodo en la empresa Contable";
      |CIERRA #30;
      |ERROR;
      |ACABA;
  |FINSI;
  |CIERRA #30;
  |SI #30#21 = "I";
      |MENAV men9;
      |ERROR;
      |ACABA;
  |FINSI;
  sw_error = 0;
  |HAZ caldigitos;
  |SI sw_error = 1;
      sw_error = 0;
      |MENAV "      NO SE CORRESPONDEN LOS NIVELES:";
      |ERROR;
      |ACABA;
  |FINSI;
  x = Campo + 20;
  #0x = #30#1;
  |PINTA #0x;
  sw_error = 0;
  x = Campo - 20;
  alfa1 = #0x;
  alfa2 = #0Campo;
  |HAZ estruc;
  |SI sw_error = 1;
      sw_error = 0;
      |MENAV "     ATENCION LA EMPRESA NO TIENE ESTRUCTURA DE DESCARGA";
      |ERROR;
      |ACABA;
  |FINSI;
  |HAZ conexi;
|FPRC;

|PROCESO conexi;
  d_mes = #0#7;
  h_mes = #0#8;
  estru = #0#6;
  xx = Campo - 20;
  yy = Campo + 20;
  r_emp = #0xx;
  r_per = #0Campo;
  r_eje = #30#26;
  r_nom = #30#1;
  |SUB_EJECUTA "caexicon.run";
|FPRC;

|PROCESO pintames; |TIPO 0;
  nNume1 = Campo + 2;
  |SI #0Campo ! 0; |Y #0Campo ! 13;
      NumMes = dig1 + (#0Campo - 1);
      |SI NumMes > 12;
          NumMes = NumMes - 12;
      |FINSI;
  |SINO;
      NumMes = #0Campo;
  |FINSI;
  |HAZ NombreMes;
  #0nNume1 = NombreMes; |PINTA #0nNume1;

  |SI Campo = 1; |Y  #0#0 > #0#1;
      |SI nSwMen = 0;
          |MENAV "    Error de Entrada ...";
          |ERROR;
      |FINSI;
  |FINSI;
|FPRC;

|PROCESO pintames2; |TIPO 0;
  |SI #0Campo ! 0; |Y #0Campo ! 13;
      NumMes = dig1 + (#0Campo - 1);
      |SI NumMes > 12;
          NumMes = NumMes - 12;
      |FINSI;
  |SINO;
      NumMes = #0Campo;
  |FINSI;
  |HAZ NombreMes;

  |ATRI I;
  |SI Campo = 7;  |PINTA 1519, NombreMes;  |FINSI;
  |SI Campo = 8;  |PINTA 1554, NombreMes;  |FINSI;
  |ATRI 0;
  |SI Campo = 8;  |Y #0#7 > #0#8;
      |SI nSwMen = 0;
          |MENAV "    Error de Entrada ...";
          |ERROR;
      |FINSI;
  |FINSI;
|FPRC;

|PROCESO NombreMes;
  |SI NumMes = 0;  NombreMes = "Apertura  "; |FINSI;
  |SI NumMes = 1;  NombreMes = "Enero     "; |FINSI;
  |SI NumMes = 2;  NombreMes = "Febrero   "; |FINSI;
  |SI NumMes = 3;  NombreMes = "Marzo     "; |FINSI;
  |SI NumMes = 4;  NombreMes = "Abril     "; |FINSI;
  |SI NumMes = 5;  NombreMes = "Mayo      "; |FINSI;
  |SI NumMes = 6;  NombreMes = "Junio     "; |FINSI;
  |SI NumMes = 7;  NombreMes = "Julio     "; |FINSI;
  |SI NumMes = 8;  NombreMes = "Agosto    "; |FINSI;
  |SI NumMes = 9;  NombreMes = "Septiembre"; |FINSI;
  |SI NumMes = 10; NombreMes = "Octubre   "; |FINSI;
  |SI NumMes = 11; NombreMes = "Noviembre "; |FINSI;
  |SI NumMes = 12; NombreMes = "Diciembre "; |FINSI;
  |SI NumMes = 13; NombreMes = "Cierre    "; |FINSI;
|FPRC;

|PROCESO nivel; |TIPO 7;
 aAlfa1 = #0#4;
 |C_M #0#5, 1,aAlfa1;
 |SI aAlfa1 = "N";
     |SI dig3 = 1; #0#5 = dig4; |FINSI;
     |SI dig3 = 2; #0#5 = dig5; |FINSI;
     |SI dig3 = 3; #0#5 = dig6; |FINSI;
     |SI dig3 = 4; #0#5 = dig7; |FINSI;
     |SI dig3 = 5; #0#5 = dig8; |FINSI;
     |SI dig3 = 6; #0#5 = dig9; |FINSI;
     |PINTA #0#5;
 |FINSI;
|FPRC;

|PROCESO miraniv; |TIPO 0;
  nLongT = #0#5;
  |SI nLongT > 4;
      nivel = 0;
      |SI nLongT = dig4; nivel = 1;  |FINSI;
      |SI nLongT = dig5; nivel = 2;  |FINSI;
      |SI nLongT = dig6; nivel = 3;  |FINSI;
      |SI nLongT = dig7; nivel = 4;  |FINSI;
      |SI nLongT = dig8; nivel = 5;  |FINSI;
      |SI nLongT = dig9; nivel = 6;  |FINSI;
      |SI nivel = 0;
          |SI nSwMen = 0;
              |MENAV "    Error. Longitud incorrecta";
              |ERROR;
          |FINSI;
      |FINSI;
  |FINSI;
|FPRC;

|PROCESO cue_per; || calcula la cuenta de perdidas y ganancias
  cue_perdi = #6#2;
  dig_perdi = #6#3;
  |SI #0#4 = "N";
      aAlfa1 = #6#2; |QBF aAlfa1;
      aAlfa1 = aAlfa1 % 0;
      nLong  = aAlfa1;
  |SINO;
      traraf = 100 + #0#5;
      cue_perdi = #6#2 % traraf;
      dig_perdi = nivel;
  |FINSI;
  cue_perdi = cue_perdi + "            ";
  cue_perdi = cue_perdi % 112;
|FPRC;

|PROCESO descri; |TIPO 6;
 |ABRE #420;
 |DDEFECTO #420;
 #420#0 = #0Campo;
 |LEE 001#420=;
 |SI FSalida ! 0;
     |SI nSwMen = 0;
         |MENAV "   Error. No existe Numero de Balance";
         |ERROR;
     |FINSI;
 |FINSI;
 |CIERRA #420;
 nCampo = Campo + 1;
 #0nCampo = #420#1; |PINTA #0nCampo;
 |SI #420#2 = 3;
     |MENAV "    Numero de Balance PYME, informe no operativo";
     |ERROR;
 |FINSI;
|FPRC;

|| ***************************************************************************
|| *********************  GRABACION DE TEMPORALES Y CALCULOS *****************
|| ***************************************************************************
|PROCESO MSigno_sLeo;
  #410#12 = enEjerBal;
  #410#0  = aCtaM;
  #410#10 = eNum2008;
  |LEE 001#410=;
  |SI FSalida = 0;
      nSI  = 1;
      aAPR = #410#3;
  |FINSI;
|FPRC;

|PROCESO MSigno08_s;
  nSI = 0;
  aCtaM = aCuenta % 105;
  |HAZ MSigno_sLeo;
  |SI nSI = 1; |ACABA; |FINSI;

  aCtaM = aCuenta % 104;
  |HAZ MSigno_sLeo;
  |SI nSI = 1; |ACABA; |FINSI;

  aCtaM = aCuenta % 103;
  |HAZ MSigno_sLeo;
  |SI nSI = 1; |ACABA; |FINSI;

  aCtaM = aCuenta % 102;
  |HAZ MSigno_sLeo;
|FPRC;


|PROCESO BuscaCuenta;
 nNume1  = 100 + nLongT;
 aCuenta = aCuenta % nNume1;

 #901#0 = aCuenta;
 #901#1 = nivel;
 |LEE 001#901=;
 |SI FSalida = 0;
     aTitulo = #901#2;
     aAPR    = #901#5; |HAZ MSigno08_s;
 |SINO;
     |SI nLongT [ 4;
         #410#12 = enEjerBal;
         #410#0  = aCuenta;
         #410#10 = eNum2008;
         |LEE 001#410=;
         |SI FSalida = 0;
             aTitulo = #410#1;
             aAPR    = #410#3;
         |FINSI;
     |FINSI;
 |FINSI;
|FPRC;

|PROCESO CambioCta;
  aAlfa1 = aCuenta; |QBF aAlfa1;
  aAlfa1 = aAlfa1 % 0;
  nLong  = aAlfa1;
  |SI nLong ! nLongT;
      |HAZ BuscaCuenta;
  |FINSI;
|FPRC;
|| .............................................................

|PROCESO cargatra_3;  ||Carga Trabajo de Cuentas
  #3#0 = nBalNum;
  #3#1 = nBalance;
  #3#2 = nCampo2;
  #3#3 = aCampo3;
  #3#4 = nCampo4;
  #3#5 = nCampo5;
  #3#6 = nCampo6;
  #3#7 = nCampo7;
  #3#8 = nCampo8;
  |LEE 101#3=;
  |SI FSalida = 0;
      #3acumula = #3acumula + nImporte;
      |GRABA 020#3a;
  |FINSI;
  |LIBERA #3;
|FPRC;

|PROCESO LosTotales;
  nCampo2 = #5#2;
  aCampo3 = #5#3;
  nCampo4 = #5#4;
  nCampo5 = #5#5;
  nCampo6 = #5#6;
  nCampo7 = #5#7;
  nCampo8 = #5#8;

  |HAZ cargatra_3;

  |SI nCampo8 ! 0;    ||Subpartida (A/P = 0)
      nCampo8 = 0;
      |HAZ cargatra_3;
  |FINSI;

  |SI nCampo7 ! 0;   ||Partida
      nCampo8 = 0;
      nCampo7 = 0;
      |HAZ cargatra_3;
  |FINSI;

  |SI nCampo6 ! 0;   ||Epigrafe
      nCampo8 = 0;
      nCampo7 = 0;
      nCampo6 = 0;
      |HAZ cargatra_3;
  |FINSI;

/*
  |SI nCampo5 ! 0;  ||Subagrupacion
      nCampo8 = 0;
      nCampo7 = 0;
      nCampo6 = 0;
      nCampo5 = 0;
      |HAZ cargatra_3;
  |FINSI;

  |SI nCampo4 ! 0;  ||Agrupacion
      nCampo8 = 0;
      nCampo7 = 0;
      nCampo6 = 0;
      nCampo5 = 0;
      nCampo4 = 0;
      |HAZ cargatra_3;
  |FINSI;

  |SI aCampo3 = "A";
      activo = (activo + nImporte) ! EURODB;
  |SINO;
      pasivo = (pasivo + nImporte) ! EURODB;
  |FINSI;
*/
|FPRC;

|PROCESO cargatra_5;  ||Carga Trabajo de Cuentas
  #5#0 = nBalNum;
  #5#1 = nBalance;
  #5#2 = #422#2;
  #5#3 = #422#3;
  #5#4 = #422#4;
  #5#5 = #422#5;
  #5#6 = #422#6;
  #5#7 = #422#7;
  #5#8 = #422#12;
  #5#9 = #422#8;
  #5#10 = aCuenta;
  |LEE 001#5=;
  |SI FSalida ! 0;
      |DDEFECTO #5;
      #5#0 = nBalNum;
      #5#1 = nBalance;
      #5#2 = #422#2;
      #5#3 = #422#3;
      #5#4 = #422#4;
      #5#5 = #422#5;
      #5#6 = #422#6;
      #5#7 = #422#7;
      #5#8 = #422#12;
      #5#9 = #422#8;
      #5#10 = aCuenta;
      |GRABA 020#5b;
  |FINSI;

  #5#11 = aTitulo;
  #5#12 = nLong;
  #5acumula = #5acumula + nImporte;
  |GRABA 020#5a;
  |LIBERA #5;

  |HAZ LosTotales;
|FPRC;

||......................................................
|PROCESO descarga;
  |HAZ digitos2;
  |SI d_error ! 0;  |ACABA;  |FINSI;

  linea = linea + 1;
  #4#0 = linea;
  #4#8 = #0#6;
  |LEE 001#4=;
  |SI FSalida = 0;  |BORRA 020#4a;  |FINSI;

  |DDEFECTO #4;
  #4#0 = linea;    || linea
  #4#8 = #0#6;     || estructura
  #4#1 = #1#0;     || cuenta
  #4#2 = #1#1;     || digito
  #4#3 = #1#2;     || descripcion

  mes_fin  = #0#7 + 11;  || mes iniciales
  #4e1     = #7mes_fin;    || exist. iniciales
  mes_fin  = #0#8 + 11;  || mes finales
  #4e2     = #7mes_fin;    || exist. finales
  tracan5  = #7mes_fin;

  descar = (#4e1 - #4e2) ! EURODB; || ... iniciales - finales
  |SI descar ] 0;
      #4e3 = #7#5;     || cuenta descarga D
      #4e4 = #7#6;     || digito descarga D
  |SINO;
      #4e3 = #7#8;     || cuenta descarga H
      #4e4 = #7#9;     || digito descarga H
  |FINSI;
  |GRABA 020#4n;
|FPRC;

|PROCESO lineades;
  #1#0 = #7#2;
  #1#1 = #7#3;
  |LEE 000#1=;
  |SI FSalida = 0;
      |HAZ descarga;
  |FINSI;
|FPRC;

|PROCESO pideexis;
  |ABRE #6;
  #6#0 = #0#6;
  |LEE 001#6=;
  |HAZ cue_per;
  |BUCLELIN 2lineades#6;
  |CIERRA #6;
|FPRC;

|PROCESO ver_exp;
 |SI #4e3 = #1#0; |Y #4e4 = #1#1;
     sw_exp = 1;
     descar = (#4e1-#4e2);
     |SI aAPR = "H"; descar = -descar; |FINSI;
     descn = descn + descar;
 |FINSI;
|FPRC;

|DEFBUCLE cabasitu9;
 #4#1;
 ;
 nDEstru, 001;
 nHEstru, 999;
 e;
 NULL, ver_exp;
|FIN;

|PROCESO calexpl01;
  aCuenta = #1#0;
  nNivel  = #1#1;
  aAPR    = #1#5; |HAZ MSigno08_s;
  |SI aAPR ! "D"; |Y aAPR ! "H"; |ACABA; |FINSI;

  |HAZ digitos2;
  |SI d_error ! 0;  |ACABA;  |FINSI;

  sw_exp = 0;
  descn  = 0;
  |BUCLE cabasitu9;
  |SI sw_exp = 1
      |SI aAPR = "D";
          debe1 = debe1 + descn;
      |SINO;
          haber1 = haber1 + descn;
     |FINSI;
  |SINO;
     |SI aAPR = "D";
         |PARA x = eldesde; |SI x [ elhasta; |HACIENDO x = x + 3;
               debe1  = (debe1  + #1x) ! EURODB;
         |SIGUE;
     |SINO;
         |PARA x = eldesde; |SI x [ elhasta; |HACIENDO x = x + 3;
               haber1 = (haber1 - #1x) ! EURODB;
         |SIGUE;
     |FINSI;
 |FINSI;
|FPRC;

|DEFBUCLE calexpl0;
 #1#1;
 3;
 "            ", 0, "S";  || "D", 1, 00, 00, 00,"S";
 "zzzzzzzzzzzz", 9, "S";  || "H", 4, 99, 99, 99,"S";
 e;
 NULL,calexpl01;
|FIN;

|PROCESO explota;
  debe1  = 0;
  haber1 = 0;

  |BUCLE calexpl0;

  explo = (haber1 - debe1) ! EURODB;

  |ABRE #1;
  |DDEFECTO #1;
  #1#0 = #6#2;
  #1#1 = #6#3;
  |LEE 000#1=;
  |CIERRA #1;
  aCuenta = cue_perdi;
  nNivel  = dig_perdi;
  aAPR    = #1#5; |HAZ MSigno08_s;
  aTitulo = #1#2;

  |PARA x = eldesde; |SI x [ elhasta; |HACIENDO x = x + 3;
        explo = explo - #1x;
  |SIGUE;
  nImporte = explo;
  #422#2  = 6;
  #422#3  = "E";
  #422#4  = 1;
  #422#5  = 1;
  #422#6  = 0;
  #422#7  = 0;
  #422#8  = 1;
  #422#12 = 0;
  |HAZ cargatra_5;
|FPRC;

|PROCESO grabatra; || grabo todas
   nImporte = 0;   || haber - debe
   |PARA x = eldesde; |SI x [ elhasta; |HACIENDO x = x + 3;
         nImporte = nImporte - #1x;
   |SIGUE;

  |SI #0#4 = "S"; |HAZ CambioCta; |FINSI;
  |SI nImporte ! 0;
      |SI #422#11 ! " ";
          |SI #422#11 = "P"; |Y nImporte < 0; |ACABA; |FINSI;
          |SI #422#11 = "N"; |Y nImporte > 0; |ACABA; |FINSI;
      |FINSI;
      |SI #422#10 = "-"; |Y aAPR = "G"; nImporte = - nImporte; |FINSI;
      |SI #422#10 = "+"; |Y aAPR = "I"; nImporte = - nImporte; |FINSI;
      |HAZ cargatra_5;
  |FINSI;
|FPRC;

|PROCESO calculit;

  aCuenta = #1#0;
  nNivel  = #1#1;
  aTitulo = #1#2;
  aAPR    = #1#5; |HAZ MSigno08_s;
  |SI aAPR ! "G"; |Y aAPR ! "I"; |ACABA; |FINSI;

  |HAZ digitos2;
  |SI d_error ! 0;  |ACABA;  |FINSI;

  aAlfa1 = #1#0; |QBF aAlfa1;
  aAlfa1 = aAlfa1 % 0;
  nLong  = aAlfa1;
  aCuenta = #1#0;
  nNivel  = #1#1;
  aTitulo = #1#2;
  nImporte = 0;

  |HAZ grabatra;
|FPRC;

|DEFBUCLE cabasitu0;
 #1#1;
 3;
 aDCta, 0, "S";
 aHCta, 9, "S";
 e;
 NULL,calculit;
|FIN;

|PROCESO calculit_4;
 aAlfa1 = #422#9; |QBF aAlfa1;
 aDCta = (aAlfa1 + "            ") % 112;
 aHCta = (aAlfa1 + "zzzzzzzzzzzz") % 112;
 |BUCLE cabasitu0;
|FPRC;

|DEFBUCLE cabasitu4;
 #422#1;
 ;
 enEjerBal, nBalNum, nBalance, 6, "E", 00, 0, 00, 00, 0, 001;
 enEjerBal, nBalNum, nBalance, 9, "E", 99, 9, 99, 99, 9, 999;
 e;
 NULL,calculit_4;
|FIN;

|PROCESO LeoTotales;
 |SI #3#9 = "C"; |O nCampo4 = 1;
     nImporte = nImporte + #3acumula;
 |FINSI;
|FPRC;

|DEFBUCLE LeoTotales;
 #3#1;
 ;
 nBalNum, nBalance, nCampo2, aCampo3, nCampo4, nDCampo5, nDCampo6, 00,0;
 nBalNum, nBalance, nCampo2, aCampo3, nCampo4, nHCampo5, nHCampo6, 99,9;
 ;
 NULL, LeoTotales;
|FIN;

|PROCESO elTotal;
  nImporte = 0;
  nCampo2 = #421#2;  ||Bloque
  aCampo3 = #421#3;  ||Tipo
  nCampo4 = #421#4;  ||Agrupacion
  nCampo5 = #421#5;

  |SI nCampo4 = 01;
      nDCampo5 = 1; nHCampo5 = 1;
      nDCampo6 = 0; nHCampo6 = 0;
      |BUCLE LeoTotales;
  |FINSI;
  |SI nCampo4 = 02; |O nCampo4 = 03;
      nDCampo5 = 0; nHCampo5 = 0;
      nDCampo6 = 1; nHCampo6 = 99;
      |BUCLE LeoTotales;
  |FINSI;
  |SI nCampo4 = 4;
      nImporte = nImporte1 + nImporte2 + nImporte3;
  |FINSI;

|SI nCampo4 ! 1;
  |ABRE #3;
  nCampo6 = 0;
  nCampo7 = 0;
  nCampo8 = 0;
  |HAZ cargatra_3;
  |CIERRA #3;
|FINSI;
  |SI nCampo4 = 1; nImporte1 = nImporte; |FINSI;
  |SI nCampo4 = 2; nImporte2 = nImporte; |FINSI;
  |SI nCampo4 = 3; nImporte3 = nImporte; |FINSI;
  |SI nCampo4 = 4; nImporte4 = nImporte; |FINSI;
|FPRC;

|DEFBUCLE elTotal;
 #421#1;
 ;
 enEjerBal, nBalNum, nBalance, 6, "E", 00, 1, 00, 00, 0;
 enEjerBal, nBalNum, nBalance, 6, "E", 99, 9, 00, 00, 0;
 e;
 NULL, elTotal;
|FIN;

|PROCESO ElCalculoFinal;
 nImporte1 = 0;
 nImporte2 = 0;
 nImporte3 = 0;
 nImporte4 = 0;
 nImporte5 = 0;
 |BUCLE elTotal;
|FPRC;

|| ***************************************************************************
|| ***************************************************************************
|| ***************************************************************************
|| *********************  CALCULO DE IMPRESION *******************************
|| ***************************************************************************

|PROCESO cuentas;
  cantidad = #5#13;
  |SI cantidad ! 0; |O eaValeCero ! "N";
      |PRINT;
  |FINSI;
  sw_inf = 4;
|FPRC;

|DEFBUCLE cabasitu2;
 #5#1;
 ;
 #3#0, #3#1, #3#2, #3#3, #3#4, #3#5, #3#6, #3#7, #3#8, "            ";
 #3#0, #3#1, #3#2, #3#3, #3#4, #3#5, #3#6, #3#7, #3#8, "zzzzzzzzzzzz";
 e;
 NULL, cuentas;
|FIN;

|PROCESO agrupacion;
  sw_epi = 1;                || fuerza la impresion del epigrafe
|FPRC;

|PROCESO impresio;

||  |SI #3#13 = 0; |Y #3#6 ! 0;
||      |SI eaValeCero = "N";  |ACABA; |FINSI;
||  |FINSI;

  |SI #3#9 ! "A"; nImprimir = 0; |FINSI;
  |SI sw = 0;
      tipo     = #3#3;
      agrupaci = #3#4;
      subagrup = #3#5;
      |HAZ agrupacion;
      sw = 1;
  |FINSI;

  |SI agrupaci ! #3#4; |O subagrup ! #3#5;
      sw_inf = 5;  |PRINT;       || linea en blanco
      |HAZ agrupacion;
      agrupaci = #3#4;
      subagrup = #3#5;
  |FINSI;

  |SI #3#9 = "T";
      sw_inf = 0;
      |SI #3#5 = 0; |Y #3#6 = 0; sw_inf = 6; |FINSI;
  |FINSI;
  |SI #3#9 = "S"; sw_inf = 0; |FINSI;
  |SI #3#9 = "C";
      sw_inf = 2;
      |SI #3#5 = 0; |Y #3#7 = 0; sw_inf = 0; |FINSI;  ||es un total con Cuentas
  |FINSI;
  |SI #3#9 = "A"; sw_inf = 7; |FINSI;

  |SI #3#9 ! "A"; |O nImprimir = 1;
      |PRINT;
      nImprimir = 1;
  |FINSI;

  |SI #3#9 = "C"; |Y #0#4 = "S";
      sw_inf=3;
      |BUCLE cabasitu2;
   |FINSI;
|FPRC;

|DEFBUCLE cabasitu1;
 #3#1;
 ;
 nBalNum, nBalance, 0, "E", 00, 0, 00, 00, 0;
 nBalNum, nBalance, 9, "E", 99, 9, 99, 99, 9;
 e;
 NULL,impresio;
|FIN;

|| ---------------------------------------------
|PROCESO GrabaAuxiliar;
     |DDEFECTO #3;
     #3#0  = #421#0;
     #3#1  = #421#1;
     #3#2  = #421#2;
     #3#3  = #421#3;
     #3#4  = #421#4;
     #3#5  = #421#5;
     #3#6  = #421#6;
     #3#7  = #421#7;
     #3#8  = #421#10;
     #3#9  = #421#8;
     #3#10 = #421#9;
     |GRABA 000#3n;
|FPRC;

|DEFBUCLE GrabaAuxiliar;
 #421#1;
 ;
 enEjerBal, nBalNum, nBalance, 0, " ", 00, 0, 00, 00, 0;
 enEjerBal, nBalNum, nBalance, 9, "z", 99, 9, 99, 99, 9;
 ;
 NULL, GrabaAuxiliar;
|FIN;

|| *****************************************************************
|PROCESO MiroSiHay;
  |SI nHay = -1; nHay = 0; |FINSI;
  nNume1 = 0;
  |PARA nPara1 = 9; |SI nPara1 [ 47; |HACIENDO nPara1 = nPara1 + 1;
        |SI #1nPara1 ! 0; nNume1 = 1; |SAL; |FINSI;
  |SIGUE;
  |SI nNume1 ! 0; nHay = 1; |ERROR10; |FINSI;
|FPRC;

|DEFBUCLE MiroSiHay;
 #1#1;
 3;
 "8           ", 0, "S";
 "999999999999", 9, "S";
 ;
 NULL, MiroSiHay;
|FIN;

|PROCESO HazTodo;
 |PATH_DAT #1  dirfich0;
 |PATH_DAT #6  dirfich0;
 |PATH_DAT #7  dirfich0;
 |PATH_DAT #8  dirfich0;

 nHay = -1;
 |BUCLE MiroSiHay;
 |SI nHay < 0; |ACABA; |FINSI;  || No procede Balance

 enEmpresa = #30#2;
 enPeriodo = #30#3;
 swOperacion = 0
 |SUB_EJECUTA "camoneda.tab";

 anu = 1900 + #30#26;
 |SI anu [ 1970;  anu = anu + 100;  |FINSI;

 nLongT = #0#5;
 |SI #0#4 = "N";
     |SI dig3 = 1; nLongT = dig4; nivel = 1; |FINSI;
     |SI dig3 = 2; nLongT = dig5; nivel = 2; |FINSI;
     |SI dig3 = 3; nLongT = dig6; nivel = 3; |FINSI;
     |SI dig3 = 4; nLongT = dig7; nivel = 4; |FINSI;
     |SI dig3 = 5; nLongT = dig8; nivel = 5; |FINSI;
     |SI dig3 = 6; nLongT = dig9; nivel = 6; |FINSI;
 |SINO;
     nivel = 0;
     |SI nLongT = dig4; nivel = 1; |FINSI;
     |SI nLongT = dig5; nivel = 2; |FINSI;
     |SI nLongT = dig6; nivel = 3; |FINSI;
     |SI nLongT = dig7; nivel = 4; |FINSI;
     |SI nLongT = dig8; nivel = 5; |FINSI;
     |SI nLongT = dig9; nivel = 6; |FINSI;
 |FINSI;

  |HAZ Borrar;
  nDEstru = #0#6; nHEstru = #0#6;
  |SI #0#6 = 0; nDEstru = 01; nHEstru = 99; |FINSI;

  |ABRE #3;
  |BUCLE GrabaAuxiliar;
  |CIERRA #3;

  |DBASE aMas;
  aDef = aMas + "def/cacuenta";
  |CARGA_DEF aDef, cuentas@;
  |SI FSalida ! 0;
      |MENAV "    Error al Carga fichero de Cuentas";
      |ACABA;
  |FINSI;
  |PATH_DAT #901 dirfich0;

  eldesde = (#0#0 * 3) + 11;
  elhasta = (#0#1 * 3) + 11;


  |PARA dos = 1; |SI dos [ 2; |HACIENDO dos = dos + 1;

       acumula = 13;
       e1 = 4;
       e2 = 5;
       e3 = 6;
       e4 = 7;

       |SI dos = 2; |Y #0#13 = "N";
           activ1 = activo;
           pasiv1 = pasivo;
           expl1 = explo;
           |SAL;
       |FINSI;

       |SI dos = 2;
           activ1 = activo;
           pasiv1 = pasivo;
           expl1 = explo;
           activo = 0;
           pasivo = 0;
           explo = 0;

           debe1 = 0;
           haber1 = 0;
           acumula = 14;
           e1 =  9;
           e2 = 10;
           e3 = 11;
           e4 = 12;

          |DBASE dir_fich;
          dir_fich = dir_fich + "fich/";
          alfa1 = #0#14;   alfa1 = "00000" + alfa1;   alfa1 = alfa1 % -105;
          alfa2 = #0#15;   alfa2 = "0"   + alfa2;   alfa2 = alfa2 % -101;
          alfa3 = dir_fich + alfa1 + alfa2 + "/";

          |PATH_DAT #1   alfa3;
          |PATH_DAT #901 alfa3;
          |PATH_DAT #6 alfa3;
          |PATH_DAT #7 alfa3;
          |PATH_DAT #8 alfa3;


          #30#2 = #0#14;
          #30#3 = #0#15;
          |LEE 011#30=;
          anu1 = 1900 + #30#26;
          |SI anu1 [ 1970;  anu1 = anu1 + 100;  |FINSI;
  |FINSI;

  |SI dos = 1;
      r_emp = empre;
      r_per = ejerc;
  |SINO;
      r_emp = #0#14;
      r_per = #0#15;
  |FINSI;

  |ABRE #8;
  |DDEFECTO #8;
  #8#0 = 1;
  |LEE 000#8=;
  |CIERRA #8;
|| ---------------------------------------------------------------------
  |ABRE #1;
  |ABRE #901;
  |ABRE #410;
  |ABRE #3;
  |ABRE #4;
  |ABRE #5;
  |HAZ pideexis;        || calcula existencias y graba
  |CIERRA #1;

  |HAZ explota;         || calcula la explotacion

  |BUCLE cabasitu4;     || calcula resto de cuentas
  |CIERRA #5;
  |CIERRA #4;
  |CIERRA #3;

  |HAZ ElCalculoFinal;
  |CIERRA #410;
  |CIERRA #901;
|SIGUE; || ==================================================================
  |DESCARGA_DEF #901;

  |SI #0#33 = "S";
      #30#2 = empre;
      #30#3 = ejerc;
      |LEE 011#30=;
  |FINSI;

   |BUCLE cabasitu1;
   |HAZ final;
   |PIEINF;

   |DELINDEX #3;
   |DELINDEX #4;
   |DELINDEX #5;
|FPRC;

|PROCESO Borrar;
    activo = 0;
    pasivo = 0;
    activ1 = 0;
    pasiv1 = 0;
    explo = 0;
    expl1 = 0;
    debe1 = 0;
    haber1 = 0;
    sw = 0;
    agrupaci = 0;
    epigrafe = 0;
    sw_epi = 0;
|FPRC;

|PROCESO final;
|FPRC;

||--------------------------------------------------------------

|PROCESO esdesd1;

  |SI #30#21 = "I"; |ACABA; |FINSI;

  |DBASE dir_fich;
  dirfich0 = dir_fich + "fich/";

  alfa1 = #30#2; alfa1 = "00000" + alfa1;   alfa1 = alfa1 % -105;
  alfa2 = #30#3; alfa2 = "0"   + alfa2;   alfa2 = alfa2 % -101;

  dirfich0 = dirfich0 + alfa1 + alfa2 + "/";
  emp1 = #30#2; empre = #30#2;
  per1 = #30#3; ejerc = #30#3;

  |SI #0#33 = "S"; |Y #0#34 = "S";
      #0#14 = #30#2;
      #0#15 = #30#3 - 1;
      |SI #0#15 < 0;  #0#15 = 9;  |FINSI;
      #0#16 = #30#1;
  |FINSI;
  dig3 = #30#13; dig4 = #30#14;
  dig5 = #30#15; dig6 = #30#16;
  dig7 = #30#17; dig8 = #30#18;
  dig9 = #30#19;

  |SI #30#26 ] 80;
      enEjercicio   = 1900 + #30#26;
  |SINO;
      enEjercicio   = 2000 + #30#26;
  |FINSI;
  |SI enEjercicio < 2008; |ACABA; |FINSI;

  |HAZ HazTodo;
  |SI #0#34 = "S";
      #30#2 = emp1;
      #30#3 = per1;
      |LEE 141#30=;
  |FINSI;
|FPRC;

|DEFBUCLE esdesde;
   #30#1;
   ;
   #0#35, #0#37;
   #0#36, #0#38;
   e;
   NULL, esdesd1;
|FIN;

|PROCESO esunaauna;

  |SI #0#33 = "S";
      |PARA xx = 39;  |SI xx [ 57;  |HACIENDO xx = xx + 2;
            |SI #0xx = 0;  |SAL;  |FINSI;
            yy = xx + 20;
            #30#2 = #0xx;
            #30#3 = #0yy;
            |LEE 041#30=;
            |SI FSalida = 0;
                yy = xx + 1;
                #0#14 = #0yy; yy = yy + 20;
                #0#15 = #0yy; yy = yy + 20;
                #0#16 = #0yy;
                |HAZ esdesd1;
            |FINSI;
      |SIGUE;
  |SINO;
      |PARA xx = 39;  |SI xx [ 58;  |HACIENDO xx = xx + 1;
            |SI #0xx = 0;  |SAL;  |FINSI;
            yy = xx + 20;
            #30#02 = #0xx;
            #30#03 = #0yy;
            |LEE 041#30=;
            |SI FSalida = 0;
                 |HAZ esdesd1;
            |FINSI;
      |SIGUE;
  |FINSI;
|FPRC;

|PROCESO impre;  |TIPO 3;

  |DDEFECTO #100;
  #100#0  = #0#0;  #100#1  = #0#1;  #100#2  = #0#2;  #100#3  = #0#3;
  #100#4  = #0#4;  #100#5  = #0#5;  #100#6  = #0#6;  #100#7  = #0#7;
  #100#8  = #0#8;  #100#9  = #0#9;  #100#10 = #0#10; #100#11 = #0#11;
  #100#12 = #0#12; #100#13 = #0#33;
  #100#17 = #0#17; #100#18 = #0#18;
  #100#25 = #0#25; #100#26 = #0#26;
  #100#27 = #0#27; #100#27 = #0#27;
  #100#29 = #0#29;


  |IMPRE 0;
  |SI FSalida ! 0;  |ACABA;  |FINSI;

  nBalNum = #0#26;
  aFichero = ("8t" + Usuario) % 108;
  |NOME_DAT #3 aFichero;|DELINDEX #3;

  aFichero = ("8e" + Usuario) % 108;
  |NOME_DAT #4 aFichero;|DELINDEX #4;

  aFichero = ("8c" + Usuario) % 108;
  |NOME_DAT #5 aFichero;|DELINDEX #5;

  |HAZ los_informes;
  |SI #0#33 = "N";
      informe = que_i1;
  |SINO;
      informe = que_i2;
  |FINSI;

  |INFOR informe;
  |SI FSalida ! 0;
     |MENAV "     Error en informe.";
     |FINIMP;
     |ACABA;
  |FINSI;

  |ABRET #30;
  |SI #0#34 = "S";
      |BUCLE esdesde;
  |SINO;
      |HAZ esunaauna;
  |FINSI;
  |CIERRAT #30;
  |FININF;
  |FINIMP;
|FPRC;
