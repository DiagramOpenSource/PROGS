|FICHEROS;
    cacompar #0;
    cacuenta #5;         || plan contable
    cacompt1 #11;        || tmp. comparacion
    caconimp #8;
    caseslin #110;
|FIN;

|VARIABLES;
  &eaFicbal = "";
  &eaNomSes = "";
  &eaDepSes = "";
  &eaDFecha   = "";
  &eaHFecha   = "";
  &eaConforme = "";
  &eaSelec    = "";

  &enNumSes = 0;
  &eaDesDep = "";
  &enMasDep = 0;
  nswPrim  = 0;
  nSwError = 0;
  nSwPrint = 0;

  &enDConcep  = 0;
  &enHConcep  = 0;
  &enGr1      = 0;
  &enGr2      = 0;
  &enGr3      = 0;
  &enCon1     = 0;
  &enCon2     = 0;
  &enCon3     = 0;
  &enCon4     = 0;
  &enCon5     = 0;
  &enCon6     = 0;
  &enCon7     = 0;
  &enCon8     = 0;

  &pathbal = "";
  &balance = 1;
  &aparam  = "";

  aEmpresa = ""; aNombre = "";
  nCuentador = 0;

    aAlfa  = "";
    aAlfa1 = "";
    alfa1 = "";
    alfa2 = "";
    alfa3 = "";
    nume1 = 0;
    nume2 = 0;
    para1 = 0;
    para2 = 0;
    para3 = 0;
    para4 = 0;
    fech1 = @;
    fech2 = @;
    fech3 = @;
    fech4 = @;
    elsw1 = 0;
    elsw2 = 0;
    elsw3 = 0;
    elsw4 = 0;
    FEstado = 0;
    seleccio = 0;
    modo = 0;
    sw_rup = 0;
    saldo  = 0;

    men2  = "     Longitud de Cuenta Fuera de Nivel !";
    p_mes = "";
    d_mes = 0;
    h_mes = 0;
    mas = 0;
    sw_chequeo = 0;
    sw_movimin = 0;
    acum = 0;
    digito = 0;
    swdig = 0;
    long = "";
    cta  = "";

    &nom1 = "";
    &nom2 = "";
    &ej1  = 0;
    &ej2  = 0;
    &colum = "";
    &des = "";
    &has = "";
    &r_emp = 0;
    &r_per = 0;
    xx = 0;
    yy = 0;

    swniv = 0;
    niv1  = 0;
    niv2  = 0;
    niv3  = 0;
    niv4  = 0;
    niv5  = 0;
    niv6  = 0;

    Comodin = "";
    aFichero = "";
|FIN;

|INCLUYE acceso_i;
|INCLUYE puntos_i;
|INCLUYE dsmone_i;

____________________________________/ CALCULOS PANTALLA

|CALCULO LosDefectos; |TIPO 7;
 |HAZ PintoSes;
 #0#2 = empre;
 #0#3 = ejerc;
 #0#0 = #0#2;
 #0#1 = #0#3 - 1; |SI #0#1 < 0; #0#1 = 9; |FINSI;

 |PINTA #0#0;
 |PINTA #0#1;
 |PINTA #0#2;
 |PINTA #0#3;
|FCAL;

|CALCULO  mayus; |TIPO 0;
  #0Campo = #0Campo > #0Campo;
  |PINTA #0Campo;
|FCAL;

|CALCULO lee_empre; |TIPO 0;
  |SI FSalida = 2;  |ACABA;  |FINSI;
  nume1 = Campo - 1;
  |ABRE #30;
  #30#2 = #0nume1;
  #30#3 = #0Campo;
  |LEE 000#30=;
  |SI FSalida ! 0;
      #30#1 = "";
      |MENAV "    Empresa INEXISTENTE ...";
      |ERROR;
  |SINO;
      |SI Campo = 1; niv1 = #30#14; niv2 = #30#15; niv3 = #30#16;
                     niv4 = #30#17; niv5 = #30#18; niv6 = #30#19;
      |SINO;
          swniv = 0;
          |SI niv1 ! #30#14; swniv= 1; |FINSI;
          |SI niv2 ! #30#15; swniv= 1; |FINSI;
          |SI niv3 ! #30#16; swniv= 1; |FINSI;
          |SI niv4 ! #30#17; swniv= 1; |FINSI;
          |SI niv5 ! #30#18; swniv= 1; |FINSI;
          |SI niv6 ! #30#19; swniv= 1; |FINSI;
          |SI swniv ! 0;
              |MENAV "    NO CORRESPONDEN LOS NIVELES";
              |ERROR;
          |FINSI;
      |FINSI;
  |FINSI;
  |ATRI I;

  |SI Campo = 1;
     |PINTA 1032, #30#1;
     nom1 = #30#1;
     ej1  = #30#26;
     Comodin = ej1;
     |SI ej1 > 80;
         ej1 = 1900 + ej1;
     |SINO;
         ej1 = 2000 + ej1;
     |FINSI;
     enEmpresa = #0#0;
     enPeriodo = #0#1;
     |HAZ LeeMonedaI;
     aMonedaC = eaMoneda;
  |SINO;
     |PINTA 1132, #30#1;
     nom2 = #30#1;
     ej2 = #30#26;
     |SI ej2 > 80;
         ej2 = 1900 + ej2;
     |SINO;
         ej2 = 2000 + ej2;
     |FINSI;
     enEmpresa = #0#2;
     enPeriodo = #0#3;
     |HAZ LeeMonedaI;
     aMonedaA = eaMoneda;
  |FINSI;

  |ATRI 0;
  |CIERRA #30;
|FCAL;

|CALCULO consulta;
  |SI FSalida ! 9;  |ACABA;  |FINSI;
  |SUB_EJECUTA "caconemp.run";
  |ERROR;
  |SI Campo = 0;
      #0#0 = r_emp;
      #0#1 = r_per;
      |PTEC 802;
      |PTEC 802;
  |FINSI;
  |SI Campo = 2;
      #0#2 = r_emp;
      #0#3 = r_per;
      |PTEC 802;
      |PTEC 802;
  |FINSI;
|FCAL;

|CALCULO misma; |TIPO 0;
  |SI #0#0 = #0#2;|Y #0#1 = #0#3;|Y FSalida ! 2;
      |MENAV "    ATENCION!!! No se puede comparar la misma EMPRESA ...";
      |ERROR;
  |FINSI;
|FCAL;

|CALCULO pintames; |TIPO 0;
  |SI #0Campo =  0;  p_mes = "Apertura  ";  |FINSI;
  |SI #0Campo =  1;  p_mes = "Enero     ";  |FINSI;
  |SI #0Campo =  2;  p_mes = "Febrero   ";  |FINSI;
  |SI #0Campo =  3;  p_mes = "Marzo     ";  |FINSI;
  |SI #0Campo =  4;  p_mes = "Abril     ";  |FINSI;
  |SI #0Campo =  5;  p_mes = "Mayo      ";  |FINSI;
  |SI #0Campo =  6;  p_mes = "Junio     ";  |FINSI;
  |SI #0Campo =  7;  p_mes = "Julio     ";  |FINSI;
  |SI #0Campo =  8;  p_mes = "Agosto    ";  |FINSI;
  |SI #0Campo =  9;  p_mes = "Septiembre";  |FINSI;
  |SI #0Campo = 10;  p_mes = "Octubre   ";  |FINSI;
  |SI #0Campo = 11;  p_mes = "Noviembre ";  |FINSI;
  |SI #0Campo = 12;  p_mes = "Diciembre ";  |FINSI;
  |SI #0Campo = 13;  p_mes = "Cierre    ";  |FINSI;

  |SI Campo = 11;     |PINTA 1527, p_mes; des = p_mes;   |FINSI;
  |SI Campo = 12;     |PINTA 1541, p_mes; has = p_mes;   |FINSI;
|FCAL;

|CALCULO nosaldo; |TIPO 7;
  aAlfa1 = "S";
  |SI #0#18 = "S";  aAlfa1 = "N";  #0#19 = "S"; |FINSI;
  |CAMPO_MODIFICA #0#19, 1, aAlfa1;
  |PINTA #0#19;
|FCAL;

|CALCULO cuenta; |TIPO 6;
 p_alfa1 = #0Campo;
 salidas = FSalida;
 |HAZ punto;
 |SI p_error ! 0;  |ERROR;  |ACABA;  |FINSI;
 |SI #0Campo = "            ";  |ERROR;  |ACABA;  |FINSI;
     digito = Campo + 2;
     cta  = #0Campo; |QBF cta;
     long = cta % 0;
     swdig = 0;
 |SI long = #30#14; swdig = 1; #0digito = 1; |FINSI;
 |SI long = #30#15; swdig = 1; #0digito = 2; |FINSI;
 |SI long = #30#16; swdig = 1; #0digito = 3; |FINSI;
 |SI long = #30#17; swdig = 1; #0digito = 4; |FINSI;
 |SI long = #30#18; swdig = 1; #0digito = 5; |FINSI;
 |SI long = #30#19; swdig = 1; #0digito = 6; |FINSI;
 |SI swdig = 0;
     |MENAV men2;
     |ERROR;
 |FINSI;
|FCAL;

|PROCESO AntesConceptos; |TIPO 7;
   |SI aparam = "fech";
      |PARA nCuentador = 20; |SI nCuentador [ 33; |HACIENDO nCuentador = nCuentador + 1;
           |C_M #0nCuentador,1,"N";
      |SIGUE;
   |FINSI;
|FPRC;

|PROCESO TipoConceptos;

   |SI aparam = "fech"; |ACABA; |FINSI;

   |C_M #cacompar#21,1,"N"; |C_M #cacompar#22,1,"N";
   |PARA nCuentador = 21; |SI nCuentador [ 33; |HACIENDO nCuentador = nCuentador + 1;
      |C_M #cacompar#nCuentador#,1,"N";
   |SIGUE;
   |SI #cacompar#20 = "D";
      |PARA nCuentador = 23; |SI nCuentador [ 33; |HACIENDO nCuentador = nCuentador + 1;
         |C_M #cacompar#nCuentador#,1,"N";
         #cacompar#nCuentador# = 0; |PINTA #cacompar#nCuentador#;
      |SIGUE;
      |C_M #cacompar#21,1,"S"; |C_M #cacompar#22,1,"S";
   |FINSI;
   |SI #cacompar#20 = "G";
      |C_M #cacompar#21,1,"N"; #cacompar#21 = 1;   |PINTA #cacompar#21;
      |C_M #cacompar#22,1,"N"; #cacompar#22 = 999; |PINTA #cacompar#22;
      |PARA nCuentador = 23; |SI nCuentador [ 33; |HACIENDO nCuentador = nCuentador + 1;
         |C_M #cacompar#nCuentador#,1,"N";
         #cacompar#nCuentador# = 0; |PINTA #cacompar#nCuentador#;
      |SIGUE;
      |C_M #cacompar#23,1,"S"; |C_M #cacompar#24,1,"S"; |C_M #cacompar#25,1,"S";
   |FINSI;
   |SI #cacompar#20 = "P";
      |C_M #cacompar#21 ,1,"N"; #cacompar#21 = 1;   |PINTA #cacompar#21;
      |C_M #cacompar#22 ,1,"N"; #cacompar#22 = 999; |PINTA #cacompar#22;
      |C_M #cacompar#23,1,"N"; #cacompar#23 = 0;   |PINTA #cacompar#23;
      |C_M #cacompar#24,1,"N"; #cacompar#24 = 0;   |PINTA #cacompar#24;
      |C_M #cacompar#25,1,"N"; #cacompar#25 = 0;   |PINTA #cacompar#25;
      |PARA nCuentador = 26; |SI nCuentador [ 33; |HACIENDO nCuentador = nCuentador + 1;
         |C_M #cacompar#nCuentador#,1,"S";
      |SIGUE;
   |FINSI;
|FPRC;

|| ****************************************************************
|| ****************************************************************
____________________________________/ LECTURA DEL TEMPORAL
|PROCESO lee_tempo1;
  |PRINT;
  seleccio = 1;
|FPRC;

|DEFBUCLE lee_tempo;
 #11#1;
 ;
 "            ", 0;
 "zzzzzzzzzzzz", 9;
 e;
 NULL, lee_tempo1;
|FIN;
____________________________________/ GRABACION DEL TEMPORAL

|PROCESO chequeo;
  sw_chequeo = 0;
  |SI #0#19 = "N";|Y saldo = 0;|Y sw_movimin = 1;
      sw_chequeo = 1;
  |FINSI;
  |SI sw_movimin = 0;|Y #0#18 = "N";
      sw_chequeo = 1;
  |FINSI;
|FPRC;

|PROCESO acumula;
  sw_movimin = 0;
  saldo = 0;
  |PARA para3 = d_mes; |SI para3 [ h_mes; |HACIENDO para3 = para3 + 3;

        |SI #5para3 ! 0;  sw_movimin = 1;  |FINSI;
        |SI #0#17 = "S"; |Y #5para3 = 0;
            xx = para3 - 2; yy = para3 - 1;
            |SI #5xx ! 0; |O #5yy ! 0;
                sw_movimin = 1;
            |FINSI;
        |FINSI;
        impMoneda = #5para3;
        |SI aMonedaA ! aMonedaC; |Y acum = 2;
            |HAZ ElCMoneda;
        |FINSI;
        saldo = saldo + impMoneda;
  |SIGUE;
|FPRC;

|PROCESO columna1_1;
  |HAZ acumula;
  |HAZ chequeo;
  |SI sw_chequeo ! 0;  |ACABA;  |FINSI;

  #11#0 = #5#0;    || cuenta
  #11#1 = #5#1;    || digito
  |LEE 101#11=;
  |SI FSalida ! 0;
      |DDEFECTO #11;
      #11#0 = #5#0;    || Cuenta
      #11#1 = #5#1;    || digito
      #11#4 = #5#2;    || Nombre
      |GRABA 020#11b;
  |FINSI;

  #11acum = saldo;    || acumulado
  |GRABA 020#11a;
  |LIBERA #11;
|FPRC;

|DEFBUCLE columna1;
   #5#1;
   ;
   #0#13, #0#4;
   #0#14, #0#4;
   e;
   NULL, columna1_1;
|FIN;

|PROCESO antesque;
  aparam = PARAMETRO; |QBF aparam;
  |SI aparam = "fech";
      |HAZ AntesSesion;
      |NOME_DAT #5 eaFicbal;
  |FINSI;
|FPRC;

|PROCESO PintoSes; |TIPO 7;
  aparam = PARAMETRO; |QBF aparam;
  |SI aparam = "fech";
      |HAZ AntesSesion;
      |SI eaNomSes > "";
          |ATRI I; |PINTA 0505, "SESION: "; |PINTA 0513,eaNomSes;
          |SI eaDepSes > "";
              |PINTA 0558, "Departamento: "; |PINTA 0572, eaDepSes;
          |FINSI;
          |ATRI 0;
      |FINSI;
  |FINSI;
|FPRC;


|PROCESO HagoTodo;

  |DELINDEX #11;

  seleccio = 0;
  acum = 2;
  |ABRE #11;
  |PARA para1 = 0; |SI para1 [ 2; |HACIENDO para1 = para1 + 2;

        para2 = para1 + 1;
        |DBASE dir_fich;
        dir_fich = dir_fich + "fich/"
        aAlfa1 = #0para1;  aAlfa1 = "00000" + aAlfa1;   aAlfa1 = aAlfa1 % -105;
        alfa2  = #0para2;   alfa2 = "0"   + alfa2;   alfa2 = alfa2 % -101;
        alfa3 = dir_fich + aAlfa1 + alfa2 + "/";
        |PATH_DAT #5 alfa3;
        |PATH_DAT #8 alfa3;

        |ABRE #30;
        #30#2 = #0para1;
        #30#3 = #0para2;
        |LEE 011#30=;
        |CIERRA #30;
        |SI para1 = 0;
            eaMoneda = aMonedaC; |HAZ MonedasI;
            |SI aMonedaA = "E";  EURODB = 2; |FINSI;
        |FINSI;
        |SI para1 = 2; eaMoneda = aMonedaA; |HAZ MonedasI; |FINSI;

        |ABRE #8;
        |LEE 111#8p;
        |SI #8#2 = "S";  |Y #8#3 = "N";  |Y aparam = "";
            pathbal = alfa3;
            balance = 1;
            |CIERRA #8;
            |SUB_EJECUTA "careccue.tab";
        |FINSI;
        |CIERRA #8;

        |SI para1 = 2;  acum = 3; |FINSI;

        |SI aparam = "";
            aEmpresa = (("00000" + #cacompar#para1#) % -105) + #cacompar#para2#;
            aNombre  = "c" + para1 + Usuario;
            |HAZ recupera;

            |NOME_DAT #5 aNombre;
            |PATH_DAT #5 alfa3;

        |FINSI;

        |BUCLE columna1;

  |SIGUE;
  |CIERRA #11;

  eaMoneda = aMonedaA; |HAZ MonedasI;

  |BUCLE lee_tempo;
  |SI seleccio ! 0;
      |PIEINF;
  |FINSI;
|FPRC;

|PROCESO PorSesiones;
   eaDepSes = #110#1;
   eaDesDep = #110#7;

   alfa1 = #110#6; |QBF alfa1;
   |SI alfa1 ! "";
       |CIERRA #5;
       eaFicbal  = alfa1;
       |NOME_DAT #5 eaFicbal;
       |HAZ HagoTodo;
   |FINSI;
|FPRC;

|DEFBUCLE PorSesiones;
 #110#1;
 ;
 enNumSes, "  ";
 enNumSes, "zz";
 ;
 NULL, PorSesiones;
|FIN;

____________________________________/ CALCULO PRINCIPAL
|PROCESO tipo3; |TIPO 3;

  |CIERRA #5;
  eaMoneda = aMonedaC; |HAZ MonedasI;
  |HAZ antesque;

  |IMPRE 0;
  |SI FSalida ! 0;  |ACABA;  |FINSI;

  |INFOR "cacompar";
  |SI FSalida ! 0; |FINIMP; |ACABA; |FINSI;

|| *****************************************************************
  |SI #0#17 = "D";    mas = 0;  colum = "DEBE";     |FINSI;
  |SI #0#17 = "H";    mas = 1;  colum = "HABER";    |FINSI;
  |SI #0#17 = "S";    mas = 2;  colum = "SALDO";    |FINSI;
  d_mes = (9 + mas) + (#0#11 * 3);
  h_mes = (9 + mas) + (#0#12 * 3);

  aFichero = ("xx" + Usuario) % 108;
  |NOME_DAT #11 aFichero;

  |SI enMasDep = 0;
      |HAZ HagoTodo;
  |SINO;
      |BUCLE PorSesiones;
      |SI nswPrim = 1; |FININF; |FINIMP; |FINSI;
      eaDepSes = "TODOS";
  |FINSI;

  |FININF;
  |FINIMP;
  |DELINDEX #11;
|FPRC;

|PROCESO recupera;
     |SI #cacompar#20 = "D";
        enDConcep = #0#21; enHConcep = #0#22;
        enGr1 = 0;  enGr2 = 0;  enGr3 = 0;
        enCon1 = 0; enCon2 = 0; enCon3 = 0; enCon4 = 0; enCon5  = 0;
        enCon6 = 0; enCon7 = 0; enCon8 = 0;
     |SINO;
        |SI #cacompar#20 = "P";
           enDConcep = 0; enHConcep = 999;
           enGr1 = 0;  enGr2 = 0;  enGr3 = 0;
           enCon1 = #0#26; enCon2 = #0#27; enCon3 = #0#28; enCon4 = #0#29; enCon5  = #0#30;
           enCon6 = #0#31; enCon7 = #0#32; enCon8 = #0#33;
        |FINSI;
        |SI #cacompar#20 = "G";
           enDConcep = 0; enHConcep = 999;
           enGr1 = #0#23; enGr2 = #0#24; enGr3 = #0#25;
           enCon1 = 0; enCon2 = 0; enCon3 = 0; enCon4 = 0; enCon5  = 0;
           enCon6 = 0; enCon7 = 0; enCon8 = 0;
       |FINSI;
     |FINSI;

     eaDFecha = "01.01.0000";     eaHFecha = "31.12.9999";
     eaConforme = "S";            eaSelec  = #cacompar#20;

     aAlfa = "carecufe.tab;" + aEmpresa + "," + aNombre;
     |SUB_EJECUTA_NP aAlfa;
|FPRC;
