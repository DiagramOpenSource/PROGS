|FICHEROS;
    carieinf #0;
    camancob #1;         || cobros
    camanban #2;         || bancos
    caremesc #3;         || remesas cabs.
    caremesl #4;         || remesas lins.
    cariein1 #11;        || tmp. totales
||    cariein2 #12;        || def. informe
    cariein3 #13;        || tmp. detallado
|FIN;

|VARIABLES;
    alfa1 = "";
    alfa2 = "";
    alfa3 = "";
    alfa4 = "";
    nume1 = 0;
    nume2 = 0;
    nume3 = 0;
    nume4 = 0;
    para1 = 0;
    para2 = 0;
    para3 = 0;
    para4 = 0;
    fech1 = @;
    fech2 = @;
    fech3 = @;
    fech4 = @;
    elsw1 = 0;
    elsw2 = 0;
    elsw3 = 0;
    elsw4 = 0;
    FEstado = 0;
    seleccio = 0;
    modo = 0;

    venci = 0;
    refer = 0;
    negoc = 0;
    dias1 = 0;
    dias2 = 0;
    campo = 0;
    el_banco = 0;
    sw_sel = 0;
    &nom_ban = "";
    &sw_inf = 0;
    &plazo = "";
    &total_ban = 0;
    &total = 0;
    ant_ban = -1;
    feini = @;
    fefin = @;
|FIN;

|INCLUYE acces2_i;
|INCLUYE i_ficher;
____________________________________/ CALCULOS DE PANTALLA
|PROCESO mayus; |TIPO 0;
    #0Campo = #0Campo > #0Campo;
|PINTA #0Campo;
|FPRC;

|PROCESO nom_banc; |TIPO 0;
    |SI #0#7 = "N"; |ACABA; |FINSI;
    |PINTA 1233, #2#1;
|FPRC;

|PROCESO no_entres; |TIPO 7;
    alfa1 = #0#7;
|CAMPO_MODIFICA #0#2, 1, alfa1;
|FPRC;
____________________________________/ IMPRESION DETALLE
|PROCESO lee_detalle1;
|SI ant_ban = -1;   ant_ban = #13#1;    |FINSI;

|SI ant_ban ! #13#1;
    |DDEFECTO #2;
        #2#0 = ant_ban;
    |LEE 020#2=;        || lee el banco

        sw_inf = 3; |PRINT;        || total banco

        ant_ban = #13#1;
        total = total + total_ban;
        total_ban = 0;
|FINSI;

|DDEFECTO #1;
    #1#0 = #13#3;
    #1#1 = #13#4;
    #1#2 = #13#5;
    #1#3 = #13#6;
|LEE 020#1=;        || lee el cobro

    total_ban = total_ban + #1#8;

    sw_inf = 0;     |PRINT;
|FPRC;

|DEFBUCLE lee_detalle;
 #13#2;
 ;
 campo,   0, feini, "            ", feini;
 campo, 999, fefin, "zzzzzzzzzzzz", fefin;
 e;
 NULL, lee_detalle1;
|FIN;

|PROCESO la_impresion;
    campo = para1;
    ant_ban = -1;
    sw_inf = 2; |PRINT;                 || encabezamiento (titulo)
    feini = "01.01.0001";
    fefin = "31.12.8999";
|BUCLE lee_detalle;                     || detalle
|SI ant_ban ! -1;
    |DDEFECTO #2;
        #2#0 = ant_ban;
    |LEE 020#2=;        || lee el banco
        sw_inf = 3; |PRINT;             || total por banco
|FINSI;
    total = total + total_ban;
    total_ban = 0;
    sw_inf = 1; |PRINT;                 || total por titulo
    total = 0;
|FPRC;

|PROCESO el_plazo;
|SI para1 = 2;|O para1 = 10;  plazo = "DE  1 A 10 DIAS-";   |FINSI;
|SI para1 = 3;|O para1 = 11;  plazo = "DE 11 A 20 DIAS-";   |FINSI;
|SI para1 = 4;|O para1 = 12;  plazo = "DE 21 A 30 DIAS-";   |FINSI;
|SI para1 = 5;|O para1 = 13;  plazo = "DE 31 A 60 DIAS-";   |FINSI;
|SI para1 = 6;|O para1 = 14;  plazo = "DE 61 A 90 DIAS-";   |FINSI;
|SI para1 = 7;|O para1 = 15;  plazo = " MAS DE 90 DIAS-";   |FINSI;
|FPRC;

|PROCESO detallando;
|INFOR "cariein1";

|PARA para1 =  2; |SI para1 [ 7; |HACIENDO para1 = para1 + 1;
        total = 0;
    |HAZ el_plazo;
        plazo = "-CORTO PLAZO -> " + plazo;
    |HAZ la_impresion;
|SIGUE;
|PARA para1 = 10; |SI para1 [ 15; |HACIENDO para1 = para1 + 1;
    |HAZ el_plazo;
        plazo = "-LARGO PLAZO -> " + plazo;
    |HAZ la_impresion;
|SIGUE;

|PIEINF;
|FININF;
|FPRC;
____________________________________/ IMPRESION RESUMEN
|PROCESO lee_tempo1;
|DDEFECTO #2;
    #2#0 = #11#0;
|LEE 020#2=;

    nom_ban = #2#1;
|QBF nom_ban;
    nom_ban = nom_ban + "........................................";
    nom_ban = nom_ban % 140;
/*
|PARA para1 = 0; |SI para1 [ 15; |HACIENDO para1 = para1 + 1;
        #12para1 = #11para1;
|SIGUE;
*/
|PRINT;
|FPRC;

|DEFBUCLE lee_tempo;
 #11#1;
 ;
   0;
 999;
 e;
 NULL, lee_tempo1;
|FIN;
____________________________________/ GRABACION DEL TEMPORAL POR BANCOS
|PROCESO lee_remesa;
|DDEFECTO #3;
    #3#0 = #1#12;
|LEE 000#3=;
|SI FSalida ! 0;|O #1#12 = 0;      || carga el banco correspondiente
  #caremesl#2 = #camancob#0;
  #caremesl#3 = #camancob#1;
  #caremesl#4 = #camancob#2;
  #caremesl#5 = #camancob#3;
  |LEE 000#caremesl.=;
  |SI FSalida = 0;
     #camancob#12 = #caremesl#0; |GRABA 020#camancob.a;
     |DDEFECTO #3;
     #3#0 = #1#12;
     |LEE 000#3=;
     el_banco = #3#2;
  |SINO;
    |SI #0#7 = "S";
            el_banco = #0#2;       || si entran los no remesados
    |SINO;
            el_banco = -1;         || no entran los no remesados
    |FINSI;
  |FINSI;
|SINO;
        el_banco = #3#2;
|FINSI;

|SI el_banco ] #0#0;|Y el_banco [ #0#1;      || comprueba la seleccion
        sw_sel = 1;
|SINO;
    |SI el_banco = #0#2;
            sw_sel = 1;
    |FINSI;
|FINSI;
|FPRC;

|PROCESO gra_tempo1;
    sw_sel = 0;
|HAZ lee_remesa;
|SI sw_sel = 1;
        venci = #1#7;
        refer = #0#3;
        dias1 = venci - refer;     || dias que faltan para vto.

    |SI #1#12 ! 0;
        |SI #0#9 = "R";
            negoc = #3#1;
        |SINO;
            negoc = #0#3;
        |FINSI;
        dias2 = venci - negoc; || para saber si es corto o largo plazo
        |SI dias2 < 0; dias2 = 0;     |FINSI;
    |SINO;
            dias2 = 0;             || si no tiene remesa es corto plazo
    |FINSI;

        campo = 0;
    |SI dias2 [ 90;
        |SI dias1 ]  1;|Y dias1 [ 10;  campo = 2;     |FINSI;
        |SI dias1 ] 11;|Y dias1 [ 20;  campo = 3;     |FINSI;
        |SI dias1 ] 21;|Y dias1 [ 30;  campo = 4;     |FINSI;   || C/P
        |SI dias1 ] 31;|Y dias1 [ 60;  campo = 5;     |FINSI;
        |SI dias1 ] 61;|Y dias1 [ 90;  campo = 6;     |FINSI;
        |SI dias1 ] 91;                campo = 7;     |FINSI;
    |FINSI;
    |SI dias2 > 90;
        |SI dias1 ]  1;|Y dias1 [ 10;  campo = 10;    |FINSI;
        |SI dias1 ] 11;|Y dias1 [ 20;  campo = 11;    |FINSI;
        |SI dias1 ] 21;|Y dias1 [ 30;  campo = 12;    |FINSI;   || L/P
        |SI dias1 ] 31;|Y dias1 [ 60;  campo = 13;    |FINSI;
        |SI dias1 ] 61;|Y dias1 [ 90;  campo = 14;    |FINSI;
        |SI dias1 ] 91;                campo = 15;    |FINSI;
    |FINSI;

    |SI campo ! 0;
        |DDEFECTO #11;
            #11#0 = el_banco;     || banco
        |LEE 101#11=;
            FEstado = FSalida;
            #11campo = #11campo + #1#8;
            #11#8 = #11#8 + #1#8;
        |SI FEstado = 0;    |GRABA 020#11a;     |FINSI;
        |SI FEstado ! 0;    |GRABA 020#11n;     |FINSI;
        |SI FSalida = 0;
                seleccio = 1;
            |SI #0#8 = "D";
                |DDEFECTO #13;
                    #13#0 = campo;      || campo clave
                    #13#1 = el_banco;   || banco
                    #13#2 = #1#7;       || vencimiento
                    #13#3 = #1#0;       || libro
                    #13#4 = #1#1;       || factura
                    #13#5 = #1#2;       || serie
                    #13#6 = #1#3;       || nro. cobro
                    #13#7 = #1#4;       || cliente
                    #13#8 = #1#6;       || emision
                |GRABA 020#13n;
            |FINSI;
        |FINSI;
        |LIBERA #11;
    |FINSI;
|FINSI;
|FPRC;

|DEFBUCLE gra_tempo;
 #1#4;
 ;
 #0#5, feini;
 #0#6, fefin;
 e;
 NULL, gra_tempo1;
|FIN;
____________________________________/ CALCULO PRINCIPAL
|PROCESO tipo3; |TIPO 3;
|IMPRE 0;
|SI FSalida = 0;
    |MENSA "    ...Preparando Temporales...";
        comienza = "1";  |HAZ fichero;  |NOME_DAT #11 fichero; |DELINDEX #11;
        comienza = "2";  |HAZ fichero;  |NOME_DAT #13 fichero; |DELINDEX #13;
    |ABRE #11;
    |ABRE #13;
    |ABRE #3;
    |ABRE #caremesl; |SELECT #2#caremesl;
    feini = "01.01.0001";
    fefin = "31.12.8999";
    |BUCLE gra_tempo;
    |CIERRA #11;
    |CIERRA #13;
    |SELECT #1#caremesl; |CIERRA #caremesl;
    |CIERRA #3;
    |BLIN 4;

    |SI seleccio ! 0;
        |MENSA "    ...Imprimiendo...";
        |INFOR "carieinf";
        |ABRE #2;
        |BUCLE lee_tempo;
        |PIEINF;
        |FININF;
        |SI #0#8 = "D";
            |ABRE #1;
            |HAZ detallando;
            |CIERRA #1;
        |FINSI;
        |CIERRA #2;
        |BLIN 4;
    |SINO;
        |MENAV "    Selecci¢n vacia ...";
    |FINSI;
    |DELINDEX #11;
    |DELINDEX #13;
|SINO;
    |MENAV "    Proceso Abortado ...";
|FINSI;
|FINIMP;
|FPRC;
