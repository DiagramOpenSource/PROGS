|FICHEROS;
    cabatodo #0;
    cacuenta #1;
    caparexp #2;
    caparbal #9;
    cabaexp0 #3; || fichero trabajo totales
    cabaexp2 #5; || fichero trabajo cuentas
    caexistc #6;
    caexistl #7;
    canempre #30;
    caconimp #8;
    catrabap #10; || Pantalla del Balance

    cabucem1 #100;
    camaasic #21;
    caconcep #22;
    camaasil #23;

    caseslin #110;
|FIN;

|VARIABLES;
  aFichero = "";
  &eaUltNiv = "";
  &pathbal = "";
  &balance = 1;
  &entrada = 0;
  &aparam = "";

  &eaFicbal = "";
  &eaNomSes = "";
  &enNumSes = 0;
  &eaDepSes = "";
  &eaDesDep = "";
  &enMasDep = 0;
  nswPrim  = 0;
  nSwError = 0;
  nSwPrint = 0;

  Comodin1 = "";
  Numeracion = 0;
  aAlfa    = "";

  Empresa = 0;
  Periodo = 0;
  Calc    = 0;
  Handle  = 0;
  Compara = 0;
  Per1    = 0;
  Per2    = 0;
  informe = "";
  alfa1 = "";
  alfa2 = "";
  alfa3 = "";
  nume1 = 0;
    x = 0;
    y = 0;
  &r_emp = 0;
  &r_per = 0;
  &d_mes = 0;
  &h_mes = 0;
  &estru = 0;
  &r_eje = 0;
  &r_nom = "";
  &anu = 0;
  &anu1 = 0;
    varcamp = 0;
    mes = 0;
    letrames = "";
    nivel = 0;
    sw_error = 0;
    cue_perdi = "";
    dig_perdi = 0;
    traraf = 0;
    acor_c = "";
    alar_c = "";
    acor_d = 0;
    t_agru = "";
    t_epig = "";
    t_part = "";

  &empcon = "";
  &ejecon = 0;

  &eldesde = 0;
  &elhasta = 0;
  des_exi = 0;
  has_exi = 0;
  d_cuen = "";
  h_cuen = "";
  d_digi = 0;
  h_digi = 9;
    dos = 0;
    acumula = 0;
    acumul2 = 0;
 act_cuent = "";
 act_nivel = 0;
 saldo   = 0;
 debe    = 0;
 haber   = 0;
 sw_exp  = 0;
 descar  = 0;
 bene    = 0;
  activo = 0;
  pasivo = 0;
   explo = 0;
  debe1  = 0;
  debe2  = 0;
  debe3  = 0;
  debe4  = 0;
  haber1 = 0;
  haber2 = 0;
  haber3 = 0;
  haber4 = 0;
  tipo = "";
  bloque = 0;
  agrupaci = 0;
  epigrafe = 0;
  partida = 0;

  canti = 0;
  punto_h1 = 0;
  punto_h2 = 0;
  punto_h3 = 0;
  punto_h4 = 0;
  punto_h5 = 0;
  punto_h6 = 0;
  punto_d1 = 0;
  punto_d2 = 0;
  punto_d3 = 0;
  punto_d4 = 0;
  punto_d5 = 0;
  punto_d6 = 0;

  sw = 0;
  sw_agr = 0;
  sw_epi  = 0;
  sw_alguno = 0;

  &titulo = "";
  &sw_inf = 0;
  &cantidad = 0;
  &cantidad2 = 0;
  &variable = "";
  &doh = "";
  &cta = "";
  &nom = "";
  &mesi = "";
  &mesf = "";
  &fec  = @;
 d_tipo = "";
 h_tipo = "";
 d_bloque = 0;
 h_bloque = 0;
    {-1}menu  = "";           || Variables menu impresora o pantalla
        menu1 = "2400";
        menu2 = "1";
        menu3 = "Elija opcion: ";
        menu4 = "PI";
        menu5 = " Pantalla ";
        menu6 = " Impresora ";

 lin = 0;
 swfinal = 0;

  Comodin = "";
  swParam = 0;

  aAlfa1 = "";
  nDiario = 0;
  fFecha = @;
  nMesAper = 0;
  nMesCier = 0;
  &enBasImp   = 0;
  &eaNomImp   = "";
  swimp = 0;

  nDEstru = 0;
  nHEstru = 0;
  nSw     = 0;
  aCuenta = "";
  nNivel  = 0;
  nTSaldo = 0;
  nSwOp   = 0;

  nUnError = 0;
  nLegalia = 0;
  &eaLegEmp = "";
  &eaLegPer = "";
  &efFecLeg = @;
  &eaUnidadBasico = "";
  &eaUnidadBTmp   = "";
|FIN;

|INCLUYE acceso_i;
|INCLUYE digito_i;
|INCLUYE dsmone_i;

|| ========================================================================
||                        CALCULOS PANTALLA
|| ========================================================================

|PROCESO mayor;
  alfa1 = #0Campo > " ";
  #0Campo = alfa1;
  |PINTA #0Campo;
|FPRC;

|PROCESO mayor1;
  |HAZ mayor;
  |SI #0Campo = "N";
      #0#14 = empre;
      #0#15 = ejerc;
      |CAMPO_MODIFICA #0#14,1,"N";
      |CAMPO_MODIFICA #0#15,1,"N";
      |PINTA #0#14;
      |PINTA #0#15;
  |SINO;
      |CAMPO_MODIFICA #0#14,1,"S";
      |CAMPO_MODIFICA #0#15,1,"S";
  |FINSI;
|FPRC;

|PROCESO deentrada;  |TIPO 7;
  |SI #0#14 = 0;  |Y #0#15 = 0;
      #0#14 = empre;
      #0#15 = ejerc - 1;
      |SI #0#15 < 0; #0#15 = 9; |FINSI;
      |PINTA #0#14;
      |PINTA #0#15;
  |FINSI;
|FPRC;

|PROCESO esbuena;
  |SI FSalida = 9;  |ACABA;  |FINSI;
  |ABRE #30;
  #30#2 = #0#14;
  #30#3 = 0;
  |LEE 011#30];
  |SI FSalida ! 0;  |O #30#2 ! #0#14;
      nUnError = 1;
      |SI nLegalia = 0;
          |MENAV "     NO Existe la empresa Contable";
          |ERROR;
      |FINSI;
  |FINSI;
  |CIERRA #30;
|FPRC;

|PROCESO consulta;
  |SI FSalida ! 9;  |ACABA;  |FINSI;
  |CIERRAT #30;
  r_emp = 0;
  |SUB_EJECUTA "caconemp";
  |ABRET #30;
  |ERROR;
  |SI Campo = 14;
      #0#14 = r_emp;
      #0#15 = r_per;
      |PTEC 802;
      |PTEC 802;
  |FINSI;
|FPRC;

|PROCESO esbuena1;
  |SI FSalida = 2; |ACABA; |FINSI;
  |ABRE #30;
  #30#2 = #0#14;
  #30#3 = #0#15;
  |LEE 011#30=;
  |SI FSalida ! 0;
      nUnError = 1;
      |SI nLegalia = 0;
          |MENAV "     NO Existe Periodo en la empresa Contable";
          |ERROR;
      |FINSI;
      |CIERRA #30;
      |ACABA;
  |FINSI;
  |SI #30#13 ! dig3;  |O #30#14 ! dig4;  |O #30#15 ! dig5;
                      |O #30#16 ! dig6;  |O #30#17 ! dig7;
                      |O #30#18 ! dig8;  |O #30#19 ! dig9;
      nUnError = 1;
      |SI nLegalia = 0;
          |MENAV "     NO CORRESPONDEN LOS NIVELES";
          |ERROR;
      |FINSI;
      |CIERRA #30;
      |ACABA;
  |FINSI;
  #0#16 = #30#1;
  anu1 = 1900 + #30#26;
  |SI anu1 [ 1970;
      anu1 = anu1 + 100;
  |FINSI;
  |PINTA #0#16;
  #30#2 = empre;
  #30#3 = ejerc;
  |LEE 011#30=;
  anu = 1900 + #30#26;
  |SI anu [ 1970;
      anu = anu + 100;
  |FINSI;
  |CIERRA #30;
|FPRC;

|PROCESO pintames; |TIPO 0;
  varcamp = Campo + 2;                || Coge el campo del nombre del mes
  |SI #0Campo ! 0;|Y #0Campo ! 13;
      mes = #30#11 + (#0Campo - 1);   || Operacion para saber que mes es
      |SI mes > 12; mes = mes - 12; |FINSI;
  |SINO;
      mes = #0Campo;
  |FINSI;
  |HAZ mesletra;                      || Recoge el mes en letra
  #0varcamp = letrames;
  |PINTA #0varcamp;                   || Pinta el nombre del mes
  |SI Campo = 1;  |Y #0#0 > #0#1;
      |MENAV "    Error de Entrada ...";
      |ERROR;
  |FINSI;
|FPRC;

|PROCESO pintames2; |TIPO 0;
  |SI #0Campo ! 0;|Y #0Campo ! 13;
      mes = #30#11 + (#0Campo - 1);   || Operacion para saber que mes es
      |SI mes > 12; mes = mes - 12; |FINSI;
  |SINO;
      mes = #0Campo;
  |FINSI;
  |HAZ mesletra;
  |ATRI I;
  |SI Campo = 7;  |PINTA 1625, letrames;  |FINSI;
  |SI Campo = 8;  |PINTA 1661, letrames;  |FINSI;
  |ATRI 0;
  |SI Campo = 8;  |Y #0#7 > #0#8;
      |MENAV "    Error de Entrada ...";
      |ERROR;
   |FINSI;
|FPRC;

|PROCESO mesletra;
  |SI mes = 0;  letrames = "Apertura  "; |FINSI;
  |SI mes = 1;  letrames = "Enero     "; |FINSI;
  |SI mes = 2;  letrames = "Febrero   "; |FINSI;
  |SI mes = 3;  letrames = "Marzo     "; |FINSI;
  |SI mes = 4;  letrames = "Abril     "; |FINSI;
  |SI mes = 5;  letrames = "Mayo      "; |FINSI;
  |SI mes = 6;  letrames = "Junio     "; |FINSI;
  |SI mes = 7;  letrames = "Julio     "; |FINSI;
  |SI mes = 8;  letrames = "Agosto    "; |FINSI;
  |SI mes = 9;  letrames = "Septiembre"; |FINSI;
  |SI mes = 10; letrames = "Octubre   "; |FINSI;
  |SI mes = 11; letrames = "Noviembre "; |FINSI;
  |SI mes = 12; letrames = "Diciembre "; |FINSI;
  |SI mes = 13; letrames = "Cierre    "; |FINSI;
|FPRC;

|PROCESO nivel; |TIPO 7;
  |SI #0#4 = "N";
      |CAMPO_MODIFICA #0#5,1,"N";
  |SINO;
      |CAMPO_MODIFICA #0#5,1,"S";
  |FINSI;
|FPRC;

|PROCESO miraniv; |TIPO 0;
  nivel = #0#5;
  |SI nivel = 1;|Y dig4 = 0;  |AVISO;  |ERROR;  |FINSI;
  |SI nivel = 2;|Y dig5 = 0;  |AVISO;  |ERROR;  |FINSI;
  |SI nivel = 3;|Y dig6 = 0;  |AVISO;  |ERROR;  |FINSI;
  |SI nivel = 4;|Y dig7 = 0;  |AVISO;  |ERROR;  |FINSI;
  |SI nivel = 5;|Y dig8 = 0;  |AVISO;  |ERROR;  |FINSI;
  |SI nivel = 6;|Y dig9 = 0;  |AVISO;  |ERROR;  |FINSI;
|FPRC;

|PROCESO estruct; |TIPO 0;

  |SI #0#6 = 0;
      |SI #0#0 ! 0; |O #0#1 ! 12;
          |MENAV "    Para informar Estructura 00, seleccione ejercicio completo";
          |ERROR;
          |ACABA;
      |FINSI;
      |ATRI S; |PINTA 1529, " Conc.Explotacion:                              "; |ATRI 0;
      |C_M #0#17,1,"S";
      |C_V #0#17,1,"S";
      #0#17 = 1; |PINTA #0#17;
  |SINO;
      |PINTA 1529, "                                                ";
      |C_M #0#17,1,"N";
      |C_V #0#17,1,"N";
      #0#17 = 1;
  |FINSI;

  |HAZ cue_per;
  |SI sw_error ! 0;
      |MENAV "    No existe la estructura de descarga.";
      |ERROR;
  |FINSI;
|FPRC;

|PROCESO LeoConcep;
 |C_M #0#17,0,alfa1;
 |SI alfa1 = "N"; |ACABA; |FINSI;

 |ABRE #22;
 #22#0 = #0#17;
 |LEE 001#22=;
 |SI FSalida ! 0;
     |MENAV "    Error. No existe Concepto";
     |ERROR;
 |SINO;
     alfa1 = #22#1; alfa1 = alfa1 % 124;
     |ATRI I; |PINTA 1552, alfa1; |ATRI 0;
 |FINSI;
 |CIERRA #22;
|FPRC;

|PROCESO cue_per; || calcula la cuenta de perdidas y ganancias
  sw_error = 0;

  |SI #0#6 = 0;
      cue_perdi = "129";
      |ACABA;
  |FINSI;

  |ABRE #6;
  |DDEFECTO #6;
  #6#0 = #0#6;
  |LEE 040#6=;
  |SI FSalida ! 0;
      sw_error = 1;
      |CIERRA #6;
      |ACABA;
  |FINSI;
  cue_perdi = #6#2;
  dig_perdi = #6#3;

  |SI #0#4 = "S";
      acor_c = cue_perdi;
      acor_d = dig_perdi;
      |HAZ acortacuen;
      cue_perdi = acor_c;
      dig_perdi = acor_d;
  |FINSI;
  |CIERRA #6;
|FPRC;

|| ... procesos iniciales
|PROCESO antesque;
  swParam = 0;
  aparam = PARAMETRO;
  |QBF aparam;
  |SI aparam = "fech";
      swParam = 1;
      |HAZ AntesSesion;
      |NOME_DAT #1 eaFicbal;
  |FINSI;
|FPRC;

|PROCESO PintoSes; |TIPO 7;
  aparam = PARAMETRO; |QBF aparam;
  |SI aparam = "fech";
      |HAZ AntesSesion;
      |SI eaNomSes > "";
          |ATRI I; |PINTA 0505, "SESION: "; |PINTA 0513,eaNomSes;
          |SI eaDepSes > "";
              |PINTA 0558, "Departamento: "; |PINTA 0572, eaDepSes;
          |FINSI;
          |ATRI 0;
      |FINSI;
  |FINSI;
|FPRC;


|PROCESO conexi; |TIPO 0;

   nCam1 = empre;
   nCam2 = ejerc;

  d_mes = #0#7;
  h_mes = #0#8;
  estru = #0#6;
  r_emp = empre;
  r_per = ejerc;
  r_eje = #30#26;
  r_nom = #30#1;

  |SI #0#6 = 0; |ACABA; |FINSI;
  |SUB_EJECUTA "caexicon";

  |SI #0#13 = "N"; |ACABA; |FINSI;
  r_emp = #0#14;
  r_per = #0#15;
  r_eje = anu1 - 2000; |SI r_eje < 0; r_eje = anu1 - 1900; |FINSI;
  r_nom = #0#16;
  |SUB_EJECUTA "caexicon";
  empre = nCam1;
  ejerc = nCam2;
|FPRC;

|| ========================================================================
||                     CALCULOS de FICHEROS AUXILIARES
|| ========================================================================
|PROCESO grabatra;  || ... Graba temporal de Agrupaciones Exp0;

  |SI saldo = 0; |ACABA; |FINSI;

  |SI aMonedaC ! aMonedaA; |Y dos = 2;
      nRedo1 = EURODB;
      EURODB = nRedon;
  |FINSI;

  |SI nSwOp ! 2;

      #3#0 = #1#5;
      #3#1 = #1#4;
      #3#2 = #1#6;
      #3#3 = #1#7;
      #3#4 = #1#8;
      |LEE 101#3=;
      |SI FSalida ! 0;
          |DDEFECTO #3;
          #3#0 = #1#5;
          #3#1 = #1#4;
          #3#2 = #1#6;
          #3#3 = #1#7;
          #3#4 = #1#8;
          |HAZ leexplot;
          #3#5 = t_agru;
          #3#6 = t_epig;
          #3#7 = t_part;
          |GRABA 020#3c;
      |FINSI;

      #3acumula = #3acumula + saldo;
      |SI saldo ! 0;
          #3#15 = 1;
          #3#16 = 1;
      |FINSI;

      |GRABA 020#3a;
      |LIBERA #3;

  |FINSI;

  |SI nSwOp = 1; |VETE Acabar; |FINSI;

|| ... Graba Auxiliar de Cuentas.

  |SI #0#4 = "N"; |VETE Acabar; |FINSI;

  #5#0 = #1#5;
  #5#1 = #1#4;
  #5#2 = #1#6;
  #5#3 = #1#7;
  #5#4 = #1#8;
  #5#5 = #1#0;
  #5#6 = #1#1;
  #5#7 = #1#2;
  |LEE 000#5=;
  |SI FSalida ! 0;
      |DDEFECTO #5;
      #5#0 = #1#5;
      #5#1 = #1#4;
      #5#2 = #1#6;
      #5#3 = #1#7;
      #5#4 = #1#8;
      #5#5 = #1#0;
      #5#6 = #1#1;
      #5#7 = #1#2;
      |HAZ leexplot;
      #5#8 = t_agru;
      #5#9 = t_epig;
      #5#10 = t_part;
      #5#11 = 0;
      #5#12 = 0;
      |GRABA 020#5c;
  |FINSI;

  #5acumul2 = #5acumul2 + saldo;
  |GRABA 020#5a;
  |LIBERA #5;


  |ET Acabar;
  |SI aMonedaC ! aMonedaA; |Y dos = 2;
       EURODB = nRedo1;
  |FINSI;
|FPRC;

|PROCESO exis_expl;
  descar = #7des_exi - #7has_exi; || iniciales - finales
  |SI descar = 0; |ACABA; |FINSI;

  |SI aMonedaC ! aMonedaA; |Y dos = 2;
      impMoneda = descar;
      |HAZ ElCMoneda;
      descar = impMoneda;
  |FINSI;

  |SI descar ] 0;
      act_cuent = #7#5;     || cuenta descarga D
      act_nivel = #7#6;     || digito descarga D
  |SINO;
      act_cuent = #7#8;     || cuenta descarga H
      act_nivel = #7#9;     || digito descarga H
  |FINSI;

  acor_c = act_cuent; acor_d = act_nivel;
  |HAZ acortacuen;
  act_cuent = acor_c; act_nivel = acor_d;

  |SI act_cuent = #1#0; |Y act_nivel = #1#1;
      sw_exp = 1;
      |SI descar ] 0;
          debe = debe + descar;
      |SINO;
          haber = haber - descar;
      |FINSI;
       saldo = debe - haber;
  |FINSI;
|FPRC;

|DEFBUCLE exis2;
 #7#1;
 ;
 nDEstru, 01;
 nHEstru, 99;
 be;
 NULL, exis_expl;
|FIN;

|PROCESO exis_3000;
  sw_exp  = 1;
  impMoneda = #7has_exi;
  |SI aMonedaC ! aMonedaA; |Y dos = 2; |HAZ ElCMoneda; |FINSI;

  saldo   = saldo + impMoneda;
|FPRC;

|DEFBUCLE exis1;
 #7#2;
 ;
 nDEstru, #1#0, #1#1;
 nHEstru, alar_c, 9;
 e;
 NULL,exis_3000;
|FIN;

|PROCESO calcular;

 saldo   = 0;
 debe    = 0;
 haber   = 0;
 sw_exp  = 0;
 alfa1   = #1#0 % 101;

 |SI #0#6 ! 0;
     nDEstru = #0#6;
     nHEstru = #0#6;
 |SINO;
     nDEstru = 01;
     nHEstru = 99;
 |FINSI;

 |SI alfa1 = "3";                 |BUCLE exis1; |FINSI;
 |SI alfa1 = "6"; |O alfa1 = "7"; |BUCLE exis2; |FINSI;
 |SI sw_exp = 1;
     saldo = saldo ! EURODB;
     |ACABA;
 |FINSI;

 |PARA x = eldesde; |SI x [ elhasta; |HACIENDO x = x + 3;
       y = x + 1;

       impMoneda = #1x;
       |SI aMonedaA ! aMonedaC; |Y dos = 2; |HAZ ElCMoneda; |FINSI;
       debe  = debe  + impMoneda;

       impMoneda = #1y;
       |SI aMonedaA ! aMonedaC; |Y dos = 2; |HAZ ElCMoneda; |FINSI;
       haber = haber + impMoneda;

       saldo = debe - haber;
 |SIGUE;

 saldo = saldo ! EURODB;
|FPRC;

|PROCESO grabar;
  |HAZ digitos2;
  |SI d_error ! 0;  |ACABA;  |FINSI;
  |SI #0#4 = "N";  |Y #1#3 = "N";                     |ACABA;  |FINSI;
  |SI #0#4 = "S";  |Y #1#55 > nivel;                  |ACABA;  |FINSI;
  |SI #0#4 = "S";  |Y #1#3 = "N";  |Y #1#55 ! nivel;  |ACABA;  |FINSI;

  alar_c = #1#0;
  |HAZ alargacuen;

  |SI #0#6 ! 0; |Y #1#0 = cue_perdi;
      |ACABA;
  |FINSI;
  |SI #0#6 = 0;
      aAlfa1 = #1#0; aAlfa1 = aAlfa1 % 103;
      |SI aAlfa1 = cue_perdi; |ACABA; |FINSI;
  |FINSI;

  |HAZ calcular;
  |SI saldo ! 0;
      |SI #1#5 = "H"; |O #1#5 = "P";
          saldo = - saldo;
      |FINSI;
      |HAZ grabatra;
      |HAZ sumasaldo;
  |FINSI;
|FPRC;

|DEFBUCLE cabatodo0;
 #1#1;
 ;
 d_cuen, d_digi;
 h_cuen, h_digi;
 be;
 NULL, grabar;
|FIN;
|| ************************************************************************

|PROCESO sumasaldo;    || Calculamos cada cuenta
 |SI #1#5 = "A"; activo = activo + saldo; |FINSI;
 |SI #1#5 = "P"; pasivo = pasivo + saldo; |FINSI;
 |SI #1#5 = "D";
     |SI #1#4 = 1;
         debe1 = debe1 + saldo;
     |FINSI;
     |SI #1#4 = 2;
         debe2 = debe2 + saldo;
     |FINSI;
     |SI #1#4 = 3;
         debe3 = debe3 + saldo;
     |FINSI;
     |SI #1#4 = 4;
         debe4 = debe4 + saldo;
     |FINSI;
 |FINSI;
 |SI #1#5 = "H";
     |SI #1#4 = 1;
         haber1 = haber1 + saldo;
     |FINSI;
     |SI #1#4 = 2;
         haber2 = haber2 + saldo;
     |FINSI;
     |SI #1#4 = 3;
         haber3 = haber3 + saldo;
     |FINSI;
     |SI #1#4 = 4;
         haber4 = haber4 + saldo;
     |FINSI;
 |FINSI;
|FPRC;

|PROCESO calcula_tot;    || Calculamos totales

  activo = activo ! EURODB;
  pasivo = pasivo ! EURODB;
  debe1  = debe1 ! EURODB;
  debe2  = debe2 ! EURODB;
  debe3  = debe3 ! EURODB;
  debe4  = debe4 ! EURODB;
  haber1 = haber1 ! EURODB;
  haber2 = haber2 ! EURODB;
  haber3 = haber3 ! EURODB;
  haber4 = haber4 ! EURODB;

  punto_d1 = 0;   punto_h1 = 0;
  punto_d2 = 0;   punto_h2 = 0;
  punto_d3 = 0;   punto_h3 = 0;
  punto_d4 = 0;   punto_h4 = 0;
  punto_d5 = 0;   punto_h5 = 0;
  punto_d6 = 0;   punto_h6 = 0;

  canti = haber1 - debe1;
  |SI canti < 0;
      punto_h1 = - canti;
  |SINO;
      punto_d1 = canti;
  |FINSI;

  canti = haber2 - debe2;
  |SI canti < 0;
      punto_h2 = - canti;
  |SINO;
      punto_d2 = canti;
  |FINSI;

  canti = punto_d1 + punto_d2 - punto_h1 - punto_h2;
  |SI canti < 0;
      punto_h3 = - canti;
  |SINO;
      punto_d3 = canti;
  |FINSI;

  canti = haber3 - debe3;
  |SI canti < 0;
      punto_h4 = - canti;
  |SINO;
      punto_d4 = canti;
  |FINSI;

  canti = punto_d3 + punto_d4 - punto_h3 - punto_h4;
  |SI canti < 0;
      punto_h5 = - canti;
  |SINO;
      punto_d5 = canti;
  |FINSI;

  punto_h6 = punto_h5;
  |SI punto_h5 > 0;
      punto_h6 = punto_h6 + debe4;
  |FINSI;
  |SI punto_h6 < 0; punto_h6 = 0; |FINSI;

  punto_d6 = punto_d5;
  |SI punto_d6 > 0;
      punto_d6 = punto_d6 - debe4;
  |SINO;
      |SI debe4 > 0;                       || R.PRIEGO (24.03.99) impuesto neg.
          punto_d6 = punto_d6 - debe4;
      |FINSI;
  |FINSI;
  |SI punto_d6 < 0;
      |SI punto_h6 = 0;  punto_h6 = - punto_d6; |FINSI;
      punto_d6 = 0;
  |FINSI;
|FPRC;
|| ***********************************************************************
|| ***********************************************************************
|| ***********************************************************************
|PROCESO AstoExplota;
 |SI #23#8 = "D";
     saldo = saldo + #23#9;
 |SINO;
     saldo = saldo - #23#9;
 |FINSI;
|FPRC;

|DEFBUCLE AstoExplota;
 #23#3;
 6;
 #1#0, #1#1, "01.01.0000", 000001, #0#17;
 #1#0, #1#1, "31.12.2999", 999999, #0#17;
 e;
 NULL, AstoExplota;
|FIN;

|PROCESO grabar129;

 |PARA x = eldesde; |SI x [ elhasta; |HACIENDO x = x + 3;
       y = x + 1;
      impMoneda = #1x;
      |SI aMonedaC ! aMonedaA; |Y dos = 2;  |HAZ ElCMoneda; |FINSI;
      debe  = debe  + impMoneda;

      impMoneda = #1y;
      |SI aMonedaC ! aMonedaA; |Y dos = 2; |HAZ ElCMoneda; |FINSI;
      haber = haber + impMoneda;
      saldo = debe - haber;
 |SIGUE;
 saldo = saldo ! EURODB;
 nTSaldo = nTSaldo + saldo;

 |SI nSw = 0;
     aCuenta = #1#0;
     nNivel  = #1#1;
     nSw     = 1;
 |FINSI;

 saldo = 0;

 |BUCLE AstoExplota;
 |SI #1#5 = "P";
     saldo = - saldo;
 |FINSI;
 |SI saldo ! 0;
     nSwOp = 2; |HAZ grabatra; nSwOp = 0;
 |FINSI;
|FPRC;

|DEFBUCLE grabar129;
 #1#1;
 3;
 d_cuen, d_digi, "S";
 h_cuen, h_digi, "S";
 be;
 NULL, grabar129;
|FIN;

|PROCESO TodasLas129;
 nSw     = 0;
 aCuenta = "129";
 nNivel  = 0;
 nTSaldo = 0;
 d_cuen  = "129         "; d_digi = 0;
 h_cuen  = "129zzzzzzzzz"; h_digi = 9;
 |BUCLE grabar129;

 debe  = debe1  + debe2  + debe3  + debe4;
 haber = haber1 + haber2 + haber3 + haber4;
 saldo = nTSaldo + ( debe - haber);

 |ABRE #1;
 |DDEFECTO #1;
 #1#0 = aCuenta;
 #1#1 = nNivel;
 |LEE 041#1=;
 |CIERRA #1;

 |SI #1#5 = "P";
     saldo = - saldo;
 |FINSI;

 nSwOp = 1; |HAZ grabatra; nSwOp = 0;
 |SI #1#5 = "A"; activo = activo + saldo; |FINSI;
 |SI #1#5 = "P"; pasivo = pasivo + saldo; |FINSI;
|FPRC;

|PROCESO gr_final; || Grabar totales
 debe = 0;
 haber = 0;

 |SI #0#6 ! 0;

     |ABRE #1;
     #1#0 = cue_perdi;
     #1#1 = dig_perdi;
     |LEE 001#1=;
     |SI FSalida ! 0;
         sw_error = 1;
         |MENAV "    ATENCION !!!. NO existe la cuenta de Perdidas y Ganancias ";
         |ACABA;
     |FINSI;
     |CIERRA #1;

     |PARA x = eldesde; |SI x [ elhasta; |HACIENDO x = x + 3;
           y = x + 1;
          impMoneda = #1x;
          |SI aMonedaC ! aMonedaA; |Y dos = 2;  |HAZ ElCMoneda; |FINSI;
          debe  = debe  + impMoneda;

          impMoneda = #1y;
          |SI aMonedaC ! aMonedaA; |Y dos = 2; |HAZ ElCMoneda; |FINSI;
          haber = haber + impMoneda;
          saldo = debe - haber;
     |SIGUE;
     saldo = saldo ! EURODB;

     debe  = debe1  + debe2  + debe3  + debe4;
     haber = haber1 + haber2 + haber3 + haber4;
     saldo = saldo + ( debe - haber);

     |SI #1#5 = "P";
         saldo = - saldo;
     |FINSI;

     |HAZ grabatra; || ....................... Graba la cuenta 129. como otra mas.
     |SI #1#5 = "A"; activo = activo + saldo; |FINSI;
     |SI #1#5 = "P"; pasivo = pasivo + saldo; |FINSI;

 |SINO;

     |HAZ TodasLas129;

 |FINSI;

|| Grabar totales

 |HAZ calcula_tot;

|PARA nume1 = 1; |SI nume1 [ 14; |HACIENDO nume1 = nume1 + 1;
   saldo = 0;
   agrupaci = 99;
   epigrafe = 00;
   partida  = 00;
   |SI nume1 = 1;               bloque = 0;         tipo = "A"; |FINSI;
   |SI nume1 = 2;               bloque = 0;         tipo = "P"; |FINSI;
   |SI nume1 ] 3; |Y nume1 [ 8; bloque = nume1 - 2; tipo = "D"; |FINSI;
   |SI nume1 ] 9;               bloque = nume1 - 8; tipo = "H"; |FINSI;
   |SI bloque = 3; bloque = 2; agrupaci = 100; |FINSI;
   |SI bloque = 4; bloque = 3; |FINSI;
   |SI bloque = 5; bloque = 3; agrupaci = 100; |FINSI;
   #3#0 = tipo;
   #3#1 = bloque;
   #3#2 = agrupaci;
   #3#3 = epigrafe;
   #3#4 = partida;
   |LEE 101#3=;
   |SI FSalida ! 0;
       |DDEFECTO #3;
       #3#0 = tipo;
       #3#1 = bloque;
       #3#2 = agrupaci;
       #3#3 = epigrafe;
       #3#4 = partida;
       |HAZ titulos;
       t_agru = variable %  140;
       t_epig = variable % 4120;
       #3#5 = t_agru;
       #3#6 = t_epig;
       #3#7 = "";
       |GRABA 020#3c;
  |FINSI;
  |SI tipo = "A"; saldo = activo; |FINSI;
  |SI tipo = "P"; saldo = pasivo; |FINSI;
  |SI tipo = "D";
      |SI nume1 = 3; saldo = punto_d1; |FINSI;
      |SI nume1 = 4; saldo = punto_d2; |FINSI;
      |SI nume1 = 5; saldo = punto_d3; |FINSI;
      |SI nume1 = 6; saldo = punto_d4; |FINSI;
      |SI nume1 = 7; saldo = punto_d5; |FINSI;
      |SI nume1 = 8; saldo = punto_d6; |FINSI;
  |FINSI;
  |SI tipo = "H";
      |SI nume1 =  9; saldo = punto_h1; |FINSI;
      |SI nume1 = 10; saldo = punto_h2; |FINSI;
      |SI nume1 = 11; saldo = punto_h3; |FINSI;
      |SI nume1 = 12; saldo = punto_h4; |FINSI;
      |SI nume1 = 13; saldo = punto_h5; |FINSI;
      |SI nume1 = 14; saldo = punto_h6; |FINSI;
  |FINSI;
  #3acumula = #3acumula + saldo;
  |SI saldo ! 0;
      #3#15 = 1;
      #3#16 = 1;
  |FINSI;
  |GRABA 020#3a;
  |LIBERA #3;
|SIGUE;
|FPRC;

|| ***********************************************************************
|PROCESO cantidad;
 |SI sw_inf = 0;
     |SI menu2 = 2;
         |PRINT; sw = 2;
     |SINO;
         |HAZ operapanta;
     |FINSI;
 |SINO;
     |SI cantidad ! 0; |O cantidad2 ! 0; |O swimp = 0;
         |SI menu2 = 2;
             |PRINT; sw = 2;
         |SINO;
             |HAZ operapanta;
         |FINSI;
     |FINSI;
 |FINSI;
|FPRC;

|PROCESO cuenta1;
  sw_alguno = 1;
  cta = #5#5;
  nom = #5#7;
  cantidad = #5#11;
  cantidad2 = #5#12;
  sw_inf = 2;
  |HAZ cantidad;
  |SI menu2 = 1;
      |SI sw_error = 1; sw_alguno = 0; sw = 0; |ERROR10; |FINSI;
  |FINSI;
|FPRC;

|DEFBUCLE cabatodo5;
 #5#1;
 ;
 #3#0, #3#1, #3#2, #3#3, #3#4, "            ", 0;
 #3#0, #3#1, #3#2, #3#3, #3#4, "zzzzzzzzzzzz", 9;
 e;
 NULL, NULL, NULL, NULL, NULL, cuenta1;
 #5#5, #5#6;
|FIN;

|PROCESO impresio;
  |SI d_bloque = 0;
      |SI #3#0 = "D"; |O #3#0 = "H"; |ACABA; |FINSI; || hay algun error el la estructura
  |FINSI;
  |SI sw = 0;
      |HAZ canv_tipo;
      |SI menu2 = 1; |HAZ panta; |FINSI;
      sw = 1;
  |FINSI;

  |SI tipo ! #3#0;
      |SI menu2 = 2;
          |SI sw = 2; |PIEINF; |FINSI;
      |FINSI;
      |HAZ canv_tipo;
      |SI menu2 = 1;
          |HAZ cambiapag;
          |SI sw_error = 1; sw = 0; |ERROR10; |ACABA; |FINSI;
      |FINSI;
  |FINSI;

  |SI bloque ! #3#1; |HAZ canv_bloq; |FINSI;

  cantidad  = #3#8;
  cantidad2 = #3#10;

  |SI agrupaci ! #3#2; |HAZ canv_agru; |FINSI;

  |SI sw_agr = 0;
      |HAZ imp_agr;
      |SI menu2 = 1;
          |SI sw_error = 1; sw = 0; |ERROR10; |ACABA; |FINSI;
      |FINSI;
  |FINSI;

  |SI epigrafe ! #3#3; |HAZ canv_epi; |FINSI;

  |SI sw_epi = 0;
      |HAZ imp_epi;
      |SI menu2 = 1;
          |SI sw_error = 1; sw = 0; |ERROR10; |ACABA; |FINSI;
      |FINSI;
  |FINSI;

  |SI #3#4 ! 0; |Y #3#4 < 99;
      sw_inf = 1;
      variable = "          " + #3#7;
      |HAZ cantidad;
      |SI menu2 = 1;
          |SI sw_error = 1; sw = 0; |ERROR10; |ACABA; |FINSI;
      |FINSI;
  |FINSI;

  partida = #3#4;
  |SI #0#4 = "S";
      sw_alguno = 0;
      |BUCLE cabatodo5;
      |SI menu2 = 1;
          |SI sw_error = 1; sw = 0; |ERROR10; |ACABA; |FINSI;
      |FINSI;
      |SI sw_alguno = 1;
          sw_inf = 4;
          |SI menu2 = 2;
              |PRINT; sw = 2;
          |SINO;
              |HAZ operapanta;
          |FINSI;
      |FINSI;
  |FINSI;
|FPRC;

|PROCESO imp_agr;
  sw_inf = 0;
  variable = #3#5;
  |SI #3#2 ] 99;
      sw_epi = 1;
      sw_inf = 3;
      variable = #3#5 + #3#6;
  |SINO;
      |SI #3#3 = 0; |Y #3#4 = 0; || ... Agrupacion con importe.
          sw_epi = 1;
          sw_inf = 1;
      |FINSI;
  |FINSI;
  sw_agr = 1;
  |HAZ cantidad;
|FPRC;

|PROCESO imp_epi;
  sw_inf = 0;
  variable = "     " + #3#6;
  |SI #3#4 = 0;
      sw_inf = 1;
  |FINSI;
  sw_epi = 1;
  |SI #3#3 = 0; |ACABA; |FINSI;
  swimp = 0; |SI #3#16 = 0; swimp = 1; |FINSI;
  |HAZ cantidad;
  swimp = 0;
|FPRC;

|PROCESO canv_tipo;
  sw_agr = 0;
  sw_epi = 0;
  tipo     = #3#0;
  bloque   = #3#1;
  agrupaci = #3#2;
  epigrafe = #3#3;
  partida  = 0;
  |SI #3#0 = "A"; doh = "ACTIVO"; |FINSI;
  |SI #3#0 = "P"; doh = "PASIVO"; |FINSI;
  |SI #3#0 = "D"; doh = "DEBE";   |FINSI;
  |SI #3#0 = "H"; doh = "HABER";  |FINSI;
|FPRC;

|PROCESO canv_bloq;
  |SI #3#8 ! 0; |O #3#10 ! 0;
      sw_inf = 4;
      |SI menu2 = 2;
          |PRINT; sw = 2;
      |SINO;
          |HAZ operapanta;
      |FINSI;
  |FINSI;
  sw_epi = 0;
  bloque   = #3#1;
  agrupaci = #3#2; |SI agrupaci ] 99; sw_agr = 0; |FINSI;
  epigrafe = #3#3;
|FPRC;

|PROCESO canv_agru;
  |SI #3#8 ! 0; |O #3#10 ! 0; |O #3#15 ! 0;
      sw_inf = 4;
      |SI menu2 = 2;
          |PRINT; sw = 2;
      |SINO;
          |HAZ operapanta;
      |FINSI;
  |FINSI;
  sw_agr = 0;
  sw_epi = 0;
  agrupaci = #3#2;
  epigrafe = #3#3;
|FPRC;

|PROCESO canv_epi;
  sw_epi = 0;
  epigrafe = #3#3;
|FPRC;

|DEFBUCLE cabatodo2;
 #3#1;
 ;
 d_tipo, d_bloque,  00, 00, 00;
 h_tipo, h_bloque, 100, 99, 99;
 be;
 NULL, impresio;
|FIN;

|PROCESO cabatodo1;
 titulo = "     < BALANCE DE SITUACION >     ";
 d_tipo = "A";
 h_tipo = "P";
 d_bloque = 0;
 h_bloque = 0;
 sw = 0;
 |BUCLE cabatodo2;
 |SI sw ] 1;
     |SI menu2 = 2;
         |SI sw = 2; |PIEINF; |FINSI;
     |SINO;
         swfinal = 1;
         |HAZ cambiapag;
         |SI sw_error = 1; |ACABA; |FINSI;
         swfinal = 0;
     |FINSI;
 |FINSI;
 titulo = "< CUENTA DE PERDIDAS Y GANANCIAS >";
 d_tipo = "D";
 h_tipo = "H";
 d_bloque = 1;
 h_bloque = 9;
 sw = 0;
 |BUCLE cabatodo2;
 |SI sw ] 1;
     |SI menu2 = 2;
         |SI sw = 2;  |PIEINF; |FINSI;
     |SINO;
         swfinal = 1;
         |HAZ cambiapag;
         |SI sw_error = 1; |ACABA; |FINSI;
         swfinal = 0;
     |FINSI;
 |FINSI;
|FPRC;

|| *******************************************************************

|PROCESO HagoTodo;
  mesi = #0#2; mesf = #0#3;
  fec  = #0#12;
  |SI #0#1 ! 0; |Y #0#1 < nMesAper;
      #0#0 = #0#1;
  |FINSI;

  eldesde = (#0#0 * 3) + 9;
  elhasta = (#0#1 * 3) + 9;
  des_exi =  #0#7 + 11;
  has_exi =  #0#8 + 11;
  d_cuen = "            "; d_digi = 0;
  h_cuen = "999999999999"; h_digi = 9;
  nivel = #0#5;

|PARA dos = 1; |SI dos [ 2; |HACIENDO dos = dos + 1;
      acumula = 8;
      acumul2 = 11;
      activo = 0;
      pasivo = 0;
       explo = 0;
      debe1  = 0;
      debe2  = 0;
      debe3  = 0;
      debe4  = 0;
      haber1 = 0;
      haber2 = 0;
      haber3 = 0;
      haber4 = 0;
      |SI dos = 2; |Y #0#13 = "N";
          |SAL;
      |FINSI;
      |SI dos = 2;
          acumula = 10;
          acumul2 = 12;
          |DBASE dir_fich;
          dir_fich = dir_fich + "fich/";
          alfa1 = #0#14;   alfa1 = "00000" + alfa1;   alfa1 = alfa1 % -105;
          alfa2 = #0#15;   alfa2 = "0"   + alfa2;   alfa2 = alfa2 % -101;
          alfa3 = dir_fich + alfa1 + alfa2 + "/";
          |PATH_DAT #1 alfa3;
          |PATH_DAT #2 alfa3;
          |PATH_DAT #9 alfa3;
          |PATH_DAT #6 alfa3;
          |PATH_DAT #7 alfa3;
          |PATH_DAT #8 alfa3;
          |PATH_DAT #21 alfa3;
          |PATH_DAT #23 alfa3;
          |HAZ VeoFecAper;
          |ABRE #30;
          #30#2 = #0#14;
          #30#3 = #0#15;
          |LEE 011#30=;
          |CIERRA #30;
          enEmpresa = #0#14;
          enPeriodo = #0#15;
          |HAZ LeeMonedaI;
          aMonedaC = eaMoneda;
          |SI aMonedaA = "E"; EURODB = 2; |FINSI;
  |FINSI;
  |ABRE #8;
  |LEE 111#8p;
  |SI #8#2 = "S";  |Y #8#3 = "N";  |Y aparam = "";
      |SI dos = 2;
          pathbal = alfa3;
      |SINO;
          |DFICE pathbal;
      |FINSI;
      balance = 1;
      |CIERRA #8;
      |SUB_EJECUTA "careccue.tab";
  |FINSI;
  |CIERRA #8;

||... Modificar Cuentas a balance segun saldo
  |SI dos = 1;
      r_emp = empre;
      r_per = ejerc;
  |SINO;
      r_emp = #0#14;
      r_per = #0#15;
  |FINSI;
  eldesde = (#0#0 * 3) + 11;
  elhasta = (#0#1 * 3) + 11;
  |SUB_EJECUTA "carecsig.run";
  eldesde = (#0#0 * 3) + 9;
  elhasta = (#0#1 * 3) + 9;
|| ---------------------------------------------------------------------
  |SI aMonedaA = "E"; EURODB = 2; |FINSI;

  |HAZ cue_per;  || calculamos la cuenta de perdidas y ganancias.

  |ABRET #1;
  |ABRE #2;
  |ABRE #9;
  |ABRE #3;
  |ABRE #5;
  |BUCLE cabatodo0;      || Bucle de Calculos cuentas
  |HAZ gr_final;         || Graba Totales

  |CIERRAT #1;
  |CIERRA #2;
  |CIERRA #9;
  |CIERRA #3;
  |CIERRA #5;
  |SI sw_error = 1; |ACABA; |FINSI;

  d_cuen = "            "; d_digi = 0;
  h_cuen = "999999999999"; h_digi = 9;
  nivel = #0#5;
|SIGUE; || ==================================================================
          |ABRE #30;
          #30#2 = empre;
          #30#3 = ejerc;
          |LEE 011#30=;
          |CIERRA #30;
          empcon = #30#1;
          |SI #30#26 ] 80;
              ejecon = 1900 + #30#26;
          |SINO;
              ejecon = 2000 + #30#26;
          |FINSI;
          eaMoneda = aMonedaA; |HAZ MonedasI;

  |ABRE #3;
  |ABRE #5;

  |HAZ cabatodo1;      || Bucle de impresion

  |SI menu2 = 2;  |Y enMasDep = 0; || Ha elegido Impresora
      |FININF;
      |FINIMP;
      nswPrim  = 0;
  |FINSI;
  |CIERRA #3;
  |CIERRA #5;

  |DELINDEX #3;
  |DELINDEX #5;
|FPRC;

|PROCESO PorSesiones;
   eaDepSes = #110#1;
   eaDesDep = #110#7;

   alfa1 = #110#6; |QBF alfa1;
   |SI alfa1 ! "";
       eaFicbal  = alfa1;
       |NOME_DAT #1 eaFicbal;
       |HAZ HagoTodo;
   |FINSI;
|FPRC;

|DEFBUCLE PorSesiones;
 #110#1;
 ;
 enNumSes, "  ";
 enNumSes, "zz";
 ;
 NULL, PorSesiones;
|FIN;

|| ******************** Programa por TIPO 3 **************************
|PROCESO impre;

  aMonedaA = eaMoneda;
  |SI #0#13 = "N"; aMonedaC = aMonedaA; |FINSI;

  |HAZ antesque;
  |SI aparam = ""; |HAZ conexi; |FINSI;
  eaMoneda = aMonedaA; |HAZ MonedasI;

  aFichero = ("3t" + Usuario) % 108;
  |NOME_DAT #3 aFichero;
  aFichero = ("5t" + Usuario) % 108;
  |NOME_DAT #5 aFichero;

  |ABRE #3; |CIERRA #3; |DELINDEX #3;
  |ABRE #5; |CIERRA #5; |DELINDEX #5;

  |SI menu2 = 2;  || Ha elegido Impresora
      |SI PARAMETRO ! ""; |Y PARAMETRO ! "fech";
          Impresora = eaNomImp + "?"+ eaUnidadBTmp + "\tmp1,,,?";
          |SI Compara = 1;
              Impresora = Impresora + "Bal.Sit.Comp";
          |SINO;
              Impresora = Impresora + "Bal.Sit.";
          |FINSI;
          |SI enBasImp ! 0;
              |IMPRE -1;
              |SI FSalida ! 0;
                  Impresora = Impresora - eaNomImp;
                  |IMPRE 0;
              |FINSI;
          |SINO;
              |IMPRE 0;
          |FINSI;
      |SINO;
          |IMPRE 0;
      |FINSI;
      |SI FSalida ! 0;  |ACABA;  |FINSI;

      |HAZ los_informes;
      |SI #0#13 = "N";
          informe = que_i1;
      |SINO;
          informe = que_i2;
      |FINSI;

      |INFOR informe;
      |SI FSalida ! 0;
          |MENAV "     Error en informe.";
          |FINIMP;
          |ACABA;
      |FINSI;
      nswPrim  = 1;
  |FINSI;

  |SI enMasDep = 0;
      |HAZ HagoTodo;
  |SINO;
      |BUCLE PorSesiones;
      |SI nswPrim = 1; |Y menu2 = 2; |FININF; |FINIMP; |FINSI;
      eaDepSes = "TODOS";
  |FINSI;
|FPRC;

|PROCESO leexplot;  || Lee el fichero de explotacion
  t_agru = "";
  t_epig = "";
  t_part = "";
  |SI #1#4 = 0;
      #9#0 = #1#5;
      #9#1 = #1#6;
      #9#3 = #1#7;
      #9#5 = #1#8;
      |LEE 040#9=; |SI FSalida ! 0; |DDEFECTO #9; |FINSI;
      t_agru = #9#2;
      t_epig = #9#4;
      t_part = #9#6;
 |SINO;
      #2#0 = #1#4;
      #2#1 = #1#5;
      #2#2 = #1#6;
      #2#4 = #1#7;
      #2#6 = #1#8;
      |LEE 040#2=; |SI FSalida ! 0; |DDEFECTO #2; |FINSI;
      t_agru = #2#3;
      t_epig = #2#5;
      t_part = #2#7;
  |FINSI;
|FPRC;

|PROCESO acortacuen;
  |SI #0#4 = "N"; |ACABA; |FINSI;
  |SI nivel < acor_d;
      |SI nivel = 1;  traraf = 100 + dig4;  |FINSI;
      |SI nivel = 2;  traraf = 100 + dig5;  |FINSI;
      |SI nivel = 3;  traraf = 100 + dig6;  |FINSI;
      |SI nivel = 4;  traraf = 100 + dig7;  |FINSI;
      |SI nivel = 5;  traraf = 100 + dig8;  |FINSI;
      acor_c = acor_c % traraf;
      acor_d = nivel;
      acor_c = acor_c + "              ";
      acor_c = acor_c % 112;
  |FINSI;
|FPRC;

|PROCESO alargacuen;
  |QBF alar_c;
  alar_c = alar_c + "99999999999999";
  alar_c = alar_c % 112;
|FPRC;

|PROCESO titulos;

|SI nume1 =  1; variable = "123456789*123456789*123456789*123456789*123456789*123456789*"; |FINSI;
|SI nume1 =  1; variable = "                         T O T A L   A C T I V O :          "; |FINSI;
|SI nume1 =  2; variable = "                         T O T A L   P A S I V O :          "; |FINSI;
|SI nume1 =  3; variable = "  I. BENEFICIOS DE EXPLOTACION ............................."; |FINSI;
|SI nume1 =  4; variable = " II. RESULTADOS FINANCIEROS POSITIVOS ......................"; |FINSI;
|SI nume1 =  5; variable = "III. BENEFICIOS DE LAS ACTIVIDADES ORDINARIAS .............."; |FINSI;
|SI nume1 =  6; variable = " IV. RESULTADOS EXTRAORDINARIOS POSITIVOS .................."; |FINSI;
|SI nume1 =  7; variable = "  V. BENEFICIO ANTES DE IMPUESTOS .........................."; |FINSI;
|SI nume1 =  8; variable = " VI. RESULTADO DEL EJERCICIO (BENEFICIOS) .................."; |FINSI;
|SI nume1 =  9; variable = "  I. PERDIDAS DE EXPLOTACION ..............................."; |FINSI;
|SI nume1 = 10; variable = " II. RESULTADOS FINANCIEROS NEGATIVOS ......................"; |FINSI;
|SI nume1 = 11; variable = "III. PERDIDAS DE LAS ACTIVIDADES ORDINARIAS ................"; |FINSI;
|SI nume1 = 12; variable = " IV. RESULTADOS EXTRAORDINARIOS NEGATIVOS .................."; |FINSI;
|SI nume1 = 13; variable = "  V. PERDIDAS ANTES DE IMPUESTOS ..........................."; |FINSI;
|SI nume1 = 14; variable = " VI. RESULTADO DEL EJERCICIO (PERDIDAS) ...................."; |FINSI;
|FPRC;

|| ***********************************************************************
|PROCESO operapanta;   || Operaciones por pantalla;

|SI lin > 1300;
    |SI sw_inf = 4; |ACABA; |FINSI;
    swfinal = 0;
    |HAZ cambiapag;  || Cambia de pagina
    |SI sw_error = 1; |ACABA; |FINSI;
|FINSI;

 |C_V #10#9, 1,"N";
 |C_V #10#10,1,"N";
 |C_V #10#11,1,"N";
 |C_V #10#12,1,"N";
 |C_V #10#13,1,"N";
 |C_V #10#14,1,"N";

 |PDEFECTO #10, 9,15;

 |SI sw_inf = 0;
     #10#12  = variable; |C_V #10#12,1,"S";
 |FINSI;

 |SI sw_inf = 1; |O sw_inf = 3;
     #10#12  = variable; |C_V #10#12,1,"S";
     |SI #0#13 = "S";
         #10#13 = cantidad;  |C_V #10#13,1,"S";
         #10#14 = cantidad2; |C_V #10#14,1,"S";
     |SINO;
         #10#14 = cantidad;  |C_V #10#14,1,"S";
     |FINSI;
  |FINSI;
  |SI sw_inf = 2;
      #10#9 = cta + " " + nom; |C_V #10#9,1,"S";
      #10#10 = cantidad;       |C_V #10#10,1,"S";
      |SI #0#13 = "S";
          #10#11 = cantidad2; |C_V #10#11,1,"S";
      |FINSI;
   |FINSI;

 Pos = lin;
 |PINDA #0#10;         || Pinta las lineas
 Pos = -1;
 lin = lin + 100;     || Posicionar una linea mas abajo
|FPRC;

|PROCESO cambiapag;
 sw_error = 0;
 lin = 100;
 |PAUSA;
 |SI 1 = FSalida;|O 7 = FSalida;
     sw_error = 1;
 |SINO;
     |SI swfinal = 0; |HAZ panta; |FINSI;
 |FINSI;
|FPRC;

|PROCESO PintaPantalla;
  |PINPA #0#10;       || Pinta la pantalla del informe
  |SI eaNomSes > "";
      |ATRI I; |PINTA 2305, "SESION: "; |PINTA 02313,eaNomSes;
      |SI eaDepSes > "";
          |PINTA 2358, "Departamento: "; |PINTA 2372, eaDepSes;
      |FINSI;
      |ATRI 0;
  |FINSI;
|FPRC;

|PROCESO panta;
    lin = 100;
    |DDEFECTO #10;
    |HAZ PintaPantalla;
    #10#0 = fec;        || Fecha
    #10#1 = #30#2;      || Empresa
    #10#2 = #30#26;     || Ejercicio
    #10#3 = #30#1;      || Nombre Empresa
    #10#4 = mesi;       || mes desde
    #10#5 = mesf;       || mes hasta
    #10#6 = doh;        || titulo (Activo/Pasivo/Debe/Haber)
    #10#7 = anu;
    #10#8 = anu1;

    |SI #0#13 = "S";
        |ATRI R;
        |PINTA 0856, "A¤o: ";
        |PINTA 0870, "A¤o: ";
        |ATRI 0;
        |C_V #10#7,1,"S";
        |C_V #10#8,1,"S";
    |SINO;
        |C_V #10#7,1,"N";
        |C_V #10#8,1,"N";
    |FINSI;
 |C_V #10#9, 1,"N";
 |C_V #10#10,1,"N";
 |C_V #10#11,1,"N";
 |C_V #10#12,1,"N";
 |C_V #10#13,1,"N";
 |C_V #10#14,1,"N";
    |PINDA #0#10;
|FPRC;

|PROCESO Empresas;

    nLegalia = 1;
    #cabucem1#0 = eaLegEmp;
    #cabucem1#1 = eaLegPer;
    cod1 = eaLegEmp;
    cod2 = eaLegPer;
    emEmp = 1;
    |HAZ leelo;
    |SI swerror ! 0;  |ACABA;   |FINSI;

    |DBASE fich_alta; |QBF fich_alta;
    fich_alta = fich_alta + "fich/" + (("00000" + #cabucem1#0) % -105) + #cabucem1#1 + "/";
    |PATH_DAT #0 fich_alta; |PATH_DAT #1 fich_alta;
    |PATH_DAT #2 fich_alta; |PATH_DAT #5 fich_alta;
    |PATH_DAT #6 fich_alta; |PATH_DAT #7 fich_alta;
    |PATH_DAT #8 fich_alta; |PATH_DAT #9 fich_alta;
    |PATH_DAT #3 fich_alta; |PATH_DAT #10 fich_alta;
    |PATH_DAT #21 fich_alta; |PATH_DAT #23 fich_alta;

  ||      #30#2 = #48#2; #30#3 = #48#3; |LEE 000#30=;
  ||      |HAZ inicio;
    aMonedaA = eaMoneda;
    |SI #0#13 = "N"; aMonedaC = aMonedaA; |FINSI;

    |DDEFECTO #0;
    |SI Compara = 1;
        nUnError = 0;
        |ABRE #30;
        #30#2 = #cabucem1#0;
        #30#3 = #cabucem1#1 - 1; |SI #30#3 < 0; #30#3 = 9; |FINSI;
        |LEE 000#30=;
        |SI FSalida = 0;
            #cabatodo#13 = "S";
            #cabatodo#14 = #cabucem1#0;
            |SI #cabucem1#1 > 0;
                #cabatodo#15 = #cabucem1#1 - 1;
            |SINO;
                #cabatodo#15 = 9;
            |FINSI;
            |CIERRA #30;
            |HAZ esbuena; |HAZ esbuena1;
        |SINO;
            |CIERRA #30;
            nUnError = 1;
        |FINSI;
        |SI nUnError = 1;  |ACABA;  |FINSI;
    |FINSI;

        #cabatodo#0  = Per1; #cabatodo#7 = Per1;
        #cabatodo#1  = Per2; #cabatodo#8 = Per2;
        Campo = 0; |HAZ pintames;
        Campo = 1; |HAZ pintames;
        Campo = 7; |HAZ pintames;
        Campo = 8; |HAZ pintames;
        #cabatodo#4  = "S";
        #cabatodo#5 = #48#13;
        |SI eaUltNiv = "N"; #0#5 = #0#5 - 1; |FINSI;
        #0#12 = efFecLeg;
        |HAZ impre;
|FPRC;


|PROGRAMA;
   aparam = PARAMETRO; |QBF aparam;

   |SI aparam = ""; |O aparam = "fech";
      |HAZ inicio;
      |DFICE alfa3;
      |HAZ VeoFecAper;

      |CLS;
      |CABEZA "Balance situacion y cta.resultados";
      |PINPA #0#0; |PINDA #0#0;
      |ENDATOS #1#0;
      |SI FSalida = 0;
         |MENU menu;
         menu2 = FSalida;
         |SI 0 ] menu2;
            |ACABA;
         |FINSI;
         |HAZ impre;
      |FINSI;

   |SINO;

      Comodin = PARAMETRO;
      Comodin = Comodin - ",C";
      |SI FSalida ! 0;
         Compara = 1;
      |SINO;
         Compara = 0;
      |FINSI;
      Comodin = Comodin - ",";
      |SI FSalida ! 0;
         Calc = ((FSalida / 100) ! 0) + 99;
         Comodin1 = Comodin % 0101;
         Per1 = Comodin1; Comodin = Comodin - Comodin1;
         Per2 = Comodin;
      |FINSI;
      menu2 = 2;
      Empresa = #48#2; Periodo = #48#3;
      |HAZ Empresas;

      #48#2 = Empresa;
      #48#3 = Periodo;
      |LEE 000#48=;
      |SI FSalida = 0;
         |DBASE fich_alta; |QBF fich_alta;
         fich_alta = fich_alta + "fich/" + (("00000" + #cabucem1#0) % -105) + #cabucem1#1 + "/";
         |PATH_DAT #0 fich_alta; |PATH_DAT #1 fich_alta;
         |PATH_DAT #2 fich_alta; |PATH_DAT #5 fich_alta;
         |PATH_DAT #6 fich_alta; |PATH_DAT #7 fich_alta;
         |PATH_DAT #8 fich_alta; |PATH_DAT #9 fich_alta;
         |PATH_DAT #3 fich_alta; |PATH_DAT #10 fich_alta;
         |PATH_DAT #21 fich_alta; |PATH_DAT #23 fich_alta;
      |FINSI;
   |FINSI;
|FPRO;


|| *******************************************************
|PROCESO LeeDiario;
 fFecha = #21#1;
 |ERROR10;
|FPRC;

|DEFBUCLE LeeDiario;
 #21#1;
 ;
 nDiario, fec1, 000001;
 nDiario, fec2, 999999;
 ;
 NULL, LeeDiario;
|FIN;

|PROCESO VeoFecAper;

  |PATH_DAT #21 alfa3;
  fFecha = "01.00.0000"; nDiario = 0;  |BUCLE LeeDiario;
  aAlfa1 = fFecha; aAlfa1 = aAlfa1 % 402; nMesAper = aAlfa1;
  fFecha = "01.00.0000"; nDiario = 99; |BUCLE LeeDiario;
  aAlfa1 = fFecha; aAlfa1 = aAlfa1 % 402; nMesCier = aAlfa1;
|FPRC;
