|FICHEROS;
    imppteos #0;

    caclipro #12;   || clientes/proveedores
    caivarep #13;   || iva repercutido
    calibros #14;   || Libros Iva
    caivasop #15;   || iva soportado

    camandia #20;   || diarios AGICONT
    camaasic #22;   || asientos cabs. AGICONT
    camaasil #23;   || asientos lins. AGICONT
    cacuenta #24;   || cuentas AGICONT
    caconasi #26;   || contador asientos AGICONT
    catradia #27;   || diario de trabajo
    cacuebal #28;   || cuentas de balance

    imptteo1 #40;   || def trabajo importacion Asientos
    imptteo2 #41;   || def trabajo importacion Facturas
    imppeur3 #42;   || def trabajo Cuentas
    imptteo3 #43;   || def trabajo formateo de ficheros
    ca8m0002 #422;
|FIN;

|VARIABLES;
    &enDiario   = 0;
    &enContador = 0;
    &modcon = 0;

    alfa1 = "";
    alfa2 = "";
    alfa3 = "";
    alfa4 = "";
    alfa5 = "";
    alfa6 = "";
    alfa7 = "";
    cm    = "";
    x     = 0;
    nume1 = 0;
    nume2 = 0;
    nume3 = 0;
    nume4 = 0;
    nume5 = 0;
    para1 = 0;
    para2 = 0;
    para3 = 0;
    para4 = 0;

 desl_rep = "";
 desl_sop = "";
    estado  = 0;
    signe   = "";
    conte   = "";
    digcu   = 0;
    numfac  = 0;
    tipof   = "";
    diario  = 0;
    docume  = 0;
    fecha   = @;
    linea   = 0;

    rlong   = 0;
    afecha  = "";
    anume   = "";
    importe = 0;
    swdiario = 0;

    descrip = "";
    diaant  = 0;
    ordant  = 0;
    fecant  = "";
    nocta   = 0;
    asient  = 0;
    sw      = 0;
    trim1   = "";
    trim2   = "";
    trim3   = "";

    ini = 0;
    fecalf = "";
    mes = "";
    mesfec = 0;
    mesac = 0;
    a = 0;
    b = 0;
    c = 0;

    lon = 0;
    aa = 0;
    mensa = "";
    aar = "";
    traba = "";
    errcuent = "aaaaaaaaaaaa";

    fabre = "";
    fcanl = "";
    field = "";

    guarda_pos = 0;
    RegAstos   = 0;
    RegFact    = 0;
    sw_fecha   = 0;

     CampoDni   = "";
     Letra1     = "";
     Letra2     = "";
     Letra3     = "";
     Letra4     = "";
     Letra5     = "";
     Letra6     = "";
     Letra7     = "";
     Letra8     = "";
     Caracter   = "";
     Carac1     = 0;
     Carac2     = 0;
     Carac3     = 0;
     Carac4     = 0;
     Carac5     = 0;
     XPara      = 0;
     titulo     = "";

   swdos  = 0;
   aIVA1 = "";
   aIVA2 = "";
   aIVA3 = "";
   iva   = 0;

   Comillas = 0;
   Grabando = "";
   sw_rup = 0;
   FormaFich = "";

    &eaVarDni  = "";
    &enCalcCif = 0;
|FIN;

|INCLUYE acceso_i;
|INCLUYE tempo_i;
|INCLUYE varnue_i;
|INCLUYE defec1_i;

|| *************************************************************************
|| ********************* CALCULOS PANTALLA
|| *************************************************************************

|PROCESO mayus; |TIPO 0;
 #0Campo = #0Campo > #0Campo;
 |PINTA #0Campo;
 |SI Campo = 11; |ACABA; |FINSI;

 alfa1 = #0Campo;
 |SI Campo = 0; |C_M #0#4,1, alfa1; |FINSI;
 |SI Campo = 1;
     |C_M #0#5, 1, alfa1;
     |C_M #0#8, 1, alfa1;
     |C_M #0#19, 1, alfa1;
     |C_M #0#20, 1, alfa1;
 |FINSI;
 |SI Campo = 2;
     |C_M #0#6, 1, alfa1;
     |C_M #0#9, 1, alfa1;
     |C_M #0#13,1, alfa1;
     |C_M #0#14, 1,alfa1;
     || ....... |C_M #0#17,1, alfa1;
 |FINSI;
 |SI Campo = 3;
     |C_M #0#7, 1, alfa1;
     |C_M #0#10,1, alfa1;
     |C_M #0#15,1, alfa1;
     |C_M #0#16,1, alfa1;
     || ....... |C_M #0#18,1, alfa1;
 |FINSI;
|FPRC;

|PROCESO libro; |TIPO 0;
 |ABRE #14;
 #14#0 = #0Campo;
 |LEE 001#14=;
 |SI FSalida ! 0;
     |MENAV "0000Atencion.! NO EXISTE LIBRO DE IVA";
     |CIERRA #14;
     |ERROR;
     |ACABA;
 |FINSI;
 |SI Campo = 9;
     |SI #14#2 ! "R";
         |MENAV "0000Atencion.! Libro de IVA Soportado ... ";
         |ERROR;
     |SINO;
         desl_rep = #14#1;
         |ATRI I; |PINTA 1627, #14#1; |ATRI 0;
     |FINSI;
 |SINO;
     |SI #14#2 ! "S";
         |MENAV "0000Atencion.! Libro de IVA Repercutido ... ";
         |ERROR;
     |SINO;
         desl_sop = #14#1;
         |ATRI I; |PINTA 1927, #14#1; |ATRI 0;
     |FINSI;
 |FINSI;
 |CIERRA #14;
|FPRC;
|| ************************************************************************
|| *************************************************************************

|PROGRAMA;
 |CLS;
 |CABEZA "Enlace THEOS";
 |PINPA #0#0;
 |HAZ inicio;
 |HAZ eVarEmpresa;
 |DDEFECTO #0;
 |ABRE #0;
 |LEE 101#0p;
 |SI FSalida ! 0; |GRABA 020#0b; |FINSI;
 |PINDA #0#0;
 |ENDATOS #1#0;
 |SI FSalida = 0;
     |SI #0#11 = "SI";
             #0#11 = "NO";
         |GRABA 020#0a;
         |HAZ tipo3;
     |FINSI;
 |FINSI;
 |LIBERA #0;
 |CIERRA #0;

 || ............... Terminar
 |SI modcon = 0;
     |ABRET #27;
     |DDEFECTO #27;
     #27#0 = codcon;
     |LEE 040#27=;
     |BORRA 101#27a;
     |CIERRAT #27;
 |SINO;
     |CORRE "camaapua.tab"
 |FINSI;
|FPRO;

|| **************************************************************************
|| ...................... Repercutido
|PROCESO fra_rep;
 |DDEFECTO #13;
 #13#0  = #0#9;      || libro
 #13#2  = numfac;    || nro. fra.
 #13#27 = "  ";      || serie (inhibida)
 |LEE 101#13=;
 estado = FSalida;
 |SI estado = 0;
     |AVISO;
     alfa1 = "    La factura [" + #13#0 + "-" + #13#2 + "] YA existe ...";
     |MENSA alfa1;
     |CONFI "    NRemplazarla (S/N): ";
     |SI FSalida = 0;
         |HAZ gra_factura_r;
     |SINO;
         |MENAV "    Deberia revisar el Libro de IVA Repercutido ...";
         |DDEFECTO #13;
     |FINSI;
 |SINO;
     |HAZ gra_factura_r;
 |FINSI;
 |LIBERA #13;
|FPRC;

|PROCESO gra_factura_r;
 #13#1 = desl_rep;
 #13#3 = #41#2;          || Fecha
 #13#4 = #41#3;          || Cuenta
 #13#5 = #41#4;          || digito cuenta
 #13#6 = #0#13;          || Concepto
 descrip = #0#14; |QBF descrip;
 descrip = descrip + " " + #41#7;
 #13#7 = descrip;        || Descripcion

 #13#26 = #41#8;  || Total Factura
 #13#8  = #41#9;  || Base Imponible
 #13#14 = #41#10; || IVA
 |HAZ tpiva;
 #13#11 = iva;

 |HAZ periodo;
 #13#50 = #41#5;
 #13#51 = #41#6;
 |SI estado ! 0; |GRABA 020#13n; |FINSI;
 |SI estado = 0; |GRABA 020#13a; |FINSI;
 |BORRA 020#41a;
 |LIBERA #41;
|FPRC;

|| ...................... Soportado
|PROCESO fra_sop;
 |DDEFECTO #15;
 #15#0  = #0#10;      || libro
 #15#2  = numfac;    || nro. fra.
 #15#27 = "  ";      || serie (inhibida)
 |LEE 101#15=;
 estado = FSalida;
 |SI estado = 0;
     |AVISO;
     alfa1 = "    La factura [" + #15#0 + "-" + #15#2 + "] YA existe ...";
     |MENSA alfa1;
     |CONFI "    NRemplazarla (S/N): ";
     |SI FSalida = 0;
         |HAZ gra_factura_s;
     |SINO;
         |MENAV "    Deberia revisar el Libro de IVA Soportado ...";
         |DDEFECTO #15;
     |FINSI;
 |SINO;
     |HAZ gra_factura_s;
 |FINSI;
 |LIBERA #15;
|FPRC;

|PROCESO gra_factura_s;
 #15#1 = desl_sop;
 #15#3 = #41#2;          || Fecha
 #15#4 = #41#3;          || Cuenta
 #15#5 = #41#4;          || digito cuenta
 #15#6 = #0#15;          || Concepto
 descrip = #0#16; |QBF descrip;
 descrip = descrip + " " + #41#7;
 #15#7 = descrip;        || Descripcion

 #15#26 = #41#8;  || Total Factura
 #15#8  = #41#11; || Base Imponible
 #15#14 = #41#12; || IVA
 |HAZ tpiva;
 #15#11 = iva;

 |HAZ periodo_s;
 #15#50 = #41#5;
 #15#51 = #41#6;
 |SI estado ! 0; |GRABA 020#15n; |FINSI;
 |SI estado = 0; |GRABA 020#15a; |FINSI;
 |BORRA 020#41a;
 |LIBERA #41;
|FPRC;

|| --------------------------------------------------------------------
|PROCESO tpiva;
|SI tipof = "C";
       nume1 = (#41#12 * 100) / #41#11;
|SINO;
       nume1 = (#41#10 * 100) / #41#9;
|FINSI;
 iva = nume1 ! 0;
 |SI nume1 > 15;   iva = 16;   |ACABA; |FINSI;
 |SI nume1 > 6;    iva = 7;    |ACABA; |FINSI;
 |SI nume1 > 3;    iva = 4;    |ACABA; |FINSI;
 |SI nume1 > 0.6;  iva = 1;    |ACABA; |FINSI;
 |SI nume1 > 0;    iva = 0.5;  |ACABA; |FINSI;
|FPRC;

|| --------------------------------------------------------------------
|PROCESO hacer2;
 |PINTA 2230, #41#1;
 |PINTA 2237, #41#2;
 sw = 1;

 numfac = #41#1;
 |SI tipof  = "C";
     |HAZ fra_sop;
 |SINO;
     |HAZ fra_rep;
 |FINSI;
|FPRC;

|DEFBUCLE theos2;
 #41#2;
 ;
 000001;
 999999;
 e;
 NULL, hacer2;
|FIN;

|PROCESO pase_facturas;
 |ABRE #12;     || Clientes/Proveedores
 |SI tipof = "V"; |ABRE #13; |FINSI;    || IVA Repercutido
 |SI tipof = "C"; |ABRE #15; |FINSI;    || IVA Soportado
 |ABRET #24;     || Plan Contable

 sw = 0;
 |BUCLE theos2;

 |CIERRA #12;
 |SI tipof = "V"; |CIERRA #13; |FINSI;    || IVA Repercutido
 |SI tipof = "C"; |CIERRA #15; |FINSI;    || IVA Soportado
 |CIERRAT #24;

|FPRC;

||**************************************************************************
|PROCESO pase_auxiliar2;
 aIVA1 = (aIVA1 + (80 * " ")) % 180;
 alfa1 = aIVA2 % 0;
 nume1 = alfa1;
 nume1 = nume1 - 10;
 nume1 = (-1) * (100 + nume1);
 aIVA2 = aIVA2 % nume1;
 aIVA3 = aIVA1 + aIVA2;

 alfa1 = aIVA3 % 0;
 rlong = alfa1;

 |DDEFECTO #41;

 RegFact  = RegFact + 1;
 #41#0    = RegFact;

 alfa1 = aIVA3 % 0504;
 #41#1 = alfa1;           || Numero Factura

 afecha = aIVA3 % 0104;   || Fecha Contable MM/DD
 |HAZ fecha;
 #41#2 = afecha;           || Fecha dd.mm.aaaa

 #41#5 = aIVA3 % 1235;     || Nombre del Cliente/proveedor

 CampoDni = aIVA3 % 8015;  || NIF
 |HAZ LetraDni;
 #41#6 = CampoDni;         || NIF

 #41#7 = aIVA3 % 4730;     || Descripcion
 nume1 = 0;
 alfa5 = "";
 |PARA para1 = 1; |SI para1 [ 30; |HACIENDO para1 = para1 + 1;
       nume2 = (100 * para1) + 01;
       alfa4 = #41#7 % nume2;
       |SI alfa4 ! " "; |O nume1 = 1;
           nume1 = 1;
           alfa5 = alfa5 + alfa4;
       |FINSI;
 |SIGUE;
 #41#7 = alfa5;

 cm = &034;
 alfa7  = "";
 |PARA para1 = rlong; |SI para1 ] 1; |HACIENDO para1 = para1 - 1;
       nume2 = (100 * para1) + 01;
       alfa4 = aIVA3 % nume2;
       |SI alfa4 = cm; |SAL; |FINSI;
       alfa7 = alfa4 + alfa7;
 |SIGUE;

 #41#3 = alfa7 % 0208;
 conte = #41#3;        || Cuenta Contable
 |HAZ cuentas;
 #41#4 = digcu;        || Digito Contable

 alfa1 = alfa7 % 0;
 nume1 = alfa1;
 nume3 = 8;
 cm    = ",";
 alfa5 = "";
 |PARA para1 = 11; |SI para1 [ nume1; |HACIENDO para1 = para1 + 1;
       nume2 = (100 * para1) + 01;
       alfa4 = alfa7 % nume2;
       |SI alfa4 = cm;
           #41nume3 = alfa5;
           alfa5 = "";
           nume3 = nume3 + 1;
           |SI nume3 > 17; |SAL; |FINSI; || No es posible
       |SINO;
           alfa5 = alfa5 + alfa4;
       |FINSI;
 |SIGUE;

 |PINTA 2230, #41#1;
 |PINTA 2237, #41#2;

 |GRABA 020#41n;
|FPRC;

|| *****************************************************************
|PROCESO nueva;
 diario = #40#9;      || Diario
 fecha  = #40#8;      || Fecha
 |SI fecha < fec1; |O fecha > fec2; sw_fecha = 1; |FINSI;
 docume = #40#1;      || Documento
 asient = docume;     || Asiento
 |HAZ cabecera;

 enDiario = #40#9;
 |HAZ contador;
 |SI #26#1 < docume;
     enDiario = #40#9;
     enContador = docume;
     |HAZ GrabaContador;
 |FINSI;

 |PINTA 2230, #22#0;
 |PINTA 2233, #22#1;
 |PINTA 2244, #22#2;
|FPRC;

|PROCESO hacer;
 |SI sw = 0;
     |HAZ nueva;
     diaant = #40#9;
     fecant = #40#8;
     ordant = #40#1;
     sw = 1;
 |FINSI;
 |SI ordant ! #40#1; |O fecant ! #40#8; |O diaant ! #40#9;
     |HAZ nueva;
     diaant = #40#9;
     fecant = #40#8;
     ordant = #40#1;
 |FINSI;

 conte  = #40#3;
 digcu  = #40#4;
 signe  = #40#6;

 |DDEFECTO #23;
 #23#0 = diario;
 #23#1 = fecha;
 #23#2 = docume;
 linea = linea + 10;
 #23#3 = linea;
 #23#4 = conte;
 #23#5 = digcu;
 #23#6 = 1;
 #23#7 = #40#7;
 #23#8 = signe;
 #23#9 = #40#5;
 |HAZ adapta;
 |HAZ a_diario;
 |HAZ a_cuenta;

 |PINTA 2251, #23#3;

 |GRABA 020#23n;
 |HAZ gratra;
 |HAZ act_cabecera;

 |BORRA 020#40a;
|FPRC;

|DEFBUCLE theos1;
 #40#2;
 ;
 00, #0#19, 000001, 0001;
 99, #0#20, 999999, 9999;
 e;
 NULL, hacer;
|FIN;

|| -------------------------------------------
|PROCESO pase_asientos;
 |ABRE #20;     || Diarios
 |ABRE #22;     || Apuntes Cabs.
 |ABRE #23;     || Apuntes Lins
 |ABRE #24;     || Plan Contable
 sw = 0;
|| .................. Abre fichero Auxiliar de Cuentas
 |ABRE #27;
 |LEE 101#27u;
 |SI FSalida ! 0;
     |DDEFECTO #27;
     #27#0 = 1;
 |SINO;
     #27#0 = #27#0 + 1;
 |FINSI;
 |GRABA 101#27n;
 codcon = #27#0;
 |LIBERA #27;

 |BUCLE theos1;

 |CIERRA #20;
 |CIERRA #22;
 |CIERRA #23;
 |CIERRA #24;
 |CIERRA #26;
 |CIERRA #27;

|FPRC;

||**************************************************************************
|PROCESO pase_auxiliar1;
 alfa1 = field % 0101;
 |SI alfa1 < "0"; |O alfa1 > "9"; |ACABA; |FINSI;  || No es Apunte

 |DDEFECTO #40;
 RegAstos = RegAstos + 1;
 #40#0 = RegAstos;

 alfa7 = field % 0120;     || Primera Parte

 alfa1 = alfa7 % 1304;
 #40#1 = alfa1;            || Numero Asiento
 alfa1 = alfa7 % 1704;
 #40#2 = alfa1;            || Numero Apunte

 swdiario = 0;
 afecha = alfa7 % 0904;    || Fecha Contable MM/DD
 |HAZ fecha;
 #40#8 = afecha;           || Fecha dd.mm.aaaa

 #40#9 = #0#8;                             || Diario Pantalla
 |SI swdiario = 1; #40#9 = 0; |FINSI;      || Diario Apertura

 #40#3 = alfa7 % 0108;    || Cuenta Contable
 conte = #40#3;
 |HAZ cuentas;
 #40#4 = digcu;           || Digito Contable
|| -------------------------------------------------

 alfa2 = field % 0;
 nume4 = alfa2; nume4 = nume4 - 23;
 nume4 = (-1) * (100 + nume4);
 alfa7 = field % nume4;  || Coge el resto del campo

 #40#6 = alfa7 % 0101;   || Signo
 #40#7 = alfa7 % 1030;   || Descripcion

 anume = alfa7;     || Para Buscar Importe
 |HAZ importe;
 #40#5 = importe;

 |PINTA 2230, #40#1;
 |PINTA 2237, #40#2;
 |PINTA 2242, #40#8;

 |GRABA 020#40n;
|FPRC;

|| *****************************************************************
|PROCESO altacuenta;
 guarda_pos = Pos;
 Pos = -1;

 digcu = 0;
 alfa1 = #42#0; |QBF alfa1;
 alfa2 = alfa1 % 0;
 nume1 = alfa2;
 |SI nume1 = dig4;  digcu = 1;  |FINSI;
 |SI nume1 = dig5;  digcu = 2;  |FINSI;
 |SI nume1 = dig6;  digcu = 3;  |FINSI;
 |SI nume1 = dig7;  digcu = 4;  |FINSI;
 |SI nume1 = dig8;  digcu = 5;  |FINSI;
 |SI nume1 = dig9;  digcu = 6;  |FINSI;
 |SI nume1 = 0;     digcu = 0;  |FINSI;
 |SI digcu = 0;
     mensa = "    Cuenta Fuera de NIVEL: " + conte;
     |MENAV mensa;
 |FINSI;

 conte = (alfa1 + "            ") % 112;
 #24#0 = conte;
 #24#1 = digcu;
 |LEE 001#24=;
 |SI FSalida ! 0;
     |DDEFECTO #24;
     #24#0 = conte;
     #24#1 = digcu;
     #24#2 = #42#2;
     #24#3 = "N";
     |SI #24#1 = dig3;
         #24#3 = "S";
     |FINSI;
     |HAZ defect1;
     |GRABA 020#24n;
 |FINSI;

 |SI #24#3 = "N";  |ACABA;  |FINSI;     || Si no es del Ultimo Nivel Termina

 alfa5 = conte % 103;
 |SI alfa5 = "430"; |O alfa5 = "440";
     alfa5 = "C"; |HAZ cuentas2;
 |FINSI; || cliente
 |SI alfa5 = "400"; |O alfa5 = "410";
                    |O alfa5 = "514";
                    |O alfa5 = "515";
                    |O alfa5 = "523";
     alfa5 = "P"; |HAZ cuentas2;
 |FINSI;      || proveedor
|FPRC;

|PROCESO cuentas2;
 #12#23 = alfa5;
 #12#10 = conte;
 #12#11 = digcu;
 |LEE 000#12=;
 |SI FSalida ! 0;
     |DDEFECTO #12;
     #12#23 = alfa5;         || C/P
     #12#10 = conte;         || Cuenta Contable
     #12#11 = digcu;         || Digito Contable
     #12#0  = #42#0;         || Codigo
     #12#1  = #42#2;         || Nombre

     CampoDni = #42#6;
     |HAZ LetraDni;
     #12#9 = CampoDni;       || NIF
     |GRABA 020#12n;
  |FINSI;
  Pos = guarda_pos;
|FPRC;

|PROCESO pase_cta;
 alfa1 = field % 0101;
 |SI alfa1 < "0"; |O alfa1 > "9"; |ACABA; |FINSI;  || No es una Cuentas

 alfa4 = field % 0;
 nume4 = alfa4;
 |DDEFECTO #42;

 alfa1  = field % 0110; |QBT alfa1;
 alfa2  = alfa1 % 0;
 alfa3  = "";
 nume1  = alfa2;
 |PARA para1 = 1; |SI para1 [ nume1; |HACIENDO para1 = para1 + 1;
       nume2 = (100 * para1) + 01;
       alfa4 = alfa1 % nume2;
       |SI alfa4 = ":"; |SAL; |FINSI;
       alfa3 = alfa3 + alfa4;
 |SIGUE;
 #42#0  = alfa3;    || Cuenta Contable

 nume5 = 0;
 alfa1  = field % 1235; |QBF alfa1;
 alfa2  = alfa1 % 0;
 alfa3  = "";
 nume1  = alfa2;
 |PARA para1 = 1; |SI para1 [ nume1; |HACIENDO para1 = para1 + 1;
       nume2 = (100 * para1) + 01;
       alfa4 = alfa1 % nume2;
       cm    = &034;
       |SI alfa4 = cm;
           nume5 = nume5 + 1;
           |SI nume5 = 1; |O nume5 = 3;
               alfa3 = alfa3 + alfa4;
           |FINSI;
       |SINO;
           alfa3 = alfa3 + alfa4;
       |FINSI;
 |SIGUE;
|| #42#2  = field % 1235;            || alfa3;            || Titulo Cuenta
 #42#2  = alfa3;            || alfa3;            || Titulo Cuenta

 #42#4  = field %  4701;    || Pase Directo S/N

 |SI nume4 > 50;
     #42#6  = field %  5015;    || Direccion/DNI
 |FINSI;

 |PINTA 2229, #42#0;
 |HAZ altacuenta;
|FPRC;

|| ********************************************************************
|PROCESO tipo3;
|HAZ Formateo;

 |D_CUADRO 2111, 2356;
 |PINTA 2212, "                                            ";

|ET pcuentas;
 |SI #0#0 ! "S"; |VETE pasiento; |FINSI;

 || ... Cuentas
  fabre = #0#4; |QBT fabre;
  fabre = "rt  " + fabre;
  fabre = fabre + "z";
  |FABRE fabre;
  fcanl = FSalida;
  |SI fcanl < 0;
      alfa1 = "    [" + (fabre % 5) + "] inaccesible o inexistente ...";
      |MENAV alfa1;
  |SINO;
      |ATRI I; |PINTA 2212, " Pasando Cuenta:                            "; |ATRI 0;
      |ABRE #12;
      |ABRE #24;
      |PARA; |SI; |HACIENDO;
             field = fcanl + " 250";
             |FLEE field;
             |SI FSalida < 0; |SAL; |FINSI;
             |HAZ pase_cta;
      |SIGUE;
      |CIERRA #12;
      |CIERRA #24;
  |FINSI;
  |FCIERRA fcanl;
  Pos = -1;

|| *******************************************************************
|ET pasiento;
  sw_fecha = 0;
  |SI #0#1 ! "S"; |VETE pventas; |FINSI;

  || ... Asientos
  fabre = #0#5; |QBT fabre;
  fabre = "rt  " + fabre;
  fabre = fabre + "z";
  |FABRE fabre;
  fcanl = FSalida;
  |SI fcanl < 0;
      alfa1 = "    [" + (fabre % 5) + "] inaccesible o inexistente ...";
      |MENAV alfa1;
  |SINO;
      |ATRI I; |PINTA 2212, " Leyendo Asiento:                           "; |ATRI 0;
      RegAstos = 0;
      |HAZ nom_usu;
      alfa1 = "a" + nom_tmp;
      |NOME_DAT #40 alfa1;
      |ABRE #40;
      |CIERRA #40;
      |DELINDEX #40;

      |ABRE #40;
      |PARA; |SI; |HACIENDO;
             field = fcanl + " 250";
             |FLEE field;
             |SI FSalida < 0; |SAL; |FINSI;
             |HAZ pase_auxiliar1;
      |SIGUE;
      |CIERRA #40;
  |FINSI;
  |FCIERRA fcanl;
  Pos = -1;

  |SI RegAstos ! 0;
      |ATRI I; |PINTA 2212, " Pasando Asiento:                           "; |ATRI 0;
      |HAZ pase_asientos;
  |FINSI;
  |SI sw_fecha = 1;
     |MENAV "    Hay apuntes con fecha fuera de periodo ...";
  |FINSI;
  |DELINDEX #40;

|| *******************************************************************
|ET pventas;
  sw_fecha = 0;
  |SI #0#2 ! "S"; |VETE pcompras; |FINSI;

  || ... Ventas
  fabre = #0#6; |QBT fabre;
  fabre = "rt  " + fabre;
  ||fabre = fabre + "z";
  |FABRE fabre;
  fcanl = FSalida;
  |SI fcanl < 0;
      alfa1 = "    [" + (fabre % 5) + "] inaccesible o inexistente ...";
      |MENAV alfa1;
  |SINO;
      |ATRI I; |PINTA 2212, " Leyendo Ventas:                            "; |ATRI 0;
      RegFact  = 0;
      tipof    = "V";
      |HAZ nom_usu;
      alfa2 = "b" + nom_tmp;
      |NOME_DAT #41 alfa2;
      |ABRE #41;
      |CIERRA #41;
      |DELINDEX #41;

      |ABRE #41;
      |PARA; |SI; |HACIENDO;
             field = fcanl + " 250";
             |FLEE field;
             |SI FSalida < 0; |SAL; |FINSI;
             swdos = swdos + 1;
             |SI swdos = 1;
                 aIVA1 = field;
             |SINO;
                 aIVA2 = field;
                 |HAZ pase_auxiliar2;
                 swdos = 0;
             |FINSI;
      |SIGUE;
      |CIERRA #41;
  |FINSI;
  |FCIERRA fcanl;
  Pos = -1;

  |SI RegFact ! 0;
      |ATRI I; |PINTA 2212, " Pasando Ventas:                            "; |ATRI 0;
      |HAZ pase_facturas;
  |FINSI;
  |DELINDEX #41;
|| *******************************************************************

|ET pcompras;

  sw_fecha = 0;
  |SI #0#3 ! "S"; |ACABA; |FINSI;

  || ... Compras
  fabre = #0#7; |QBT fabre;
  fabre = "rt  " + fabre;
  ||fabre = fabre + "z";
  |FABRE fabre;
  fcanl = FSalida;
  |SI fcanl < 0;
      alfa1 = "    [" + (fabre % 5) + "] inaccesible o inexistente ...";
      |MENAV alfa1;
  |SINO;
      |ATRI I; |PINTA 2212, " Leyendo Compras:                           "; |ATRI 0;
      RegFact  = 0;
      tipof    = "C";
      |HAZ nom_usu;
      alfa2 = "c" + nom_tmp;
      |NOME_DAT #41 alfa2;
      |ABRE #41;
      |CIERRA #41;
      |DELINDEX #41;

      |ABRE #41;
      swdos = 0;
      |PARA; |SI; |HACIENDO;
             field = fcanl + " 250";
             |FLEE field;
             |SI FSalida < 0; |SAL; |FINSI;
             swdos = swdos + 1;
             |SI swdos = 1;
                 aIVA1 = field;
             |SINO;
                 aIVA2 = field;
                 |HAZ pase_auxiliar2;
                 swdos = 0;
             |FINSI;
      |SIGUE;
      |CIERRA #41;
  |FINSI;
  |FCIERRA fcanl;
  Pos = -1;


  |SI RegFact ! 0;
      |ATRI I; |PINTA 2212, " Pasando Compras:                           "; |ATRI 0;
      |HAZ pase_facturas;
  |FINSI;

  |DELINDEX #41;
|FPRC;

|| ************* Rutinas varias
|PROCESO cuentas;
 nocta = 0;
 |QBF conte;
 |SI conte = ""; nocta = 1; |ACABA; |FINSI;
 digcu = 0;
 alfa2 = conte % 0;
 nume1 = alfa2;
 |SI nume1 = dig4;  digcu = 1;  |FINSI;
 |SI nume1 = dig5;  digcu = 2;  |FINSI;
 |SI nume1 = dig6;  digcu = 3;  |FINSI;
 |SI nume1 = dig7;  digcu = 4;  |FINSI;
 |SI nume1 = dig8;  digcu = 5;  |FINSI;
 |SI nume1 = dig9;  digcu = 6;  |FINSI;
 |SI nume1 = 0;     digcu = 0;  |FINSI;
 |SI digcu = 0; nocta = 1; |FINSI;
 conte = (conte + "            ") % 112;
|FPRC;

|PROCESO fecha;
 alfa1 = afecha % 302;                          || dia
 alfa2 = afecha % 102;                          || mes
 alfa3 = #30#26; alfa3 = ("00" + alfa3) % -102; || a¤o
 swdiario = 0;
 |SI alfa2 = "00";
     swdiario = 1;
     alfa2 = #30#11; || Mes inicial de la Empresa
     alfa2 = ("00" + alfa2) % -102; || mes
 |FINSI;
 nume1 = alfa3;
 |SI nume1 > 80;  alfa3="19"+alfa3;  |SINO;  alfa1="20"+alfa3;  |FINSI;
 alfa4 = alfa1 + "." + alfa2 + "." + alfa3;
 afecha = alfa4;
|FPRC;

|PROCESO importe;
 importe = 0;
 |QBT anume;
 alfa1 = anume % 0;
 nume2 = alfa1;
 alfa5 = "";
 |PARA nume1 = nume2; |SI nume1 ] 1; |HACIENDO nume1 = nume1 - 1;
       nume3 = (nume1 * 100) + 1;
       alfa3 = anume % nume3;
       |SI alfa3 = ",";
           |SAL;
       |FINSI;
       |SI alfa3 ] "0"; |Y alfa3 [ "9";
           alfa5 = alfa3 + alfa5;
       |FINSI;
 |SIGUE;
 importe = alfa5;
|FPRC;


|PROCESO LetraDni;
     eaVarDni   = CampoDni;
     enCalcCif  = 3;
     |HAZ CalculoDNI;
     CampoDni = eaVarDni;
     CampoDni = ("000000000" + CampoDni) % -109;
|FPRC;

|PROCESO gratra; || trabajo para actualizar cuentas del Plan Contable
 modcon = 1;
 #27#0 = codcon;
 #27#1 = #23#4;
 #27#2 = #23#5;
 #27#3 = #23#1;
 #27#4 = #23#8;
 #27#6 = #23#0;
 |LEE 101#27=;
 |SI FSalida ! 0;
     |DDEFECTO #27;
     #27#0 = codcon;
     #27#1 = #23#4;
     #27#2 = #23#5;
     #27#3 = #23#1;
     #27#4 = #23#8;
     #27#6 = #23#0;
     |GRABA 101#27b;
 |FINSI;
 #27#5 = #27#5 + #23#9;
 |GRABA 101#27a;
 |LIBERA #27;
|FPRC;

|PROCESO cabecera; || lee la cabecera.
|DDEFECTO #22;
  #22#0 = diario;
  #22#1 = fecha;
  #22#2 = docume;
  #22#3 = asient;
  linea = -9;
  |GRABA 020#22n;
  |SI FSalida ! 0;
      #23#0 = #22#0;
      #23#1 = #22#1;
      #23#2 = #22#2;
      #23#3 = 9999;
      |LEE 040#23];
      |SI FSalida = 0;
          |LEE 040#23a;
          |SI #23#0 = #22#0; |Y #23#1 = #22#1; |Y #23#2 = #22#2;
              linea = #23#3;
          |FINSI;
      |SINO;
          |LEE 040#23u;
          |SI FSalida = 0; |Y  #23#0 = #22#0; |Y #23#1 = #22#1; |Y #23#2 = #22#2;
              linea = #23#3;
          |FINSI;
      |FINSI;
  |FINSI;
|FPRC;

|PROCESO act_cabecera;
  #22#0 = diario;
  #22#1 = fecha;
  #22#2 = docume;
  |LEE 101#22=;
  |SI #23#8 = "D";
       #22#4 = #22#4 + #23#9;
       |SI #22#09 = 0; #22#08 = #23#4; #22#09 = #23#5; |FINSI;  || contrp.HABER
  |SINO;
       #22#5 = #22#5 + #23#9;
       |SI #22#11 = 0; #22#10 = #23#4; #22#11 = #23#5; |FINSI;  || contrp.DEBE
  |FINSI;
  #22#6 = #22#4 - #22#5;        || saldo
  |GRABA 020#22a;
  |LIBERA #22;
|FPRC;

|PROCESO adapta;
 |SI #23#8 = "D";
     #23#16 = #23#4;
     #23#17 = #23#5;
     #23#10 = "";
     #23#11 = 0;
 |SINO;
     #23#16 = "";
     #23#17 = 0;
     #23#10 = #23#4;
     #23#11 = #23#5;
 |FINSI;
|FPRC;

|PROCESO a_diario;
 |ABRE #20;
 #20#0 = #23#0;
 |LEE 101#20=;
 |SI FSalida ! 0;              || actualiza el diario
     |DDEFECTO #20;
     #20#0 = #23#0;
     #20#1 = "<DIARIO SIN DEFINIR>";
    |GRABA 020#20b;
 |FINSI;
 |SI #23#8 = "D";  #20#2 = #20#2 + #23#9;  |FINSI;
 |SI #23#8 = "H";  #20#3 = #20#3 + #23#9;  |FINSI;
 |GRABA 020#20a;
 |LIBERA #20;
 |CIERRA #20;
|FPRC;

|PROCESO periodo;
 alfa1 = #13#3;   alfa1 = alfa1 % -104;      || saca a¤o
 alfa2 = "31.03." + alfa1;  trim1 = alfa2;  || primer trimestre
 alfa2 = "30.06." + alfa1;  trim2 = alfa2;  || segundo trimestre
 alfa2 = "30.09." + alfa1;  trim3 = alfa2;  || tercer trimestre
 |SI #13#3 [ trim1;                    #13#44 = 1;  |FINSI;
 |SI #13#3 > trim1;|Y #13#3 [ trim2;   #13#44 = 2;  |FINSI;
 |SI #13#3 > trim2;|Y #13#3 [ trim3;   #13#44 = 3;  |FINSI;
 |SI #13#3 > trim3;                    #13#44 = 4;  |FINSI;
|FPRC;

|PROCESO periodo_s;
 alfa1 = #15#3;   alfa1 = alfa1 % -104;     || saca a¤o
 alfa2 = "31.03." + alfa1;  trim1 = alfa2;  || primer trimestre
 alfa2 = "30.06." + alfa1;  trim2 = alfa2;  || segundo trimestre
 alfa2 = "30.09." + alfa1;  trim3 = alfa2;  || tercer trimestre
 |SI #15#3 [ trim1;                    #15#44 = 1;  |FINSI;
 |SI #15#3 > trim1;|Y #13#3 [ trim2;   #15#44 = 2;  |FINSI;
 |SI #15#3 > trim2;|Y #13#3 [ trim3;   #15#44 = 3;  |FINSI;
 |SI #15#3 > trim3;                    #15#44 = 4;  |FINSI;
|FPRC;

|| ****** ACTUALIZACION DE CUENTAS
|PROCESO a_cuenta;
  #24#0 = #23#4;
  #24#1 = #23#5;
  |LEE 101#24=;
  |SI FSalida ! 0;
      |LIBERA #24;
      |DDEFECTO #24;
      #24#0 = #23#4;
      #24#1 = #23#5;
      #24#2 = titulo;
      #24#3 = "S";
      |HAZ defect1;
      |GRABA 020#24b;
  |FINSI;
  |SI #23#0 = 0;
      |SI #23#8 = "D";
          #24#9  = #24#9 + #23#9;
          #24#51 = #24#51 + #23#9;
      |SINO;
          #24#10 = #24#10 + #23#9;
          #24#52 = #24#52 + #23#9;
      |FINSI;
      #24#11 = #24#9 - #24#10;
      #24#53 = #24#51 - #24#52;
  |SINO;
      |SI #23#0 = 99;
          |SI #23#8 = "D";
              #24#48 = #24#48 + #23#9;
              #24#51 = #24#51 + #23#9;
          |SINO;
              #24#49 = #24#49 + #23#9;
              #24#52 = #24#52 + #23#9;
          |FINSI;
          #24#50 = #24#48 - #24#49;
          #24#53 = #24#51 - #24#52;
       |SINO;
          |HAZ actualiz2;
       |FINSI;
  |FINSI;
  |SI #24#58 < #23#1;
      #24#58 = #23#1;
  |FINSI;
  |GRABA 020#24a;
  |LIBERA #24;
|FPRC;

|PROCESO actualiz2;
    ini = dig1;      || numero de niveles
    fecalf = #23#1;
    mes = fecalf % 402;
    mesfec = mes;
    |SI ini = 1;
        mesac = mesfec;
    |SINO;
        mesac = (mesfec - ini) + 1;
        |SI mesac [ 0;
            mesac = (13 - ini) + mesfec;
        |FINSI;
    |FINSI;
    a = (mesac * 3) + 9;
    b = a + 1;
    c = a + 2;
   |SI #23#8 = "D";
        #24a = #24a + #23#9;
        #24#51 = #24#51 + #23#9;
   |SINO;
        #24b = #24b + #23#9;
        #24#52 = #24#52 + #23#9;
   |FINSI;
   #24c = #24a - #24b;
   #24#53 = #24#51 - #24#52;
|FPRC;
||**************************************************************************

|PROCESO Grabalos;
    alfa1 = #43#1 - ":";
|SI alfa1 ! #43#1;
    |SI Grabando ! "";
            field = fcanl + " " + Grabando;
        |FGRABA field;
            Grabando = "";
    |FINSI;
        Grabando = #43#1;
    |QBF Grabando;
        Grabando = Grabando - &13;
        Grabando = Grabando - &10;
|SINO;
        Grabando = Grabando + (#43#1 % Comillas);
    |QBF Grabando;
        Grabando = Grabando - &13;
        Grabando = Grabando - &10;
        field = fcanl + " " + Grabando;
    |FGRABA field;
        Grabando = "";
|FINSI;
|FPRC;

|PROCESO BuscaComillas;
    Comillas = 0;
    alfa2 = &34;
|PARA para1 = 1; |SI para1 [ 90; |HACIENDO para1 = para1 + 1;
        nume1 = (para1 * 100) + 1;
        alfa1 = #43#1 % nume1;
    |SI alfa1 = alfa2;
            Comillas = para1;
        |SAL;
    |FINSI;
|SIGUE;
|FPRC;

|PROCESO tempo4;
|SI Grabando ! "";
        field = fcanl + " " + Grabando;
    |FGRABA field;
        Grabando = "";
|FINSI;
|FPRC;

|PROCESO tempo1;
|SI sw_rup = 0;|Y Comillas = 0;
        sw_rup = 1;
    |HAZ BuscaComillas;
    |SI Comillas = 0;
        |MENAV "    Formato desconocido ...";
        |ERROR10;
    |FINSI;
|FINSI;
|HAZ Grabalos;
|FPRC;

|DEFBUCLE tempo;
 #43#1;
 ;
      1;
 999999;
 ;
 NULL, tempo1, NULL, NULL, tempo4;
|FIN;

|PROCESO Formateo1;
|DELINDEX #43;
|DDEFECTO #43;
|ABRE #43;
    fabre = FormaFich; |QBT fabre;
    fabre = "rt  " + fabre;
|FABRE fabre;
    fcanl = FSalida;
|SI fcanl < 0;
        alfa1 = "    [" + (fabre % 5) + "] inaccesible o inexistente ...";
    |MENAV alfa1;
|SINO;
        alfa1 = "    Formateando fichero [" + (fabre % 5) + "] ...";
    |MENSA alfa1;
    |PARA; |SI; |HACIENDO;
            field = fcanl + " 250";
        |FLEE field;
        |SI FSalida < 0; |SAL; |FINSI;
        |QBF field;
        |SI field ! "";
                #43#1 = field;
            |GRABA 020#43n;
                #43#0 = #43#0 + 1;
        |FINSI;
    |SIGUE;
|FINSI;
|FCIERRA fcanl;
    Pos = -1;
|CIERRA #43;

    fabre = FormaFich; |QBT fabre;
    fabre = fabre + "z";
    fabre = "wt  " + fabre;
|FABRE fabre;
    fcanl = FSalida;
|SI fcanl < 0;
        alfa1 = "    [" + (fabre % 5) + "] inaccesible para escritura ...";
    |MENAV alfa1;
|SINO;
        Grabando = "";   sw_rup = 0; |BUCLE tempo;
|FINSI;
|FCIERRA fcanl;
    Pos = -1;
|DELINDEX #43;
|BLIN 4;
|FPRC;

|PROCESO Formateo;
|SI #0#0 = "S"; Comillas = 0;  FormaFich = #0#4;  |HAZ Formateo1;     |FINSI;
|SI #0#1 = "S"; Comillas = 13; FormaFich = #0#5;  |HAZ Formateo1;     |FINSI;
||SI #0#2 = "S"; Comillas = 11; FormaFich = #0#6;  |HAZ Formateo1;     |FINSI;
||SI #0#3 = "S"; Comillas = 11; FormaFich = #0#7;  |HAZ Formateo1;     |FINSI;
|FPRC;
