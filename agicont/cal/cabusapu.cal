|FICHEROS;
     cabusapu #0;
     cabusap1 #1;
     cacuenta #2;
     camaasic #3;
     camaasil #4;
|FIN;

|VARIABLES;
     &tipo     = "";
     &pag      = "";
     alfa      = "";
     alf1      = "";
     alf2      = "";
     cta       = 0;
     long      = "";
     sw        = 0;
     fichero   = "";
     hay       = 0;
     sw_des    = 0;
     i         = 0;
     ds        = "";
     hs        = "";
 {-1}menu      = "";
     menu1     = "2400",
     menu2     = "1";
     menu3     = "Tipo Informe: ";
     menu4     = "PI";
     menu5     = " Pantalla ";
     menu6     = " Impresora ";
 {-1}amenu      = "";
     amenu1     = "2400",
     amenu2     = "1";
     amenu3     = "Salto Pagina por Cuenta: ";
     amenu4     = "SN";
     amenu5     = " SI ";
     amenu6     = " NO ";
|FIN;

|INCLUYE acceso_i;
|INCLUYE puntos_i;

|PROCESO mayus; |TIPO 0;
     #0Campo = #0Campo > "";
     |PINTA #0Campo;
|FPRC;

|PROCESO defectos; |TIPO 7;
    #0#2 = fec1; |PINTA #0#2;
    #0#3 = fec2; |PINTA #0#3;
|FPRC;

|PROCESO cuenta; |TIPO 6;
     p_alfa1 = #0Campo;
     salidas = FSalida;
     |HAZ punto;
     |SI p_error ! 0;  |ERROR;  |ACABA;  |FINSI;
     |SI #0Campo ! "            ";
         cta  = #0Campo;
         long = cta % 0;
         sw = 0;
         |SI long = #30#14; sw = 1; |FINSI;
         |SI long = #30#15; sw = 1; |FINSI;
         |SI long = #30#16; sw = 1; |FINSI;
         |SI long = #30#17; sw = 1; |FINSI;
         |SI long = #30#18; sw = 1; |FINSI;
         |SI long = #30#19; sw = 1; |FINSI;
         |SI sw = 0;
             |MENAV "    Longitud de Cuenta fuera de Nivel ...";
             |ERROR;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO tipo7; |TIPO 7;
     alfa = #0#4 + #0#5; |QBF alfa;
     |SI alfa = "";
          |ABRE #2;
          |LEE 000#2p;
          #0#4 = #2#0; |PINTA #0#4;
          |LEE 000#2u;
          #0#5 = #2#0; |PINTA #0#5;
          |CIERRA #2;
     |FINSI;
|FPRC;
-------------------------------------------------------------------------
|PROCESO camaasil;
     |SI sw_des = 1;
          |PARA i = 6; |SI i [ 11; |HACIENDO i = i + 1;
               alf1 = #4#7; |QBF alf1; alf1 = alf1 > "";
               alf2 = #0i;  |QBF alf2; alf2 = alf2 > "";
               alfa = alf1 - alf2;
               |SI alf1 ! alfa; |SAL; |FINSI;
          |SIGUE;
          |SI i > 11; |ACABA; |FINSI;
     |FINSI;
     |PARA i = 0; |SI i [ 23; |HACIENDO i = i + 1;
          #1i = #4i;
     |SIGUE;
     |GRABA 021#1n;
     hay = hay + 1;
|FPRC;

|DEFBUCLE camaasil;
     #4#3;
     9,6,8; ||Cta,Dig,Fec,Doc,Impo,Conce,Signo
     #0#4, 0, #0#2, 000001, #0#0, #0#12, ds;
     #0#5, 9, #0#3, 999999, #0#1, #0#13, hs;
     ;
     NULL, camaasil;
|FIN;

|PROCESO tipo3; |TIPO 3;
     |SI #0#14 = "T";
          ds = " "; hs = "z";
     |SINO;
          ds = #0#14; hs = #0#14;
     |FINSI;

     alfa = #0#6 + #0#7 + #0#8 + #0#9 + #0#10 + #0#11;
     |QBF alfa;
     |SI alfa ! ""; sw_des = 1; |FINSI;

     fichero = Usuario; |QBF fichero;
     |NOME_DAT #1 fichero;
     |DELINDEX #1;

     |ABRE #1;
     |BUCLE camaasil;
     |CIERRA #1;
     |SI hay = 0;
          |MENAV "0000Seleccion Vacia...";
     |SINO;
          |AVISO; |MENU menu;
          tipo = FSalida;
          |SI tipo = 2;
               |AVISO; |MENU amenu;
               pag = FSalida;
          |FINSI;
          alfa = "cabusap1" + &59 + fichero;
          |SUB_EJECUTA alfa;   ||**** Muestra la seleccion
     |FINSI;

     |DELINDEX #1;
|FPRC;
