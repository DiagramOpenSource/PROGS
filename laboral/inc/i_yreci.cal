|FICHEROS;
    labempre #01;   || empresas
    labtraba #02;   || trabajadores
    nomconst #03;   || constantes
    nomcalca #04;   || calculo cabeceras
    nomcalli #05;   || calculo lineas
    labcentr #06;   || centros de trabajo
    nomtrnom #10;   || def nomina
||    eosm0027 #27;   || claves
||    eosm0032 #32;   || acum.lins.IRPF
    nomcenes #35;   || centros 18/93
    nomcontr #36;   || control
    nomdesam #100;  || descrip. ampliadas
    nomtraca #101;  || trabajadores cabs.
    nomconca #102;  || convenios cabs.
    nomvarca #103;  || variaciones empresa mes
|FIN;

|VARIABLES;
    afili = "";
    alfa1 = "";
    alfa2 = "";
    alfa3 = "";
    anyo = 0;
    anyrec = 0;
    anytra = 0;
    apordfp = 0;
    apord = 0;
    aporfp = 0;
    aporhe1 = 0;
    aporhe2 = 0;
    arteris = "";
    bisiesto = 0;
    camdesc = 0;
    campo = 0;
    camptas = 0;
    coment = 0;
    concep = 0;
    dcal_101 = "";
    deducir = 0;
    desacc = 0;
    descri = "";
    desdia = 0;
    desenf = 0;
    diaalta = 0;
    diabaja = 0;
    diarec = 0;
    diatra = 0;
    domemp = "";
    duni_101 = 0;
    dval_101 = 0;
    duni_102 = 0;
    fecsys = @;
    fectra = "";
    hasacc = 0;
    hasdia = 0;
    hasenf = 0;
    import = 0;
    incluye = 0;
    lineas = 0;
    mesalf = "";
    mesalta = 0;
    mesbaja = 0;
    mesliq = "";
    mesnum = 0;
    mesrec = "";
    mestra = 0;
    nume1 = 0;
    nume2 = 0;
    nume3 = 0;
    nume4 = 0;
    numer = 0;
    nro_copias = 0;
    p_mes = "";
    pantalla = 0;
    pobemp = "";
    seleccio = 0;
    situac = 0;
    sw_comen = 0;
    swbaja = 0;
    swtacc = 0;
    swtenf = 0;
    tantici = 0;
    taporta = 0;
    tbcotiz = 0;
    tcotiz = 0;
    topemes = 0;
    tot_109 = 0;
    tot_110 = 0;
    tot_111 = 0;
    tot_112 = 0;
    tot_113 = 0;
    tot_114 = 0;
    tot_115 = 0;
    unidad = 0;
    valor = 0;
    deci_h = 0;
    sw_control = 0;
    CodigoCons = 0;
    cta_co = "";
    &enCanti = 0;
    &eaMoneda = "";
    &aMonedaRec = "";
    para1 = 0;
    para2 = 0;
    duni_hest = 0;
    duni_hnoest = 0;
|FIN;

||****************************************************************************
||***** calculos desde el mantenimiento **************************************
||****************************************************************************

|PROCESO defecto; |TIPO 7; ||calcula el periodo por defecto segun fecha sistema
    fecsys = ~;
    alfa1 = fecsys;
    alfa1 = alfa1 % -102;
    #0Campo = alfa1;
|PINTA #0Campo;
|FPRC;

|PROCESO pintames1; |TIPO 0;
|SI #0#4 < 80;
        anyo = #0#4 + 2000;
|SINO;
        anyo = #0#4 + 1900;
|FINSI;

    nume1 = anyo $ 4;
|SI nume1 = 0;
        bisiesto = 1;
|SINO;
        bisiesto = 0;
|FINSI;

|SI #0#5 =  1;  p_mes = "ENERO     ";  topemes = 31;  |FINSI;
|SI #0#5 =  2;  p_mes = "FEBRERO   ";  topemes = 28 + bisiesto;  |FINSI;
|SI #0#5 =  3;  p_mes = "MARZO     ";  topemes = 31;  |FINSI;
|SI #0#5 =  4;  p_mes = "ABRIL     ";  topemes = 30;  |FINSI;
|SI #0#5 =  5;  p_mes = "MAYO      ";  topemes = 31;  |FINSI;
|SI #0#5 =  6;  p_mes = "JUNIO     ";  topemes = 30;  |FINSI;
|SI #0#5 =  7;  p_mes = "JULIO     ";  topemes = 31;  |FINSI;
|SI #0#5 =  8;  p_mes = "AGOSTO    ";  topemes = 31;  |FINSI;
|SI #0#5 =  9;  p_mes = "SEPTIEMBRE";  topemes = 30;  |FINSI;
|SI #0#5 = 10;  p_mes = "OCTUBRE   ";  topemes = 31;  |FINSI;
|SI #0#5 = 11;  p_mes = "NOVIEMBRE ";  topemes = 30;  |FINSI;
|SI #0#5 = 12;  p_mes = "DICIEMBRE ";  topemes = 31;  |FINSI;
|FPRC;

|PROCESO mirames; |TIPO 7; ||calcula el mes por defecto segun fecha sistema
    fecsys = ~;
    alfa1 = fecsys;
    alfa1 = alfa1 % 402;
    #0Campo = alfa1;
|PINTA #0Campo;
|HAZ pintames;
|FPRC;

|PROCESO finalmes; |TIPO 7;
    alfa1 = anyo;                                               || anyo
    alfa2 = topemes;                                            || dia
    alfa3 = #0#5;  alfa3 = "00" + alfa3;  alfa3 = alfa3 % -102; || mes

    alfa1 = alfa2 + "." + alfa3 + "." + alfa1;                  || fecha entera
    #0Campo = alfa1;
|PINTA #0Campo;
|FPRC;

||****************************************************************************
||***** calculos de lectura e impresion **************************************
||****************************************************************************

|PROCESO control;
|DDEFECTO #36;
|ABRE #36;
|LEE 000#36p;
|SI FSalida ! 0;
    |MENAV "    No existe control, usando defectos ...";
|FINSI;
|CIERRA #36;
|FPRC;

|PROCESO proceso;
|SI sw_control = 0; |HAZ control;  sw_control = 1;     |FINSI;

    lineas = 0;     || para saber si los conceptos leidos son buenos
    swbaja = 0;
    coment = 0;
|DDEFECTO #10;
|HAZ comple_0;   || lee empresa, trabajador y constante
|SI runnom = "nomyreci";
    |SI #0#10 = 2;|Y #1#39 ! "2";
        |ACABA;      || finiquito no separado!!! (recibo)
    |FINSI;
|FINSI;
|SI runnom = "nomycalf";
    |SI #0#10 = 2;|Y #1#39 ! "3";
        |ACABA;      || finiquito no separado!!! (carta)
    |FINSI;
|FINSI;
|SI runnom="nomyreci"; |O runnom="nomycalf";
     |SI #0#66 < #2#77;|O #0#65 > #2#77;     || seleccion centros
          |ACABA;
     |FINSI;
|FINSI;
|HAZ comple_1;   || construye literales datos empresa
|HAZ comple_2;   || construye periodo de liquidacion nomina
|HAZ comple_3;   || construye periodo de liquidacion de enferm. y accte.
|HAZ comple_3_1; || pasa periodos de IT tal cual estan en el nomcalca
|HAZ comple_4;   || halla totales varios
|HAZ comple_5;   || construye fecha firma
|HAZ comple_6;   || construye el comentario
|HAZ cargas_0;           || carga campos cabecera calculos
|BUCLELIN 0cargas_1#4;   || carga campos lineas calculos
|SI lineas = 1;
        seleccio = 1;    || para saber si se ha leido algo
    |HAZ cargas_2;       || carga campos totales
    nro_copias = #36#34; || carga numero de copias de recibo (ctrl. aplicacion)
    |PARA nume1 = 1; |SI nume1 [ nro_copias; |HACIENDO nume1 = nume1 + 1;
        |HAZ imprime;
    |SIGUE;
|FINSI;
|FPRC;

|PROCESO cta_cotizacion;
|SI #2#45 = "087";|O #2#45 = "097";|O #2#45 = "085";
                  |O #2#45 = "064";|O #2#45 = "065";
    |DDEFECTO #35;
        #35#0 = #2#0;
        #35#13= #2#77;
    |SI #2#45 = "097";|O #2#45 = "085";
            #35#1 = "87";
    |SINO;
        |SI #2#45 = "065";
                #35#1 = "64";
        |SINO;
                #35#1 = #2#45 % 2;
        |FINSI;
    |FINSI;
    |LEE 000#35=;
        cta_co = #35#10;      || cta.cto. del centro de trabajo especial
|SINO;
        cta_co = #1#13;       || cta.cot. de la empresa
    |DDEFECTO #6;
        #6#0 = #2#0;
        #6#1 = #2#77;
    |LEE 000#6=;
    |SI #1#45 = "S";
            cta_co = #6#10;        || cta.cot. centro de trabajo
    |FINSI;
|FINSI;
|FPRC;

|PROCESO ordena;
|DDEFECTO #1;
    #1#0 = #4#0;
|LEE 000#1=;

|DDEFECTO #2;
    #2#0 = #4#0;
    #2#1 = #4#1;
|LEE 000#2=;

|DDEFECTO #102;
    #102#0 = #2#87;
|LEE 000#102=;

|HAZ cta_cotizacion;
    #0#64 = cta_co;
|FPRC;

||****************************************************************************
||***** calculos de carga del 'def' nomina ***********************************
||****************************************************************************

|PROCESO cargas_0;      || CARGA LOS CAMPOS LITERALES Y LOS FIJOS (no todos !!!)
    #10#000 = #1#10;          || cif empresa
    #10#001 = #1#00;          || codigo empresa
    #10#002 = #2#01;          || codigo trabajador
    #10#003 = #1#02;          || nombre empresa
    #10#004 = domemp;         || direccion empresa (*)
    #10#005 = pobemp;         || poblacion empresa (*)
    #10#006 = #2#02;          || nombre trabajador
    #10#007 = #2#18;          || categoria profesional
    #10#008 = #2#238;         || puesto de trabajo
    #10#009 = afili;          || s.s. empresa (*)
    #10#010 = #2#38;          || fecha de impresion
    #10#011 = #2#31;          || nro. de matricula
    #10#012 = #2#07;          || d.n.i. trabajador
    #10#013 = #2#75;          || s.s. trabajador
    #10#172 = #2#77;          || centro de trabajo
    #10#014 = desdia;         || desde el dia
    #10#015 = mesliq;         || desde el mes
    #10#016 = hasdia;         || hasta el dia
    #10#017 = mesliq;         || hasta el mes
    #10#018 = #0#04;          || a¤o confeccion
    |SI #10#18 > 80;  #10#18 = #10#18 + 1900;  |SINO;  #10#18 = #10#18 + 2000;  |FINSI;
|SI #2#42 ! "A";|Y #2#42 ! "B";|Y #2#42 ! "E";
        #10#019 = #4#08;          || total dias liquidacion
|SINO;
        #10#019 = #4#11;          || total jornadas (AGRARIOS*)
|FINSI;
    ||#10#101 = #4#55;          || proteccion a la familia
    ||#10#102 = #4#56;          || ayuda a la subnormalidad
    #10#103 = #4#57;          || ptas. enfermedad
    #10#104 = #4#58;          || ptas. accidente
    #10#105 = desenf;         || del dia enfermedad
    #10#106 = hasenf;         || al dia enfermedad
    #10#107 = desacc;         || del dia accidente
    #10#108 = hasacc;         || al dia accidente
    #10#119 = tcotiz;         || base cotizacion
    #10#120 = #4#38;          || base prorratas
    #10#121 = tbcotiz;        || total bases de cotizacion
    #10#122 = #2#33;          || grupo de cotizacion
    #10#123 = #4#39;          || base contingencias comunes
    #10#124 = #4#40;          || base D+FP
    #10#125 = #4#41;          || base horas extra estruc.
    #10#126 = #4#42;          || base horas extra no estruc.
    #10#127 = #4#61;          || (%) contingencias comunes
    #10#128 = apordfp;        || (%) d+fp
    #10#129 = aporhe1;        || (%) horas extra estruc.
    #10#130 = aporhe2;        || (%) horas extra no estruc.
    #10#131 = #4#48;          || ptas. contingencias comunes
    #10#132 = #4#49;          || ptas. d+fp
    #10#133 = #4#50;          || ptas. horas extra estruc.
    #10#134 = #4#51;          || ptas. horas extra no estruc.
    #10#135 = #4#13;          || (%) IRPF 1
    #10#137 = taporta;        || total aportacion
    #10#139 = tantici;        || total anticipos
    #10#140 = #4#54;          || en especie
    #10#141 = deducir;        || total a deducir
    #10#143 = diarec;         || dia RECIBI
    #10#144 = mesrec;         || mes RECIBI
    #10#145 = anyrec;         || a¤o RECIBI
    |SI #10#145 > 80;  #10#145 = #10#145 + 1900;  |SINO;  #10#145 = #10#145 + 2000;  |FINSI;
    #10#150 = "";             || Literal fijo
    #10#151 = #4#67;          || (%) IRPF 2
    #10#152 = #4#68;          || (%) IRPF 3
    #10#160 = #4#52;          || anticipos 1
    #10#161 = #4#53;          || anticipos 2
    ||#10#175 = #4#86;          || anticipos 3
|SI #4#49 ! 0;
        #10#162 = apord;           || (%) desempleo
        #10#163 = aporfp;          || (%) form.prof.
        #10#164 = #4#40 % apord;   || ptas. desempleo
        #10#165 = #4#49 - #10#164; || ptas. form.prof.
|FINSI;

|| Buscar comentarios de anticipos 1 y 2 y en especie
|DDEFECTO #103;
|ABRE #103;
#103#0=#4#0;   || empresa
#103#1=#4#1;   || trabajador
#103#2=#4#2;   || mes
#103#3=#4#3;   || tipo
|LEE 000#103=;
|CIERRA #103;
#10#169=#103#28;
#10#170=#103#29;
#10#171=#103#30;
||#10#176 = "Anticipos P.Extra";

||HAZ AcumuladosIRPF;
|FPRC;

|PROCESO cargas_1;       || CARGA LOS CAMPOS SEGUN SU SITUACION
    incluye = 0;
    concep = #5#04;
    situac = #5#07;
    descri = #5#08;
    unidad = #5#11;      || unidades calculo
    valor  = #5#10;
    import = unidad * valor;
|SI situac ! 0;
    |SI concep ] 201; |Y concep [ 208; |Y #5#5 ! "P"; |Y #0#10 = 0;
        |SI swbaja = 1;
            |SI #2#48="S"; incluye = 1; |FINSI;  || si es finiquito
        |SINO;
            |SI #1#25=2;   incluye = 1; |FINSI;  || si la paga va junta
        |FINSI;
    |SINO;
            incluye = 1;
    |FINSI;
    |SI incluye = 1;
            lineas = 1;
            camptas = situac + 22;
            camdesc = situac + 61;
            #10camptas = #10camptas + import;
            arteris = descri % 101;
        |SI arteris ! "*";
            |DDEFECTO #100; #100#0 = #2#87; #100#1 = concep; |LEE 000#100=;
                alfa1 = #100#2;     |QBF alfa1;
            |SI alfa1 ! "";
                    #10camdesc = #100#2;
            |SINO;
                    #10camdesc = descri;
            |FINSI;
        |SINO;
                #10camdesc = "";
        |FINSI;
        |SI concep = 101;
                dcal_101 = #5#05;
                duni_101 = unidad;
                dval_101 = valor;
        |FINSI;
        |SI concep = 102;               ||   unidades concepto 102
                duni_102 = unidad;
        |FINSI;
        |SI concep ] 301; |Y concep [ 305;
            duni_hest = duni_hest + #5#11;
        |FINSI;
        |SI concep ] 306; |Y concep [ 310;
            duni_hnoest = duni_hnoest + #5#11;
        |FINSI;
    |FINSI;
|FINSI;
|FPRC;

|PROCESO cargas_2;       || CARGA LOS CAMPOS TOTALES
|SI #2#42!"A";|Y #2#42!"B";|Y #2#42!"E";|Y #2#42!"C";|Y #2#42!"F";|Y #2#42!"P";|Y #2#42!"I";
    |SI dcal_101 ! "L";
            deci_h = ((#4#33 * 100) $ 100) / 100;
            ||#10#021 = #4#8 - (#4#31 + #4#32 + #4#34 + #4#65 + deci_h + #4#66); ||unid.sal.base
            #10#021 = #4#8 - (#4#31 + #4#32 + #4#34 + #4#65 + #4#66); ||unid.sal.base
    |SINO;
            #10#021 = duni_101;
    |FINSI;
|SINO;
        #10#021 = duni_101;  || AGRARIA/CITRICOS
|FINSI;
|SI #10#021 < 0;
        #10#021 = 0;                                   || si es neg., igual a 0
        #10#022 = 0;
|SINO;
        #10#022 = #10#23 / #10#21;                     || valor salario base
|FINSI;
        #10#177 = duni_102;              || unidades concepto 102
|SI #10#022 = 0;  #10#021 = 0;  |FINSI;  || si valor es 0, unidades tambien
|PARA campo = 24; |SI campo [ 28;  |HACIENDO campo = campo + 1;
        tot_109 = campo;        #10#109 = #10#109 + #10tot_109;  || personales
        tot_110 = campo +  5;   #10#110 = #10#110 + #10tot_110;  || puesto trab.
        tot_111 = campo + 10;   #10#111 = #10#111 + #10tot_111;  || calidad ...
        tot_112 = campo + 15;   #10#112 = #10#112 + #10tot_112;  || vencimientos
        tot_113 = campo + 20;   #10#113 = #10#113 + #10tot_113;  || en especie
        tot_114 = campo + 25;   #10#114 = #10#114 + #10tot_114;  || acc.empres.
        tot_115 = campo + 30;   #10#115 = #10#115 + #10tot_115;  || indemnizac.
|SIGUE;
    #10#116 = #10#101 + #10#102 + #10#103 + #10#104;    || prestaciones S.S.
    #10#117 = #10#059 + #10#060 + #10#061;              || mejoras voluntarias
|PARA campo = 109; |SI campo [ 117; |HACIENDO campo = campo + 1;
    #10#118 = #10#118 + #10campo;                       || t.devengado parcial
|SIGUE;
    #10#118 = #10#118 + #10#023;                   || t.devengado+s.base
    #10#020 = #10#118;                             || importe total
    #10#142 = #10#118 - #10#141;                   || liquido a percibir

    #10#136 = #4#14 + #4#69 + #4#70;                    || tot.base IRPF
    #10#138 = #4#15 + #4#71 + #4#72;                    || tot.ret. IRPF
    #10#153 = #4#14;                                    || base IRPF 1
    #10#154 = #4#69;                                    || base IRPF 2
    #10#155 = #4#70;                                    || base IRPF 3
    #10#156 = dval_101;                                 || valor orig. s.base
    #10#157 = #4#15;                                    || ret. IRPF 1
    #10#158 = #4#71;                                    || ret. IRPF 2
    #10#159 = #4#72;                                    || ret. IRPF 3

|SI swbaja = 0; |Y #1#25 = 2;      || si la paga va junta y no es baja
        #10#136 = #10#136 + #4#16;    || tot. base IRPF + pagas
        #10#138 = #10#138 + #4#17;    || tot. ret. IRPF + ret. pagas
        #10#153 = #10#153 + #4#16;    || base IRPF 1 + pagas
        #10#157 = #10#157 + #4#17;    || ret. IRPF 1 + ret. pagas
|FINSI;
    enCanti = #10#142;         ||  conversion de liquidos a euros o a pesetas
    |HAZ ConverMoneda2;
    #10#173 = aMonedaRec;
    #10#174 = enCanti;
    #10#190 = duni_hest;
    #10#191 = duni_hnoest;
|FPRC;

||****************************************************************************
||***** calculos complementarios *********************************************
||****************************************************************************

|PROCESO comple_0;       || LEE EMPRESA,TRABAJADOR,CONSTANTE Y CENTRO
|DDEFECTO #1;
    #1#0 = #4#0;
|LEE 020#1=;             || empresa

|DDEFECTO #2;
    #2#0 = #4#0;
    #2#1 = #4#1;
|LEE 020#2=;             || trabajador

|ABRE #101;
|DDEFECTO #101;
    #101#0 = #4#0;
    #101#1 = #4#1;
|LEE 000#101=;
|SI FSalida = 0;|Y #101#3 ! 0;
     CodigoCons = #101#3;
|SINO;
     CodigoCons = #1#32;
|FINSI;
|CIERRA #101;

|DDEFECTO #3;
    #3#0 = CodigoCons;
|LEE 020#3=;             || constante

|DDEFECTO #6;
    #6#0 = #2#0;
    #6#1 = #2#77;
|LEE 020#6=;             || centro

duni_hest = 0;           || inicializa variables acumuladoras del
duni_hnoest = 0;         || nro. de horas extras estructurales y no estruct.

|FPRC;

|PROCESO comple_1;       || CONSTRUYE LITERALES DATOS EMPRESA
|SI #0#9 = 1; alfa1 = #1#03; |SINO; alfa1 = #6#03; |FINSI;
|QBF alfa1;
|SI #0#9 = 1; alfa2 = #1#04; |SINO; alfa2 = #6#04; |FINSI;
|QBF alfa2;
|SI #0#9 = 1; alfa3 = #1#05; |SINO; alfa3 = #6#05; |FINSI;
|QBF alfa3;
    domemp = alfa1 + " " + alfa2 + " " + alfa3;  || domicilio empresa
|SI #0#9 = 1; alfa1 = "00" + #1#07; |SINO; alfa1 = "00" + #6#07; |FINSI;
    alfa1 = alfa1 % -102;
|SI #0#9 = 1; alfa2 = "000" + #1#08;|SINO; alfa2 = "000" + #6#08;|FINSI;
    alfa2 = alfa2 % -103;
    alfa3 = "(" + alfa1 + alfa2 + ") ";
|SI #0#9 = 1; alfa1 = #1#06; |SINO; alfa1 = #6#06; |FINSI;
|QBF alfa1;
    pobemp = alfa3 + alfa1;                      || poblacion empresa
|SI #0#9 = 1; afili = #1#13; |SINO; afili = #6#10; |FINSI;
|SI #4#9 = 87;|O #4#9 = 85;|O #4#9 = 97;
              |O #4#9 = 64;|O #4#9 = 65;
    |DDEFECTO #35;
        #35#0 = #2#0;
        #35#13 = #2#77;
    |SI #4#9 = 85;|O #4#9 = 97;
            #35#1 = 87;
    |SINO;
        |SI #4#9 = 65;
                #35#1 = 64;
        |SINO;
                #35#1 = #4#9;
        |FINSI;
    |FINSI;
    |LEE 000#35=;
        afili = #35#10;
|FINSI;
|FPRC;

|PROCESO comple_2;        || CONSTRUYE PERIODO DE LIQUIDACION
    mesnum = #0#5;
    anytra = #0#4;
|HAZ lib_0;             || saca ultimo dia natural del mes
    mesliq = mesalf;
    fectra = #4#4;      || desglosa fecha de alta
|HAZ lib_1;
    diaalta = diatra;
    mesalta = mestra;
|SI mesalta = #0#5; |Y anytra = #0#4;  || si es alta este mes
        desdia = diaalta;
|SINO;
        desdia = 1;
|FINSI;
    fectra = #4#5;      || desglosa fecha de baja
|HAZ lib_1;
    diabaja = diatra;
    mesbaja = mestra;
|SI mesbaja = #0#5; |Y anytra = #0#4; |Y diabaja ! topemes;
        swbaja = 1;      || switch para saber si es baja en 'cargas_1'
        hasdia = diabaja;
|SINO;
        hasdia = topemes;
|FINSI;
|SI mesbaja = #0#5; |Y anytra = #0#4;
        coment = 1;
|FINSI;
|FPRC;

|PROCESO comple_3;        || CARGA LOS PERIODOS DE LIQ. DE ENFERM. Y ACCTE.
    desenf = 0;  hasenf = 0;
    desacc = 0;  hasacc = 0;
    swtenf = 0;  swtacc = 0;
|SI #4#27="E"; |O #4#27="M";
                  desenf=#4#19; hasenf=#4#23; swtenf=1;
|FINSI;
|SI #4#28="E"; |O #4#28="M";
    |SI swtenf=0; desenf=#4#20; hasenf=#4#24; swtenf=1; |FINSI;
|FINSI;
|SI #4#29="E"; |O #4#29="M";
    |SI swtenf=0; desenf=#4#21; hasenf=#4#25; swtenf=1; |FINSI;
|FINSI;
|SI #4#30="E"; |O #4#30="M";
    |SI swtenf=0; desenf=#4#22; hasenf=#4#26; swtenf=1; |FINSI;
|FINSI;
|SI #4#27="A";              desacc=#4#19; hasacc=#4#23; swtacc=1; |FINSI;
|SI #4#28="A"; |Y swtacc=0; desacc=#4#20; hasacc=#4#24; swtacc=1; |FINSI;
|SI #4#29="A"; |Y swtacc=0; desacc=#4#21; hasacc=#4#25; swtacc=1; |FINSI;
|SI #4#30="A"; |Y swtacc=0; desacc=#4#22; hasacc=#4#26; swtacc=1; |FINSI;
|SI swtacc = 1;|Y #36#19 = "S";
        desacc = desacc - 1;
    |SI desacc < 0;  desacc = 1;  |FINSI;  || por el rollo de las mutuas
|FINSI;
|SI desenf = 0;|Y swtenf = 1;   desenf = 1;    |FINSI;
|SI hasenf = 0;|Y swtenf = 1;   hasenf = #4#7; |FINSI;
|SI desacc = 0;|Y swtacc = 1;   desacc = 1;    |FINSI;
|SI hasacc = 0;|Y swtacc = 1;   hasacc = #4#7; |FINSI;
|FPRC;

|PROCESO comple_3_1;
     |PARA para1 = 19; |SI para1 [ 30; |HACIENDO para1 = para1 + 1;
          para2 = para1 + 159;
          |SI para1 [ 26;
               #10para2 = #4para1;
          |SINO;
               |SI #4para1 = "E"; #10para2 = "Enfermedad"; |FINSI;
               |SI #4para1 = "A"; #10para2 = "Accidente"; |FINSI;
               |SI #4para1 = "H"; #10para2 = "Huelga"; |FINSI;
               |SI #4para1 = "F"; |O #4para1 = "f"; #10para2 = "Absentismo"; |FINSI;
               |SI #4para1 = "M"; #10para2 = "Maternidad"; |FINSI;
               |SI #4para1 = "V"; #10para2 = "Vacaciones"; |FINSI;
               |SI #4para1 = "D"; #10para2 = "Desempleo"; |FINSI;
          |FINSI;
     |SIGUE;
|FPRC;

|PROCESO comple_4;        || HALLA TOTALES VARIOS
    tcotiz  = #4#37 + #4#62;
    tbcotiz = tcotiz + #4#38;           || total bases de cotizacion
|SI #4#40 ! 0;|Y #2#45 ! "087";|Y #2#45 ! "097";|Y #2#45 ! "085";
        apordfp = #3#13+#3#15+#3#17+#3#19;   ||(%) d+fp
        aporfp = #3#17;                      ||(%) fp
        apord = apordfp - aporfp;            ||(%) d
|SINO;
        apordfp = 0;
        aporfp = 0;
        apord = 0;
|FINSI;
|SI #4#41 ! 0;
        aporhe1 = #3#09;                || (%) horas extra estruc.
    |SI #2#45 = "087";|O #2#45 = "097";|O #2#45 = "085";
            aporhe1 = aporhe1 + #3#13+#3#15+#3#17+#3#19;    || aprendices
    |FINSI;
|SINO;
        aporhe1 = 0;
|FINSI;
|SI #4#42 ! 0;
        aporhe2 = #3#11;                || (%)horas extra no estruc.
    |SI #2#45 = "087";|O #2#45 = "097";|O #2#45 = "085";
            aporhe2 = aporhe2 + #3#13+#3#15+#3#17+#3#19;    || aprendices
    |FINSI;
|SINO;
        aporhe2 = 0;
|FINSI;
     || para los de formacion que estan en maternidad todo el mes, esos
     || no cotizan la parte del trabajador
     |SI #2#45 = "087";|O #2#45 = "097";|O #2#45 = "085";
          |SI #4#65 ] #4#8;
               #4#48 = 0;
               #4#49 = 0;
          |FINSI;
     |FINSI;
    taporta = #4#48 + #4#49 + #4#50 + #4#51;      || total aportacion
    tantici = #4#52 + #4#53;                      || total anticipos
    deducir = taporta + #4#75 + tantici + #4#54;  || total a deducir

|SI swbaja = 0; |Y #1#25 = 2;         || si la paga va junta y no es baja,
        deducir = deducir + #4#17;    || /le suma la parte IRPF de pagas
        tantici = tantici + #4#86;    || y le suma el anticipo Paga Ext(-3-)
        deducir = deducir + #4#86;    || al total anticipos y deducciones
|FINSI;
|FPRC;

|PROCESO comple_5;        || CONSTRUYE FECHA LITERAL DEL RECIBI
|HAZ sibaja;              || (tiene en cuenta si el trabajador es baja)
    fectra = #0#6;
|HAZ lib_1;
|SI diabaja ! 0;
        diarec = diabaja;
|SINO;
        diarec = diatra;
|FINSI;
    anyrec = anytra;
    mesnum = mestra;
|HAZ lib_0;
    mesrec = mesalf;
|FPRC;


|PROCESO comple_6;        || CONSTRUYE COMENTARIO
    sw_comen = 0;

|SI runnom ! "nomycalf";|Y #1#39 = "1";
        sw_comen = 1;
|SINO;
    |SI runnom ! "nomycalf";|Y #0#10 ! 0;  sw_comen = 1;  |FINSI;
|FINSI;

|SI sw_comen = 1;
        alfa1 = #2#49 % 109;
        alfa2 = #2#49 % 101;
    |SI alfa2 = "*";
        |SI coment ! 1;
                alfa2 = " ";
                #2#49 = "";
        |FINSI;
    |FINSI;
    |SI alfa1 = "FINIQUITO"; |O alfa2 = "*";
            #10#146 = #36#3;
            #10#147 = #36#4;
            #10#148 = #36#5;
            #10#149 = #36#6;
    |SINO;
            #10#146 = #2#49 % 130;
            #10#147 = "";
            #10#148 = "";
            #10#149 = "";
    |FINSI;
|SINO;
        alfa1 = #2#49 % 101;
    |SI alfa1 = "*";     #2#49 = "";    |FINSI;
        #10#146 = #2#49;      || comentario del fichero en 'carta finiquito'
|FINSI;
|FPRC;

||****************************************************************************
||******* SUBRUTINAS *********************************************************
||****************************************************************************


|PROCESO sibaja;   || COMPRUEBA SI EL TRABAJADOR ES BAJA EN ESTE MES
    diabaja = 0;
|SI #4#5 ! "          "; |Y #4#5 ! "..........";
        fectra = #4#5;
    |HAZ lib_1;
    |SI #0#5 = mestra; |Y #0#4 = anytra;
            diabaja = diatra;
    |FINSI;
|FINSI;
|FPRC;

||****************************************************************************
||******* LIBRERIAS **********************************************************
||****************************************************************************

|PROCESO lib_0;        || CONVIERTE A LITERAL MES EN NUMERO Y HALLA ULTIMO DIA
                       || PARAMETROS ENTRADA:  'mesnum', 'anynum'
                       || PARAMETROS SALIDA:   'mesalf', 'topemes'
|SI mesnum =  1;  mesalf = "ENERO     ";  topemes = 31;  |FINSI;
|SI mesnum =  2;
        mesalf = "FEBRERO   ";
        numer = #0#4 $ 4;
    |SI numer = 0; topemes = 29;  |SINO;  topemes = 28;  |FINSI;
|FINSI;
|SI mesnum =  3;  mesalf = "MARZO     ";  topemes = 31;  |FINSI;
|SI mesnum =  4;  mesalf = "ABRIL     ";  topemes = 30;  |FINSI;
|SI mesnum =  5;  mesalf = "MAYO      ";  topemes = 31;  |FINSI;
|SI mesnum =  6;  mesalf = "JUNIO     ";  topemes = 30;  |FINSI;
|SI mesnum =  7;  mesalf = "JULIO     ";  topemes = 31;  |FINSI;
|SI mesnum =  8;  mesalf = "AGOSTO    ";  topemes = 31;  |FINSI;
|SI mesnum =  9;  mesalf = "SEPTIEMBRE";  topemes = 30;  |FINSI;
|SI mesnum = 10;  mesalf = "OCTUBRE   ";  topemes = 31;  |FINSI;
|SI mesnum = 11;  mesalf = "NOVIEMBRE ";  topemes = 30;  |FINSI;
|SI mesnum = 12;  mesalf = "DICIEMBRE ";  topemes = 31;  |FINSI;
|SI mesnum =  0;  mesalf = "          ";  topemes =  0;  |FINSI;
|FPRC;

|PROCESO lib_1;        || SEPARA UNA FECHA DADA EN DIA,MES Y A¥O
                       || PARAMETROS ENTRADA:  'fectra'
                       || PARAMETROS SALIDA:   'diatra', 'mestra', 'anytra'
                       || VARIABLES: G_
    diatra = 0;
    mestra = 0;
    anytra = 0;
|SI fectra ! "          "; |Y fectra ! "..........";
    alfa1 = fectra % 102;
    diatra = alfa1;
    alfa1 = fectra % 402;
    mestra = alfa1;
    alfa1 = fectra % -102;
    anytra = alfa1;
|FINSI;
|FPRC;

/*
|| CALCULO DE ACUMULADOS IRPF

|PROCESO eosm0032_1;
     |DDEFECTO #eosm0027;
     #eosm0027#0 = #eosm0032#3;
     #eosm0027#1 = #eosm0032#4;
     |LEE 000#eosm0027.=;
     |SI #eosm0027#17 = "N";
          #10#166 = #10#166 + (#eosm0032#7 + #eosm0032#10);
          #10#167 = #10#167 + (#eosm0032#9 + #eosm0032#12);
          #10#168 = #10#168 + #eosm0032#15;
     |FINSI;
|FPRC;

|DEFBUCLE eosm0032;
     #eosm0032#1;
     ;
     #labempre#0, anyo, #labtraba#7, " ", "  ", 0,    1;
     #labempre#0, anyo, #labtraba#7, "z", "zz", 9, 9999;
     ;
     NULL, eosm0032_1;
|FIN;

|PROCESO AcumuladosIRPF;
     #10#166 = 0;
     #10#167 = 0;
     #10#168 = 0;
     |ABRE #eosm0027;
     |BUCLE eosm0032;
     |CIERRA #eosm0027;
     #10#166 = #10#166 + (#4#14 + #4#69 + #4#70 + #4#16);
     #10#167 = #10#167 + (#4#15 + #4#71 + #4#72 + #4#17);
     #10#168 = #10#168 + (#4#48 + #4#49 + #4#50 + #4#51);
|FPRC;
*/
