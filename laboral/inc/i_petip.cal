|VARIABLES;
    palfa1 = "";
    palfa2 = "";
    pnume1 = 0;
    pelmes = 0;
    fabre = "";
    field = "";
    fcanl = 0;
    unida = "";
    dire = "";
    sistem = "";
    empresa = "";
    fichero1 = "";
    fichero2 = "";
    secuenci = "";
|FIN;

|PROCESO fich_peti; |TIPO 7;
    palfa1 = #0peti1;
|C_M #0peti2, 1, palfa1;

|SI #0peti1 = "S";
        palfa1 = #0pany;      palfa1 = palfa1 % -102;
        palfa2 = "p" + palfa1;
        palfa1 = #0pmes;      palfa1 = ("00" + palfa1) % -102;
        palfa2 = palfa2 + palfa1;
        #0peti2 = palfa2;
    |PINTA #0peti2;
|FINSI;
|FPRC;

|PROCESO peti_graba;
    field = fcanl + " " + palfa1;
|FGRABA field;
|FPRC;

|PROCESO peti_field;
|SI fcanl ] 0;
    |DDEFECTO #5;
        #5#0 = #7#0;
        #5#1 = #7#39;
    |LEE 020#5=;

        palfa1 = pelmes;
        palfa1 = ("00" + palfa1) % -102;
    |HAZ peti_graba;          || mes (2N)

        palfa1 = #0pany;
    |HAZ peti_graba;          || a¤o (4N)

        palfa1 = antcuent % 102;    |QBT palfa1;
        palfa1 = ("00" + palfa1) % -102;
    |HAZ peti_graba;          || provincia cuenta cotizacion (2N)

        palfa1 = antcuent % 309;    |QBT palfa1;
        palfa1 = ("000000000" + palfa1) % -109;
    |HAZ peti_graba;          || provincia cuenta cotizacion (9N)

        palfa1 = antcif % 101;
        palfa2 = antcif % 208;
    |SI palfa1 ] "0";|Y palfa1 [ "9";        || si no es un cif
            palfa1 = antcif;   |QBF palfa1;
            palfa1 = palfa1 % -101;
            palfa2 = antcif % 108;
        |SI palfa1 ] "0";|Y palfa1 [ "9";    || si no es un dni
                palfa1 = "";
        |FINSI;
    |FINSI;
    |HAZ peti_graba;          || letra del CIF o DNI (1A)

        palfa1 = palfa2;
        palfa1 = ("00000000" + palfa1) % -108;
    |HAZ peti_graba;          || numero del CIF o DNI (7N)

||        palfa1 = " ";
||    |HAZ peti_graba;          || codigo de control ¨...? (1A)

        palfa1 = #7#14;  palfa1 = ("000" + palfa1) % -103;
    |HAZ peti_graba;          || centro de trabajo (3N)

        palfa1 = #7#15;
    |HAZ peti_graba;          || codigo provincia NAFSS (2N)

        palfa1 = #7#16;  |QBF palfa1;
        palfa2 = #7#17;  |QBF palfa2;
        palfa1 = palfa1 + palfa2;
        palfa1 = ("0000000000" + palfa1) % -110;
    |HAZ peti_graba;          || resto NAFSS (10N)

        palfa1 = #7#18;  |QBF palfa1;
        palfa2 = " " * 9;
        palfa1 = (palfa1 + palfa2) % 109;
    |HAZ peti_graba;          || NIF (9A)

        palfa1 = #5#2;   |QBF palfa1;
        palfa2 = " " * 50;
        palfa1 = (palfa1 + palfa2) % 150;
    |HAZ peti_graba;          || nombre y apellidos (50A)

        pnume1 = #7#20 ! 0;
        palfa1 = pnume1;
        palfa1 = ("00" + palfa1) % -102;
    |HAZ peti_graba;          || dias en alta (2N)

        palfa1 = #7#21;
        palfa1 = ("00" + palfa1) % -102;
    |HAZ peti_graba;          || grupo cotizacion (2N)

        palfa1 = #7#25;
        palfa1 = ("000000" + palfa1) % -106;
    |HAZ peti_graba;          || base cotizacion CC (6N)

        palfa1 = #5#3;   |QBF palfa1;
        palfa2 = #5#4;   |QBF palfa2;   || nro.
        palfa1 = palfa1 + " " + palfa2;
        palfa2 = #5#5;   |QBF palfa2;   || piso
        palfa1 = palfa1 + " " + palfa2;
        palfa2 = " " * 34;
        palfa1 = (palfa1 + palfa2) % 134;
    |HAZ peti_graba;          || domicilio (34A)

        palfa1 = #5#8;
        palfa1 = ("00" + palfa1) % -102;
    |HAZ peti_graba;          || cod.postal 1 (2N)

        palfa1 = #5#9;
        palfa1 = ("000" + palfa1) % -103;
    |HAZ peti_graba;          || cod.postal 2 (3N)

        palfa1 = #5#6;   |QBF palfa1;
        palfa2 = " " * 20;
        palfa1 = (palfa1 + palfa2) % 120;
    |HAZ peti_graba;          || localidad (20A)

        palfa1 = #5#244; |QBF palfa1;
        palfa1 = ("0000000" + palfa1) % -107;
    |HAZ peti_graba;          || telefono (7N)

    |SI #5#11 ! "          ";|Y #5#11 ! "..........";
            palfa1 = (#5#11 % 102) + (#5#11 % 402) + (#5#11 % -104);
    |SINO;
            palfa1 = "00000000";
    |FINSI;
    |HAZ peti_graba;          || fecha nacimiento (8N)

        palfa1 = "0";
    |HAZ peti_graba;          || sexo (1N)

        palfa1 = #5#18;  |QBF palfa1;
        palfa2 = " " * 25;
        palfa1 = (palfa1 + palfa2) % 125;
    |HAZ peti_graba;          || categoria laboral (25A)

        palfa1 = &13 + &10;
    |HAZ peti_graba;          || final de linea
|FINSI;
|FPRC;

|PROCESO peti_cierra;
|SI fcanl ] 0;
        palfa1 = fcanl;
    |FCIERRA palfa1;
        Pos = -1;
|FINSI;
|FPRC;

|PROCESO peti_abre;
|DFICE palfa1;  |QBF palfa1;
    fabre = "wb  " + palfa1 + #0peti2;
|QBF fabre;
    Pos = -1;
|FABRE fabre;
    fcanl = FSalida;
|SI fcanl < 0;
        palfa1 = "    [" + (fabre % 5) + "] no accesible intentando escritura ...";
    |MENAV palfa1;
|FINSI;
|FPRC;

|PROCESO peti_copia;          || copia a disco
|BLIN 23;
|CONFI "2305SConfirme la copia a diskette (S/N): ";
|SI FSalida = 0;
    |PINTA 2305, "Unidad de diskette: ";
        E_sup = 1;
        E_inf = "ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz";
        unida = "A";
        unida = 2325 ? 0;
    |MENAV "2305Introduzca un diskette formateado en D.O.S. ...";
    |DFICE empresa;
        secuenci = #0peti2;   |QBF secuenci;
        dire = "";
    |DIRECTORIO dire;
    |QUE_SISTEMA sistem;
    |BLIN 23;
    |PINTA 2305, "Copiando ...";
    |SI sistem ! "ESDOS";
            alfa2 = dire + "bin/agcopiaa " + empresa + secuenci + " " + unida + ": ";
        |SYSTEM alfa2;
    |SINO;
            fichero1 = empresa + secuenci;
            fichero2 = unida + ":" + secuenci;
        |COPIA_FICHERO fichero1, fichero2;
        |SI FSalida ! 0;
                alfa1 = "    Error [" + FSalida + "] en copia de ficheros ...";
            |MENAV alfa1;
        |FINSI;
    |FINSI;
|FINSI;
|FPRC;
