/******* PROCESO COMUNES PARA EL CALCULO DE NOMINA Y EMISIONES **************/
/*******          DE RECIBOS: LAS PARRILLAS Y OTROS            **************/

|FICHEROS;
     nomgruec;
     nomgruel;
     labempre #1;
|FIN;

|VARIABLES;
     nCampo = 0;
     aMensa = "";
     || aAlfa1 = "";
     || aAlfa2 = "";
     nMinGru = 0;
     nMaxGru = 0;
     swF11Emp = 0;
     swF11Tra = 0;
     swF11Gru = 0;
     &enEmpresa = 0;
     &enTrabajador = 0;
     nCampoEmp = 0;
     nCampoTra = 0;
     nHastaCampo = 0;
     nC1 = 0;
     nC2 = 0;
|FIN;

|PROCESO Min;
     #labtraba#0 = #0nCampo;
     #labtraba#1 = 00001;
|FPRC;

|PROCESO Max;
     #labtraba#0 = #0nCampo;
     #labtraba#1 = 99999;
|FPRC;

|PROCESO BuscaTraba;
     FEstado = FSalida;
     |SI Campo = 2; |O Campo = 3;
          nCampo = Campo - 2;
     |SINO;
          nCampo = Campo - 21;         || a ver si ha metido o no la empresa
     |FINSI;
     |SI FEstado = 13; |O swF11Tra = 1;
          |SI #0nCampo = 0;
               swF11Tra = 0;
               |ACABA;
          |SINO;
               |ABRE #labtraba;
               |CONSULTA_F_CLAVE #1#labtraba, Min, Max;
               |SI FSalida = 0;
                    #0Campo = #labtraba#1;
                    |PINTA #0Campo;
               |SINO;
                    |ERROR;
               |FINSI;
               |CIERRA #labtraba;
               swF11Tra = 0;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO CompTrab;
     nCampo = Campo - 21;
     |SI #0Campo ! 0; |Y #0nCampo ! 0;
          |ABRE #labtraba;
          #labtraba#0 = #0nCampo;
          #labtraba#1 = #0Campo;
          |LEE 000#labtraba.=;
          |SI FSalida ! 0;
               aAlfa1 = #0Campo;  aAlfa1 = "00000" + aAlfa1; aAlfa1 = aAlfa1 % -105;
               aAlfa2 = #0nCampo; aAlfa2 = "00000" + aAlfa2; aAlfa2 = aAlfa2 % -105;
               aMensa = "    Trabajador " + aAlfa2 + "." + aAlfa1 + " no existe";
               |MENAV aMensa;
               |ERROR;
          |FINSI;
          |CIERRA #labtraba;
     |FINSI;
|FPRC;

|PROCESO F11Tra; |TIPO 66;
     swF11Tra = 1;
     |HAZ BuscaTraba;
|FPRC;

|PROCESO F11Emp; |TIPO 66;
     swF11Emp = 1;
     |HAZ BuscaEmpresa;
|FPRC;

|PROCESO CompEmpre;
     |DFICE aAlfa1;
     |DFICB aAlfa2;

     |SI aAlfa1 ! aAlfa2;
          |PATH_DAT #1 aAlfa1;
     |FINSI;

     |SI #0Campo ! 0;
          |ABRE #labempre;
          #labempre#0 = #0Campo;
          |LEE 000#labempre.=;
          |SI FSalida ! 0;
               aAlfa1 = #0Campo; aAlfa1 = "00000" + aAlfa1; aAlfa1 = aAlfa1 % -105;
               aMensa = "    La Empresa " + aAlfa1 + " no existe";
               |MENAV aMensa;
               |ERROR;
          |FINSI;
          |CIERRA #labempre;
     |FINSI;

     |DFICE aAlfa1;
     |DFICB aAlfa2;

     |SI aAlfa1 ! aAlfa2;
          |PATH_DAT #1 aAlfa2;
     |FINSI;
|FPRC;

|PROCESO BuscaEmpresa;
     FEstado = FSalida;
     |DFICE aAlfa1;
     |DFICB aAlfa2;

     |SI aAlfa1 ! aAlfa2;
          |PATH_DAT #1 aAlfa1;
     |FINSI;

     |SI FSalida = 13; |O swF11Emp = 1;
          |ABRE #labempre;
          |CONSULTA_CLAVE #1#labempre;
          |SI FSalida = 0;
               #0Campo = #labempre#0;
               |PINTA #0Campo;
          |SINO;
               |ERROR;
          |FINSI;
          |CIERRA #labempre;
          swF11Emp = 0;
     |FINSI;

     |DFICE aAlfa1;
     |DFICB aAlfa2;

     |SI aAlfa1 ! aAlfa2;
          |PATH_DAT #1 aAlfa2;
     |FINSI;
|FPRC;

|PROCESO CompGrupo;
     |SI #0Campo ! 0;
          |ABRE #nomgruec;
          #nomgruec#0 = #0Campo;
          |LEE 000#nomgruec.=;
          |SI FSalida ! 0;
               aAlfa1 = #0Campo; aAlfa1 = "0000" + aAlfa1; aAlfa1 = aAlfa1 % -104;
               aMensa = "    El grupo " + aAlfa1 + " no existe";
               |MENAV aMensa;
               |ERROR;
          |FINSI;
          |CIERRA #nomgruec;
     |FINSI;
|FPRC;

|PROCESO BuscaGrupo;
     |SI FSalida = 13; |O swF11Gru = 1;
          |ABRE #nomgruec;
          |CONSULTA_CLAVE #1#nomgruec;
          |SI FSalida = 0;
               #0Campo = #nomgruec#0;
               |PINTA #0Campo;
               |CIERRA #nomgruec;
               swF11Gru = 0;
          |SINO;
               |CIERRA #nomgruec;
               swF11Gru = 0;
               |ERROR;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO F11Gru; |TIPO 66;
     swF11Gru = 1;
     |HAZ BuscaGrupo;
|FPRC;

|| ***************************************************************************
||           PROCESOS COMUNES QUE ESTABAN REPETIDOS EN CADA EMISION
|| ***************************************************************************

|PROCESO salteada;
     |ABRE #labtraba;
     |PARA salte_1 = 21; |SI salte_1 [ 41; |HACIENDO salte_1 = salte_1 + 1;
          salte_2 = salte_1 + 21;
          #labtraba#0 = #0salte_1;
          #labtraba#1 = #0salte_2;
          |SI #labtraba#0 ] 1;|Y #labtraba#1 ] 1;
          |LEE 000#labtraba.=;
          |SI FSalida = 0;|Y #labtraba#44 = "A";
               |HAZ proceso;
          |FINSI;
     |FINSI;
     |SIGUE;
     |CIERRA #labtraba;
|FPRC;

|PROCESO CamposGru;
     |SI runnom = "nomcalat";      || nMinGru y nMaxGru son el primero
          nMinGru = 68;            || y el ultimo campo que corresponden
          nMaxGru = 75;            || a la seleccion de grupos en el DEF
     |FINSI;                       || lo hago asi porque en el nomcalat
                                   || cambia (putadita) y en el nomrecib igual
     aAlfa1 = runnom % -104;
     |SI aAlfa1 = "reci"; |O aAlfa1 = "abie"; |O aAlfa1 = "calf";
          nMinGru = 70;
          nMaxGru = 77;
     |FINSI;
     |SI runnom = "nomcalcu"; |O runnom = "nomcalf1"; |O runnom = "gomcalcu";
     |O runnom = "nomgir00";
          nMinGru = 63;
          nMaxGru = 70;
     |FINSI;
|FPRC;

|PROCESO MensaSele; |TIPO 66;
     |ABRE #labempre;
     |CONSULTA_CLAVE #1#labempre;
     |SI FSalida = 0;
          #0Campo = #labempre#0;
          |PINTA #0Campo
     |FINSI;
     |CIERRA #labempre;
|FPRC;

|PROCESO DesdeHastaCentro;
     nC1 = 0;
     nC2 = 0;

     aAlfa = runnom; |QBF aAlfa;
     |SI aAlfa = "nomcalcu"; |O aAlfa = "nomcalf1"; |O aAlfa = "gomcalcu";
     |O aAlfa = "nomgir00";
          nC1 = 71; nC2 = 72;
     |FINSI;
     |SI aAlfa = "nomcalat";
          nC1 = 77; nC2 = 78;
     |FINSI;

     |SI nC1 ! 0; |Y nC2 ! 0;
          |CAMPO_MODIFICA #0nC1, 1, alfa1;
          |CAMPO_MODIFICA #0nC2, 1, alfa1;
     |FINSI;
|FPRC;

|PROCESO seleccion1; |TIPO 0;
     |SI #0#21 = 0;
          alfa1 = "S";
          alfa2 = "N";
          |PARA nume1 = 21; |SI nume1 [ 62; |HACIENDO nume1 = nume1 + 1;
               #0nume1 = 0;
               |SI nume1 ! 27; |Y nume1 ! 34; |Y nume1 ! 41;
                |Y nume1 ! 48; |Y nume1 ! 55; |Y nume1 ! 62;
                    |PINTA #0nume1;
               |FINSI;
          |SIGUE;
     |SINO;
          |SI nMinGru ! 0;
               || no deberia estar asi, pero si lo quito me iguala a cero
               || el desde empresa y no debe, tengo tiempo de ver por que
               |SI runnom ! "labnct02"; |Y runnom ! "labnct04";
                    |PARA nume1 = nMinGru; |SI nume1 [ nMaxGru; |HACIENDO nume1 = nume1 + 1;
                         #0nume1 = 0;
                    |SIGUE;
               |FINSI;
               |PINDA #0#0;
          |FINSI;
          alfa1 = "N";
          alfa2 = "S";
     |FINSI;

     aAlfa = runnom;
     |QBF aAlfa;
     aAlfa = aAlfa % -104;
     |SI aAlfa = "reci";      || si no me ponia como no modificable el campo
          nHastaCampo = 3;    || a¤o y otros en emision de recibos
     |SINO;
          nHastaCampo = 7;
     |FINSI;

     |PARA para1 = 0; |SI para1 [ nHastaCampo; |HACIENDO para1 = para1 + 1;
          |CAMPO_MODIFICA #0para1, 1, alfa1;
     |SIGUE;
     |PARA para1 = 22; |SI para1 [ 62; |HACIENDO para1 = para1 + 1;
          |CAMPO_MODIFICA #0para1, 1, alfa2;
     |SIGUE;
     |HAZ CamposGru;
     |PARA para1 = nMinGru; |SI para1 [ nMaxGru; |HACIENDO para1 = para1 + 1;
          |CAMPO_MODIFICA #0para1, 1, alfa1;
     |SIGUE;

     |HAZ DesdeHastaCentro;
|FPRC;

|PROCESO seleccion2; |TIPO 0;
     |SI #0nMinGru = 0;
          alfa1 = "S";
          alfa2 = "N";
          nume2 = nMinGru + 1;
          |PARA nume1 = nume2; |SI nume1 [ nMaxGru; |HACIENDO nume1 = nume1 + 1;
               #0nume1 = 0;
          |SIGUE;
          |PINDA #0#0;
     |SINO;
          alfa1 = "N";
          alfa2 = "S";
     |FINSI;

     |SI #0#21 = 0;
          |PARA para1 = 0; |SI para1 [ 7; |HACIENDO para1 = para1 + 1;
               |CAMPO_MODIFICA #0para1, 1, alfa1;
          |SIGUE;

          |HAZ DesdeHastaCentro;
     |FINSI;

     para1 = 21;
     |CAMPO_MODIFICA #0para1, 1, "S";
     |PARA para1 = 22; |SI para1 [ 62; |HACIENDO para1 = para1 + 1;
          |CAMPO_MODIFICA #0para1, 1, "N";
     |SIGUE;
     |PARA para1 = nume2; |SI para1 [ nMaxGru; |HACIENDO para1 = para1 + 1;
          |CAMPO_MODIFICA #0para1, 1, alfa2;
     |SIGUE;
     aAlfa2 = runnom % -104;
     |SI aAlfa2 = "reci";
          |SI #0#70 = 0; |Y #0#21 = 0;
               para1 = 65;
               |CAMPO_MODIFICA #0para1, 1, "S";
               para1 = 66;
               |CAMPO_MODIFICA #0para1, 1, "S";
          |SINO;
               para1 = 65;
               |CAMPO_MODIFICA #0para1, 1, "N";
               para1 = 66;
               |CAMPO_MODIFICA #0para1, 1, "N";
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO EnAlta;
     |SI FEstado = 18;
          |SI Campo ] 21; |Y Campo [ 41;          || desde empresa
               nCampoEmp = Campo;
               nCampoTra = Campo + 21;
          |SINO;
               |SI Campo ] 42; |Y Campo [ 62;     || desde trabajador
                    nCampoEmp = Campo - 21;
                    nCampoTra = Campo;
               |FINSI;
          |FINSI;
          ||SI #0nCampo ! 0;
               |SI nCampoEmp = Campo;
                    enEmpresa = 0;
               |SINO;
                    enEmpresa = #0nCampoEmp;
               |FINSI;
               enTrabajador = #0nCampoTra;
               |HAZ EnAlta_plb;
               |SI enTrabajador ! 0;
                    #0nCampoEmp = enEmpresa;
                    #0nCampoTra = enTrabajador;
                    |PINTA #0nCampoEmp;
                    |PINTA #0nCampoTra;
               |SINO;
                    |ERROR;
               |FINSI;
          ||FINSI;
     |FINSI;
|FPRC;
