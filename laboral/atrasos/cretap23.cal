|FICHEROS;
     cretap23 #0;
     cretam36;
     cretam37;

     cretam18;

     cretam02;

     labempre;
     labcentr;
     nomcenes;
     labtraba;
|FIN;

|VARIABLES;
     &eaFichero = "";
     &aSoloFichero = "";

     nNume = 0;
     aLon = "";
     nLon = 0;
     nEspacio = 0;
     aEspacio = "";
     nBlancos = 0;
     nBlancosIzd = 0;
     nBlancosDer = 0;
     aBlancosIzd = "";
     aBlancosDer = "";

     aCadena = "";
     aCadenaSal = "";

     FLinea = 0;
     nNroLinea = 0;
     aNroLinea = "";
     aAlfa = "";

     nHandle = 0;
     aLinea = "";
     &aTotalLineas = "";

     aCCC = "";
     nAnyDesde = 0;
     nMesDesde = 0;
     aAnyDesde = "";
     aMesDesde = "";
     nTipo = 0;
     aDia = "";
     aMes = "";
     aAny = "";
     aEtiqueta = "";
     nLoc = 0;
     aValor = "";
     nValor = "";
     aCampoFecha = "";
     aDiaRec = "";
     aMesRec = "";
     aAnyRec = "";
     aHoraRec = "";
     aEtiquetaIni = "";
     aEtiquetaFin = "";
     aMesHasta = "";
     aAnyHasta = "";
     aTipo = "";

     nEmp = 0;
     nTra = 0;
     swEncontrada = 0;
     aAlfa1 = "";
     aAlfa2 = "";
     &enMes = 0;
     &enAny = 0;
     &enSwAlta = 0;

     aNaf = "";
     aEstado = "";
     fFechaRec = @;

     aTipoIPF = "";
     aNumeroIPF = "";
     aSiglas = "";

     aDiaDesdeTramo = "";
     aMesDesdeTramo = "";
     aAnyDesdeTramo = "";
     aFechaDesdeTramo = "";
     aDiaHastaTramo = "";
     aMesHastaTramo = "";
     aAnyHastaTramo = "";
     aFechaHastaTramo = "";

     aDias = "";
     aHoras = "";
     aDescripcion = "";
     nConcepto = 0;
     nImporte = 0;
     aCentro = "";

     nCampo = 0;
     nCampo1 = 0;
     nCampo2 = 0;
     nCampo3 = 0;
     aTipoSuma = "";
     nBase = 0;
     aTipoRLC = "";
     fFecha = @;
     aHora = "";
     &eaCadena = "";
     &eaCadenaSal = "";
     &swMsDos = 0;
     &swBasico = 0;
|FIN;

|PROCESO Centra;
     aLon = aCadena % 0;
     nLon = aLon;
     nBlancos = nEspacio - nLon;
     nNume = nBlancos / 2;
     nBlancosIzd = nNume ! 0;
     nBlancosDer = nBlancos - nBlancosIzd;
     aBlancosIzd = " " * nBlancosIzd;
     aBlancosDer = " " * nBlancosDer;
     aCadenaSal = aBlancosIzd + aCadena + aBlancosDer;

|FPRC;

/****************************************************************************/
/***************************   BORRADO PREVIO *******************************/
/****************************************************************************/

|PROCESO Borra_cretam37;
     |BORRA 020#cretam37.a;
     |LIBERA #cretam37;
|FPRC;

|DEFBUCLE Borra_cretam37;
     #cretam37#2;
     ;
     aCCC, nAnyDesde, nMesDesde, nTipo;
     aCCC, nAnyDesde, nMesDesde, nTipo;
     ;
     NULL, Borra_cretam37;
|FIN;

|PROCESO Borra_cretam36;
     |BORRA 020#cretam36.a;
     |LIBERA #cretam36;
|FPRC;

|DEFBUCLE Borra_cretam36;
     #cretam36#2;
     ;
     aCCC, nAnyDesde, nMesDesde, nTipo;
     aCCC, nAnyDesde, nMesDesde, nTipo;
     ;
     NULL, Borra_cretam36;
|FIN;

/****************************************************************************/
/*********** PROCESOS PARA BUSCAR CODIGOS DE EMP, TRA Y CEN *****************/
/****************************************************************************/

|PROCESO BuscaCCC;
     nEmp = #cretam02#0;
     swEncontrada = 1;
|FPRC;

|DEFBUCLE BuscaCCC;
     #cretam02#1;
     ;
     00001, nAnyDesde, nMesDesde, nTipo, aCCC;
     99999, nAnyDesde, nMesDesde, nTipo, aCCC;
     ;
     NULL, BuscaCCC;
|FIN;

|PROCESO BuscaPorCCC;
     swEncontrada = 0;
     |BUCLE BuscaCCC;
|FPRC;

/****************************************************************************/
/********************   TIPOS DE DATOS COMUNES  *****************************/
/****************************************************************************/

|PROCESO TipoFecha;
     aDia = "";
     aMes = "";
     aAny = "";

     |PARA; |SI; |HACIENDO;
          |HAZ LeeLinea;

          aEtiqueta = "Dia";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               aDia = aValor;
          |FINSI;

          aEtiqueta = "Mes";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               aMes = aValor;
          |FINSI;

          aEtiqueta = "Anho";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               aAny = aValor;
          |FINSI;

          aEtiqueta = aCampoFecha;
          |HAZ BuscaEtiFin;
          |SI nLoc = 1;
               |SAL;
          |FINSI;
     |SIGUE;
|FPRC;

|PROCESO FechaHoraRec;
     |PARA; |SI; |HACIENDO;
          |HAZ LeeLinea;

          aEtiqueta = "FechaRecaudacion";
          |HAZ BuscaEtiIni;
          |SI nLoc = 1;
               aCampoFecha = "FechaRecaudacion";
               |HAZ TipoFecha;
               aDiaRec = aDia;
               aMesRec = aMes;
               aAnyRec = aAny;
               aAlfa = aDiaRec + "." + aMesRec + "." + aAnyRec;
               fFechaRec = aAlfa;
          |FINSI;

          aEtiqueta = "HoraRecaudacion";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               aHoraRec = (aValor % 102) + ":" + (aValor % 302) + ":" + (aValor % 502);
          |FINSI;

          aEtiqueta = "FechaHoraRecaudacion";
          |HAZ BuscaEtiFin;
          |SI nLoc = 1;
               |SAL;
          |FINSI;
     |SIGUE;
|FPRC;

|PROCESO CCC;
     |PARA; |SI; |HACIENDO;
          |HAZ LeeLinea;

          aEtiqueta = "Regimen";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               aCCC = aValor;
          |FINSI;

          aEtiqueta = "Provincia";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               aCCC = aCCC + aValor;
          |FINSI;

          aEtiqueta = "Numero";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               aCCC = aCCC + aValor;
          |FINSI;

          aEtiqueta = "Ccc";
          |HAZ BuscaEtiFin;
          |SI nLoc = 1;
               |SAL;
          |FINSI;
     |SIGUE;
|FPRC;

/****************************************************************************/
/***********     PROCESOS PARA BUSCAR LAS ETIQUETAS       *******************/
/****************************************************************************/

|PROCESO BuscaEtiFin;
     nLoc = 0;
     aEtiqueta = "</" + aEtiqueta + ">";
     aAlfa = aLinea - aEtiqueta;
     |SI aAlfa ! aLinea;
          nLoc = 1;
     |FINSI;
|FPRC;

|PROCESO BuscaEtiIni;
     nLoc = 0;
     aEtiqueta = "<" + aEtiqueta;
     aAlfa = aLinea - aEtiqueta;
     |SI aAlfa ! aLinea;
          nLoc = 1;
     |FINSI;
|FPRC;

|PROCESO BuscaEtiIniFin;
     aValor = "";
     nLoc = 0;

     aEtiquetaIni = "<" + aEtiqueta + ">";
     aEtiquetaFin = "</" + aEtiqueta + ">";
     aAlfa = aLinea - aEtiquetaIni;
     aAlfa = aAlfa  - aEtiquetaFin;
     |SI aAlfa ! aLinea;
          aValor = aAlfa;
          |SI aEtiqueta = "RazonSocial"; |O aEtiqueta = "EntidadAtEp";
          |O aEtiqueta = "DescripcionLRLC"; |O aEtiqueta = "ModalidadPago";
          |O aEtiqueta = "TextoPieRLC";
               |QBF aValor;
          |SINO;
               |QBT aValor;
          |FINSI;
          nLoc = 1;
          |SI aEtiqueta = "ModalidadPago";
               swMsDos = 1;
               swBasico = 0;
               eaCadena = aValor;
               |HAZ CambiaTildes_plb;
               aValor = eaCadenaSal;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO LeeLinea;
     |XLEE nHandle, 300, aLinea;
     FLinea = FSalida;
     nNroLinea = nNroLinea + 1;
     aNroLinea = nNroLinea;
     aAlfa = " Linea " + aNroLinea + " de " + aTotalLineas;

     |ATRI R;
     aCadena = aAlfa; nEspacio = 35; |HAZ Centra;
     |PINTA 2223, aCadenaSal;
     |ATRI 0;
|FPRC;

|PROCESO TextoPie;
     aAlfa = #cretam36#31 % 101;
     |SI aAlfa = "P";
          aAlfa = "Este recibo no implica el pago de las cuotas si no va ";
          aAlfa = aAlfa + "acompa¤ado del correspondiente ";
          #cretam36#44 = aAlfa;

          aAlfa = "comprobante de ingreso, sello o validaci¢n mec nica de la ";
          aAlfa = aAlfa + "Entidad Financiera. Este documento ";
          #cretam36#45 = aAlfa;

          aAlfa = " recoge los c lculos realizados a la fecha ";
          aAlfa = aAlfa + "de la  confirmaci¢n/cierre de la liquidaci¢n.";
          #cretam36#46 = aAlfa;
     |FINSI;
     |SI aAlfa = "D";
          aAlfa = "Este recibo no implica el pago de las cuotas si no va ";
          aAlfa = aAlfa + "acompa¤ado del correspondiente ";
          #cretam36#44 = aAlfa;

          aAlfa = "comprobante de ingreso de la Entidad Financiera. ";
          aAlfa = aAlfa + "Este documento recoge los c lculos ";
          #cretam36#45 = aAlfa;

          aAlfa = "realizados a la fecha ";
          aAlfa = aAlfa + "de la confirmaci¢n/cierre de la liquidaci¢n.";
          #cretam36#46 = aAlfa;
     |FINSI;
|FPRC;

|PROCESO DatosPiePaginaRLC;
     #cretam36#0 = nEmp;
     #cretam36#1 = nAnyDesde;
     #cretam36#2 = nMesDesde;
     #cretam36#3 = nTipo;
     #cretam36#4 = aCCC;
     |LEE 000#cretam36.=;
     || ha de existir, la he grabado en la cabecera

     || lo primero grabar los campos del pie, resulta que cuando es pago elec-
     || tronico, la linea del texto es de mas de 255 caracteres, con lo que
     || el basico no sabe tratar la variable, lo hago a pelo y ya esta

     || la primera es la del pie, no tengo que mirarla que si no esto peta
     |XLEE nHandle, 300, aLinea;

     |HAZ TextoPie;

     FLinea = 0;

     |PARA;
     |SI FLinea ] 0;
     |HACIENDO;
          FLinea = 0;
          |HAZ LeeLinea;

          || aEtiqueta = "TextoPieRLC";
          ||HAZ BuscaEtiIni;
          ||SI nLoc = 1;
               ||HAZ TextoPie;
          ||FINSI;

          aEtiqueta = "IdentificacionAplicacionCB";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               #cretam36#48 = aValor;
          |FINSI;

          aEtiqueta = "TipoFormatoCB";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               #cretam36#49 = aValor;
          |FINSI;

          aEtiqueta = "EmisoraCB";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               #cretam36#50 = aValor;
          |FINSI;

          aEtiqueta = "SufijoCB";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               #cretam36#51 = aValor;
          |FINSI;

          aEtiqueta = "ReferenciaCB";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               #cretam36#52 = aValor;
          |FINSI;

          aEtiqueta = "IdentificadorCB";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               #cretam36#53 = aValor;
          |FINSI;

          aEtiqueta = "ImporteCB";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               #cretam36#54 = aValor;
          |FINSI;

          aEtiqueta = "DigitoParidadCB";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               #cretam36#55 = aValor;
          |FINSI;

          aEtiqueta = "DatosPiePaginaRLC";
          |HAZ BuscaEtiFin;
          |SI nLoc = 1;
               |GRABA 020#cretam36.a;
               |LIBERA #cretam36;
               |SAL;
          |FINSI;
     |SIGUE;
|FPRC;

|PROCESO LineaRLC;
     FLinea = 0;

     nConcepto = 0;
     aDescripcion = "";
     nBase = 0;
     nImporte = 0;

     |PARA;
     |SI FLinea ] 0;
     |HACIENDO;
          FLinea = 0;
          |HAZ LeeLinea;

          aEtiqueta = "DescripcionLRLC";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               aDescripcion = aValor;
               aAlfa = " " * 100;
               aDescripcion = aDescripcion + aAlfa;
               aDescripcion = (aDescripcion % 199) + " ";
               |SELECT #2#cretam18;
               #cretam18#1 = aDescripcion;
               |LEE 000#cretam18.=;
               |SI FSalida = 0;
                    nConcepto = #cretam18#0;
               |FINSI;
          |FINSI;

          aEtiqueta = "BaseLRLC";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               nBase = aValor;
               nBase = nBase / 100;
          |FINSI;

          aEtiqueta = "ImporteLRLC";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               nImporte = aValor;
               nImporte = nImporte / 100;
          |FINSI;

          aEtiqueta = "LineaRLC";
          |HAZ BuscaEtiFin;
          |SI nLoc = 1;
               || ya tengo los datos para guardar la linea (el cretam37)

               |DDEFECTO #cretam37;
               #cretam37#0 = nEmp;
               #cretam37#1 = nAnyDesde;
               #cretam37#2 = nMesDesde;
               #cretam37#3 = nTipo;
               #cretam37#4 = aCCC;
               #cretam37#5 = nConcepto;
               |LEE 000#cretam37.=;
               |SI FSalida ! 0;
                    || tiene que entrar siempre, no puede existir
                    #cretam37#6 = aDescripcion;
                    #cretam37#7 = nBase;
                    #cretam37#8 = nImporte;
                    #cretam37#9 = aSoloFichero;

                    |GRABA 020#cretam37.n;
                    |LIBERA #cretam37;
               |FINSI;

               |SAL;
          |FINSI;
     |SIGUE;
|FPRC;

|PROCESO DatosCuerpoRLC;
     FLinea = 0;

     |PARA;
     |SI FLinea ] 0;
     |HACIENDO;
          FLinea = 0;
          |HAZ LeeLinea;

          aEtiqueta = "LineaRLC";
          |HAZ BuscaEtiIni;
          |SI nLoc = 1;
               |HAZ LineaRLC;
          |FINSI;

          aEtiqueta = "DatosCuerpoRLC";
          |HAZ BuscaEtiFin;
          |SI nLoc = 1;
               |SAL;
          |FINSI;
     |SIGUE;
|FPRC;

|PROCESO DatosCabeceraRLC;
     || primero grabo ya mi cabecera (el cretam36)

     |HAZ BuscaPorCCC;

     #labempre#0 = nEmp;
     |LEE 000#labempre.=;

     |DDEFECTO #cretam36;
     #cretam36#0 = nEmp;
     #cretam36#1 = nAnyDesde;
     #cretam36#2 = nMesDesde;
     #cretam36#3 = nTipo;
     #cretam36#4 = aCCC;
     |LEE 000#cretam36.=;
     |SI FSalida ! 0;
          || tiene que entrar siempre
          #cretam36#6 = #labempre#2;
          #cretam36#11 = aTipo;
          #cretam36#14 = aTipoRLC;
          #cretam36#12 = fFechaRec;
          #cretam36#13 = aHoraRec;

          #cretam36#17 = aMesDesde;
          #cretam36#18 = aAnyDesde;
          #cretam36#19 = aMesHasta;
          #cretam36#20 = aAnyHasta;
          #cretam36#28 = aSoloFichero;
          |GRABA 020#cretam36.n;

          || la leo otra vez para dejarla leida y despues grabarla de nuevo

          #cretam36#0 = nEmp;
          #cretam36#1 = nAnyDesde;
          #cretam36#2 = nMesDesde;
          #cretam36#3 = nTipo;
          #cretam36#4 = aCCC;
          |LEE 000#cretam36.=;
     |FINSI;

     FLinea = 0;

     |PARA;
     |SI FLinea ] 0;
     |HACIENDO;
          FLinea = 0;
          |HAZ LeeLinea;

          aEtiqueta = "AlcanceCalculo";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               #cretam36#29 = aValor;
          |FINSI;

          aEtiqueta = "CuotaALiquidar";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               #cretam36#30 = aValor;
          |FINSI;

          aEtiqueta = "Autorizado";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               #cretam36#15 = aValor;
          |FINSI;

          aEtiqueta = "Envio";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               #cretam36#16 = aValor;
          |FINSI;

          aEtiqueta = "DescripcionTipoLiquidacion";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               #cretam36#21 = aValor;
          |FINSI;

          aEtiqueta = "RazonSocial";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               #cretam36#22 = aValor;
          |FINSI;

          aEtiqueta = "TipoIpf";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               #cretam36#23 = aValor;
          |FINSI;

          aEtiqueta = "NumeroIpf";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               #cretam36#24 = (aValor % -109);
          |FINSI;

          aEtiqueta = "EntidadAtEp";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               #cretam36#25 = aValor;
          |FINSI;

          aEtiqueta = "NumeroTrabajadores";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               #cretam36#26 = aValor;
          |FINSI;

          aEtiqueta = "ModalidadPago";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               #cretam36#31 = aValor;
          |FINSI;

          aEtiqueta = "NumeroLiquidacion";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               #cretam36#27 = aValor;
          |FINSI;

          aEtiqueta = "ReferenciaCI";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               #cretam36#32 = aValor;
          |FINSI;

          aEtiqueta = "FechaCI";
          |HAZ BuscaEtiIni;
          |SI nLoc = 1;
               aCampoFecha = "FechaCI";
               |HAZ TipoFecha;
               aAlfa = aDia + "." + aMes + "." + aAny;
               fFecha = aAlfa;
               #cretam36#33 = fFecha;
          |FINSI;

          aEtiqueta = "HoraCI";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               aAlfa = (aValor % 102) + ":" + (aValor % 302) + ":" + (aValor % 502);
               #cretam36#34 = aAlfa;
          |FINSI;

          aEtiqueta = "HuellaCI";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               #cretam36#35 = aValor;
          |FINSI;

          aEtiqueta = "PeriodoPagoPE";
          |HAZ BuscaEtiIni;
          |SI nLoc = 1;
               |HAZ LeeLinea;
               aEtiqueta = "PeriodoDesde";
               |HAZ BuscaEtiIni;
               |SI nLoc = 1;
                    aCampoFecha = "PeriodoDesde";
                    |HAZ TipoFecha;
                    #cretam36#36 = aMes;
                    #cretam36#37 = aAny;
               |FINSI;

               |HAZ LeeLinea;
               aEtiqueta = "PeriodoHasta";
               |HAZ BuscaEtiIni;
               |SI nLoc = 1;
                    aCampoFecha = "PeriodoHasta";
                    |HAZ TipoFecha;
                    #cretam36#38 = aMes;
                    #cretam36#39 = aAny;
               |FINSI;
          |FINSI;

          aEtiqueta = "EmisoraPE";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               #cretam36#40 = aValor;
          |FINSI;

          aEtiqueta = "NumeroReferenciaPE";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               #cretam36#41 = aValor;
          |FINSI;

          aEtiqueta = "IdentificacionPE";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               #cretam36#42 = aValor;
          |FINSI;

          aEtiqueta = "ImportePE";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               nValor = aValor;
               #cretam36#43 = nValor / 100;
          |FINSI;

          aEtiqueta = "DatosCabeceraRLC";
          |HAZ BuscaEtiFin;
          |SI nLoc = 1;
               |GRABA 020#cretam36.a;
               |LIBERA #cretam36;
               |SAL;
          |FINSI;
     |SIGUE;
|FPRC;

|PROCESO Lectura;
     FLinea = 0;

     aAlfa = eaFichero;
     |XABRE "", aAlfa, nHandle; nHandle = FSalida;
     |SI FSalida ] 0;
          |PARA;
          |SI FLinea ] 0;
          |HACIENDO;
               FLinea = 0;
               |HAZ LeeLinea;

               aEtiqueta = "Ccc";
               |HAZ BuscaEtiIni;
               |SI nLoc = 1;
                    |HAZ CCC;
               |FINSI;

               aEtiqueta = "PeriodoDesde";
               |HAZ BuscaEtiIni;
               |SI nLoc = 1;
                    aCampoFecha = "PeriodoDesde";
                    |HAZ TipoFecha;
                    aMesDesde = aMes;
                    nMesDesde = aMes;
                    aAnyDesde = aAny;
                    nAnyDesde = aAny;
               |FINSI;

               aEtiqueta = "PeriodoHasta";
               |HAZ BuscaEtiIni;
               |SI nLoc = 1;
                    aCampoFecha = "PeriodoHasta";
                    |HAZ TipoFecha;
                    aMesHasta = aMes;
                    aAnyHasta = aAny;
               |FINSI;

               aEtiqueta = "Tipo";
               |HAZ BuscaEtiIniFin;
               |SI nLoc = 1;
                    aTipo = aValor;
                    |SI aAlfa = "L00";
                         nTipo = 00;
                    |FINSI;
                    |SI aAlfa = "L13";
                         nTipo = 02;
                    |FINSI;
                    |BUCLE Borra_cretam36;
                    |BUCLE Borra_cretam37;
                    |SELECT #1#cretam36;
                    |SELECT #1#cretam37;
               |FINSI;

               aEtiqueta = "FechaHoraRecaudacion";
               |HAZ BuscaEtiIni;
               |SI nLoc = 1;
                    |HAZ FechaHoraRec;
               |FINSI;

               aEtiqueta = "TipoRLC";
               |HAZ BuscaEtiIniFin;
               |SI nLoc = 1;
                    aTipoRLC = aValor;
               |FINSI;

               aEtiqueta = "DatosCabeceraRLC";
               |HAZ BuscaEtiIni;
               |SI nLoc = 1;
                    |HAZ DatosCabeceraRLC;
               |FINSI;

               aEtiqueta = "DatosCuerpoRLC";
               |HAZ BuscaEtiIni;
               |SI nLoc = 1;
                    |HAZ DatosCuerpoRLC;
               |FINSI;

               aEtiqueta = "DatosPiePaginaRLC";
               |HAZ BuscaEtiIni;
               |SI nLoc = 1;
                    |HAZ DatosPiePaginaRLC;
               |FINSI;

               aEtiqueta = "ReciboLiquidacionCotizaciones";
               |HAZ BuscaEtiFin;
               |SI nLoc = 1;
                    |SAL;
               |FINSI;
          |SIGUE;
     |FINSI;

     |XCIERRA nHandle;
|FPRC;

|PROGRAMA;
     |ABRE #cretam36;
     |ABRE #cretam37;

     |ABRE #cretam18;

     |ABRE #labempre;
     |ABRE #labcentr;
     |ABRE #nomcenes;
     |ABRE #labtraba;

     |HAZ Lectura;

     |CIERRA #cretam37;
     |CIERRA #cretam36;

     |CIERRA #cretam18;

     |CIERRA #labempre;
     |CIERRA #labcentr;
     |CIERRA #nomcenes;
     |CIERRA #labtraba;
|FPRO;
