|FICHEROS;
     cretap02 #0;

     cretam00;
     cretam01;
     cretam02;
     cretam03;
     cretam04;
     cretam05;

     cretae02;
     cretae03;

     cretat08;

     labempre;
     labtraba;

     tsilco14,MANTE;      || tmp. parrilla empresas
     tsilco15,MANTE;      || tmp. parrilla trabajadores

     tsilcon9,MANTE;      || tmp. centros

     silcon01;
|FIN;

|VARIABLES;
     nMarcarT = 0;
     sw_parrilla = 0;
     alfa1 = "";
     alfa2 = "";
     alfa3 = "";
     alfa4 = "";
     traba = 0;
     swHayAlguno = 0;
     swSelTra = 0;
     aAlfa = "";
     aAlfa0 = "";
     aAlfa1 = "";
     aAlfa2 = "";
     aAlfa3 = "";
     nContadorRef = 0;
     nNume = 0;

     aCadenaSal = "";
     aCadena = "";
     aLongitud = "";
     nLongitud = 0;
     nPos = 0;
     nBusca = 0;
     aCaracter = "";
     aBusca = "";
     aCambia = "";

     aHora = "";
     fFecha = @;
     aFecha = "";
     aFichero = "";
     &eaFichero = "";
     aRuta = "";

     nNivel = 0;
     swSinReturn = 0;
     nHandle = 0;

     aValor = "";
     nDesdeEmpresa = 0;
     nHastaEmpresa = 0;
     &enTipo = 0;

     nCampo = 0;
     nCampo1 = 0;
     nValor = 0;

     aOrigen = "";
     aDestino = "";
     aIP = "";

     &enMes = 0;
     &enAny = 0;
     &eaTipoXML = "";
     &eaReferencia = "";
     &efFecha = @;
     &eaHora = "";
     nAny = 0;
     nMes = 0;
     aCentro = "";
     aTrabajador = "";
     FEstado = 0;
     aLocalRemoto = "";
     swSigue = 0;
     nMesLiq = 0;
     nDesdeLinea = 0;
     nHastaLinea = 0;
     aDesdeNAF = "";
     aHastaNAF = "";
     nDesdeMes = 0;
     nHastaMes = 0;
     swAtrasos = 0;
     aAntCCC = "";
     swHeEntrado = 0;
     nAnyCot = 0;
|FIN;

/****************************************************************************/
/************************ PROCESOS DESDE LA PANTALLA ************************/
/****************************************************************************/

|PROCESO ActivaCampos;
     |SI 5 [ #0#5; |Y #0#5 [ 20;
          |CAMPO_MODIFICA #0#6, 1, "S";
          |CAMPO_VISUAL #0#6, 1, "S";
          #0#6 = #0#1;
          |PINTA 0950, "-    ³";
          |PINTA #0#6;

          |CAMPO_MODIFICA #0#7, 1, "S";
          |CAMPO_VISUAL #0#7, 1, "S";
          |PINTA 1024, "³ A¤o lote XML ......:         ³";
          #0#7 = #0#0;
          |PINTA #0#7;
          |PINTA 1124, "ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´";
     |SINO;
          |CAMPO_MODIFICA #0#6, 1, "N";
          |CAMPO_VISUAL #0#6, 1, "N";
          |PINTA 0950, "     ³";

          |CAMPO_MODIFICA #0#7, 1, "N";
          |CAMPO_VISUAL #0#7, 1, "N";
          |PINTA 1024, "³                              ³";
          |PINTA 1124, "ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´";
     |FINSI;
|FPRC;

|PROCESO ActivaAntes; |TIPO 7;
     |HAZ ActivaCampos;
|FPRC;

|PROCESO Restaura;
     |POPV;
|FPRC;

|PROCESO AyudaTipoLiq; |TIPO 7;
     |PUSHV 07571380;
     |ATRI R;
     ||            5  6         7
     ||            78901234567890123456789
     |PINTA 0757, "       - Opciones -    ";
     |PINTA 0857, " 00 :Normal            ";
     |PINTA 0957, " 02 :Finiquitos        ";
     |PINTA 1057, "5-20:Atrasos           ";
     |PINTA 1157, " 98 :Salarios de       ";
     |PINTA 1257, "     tramitaci¢n       ";
     |ATRI 0;
|FPRC;

|PROCESO AyudaTipos; |TIPO 7;
     |PUSHV 06531380;
     |ATRI R;
     |BLANCO 06531380;
                || 5      6         7
                || 3456789012345678901234567890
     |PINTA 0653, "Opciones";
     |PINTA 0753, "(N) Normal: liquidaciones   ";
     |PINTA 0853, "    que a£n no han sido en- ";
     |PINTA 0953, "    viadas ni confirmadas.  ";
     |PINTA 1053, "(R) Rectificaciones: para   ";
     |PINTA 1153, "    enviar rectificaciones  ";
     |PINTA 1253, "    de las liquidaciones ya ";
     |PINTA 1353, "    confirmadas.            ";

     |ATRI 0;
|FPRC;

|PROCESO MensajeTodasRec;
     aAlfa = "    ATENCION. Ha seleccionado la creación de un fichero de ";
     aAlfa = aAlfa + "RECTIFICACION de todos los CCC";
     |MENAV aAlfa;

     aAlfa = "    N¿Confirma la generación del fichero de RECTIFICACION masivo?";
     |CONFI aAlfa;
|FPRC;

|PROCESO CompRect;
     |SI #0#4 = "R"; |Y #0#2 = "N";
          |HAZ MensajeTodasRec;
          |SI FSalida ! 0;
               |ERROR;
          |FINSI;
     |FINSI;
|FPRC;

/****************************************************************************/
/*********** PROCESOS PARA LA SELECCION DE EMPRESAS Y TRABAJADORES **********/
/****************************************************************************/

|PROCESO cretam03_Busca;
     swHayAlguno = 1;
|FPRC;

|DEFBUCLE cretam03_Busca;
     #cretam03#1;
     ;
     #cretam01#0, #0#0, #cretam01#2, #0#5, "           ", "            ";
     #cretam01#0, #0#0, #cretam01#2, #0#5, "zzzzzzzzzzz", "zzzzzzzzzzzz";
     ;
     NULL, cretam03_Busca;
|FIN;

|PROCESO reenvio1;
     swHayAlguno = 0;    || a ver si hay algun trabajador dentro de esta
                         || empresa
     |BUCLE cretam03_Busca;

     |SI swHayAlguno = 1;
          #labempre#0 = #cretam01#0;
          |LEE 000#labempre.=;

          |DDEFECTO #tsilco14;
          #tsilco14#0 = #cretam01#0;
          #tsilco14#1 = #labempre#2;
          #tsilco14#2 = " ";
          |LEE 101#tsilco14.=;
          |SI FSalida ! 0;
               |GRABA 020#tsilco14.n;
          |FINSI;
          |LIBERA #tsilco14;
     |FINSI;
|FPRC;

|DEFBUCLE reenvio;
     #cretam01#1;
     ;
     00001, #0#0, #0#1, #0#5;
     99999, #0#0, #0#6, #0#5;
     ;
     NULL, reenvio1;
|FIN;

|PROCESO min14;
    #tsilco14#0 = 00001;
|FPRC;

|PROCESO max14;
    #tsilco14#0 = 99999;
|FPRC;

|PROCESO parrilla; |TIPO 0;
     |SI #0#2 = "S";
          |ATRI P;   |MENSA "    Cargando seleccion ...";    |ATRI 0;

          |ABRE #labempre;
          |ABRE #tsilco14;
          |ABRE #tsilco15;
          |ABRE #tsilcon9;

          |LEE 000#tsilco14.p;

          |SI FSalida ! 0;

               || para que coja bien el bucle:

               |SI 05 [ #0#5; |Y #0#5 [ 20;
                    || atrasos
               |SINO;
                    #0#6 = #0#1;
               |FINSI;

               |BUCLE reenvio;
          |FINSI;

          |CIERRA #tsilcon9;
          |CIERRA #labempre;
          |CIERRA #tsilco15;
          |CIERRA #tsilco14;
          |ATRI R;
          |BLIN 4;
          |ATRI 0;
          sw_parrilla = 1;

          |PUSHV 0501 2380;
          |ENTLINEAL #1#tsilco14, 1, 4, 22, 1, min14, max14;
          |POPV;
     |FINSI;
|FPRC;

|PROCESO sustituye;
     || aCadena es la cadena a buscar un caracter
     || aBusca es el caracter a buscar
     || aCambia es por el que lo queremos cambiar

     aCadenaSal = "";
     aLongitud = aCadena % 0;
     nLongitud = aLongitud;
     |PARA nPos = 1; |SI nPos [ nLongitud; |HACIENDO nPos = nPos + 1;
          nBusca = (nPos * 100) + 1;
          aCaracter = aCadena % nBusca;
          |SI aCaracter = aBusca;
               aCadenaSal = aCadenaSal + aCambia;
          |SINO;
               aCadenaSal = aCadenaSal + aCaracter;
          |FINSI;
     |SIGUE;
|FPRC;

|PROCESO Defectos;
     fFecha = ~;
     aFecha = fFecha;
     aAlfa = aFecha % -104;
     nAny = aAlfa;
     aAlfa = aFecha % 402;
     nMes = aAlfa;

     nMes = nMes - 1;
     |SI nMes = 0;
          nMes = 12;
          nAny = nAny - 1;
     |FINSI;

     #0#0 = nAny;
     |PINTA #0#0;

     #0#1 = nMes;
     |PINTA #0#1;
|FPRC;

|PROCESO Fichero;
     |SI aHora = "";
          |HORA aHora;
     |FINSI;

     fFecha = ~;
     aFecha = fFecha;

     aAlfa1 = #0#1;
     |QBF aAlfa1;
     aAlfa1 = ("00" + aAlfa1) % -102;
     aAlfa2 = #0#0;

     eaHora = aHora;
     efFecha = fFecha;

     aHora = aHora - ":" - ":";
     aFecha = aFecha - "." - ".";
     aFichero = "BASES" + "_" + aAlfa2 + aAlfa1 + "_" + aFecha + "_" + aHora;
     |SI #0#5 = 00;
          aFichero = aFichero + "_NOM";
     |FINSI;
     |SI #0#5 = 02;
          aFichero = aFichero + "_FIN";
     |FINSI;
     |SI #0#5 ] 05; |Y #0#5 [ 20;
          aAlfa1 = #0#7;
          aAlfa2 = #0#1; |QBF aAlfa2; aAlfa2 = ("00" + aAlfa2) % -102;
          aAlfa3 = #0#6; |QBF aAlfa3; aAlfa3 = ("00" + aAlfa3) % -102;
          aFichero = aFichero + "_ATR" + aAlfa1 + aAlfa2 + aAlfa3;
     |FINSI;
     |SI #0#5 = 98;
          aFichero = aFichero + "_SAL";
     |FINSI;
     aFichero = aFichero + ".xml"
     eaFichero = aFichero;

     #0#3 = aFichero;
     |PINTA #0#3;
|FPRC;

|PROCESO FicheroAntes; |TIPO 7;
     |HAZ Fichero;
|FPRC;

|PROCESO Ruta;
     aCadena = #cretam00#2;
     aBusca = "/";
     aCambia = "\";
     |HAZ sustituye;
     aRuta = aCadenaSal;

     |QBF aRuta;
     aAlfa = aRuta % -101;
     |SI aAlfa ! "\";
          aRuta = aRuta + "\";
     |FINSI;
     aRuta = aRuta + "BASES\";

     |RMKDIR aRuta;

     aFichero = #0#3;
     |QBF aFichero;
     aAlfa = aFichero % -104;
     |SI aAlfa ! ".xml";
          aFichero = aFichero + ".xml";
     |FINSI;

     aRuta = aRuta + aFichero;
|FPRC;

|PROCESO Linea;
     |QBF aAlfa;
     aCadena = aAlfa;
     |HAZ QuitaRaros;
     |SI nNivel = 1;
          aCadena = aCadena;
     |FINSI;
     |SI nNivel = 2;
          || aCadena = &09 + aCadena;
          aCadena = "  " + aCadena;
     |FINSI;
     |SI nNivel = 3;
          aCadena = "  " + "  " + aCadena;
     |FINSI;
     |SI nNivel = 4;
          aCadena = "  " + "  " + "  " + aCadena;
     |FINSI;
     |SI nNivel = 5;
          aCadena = "  " + "  " + "  " + "  " + aCadena;
     |FINSI;
     |SI nNivel = 6;
          aCadena = "  " + "  " + "  " + "  " + "  " + aCadena;
     |FINSI;
     |SI nNivel = 7;
          aCadena = "  " + "  " + "  " + "  " + "  " + "  " + aCadena;
     |FINSI;
     |SI nNivel = 8;
          aCadena = "  " + "  " + "  " + "  " + "  " + "  " + "  " + aCadena;
     |FINSI;
     |SI nNivel = 9;
          aCadena = "  " + "  " + "  " + "  " + "  " + "  " + "  " + "  " + aCadena;
     |FINSI;
     |SI nNivel = 10;
          aCadena = "  " + "  " + "  " + "  " + "  " + "  " + "  " + "  " + "  " + aCadena;
     |FINSI;
     |SI swSinReturn = 0;
          aCadena = aCadena + &13 + &10;
     |FINSI;
     aAlfa = aCadena;
     |XGRABA nHandle, aAlfa;
|FPRC;

|PROCESO Horas;
     |PARA nCampo = 60; |SI nCampo [ 69; |HACIENDO nCampo = nCampo + 2;
          |SI #cretam04#nCampo# ! 00;
               nCampo1 = nCampo + 1;

               nNivel = 9;
               aAlfa = "<Dato>";
               |HAZ Linea;

               nNivel = 10;

               aAlfa = "<TipoDato>" + "H" + "</TipoDato>";
               |HAZ Linea;

               aValor = #cretam04#nCampo#;
               |QBF aValor;
               aValor = ("00" + aValor) % -102;
               aAlfa = "<Codigo>" + aValor + "</Codigo>";
               |HAZ Linea;

               aValor = #cretam04#nCampo1#;
               aAlfa = "<Valor>" + aValor + "</Valor>";
               |HAZ Linea;

               nNivel = 9;
               aAlfa = "</Dato>";
               |HAZ Linea;
          |FINSI;
     |SIGUE;
|FPRC;

|PROCESO Indicadores;
     |PARA nCampo = 50; |SI nCampo [ 59; |HACIENDO nCampo = nCampo + 2;
          |SI #cretam04#nCampo# ! 00;
               nCampo1 = nCampo + 1;

               nNivel = 9;
               aAlfa = "<Dato>";
               |HAZ Linea;

               nNivel = 10;

               aAlfa = "<TipoDato>" + "I" + "</TipoDato>";
               |HAZ Linea;

               aValor = #cretam04#nCampo#;
               aAlfa = "<Codigo>" + aValor + "</Codigo>";
               |HAZ Linea;

               aValor = #cretam04#nCampo1#;
               aAlfa = "<Valor>" + aValor + "</Valor>";
               |HAZ Linea;

               nNivel = 9;
               aAlfa = "</Dato>";
               |HAZ Linea;
          |FINSI;
     |SIGUE;
|FPRC;

|PROCESO Conceptos;
     || contratos 421 o 422, formacion o becarios
     || NO HAY QUE INCLUIR LAS BASES, solo las horas formacion
     || si las hubiera
     || Y tambien las PRESTACIONES (se supone) esta por comprobar

     |PARA nCampo = 30; |SI nCampo [ 48; |HACIENDO nCampo = nCampo + 2;
          swSigue = 0;

          |SI #cretam04#73 = 421; |O #cretam04#73 = 422;
               || los contratos de formacion solo se crean tramos en caso
               || de ITs con prestaciones, es decir cuando haya algo que
               || comunicar que no sean solo las bases
               || en IT sin prestacion (ni horas claro) no hay que pintar
               || nada, ya que la base ya la saben
               |SI #cretam04#nCampo# = 563; |O #cretam04#nCampo# = 663;
                    swSigue = 1;
               |FINSI;
          |SINO;
               || para el resto de contratos
               |SI #cretam04#nCampo# ! 000;
                    swSigue = 1;
               |FINSI;
          |FINSI;

          |SI swSigue = 1;
               nCampo1 = nCampo + 1;

               nNivel = 9;
               aAlfa = "<Dato>";
               |HAZ Linea;

               nNivel = 10;

               aAlfa = "<TipoDato>" + "C" + "</TipoDato>";
               |HAZ Linea;

               aValor = #cretam04#nCampo#;
               aAlfa = "<Codigo>" + aValor + "</Codigo>";
               |HAZ Linea;

               nValor = #cretam04#nCampo1#;
               nValor = nValor * 100;
               aValor = nValor;
               aAlfa = "<Valor>" + aValor + "</Valor>";
               |HAZ Linea;

               nNivel = 9;
               aAlfa = "</Dato>";
               |HAZ Linea;
          |FINSI;
     |SIGUE;
|FPRC;

|PROCESO RegistroNAFSS;
     || PRIMERO ACTUALIZO EL cretae02 (por CCC) con el centro

     |DDEFECTO #cretae02;
     |DDEFECTO #cretae03;

     |ABRE #cretae02;
     #cretae02#0 = #cretam04#0;
     #cretae02#1 = #cretam04#1;
     #cretae02#2 = #cretam04#2;
     #cretae02#3 = #cretam04#3;
     #cretae02#4 = #cretam04#4;
     #cretae02#6 = eaReferencia;
     |LEE 101#cretae02.=;
     |SI FSalida = 0;
          || ha de existir

          #labtraba#0 = #cretam04#0;
          #labtraba#1 = #cretam04#7;
          |LEE 000#labtraba.=;
          aCentro = #labtraba#77;
          |QBF aCentro;
          aCentro = ("00" + aCentro) % -102;

          aAlfa = #cretae02#9;
          |QBF aAlfa;

          |SI aAlfa = "";
               #cretae02#9 = aCentro;
               |GRABA 020#cretae02.a;
          |SINO;
               aAlfa1 = aAlfa - aCentro;
               |SI aAlfa = aAlfa1;
                    aAlfa = #cretae02#9;
                    |QBF aAlfa;

                    aAlfa = aAlfa + " " + aCentro;
                    #cretae02#9 = aAlfa;
                    |GRABA 020#cretae02.a;
               |FINSI;
          |FINSI;
     |FINSI;

     |LIBERA #cretae02;
     |CIERRA #cretae02;

     || DESPUES ACTUALIZO O CREO EL cretae03 (por NAFSS)

     |ABRE #cretae03;
     #cretae03#0 = #cretam04#0;
     #cretae03#1 = #cretam04#1;
     #cretae03#2 = #cretam04#2;
     #cretae03#3 = #cretam04#3;
     #cretae03#4 = #cretam04#4;
     #cretae03#5 = #cretam04#5;
     #cretae03#6 = eaReferencia;
     |LEE 101#cretae03.=;
     FEstado = FSalida;

     #labtraba#0 = #cretam04#0;
     #labtraba#1 = #cretam04#7;
     |LEE 000#labtraba.=;
     aCentro = #labtraba#77;
     |QBF aCentro;
     aCentro = ("00" + aCentro) % -102;

     #cretae03#9 = aCentro;
     |QBF aAlfa;

     aTrabajador = #cretam04#7;
     |QBF aTrabajador;
     aTrabajador = ("00000" + aTrabajador) % -105;

     aAlfa = #cretae03#10;
     |QBF aAlfa;

     |SI aAlfa = "";
          #cretae03#10 = aTrabajador;
     |SINO;
          aAlfa1 = aAlfa - aTrabajador;
          |SI aAlfa = aAlfa1;
               aAlfa = #cretae03#10;
               |QBF aAlfa;

               aAlfa = aAlfa + " " + aTrabajador;
               #cretae03#10 = aAlfa;
          |FINSI;
     |FINSI;

     #cretae03#7 = "B";
     #cretae03#11 = #0#3;
     #cretae03#12 = efFecha;
     #cretae03#13 = (eaHora % 102) + ":" + (eaHora % 302);
     |SI swAtrasos = 1;
          #cretae03#17 = #0#1;
          #cretae03#18 = #0#6;
          #cretae03#19 = #0#7;
     |FINSI;

     |SI FEstado = 0;
          |GRABA 020#cretae03.a;
     |SINO;
          |GRABA 020#cretae03.n;
     |FINSI;

     |LIBERA #cretae03;
     |CIERRA #cretae03;

     || AHORA POR ULTIMO LOS PROPIOS MANTENIMIENTOS DE LAS BASES
     || GENERADAS, ASI TENGO MARCADO QUE EL FICHERO CON ESTE TRAB.
     || SE HA GENERADO EN EL PROPIO MANTENIMIENTO DE BASES

     #cretam03#0 = #cretam04#0;
     #cretam03#1 = #cretam04#1;
     #cretam03#2 = #cretam04#2;
     #cretam03#3 = #cretam04#3;
     #cretam03#4 = #cretam04#4;
     #cretam03#5 = #cretam04#5;
     |LEE 101#cretam03.=;
     |SI FSalida = 0;
          #cretam03#13 = "G";
          #cretam03#14 = eaReferencia;
          |SI swAtrasos = 1;
               #cretam03#17 = #0#1;
               #cretam03#18 = #0#6;
               #cretam03#19 = #0#7;
          |FINSI;
          |GRABA 020#cretam03.a;
          |LIBERA #cretam03;
     |FINSI;
|FPRC;

|PROCESO CompruebaTramo;
     || si tengo prestacion IT tengo que hacer el tramo
     |PARA nCampo = 30; |SI nCampo [ 38; |HACIENDO nCampo = nCampo + 2;
          |SI #cretam04#nCampo# ! 000;
               swSigue = 1;
               |ACABA;
          |FINSI;
     |SIGUE;
     |PARA nCampo = 50; |SI nCampo [ 58; |HACIENDO nCampo = nCampo + 2;
          |SI #cretam04#nCampo# = 51;
               swSigue = 1;
               |ACABA;
          |FINSI;
     |SIGUE;
     |PARA nCampo = 60; |SI nCampo [ 68; |HACIENDO nCampo = nCampo + 2;
          |SI #cretam04#nCampo# = 03; |O #cretam04#nCampo# = 04;
               swSigue = 1;
               |ACABA;
          |FINSI;
     |SIGUE;
|FPRC;

|PROCESO FormacionIT;
     || si tengo horas tengo que hacer el tramo
     |SI enTipo = 00;
          |PARA nCampo = 60; |SI nCampo [ 68; |HACIENDO nCampo = nCampo + 2;
               |SI #cretam04#nCampo# = 03; |O #cretam04#nCampo# = 04;
                    swSigue = 1;
                    |ACABA;
               |FINSI;
          |SIGUE;
          || si tengo prestacion IT tengo que hacer el tramo
          |PARA nCampo = 30; |SI nCampo [ 38; |HACIENDO nCampo = nCampo + 2;
               |SI #cretam04#nCampo# = 563; |O #cretam04#nCampo# = 663;
                    swSigue = 1;
                    |ACABA;
               |FINSI;
          |SIGUE;
     |FINSI;
|FPRC;

|PROCESO cretam04;
     |SI enTipo = 02;
          |SI #cretam04#73 = 421; |O #cretam04#73 = 422;
               |SI #cretam04#6 ! 99;
                    |ACABA;
               |FINSI;
          |FINSI;
     |FINSI;

     || trabajadores con contrato 421 o 422 que no tengan horas formativas
     || o que esten en IT y que no tengan prestacion: no tengo que hacer tramo

     |SI #cretam04#73 = 421; |O #cretam04#73 = 422;
          swSigue = 0;
          |HAZ FormacionIT;
          |SI swSigue = 0;
               |ACABA;
          |FINSI;
     |FINSI;

     || esto para comprobar que un tramo SIN BASES, salvo que lleve el
     || indicativo 51-M o que tenga prestacion, NO HAY QUE INCLUIRLO
     || en el xml

     |SI enTipo = 00; |O swAtrasos = 1;
          swSigue = 0;
          |HAZ CompruebaTramo;
          |SI swSigue = 0;
               |ACABA;
          |FINSI;
     |FINSI;

     |HAZ RegistroNAFSS;

     || cada registro de este fichero es un tramo distinto

     nNivel = 7;
     aAlfa = "<Tramo>";
     |HAZ Linea;

     || DESDE FECHA

     nNivel = 8;
     aAlfa = "<FechaDesde>";
     |HAZ Linea;

     nNivel = 9;
     aFecha = #cretam04#8;

     aValor = aFecha % 102;
     aAlfa = "<Dia>" + aValor + "</Dia>";
     |HAZ Linea;

     aValor = aFecha % 402;
     aAlfa = "<Mes>" + aValor + "</Mes>";
     |HAZ Linea;

     |SI swAtrasos = 1;
          aValor = #0#7;
     |SINO;
          aValor = aFecha % -104;
     |FINSI;
     aAlfa = "<Anho>" + aValor + "</Anho>";
     |HAZ Linea;

     nNivel = 8;
     aAlfa = "</FechaDesde>";
     |HAZ Linea;

     || HASTA FECHA

     nNivel = 8;
     aAlfa = "<FechaHasta>";
     |HAZ Linea;

     nNivel = 9;
     aFecha = #cretam04#9;

     aValor = aFecha % 102;
     aAlfa = "<Dia>" + aValor + "</Dia>";
     |HAZ Linea;

     aValor = aFecha % 402;
     aAlfa = "<Mes>" + aValor + "</Mes>";
     |HAZ Linea;

     |SI swAtrasos = 1;
          aValor = #0#7;
     |SINO;
          aValor = aFecha % -104;
     |FINSI;
     aAlfa = "<Anho>" + aValor + "</Anho>";
     |HAZ Linea;

     nNivel = 8;
     aAlfa = "</FechaHasta>";
     |HAZ Linea;

     || DATOS DEL TRAMO

     nNivel = 8;
     aAlfa = "<DatosTramo>";
     |HAZ Linea;

     |HAZ Conceptos;
     |HAZ Indicadores;
     |HAZ Horas;

     nNivel = 8;
     aAlfa = "</DatosTramo>";
     |HAZ Linea;

     nNivel = 7;
     aAlfa = "</Tramo>";
     |HAZ Linea;
|FPRC;

|DEFBUCLE cretam04;           || TRAMOS
     #cretam04#1;
     ;
     #cretam03#0, #cretam03#1, #cretam03#2, #cretam03#3, #cretam03#4, #cretam03#5, nDesdeLinea;
     #cretam03#0, #cretam03#1, #cretam03#2, #cretam03#3, #cretam03#4, #cretam03#5, nHastaLinea;
     ;
     NULL, cretam04;
|FIN;

|PROCESO cretam04_99;
     swSigue = 1;
     |ERROR10;
|FPRC;

|DEFBUCLE cretam04_99;
     #cretam04#1;
     ;
     #cretam02#0, #cretam02#1, #cretam02#2, #cretam02#3, #cretam02#4, aDesdeNAF, nDesdeLinea;
     #cretam02#0, #cretam02#1, #cretam02#2, #cretam02#3, #cretam02#4, aHastaNAF, nHastaLinea;
     ;
     NULL, cretam04_99;
|FIN;

|PROCESO cretam04_buscaFor;
     |SI enTipo = 00;
          |PARA nCampo = 60; |SI nCampo [ 68; |HACIENDO nCampo = nCampo + 2;
               |SI #cretam04#nCampo# = 03; |O #cretam04#nCampo# = 04;
                    swSigue = 1;
                    |SAL;
               |FINSI;
          |SIGUE;
          |PARA nCampo = 30; |SI nCampo [ 38; |HACIENDO nCampo = nCampo + 2;
               |SI #cretam04#nCampo# = 563; |O #cretam04#nCampo# = 663;
                    swSigue = 1;
                    |SAL;
               |FINSI;
          |SIGUE;
     |FINSI;
     |SI enTipo = 02;
          |SI #cretam04#6 = 99;
               swSigue = 1;
               ||SAL;    || me da SAL ilega, lo quito, da igual
          |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE cretam04_buscaFor2;
     #cretam04#1;
     ;
     #cretam03#0, #cretam03#1, #cretam03#2, #cretam03#3, #cretam03#4, #cretam03#5, 01;
     #cretam03#0, #cretam03#1, #cretam03#2, #cretam03#3, #cretam03#4, #cretam03#5, 99;
     ;
     NULL, cretam04_buscaFor;
|FIN;

|DEFBUCLE cretam04_buscaFor;
     #cretam04#1;
     ;
     #cretam02#0, #cretam02#1, #cretam02#2, #cretam02#3, #cretam02#4, "            ", 01;
     #cretam02#0, #cretam02#1, #cretam02#2, #cretam02#3, #cretam02#4, "zzzzzzzzzzzz", 99;
     ;
     NULL, cretam04_buscaFor;
|FIN;

|PROCESO cretam03;
     |SI #0#2 = "S"; |Y #tsilco14#3 = "*";
          || hay seleccion de empresas, y dentro de esta he seleccionado
          || trabajadores

          |SELECT #1#tsilco15;
          #tsilco15#0 = #cretam03#0;
          #tsilco15#1 = #cretam03#11;
          |LEE 000#tsilco15.=;
          |SI #tsilco15#7 ! "*";
               |ACABA;
          |FINSI;
     |FINSI;

     || En CCCs de formacion, los trabajadores que no tengan horas NO tienen
     || que salir cuando es tipo 00
     || cuando es tipo 02 (finiquitos) no tienen que salir los que no tengan
     || linea 99 con dias de vacaciones del mes siguiente

     |SI #cretam02#11 = "S";
          swSigue = 0;
          |BUCLE cretam04_buscaFor2;
          |SI swSigue = 0;
               |ACABA;
          |FINSI;
     |FINSI;

     |SI #0#5 = 02;
          |SI nMesLiq = 0;
               || estoy en un L13 del mes siguiente al de la liquidacion, busco
               || primero que existan lineas 99

               swSigue = 0;
               aDesdeNAF = #cretam03#5;
               aHastaNAF = #cretam03#5;
               nDesdeLinea = 01;
               nHastaLinea = 31;
               |BUCLE cretam04_99;
               |SI swSigue = 0;
                    |ACABA;
               |FINSI;
          |FINSI;

          |SI nMesLiq = 1;
               || estoy en un L13 del mes siguiente al dde la liquidacion, busco
               || primero que existan lineas 99

               swSigue = 0;
               aDesdeNAF = #cretam03#5;
               aHastaNAF = #cretam03#5;
               nDesdeLinea = 99;
               nHastaLinea = 99;
               |BUCLE cretam04_99;
               |SI swSigue = 0;
                    |ACABA;
               |FINSI;
          |FINSI;
     |FINSI;

     nNivel = 5;
     aAlfa = "<Trabajador>";
     |HAZ Linea;

     aAlfa1 = #cretam03#0;
     |QBF aAlfa1;
     aAlfa1 = ("00000" + aAlfa1) % -105;

     aAlfa2 = #cretam03#11;
     |QBF aAlfa2;
     aAlfa2 = ("00000" + aAlfa2) % -105;

     aAlfa = aAlfa1 + "." + aAlfa2 + " " + #cretam03#9;
     aAlfa = "<!-- " + aAlfa + " -->";
     |HAZ Linea;

     nNivel = 6;
     aValor = #cretam03#5;
     aAlfa = "<Naf>" + aValor + "</Naf>";
     |HAZ Linea;

     aAlfa = "<Tramos>";
     |HAZ Linea;

     |SI nMesLiq = 0;
          nDesdeLinea = 01;
          nHastaLinea = 31;
     |FINSI;
     |SI nMesLiq = 1;         || esto evidentemente solo pasara en los L13
          nDesdeLinea = 99;   || que tengan trabajadores con dias en el mes
          nHastaLinea = 99;   || siguiente al de la liquidacion
     |FINSI;

     |BUCLE cretam04;

     nNivel = 6;
     aAlfa = "</Tramos>";
     |HAZ Linea;

     nNivel = 5;
     aAlfa = "</Trabajador>";
     |HAZ Linea;
|FPRC;

|DEFBUCLE cretam03;           || NAFSS
     #cretam03#1;
     ;
     #cretam02#0, #cretam02#1, #cretam02#2, #cretam02#3, #cretam02#4, "            ";
     #cretam02#0, #cretam02#1, #cretam02#2, #cretam02#3, #cretam02#4, "zzzzzzzzzzzz";
     ;
     NULL, cretam03;
|FIN;

|PROCESO RegistroCCC;
     |DDEFECTO #cretae02;

     |ABRE #cretae02;
     #cretae02#0 = #cretam02#0;
     #cretae02#1 = #cretam02#1;
     #cretae02#2 = #cretam02#2;
     #cretae02#3 = #cretam02#3;
     #cretae02#4 = #cretam02#4;
     #cretae02#6 = eaReferencia;
     |LEE 101#cretae02.=;
     FEstado = FSalida;

     #cretae02#7 = "B";
     #cretae02#11 = #0#3;
     #cretae02#12 = efFecha;
     #cretae02#13 = (eaHora % 102) + ":" + (eaHora % 302);

     |SI swAtrasos = 1;
          #cretae02#17 = #0#1;
          #cretae02#18 = #0#6;
          #cretae02#19 = #0#7;
     |FINSI;

     |SI FEstado = 0;
          |GRABA 020#cretae02.a;
     |SINO;
          |GRABA 020#cretae02.n;
     |FINSI;

     |LIBERA #cretae02;
     |CIERRA #cretae02;

     #cretam02#0 = #cretae02#0;
     #cretam02#1 = #cretae02#1;
     #cretam02#2 = #cretae02#2;
     #cretam02#3 = #cretae02#3;
     #cretam02#4 = #cretae02#4;
     |LEE 101#cretam02.=;
     |SI FSalida = 0;
          #cretam02#7 = "G";
          #cretam02#8 = eaReferencia;
          |SI swAtrasos = 1;
               #cretam02#17 = #0#1;
               #cretam02#18 = #0#6;
               #cretam02#19 = #0#7;
          |FINSI;
          |GRABA 020#cretam02.a;
          |LIBERA 020#cretam02;
     |FINSI;
|FPRC;

|PROCESO LiquidacionMes;
     |SI #0#5 = 02;
          || en los L13 tengo que comprobar primero si hay registros, antes de
          || crear la etiqueta del mes liquidativo

          || primero de los del mes de la liquidacion
          |SI nMesLiq = 0;
               swSigue = 0;
               aDesdeNAF = "            ";
               aHastaNAF = "zzzzzzzzzzzz";
               nDesdeLinea = 01;
               nHastaLinea = 31;
               |BUCLE cretam04_99;
               |SI swSigue = 0;
                    |ACABA;
               |FINSI;
          |FINSI;

          || y luego de los dias del mes siguiente que hay que cotizar
          |SI nMesLiq = 1;
               swSigue = 0;
               aDesdeNAF = "            ";
               aHastaNAF = "zzzzzzzzzzzz";
               nDesdeLinea = 99;
               nHastaLinea = 99;
               |BUCLE cretam04_99;
               |SI swSigue = 0;
                    |ACABA;
               |FINSI;
          |FINSI;
     |FINSI;

     nNivel = 3;
     aAlfa = "<LiquidacionMes>";
     |HAZ Linea;

     nNivel = 4;
     aAlfa = "<MesLiquidativo>";
     |HAZ Linea;

     nNivel = 5;

     nNume = #cretam02#2;
     |SI nMesLiq = 1;
          nNume = #cretam02#2 + 1;
          |SI nNume = 13;
               nNume = 1;
          |FINSI;
     |FINSI;

     aValor = nNume;
     |QBF aValor;
     aValor = ("00" + aValor) % -102;
     aAlfa = "<Mes>" + aValor + "</Mes>";
     |HAZ Linea;

     nNivel = 5;
     nNume = #0#0;
     |SI nMesLiq = 1;
          |SI #cretam02#2 = 12;
               nNume = #0#0 + 1;
          |FINSI;
     |FINSI;

     |SI swAtrasos = 1;
          aValor = #0#7;
     |SINO;
          aValor = nNume;
     |FINSI;

     |QBF aValor;
     aAlfa = "<Anho>" + aValor + "</Anho>";
     |HAZ Linea;

     nNivel = 4;
     aAlfa = "</MesLiquidativo>";
     |HAZ Linea;

     || aqui empieza por trabajador (por NAFSS)

     nNivel = 4;
     aAlfa = "<Trabajadores>";
     |HAZ Linea;

     |BUCLE cretam03;

     nNivel = 4;
     aAlfa = "</Trabajadores>";
     |HAZ Linea;

     nNivel = 3;
     aAlfa = "</LiquidacionMes>";
     |HAZ Linea;
|FPRC;

|PROCESO DatosLiquidacion;
     nNivel = 3;
     aAlfa = "<DatosLiquidacion>";
     |HAZ Linea;

     nNivel = 4;
     aAlfa = "<Dato>";
     |HAZ Linea;

     nNivel = 5;
     aAlfa = "<TipoDato>" + "C" + "</TipoDato>";
     |HAZ Linea;

     aAlfa = "<Codigo>" + "763" + "</Codigo>";
     |HAZ Linea;

     nNume = #cretam02#15 * 100;
     aValor = nNume;
     aAlfa = "<Valor>" + aValor + "</Valor>";
     |HAZ Linea;

     nNivel = 4;
     aAlfa = "</Dato>";
     |HAZ Linea;

     nNivel = 3;
     aAlfa = "</DatosLiquidacion>";
     |HAZ Linea;
|FPRC;

|PROCESO CabeceraLiquidacion;

     || en CCC de formacion tengo que comprobar si hay algun trabajador
     || con HORAS de formacion para ver si tengo que incluir este CCC
     || en el fichero de BASES o no

     || si es en finiquitos, busco que haya linea 99
     || si no hay, los de formacion no tienen que salir

     |SI #cretam02#11 = "S";
          swSigue = 0;
          |BUCLE cretam04_buscaFor;
          |SI swSigue = 0;
               #cretat08#0 = #cretam02#0;
               #cretat08#1 = #cretam02#4;
               |LEE 101#cretat08.=;
               FEstado = FSalida;

               #cretat08#2 = #labempre#2;
               aAlfa = #cretam02#5;
               |QBF aAlfa;
               aAlfa = ("00" + aAlfa) % 102;
               #cretat08#3 = aAlfa;
               |SI FEstado = 0;
                    |GRABA 020#cretat08.a;
               |SINO;
                    |GRABA 020#cretat08.n;
               |FINSI;

               |ACABA;
          |FINSI;
     |FINSI;

     nNivel = 2;
     aAlfa = "<Liquidacion>";
     |HAZ Linea;

     swHeEntrado = 1;

     #labempre#0 = #cretam02#0;
     |LEE 000#labempre.=;
     aAlfa = #cretam02#0;
     |QBF aAlfa;
     aAlfa = ("00000" + aAlfa) % -105;
     nNivel = 3;
     aAlfa = aAlfa + "  " + #labempre#2;
     aAlfa = "<!-- " + aAlfa + " -->";
     |HAZ Linea;

     aAlfa = #cretam02#5;
     |QBF aAlfa;
     aAlfa = ("00" + aAlfa) % -102;
     nNivel = 3;
     aAlfa = aAlfa + "  - " + #cretam02#6;
     aAlfa = "<!-- " + aAlfa + " -->";
     |HAZ Linea;

     nNivel = 3;
     aAlfa = "<Ccc>";
     |HAZ Linea;
     |HAZ RegistroCCC;

     nNivel = 4;
     aValor = #cretam02#4 % 0104;
     aAlfa = "<Regimen>" + aValor + "</Regimen>";
     |HAZ Linea;
     aValor = #cretam02#4 % 0502;
     aAlfa = "<Provincia>" + aValor + "</Provincia>";
     |HAZ Linea;
     aValor = #cretam02#4 % 0709;
     aAlfa = "<Numero>" + aValor + "</Numero>";
     |HAZ Linea;

     nNivel = 3;
     aAlfa = "</Ccc>";
     |HAZ Linea;

     nNivel = 3;
     aAlfa = "<PeriodoDesde>";
     |HAZ Linea;

     nNivel = 4;
     aValor = nDesdeMes;
     |QBF aValor;
     aValor = ("00" + aValor) % -102;
     aAlfa = "<Mes>" + aValor + "</Mes>";
     |HAZ Linea;

     nNivel = 4;
     |SI swAtrasos = 1;
          aValor = #0#7;
     |SINO;
          aValor = #0#0;
     |FINSI;
     |QBF aValor;
     aAlfa = "<Anho>" + aValor + "</Anho>";
     |HAZ Linea;

     nNivel = 3;
     aAlfa = "</PeriodoDesde>";
     |HAZ Linea;

     nNivel = 3;
     aAlfa = "<PeriodoHasta>";
     |HAZ Linea;

     nNivel = 4;
     aValor = nHastaMes;
     |QBF aValor;
     aValor = ("00" + aValor) % -102;
     aAlfa = "<Mes>" + aValor + "</Mes>";
     |HAZ Linea;

     nNivel = 4;
     |SI swAtrasos = 1;
          aValor = #0#7;
     |SINO;
          aValor = #0#0;
     |FINSI;
     |QBF aValor;
     aAlfa = "<Anho>" + aValor + "</Anho>";
     |HAZ Linea;

     nNivel = 3;
     aAlfa = "</PeriodoHasta>";
     |HAZ Linea;

     nNivel = 3;
     |SI enTipo = 00;
          aAlfa = "L00";
     |FINSI;
     |SI enTipo = 02;
          aAlfa = "L13";
     |FINSI;
     |SI swAtrasos = 1;
          aAlfa = "L03";
     |FINSI;
     aAlfa = "<Tipo>" + aAlfa + "</Tipo>";
     |HAZ Linea;

     |SI #cretam02#15 ! 0;
          || las bonificaciones de formacion continua, se meten en esta
          || etiqueta
          |HAZ DatosLiquidacion;
     |FINSI;

/*
     nNivel = 3;
     aAlfa = "<AceptarBasesAnteriores>" + "S" + "</AceptarBasesAnteriores>";
     |HAZ Linea;
*/

|FPRC;

|PROCESO Liquidacion;
     aAlfa = #cretam02#4;
     |QBF aAlfa;
     |SI aAlfa ! aAntCCC; |Y aAntCCC ! "";
          || Cierro liquidacion, si venia abierta
          nNivel = 2;
          aAlfa = "</Liquidacion>";
          |HAZ Linea;
     |FINSI;

     aAlfa = #cretam02#4;
     |QBF aAlfa;
     |SI aAlfa ! aAntCCC;
          || para que en atrasos solo la pinte una vez por CCC
          |HAZ CabeceraLiquidacion;
     |FINSI;

     |SI #0#5 = 00; |O #0#5 = 98; |O swAtrasos = 1;
          |HAZ LiquidacionMes;
     |FINSI;

     |SI #0#5 = 02;
          || paso dos veces, una vez para los dias de vacaciones a declarar
          || el mes de la liquidacion, y otra vez para los del mes siguiente

          |PARA nMesLiq = 0; |SI nMesLiq [ 1; |HACIENDO nMesLiq = nMesLiq + 1;
               |HAZ LiquidacionMes;
          |SIGUE;
     |FINSI;

     aAntCCC = #cretam02#4;
     |QBF aAntCCC;
|FPRC;

|PROCESO Cabecera;
     |ABRE #cretam00;
     |DDEFECTO #cretam00;
     |LEE 000#cretam00.p;
     |CIERRA #cretam00;

     |ABRE #silcon01;
     |DDEFECTO #silcon01;
     |LEE 000#silcon01.p;
     |CIERRA #silcon01;

     |XABRE "A", aOrigen, nHandle;
     |SI FSalida ] 0;
          nNivel = 1;
          aAlfa = "<?xml version=" + &34 + "1.0" + &34 + " encoding=" + &34 + "ISO-8859-1" + &34 + " ?>";
          |HAZ Linea;
          swSinReturn = 1;
          aAlfa = "<Bases xmlns:xsi=" + &34 + "http://www.w3.org/2001/XMLSchema-instance" +  &34;
          |HAZ Linea;
          aAlfa = " xmlns=" + &34 + "http://www.seg-social.es/creta/esquemas/V100/Bases" + &34;
          |HAZ Linea;
          aAlfa = " xsi:schemaLocation=" + &34 + "http://www.seg-social.es/creta/esquemas/V100/Bases Bases.xsd" + &34 + ">";
          swSinReturn = 0;
          |HAZ Linea;

          aValor = #silcon01#0;
          |QBF aValor;
          aValor = ("00000000" + aValor) % -108;
          nNivel = 2;
          aAlfa = "<Autorizado>" + aValor + "</Autorizado>";
          |HAZ Linea;

          eaTipoXML = "B";
          |HAZ RefCreta_plb;
          aValor = eaReferencia;
          aAlfa = "<ReferenciaExterna>" + aValor + "</ReferenciaExterna>";
          |HAZ Linea;

          |SI #0#4 = "R";
               aValor = "S";
               aAlfa = "<IndicadorRectificacion>" + aValor + "</IndicadorRectificacion>";
               |HAZ Linea;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Pie;
     nNivel = 1;
     aAlfa = "</Bases>";
     |HAZ Linea;

     |XCIERRA nHandle;
|FPRC;

|PROCESO BuscaPorCCC;
     swHayAlguno = 1;
     |ERROR10;
|FPRC;

|DEFBUCLE BuscaPorCCC;
     #tsilco15#2;
     ;
     #cretam02#0, 00001, #cretam02#4, "*";
     #cretam02#0, 99999, #cretam02#4, "*";
     ;
     NULL, BuscaPorCCC;
|FIN;

|PROCESO cretam02;
     || Si hay seleccion de empresas y ademas he entrado a marcar trabajadores
     || tengo que comprobar que dentro de esta empresa hay algun trabajador
     || marcado en el CCC que estoy mirando

     |SI #0#2 = "S"; |Y #tsilco14#3 = "*";
          swHayAlguno = 0;
          |BUCLE BuscaPorCCC;
          |SI swHayAlguno = 1;
               |HAZ Liquidacion;
          |FINSI;
     |SINO;
          |HAZ Liquidacion;
     |FINSI;
|FPRC;

|DEFBUCLE cretam02;           || CCCs
     #cretam02#2;
     ;
     || #cretam01#0, "           ", #cretam01#1, #cretam01#2, #cretam01#3;
     || #cretam01#0, "zzzzzzzzzzz", #cretam01#1, #cretam01#2, #cretam01#3;
     nDesdeEmpresa, "           ", #0#0, nDesdeMes, enTipo;
     nHastaEmpresa, "zzzzzzzzzzz", #0#0, nHastaMes, enTipo;
     ;
     NULL, cretam02;
|FIN;

|PROCESO cretam01;
     |BUCLE cretam02;

|FPRC;

|DEFBUCLE cretam01;           || EMPRESAS
     #cretam01#1;
     ;
     nDesdeEmpresa, #0#0, nDesdeMes, enTipo;
     nHastaEmpresa, #0#0, nHastaMes, enTipo;
     ;
     NULL, cretam01;
|FIN;

|PROCESO tsilco14;
     nDesdeEmpresa = #tsilco14#0;
     nHastaEmpresa = #tsilco14#0;

     |BUCLE cretam02;           || EMPRESAS
|FPRC;

|DEFBUCLE tsilco14;
     #tsilco14#1;
     2;
     00001, "*";
     99999, "*";
     ;
     NULL, tsilco14;
|FIN;

|PROCESO CreacionXML;
     |HAZ Ruta;

     aLocalRemoto = "";
     aAlfa = aRuta % 201;
     |SI aAlfa = ":";
          aLocalRemoto = "L";
     |SINO;
          aLocalRemoto = "R";
     |FINSI;

     |DFICE aOrigen;
     aOrigen = aOrigen + aFichero;
     aDestino = aRuta;
     |QBF aDestino;
     aDestino = aRuta;

     |HAZ Cabecera;

     |SI 5 [ #0#5; |Y #0#5 [ 20;   || por comodidad
          swAtrasos = 1;
     |FINSI;

     |SI swAtrasos = 1;
          nDesdeMes = #0#1;
          nHastaMes = #0#6;
     |SINO;
          nDesdeMes = #0#1;
          nHastaMes = #0#1;
     |FINSI;

     |SI #0#2 = "S";
          || seleccion
          enTipo = #0#5;
          |BUCLE tsilco14;
     |SINO;
          || todas las empresas

          nDesdeEmpresa = 00001;
          nHastaEmpresa = 99999;
          enTipo = #0#5;

          |BUCLE cretam02;
     |FINSI;

     |SI swHeEntrado = 1;
          || para cerrar la ultima liquidacion, si existe

          nNivel = 2;
          aAlfa = "</Liquidacion>";
          |HAZ Linea;
     |FINSI;

     |HAZ Pie;

     |IP_REMOTO aIP;
     |SI aIP ! "";
          |ENVIA_FICHERO aOrigen, aDestino;
     |SINO;
          |COPIA_FICHERO aOrigen, aDestino;
     |FINSI;

/*
     |SI aIP ! "";
          |SI aLocalRemoto = "L";
               |ENVIA_FICHERO aOrigen, aDestino;
          |SINO;
               |COPIA_FICHERO aOrigen, aDestino;
          |FINSI;
     |SINO;
          |SI aLocalRemoto = "L";
               |COPIA_FICHERO aOrigen, aDestino;
          |SINO;
               |ENVIA_FICHERO aOrigen, aDestino;
          |FINSI;
     |FINSI;
*/
     |FBORRA aOrigen;

     aAlfa = #cretam00#2;
     |QBF aAlfa;
     aAlfa = "    Se ha creado el fichero de bases en la carpeta " + aAlfa;
     |MENAV aAlfa;
|FPRC;

|PROGRAMA;
     |CLS;

     |HAZ Defectos;
     |HAZ Fichero;

     alfa1 = Usuario; |QBT alfa1;    alfa1 = ("t" + alfa1) % 108;
     alfa2 = Usuario; |QBT alfa2;    alfa2 = ("u" + alfa2) % 108;
     alfa3 = Usuario; |QBT alfa3;    alfa3 = ("v" + alfa3) % 108;
     alfa4 = Usuario; |QBT alfa4;    alfa4 = ("c3" + alfa4) % 108;
     |NOME_DAT #tsilco14#alfa1#;
     |NOME_DAT #tsilco15#alfa2#;
     |NOME_DAT #tsilcon9#alfa3#;
     |NOME_DAT #cretat08#alfa4#;
     |DELINDEX #tsilco14;
     |DELINDEX #tsilco15;
     |DELINDEX #tsilcon9;
     |DELINDEX #cretat08;

     |PINPA #0#0;
     |PINDA #0#0;

     |ENDATOS #1#0;
     |SI FSalida = 0;
          |ABRE #labempre;
          |ABRE #labtraba;
          |ABRE #tsilco15;
          |ABRE #cretat08;

          enMes = #0#1;
          enAny = #0#0;

          |HAZ CreacionXML;

          |LEE 000#cretat08.p;
          |SI FSalida = 0;
               ||CONSULTA_CLAVE #1#cretat08;

               aAlfa = "    ATENCION. Existen centros de Formación para los que ";
               aAlfa = aAlfa + "se ha de enviar una Solicitud de Borrador";
               |MENAV aAlfa;

               aAlfa = "    Se va a generar el fichero con la Solicitud de Borrador, que ";
               aAlfa = aAlfa + "es nece- sario enviar con el Fichero de Bases";
               |MENAV aAlfa;

               |SUB_EJECUTA "cretap11;cretat08";
          |FINSI;

          |DELINDEX #cretat08;
          |CIERRA #cretat08;
          |CIERRA #tsilco15;
          |CIERRA #labtraba;
          |CIERRA #labempre;
     |FINSI;
|FPRO;
