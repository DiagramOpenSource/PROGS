|FICHEROS;
     arten001 #0;
     labempre #1;        || empresas
     labempax #2;        || aux. empresas
     nomcalca #3;        || calculos cabs.
     labtraba #4;        || trabajador
     nomauxi1 #5;        || areas
     nomcontb #10;       || formato fichero enlace
     nomcontc #11;       || tmp. fichero enlace
     nomcontd #12,MANTE; || cuentas/DNI's

     camandia@ #20;   || diarios AGICONT
     || caconcep@ #21;   || conceptos AGICONT
     || dsmom030@ #221;   || conceptos AGICONT
     concepto@ #21;   || conceptos AGICONT o MODELOS
     camaasic@ #22;   || asientos cabs. AGICONT
     camaasil@ #23;   || asientos lins. AGICONT
     cacuenta@ #24;   || cuentas AGICONT
     canempre@ #25;   || empresas AGICONT
     caconasi@ #26;   || contador asientos AGICONT
     catradia@ #27;   || diario de trabajo
     cacuebal@ #28;   || cuentas de balance

     arten002;       || control de la aplicacion
                     || no lo uso de momento, lo pongo a pelo que es como estaba
|FIN;

|VARIABLES;
     alfa1 = "";
     alfa2 = "";
     alfa3 = "";
     alfa4 = "";
     nume1 = 0;
     nume2 = 0;
     nume3 = 0;
     nume4 = 0;
     para1 = 0;
     para2 = 0;
     para3 = 0;
     para4 = 0;
     fech1 = @;
     fech2 = @;
     fech3 = @;
     fech4 = @;
     elsw1 = 0;
     elsw2 = 0;
     elsw3 = 0;
     elsw4 = 0;
     FEstado = 0;
     seleccio = 0;
     modo = 0;
     sw_rup = 0;

     p_mes = "";
     deven = 0;
     rirpf = 0;
     rsegu = 0;
     rpres = 0;
     liqui = 0;
     antic = 0;
     espec = 0;
     eltc1 = 0;
     signe = "";
     impor = 0;
     conte = "";
     digcu = 0;
     conce = 0;
     empre = 0;
     dia1 = "";
     dia2 = "";
     mes1 = "";
     mes2 = "";
     any1 = "";
     any2 = "";
     any3 = "";
     any4 = "";
     topes = 0;
     fech = "";
     sw_d = 0;
     sw_m = 0;
     sw_a = 0;
     sw_b = 0;
     asiento = 0;
     apunte = 0;
     sep_campo = "";
     espac = "";
     carga = "";

     p_alfa1 = "";
     p_error = 0;
     p_lugar = 0;
     p_cuan3 = 0;
     p_cuan4 = 0;
     p_cuan5 = 0;
     p_alfa2 = "";
     p_alfa3 = "";
     p_alfa4 = "";
     p_alfa5 = "";
     p_nume1 = 0;
     p_nume2 = 0;
     p_ceros = "";

     fabre = "";
     field = "";
     fcanl = "";
     fgrab = "";

     numcam = 0;
     desglo = 0;
     ant_area = "";
     cuadra = 0;

     p_mensa = "    LONGITUD DE CUENTA FUERA DE NIVEL!";
     d_diari = 0;
     d_opera = "";
     d_impor = 0;
     &modcon = 0;
     &codcon = 0;
     importe = 0;
     opera = "";
     ressum = 0;
     ini = 0;
     dig1 = 0;
     dig3 = 0;
     dig4 = 0;
     dig5 = 0;
     dig6 = 0;
     dig7 = 0;
     dig8 = 0;
     dig9 = 0;
     fecalf = "";
     mes = "";
     mesfec = 0;
     mesac = 0;
     a = 0;
     b = 0;
     c = 0;
     ult_apunt = 0;

     juan = 0;
     errant = 0;
     cuini = "            ";
     cufin = "999999999998";
     diini = 0;
     difin = 9;
     fecini = @;
     fecfin = @;
     tiini = "D";
     tifin = "H";
     diain = 0;
     diafi = 99;
     prim = 0;
     cuant = "";
     lon = 0;
     aa = 0;
     mensa = "";
     aar = "";
     traba = "";
     errcuent = "aaaaaaaaaaaa";
     mensa2   = "     No esta definida esta cuenta en Cuentas de Balance";
     uno = "";
     dos = "";
     mascara = "";
     nivel = 0;
     cuantos = 0;
     descr = "";

     path_agicon = "";
     path_empres = "";
     cod_empre = 0;
     cod_perio = 0;
     nGuardaPos = 0;

    el503 = 0;
    el504 = 0;
    el555 = 0;
    el556 = 0;

     nControl = 0;
     aRuta = "";
     aRutaFich = "";
     aRutaDef = "";
     aAlfa = "";
     aAlfa1 = "";
     aLon = "";
     nLon1 = 0;
     nLon2 = 0;

     aRutaModelos = "";
     aRutaFichModelos = "";
     path_modelos = "";
     path_empres_modelos = "";
|FIN;
____________________________________/
|PROCESO BuscaCon; |TIPO 66;
     |ABRE #21;
     |CONSULTA_CLAVE #1#21;
     |SI FSalida = 0;
          #0#49 = #21#0;
          |PINTA #0#49;
     |FINSI;
     |CIERRA #21;
|FPRC;

|PROCESO CompCon;
     |ABRE #21;
     #21#0 = #0#49;
     |LEE 000#21=;
     |SI FSalida ! 0;
          aAlfa = "    ATENCION: No existe el concepto en contabilidad";
          |MENAV aAlfa;
          |ERROR;
     |FINSI;
     |CIERRA #21;
|FPRC;

|PROCESO BuscaEmp; |TIPO 66;
     |ABRE #25;
     |CONSULTA_CLAVE #1#25;
     |SI FSalida = 0;
          #0#47 = #25#2;
          #0#48 = #25#3;
          |PINTA #0#47;
          |PINTA #0#48;
          aAlfa = #25#1;
          |QBF aAlfa;
          |ATRI I;
          |PINTA 0704, aAlfa;
          |ATRI 0;
     |FINSI;
     |CIERRA #25;
|FPRC;

|PROCESO CompEmp;
     |ABRE #25;
     #25#2 = #0#47;
     #25#3 = #0#48;
     |LEE 000#25=;
     |SI FSalida = 0;
          aAlfa = #25#1;
          |QBF aAlfa;
          |ATRI I;
          |PINTA 0704, aAlfa;
          |ATRI 0;
     |SINO;
          aAlfa = "    ATENCION: No existe empresa contable en AGICONT";
          |MENAV aAlfa;
          aAlfa = "                                  ";
          aAlfa = aAlfa + "                                  ";
          |PINTA 0704, aAlfa;
          |ERROR;
     |FINSI;
     |CIERRA #25;
|FPRC;


|PROCESO CargaDef;
          aRuta = path_agicon - "fich/";
          |QBF aRuta;

          aRutaFich = aRuta + "fich/";

          || camandia #20;   || diarios AGICONT

          aRutaDef = aRuta + "def/camandia.mas";
          |CARGA_DEF aRutaDef, camandia@;
          |PATH_DAT #20 aRutaFich;
          |SI FSalida ! 0;
               |ACABA;
          |FINSI;
          nControl = nControl + 1;

          |SI #arten002#3 = "A";
               || caconcep #21;   || conceptos AGICONT

               aRutaDef = aRuta + "def/caconcep.mas";
               |CARGA_DEF aRutaDef, concepto@;
               |PATH_DAT #21 aRutaFich;
               |SI FSalida ! 0;
                    |ACABA;
               |FINSI;
               nControl = nControl + 1;
          |FINSI;
          |SI #arten002#3 = "C";
               aRutaModelos = path_modelos - "fich/";
               |QBF aRuta;

               aRutaFichModelos = aRutaModelos + "fich/";

               || dsmom030 #21;   || conceptos MODELOS

               aRutaDef = aRutaModelos + "def/dsmom030.mas";
               |CARGA_DEF aRutaDef, concepto@;
               |PATH_DAT #21 aRutaFichModelos;
               |SI FSalida ! 0;
                    |ACABA;
               |FINSI;
               nControl = nControl + 1;
          |FINSI;

          || camaasic #22;   || asientos cabs. AGICONT

          aRutaDef = aRuta + "def/camaasic.mas";
          |CARGA_DEF aRutaDef, camaasic@;
          |PATH_DAT #22 aRutaFich;
          |SI FSalida ! 0;
               |ACABA;
          |FINSI;
          nControl = nControl + 1;

          || camaasil #23;   || asientos lins. AGICONT

          aRutaDef = aRuta + "def/camaasil.mas";
          |CARGA_DEF aRutaDef, camaasil@;
          |PATH_DAT #23 aRutaFich;
          |SI FSalida ! 0;
               |ACABA;
          |FINSI;
          nControl = nControl + 1;

          || cacuenta #24;   || cuentas AGICONT

          aRutaDef = aRuta + "def/cacuenta.mas";
          |CARGA_DEF aRutaDef, cacuenta@;
          |PATH_DAT #24 aRutaFich;
          |SI FSalida ! 0;
               |ACABA;
          |FINSI;
          nControl = nControl + 1;

          || canempre #25;   || empresas AGICONT

          aRutaDef = aRuta + "def/canempre.mas";
          |CARGA_DEF aRutaDef, canempre@;
          |PATH_DAT #25 aRutaFich;
          |SI FSalida ! 0;
               |ACABA;
          |FINSI;
          nControl = nControl + 1;

          || caconasi #26;   || contador asientos AGICONT

          aRutaDef = aRuta + "def/caconasi.mas";
          |CARGA_DEF aRutaDef, caconasi@;
          |PATH_DAT #26 aRutaFich;
          |SI FSalida ! 0;
               |ACABA;
          |FINSI;
          nControl = nControl + 1;

          || catradia #27;   || diario de trabajo

          aRutaDef = aRuta + "def/catradia.mas";
          |CARGA_DEF aRutaDef, catradia@;
          |PATH_DAT #27 aRutaFich;
          |SI FSalida ! 0;
               |ACABA;
          |FINSI;
          nControl = nControl + 1;

          || cacuebal #28;   || cuentas de balance

          aRutaDef = aRuta + "def/cacuebal.mas";
          |CARGA_DEF aRutaDef, cacuebal@;
          |PATH_DAT #28 aRutaFich;
          |SI FSalida ! 0;
               |ACABA;
          |FINSI;
          nControl = nControl + 1;
|FPRC;

|PROCESO DesCargaDef;
     |SI nControl ! -1;
          |SI nControl > 8;
               || cacuebal #28;   || cuentas de balance
               |DESCARGA_DEF #28;
          |FINSI;
          |SI nControl > 7
               || catradia #27;   || diario de trabajo
               |DESCARGA_DEF #27;
          |FINSI;
          |SI nControl > 6
               || caconasi #26;   || contador asientos AGICONT
               |DESCARGA_DEF #26;
          |FINSI;
          |SI nControl > 5
               || canempre #25;   || empresas AGICONT
               |DESCARGA_DEF #25;
          |FINSI;
          |SI nControl > 4
               || cacuenta #24;   || cuentas AGICONT
               |DESCARGA_DEF #24;
          |FINSI;
          |SI nControl > 3
               || camaasil #23;   || asientos lins. AGICONT
               |DESCARGA_DEF #23;
          |FINSI;
          |SI nControl > 2
               || camaasic #22;   || asientos cabs. AGICONT
               |DESCARGA_DEF #22;
          |FINSI;
          |SI nControl > 1;
               || caconcep #21 o dsmom030;   || conceptos AGICONT o MODELOS
               |DESCARGA_DEF #21;
          |FINSI;
          |SI nControl > 0;
               || camandia #20;   || diarios AGICONT
               |DESCARGA_DEF #20;
          |FINSI;
     |FINSI;
|FPRC;

____________________________________/
|PROGRAMA;
     |CLS;
     |CABEZA "Enlace Contable";
     |DDEFECTO #0;
     |PINPA #0#0;
     |PINDA #0#0;

     |HAZ tipo8;

     |SI path_agicon ! "";
          |QBF path_agicon;
          aAlfa = path_agicon % -101;
          |SI aAlfa ! "/"; |Y aAlfa ! "\";
               path_agicon = path_agicon + "/";
          |FINSI;
          path_agicon = path_agicon + "fich/"
     |SINO;
          |ACABA;
     |FINSI;

     |SI path_modelos ! "";
          |QBF path_modelos;
          aAlfa = path_modelos % -101;
          |SI aAlfa ! "/"; |Y aAlfa ! "\";
               path_modelos = path_modelos + "/";
          |FINSI;
          path_modelos = path_modelos + "fich/"
     |SINO;
          |ACABA;
     |FINSI;

     |ABRE #0;
     |LEE 101#0p;
     |SI FSalida ! 0;
          |GRABA 020#0b;
     |FINSI;
     |PINDA #0#0;

     nControl = 0;
     |HAZ CargaDef;

     |HAZ PathPanes;

     |SI nControl = 9;
          |ENDATOS #1#0;
          |SI FSalida = 0;
               |HAZ tipo3;
               |GRABA 020#0a;
          |FINSI;
     |FINSI;

     |HAZ DesCargaDef;
     |LIBERA #0;
     |CIERRA #0;
|FPRO;
____________________________________/ CALCULOS PANTALLA
|PROCESO mirames; |TIPO 7;
     fech1 = ~;
     alfa1 = fech1;
     alfa1 = alfa1 % 402;
     #0Campo = alfa1;
     |PINTA #0Campo;
|FPRC;

|PROCESO pintames; |TIPO 0;
     |SI #0Campo =  1;  p_mes = "ENERO     ";  |FINSI;
     |SI #0Campo =  2;  p_mes = "FEBRERO   ";  |FINSI;
     |SI #0Campo =  3;  p_mes = "MARZO     ";  |FINSI;
     |SI #0Campo =  4;  p_mes = "ABRIL     ";  |FINSI;
     |SI #0Campo =  5;  p_mes = "MAYO      ";  |FINSI;
     |SI #0Campo =  6;  p_mes = "JUNIO     ";  |FINSI;
     |SI #0Campo =  7;  p_mes = "JULIO     ";  |FINSI;
     |SI #0Campo =  8;  p_mes = "AGOSTO    ";  |FINSI;
     |SI #0Campo =  9;  p_mes = "SEPTIEMBRE";  |FINSI;
     |SI #0Campo = 10;  p_mes = "OCTUBRE   ";  |FINSI;
     |SI #0Campo = 11;  p_mes = "NOVIEMBRE ";  |FINSI;
     |SI #0Campo = 12;  p_mes = "DICIEMBRE ";  |FINSI;
     |ATRI I;
     |PINTA 1063, p_mes;
     |ATRI 0;
|FPRC;

|PROCESO desde_hasta; |TIPO 0;
     |PARA para1 = 2; |SI para1 [ 11; |HACIENDO para1 = para1 + 1;
          |CAMPO_MODIFICA #0para1, 1, "S";
     |SIGUE;
     |CAMPO_MODIFICA #0#12, 1, "N";
     |CAMPO_MODIFICA #0#13, 1, "N";

     |SI #0#1 = 0;
          |PARA para1 = 2; |SI para1 [ 11; |HACIENDO para1 = para1 + 1;
               |CAMPO_MODIFICA #0para1, 1, "N";
          |SIGUE;
          |CAMPO_MODIFICA #0#12, 1, "S";
          |CAMPO_MODIFICA #0#13, 1, "S";
     |FINSI;
|FPRC;

|PROCESO lee_emp; |TIPO 0;
     |SI #0Campo ! 0;
          |ABRE #1;
          #1#0 = #0Campo;
          |LEE 000#1=;
          |SI FSalida ! 0;
               |MENAV "    Empresa INEXISTENTE ...";
               |ERROR;
          |FINSI;
          |CIERRA #1;
     |FINSI;
|FPRC;

|PROCESO pin_emp; |TIPO 7;
     |DDEFECTO #1;
     |SI #0Campo ! 0;
          |ABRE #1;
          #1#0 = #0Campo;
          |LEE 000#1=;
          |CIERRA #1;
     |FINSI;
     |ATRI I;   |PINTA 0922, #1#2;  |ATRI 0;
|FPRC;

|PROCESO min12;
     #12#0 = "            ";
|FPRC;

|PROCESO max12;
     #12#0 = "zzzzzzzzzzzz";
|FPRC;

|PROCESO asignacion; |TIPO 0;
     |SI #0#44 = "S";|Y FSalida = 0;
          |PUSHV 0501 2380;
          |ENTLINEAL #1#12, 1, 3, 22, 1, min12, max12;
          |POPV;
     |FINSI;
|FPRC;
____________________________________/ GRABACION DE ASIENTOS EN AGICONT
____________________________________/ ACTUALIZACION NIVELES SUPERIORES
|PROCESO actuali;
  |SI #27#2 [ juan; |O #27#2 = 9;
      |SI prim = 1;
           |GRABA 020#24a;
      |FINSI;
      prim = 0;
      |VETE acaba;
  |FINSI;
  aa = 100 + lon;
  aar = aa;
  traba = #27#1 % aar;
|ET iniemp;
  |SI prim = 0;
      cuant = traba;
      prim = 1;
      #24#0 = cuant;
      #24#1 = juan;
      |SI #24#0 = errcuent;
          prim = 0;
          |VETE acaba;
      |FINSI;
      |LIBERA #24;
      |LEE 101#24=;
      |SI FSalida ! 0;
          |LIBERA #24;
          nGuardaPos = Pos;
          Pos = -1;
          |DDEFECTO #24;
          #24#0 = cuant;
          #24#1 = juan;
          |HAZ defect1;
          |PUSHV 0501 2380;
          |PINPA #0#24;
          |PINDA #0#24;
          |ENDATOS #1#24;
          |SI FSalida ! 0;
              |POPV;
              mensa = "Actualizando nivel: " + juan + "        ";
              |PINTA 2410,mensa;
              errant = #24#0;
              prim = 0;
              |VETE acaba;
          |FINSI;
          |GRABA 020#24b;
          |POPV;
          Pos = nGuardaPos;
          mensa = "Actualizando nivel: " + juan + "        ";
          |PINTA 2410,mensa;
      |FINSI;
  |FINSI;
  |SI cuant ! traba;
      |GRABA 020#24a;
      prim = 0;
      |VETE iniemp;
  |FINSI;
  |HAZ suma;
|ET acaba;
|FPRC;

|PROCESO borrando;
  |BORRA 020#27a;
|FPRC;

|DEFBUCLE camaapua0;
 #27#1006;
 ;
 codcon,cuini,diini,fecini,tiini,diain;
 codcon,cufin,difin,fecfin,tifin,diafi;
 be;
 NULL,actuali;
|FIN;

|DEFBUCLE camaapua1;
 #27#1006;
 ;
 codcon,cuini,diini,fecini,tiini,diain;
 codcon,cufin,difin,fecfin,tifin,diafi;
 be;
 NULL,borrando;
|FIN;

|PROCESO niveles;
  |PINTA 2410,"Actualizando Niveles Contables Superiores ...";
  |ABRE #24;
  |PARA juan = dig3 - 1; |SI juan ] 1; |HACIENDO juan = juan - 1;
        prim = 0;
        |SI juan = 1; lon = dig4; |FINSI;
        |SI juan = 2; lon = dig5; |FINSI;
        |SI juan = 3; lon = dig6; |FINSI;
        |SI juan = 4; lon = dig7; |FINSI;
        |SI juan = 5; lon = dig8; |FINSI;
        fecini = "01.01.0000";
        fecfin = "31.12.8999";
        |BUCLE camaapua0;
        |SI prim = 1;
            |GRABA 020#24a;
        |FINSI;
  |SIGUE;
  |CIERRA #24;
  fecini = "01.01.0000";
  fecfin = "31.12.8999";
  |BUCLE camaapua1;
  |ABRE #27;
  |LEE 000#27u;
  |SI FSalida ! 0;
       |CIERRA #27;
       |DELINDEX #27;
  |SINO;
       |CIERRA #27;
  |FINSI;
  |BLIN 24;
|FPRC;

|PROCESO suma;
  |SI #27#6 = 0;
      |SI #27#4 = "D";
          #24#9 = #24#9 + #27#5;
          #24#51 = #24#51 + #27#5;
      |SINO;
          #24#10 = #24#10 + #27#5;
          #24#52 = #24#52 + #27#5;
      |FINSI;
      #24#11 = #24#9 - #24#10;
      #24#53 = #24#51 - #24#52;
  |SINO;
      |SI #27#6 = 99;
          |SI #27#4 = "D";
              #24#48 = #24#48 + #27#5;
              #24#51 = #24#51 + #27#5;
          |SINO;
              #24#49 = #24#49 + #27#5;
              #24#52 = #24#52 + #27#5;
          |FINSI;
          #24#50 = #24#48 - #24#49;
          #24#53 = #24#51 - #24#52;
      |SINO;
          |HAZ actualiza;
      |FINSI;
  |FINSI;
|FPRC;

|PROCESO actualiza;
  ini = dig1;
  fecalf = #27#3;
  mes = fecalf % 402;
  mesfec = mes;
  |SI ini = 1;
      mesac = mesfec;
  |SINO;
      mesac = (mesfec - ini) + 1;
      |SI mesac [ 0;
          mesac = (13 - ini) + mesfec;
      |FINSI;
  |FINSI;
  a = (mesac * 3) + 9;
  b = a + 1;
  c = a + 2;
  |SI #27#4 = "D";
      #24a = #24a + #27#5;
      #24#51 = #24#51 + #27#5;
  |SINO;
      #24b = #24b + #27#5;
      #24#52 = #24#52 + #27#5;
  |FINSI;
  #24c = #24a - #24b;
  #24#53 = #24#51 - #24#52;
|FPRC;

|PROCESO defect1;
  |ABRE #28;
  uno = #24#0;
  |QBF uno;
  dos = uno + "000000000000";
  uno = dos % A101;
  #28#0 = uno;
  |LEE 040#28=;
  |SI FSalida = 0;
      |HAZ saca1;
      |VETE findefe;
  |FINSI;
  uno = dos % A102;
  #28#0 = uno;
  |LEE 040#28=;
  |SI FSalida = 0;
      |HAZ saca1;
      |VETE findefe;
  |FINSI;
  uno = dos % A103;
  #28#0 = uno;
  |LEE 040#28=;
  |SI FSalida = 0;
      |HAZ saca1;
      |VETE findefe;
  |FINSI;
  uno = dos % A104;
  #28#0 = uno;
  |LEE 040#28=;
  |SI FSalida = 0;
      |HAZ saca1;
      |VETE findefe;
  |SINO;
      |MENAV mensa2;
  |FINSI;
|ET findefe;
  nivel = #24#1;
|| la 3a clave esta formada por la cta + zzzz y el campo fantasma del nivel;
  mascara = #24#0;
  |QBF mascara;
  mascara = mascara +"zzzzzzzzzzzz";
  mascara = mascara % 112;
  #24#54    = mascara;
  #24#55    = nivel;
  |CIERRA #28;
|FPRC;

|PROCESO saca1;
  #24#4 = #28#1;
  #24#5 = #28#2;
  #24#6 = #28#3;
  #24#7 = #28#4;
  #24#8 = #28#5;
|FPRC;
____________________________________/ GRABACION DE ASIENTOS
|PROCESO actu_diari;
|ABRE #20;
    #20#0 = d_diari;
|LEE 101#20=;
|SI FSalida ! 0;
    |DDEFECTO #20;
        #20#0 = d_diari;
        #20#1 = "<DIARIO SIN DEFINIR>";
    |GRABA 020#20n;
    |LEE 121#20=;
|FINSI;
|SI d_opera = "D";
        #20#2 = #20#2 + d_impor;        || debe
|SINO;
        #20#3 = #20#3 + d_impor;        || haber
|FINSI;
|GRABA 020#20a;
|LIBERA #20;
|CIERRA #20;
|FPRC;

|PROCESO altatra;
    modcon = 1;
|ABRE #27;
    #27#0 = codcon;
    #27#1 = #23#4;
    #27#2 = #23#5;
    #27#3 = #23#1;
    #27#4 = #23#8;
    #27#6 = #23#0;
|LEE 101#27=;
|SI FSalida ! 0;
    |DDEFECTO #27;
        #27#0 = codcon;
        #27#1 = #23#4;
        #27#2 = #23#5;
        #27#3 = #23#1;
        #27#4 = #23#8;
        #27#6 = #23#0;
    |GRABA 101#27b;
|FINSI;
    importe = #23#9;  || aqui si que se usa el importe real
|SI opera = "B";
        importe = -importe;
|FINSI;
    #27#5 = #27#5 + importe;
|GRABA 101#27a;
|LIBERA #27;
|CIERRA #27;
|FPRC;

|PROCESO alta_cuen;
    nGuardaPos = Pos;
    Pos = -1;

    #24#2 = "";          || descripcion
    #24#3 = "S";         || pase directo
    #24#58 = #0#31;      || fecha ultima operacion
|HAZ defect1;
|PUSHV 0501 2380;
|PINPA #0#24;
|PINDA #0#24;
|ENDATOS #1#24;
|SI FSalida = 0;
    |GRABA 020#24b;
|FINSI;
|POPV;

    Pos = nGuardaPos;
|FPRC;

|PROCESO actualiz1;
|DDEFECTO #24;
|ABRE #24;
    #24#0 = #23#4;
    #24#1 = #23#5;
|LEE 101#24=;
|SI FSalida ! 0;
    |HAZ alta_cuen;
|FINSI;
|SI #23#0 = 0;
    |SI #23#8 = "D";
            #24#9 = #24#9 + ressum;
            #24#51 = #24#51 + ressum;
    |SINO;
            #24#10 = #24#10 + ressum;
            #24#52 = #24#52 + ressum;
    |FINSI;
        #24#11 = #24#9 - #24#10;
        #24#53 = #24#51 - #24#52;
|SINO;
    |SI #23#0 = 99;
        |SI #23#8 = "D";
                #24#48 = #24#48 + ressum;
                #24#51 = #24#51 + ressum;
        |SINO;
                #24#49 = #24#49 + ressum;
                #24#52 = #24#52 + ressum;
        |FINSI;
            #24#50 = #24#48 - #24#49;
            #24#53 = #24#51 - #24#52;
    |SINO;
        |HAZ actualiz2;
    |FINSI;
|FINSI;
|SI #24#58 < #23#1;
        #24#58 = #23#1;
|FINSI;
|GRABA 020#24a;
|LIBERA #24;
|CIERRA #24;
|FPRC;

|PROCESO actualiz2;
    ini = dig1;      || numero de niveles
    fecalf = #23#1;
    mes = fecalf % 402;
    mesfec = mes;
|SI ini = 1;
        mesac = mesfec;
|SINO;
        mesac = (mesfec - ini) + 1;
    |SI mesac [ 0;
            mesac = (13 - ini) + mesfec;
    |FINSI;
|FINSI;
    a = (mesac * 3) + 9;
    b = a + 1;
    c = a + 2;
|SI #23#8 = "D";
        #24a = #24a + ressum;
        #24#51 = #24#51 + ressum;
|SINO;
        #24b = #24b + ressum;
        #24#52 = #24#52 + ressum;
|FINSI;
    #24c = #24a - #24b;
    #24#53 = #24#51 - #24#52;
|FPRC;

|PROCESO cabecera;
|DDEFECTO #22;
    #22#0 = #23#0;
    #22#1 = #23#1;
    #22#2 = #23#2;
|LEE 101#22=;
    FEstado = FSalida;
    #22#0 = #23#0;         || diario
    #22#1 = #23#1;         || fecha
    #22#2 = #23#2;         || documento
    #22#3 = #22#2;         || asiento
|SI #23#8 = "D";
        #22#4 = #22#4 + #23#9;
    |SI #22#09 = 0; #22#08 = #23#4; #22#09 = #23#5; |FINSI; || contrp.HABER
|FINSI;
|SI #23#8 = "H";
        #22#5 = #22#5 + #23#9;
    |SI #22#11 = 0; #22#10 = #23#4; #22#11 = #23#5; |FINSI; || contrp.DEBE
|FINSI;
|SI FEstado = 0;  |GRABA 020#22a;  |FINSI;
|SI FEstado ! 0;
    |HAZ mensaje;
    |GRABA 020#22n;
|FINSI;
|LIBERA #22;
|FPRC;

|PROCESO grabatra;
    modcon = 0;
|ABRE #27;
|LEE 101#27u;
|SI FSalida ! 0;
    |DDEFECTO #27;
        #27#0 = 1;
|SINO;
        #27#0 = #27#0 + 1;
|FINSI;
|GRABA 101#27n;
    codcon = #27#0;
|LIBERA #27;
|CIERRA #27;
|FPRC;

|PROCESO contador;
|ABRE #26;
|LEE 101#26p;
|SI FSalida ! 0;
    |DDEFECTO #26;
    |GRABA 020#26n;
    |LEE 101#26p;
|FINSI;
    #26#1 = #26#1 + 1;
|GRABA 020#26a;
|LIBERA #26;
|CIERRA #26;
    ult_apunt = 1;
|FPRC;

|PROCESO mensaje;
    cuantos = cuantos + 1;
    alfa1 = "Grabando Asiento: " + #22#2;
|ATRI I;
|PINTA 2321, alfa1;
|ATRI 0;
|FPRC;

|PROCESO la_cuenta;
  p_alfa1 = conte;
  p_alfa1 = (p_alfa1 + "            ") % 112;
  p_error = 0;
  p_lugar = 0;
  p_cuan3 = 0;
  p_cuan4 = 0;
  p_alfa3 = "";
  p_alfa4 = "";
  |PARA p_nume1 = 1; |SI p_nume1 [ 12; |HACIENDO p_nume1 = p_nume1 + 1;
        p_nume2 = (p_nume1 * 100) + 1;
        p_alfa2 = p_alfa1 % p_nume2;
        |SI p_alfa2 ! " ";
            |SI p_alfa2 = ".";
                p_lugar = p_nume1;
            |SINO;
                |SI p_lugar=0; p_alfa3=p_alfa3+p_alfa2; p_cuan3=p_cuan3+1; |FINSI;
                |SI p_lugar!0; p_alfa4=p_alfa4+p_alfa2; p_cuan4=p_cuan4+1; |FINSI;
            |FINSI;
        |FINSI;
  |SIGUE;
  |SI p_lugar ! 0;
      |SI dig3 = 1;  p_cuan5 = dig4;  digcu = 1;  |FINSI;
      |SI dig3 = 2;  p_cuan5 = dig5;  digcu = 2;  |FINSI;
      |SI dig3 = 3;  p_cuan5 = dig6;  digcu = 3;  |FINSI;
      |SI dig3 = 4;  p_cuan5 = dig7;  digcu = 4;  |FINSI;
      |SI dig3 = 5;  p_cuan5 = dig8;  digcu = 5;  |FINSI;
      |SI dig3 = 6;  p_cuan5 = dig9;  digcu = 6;  |FINSI;
      p_nume1 = p_cuan5 - (p_cuan3 + p_cuan4);
      |SI p_nume1 > 0;
          p_ceros = "0" * p_nume1;
          p_alfa5 = p_alfa3 + p_ceros + p_alfa4;
      |SINO;
          |MENAV p_mensa;
          p_error = 1;
      |FINSI;
  |SINO;
      p_alfa5 = p_alfa1;
  |FINSI;
  conte = p_alfa5;
|FPRC;

|PROCESO el_concepto;
|DDEFECTO #21;
|ABRE #21;
    #21#0 = conce;
|LEE 000#21=;
    alfa1 = descr % 103;
|SI alfa1 = "*N*";
        |DDEFECTO #labtraba;
        |ABRE #labtraba;
        alfa1 = descr % 405;  #labtraba#0 = alfa1;
        alfa1 = descr % 905;  #labtraba#1 = alfa1;
        |LEE 000#labtraba.=;
        |CIERRA #labtraba;
        descr = #labtraba#2;
|SINO;
        descr = #21#1;  |QBF descr;
        descr = descr + " " + #0#33;
|FINSI;
|CIERRA #21;
|FPRC;

|PROCESO apuntes_agicont;
|SI impor ! 0;
    |HAZ la_cuenta;
    |SI conte ! "            ";|Y digcu ! 0;
        |HAZ el_concepto;
        |DDEFECTO #23;
            #23#00 = #0#30;       || diario
            #23#01 = #0#31;       || fecha
            #23#02 = asiento;     || documento
            #23#03 = ult_apunt;   || apunte
            #23#04 = conte;       || cuenta
            #23#05 = digcu;       || digito cuenta
            #23#06 = conce;       || concepto
            #23#07 = descr;       || descripcion
            #23#08 = signe;       || signo
            #23#09 = impor;       || importe
        |SI #23#08 = "D";
                #23#16 = #23#4;
                #23#17 = #23#5;
        |SINO;
                #23#10 = #23#4;
                #23#11 = #23#5;
        |FINSI;
        |GRABA 020#23n;
        |HAZ cabecera;
            ressum = #23#9;
        |HAZ actualiz1;     || actualiza cuentas
            opera = "A";
        |HAZ altatra;       || actualiza niveles inferiores
            d_diari = #23#0;
            d_opera = #23#8;
            d_impor = #23#9;
        |HAZ actu_diari;    || actualiza diario
            ult_apunt = ult_apunt + 10;
    |SINO;
        |SI digcu = 0;
                alfa1 = "0000Cuenta [" + conte + "] fuera de nivel.";
            |MENAV alfa1;
        |FINSI;
    |FINSI;
|FINSI;
|FPRC;

|PROCESO LaField;
     alfa1 = field % 3101;
     signe = alfa1;

     alfa1 = field % 3210;
     impor = alfa1;

     alfa1 = field % 2110;
     conte = alfa1;
     |QBF conte;

     digcu = 0;
     alfa2 = conte % 0;
     nume1 = alfa2;
     |SI nume1 = dig4;   digcu = 1;     |FINSI;
     |SI nume1 = dig5;   digcu = 2;     |FINSI;
     |SI nume1 = dig6;   digcu = 3;     |FINSI;
     |SI nume1 = dig7;   digcu = 4;     |FINSI;
     |SI nume1 = dig8;   digcu = 5;     |FINSI;
     |SI nume1 = dig9;   digcu = 6;     |FINSI;

     conce = #0#49;

     alfa1 = field % 4220;
     descr = alfa1;

     alfa1 = field % 1704;
     ult_apunt = alfa1;

     alfa1 = field % 1106;
     asiento = alfa1;

     |HAZ apuntes_agicont;
|FPRC;

|PROCESO PasaAgicont;
     |HAZ grabatra;
     |ABRE #22;
     |ABRE #23;

     fabre = #10#11;
     |QBT fabre;
     fabre = "rt  " + fabre;
     |FABRE fabre;
     |SI FSalida ] 0;
          fcanl = FSalida;

          field = fcanl + " 250";  |FLEE field;
          |PARA; |SI FSalida > 0; |HACIENDO;
               |HAZ LaField;
               field = fcanl + " 250";  |FLEE field;
          |SIGUE;

          |FCIERRA fcanl;
     |SINO;
          alfa1 = "    [" + (fabre % 5) + "] no accesible ...";
          |MENAV alfa1;
     |FINSI;
     Pos = -1;

     |CIERRA #22;
     |CIERRA #23;

     |SI modcon ! 0; |HAZ niveles;  |FINSI;
|FPRC;

|PROCESO Produccion
     aAlfa = path_agicon;
     aLon = aAlfa % 0;
     nLon1 = aLon;
     aAlfa1 = aAlfa - "dsprofes/";
     aLon = aAlfa1 % 0;
     nLon2 = aLon;
     |SI nLon1 ! nLon2;
          aAlfa = aAlfa1 + "dsasesor/";
          path_agicon = aAlfa;
     |FINSI;
|FPRC;

|PROCESO tipo8;
     path_agicon = "";
     path_modelos = "";

/*
     |DIRECTORIO path_agicon;
     |QBF path_agicon;
     |HAZ Produccion
     path_agicon = path_agicon + "agicont/fich/";
*/

     |ABRE #arten002;
     |LEE 000#arten002.p;
     |SI FSalida = 0;
          path_agicon = #arten002#1;
     |SINO;
          aAlfa = "    ATENCION. No se ha definido la ruta donde se encuentra ";
          aAlfa = aAlfa + "instalada la aplicación contable.";
          |MENAV aAlfa;
          aAlfa = "    Puede configurarlo en el punto 'Parámetros de Control' ";
          aAlfa = aAlfa + "del Módulo";
          |MENAV aAlfa;
     |FINSI;

     |LEE 000#arten002.p;
     |SI FSalida = 0;
          path_modelos = #arten002#2;
     |SINO;
          aAlfa = "    ATENCION. No se ha definido la ruta donde se encuentra ";
          aAlfa = aAlfa + "instalada la aplicación modelos.";
          |MENAV aAlfa;
          aAlfa = "    Puede configurarlo en el punto 'Parámetros de Control' ";
          aAlfa = aAlfa + "del Módulo";
          |MENAV aAlfa;
     |FINSI;

     |CIERRA #arten002;

     ||PATH_DAT #25 path_agicon;
|FPRC;

|PROCESO PathPanes;
     alfa1 = path_agicon - "fich/";
     alfa1 = alfa1 + "pan/";
     |PATH_PAN #24 alfa1;
     |PATH_PAN #25 alfa1;

     |SI #arten002#3 = "A";
          |PATH_PAN #21 alfa1;
     |FINSI;
     |SI #arten002#3 = "C";
          alfa1 = path_modelos - "fich/";
          alfa1 = alfa1 + "pan/";
          |PATH_PAN #21 alfa1;
     |FINSI;
|FPRC;

|PROCESO ElPath; |TIPO 0;
     cod_empre = #0#47;
     cod_perio = #0#48;

     |ABRE #25;
     #25#2 = cod_empre;
     #25#3 = cod_perio;
     |LEE 020#25=;
     |SI FSalida = 0;
          dig1 = #25#11;       || desde mes
          dig3 = #25#13;       || nro. niveles
          dig4 = #25#14;       || 1 nivel
          dig5 = #25#15;       || 2 nivel
          dig6 = #25#16;       || 3 nivel
          dig7 = #25#17;       || 4 nivel
          dig8 = #25#18;       || 5 nivel
          dig9 = #25#19;       || 6 nivel
     |FINSI;
     |CIERRA #25;

     alfa1 = cod_empre;
     alfa1 = ("00000" + alfa1) % -105;
     path_empres = path_agicon + alfa1 + cod_perio + "/";

     |PATH_DAT #20 path_empres;
     ||PATH_DAT #21 path_empres;
     |PATH_DAT #22 path_empres;
     |PATH_DAT #23 path_empres;
     |PATH_DAT #24 path_empres;
     |PATH_DAT #26 path_empres;
     |PATH_DAT #27 path_empres;
     |PATH_DAT #28 path_empres;
|FPRC;
____________________________________/ GRABACION DE ASIENTOS
|PROCESO grabalo;
     fgrab = fcanl + " " + fgrab;
     |FGRABA fgrab;
|FPRC;

|PROCESO descrip;
     |SI #10#10 ! 0;
          |SI numcam = 17;|O numcam = 18;|O numcam = 43;    || nombre trabaj.
               |SI #0desglo = 2;
                       alfa2 = ("00000" + #4#0) % -105;
                       alfa3 = ("00000" + #4#1) % -105;
                       alfa1 = "*N*" + alfa2 + alfa3;
               |SINO;
                       alfa1 = #0#33;
               |FINSI;
          |SINO;
                  alfa1 = #0#33;
          |FINSI;
          nume1 = 100 + #10#10;
          espac = " " * 50;
          fgrab = ((alfa1 + espac) % nume1) + sep_campo;
          |HAZ grabalo;
     |FINSI;
|FPRC;

|PROCESO impor_haber;
     |SI #10#9 ! 0;
          |SI signe = "H";|O #10#8 = 0;
               alfa1 = impor;
          |SINO;
               alfa1 = "0";
          |FINSI;
          |QBT alfa1;
          nume1 = (100 + #10#9) * -1;
          espac = " " * 20;
          fgrab = ((espac + alfa1) % nume1) + sep_campo;
          |HAZ grabalo;
     |FINSI;
|FPRC;

|PROCESO impor_debe;
     |SI #10#8 ! 0;
          |SI signe = "D";|O #10#9 = 0;
               alfa1 = impor;
          |SINO;
               alfa1 = "0";
          |FINSI;
          |QBT alfa1;
          nume1 = (100 + #10#8) * -1;
          espac = " " * 20;
          fgrab = ((espac + alfa1) % nume1) + sep_campo;
          |HAZ grabalo;
     |FINSI;
|FPRC;

|PROCESO sign;
     |SI #10#7 ! 0;
          alfa1 = signe;
          |QBT alfa1;
          nume1 = 100 + #10#7;
          fgrab = (alfa1 % nume1) + sep_campo;
          |HAZ grabalo;
     |FINSI;
|FPRC;

|PROCESO cuenta;
     |SI #10#6 ! 0;
          p_alfa1 = conte;
          p_alfa1 = (p_alfa1 + "            ") % 112;
          p_error = 0;
          p_lugar = 0;
          p_cuan3 = 0;
          p_cuan4 = 0;
          p_alfa3 = "";
          p_alfa4 = "";
          |PARA p_nume1 = 1; |SI p_nume1 [ 12; |HACIENDO p_nume1 = p_nume1 + 1;
               p_nume2 = (p_nume1 * 100) + 1;
               p_alfa2 = p_alfa1 % p_nume2;
               |SI p_alfa2 ! " ";
                    |SI p_alfa2 = ".";
                         p_lugar = p_nume1;
                    |SINO;
                         |SI p_lugar=0; p_alfa3=p_alfa3+p_alfa2; p_cuan3=p_cuan3+1; |FINSI;
                         |SI p_lugar!0; p_alfa4=p_alfa4+p_alfa2; p_cuan4=p_cuan4+1; |FINSI;
                    |FINSI;
               |FINSI;
          |SIGUE;
          |SI p_lugar ! 0;
               p_cuan5 = #10#6;
               p_nume1 = p_cuan5 - (p_cuan3 + p_cuan4);
               |SI p_nume1 > 0;
                    p_ceros = "0" * p_nume1;
                    p_alfa5 = p_alfa3 + p_ceros + p_alfa4;
               |SINO;
                    p_error = 1;
               |FINSI;
          |SINO;
               p_alfa5 = p_alfa1;
          |FINSI;
          conte = p_alfa5;

          nume1 = 100 + #10#6;
          conte = conte % nume1;

          fgrab = conte + sep_campo;
          |HAZ grabalo;
     |FINSI;
|FPRC;

|PROCESO apunte;
     |SI #10#5 ! 0;
          apunte = apunte + 1;
          alfa1 = apunte;
          |QBT alfa1;
          nume1 = 100 + #10#5;
          fgrab = alfa1 % nume1;
          nume1 = nume1 * -1;
          fgrab = ("0000000000" + fgrab) % nume1;
          fgrab = fgrab + sep_campo;
          |HAZ grabalo;
     |FINSI;
|FPRC;

|PROCESO asiento;
     |SI #10#4 ! 0;
          alfa1 = asiento;
          |QBT alfa1;
          nume1 = 100 + #10#4;
          fgrab = alfa1 % nume1;
          nume1 = nume1 * -1;
          fgrab = ("0000000000" + fgrab) % nume1;
          fgrab = fgrab + sep_campo;
          |HAZ grabalo;
     |FINSI;
|FPRC;

|PROCESO fecha;
     alfa1 = #0#31;       || desglose de la fecha
     dia1 = alfa1 % 101;
     dia2 = alfa1 % 201;
     mes1 = alfa1 % 401;
     mes2 = alfa1 % 501;
     any1 = alfa1 % 701;
     any2 = alfa1 % 801;
     any3 = alfa1 % 901;
     any4 = alfa1 % 1001;

     alfa1 = #10#3;
     |QBT alfa1;
     alfa2 = alfa1 % 0;   topes = alfa2;

     fech = ""; sw_d = 0;  sw_m = 0; sw_a = 0; sw_b = 0;

     |PARA para1 = 1; |SI para1 [ topes; |HACIENDO para1 = para1 + 1;
          nume1 = (para1 * 100) + 1;
          alfa2 = alfa1 % nume1;
          carga = alfa2;
          |SI alfa2 = "D";|Y sw_d = 0; carga = dia1; alfa2 = ""; sw_d = 1; |FINSI;
          |SI alfa2 = "D";|Y sw_d = 1; carga = dia2; alfa2 = ""; sw_d = 2; |FINSI;
          |SI alfa2 = "M";|Y sw_m = 0; carga = mes1; alfa2 = ""; sw_m = 1; |FINSI;
          |SI alfa2 = "M";|Y sw_m = 1; carga = mes2; alfa2 = ""; sw_m = 2; |FINSI;
          |SI alfa2 = "A";|Y sw_a = 0; carga = any1; alfa2 = ""; sw_a = 1; |FINSI;
          |SI alfa2 = "A";|Y sw_a = 1; carga = any2; alfa2 = ""; sw_a = 2; |FINSI;
          |SI alfa2 = "A";|Y sw_a = 2; carga = any3; alfa2 = ""; sw_a = 3; |FINSI;
          |SI alfa2 = "A";|Y sw_a = 3; carga = any4; alfa2 = ""; sw_a = 4; |FINSI;
          |SI alfa2 = "a";|Y sw_b = 0; carga = any3; alfa2 = ""; sw_b = 1; |FINSI;
          |SI alfa2 = "a";|Y sw_b = 1; carga = any4; alfa2 = ""; sw_b = 2; |FINSI;
          fech = fech + carga;
     |SIGUE;
     fgrab = fech + sep_campo;
     |HAZ grabalo;
|FPRC;

|PROCESO diario;
     |SI #10#2 ! 0;
          alfa1 = #0#30;
          |QBT alfa1;
          nume1 = 100 + #10#2;
          fgrab = alfa1 % nume1;
          nume1 = nume1 * -1;
          fgrab = ("0000000000" + fgrab) % nume1;
          fgrab = fgrab + sep_campo;
          |HAZ grabalo;
    |FINSI;
|FPRC;

|PROCESO apuntes;
     |SI impor ! 0;
          |SI numcam = 16;     rsegu = rsegu + impor;   |FINSI;
          |SI numcam = 50;     rpres = rpres + impor;   |FINSI;
          |HAZ diario;
          |HAZ fecha;
          |HAZ asiento;
          |HAZ apunte;
          |HAZ cuenta;
          |HAZ sign;
          |HAZ impor_debe;
          |HAZ impor_haber;
          |HAZ descrip;

          fgrab = "";
          |SI #10#13 ! -1;     fgrab = fgrab + &#10#13; |FINSI;
          |SI #10#14 ! -1;     fgrab = fgrab + &#10#14; |FINSI;
          |SI fgrab ! "";
               |HAZ grabalo;
          |FINSI;
     |FINSI;
|FPRC;
____________________________________/ LECTURA DEL TEMPORAL

________________________________________ ASIENTO 2
|PROCESO sstmp2_1;
     |SI sw_rup = 0;
          sw_rup = 1;
     |SINO;
          |HAZ apuntes;
          cuadra = cuadra + impor;
     |FINSI;
     impor = #11#9;            || importe
     impor = impor + #11#10;   || Prestaciones

     conte = #0numcam;         || cuenta
     |QBF conte;
     alfa1 = conte % -101;
     |SI alfa1 = ".";
          conte = conte + #11#1;        || si la cuenta acaba con un punto
     |SINO;                             || se completa con el codigo
          conte = conte + ".";
     |FINSI;
|FPRC;

|DEFBUCLE sstmp2;          || por trabajadores
     #11#1;
     ;
     #1#0,     1;
     #1#0, 99999;
     ;
     NULL, sstmp2_1;
|FIN;

|PROCESO sstmp1_6;
     |HAZ sstmp1_1;
     |SI sw_rup = 0;
          sw_rup = 1;
          ant_area = #4#62;
     |FINSI;
     |SI ant_area ! #4#62;
          |HAZ apuntes;
          cuadra = cuadra + impor;
          ant_area = #4#62;
          impor = 0;
     |FINSI;
     impor = impor + #11#9;         || importe
     impor = impor + #11#10;        || Prestaciones tique 160/224

     conte = #0numcam;              || cuenta
     |QBF conte;
     alfa1 = conte % -101;
     |SI alfa1 = ".";
          conte = conte + ant_area;     || si la cuenta acaba con un punto
     |SINO;                             || se completa con el codigo
          conte = conte + ".";
     |FINSI;
|FPRC;

|PROCESO sstmp1_1;
     #4#0 = #11#0;
     #4#1 = #11#1;
     |LEE 000#4=;
|FPRC;

|DEFBUCLE sstmp1;        || por areas
     #11#1;
     ;
     #1#0,     1;
     #1#0, 99999;
     ;
     NULL, sstmp1_1, NULL, NULL, NULL, sstmp1_6;
     #4#0, #4#62, #4#1;
|FIN;

________________________________________ ASIENTO 1
|PROCESO nume1;
     |SI numcam = 43;
          nume1 = 8;
     |SINO;
          |SI numcam = 50;
               nume1 = 10;
          |SINO;
               nume1 = numcam - 12;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO tmp2_1;
     |HAZ nume1;
     impor = #11nume1;         || importe

     conte = #0numcam;         || cuenta
     |QBF conte;
     alfa1 = conte % -101;
     |SI alfa1 = ".";
          conte = conte + #11#1;   || si la cuenta acaba con un punto
     |SINO;                        || se completa con el codigo
          conte = conte + ".";
     |FINSI;

     |HAZ tmp1_1;
     #12#0 = #4#7;
     |LEE 000#12=;
     |SI FSalida = 0;|Y #0#44 = "S";|Y numcam ! 50;
          |SI numcam = 14;|Y #12#2 ! "            ";    conte = #12#2; |FINSI;
          |SI numcam = 15;|Y #12#3 ! "            ";    conte = #12#3; |FINSI;
          |SI numcam = 16;|Y #12#4 ! "            ";    conte = #12#4; |FINSI;
          |SI numcam = 17;|Y #12#5 ! "            ";    conte = #12#5; |FINSI;
          |SI numcam = 18;|Y #12#6 ! "            ";    conte = #12#6; |FINSI;
          |SI numcam = 19;|Y #12#7 ! "            ";    conte = #12#7; |FINSI;
          |SI numcam = 43;|Y #12#8 ! "            ";    conte = #12#8; |FINSI;
     |FINSI;

     |SI numcam = 14;    || sexto digito tot.devengado segun area
          |SI #4#62 = "8";         || admon
               |HAZ la_cuenta;
               alfa1 = conte % 105;
               alfa2 = conte % 7;
               conte = alfa1 + "1" + alfa2;
          |FINSI;
          |SI #4#62 = "9";         || gerencia
               |HAZ la_cuenta;
               alfa1 = conte % 105;
               alfa2 = conte % 7;
               conte = alfa1 + "2" + alfa2;
          |FINSI;
     |FINSI;

     |HAZ apuntes;
|FPRC;

|DEFBUCLE tmp2;          || por trabajadores
     #11#1;
     ;
     #1#0,     1;
     #1#0, 99999;
     ;
     NULL, tmp2_1;
|FIN;

|PROCESO tmp1_6;
     |HAZ tmp1_1;
     |SI sw_rup = 0;
          sw_rup = 1;
          ant_area = #4#62;
     |FINSI;
     |SI ant_area ! #4#62;
          |HAZ apuntes;
          ant_area = #4#62;
          impor = 0;
     |FINSI;
     |HAZ nume1;
     impor = impor + #11nume1;          || importe

     conte = #0numcam;                  || cuenta
     |QBF conte;
     alfa1 = conte % -101;
     |SI alfa1 = ".";
          conte = conte + ant_area;     || si la cuenta acaba con un punto
     |SINO;                             || se completa con el codigo
          conte = conte + ".";
     |FINSI;
|FPRC;

|PROCESO tmp1_1;
     #4#0 = #11#0;
     #4#1 = #11#1;
     |LEE 000#4=;
|FPRC;

|DEFBUCLE tmp1;          || por areas
     #11#1;
     ;
     #1#0,     1;
     #1#0, 99999;
     ;
     NULL, tmp1_1, NULL, NULL, NULL, tmp1_6;
     #4#0, #4#62, #4#1;
|FIN;

|PROCESO tmp0_5;
     conte = #0numcam;
     |HAZ apuntes;
|FPRC;

|PROCESO tmp0_1;
     |HAZ nume1;
     impor = impor + #11nume1;
|FPRC;

|DEFBUCLE tmp0;          || global
     #11#1;
     ;
     #1#0,     1;
     #1#0, 99999;
     ;
     NULL, tmp0_1, NULL, NULL, tmp0_5;
|FIN;
____________________________________/ GRABACION DEL TEMPORAL
|PROCESO labempax1;
     eltc1 = eltc1 + #2#20;
|FPRC;

|DEFBUCLE labempax;
     #2#1;
     ;
     #1#0,  1, #0#32,  0;
     #1#0, 99, #0#32, 19;
     ;
     NULL, labempax1;
|FIN;

|PROCESO nomcalca1;
     |DDEFECTO #11;
     #11#0 = #3#0;
     #11#1 = #3#1;
     |LEE 101#11=;
     FEstado = FSalida;

     |SI #0#52 = "M";         || mes
          || #11#2 = #11#2 + (#3#46 - #3#57 - #3#58 - #3#55 - #3#56); || total brutos
          #11#2 = #11#2 + (#3#46 - #3#57 - #3#58); || total brutos
          #11#3 = #11#3 + #3#75;                                   || total reten. IRPF
          #11#4 = #11#4 + (#3#48 + #3#49 + #3#50 + #3#51);         || total aport. S.S.
          #11#6 = #11#6 + #3#52;                                   || total anticipos 1
          #11#7 = #11#7 + #3#54;                                   || total en especie
          #11#8 = #11#8 + #3#53;                                   || total anticipos 2
          #11#9 = #11#9 + (#3#45 - (#3#57 + #3#58));               || ss empresa
          || #11#10 = #11#10 + (#3#57 + #3#58 + #3#55 + #3#56);       || prestac. s.s.
          #11#10 = #11#10 + (#3#57 + #3#58);       || prestac. s.s.
          el503 = el503 + #3#57;
          el504 = el504 + #3#58;
          || el555 = el555 + #3#55;
          || el556 = el556 + #3#56;
     |FINSI;
     |SI #0#52 = "P";         || paga
          #11#2 = #11#2 + #3#47;                                   || total brutos
          #11#3 = #11#3 + #3#17;                                   || total reten. IRPF
          #11#4 = 0;                                               || total aport S.S.
          #11#6 = 0;                                               || total anticipos 1
          #11#7 = 0;                                               || total en especie
          #11#8 = 0;                                               || total anticipos 2
          #11#9 = 0;                                               || ss empresa
          #11#10 = 0;                                              || prestac. s.s.
          el503  = 0;
          el504  = 0;
          el555  = 0;
          el556  = 0;
     |FINSI;
     |SI #0#52 = "A";         || ambos
          || #11#2 = #11#2 + (#3#46 + #3#47 - #3#57 - #3#58 - #3#55 - #3#56); || total brutos
          #11#2 = #11#2 + (#3#46 + #3#47 - #3#57 - #3#58); || total brutos
          #11#3 = #11#3 + (#3#75 + #3#17);                         || total reten. IRPF
          #11#4 = #11#4 + (#3#48 + #3#49 + #3#50 + #3#51);         || total aport. S.S.
          #11#6 = #11#6 + #3#52;                                   || total anticipos 1
          #11#7 = #11#7 + #3#54;                                   || total en especie
          #11#8 = #11#8 + #3#53;                                   || total anticipos 2
          #11#9 = #11#9 + (#3#45 - (#3#57 + #3#58));               || ss empresa
          || #11#10 = #11#10 + (#3#57 + #3#58 + #3#55 + #3#56);       || prestac. s.s.
          #11#10 = #11#10 + (#3#57 + #3#58);       || prestac. s.s.
          el503 = el503 + #3#57;
          el504 = el504 + #3#58;
          || el555 = el555 + #3#55;
          || el556 = el556 + #3#56;
     |FINSI;
     #11#5 = #11#2 + #11#10 - (#11#3+#11#4+#11#6+#11#7+#11#8); || liquido

     |SI FEstado = 0;    |GRABA 020#11a;     |FINSI;
     |SI FEstado ! 0;    |GRABA 020#11n;     |FINSI;
     |LIBERA #11;
|FPRC;

|DEFBUCLE nomcalca;
     #3#1;
     ;
     #1#0,     1, #0#32, 0;
     #1#0, 99999, #0#32, 9;
     ;
     NULL, nomcalca1;
|FIN;

|PROCESO calcula;
     el503 = 0;
     el504 = 0;
     el555 = 0;
     el556 = 0;
     |BUCLE nomcalca;
     eltc1 = 0;      || importe TC1      (DEBE/HABER)   (7-8)
     |BUCLE labempax;
|FPRC;
____________________________________/ LECTURA Y CALCULOS PRINCIPALES
|PROCESO conta1;
     alfa1 = Usuario;     |QBT alfa1;
     |NOME_DAT #11 alfa1;
     |DELINDEX #11;

     |ABRE #11;
     |HAZ calcula;
     |CIERRA #11;

     rsegu = 0;
     rpres = 0;

     |HAZ contador; asiento = #26#1;
     apunte = 0;
     |PARA numcam = 14; |SI numcam [ 21; |HACIENDO numcam = numcam + 1;
          |SI numcam = 20;     numcam = 43;   |FINSI;
          |SI numcam = 21;     numcam = 50;   |FINSI;

          |SI numcam = 14;     signe = "D";   |FINSI;   || total devengado
          |SI numcam = 50;     signe = "D";   |FINSI;   || prest.s.s.
          |SI numcam = 15;     signe = "H";   |FINSI;   || retencion IRPF
          |SI numcam = 16;     signe = "H";   |FINSI;   || retencion S.S.
          |SI numcam = 17;     signe = "H";   |FINSI;   || liquido
          |SI numcam = 18;     signe = "H";   |FINSI;   || anticipos 1
          |SI numcam = 19;     signe = "H";   |FINSI;   || en especie
          |SI numcam = 43;     signe = "H";   |FINSI;   || anticipos 2

          sw_rup = 0;
          impor = 0;
          desglo = numcam + 22;  || puntero desglose
          |SI numcam = 43;     desglo = 42;   |FINSI;   || anticipos 2
          |SI numcam = 50;     desglo = 51;   |FINSI;   || prest.s.s.

          |SI #0desglo = 0;   |BUCLE tmp0;                  |FINSI;
          |SI #0desglo = 1;   |BUCLE tmp1;   |HAZ apuntes;  |FINSI;
          |SI #0desglo = 2;   |BUCLE tmp2;                  |FINSI;

          |SI numcam = 43;     numcam = 20;   |FINSI;
          |SI numcam = 50;     numcam = 21;   |FINSI;
     |SIGUE;

     |SI #0#52 = "M";|O #0#52 = "A";
          |HAZ contador; asiento = #26#1;
          apunte = 0;

          |PARA numcam = 20; |SI numcam [ 21; |HACIENDO numcam = numcam + 1;
               sw_rup = 0;
               impor = 0;
               cuadra = 0;
               desglo = numcam + 25;
               |SI numcam = 20;     signe = "D";   |FINSI;
               |SI numcam = 21;     signe = "H";   |FINSI;

               |SI #0desglo = 0;
                    ||impor = eltc1 - rsegu + rpres;
                    impor = eltc1 - rsegu;
                    impor = impor + el503 + el504 + el555 + el556;  || suma de prestaciones Tique 224 / cl: 160
                    conte = #0numcam;
                    |HAZ apuntes;
               |FINSI;
               |SI #0desglo = 1;
                    |BUCLE sstmp1;
                    ||impor = ((eltc1 - rsegu + rpres) - cuadra);
                    impor = ((eltc1 - rsegu) - cuadra);
                    impor = impor + el503 + el504 + el555 + el556;  || suma de prestaciones Tique 224 / cl: 160
                    |HAZ apuntes;
               |FINSI;
               |SI #0desglo = 2;
                    |BUCLE sstmp2;
                    ||impor = ((eltc1 - rsegu + rpres) - cuadra);
                    impor = ((eltc1 - rsegu) - cuadra);
                    impor = impor + el503 + el504 + el555 + el556;  || suma de prestaciones Tique 224 / cl: 160
                    |HAZ apuntes;
               |FINSI;
          |SIGUE;
     |FINSI;

     |DELINDEX #11;
|FPRC;

|DEFBUCLE labempre;
     #1#1;
     ;
     #0#12;
     #0#13;
     ;
     NULL, conta1;
|FIN;

|PROCESO bucles;
     |SI #0#1 ! 0;
          |ABRE #1;
          |PARA empre = 1; |SI empre [ 11; |HACIENDO empre = empre + 1;
               #1#0 = #0empre;
               |LEE 000#1=;
               |SI FSalida = 0;
                    |HAZ conta1;
               |FINSI;
          |SIGUE;
          |CIERRA #1;
     |SINO;
          |BUCLE labempre;
     |FINSI;
|FPRC;

|PROCESO tipo3;
     |DDEFECTO #10;
     #10#2 = 2;          || diario
     #10#3 = "AAAAMMDD"; || fecha
     #10#4 = 6;          || asiento
     #10#5 = 4;          || apunte
     #10#6 = 10;         || cuenta
     #10#7 = 1;          || signo
     #10#8 = 10;         || importe debe
     #10#9 = 0;          || importe haber
     #10#10 = 20;        || descripcion
     |DFICE alfa1;
     alfa1 = alfa1 + "arten001.spo";
     #10#11 = alfa1;     || fichero destino
     #10#12 = "";        || sep. campos
     #10#13 = 13;        || sep. lineas 1
     #10#14 = 10;        || sep. lineas 2
     #10#15 = -1;        || final fichero

     fabre = #10#11;
     |QBT fabre;
     fabre = "wb  " + fabre;
     |FABRE fabre;
     |SI FSalida ] 0;
          fcanl = FSalida;

          sep_campo = #10#12;
          |SI sep_campo = " ";     sep_campo = "";  |FINSI;

          |ABRE #4;
          |ABRE #5;
          |ABRE #12;
          |HAZ bucles;
          |CIERRA #4;
          |CIERRA #5;
          |CIERRA #12;

          |SI #10#15 ! -1; fgrab = &#10#15;    |HAZ grabalo;  |FINSI;

          |FCIERRA fcanl;

          |HAZ PasaAgicont;
     |SINO;
          alfa1 = "    [" + (fabre % 5) + "] no accesible ...";
          |MENAV alfa1;
     |FINSI;
     Pos = -1;
|FPRC;
____________________________ PROCESOS DEL nomcontd
|VARIABLES;
    &bus_emp = 0;
    &bus_tra = 0;
    &bus_cla = 0;
|FIN;

|PROCESO busqueda; |TIPO 11;
    bus_emp = 0;
    bus_tra = 0;
    bus_cla = 0;
|SI FSalida =  9;  bus_cla = 1;  |FINSI;
|SI FSalida = 10;  bus_cla = 2;  |FINSI;
|SI FSalida = 11;  bus_cla = 3;  |FINSI;
|SI bus_cla ! 0;
    |SUB_EJECUTA "nombusca";
    |DDEFECTO #4;
    |ABRE #4;
        #4#0 = bus_emp;
        #4#1 = bus_tra;
    |LEE 000#4=;
    |SI FSalida = 0;
            #12#0 = #4#7;    |PINTA #12#0;
            #12#1 = #4#2;    |PINTA #12#1;
        |ERROR;
        |PTEC 802;
    |FINSI;
    |CIERRA #4;
|FINSI;
|FPRC;

|PROCESO selecciona; |TIPO 7;

|SI #0#36 = 2; alfa1 = "S";   |SINO;    alfa1 = "N";   |FINSI;
|C_M #12#2, 1, alfa1;
|SI alfa1="S";|Y #12#2 = "            "; alfa1=#0#14; |QBF alfa1; #12#2=alfa1+#4#1; |PINTA #12#2; |FINSI;

|SI #0#37 = 2; alfa1 = "S";   |SINO;    alfa1 = "N";   |FINSI;
|C_M #12#3, 1, alfa1;
|SI alfa1="S";|Y #12#3 = "            "; alfa1=#0#15; |QBF alfa1; #12#3=alfa1+#4#1; |PINTA #12#3; |FINSI;

|SI #0#38 = 2; alfa1 = "S";   |SINO;    alfa1 = "N";   |FINSI;
|C_M #12#4, 1, alfa1;
|SI alfa1="S";|Y #12#4 = "            "; alfa1=#0#16; |QBF alfa1; #12#4=alfa1+#4#1; |PINTA #12#4; |FINSI;

|SI #0#39 = 2; alfa1 = "S";   |SINO;    alfa1 = "N";   |FINSI;
|C_M #12#5, 1, alfa1;
|SI alfa1="S";|Y #12#5 = "            "; alfa1=#0#17; |QBF alfa1; #12#5=alfa1+#4#1; |PINTA #12#5; |FINSI;

|SI #0#40 = 2; alfa1 = "S";   |SINO;    alfa1 = "N";   |FINSI;
|C_M #12#6, 1, alfa1;
|SI alfa1="S";|Y #12#6 = "            "; alfa1=#0#18; |QBF alfa1; #12#6=alfa1+#4#1; |PINTA #12#6; |FINSI;

|SI #0#42 = 2; alfa1 = "S";   |SINO;    alfa1 = "N";   |FINSI;
|C_M #12#8, 1, alfa1;
|SI alfa1="S";|Y #12#8 = "            "; alfa1=#0#43; |QBF alfa1; #12#8=alfa1+#4#1; |PINTA #12#8; |FINSI;

|SI #0#41 = 2; alfa1 = "S";   |SINO;    alfa1 = "N";   |FINSI;
|C_M #12#7, 1, alfa1;
|SI alfa1="S";|Y #12#7 = "            "; alfa1=#0#19; |QBF alfa1; #12#7=alfa1+#4#1; |PINTA #12#7; |FINSI;
|FPRC;

|PROCESO comprueba; |TIPO 7;
|ABRE #4;
|SELECT #2#4;
    #4#7 = #12#0;
|LEE 000#4=;
|SI FSalida = 0;
        #12#1 = #4#2;
    |PINTA #12#1;
|SINO;
    |MENAV "    D.N.I. inaccesible o inexistente. Utilice la consulta (<F3>) ...";
    |PTEC 807;
|FINSI;
|CIERRA #4;
|FPRC;
