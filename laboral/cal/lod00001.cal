|FICHEROS;
    lod00001 #0;
    labempre #1;    || empresas
    labtraba #2;    || trabajadores
    labregis #4;    || registro de contratos
    labcon00 #5;    || definiciones de decretos
    labclaus #6;    || fichero de clausulas adicionales
    labclafi #8;    || tmp de clausulas adicionales (hoja adicional)
    contrato #1000; || clausulas adicionales

     pdfimtmp #100;
     nomequiv #101;  || equivalencia contratos
     labcontr;       || control aplicacion
     nomconca;
|FIN;

|VARIABLES;
    alfa1 = "";
    alfa2 = "";
    alfa3 = "";
    alfa4 = "";
    nume1 = 0;
    nume2 = 0;
    nume3 = 0;
    nume4 = 0;
    para1 = 0;
    para2 = 0;
    para3 = 0;
    para4 = 0;
    fech1 = @;
    fech2 = @;
    fech3 = @;
    fech4 = @;
    elsw1 = 0;
    elsw2 = 0;
    elsw3 = 0;
    elsw4 = 0;
    FEstado = 0;
    seleccio = 0;
    modo = 0;
    sw_rup = 0;

    &cbasica = 0;
    &tp = 0;
    &tiporet = "";
    &contador = 0;
    informe = "";
    nomimp = "";
    vuelta = 0;

    aContrato = "";
    linepunt = "";
    lineespa = "";
    clausula = 0;
    campoclau = 0;
    registro = 0;
    &texto = "";
    &anyo = "";
    &mes = "";
    &dia = "";
    dia1 = "";
    any1 = "";
    fec1 = "";
    fec2 = 0;
    meslet = "";

    &aDuplexSi = "";
    &aDuplexNo = "";

     nCampo = 0;
     aAlfa = "";
     aAlfa1 = "";
     aAlfa2 = "";
     aAlfa3 = "";
     &aArchivo = "";
     &swCopia = 0;
     aValor = "";
     aCadena = "";
     aCadenaSal = "";
     aCampo = "";
     nTipo = 0;
     nAscii = 0;
     aCaracter = "";
     aCaracterSal = "";
     aLongitud = "";
     nLongitud = 0;
     nPos = 0;
     nBusca = 0;
     &aListaFich = "";
     nNume = 0;
     &nCuantos = 0;
     &nContador = 0;
     nclausulas = 0;
     nClaus = 0;
     aClaus = "";
     aEmp = "";
     aTra = "";
     aCon = "";
     nNCA = 0;
     &nVoypor = 0;
     swDias1 = 0;
     swDias2 = 0;
     swLeidas = 0;
     aLon = "";
     nLon = 0;
     aLugar = "";
|FIN;
____________________________________/
|PROCESO repinta; |TIPO 7;
      |POPV;
|FPRC;

|PROCESO leeclaus1;
     |SI #0clausula ! 0;
          #6#1 = #0clausula;
          |LEE 000#6=;
          |PARA campoclau = 3; |SI campoclau [ 20; |Y linepunt ! #6campoclau;
                                                   |Y lineespa ! #6campoclau;
                               |HACIENDO campoclau = campoclau + 1;
               registro = registro + 1;
               #8#0 = registro;
               #8#1 = #6campoclau;       || carga clausulas en temporal
               |GRABA 020#8n;
               nclausulas = nclausulas + 1;

               |SI nclausulas [ 18;
                    nCampo = nclausulas + 81;
                    #0nCampo = #6campoclau;
                    |PINTA #0nCampo;
               |FINSI;
          |SIGUE;
     |FINSI;
|FPRC;

|PROCESO leeclaus;
     swLeidas = 1;
     |PUSHV 05012380;
     nCampo = 81;
     alfa1 = Usuario;
     |QBF alfa1;
     |NOME_DAT #8 alfa1;
     |DELINDEX #8;
     |PDEFECTO #0, 82, 100;
     |ABRE #6;
     |ABRE #8;
     linepunt = "." * 76;
     lineespa = " " * 76;
     registro = 0;
     nclausulas = 0;
     |PARA clausula = 73; |SI clausula [ 77; |HACIENDO clausula = clausula + 1;
           |HAZ leeclaus1;
     |SIGUE;
     |CIERRA #6;
     |CIERRA #8;

     fec1 = #0#33;
     |HAZ meseslet;
     dia = dia1;
     mes = meslet;
     anyo = any1;

     #1000#46 = #labempre#6;
     #1000#1 = #labtraba#0;
     #1000#2 = #labtraba#1;
     contador = #labregis#0;
|FPRC;

|PROCESO meseslet;               || SUBRUTINA GENERAL DE TRANSFORMACION
     dia1 = fec1 % 102;           || DE FECHAS A LETRAS
     any1 = fec1 % 704;
     fec1 = fec1 % 402;
     fec2 = fec1;
     |SI fec2 = 1;   meslet = "Enero";   |FINSI;
     |SI fec2 = 2;   meslet = "Febrero";   |FINSI;
     |SI fec2 = 3;   meslet = "Marzo";   |FINSI;
     |SI fec2 = 4;   meslet = "Abril";   |FINSI;
     |SI fec2 = 5;   meslet = "Mayo";   |FINSI;
     |SI fec2 = 6;   meslet = "Junio";   |FINSI;
     |SI fec2 = 7;   meslet = "Julio";   |FINSI;
     |SI fec2 = 8;   meslet = "Agosto";   |FINSI;
     |SI fec2 = 9;   meslet = "Septiembre";   |FINSI;
     |SI fec2 = 10;  meslet = "Octubre";   |FINSI;
     |SI fec2 = 11;  meslet = "Noviembre";   |FINSI;
     |SI fec2 = 12;  meslet = "Diciembre";   |FINSI;
|FPRC;

|PROCESO siexiste; |TIPO 0;
     |SI #0Campo ! 0;
         |ABRE #6;
             #6#1 = #0Campo;
         |LEE 000#6=;
         |SI FSalida ! 0;
             |MENAV "    No existe clausula !!!";
             |ERROR;
         |FINSI;
         |CIERRA #6;
     |FINSI;
|FPRC;

|PROCESO ConDuracion; |TIPO 7;
    nume1 = #0#25 + #0#26;
|SI nume1 ! 0;
    |C_M #0#72, 1, "N";
|SINO;
    |C_M #0#72, 1, "S";
|FINSI;
|FPRC;

|PROCESO duracion; |TIPO 7;
    |C_M #0#27, 1, "S";
    #0#27 = "          ";
    nume1 = #0#25 + #0#26;
|SI nume1 ! 0; |Y #0#24 ! "          ";
        nume1 = #0#25 / 1000;            || meses a sumar en formato suma
        nume2 = (#0#26 - 1) + nume1;     || fecha a sumar en formato suma
        fech1 = #0#24;
        fech1 = fech1 + nume2;
        #0#27 = fech1;
|SINO;
    |C_M #0#27, 1, "N";
|FINSI;
|PINTA #0#27;
|FPRC;

|PROCESO CodigosNuevos;
     |QBF aContrato;
     aContrato = ("000" + aContrato) % -103;
     |ABRE #nomequiv;
     #nomequiv#0 = aContrato;
     |LEE 000#nomequiv.=;
     |SI FSalida = 0;
          |SI #labtraba#42 = "X";|O #labtraba#42 = "T";|O #labtraba#42 = "P";
               aContrato = #nomequiv#2;      || tiempo parcial
          |SINO;
               aContrato = #nomequiv#1;      || tiempo completo
          |FINSI;
     |FINSI;
     |CIERRA #nomequiv;
|FPRC;

|PROCESO trabajador; |TIPO 0;
     || lee definiciones decretos y calcula si es tiempo parcial

|DDEFECTO #5;  |ABRE #5;  #5#0 = #2#45;  |LEE 000#5=;  |CIERRA #5;

|SI #2#45 = "024";|O #2#45 = "064";  tp = 1;  |SINO;  tp = 0;   |FINSI;

     aContrato = #2#45;
     |HAZ CodigosNuevos;
     #2#45 = aContrato;
|FPRC;
____________________________________/
|PROCESO PreClausulas;
     |ABRE #8;
     |PARA para1 = 1; |SI para1 [ 18; |HACIENDO para1 = para1 + 1;
          #8#0 = para1;
          |LEE 000#8=;
          FEstado = FSalida;
          nCampo = para1 + 81;
          |SI linepunt ! #0nCampo; |Y lineespa ! #0nCampo;
               #8#1 = #0nCampo;
               |SI FEstado = 0;
                    |GRABA 020#8a;
               |SINO;
                    |GRABA 020#8n;
               |FINSI;
          |FINSI;
     |SIGUE;
     |CIERRA #8;
|FPRC;

|PROCESO Clausulas;
     |PARA para1 = 1; |SI para1 [ #0#78; |HACIENDO para1 = para1 + 1;
          |INFOR "labclafi";
          |ABRE #8;
          |LEE 000#8p;
          |PARA; |SI FSalida = 0; |HACIENDO;
               texto = #8#1;
               |PRINT;
               |LEE 000#8s;
          |SIGUE;
          |PIEINF;
          |FININF;
          |CIERRA #8;
     |SIGUE;
     |PARA para1 = 1; |SI para1 [ #0#78; |HACIENDO para1 = para1 + 1;
          |INFOR "laxclafi";
          |ABRE #8;
          |LEE 000#8p;
          |PARA; |SI FSalida = 0; |HACIENDO;
               texto = #8#1;
               |PRINT;
               |LEE 000#8s;
          |SIGUE;
          |PIEINF;
          |FININF;
          |CIERRA #8;
     |SIGUE;
|FPRC;

|PROCESO graba_reg;
    #4#1 = #0#49;        || empresa
    #4#2 = #0#2;         || trabajador
    #4#3 = #2#7;         || dni
    #4#4 = #2#45;        || contrato
    #4#5 = #5#1;         || definicion decreto
    #4#6 = #5#2;         || real decreto
    #4#7 = #0#24;        || desde contrato
    #4#8 = #0#27;        || hasta contrato
    #4#9 = #0#25;        || meses duracion
    #4#10= #0#26;        || dias duracion
    #4#11= "";           || fecha presentacion
    #4#12= #2#24;        || oficina inem
    #4#13= #0#14;        || localidad inem (empresa ett)
    #4#14= "";           || nro.inem
    #4#15= 0;            || nro.prorrogas
    #4#16= 0;            || meses prorrogas
    #4#17= 0;            || dias prorrogas
    #4#18= #4#9;         || acumulado meses
    #4#19= "";           || desde prorroga
    #4#20= "";           || hasta prorroga
    #4#21= ~;            || fecha alta en fichero
    #4#22= #4#10;        || acumulado dias
    #4#23= #4#7;         || desde contrato (fecha)
    #4#24= #4#8;         || hasta contrato (fecha)
    #4#25= #4#19;        || desde prorroga (fecha)
    #4#26= #4#20;        || hasta prorroga (fecha)

|SI FEstado ! 0;    |GRABA 020#4n; |FINSI;
|SI FEstado = 0;    |GRABA 020#4a; |FINSI;
|FPRC;

|PROCESO registro;
|ABRE #4;
|SELECT #2#4;
    #4#1 = #0#49;   || empresa
    #4#2 = #0#2;    || trabajador
|LEE 000#4=;
    FEstado = FSalida;
|SELECT #1#4;
|SI FEstado ! 0;
    |LEE 000#4u;
    |SI FSalida = 0;
            contador = #4#0 + 1;
    |SINO;
            contador = 1;
    |FINSI;
|SINO;
        contador = #4#0;
|FINSI;

|DDEFECTO #4;
    #4#0 = contador;
|LEE 101#4=;
    FEstado = FSalida;

|SI FEstado = 0;
    |AVISO;
    |CONFI "    STrabajador con contrato registrado, actualizar (S/N): ";
    |SI FSalida = 0;
        |HAZ graba_reg;
    |FINSI;
|SINO;
    |HAZ graba_reg;
|FINSI;
|LIBERA #4;
|CIERRA #4;
|FPRC;

|PROCESO imprime;
|INFOR informe;
|PRINT;
|PIEINF;
|FININF;
|FPRC;

|PROCESO contrato;
|SI #0#45 = "D";    tiporet = "diarias  ";   |FINSI;
|SI #0#45 = "M";    tiporet = "mensuales";   |FINSI;
|SI #0#45 = "S";    tiporet = "semanales";   |FINSI;
|SI #0#45 = "H";    tiporet = "hora     ";   |FINSI;

|SI vuelta = 1;     informe = "lod20005";    |HAZ imprime;  |FINSI;
|SI vuelta = 2;     informe = "lod20006";    |HAZ imprime;  |FINSI;
|FPRC;

|PROCESO cpd;
|SI vuelta = 1;     informe = "lod20001";    |HAZ imprime;  |FINSI;
|SI vuelta = 2;     informe = "lod20002";    |HAZ imprime;  |FINSI;
|FPRC;

|PROCESO sustituye2;
     nAscii = & aCaracter;
     aCaracterSal = aCaracter;
     |SI nAscii = 160;             || el caracter  
          aCaracterSal = "\341";
     |FINSI;
     |SI nAscii = 130;             || el caracter ‚
          aCaracterSal = "\351";
     |FINSI;
     |SI nAscii = 161;             || el caracter ¡
          aCaracterSal = "\355";
     |FINSI;
     |SI nAscii = 162; |O nAscii = 243;            || el caracter ¢
          aCaracterSal = "\363";
     |FINSI;
     |SI nAscii = 163;             || el caracter £
          aCaracterSal = "\372";
     |FINSI;
     |SI nAscii = 164;             || el caracter ¤
          aCaracterSal = "\361";
     |FINSI;
     |SI nAscii = 241;             || el caracter ¤ (otro codigo ¨?)
          aCaracterSal = "\361";
     |FINSI;
     |SI nAscii = 165;             || el caracter ¥
          aCaracterSal = "\321";
     |FINSI;
     |SI nAscii = 129;
          aCaracterSal = "\374";   || el caracter u con dieresis
     |FINSI;
     |SI nAscii = 154;
          aCaracterSal = "\334";   || el caracter U (mayuscula) con dieresis
     |FINSI;
     |SI nAscii = 167;
          aCaracterSal = "\272";   || el caracter §
     |FINSI;
     |SI nAscii = 166;
          aCaracterSal = "\252";   || el caracter ¦
     |FINSI;
     |SI aCaracter = "(";
          aCaracterSal = "\(";
     |FINSI;
     |SI aCaracter = ")";
          aCaracterSal = "\)";
     |FINSI;
|FPRC;

|PROCESO sustituye;
     aCadenaSal = "";
     aLongitud = aCadena % 0;
     nLongitud = aLongitud;
     |PARA nPos = 1; |SI nPos [ nLongitud; |HACIENDO nPos = nPos + 1;
          nBusca = (nPos * 100) + 1;
          aCaracter = aCadena % nBusca;
          |HAZ sustituye2;
          aCadenaSal = aCadenaSal + aCaracterSal;
     |SIGUE;
|FPRC;

|PROCESO GrabaTemporal;
     aAlfa1 = aValor % 101;
     |SI aAlfa1 = ".";
          aValor = "";
     |FINSI;
     |QBF aValor;
     aCadena = aValor;
     |HAZ sustituye;
     aValor = aCadenaSal;
     #pdfimtmp#0 = aCampo;
     #pdfimtmp#1 = aValor;
     #pdfimtmp#2 = nTipo;
     |GRABA 020#pdfimtmp.n;
|FPRC;

|PROCESO EmpETTComunes
     aCampo = "ettrep"; aValor = #0#3; |HAZ GrabaTemporal;
     aCampo = "ettdnirep"; aValor = #0#4; |HAZ GrabaTemporal;
     aCampo = "ettenconc"; aValor = #0#5; |HAZ GrabaTemporal;
     aCampo = "ettempresa"; aValor = #0#6; |HAZ GrabaTemporal;
     aCampo = "ettcif"; aValor = #0#7; |HAZ GrabaTemporal;
     aCampo = "ettctacot"; aValor = #0#8; |HAZ GrabaTemporal;

     aAlfa1 = #0#9; |QBF aAlfa1;
     aAlfa2 = #0#10; |QBF aAlfa2;
     aAlfa3 = #0#11; |QBF aAlfa3;
     aAlfa = aAlfa1 + ", " + aAlfa2 + " - " + aAlfa3;
     aCampo = "ettdomic"; aValor = aAlfa; |HAZ GrabaTemporal;

     aAlfa1 = #0#12; |QBF aAlfa1; aAlfa1 = ("00" + aAlfa1) % -102;
     aAlfa2 = #0#13; |QBF aAlfa2; aAlfa2 = ("000" + aAlfa2) % -103;
     aAlfa1 = aAlfa1 + aAlfa2;
     aAlfa2 = #0#14; |QBF aAlfa2;
     aAlfa3 = #0#15; |QBF aAlfa3; aAlfa3 = "(" + aAlfa3 + ")";
     aAlfa = aAlfa1 + " - " + aAlfa2 + " " + aAlfa3;
     aCampo = "ettlocali"; aValor = aAlfa; |HAZ GrabaTemporal;

     aCampo = "ettnumauto"; aValor = #0#16; |HAZ GrabaTemporal;
     aCampo = "ettvigencia"; aValor = #0#71; |HAZ GrabaTemporal;
|FPRC;

|PROCESO TrabComunes;
     aCampo = "nombretra"; aValor = #2#2; |HAZ GrabaTemporal;
     |SI swCopia = 1;
          aCampo = "fecnactra"; aValor = #2#11; |HAZ GrabaTemporal;
          aCampo = "dnitra"; aValor = #2#7; |HAZ GrabaTemporal;
          aAlfa1 = #2#3; |QBF aAlfa1;
          aAlfa2 = #2#4; |QBF aAlfa2;
          aAlfa3 = #2#5; |QBF aAlfa3;
          aAlfa = aAlfa1 + ", " + aAlfa2 + " - " + aAlfa3 + " " + #2#6;
          aCampo = "estudiostra"; aValor = #2#12; |HAZ GrabaTemporal;
          aCampo = "codestudtra"; aValor = #2#73; |HAZ GrabaTemporal;
     |SINO;
          aAlfa = #2#6;
     |FINSI;
     |QBF aAlfa;
     aCampo = "domictra"; aValor = aAlfa; |HAZ GrabaTemporal;
|FPRC;

|PROCESO EmpUsuComunes
     aCampo = "usuempresa"; aValor = #0#53; |HAZ GrabaTemporal;
     aCampo = "usucif"; aValor = #0#54; |HAZ GrabaTemporal;
     aCampo = "usuctacot"; aValor = #0#55; |HAZ GrabaTemporal;

     aAlfa1 = #0#56; |QBF aAlfa1;
     aAlfa2 = #0#57; |QBF aAlfa2;
     aAlfa3 = #0#58; |QBF aAlfa3;
     aAlfa = aAlfa1 + ", " + aAlfa2 + " - " + aAlfa3;
     aCampo = "usudomic"; aValor = aAlfa; |HAZ GrabaTemporal;

     aAlfa1 = #0#59; |QBF aAlfa1; aAlfa1 = ("00" + aAlfa1) % -102;
     aAlfa2 = #0#60; |QBF aAlfa2; aAlfa2 = ("000" + aAlfa2) % -103;
     aAlfa1 = aAlfa1 + aAlfa2;
     aAlfa2 = #0#61; |QBF aAlfa2;
     aAlfa3 = #0#62; |QBF aAlfa3; aAlfa3 = "(" + aAlfa3 + ")";
     aAlfa = aAlfa1 + " - " + aAlfa2 + " " + aAlfa3;
     aCampo = "usulocali"; aValor = aAlfa; |HAZ GrabaTemporal;
|FPRC;

|PROCESO CenUsuComunes;
     aAlfa1 = #0#63; |QBF aAlfa1;
     aAlfa2 = #0#64; |QBF aAlfa2;
     aAlfa3 = #0#65; |QBF aAlfa3;
     aAlfa = aAlfa1 + ", " + aAlfa2 + " - " + aAlfa3;
     aCampo = "usudomicen"; aValor = aAlfa; |HAZ GrabaTemporal;

     aAlfa1 = #0#66; |QBF aAlfa1; aAlfa1 = ("00" + aAlfa1) % -102;
     aAlfa2 = #0#67; |QBF aAlfa2; aAlfa2 = ("000" + aAlfa2) % -103;
     aAlfa1 = aAlfa1 + aAlfa2;
     aAlfa2 = #0#68; |QBF aAlfa2;
     aAlfa3 = #0#69; |QBF aAlfa3; aAlfa3 = "(" + aAlfa3 + ")";
     aAlfa = aAlfa1 + " - " + aAlfa2 + " " + aAlfa3;
     aCampo = "usulocalicen"; aValor = aAlfa; |HAZ GrabaTemporal;
|FPRC;

|PROCESO FechasComunes;
     nNume = #0#25 + #0#26;
     fec1 = #0#24;
     |HAZ meseslet;
     aAlfa1 = #0#24; |QBF aAlfa1;
     aAlfa1 = (aAlfa1 % 102) + " de " + meslet + " de " + (aAlfa1 % -104);
     aAlfa2 = "";
     |SI nNume ! 0;
          fec1 = #0#27;
          |HAZ meseslet;
          aAlfa2 = #0#27; |QBF aAlfa2;
          aAlfa2 = (aAlfa2 % 102) + " de " + meslet + " de " + (aAlfa2 % -104);
          aAlfa = "Desde el " + aAlfa1 + " hasta el " + aAlfa2;
     |SINO;
          fec1 = #0#72;
          |HAZ meseslet;
          aAlfa2 = #0#72; |QBF aAlfa2;
          aAlfa = "Desde el " + aAlfa1 + " hasta " + aAlfa2;
     |FINSI;
     aCampo = "duracion"; aValor = aAlfa; |HAZ GrabaTemporal;
|FPRC;

|PROCESO FirmaComunes;
     aCampo = "locafir"; aValor = #1#6; |HAZ GrabaTemporal;
     aAlfa = #0#33;
     aAlfa1 = aAlfa % 102;
     fec1 = #0#33;
     |HAZ meseslet;
     aAlfa2 = meslet;
     aAlfa3 = aAlfa % -104;
     aCampo = "diafir"; aValor = aAlfa1; |HAZ GrabaTemporal;
     aCampo = "mesfir"; aValor = aAlfa2; |HAZ GrabaTemporal;
     aCampo = "anyfir"; aValor = aAlfa3; |HAZ GrabaTemporal;
|FPRC;

|PROCESO RegistroComunes;
     aCampo = "registro";
     aEmp = #2#0; aEmp = "00000" + aEmp; aEmp = aEmp % -105;
     aTra = #2#1; aTra = "00000" + aTra; aTra = aTra % -105;
     aCon = contador; aCon = "000000" + aCon; aCon = aCon % -105;
     aValor = aEmp + " / " + aTra + " / " + aCon;
     |HAZ GrabaTemporal;
|FPRC;

|PROCESO dias;
     |SI nNume = 4; |Y swDias1 = 0;
          aCampo = "dias1"; aValor = aAlfa - ","; |HAZ GrabaTemporal;
          aAlfa = "";
          swDias1 = 1;
     |FINSI;
|FPRC;

|PROCESO diasfin;
     aCampo = "dias1";
     aLon = aAlfa % 0;
     nLon = aLon;
     nLon = nLon - 2;
     nLon = 100 + nLon;
     aAlfa = aAlfa % nLon;
     aValor = aAlfa;
     |HAZ GrabaTemporal;
/*
     |SI nNume [ 4; |Y swDias1 = 0;
          aCampo = "dias1"; aValor = aAlfa - ","; |HAZ GrabaTemporal;
          aAlfa = "";
     |FINSI;
     |SI nNume > 4;
          aCampo = "dias2"; aValor = aAlfa - ","; |HAZ GrabaTemporal;
     |FINSI;
*/
|FPRC;

|PROCESO PintaHorario; |TIPO 7;
     aAlfa1 = #0#23;
     aAlfa2 = #0#112;
     |QBF aAlfa1;
     |QBF aAlfa2;

     |SI aAlfa1 ! "";
          aAlfa1 = #0#80;
          |QBF aAlfa1;
          |SI aAlfa1 = "";
               #0#80 = #0#23;
               |PINTA #0#80;
          |FINSI;
     |FINSI;
     |SI aAlfa2 ! "";
          aAlfa2 = #0#113;
          |QBF aAlfa2;
          |SI aAlfa2 = "";
               #0#113 = #0#112;
               |PINTA #0#113;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO PintaConvenio; |TIPO 7;
     aAlfa = #0#81;
     |QBF aAlfa;
     |SI aAlfa = "";
          |ABRE #nomconca;
          #nomconca#0 = #labempre#1;
          |LEE 000#nomconca.=;
          |SI FSalida = 0;
               #0#81 = #nomconca#1;
               |PINTA #0#81;
          |FINSI;
          |CIERRA #nomconca;
     |FINSI;
|FPRC;

|PROCESO Lugar; |TIPO 7;
     aAlfa = #0#106;
     |QBF aAlfa;
     |SI aAlfa = "";
          aLugar = "";
          aAlfa = #0#63; |QBF aAlfa;
          |SI aAlfa ! ""; aLugar = aLugar + aAlfa + ", "; |FINSI;
          aAlfa = #0#64; |QBF aAlfa;
          |SI aAlfa ! ""; aLugar = aLugar + aAlfa + " "; |FINSI;
          aAlfa = #0#65; |QBF aAlfa;
          |SI aAlfa ! ""; aLugar = aLugar + "- " + aAlfa + ", "; |FINSI;
          aAlfa1 = #0#66; |QBF aAlfa1; aAlfa1 = ("00" + aAlfa1) % -102;
          aAlfa2 = #0#67; |QBF aAlfa2; aAlfa2 = ("000" + aAlfa2) % -103;
          aAlfa = aAlfa1 + aAlfa2;
          |SI aAlfa ! "00000"; aLugar = aLugar + "(" + aAlfa + ") "; |FINSI;
          aAlfa = #0#68; |QBF aAlfa;
          |SI aAlfa ! ""; aLugar = aLugar + aAlfa + " "; |FINSI;
          aAlfa = #0#69; |QBF aAlfa;
          |SI aAlfa ! ""; aLugar = aLugar + "- " + aAlfa; |FINSI;

          #0#106 = aLugar;
          |PINTA #0#106;
     |FINSI;
|FPRC;

|PROCESO CargaCamposCDD;
     aCampo = "contrato"; aValor = #2#45; |HAZ GrabaTemporal;

     |HAZ EmpETTComunes;

     |HAZ TrabComunes;

     |SI swCopia = 1;
          aCampo = "nafsstra"; aValor = #2#75; |HAZ GrabaTemporal;
     |SINO;
          aCampo = "nafsstra"; aValor = ""; |HAZ GrabaTemporal;
     |FINSI;

     |HAZ EmpUsuComunes

     |SI #2#45 = "401"; |O #2#45 = "501";
          aCampo = "cuadroa"; aValor = "On"; |HAZ GrabaTemporal;
          aCampo = "objetivoa"; aValor = #0#79; |HAZ GrabaTemporal;
     |FINSI;

     |SI #2#45 = "402"; |O #2#45 = "502";
          aCampo = "cuadrob"; aValor = "On"; |HAZ GrabaTemporal;
          aCampo = "objetivob"; aValor = #0#79; |HAZ GrabaTemporal;
     |FINSI;

     |SI #2#45 = "410"; |O #2#45 = "510";
          aCampo = "cuadroc"; aValor = "On"; |HAZ GrabaTemporal;
          aCampo = "objetivoc"; aValor = #0#79; |HAZ GrabaTemporal;
     |FINSI;

     |SI #2#45 = "100"; |O #2#45 = "200";
          aCampo = "cuadrod"; aValor = "On"; |HAZ GrabaTemporal;
          aCampo = "objetivod"; aValor = #0#79; |HAZ GrabaTemporal;
     |FINSI;

     aCampo = "puesto"; aValor = #2#238; |HAZ GrabaTemporal;
     aCampo = "departamento"; aValor = #2#19; |HAZ GrabaTemporal;
     aCampo = "categoria"; aValor = #2#18; |HAZ GrabaTemporal;

     |HAZ CenUsuComunes;

     aCampo = "jornada"; aValor = #0#36; |HAZ GrabaTemporal;

     aValor = "";
     |SI #0#100 = "D"; aValor = "diarias"; |FINSI;
     |SI #0#100 = "S"; aValor = "semanales"; |FINSI;
     |SI #0#100 = "M"; aValor = "mensuales"; |FINSI;
     |SI #0#100 = "A"; aValor = "anuales"; |FINSI;
     aCampo = "tipohoras"; |HAZ GrabaTemporal;

     aValor = #0#80; |QBF aValor;
     aValor = aValor + " " + #0#113; |QBF aValor;
     aCampo = "horario";
     |HAZ GrabaTemporal;

     || aCampo = "horario"; aValor = #0#80; |HAZ GrabaTemporal;
     aCampo = "convenio"; aValor = #0#81; |HAZ GrabaTemporal;

     nNume = 0;
     aAlfa = "";
     swDias1 = 0;
     swDias2 = 0;
     |SI #0#37 = "S"; aAlfa = aAlfa + "lunes, "; nNume = nNume + 1; |FINSI;
     |SI #0#38 = "S"; aAlfa = aAlfa + "martes, "; nNume = nNume + 1; |FINSI;
     |SI #0#39 = "S"; aAlfa = aAlfa + "miercoles, "; nNume = nNume + 1; |FINSI;
     |SI #0#40 = "S"; aAlfa = aAlfa + "jueves, "; nNume = nNume + 1; |FINSI;
     ||HAZ dias;
     |SI #0#41 = "S"; aAlfa = aAlfa + "viernes, "; nNume = nNume + 1; |FINSI;
     ||HAZ dias;
     |SI #0#42 = "S"; aAlfa = aAlfa + "sabado, "; nNume = nNume + 1; |FINSI;
     ||HAZ dias;
     |SI #0#43 = "S"; aAlfa = aAlfa + "domingo, "; nNume = nNume + 1; |FINSI;
     |HAZ diasfin;

     aCampo = "euros"; aValor = #0#44; |HAZ GrabaTemporal;
     |SI #0#45 = "D";    tiporet = "diarias  ";   |FINSI;
     |SI #0#45 = "M";    tiporet = "mensuales";   |FINSI;
     |SI #0#45 = "S";    tiporet = "semanales";   |FINSI;
     |SI #0#45 = "H";    tiporet = "hora     ";   |FINSI;
     aCampo = "tiporet"; aValor = tiporet; |HAZ GrabaTemporal;
     aCampo = "perprueba"; aValor = #0#46; |HAZ GrabaTemporal;
     aCampo = "oficina"; aValor = #2#24; |HAZ GrabaTemporal;

     |HAZ FechasComunes;

     aCampo = "cuenta"; aValor = #2#72; |HAZ GrabaTemporal;

     |HAZ FirmaComunes;

     |SI swCopia = 1;
          aCampo = "copiabasica"; aValor = ""; |HAZ GrabaTemporal;
          aCampo = "firma1"; aValor = "El/la trabajador/a"; |HAZ GrabaTemporal;
          aCampo = "firma2"; aValor = "El/la representante de la empresa"; |HAZ GrabaTemporal;
          aCampo = "firma3"; aValor = "El/la representante legal (si procede)"; |HAZ GrabaTemporal;
          aCampo = "firmado1"; aValor = #2#2; |HAZ GrabaTemporal;
          aCampo = "firmado2"; aValor = #0#3; |HAZ GrabaTemporal;
          aCampo = "firmado3"; aValor = #2#20; |HAZ GrabaTemporal;
     |SINO;
          aCampo = "copiabasica"; aValor = "COPIA BASICA"; |HAZ GrabaTemporal;
          aCampo = "firma1"; aValor = "El/la representante de la empresa"; |HAZ GrabaTemporal;
          aCampo = "firma2"; aValor = "El/la representante sindical"; |HAZ GrabaTemporal;
          aCampo = "firmado1"; aValor = #0#3; |HAZ GrabaTemporal;
          aCampo = "firmado2"; aValor = "<NO EXISTE>"; |HAZ GrabaTemporal;
     |FINSI;

     |HAZ RegistroComunes;
|FPRC;

|PROCESO CargaCamposCPD;
     |HAZ EmpETTComunes;

     aCampo = "usurep"; aValor = #0#50; |HAZ GrabaTemporal;
     aCampo = "usudnirep"; aValor = #0#51; |HAZ GrabaTemporal;
     aCampo = "usuenconc"; aValor = #0#52; |HAZ GrabaTemporal;

     |HAZ EmpUsuComunes;

     |HAZ CenUsuComunes;

     aCampo = "supuesto"; aValor = #0#35; |HAZ GrabaTemporal;
     aCampo = "supuesto1"; aValor = #0#101; |HAZ GrabaTemporal;
     aCampo = "supuesto2"; aValor = #0#102; |HAZ GrabaTemporal;
     aCampo = "supuesto3"; aValor = #0#103; |HAZ GrabaTemporal;
     aCampo = "supuesto4"; aValor = #0#104; |HAZ GrabaTemporal;
     aCampo = "supuesto5"; aValor = #0#105; |HAZ GrabaTemporal;

     |HAZ TrabComunes;

     aCampo = "nafsstra"; aValor = #2#73; |HAZ GrabaTemporal;
     aCampo = "cualifi"; aValor = #0#18; |HAZ GrabaTemporal;
     aCampo = "funciones"; aValor = #0#19; |HAZ GrabaTemporal;
     || aCampo = "horario"; aValor = #0#23; |HAZ GrabaTemporal;

     aCampo = "precio"; aValor = #0#28; |HAZ GrabaTemporal;
     aCampo = "formapago"; aValor = #0#29; |HAZ GrabaTemporal;
     aCampo = "entidad"; aValor = #0#30; |HAZ GrabaTemporal;
     aCampo = "cuenta"; aValor = #0#31; |HAZ GrabaTemporal;
     aCampo = "otrascond1"; aValor = #0#32; |HAZ GrabaTemporal;

     |HAZ FechasComunes;
     |HAZ FirmaComunes;

     aCampo = "empusu"; aValor = "Empresa Usuaria"; |HAZ GrabaTemporal;
     aCampo = "empett"; aValor = "Empresa de Trabajo Temporal"; |HAZ GrabaTemporal;
     aCampo = "empusufir"; aValor = "Fdo: " + #0#53; |HAZ GrabaTemporal;
     aCampo = "empettfir"; aValor = "Fdo: " + #0#6; |HAZ GrabaTemporal;

     aValor = "";
     aAlfa = #0#106; |QBF aAlfa;
     |SI aAlfa ! ""; aValor = aAlfa + ". "; |FINSI;
     aAlfa = #0#23; |QBF aAlfa;
     aAlfa = aAlfa + " " + #0#112; |QBF aAlfa;
     |SI aAlfa ! ""; aValor = aValor + "Horario: " + aAlfa; |FINSI;
     |SI aValor ! "";
          aCampo = "horario"; |HAZ GrabaTemporal;
     |FINSI;
     aCampo = "convenio"; aValor = #0#81; |HAZ GrabaTemporal;

     aValor = "";
     aAlfa = #0#107; |QBF aAlfa;
     |SI aAlfa ! ""; aValor = aAlfa; |FINSI;
     aAlfa = #0#108; |QBF aAlfa;
     |SI aAlfa ! ""; aValor = " " + aValor + aAlfa; |FINSI;
     |SI aValor ! "";
          aCampo = "riesgos"; |HAZ GrabaTemporal;
     |FINSI;

     aValor = "";
     aAlfa = #0#109; |QBF aAlfa;
     |SI aAlfa ! ""; aValor = aAlfa; |FINSI;
     aAlfa = #0#110; |QBF aAlfa;
     |SI aAlfa ! ""; aValor = " " + aValor + aAlfa; |FINSI;
     |SI aValor ! "";
          aCampo = "servicios"; |HAZ GrabaTemporal;
     |FINSI;

     aCampo = "retribucion"; aValor = #0#111; |HAZ GrabaTemporal;

     |HAZ RegistroComunes;
|FPRC;

|PROCESO AnexoClaus;
     |ABRE #labclafi;
     |SI FSalida = 0;
          |PARA nClaus = 1; |SI nClaus [ nclausulas; |HACIENDO nClaus = nClaus + 1;
               #labclafi#0 = nClaus + (40 * (nVoypor - 1));
               |LEE 000#labclafi.=;
               |SI FSalida = 0;
                    aClaus = nClaus;
                    |QBF aClaus;
                    aClaus = "00" + aClaus;
                    aClaus = aClaus % -102;
                    aCampo = "clausula" + aClaus;
                    aValor = #labclafi#1;
                    |HAZ GrabaTemporal;
               |FINSI;
          |SIGUE;
     |FINSI;
     |CIERRA #labclafi;

     aCampo = "localidad";  || Localidad firma
     aValor = #1#6;
     |HAZ GrabaTemporal;

     aAlfa = #0#33;
     aAlfa1 = aAlfa % 102;
     fec1 = #0#33;
     |HAZ meseslet;
     aAlfa2 = meslet;
     aAlfa3 = aAlfa % -104;

     aCampo = "dia";  || DIA FIRMA
     aValor = aAlfa1;
     |HAZ GrabaTemporal;

     aCampo = "mes";  || MES FIRMA
     aValor = aAlfa2;
     |HAZ GrabaTemporal;

     aCampo = "anyo";  || ANYO FIRMA
     aValor = aAlfa3;
     |HAZ GrabaTemporal;

     aCampo = "pagina";  || PAGINA
     aAlfa1 = nVoypor;
     aAlfa2 = nNCA;
     aValor = "P\341gina " + aAlfa1 + " de " + aAlfa2;
     |HAZ GrabaTemporal;

     |SI swCopia = 1;
          aCampo = "trabajador";  || NOMBRE DEL TRABAJADOR TRABAJADOR
          aValor = #labtraba#2;
          |HAZ GrabaTemporal;

          aCampo = "repre_legal";  || NOMBRE REPRESENTANTE LEGAL DEL MENOR SI PROCEDE
          aValor = #labtraba#20;
          |HAZ GrabaTemporal;

          aCampo = "repre_emp";  || NOMBRE REPRESENTANTE EMPRESA
          aValor = #labempre#15;
          |HAZ GrabaTemporal;

          aCampo = "copiabasica";
          aValor = "";
          |HAZ GrabaTemporal;

          aCampo = "firma1";
          aValor = "El/la trabajador/a";
          |HAZ GrabaTemporal;

          aCampo = "firma2";
          aValor = "El/la representante de la empresa";
          |HAZ GrabaTemporal;

          aCampo = "firma3";
          aValor = "El/la representante legal del menor, si procede";
          |HAZ GrabaTemporal;
     |SINO;
          aCampo = "trabajador";
          aValor = #labempre#15;
          |HAZ GrabaTemporal;

          aCampo = "repre_emp";
          aValor = "<NO EXISTE>";
          |HAZ GrabaTemporal;

          aCampo = "copiabasica";
          aValor = "COPIA BASICA";
          |HAZ GrabaTemporal;
/*
          aCampo = "firma1";
          aValor = "El/la trabajador/a";
          |HAZ GrabaTemporal;
*/
          aCampo = "firma1";
          aValor = "El/la representante de la empresa";
          |HAZ GrabaTemporal;

          aCampo = "firma2";
          aValor = "El/la representante sindical";
          |HAZ GrabaTemporal;
     |FINSI;

     |HAZ RegistroComunes;
|FPRC;

|PROCESO CamposClauConf;
     aCampo = "firma1";
     aValor = "El/la trabajador/a";
     |HAZ GrabaTemporal;

     aCampo = "firma2";
     aValor = "El/la representante de la empresa";
     |HAZ GrabaTemporal;

     aCampo = "trabajador";
     aValor = #labtraba#2;
     |HAZ GrabaTemporal;

     aCampo = "repre_emp";
     aValor = #labempre#15;
     |HAZ GrabaTemporal;
|FPRC;

|PROCESO ImprePDF;
     aAlfa1 = Usuario; |QBT aAlfa1;    aAlfa1 = ("c" + aAlfa1) % 108;
     |NOME_DAT #100 aAlfa1;

     |DELINDEX #pdfimtmp;
     |ABRE #pdfimtmp;         || Contrato Puesta a Disposicion
     swCopia = 1;
     aArchivo = "cpd.pdf"
     |HAZ CargaCamposCPD;
     nCuantos = 1;
     |CIERRA #pdfimtmp;
     |SUB_EJECUTA "pdfgraba";

     |DELINDEX #pdfimtmp;               || Contrato duracion Determinada
     |ABRE #pdfimtmp;
     swCopia = 1;
     aArchivo="cdd.pdf"
     |HAZ CargaCamposCDD;
     nCuantos = nCuantos + 1;
     |CIERRA #pdfimtmp;
     |SUB_EJECUTA "pdfgraba";

     |DELINDEX #pdfimtmp;     || Copia Basica Contrato duracion Determinada
     |ABRE #pdfimtmp;
     swCopia = 2;
     aArchivo="cdd.pdf"
     |HAZ CargaCamposCDD;
     nCuantos = nCuantos + 1;
     |CIERRA #pdfimtmp;
     |SUB_EJECUTA "pdfgraba";

     |SI nclausulas > 0;
          nNCA = nclausulas / 40;       || registro:nro total claus,40=max/pag.
          nNCA = nNCA + 0.5;          || Numero de paginas de clausulas adic.
          nNCA = nNCA ! 0;            || que tengo que sacar (trunco)
     |FINSI;

     |PARA nVoypor = 1; |SI nVoypor [ nNCA; |HACIENDO nVoypor = nVoypor + 1;
          |DELINDEX #pdfimtmp;               || Contrato duracion Determinada
          |ABRE #pdfimtmp;
          swCopia = 1;
          aArchivo = "anexoclaus.pdf"

          |HAZ AnexoClaus;
          nCuantos = nCuantos + 1;
          |SUB_EJECUTA "pdfgraba";
     |SIGUE;
     |CIERRA #pdfimtmp;

     |PARA nVoypor = 1; |SI nVoypor [ nNCA; |HACIENDO nVoypor = nVoypor + 1;
          |DELINDEX #pdfimtmp;               || Contrato duracion Determinada
          |ABRE #pdfimtmp;
          swCopia = 2;
          aArchivo = "anexoclaus.pdf"

          |HAZ AnexoClaus;
          nCuantos = nCuantos + 1;
          |SUB_EJECUTA "pdfgraba";
     |SIGUE;
     |CIERRA #pdfimtmp;

     |DELINDEX #pdfimtmp;     || Copia Basica Contrato duracion Determinada
     |ABRE #pdfimtmp;
     swCopia = 1;
     aArchivo="clauconf.pdf"
     nCuantos = nCuantos + 1;
     |HAZ CamposClauConf;
     |CIERRA #pdfimtmp;
     |SUB_EJECUTA "pdfgraba";

     |SUB_EJECUTA "pdfimpre";
|FPRC;

|PROCESO tipo3;
     || lee empresa, trabajador y centro
     |DDEFECTO #1; |ABRE #1; #1#0 = #0#49;               |LEE 000#1=; |CIERRA #1;
     |DDEFECTO #2; |ABRE #2; #2#0 = #0#49; #2#1 = #0#2;  |LEE 000#2=; |CIERRA #2;

     |HAZ trabajador;

     |SI swLeidas = 0;        || Si no las he leido, ya que se las he leido
          |HAZ leeclaus;      || y modificado volveria a machacar lo ya modi-
     |FINSI;                  || ficado

     |SI #0#47 ! 0;
          |HAZ registro;       || actualiza registro contratos
     |FINSI;

     #labcontr#41 = "S";      || esta gente siempre imprimen en modo duplex
                              || lo pongo asi para que no haya conflicto con
                              || lo de la emision de datos de la contratacion
                              || 11.04.2004

/*
     |SI #labcontr#41 = "N";       || DUPLEX NO
          |PARA vuelta = 1; |SI vuelta [ 2; |HACIENDO vuelta = vuelta + 1;
               |PARA para1 = 1; |SI para1 [ #0#34; |HACIENDO para1 = para1 + 1;
                    |HAZ cpd;            || emite cpd's
               |SIGUE;

               |PARA para1 = 1; |SI para1 [ #0#47; |HACIENDO para1 = para1 + 1;
                    cbasica = 0;
                    |HAZ contrato;       || emite contrato
               |SIGUE;
               |PARA para1 = 1; |SI para1 [ #0#48; |HACIENDO para1 = para1 + 1;
                    cbasica = 1;
                    |HAZ contrato;       || emite copia basica
               |SIGUE;

               |SI vuelta = 1;|Y #0#70 = "S";
                    |FINIMP;
                    |AVISO;
                    alfa1 = "    Dele la vuelta al papel. A imprimir reversos ...";
                    |MENSA alfa1;
                    Impresora = nomimp;
                    |IMPRE 0;
                    |SI FSalida ! 0;
                         |SAL;
                    |FINSI;
               |FINSI;
          |SIGUE;
     |SINO;
          |IMPRIME aDuplexSi;
          |PARA para1 = 1; |SI para1 [ #0#34; |HACIENDO para1 = para1 + 1;
              vuelta = 1; |HAZ cpd;
              vuelta = 2; |HAZ cpd;
          |SIGUE;
          |IMPRIME aDuplexNo;

          |IMPRIME aDuplexSi;
          |PARA para1 = 1; |SI para1 [ #0#47; |HACIENDO para1 = para1 + 1;
              cbasica = 0;
              vuelta = 1; |HAZ contrato;
              vuelta = 2; |HAZ contrato;
          |SIGUE;
          |IMPRIME aDuplexNo;

          |IMPRIME aDuplexSi;
          |PARA para1 = 1; |SI para1 [ #0#48; |HACIENDO para1 = para1 + 1;
               cbasica = 1;
               vuelta = 1; |HAZ contrato;
               vuelta = 2; |HAZ contrato;
          |SIGUE;
          |IMPRIME aDuplexNo;
     |FINSI;
*/
     |HAZ PreClausulas;
     |HAZ ImprePDF;

     ||HAZ Clausulas;
|FPRC;

|PROCESO ModoDuplex;
     alfa1 = #labcontr#42;    |QBF alfa1;    alfa2 = alfa1 % 0;  nume2 = alfa2;
     aDuplexSi = "";
     |PARA nume1 = 1; |SI nume1 [ nume2; |HACIENDO nume1 = nume1 + 3;
          nume3 = (nume1 * 100) + 3;
          alfa2 = alfa1 % nume3;
          nume3 = alfa2;
          |SI nume3 = 27;|O nume3 = 0;
               aDuplexSi = aDuplexSi + &27 + alfa2;
          |SINO;
               aDuplexSi = aDuplexSi + &nume3;
          |FINSI;
     |SIGUE;

     alfa1 = #labcontr#43;    |QBF alfa1;    alfa2 = alfa1 % 0;  nume2 = alfa2;
     aDuplexNo = "";
     |PARA nume1 = 1; |SI nume1 [ nume2; |HACIENDO nume1 = nume1 + 3;
          nume3 = (nume1 * 100) + 3;
          alfa2 = alfa1 % nume3;
          nume3 = alfa2;
          |SI nume3 = 27;|O nume3 = 0;
               aDuplexNo = aDuplexNo + &27 + alfa2;
          |SINO;
               aDuplexNo = aDuplexNo + &nume3;
          |FINSI;
     |SIGUE;
|FPRC;

|PROCESO Control;
     |ABRE #labcontr;
     |LEE 000#labcontr.p;
     |CIERRA #labcontr;

     |HAZ ModoDuplex;
|FPRC;

|PROGRAMA;
|CLS;
|CABEZA "Emision CPD";
|PINPA #0#0;
|DDEFECTO #0;
|ABRE #0;
|LEE 101#0p;
|SI FSalida ! 0;    |GRABA 020#0b; |FINSI;
|PINDA #0#0;
|ENDATOS #1#0;
|SI FSalida = 0;
    ||IMPRE 0;
    |SI FSalida = 0;
        ||    nomimp = Impresora;
        |HAZ Control;
        |HAZ tipo3;
        |GRABA 020#0a;
    |SINO;
        |MENAV "    Proceso abortado ...";
    |FINSI;
    ||FINIMP;
|FINSI;
|LIBERA #0;
|CIERRA #0;
|FPRO;
