|FICHEROS;
     silcon03 #0;   || lotes lins.
     silcon02 #1;   || lotes cabs.
     silcon04 #2;   || configuracion campos
     tsilcon0 #3;   || presentacion linea
|FIN;

|VARIABLES;
     i       = 0;
     beta    = 0;
     nTipo   = 0;
     nCampo  = 0;
     campo   = 0;
     error   = 0;
     ind     = 0;
     long    = 0;
     posi    = 0;
     clave   = 0;
     alfa    = "";
     alfa1   = "";
     cadena  = "";
     ajuste1 = "";
     ajuste2 = "";
     ajuste3 = "";
     digito  = "";
     mascara = "";
     texto   = "";
     modo    = 0;
|FIN;

|PROCESO mayusl; |TIPO 0;
     #0Campo = #0Campo > #0Campo;
     |PINTA #0Campo;
|FPRC;

|PROCESO comp1;               || COMPRUEBA SI ES NUMERICO.
     |SI ajuste1 = "1";
          error = 0;
          |PARA ind = 1; |SI ind [ long; |HACIENDO ind = ind + 1;
               posi = 100 * ind + 1;
               digito = texto % posi;
               |SI digito ! " ";
                    |SI digito < "0"; |O digito > "9";
                         error = 1;
                         |SAL;
                    |FINSI;
               |FINSI;
          |SIGUE;
     |FINSI;
|FPRC;

|PROCESO comp2;               || COMPRUEBA SI TIENE QUE ESTAR RELLENO.
     |SI ajuste3 = "3";
          error = 0;
          FSalida = 0;
          |QBF alfa;
          |PARA ind = 1; |SI FSalida = 0; |HACIENDO ind = ind + 1;
               posi = 100 * ind + 1;
               digito = texto % posi;
               |SI digito = "";
                    error = 2;
                    |SAL;
               |FINSI;
          |SIGUE;
     |FINSI;
|FPRC;

|PROCESO comp3;               || AJUSTA I, o D. Y RELLENA DE 0 o BLANCOS.
     |SI ajuste3 = "1";       || RELLENA BLANCOS.
          mascara = "                                                                    ";
     |FINSI;
     |SI ajuste3 = "2";       || RELLENA CEROS.
          mascara = "00000000000000000000000000000000000000000000000000000000000000000000";
     |FINSI;
     |SI ajuste2 = "1";       || AJUSTA DERECHA.
          posi = 100 + long;
          posi = posi * -1;
          |QBF texto;
          texto = mascara + texto;
          texto = texto % posi;
     |FINSI;
     |SI ajuste2 = "2";
          posi = 100 + long;
          |QBF texto;
          texto = texto + mascara;
          texto = texto % posi;
     |FINSI;
|FPRC;

|PROCESO monta;
     texto = "";
     cadena = "";
     |PARA i = 0; |SI i < 30; |HACIENDO i = i + 1;
          nCampo = i;
          texto = #3nCampo;
          nTipo = i * 6 + 2;                 || MODIFICABLE.
          alfa  = #2nTipo;
          nTipo = i * 6 + 2;                 || VISUAL.
          alfa1 = #2nTipo;
          |SI alfa = "S"; |O alfa1 = "S";
               nTipo = i * 6 + 6;            || Longitud.
               long  = #2nTipo;
               nTipo = i * 6 + 7;            || AJUSTE.
               alfa  = #2nTipo;
               ajuste1 = alfa % 101;
               ajuste2 = alfa % 201;
               ajuste3 = alfa % 301;
               |HAZ comp1;
               |SI error = 1; |HAZ errores; |ACABA; |FINSI;
               |HAZ comp2;
               |SI error = 2; |HAZ errores; |ACABA; |FINSI;
               |HAZ comp3;
               cadena = cadena + texto;
          |FINSI;
     |SIGUE;
|FPRC;

|PROCESO Pantalla;
     |PARA i = 0; |SI i [ 30; |HACIENDO i = i + 1;
          nCampo = i;
          nTipo = i * 6 + 2;
          alfa  = #2nTipo;
          |CAMPO_MODIFICA #3nCampo, 1, alfa;
          nTipo = i * 6 + 3;
          alfa  = #2nTipo;
          |CAMPO_VISUAL   #3nCampo, 1, alfa;
          nTipo = i * 6 + 4;
          beta  = #2nTipo;
          |CAMPO_POSICION #3nCampo, 1, beta;
          nTipo = i * 6 + 6;
          beta  = #2nTipo;
          |CAMPO_ANCHO    #3nCampo, 1, beta;
          nTipo = i * 6 + 5;
          alfa  = "00" + #2nTipo;
          alfa1 = alfa % -102;
          nTipo = i * 6 + 6;
          alfa  = "00" + #2nTipo;
          alfa  = alfa % -102;
          alfa1 = alfa1 + alfa;
          beta  = alfa1;
          #3nCampo = #0#4 % beta;
          nTipo = i * 6 + 3;
          alfa  = #2nTipo;
          |SI alfa = "S"; |PINTA #3nCampo; |FINSI;
     |SIGUE;
|FPRC;

|PROCESO datos; |TIPO 11;
     |SI FSalida = 13; |Y clave = 0;
          |ABRE #2;
          #2#0 = #0#2;   || CLAVE 1.
          #2#1 = #0#3;   || CLAVE 2.
          |LEE 000#2=;
          |SI FSalida = 0;
               |PUSHV 0101 2380;
               |PINPA #0#3;
               |PINDA #1#2;
               |HAZ Pantalla;
               modo = (FEntrada / 100) ! 0;
               |SI modo ! 4;
                   |ENDATOS #2#3;
                   |SI FSalida = 0;
                       |HAZ monta;
                       |SI error = 0;
                           #0#4 = cadena;
                           |GRABA 020#0a;
                           |POPV;
                           |PINDA #0#0;
                       |SINO;
                           |POPV;
                       |FINSI;
                   |SINO;
                       |POPV;
                   |FINSI;
               |SINO;
                   |PINDA #0#3;
                   |PAUSA;
                   |POPV;
               |FINSI;
          |SINO;
               |MENAV "    Clave NO definida ...";
          |FINSI;
          |CIERRA #2;
          |ERROR;
     |FINSI;
|FPRC;

|PROCESO errores;
     |SI error = 1;
          |MENAV "0000EL VALOR DEL CAMPO NUMERICO INTRODUCIDO NO ES CORRECTO";
     |FINSI;
     |SI error = 2;
          |MENAV "0000CAMPO OBLIGADO SIN ESTAR COMPLETAMENTE RELLENO";
     |FINSI;
|FPRC;
