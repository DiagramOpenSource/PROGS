|FICHEROS;
     mod00250 #0;   || introd. rapida
     mod00243 #1;   || horas cabs.
     mod00244 #2;   || horas lins.
     mod00242;
     mod00249;
|FIN;

|VARIABLES;
     aAlfa1 = "";
     aAlfa2 = "";
     aAlfa3 = "";
     aAlfa4 = "";
     nNume1 = 0;
     nNume2 = 0;
     nNume3 = 0;
     nNume4 = 0;
     nPara1 = 0;
     nPara2 = 0;
     nPara3 = 0;
     nPara4 = 0;
     fFech1 = @;
     fFech2 = @;
     fFech3 = @;
     fFech4 = @;
     nFEstado = 0;
     nSeleccio = 0;
     nModo = 0;
     nInkey = 0;

     nValor = 0;
     nSwitch = 0;
     nCuantos = 0;
     nPrecio = 0;
|FIN;
____________________________________/
|PROCESO Antes; |TIPO 7;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 1;
          |DEL_BUF nSwitch, Campo, aAlfa1;
          nNume1 = aAlfa1;
          |SI nNume1 = 1;
               |DEL_BUF nValor, Campo, aAlfa1;
               #0Campo = aAlfa1;
               |PINTA #0Campo;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Despues; |TIPO 0;
     nInkey = FSalida;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 1;|Y nInkey = 0;
          aAlfa1 = "1";
          |AL_BUF nSwitch, Campo, aAlfa1;
          aAlfa1 = #0Campo;
          |AL_BUF nValor, Campo, aAlfa1;
     |FINSI;
|FPRC;
____________________________________/
|PROCESO PrecioHora;
     |ABRE #mod00242;
     #mod00242#0 = #mod00250#4;
     |LEE 000#mod00242.=;
     |CIERRA #mod00242;

     |SI #mod00242#6 ! 0;
          nPrecio = #mod00242#6;
     |SINO;
          nPrecio = #mod00242#2;
     |FINSI;

     |ABRE #mod00249;
     #mod00249#0 = #mod00250#1;
     #mod00249#1 = #mod00250#2;
     #mod00249#2 = #mod00250#4;
     |LEE 000#mod00249.=;
     |SI FSalida = 0;
          |SI #mod00249#6 ! 0;
               nPrecio = #mod00249#6;
          |SINO;
               nPrecio = #mod00249#3;
          |FINSI;
     |FINSI;
     |CIERRA #mod00249;

     #mod00250#7 = nPrecio;
     |PINTA #mod00250#7;
|FPRC;

|PROCESO Introducido_1;
     |DDEFECTO #1;
     #1#0 = #0#1;
     #1#1 = #0#2;
     |LEE 101#1=;
     |SI FSalida ! 0;
          |GRABA 020#1b;
     |FINSI;
     |LIBERA #1;

     |DDEFECTO #2;
     #2#0 = #1#0;   || empresa
     #2#1 = #1#1;   || trabajador
     #2#2 = #0#3;   || fecha
     #2#3 = #0#4;   || tipo hora
     |LEE 101#2=;
     nFEstado = FSalida;

     #2#4 = #0#5;   || descripcion hora
     #2#5 = #2#5 + #0#6; || nro. horas (acumula)
     #2#6 = #0#7;   || precio hora

     |SI nFEstado = 0;   |GRABA 020#2a; |FINSI;
     |SI nFEstado ! 0;   |GRABA 020#2n; |FINSI;
     |LIBERA #2;

     nCuantos = nCuantos + 1;
|FPRC;

|DEFBUCLE Introducido;
 #0#1;
 ;
    1;
 9999;
 ;
 NULL, Introducido_1;
|FIN;

|PROCESO Actualiza;
     |ABRE #1;
     |ABRE #2;
     |BUCLE Introducido;
     |CIERRA #1;
     |CIERRA #1;

     |SI nCuantos ! 0;
          aAlfa1 = "    Actualizadas [" + nCuantos + "] entradas ...";
          |MENAV aAlfa1;
     |SINO;
          |MENAV "    Seleccion vacia ...";
     |FINSI;
|FPRC;

|PROCESO Min0;
     #0#0 = 1;
|FPRC;

|PROCESO Max0;
     #0#0 = 9999;
|FPRC;

|PROGRAMA;
     |CLS;
     |CABEZA "Introd.Rapida";
     |PINPA #0#0;

     aAlfa1 = Usuario;   |QBF aAlfa1;
     |NOME_DAT #0 aAlfa1;
     |DELINDEX #0;

     |DIMENSIONA 20, nValor, 0;    |SI FSalida < 0;    |MENAV "    Problemas con tablas ...";  |FINSI;
     |DIMENSIONA 20, nSwitch, 0;   |SI FSalida < 0;    |MENAV "    Problemas con tablas ...";  |FINSI;
     |ENTLINEAL #1#0, 1, 1, 19, 1, Min0, Max0;
     |FINDIM nValor;
     |FINDIM nSwitch;

     |AVISO;
     |CONFI "    SConforme con la introduccion (S/N): ";
     |SI FSalida = 0;
          |HAZ Actualiza;
     |SINO;
          |AVISO;
          |CONFI "    NTodo lo introducido se perdera. ¨Continuar? ";
          |SI FSalida ! 0;
               |HAZ Actualiza;
          |FINSI;
     |FINSI;

     |DELINDEX #0;
|FPRO;
