|FICHEROS;
     nomembpa #0;
     nomembca;
     nomembli;
     labtraba;
     nomcontr;

     :integral/dsam0017 #17;
     :integral/dsam0018 #18;
|FIN;

|VARIABLES;
     aNif = "";
     nRegistro = 0;
     nUltima = 0;
     nLinea = 0;

     &nMes = 0;
     &nAny = 0;
     &nTopemes = 0;

     aAlfa = "";
     aDia = "";
     &aMes = "";
     aAny = "";
     aFecha = "";
     fFecha = @;

     nCampo = 0;
     nEmbargado = 0;
     nPendiente = 0;
     nTotal = 0;

     nUltAny = 0;
     nUltTotal = 0;
     nUltEmbargado = 0;
     nUltPendiente = 0;
     aFechaNot = "";
     fFechaNot = @;

     nCreados = 0;
     nTotalActualizados = 0;
     nActualizado = 0;

     nNume = 0;
     nDiferencia = 0;
|FIN;

|PROCESO LineaRegul;
     |DDEFECTO #18;
     nLinea = nLinea + 1;
     #18#0 = #17#0;
     #18#1 = #17#1;
     #18#2 = #17#2;
     #18#3 = nLinea;
     fFecha = ~;
     #18#4 = fFecha;
     #18#5 = "REGULARIZACION";
     #18#6 = nDiferencia;
     |GRABA 020#18n;
     |LIBERA #18;
|FPRC;

|PROCESO nomembli;
     |PARA nCampo = 3; |SI nCampo [ 25; |HACIENDO nCampo = nCampo + 2;
          |SI #nomembli#nCampo# ! 0;
               |DDEFECTO #18;
               nLinea = nLinea + 1;
               #18#0 = #nomembli#0;
               #18#1 = #labtraba#7;
               #18#2 = nRegistro;
               #18#3 = nLinea;

               nMes = (nCampo - 1) / 2;
               nAny = #nomembli#2;
               |HAZ topemes_plb;
               aDia = nTopemes; |QBF aDia;
               aMes = nMes; |QBF aMes; aMes = ("00" + aMes) % -102;
               aAny = nAny;
               aFecha = aDia + "." + aMes + "." + aAny;
               fFecha = aFecha;
               #18#4 = fFecha;

               |SI aFechaNot = "";
                    || para que entre solo el primer mes y coger el primer dia
                    || de ese mes como fecha de notificacion
                    aFechaNot = "01." + aMes + "." + aAny;
                    fFechaNot = aFechaNot;
               |FINSI;

               |HAZ MesLetra_plb;
               aAlfa = "NOMINA " + aMes + " " + aAny;
               #18#5 = aAlfa;

               #18#6 = #nomembli#nCampo#;
               |GRABA 020#18n;
               |LIBERA #18;
          |FINSI;
          nEmbargado = #nomembli#28;
          nPendiente = #nomembli#29;
     |SIGUE;
|FPRC;

|DEFBUCLE nomembli;
     #nomembli#1;
     ;
     #nomembca#0, #nomembca#1, 2005;
     #nomembca#0, #nomembca#1, 2999;
     ;
     NULL, nomembli;
|FIN;

|PROCESO CreaNuevo;
     |DDEFECTO #17;
     nRegistro = nUltima + 1;
     #17#0 = #labtraba#0;
     #17#1 = #labtraba#7;
     #17#2 = nRegistro;

     #17#7 = #nomembca#4;
     #17#12 = #nomembca#7;
     #17#11 = #nomembca#5;
     #17#8 = #nomembca#6;
     #17#13 = "S";       || es un embargo que proviene de laboral
     |GRABA 020#17n;
     |LIBERA #17;

     #nomembca#8 = nRegistro;
     |GRABA 020#nomembca.a;

     nLinea = 0;
     nEmbargado = 0;
     nPendiente = 0;
     aFechaNot = "";
     fFechaNot = ~;

     |BUCLE nomembli;

     #17#0 = #labtraba#0;
     #17#1 = #labtraba#7;
     #17#2 = nRegistro;
     |LEE 101#17=;
     |SI FSalida = 0;
          #17#3 = fFechaNot;
          #17#9 = nEmbargado;
          #17#10 = nPendiente;
          |GRABA 020#17a;
          |LIBERA #17;
     |FINSI;

     nNume = nEmbargado + nPendiente;
     nNume = nNume ! 2;
     nDiferencia = #17#8 - nNume;
     |SI nDiferencia ! 0;
          |HAZ LineaRegul;
          #17#9 = #17#9 + nDiferencia;
          |GRABA 020#17a;
          |LIBERA #17;
     |FINSI;

     nCreados = nCreados + 1;
|FPRC;

|PROCESO LeeNomembli;
     nUltAny = #nomembli#2;

     nUltTotal = #nomembli#27;
     nUltEmbargado = #nomembli#28;
     nUltPendiente = #nomembli#29;
|FPRC;

|DEFBUCLE LeeNomembli;
     #nomembli#1;
     ;
     #nomembca#0, #nomembca#1, 2005;
     #nomembca#0, #nomembca#1, 2999;
     ;
     NULL, LeeNomembli;
|FIN;

|PROCESO dsam0017;
     nTotal = nTotal + #17#8;
     nEmbargado = nEmbargado + #17#9;
     nPendiente = nPendiente + #17#10;
     nUltima = #17#2;
|FPRC;

|DEFBUCLE dsam0017;
     #17#1;
     ;
     #nomembca#0, #labtraba#7, 01;
     #nomembca#0, #labtraba#7, 99;
     ;
     NULL, dsam0017;
|FIN;

|PROCESO nomembca;
     #labtraba#0 = #nomembca#0;
     #labtraba#1 = #nomembca#1;
     |LEE 000#labtraba.=;
     |SI FSalida = 0; |Y #labtraba#44 = "A";
          aNif = #labtraba#7;
          |QBF aNif;
          aNif = (aNif + "            ") % 112
     |SINO;
          |ACABA;
     |FINSI;

     nUltima = 0;
     nTotal = 0;
     nEmbargado = 0;
     nPendiente = 0;

     |BUCLE dsam0017;

     |SI nUltima = 0;
          || no existe el embargo en la gestion de embargos de la integral
          || hay que crearlo
          |HAZ CreaNuevo;
     |SINO;
          || SI que hay embargos en la gestion, vamos a comprobar si estan
          || sincronizados con los del laboral

          nActualizado = 0;

          |SI #nomembca#6 ! nTotal;
               || el total del embargo de la cabecera no coincide
               #nomembca#6 = nTotal;
               |GRABA 020#nomembca.a;
               |LIBERA #nomembca;
               nActualizado = 1;
          |FINSI;

          nUltAny = 0;

          nUltTotal = 0;
          nUltEmbargado = 0;
          nUltPendiente = 0;

          |BUCLE LeeNomembli;

          #nomembli#0 = #nomembca#0;
          #nomembli#1 = #nomembca#1;
          #nomembli#2 = nUltAny;
          |LEE 101#nomembli.=;
          |SI FSalida = 0;
               |SI nUltTotal ! nTotal;
                    || el total del embargo del ultimo a¤o del nomembli no coincide
                    #nomembli#27 = nTotal;
                    nActualizado = 1;
               |FINSI;

               |SI nUltEmbargado ! nEmbargado;
                    || el total embargado del nomembli no coincide
                    #nomembli#28 = nEmbargado;
                    nActualizado = 1;
               |FINSI;

               |SI nUltPendiente ! nPendiente;
                    || el total pendiente del nomembli no coincide
                    #nomembli#29 = nPendiente;
                    nActualizado = 1;
               |FINSI;

               |GRABA 020#nomembli.a;
               |LIBERA #nomembli;
               |SI nActualizado = 1;
                    nTotalActualizados = nTotalActualizados + 1;
               |FINSI;
          |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE nomembca;
     #nomembca#1;
     ;
     #0#0, #0#2;
     #0#1, #0#3;
     be;
     NULL, nomembca;
|FIN;

|PROCESO Pase;
     |ABRE #17;
     |ABRE #18;
     |ABRE #labtraba;
     |ABRE #nomembca;
     |ABRE #nomembli;

     nCreados = 0;
     nTotalActualizados = 0;

     |BUCLE nomembca;

     |SI nCreados ! 0;
          aAlfa = nCreados;
          |QBF aAlfa;
          aAlfa = "    Creados " + aAlfa + " embargos en la Gestión de Embargos ";
          aAlfa = aAlfa + "de la Plataforma Integrada";
          |MENAV aAlfa;
     |FINSI;

     |SI nTotalActualizados ! 0;
          aAlfa = nTotalActualizados;
          |QBF aAlfa;
          aAlfa = "    Actualizados " + aAlfa + " embargos en Laboral desde la ";
          aAlfa = aAlfa + "Plataforma Integrada";
          |MENAV aAlfa;
     |FINSI;

     |CIERRA #nomembli;
     |CIERRA #nomembca;
     |CIERRA #labtraba;
     |CIERRA #17;
     |CIERRA #18;
|FPRC;

|PROGRAMA;
     |CLS;

     |ABRE #nomcontr; |LEE 000#nomcontr.p; |CIERRA #nomcontr;

     |SI #nomcontr#65 ! "S";
          aAlfa = "    ATENCION. El enlace con la Gestión de Embargos de PI está ";
          aAlfa = aAlfa + "desactivado.";
          |MENAV aAlfa;
          aAlfa = "    Actívelo en el apartado 'Cálculo de embargos' en la 3ra. ";
          aAlfa = aAlfa + "pantalla del Control de la Aplicación";
          |MENAV aAlfa;
          |ACABA;
     |FINSI;

     |PINPA #0#0;
     |PINDA #0#0;
     |ENDATOS #1#0;
     |SI FSalida = 0;
          |HAZ Pase;
     |FINSI;
|FPRO;
