|FICHEROS;
     cretap26 #0;
     cretam3a;
     cretam3b;

     cretam19;
     cretam02;
     cretam04;

     cretae02;

     labempre;
     labcentr;
     nomcenes;
     labtraba;
|FIN;

|VARIABLES;
     &eaFichero = "";
     &aSoloFichero = "";

     nNume = 0;
     aLon = "";
     nLon = 0;
     nEspacio = 0;
     aEspacio = "";
     nBlancos = 0;
     nBlancosIzd = 0;
     nBlancosDer = 0;
     aBlancosIzd = "";
     aBlancosDer = "";

     aCadena = "";
     aCadenaSal = "";

     FLinea = 0;
     nNroLinea = 0;
     aNroLinea = "";
     aAlfa = "";

     nHandle = 0;
     aLinea = "";
     &aTotalLineas = "";
     swAcaba = 0;

     aCCC = "";
     nAnyDesde = 0;
     nMesDesde = 0;
     aAnyDesde = "";
     aMesDesde = "";
     nTipo = 0;
     aDia = "";
     aMes = "";
     aAny = "";
     aEtiqueta = "";
     nLoc = 0;
     aValor = "";
     aCampoFecha = "";
     aDiaRec = "";
     aMesRec = "";
     aAnyRec = "";
     aHoraRec = "";
     aEtiquetaIni = "";
     aEtiquetaFin = "";
     aMesHasta = "";
     aAnyHasta = "";
     aTipo = "";

     nEmp = 0;
     nTra = 0;
     swEncontrada = 0;
     aAlfa1 = "";
     aAlfa2 = "";
     &enMes = 0;
     &enAny = 0;
     &enSwAlta = 0;

     aNaf = "";
     aEstado = "";
     fFechaRec = @;

     aTipoIPF = "";
     aNumeroIPF = "";
     aSiglas = "";

     aDiaDesdeTramo = "";
     nDiaDesdeTramo = 0;
     aMesDesdeTramo = "";
     aAnyDesdeTramo = "";
     aFechaDesdeTramo = "";
     aDiaHastaTramo = "";
     aMesHastaTramo = "";
     aAnyHastaTramo = "";
     aFechaHastaTramo = "";

     aDias = "";
     aHoras = "";
     aDescripcion = "";
     nConcepto = 0;
     nImporte = 0;
     aCentro = "";

     nCampo = 0;
     nCampo1 = 0;
     nCampo2 = 0;
     nCampo3 = 0;
     aTipoSuma = "";

     &eaCadena = "";
     &eaCadenaSal = "";
     aLongitud = "";
     nLongitud = "";
     &eaCaracter1 = "";
     &eaCaracter2 = "";
     nNume1 = 0;
     nNume2 = 0;

     aDiaUlt = "";
     aMesUlt = "";
     aAnyUlt = "";
     fFechaUlt = @;
     aHoraUlt = "";
     FEstado = 0;
     nCodigoConcepto = 0;
     nBase = 0;
     nCuotaTrab = 0;
     nCuotaEmp = 0;
     nCuotaTotal = 0;
     aReferencia = "";
     aTra = "";
     aAutorizado = "";
     aMesPres = "";
     aAnyPres = "";
     aNumLiq = "";
     nContConcepto = 0;
     nValor = 0;
     nPosicion = 0;

     &aFechaIniParam = "";
     &aFechaFinParam = "";
     aFecha = "";
     fFecha = @;

     nMesHasta = 0;
     nAnyHasta = 0;
     nMesClave = 0;
     nAnyClave = 0;
     nMesClaveAnt = 0;
     nAnyClaveAnt = 0;
     nMesControl = 0;
     nAnyControl = 0;
     swAtrasos = 0;
     swNoActiva = 0;
     nDiaBusca = 0;

     nAnyAtrasos = 0;
     nDesdeAnyAtr = 0;
     nHastaAnyAtr = 0;
|FIN;

|PROCESO Centra;
     aLon = aCadena % 0;
     nLon = aLon;
     nBlancos = nEspacio - nLon;
     nNume = nBlancos / 2;
     nBlancosIzd = nNume ! 0;
     nBlancosDer = nBlancos - nBlancosIzd;
     aBlancosIzd = " " * nBlancosIzd;
     aBlancosDer = " " * nBlancosDer;
     aCadenaSal = aBlancosIzd + aCadena + aBlancosDer;
|FPRC;

|PROCESO cretae02_atrasos;
     |SI nMesDesde = #cretae02#17;
     |Y nMesHasta = #cretae02#18;
     |Y nAnyDesde = #cretae02#19;
          || este es el tpo de atrasos que buscaba
          nTipo = #cretae02#3;
     |FINSI;
|FPRC;

|DEFBUCLE cretae02_atrasos;
     #cretae02#1;
     ;
     00001, nAnyControl, nMesControl, 05, aCCC, "        ";
     99999, nAnyControl, nMesControl, 20, aCCC, "zzzzzzzz";
     ;
     NULL, cretae02_atrasos;
|FIN;

|PROCESO BuscaTipoAtrasos;
     nTipo = 0;
     |BUCLE cretae02_atrasos;
|FPRC;

/****************************************************************************/
/***************************   BORRADO PREVIO *******************************/
/****************************************************************************/

|PROCESO Borra_cretam3b;
     |BORRA 020#cretam3b.a;
     |LIBERA #cretam3b;
|FPRC;

|DEFBUCLE Borra_cretam3b;
     #cretam3b#2;
     ;
     aCCC, nAnyClave, nMesClave, nTipo;
     aCCC, nAnyClave, nMesClave, nTipo;
     ;
     NULL, Borra_cretam3b;
|FIN;

|PROCESO Borra_cretam3a;
     |BORRA 020#cretam3a.a;
     |LIBERA #cretam3a;
|FPRC;

|DEFBUCLE Borra_cretam3a;
     #cretam3a#2;
     ;
     aCCC, nAnyClave, nMesClave, nTipo, nDesdeAnyAtr;
     aCCC, nAnyClave, nMesClave, nTipo, nHastaAnyAtr;
     ;
     NULL, Borra_cretam3a;
|FIN;

/****************************************************************************/
/*********** PROCESOS PARA BUSCAR CODIGOS DE EMP, TRA Y CEN *****************/
/****************************************************************************/

|PROCESO BuscaCCC;
     nEmp = #cretam02#0;
     swEncontrada = 1;
|FPRC;

|DEFBUCLE BuscaCCC_At;
     #cretam02#1;
     19;
     00001, 2000, nMesDesde, nTipo, aCCC, nAnyDesde;
     99999, 2999, nMesHasta, nTipo, aCCC, nAnyDesde;
     ;
     NULL, BuscaCCC;
|FIN;

|DEFBUCLE BuscaCCC;
     #cretam02#1;
     ;
     00001, nAnyDesde, nMesDesde, nTipo, aCCC;
     99999, nAnyDesde, nMesDesde, nTipo, aCCC;
     ;
     NULL, BuscaCCC;
|FIN;

|PROCESO BuscaPorCCC;
     swEncontrada = 0;
     |SI swAtrasos = 0;
          |BUCLE BuscaCCC;
     |SINO;
          |BUCLE BuscaCCC_At;
     |FINSI;
|FPRC;

|PROCESO BuscaPorNaf;
     #labtraba#0 = #cretam04#0;
     #labtraba#1 = #cretam04#7;
     |LEE 000#labtraba.=;

     #labempre#0 = #labtraba#0;
     |LEE 000#labempre.=;

     #labcentr#0 = #labtraba#0;
     #labcentr#1 = #labtraba#77;
     |LEE 000#labcentr.=;
     |SI FSalida = 0;
          aAlfa1 = #labcentr#10;
          |QBF aAlfa1;
          aCentro = #labcentr#2;
          |SI aAlfa1 = "";
               aAlfa1 = #labempre#13;
          |FINSI;
          |QBF aAlfa1;
     |FINSI;

     |SI #labtraba#45 = "421"; |O #labtraba#45 = "422";
          #nomcenes#0 = #labtraba#0;
          #nomcenes#13 = #labtraba#77;
          #nomcenes#1 = "87";
          |LEE 000#nomcenes.=;
          |SI FSalida = 0;
               aAlfa1 = #nomcenes#10;
               |QBF aAlfa1;
               aCentro = #nomcenes#2;
          |FINSI;
     |FINSI;

     aAlfa2 = aCCC % 511;
     |SI aAlfa1 = aAlfa2;
          || el CCC al que pertenece el trabajador es el mismo que me viene

          nEmp = #labtraba#0;
          nTra = #labtraba#1;
          enSwAlta = 1;
          |ERROR10;
     |FINSI;
|FPRC;

|DEFBUCLE BuscaPorNaf;
     #cretam04#1;
     ;
     00001, nAnyClave, nMesClave, nTipo, aCCC, aNaf, nDiaBusca;
     99999, nAnyClave, nMesClave, nTipo, aCCC, aNaf, nDiaBusca;
     ;
     NULL, BuscaPorNaf;
|FIN;

/****************************************************************************/
/********************   TIPOS DE DATOS COMUNES  *****************************/
/****************************************************************************/

|PROCESO TipoFecha;
     aDia = "";
     aMes = "";
     aAny = "";

     |PARA; |SI; |HACIENDO;
          |HAZ LeeLinea;

          aEtiqueta = "Dia";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               aDia = aValor;
          |FINSI;

          aEtiqueta = "Mes";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               aMes = aValor;
          |FINSI;

          aEtiqueta = "Anho";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               aAny = aValor;
          |FINSI;

          aEtiqueta = aCampoFecha;
          |HAZ BuscaEtiFin;
          |SI nLoc = 1;
               |SAL;
          |FINSI;
     |SIGUE;
|FPRC;

|PROCESO FechaUltimoCalculo;
     aCampoFecha = "FechaUltimoCalculo";
     |HAZ TipoFecha;
     aDiaUlt = aDia;
     aMesUlt = aMes;
     aAnyUlt = aAny;
     aAlfa = aDiaUlt + "." + aMesUlt + "." + aAnyUlt;
     fFechaUlt = aAlfa;

     |HAZ LeeLinea;
     aEtiqueta = "HoraUltimoCalculo";
     |HAZ BuscaEtiIniFin;
     |SI nLoc = 1;
          aHoraUlt = (aValor % 102) + ":" + (aValor % 302) + ":" + (aValor % 502);
     |FINSI;
|FPRC;

|PROCESO CCC;
     |PARA; |SI; |HACIENDO;
          |HAZ LeeLinea;

          aEtiqueta = "Regimen";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               aCCC = aValor;
          |FINSI;

          aEtiqueta = "Provincia";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               aCCC = aCCC + aValor;
          |FINSI;

          aEtiqueta = "Numero";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               aCCC = aCCC + aValor;
          |FINSI;

          aEtiqueta = "Ccc";
          |HAZ BuscaEtiFin;
          |SI nLoc = 1;
               |SAL;
          |FINSI;
     |SIGUE;
|FPRC;

/****************************************************************************/
/***********     PROCESOS PARA BUSCAR LAS ETIQUETAS       *******************/
/****************************************************************************/

|PROCESO BuscaEtiFin;
     nLoc = 0;
     aEtiqueta = "</" + aEtiqueta + ">";
     aAlfa = aLinea - aEtiqueta;
     |SI aAlfa ! aLinea;
          nLoc = 1;
     |FINSI;
|FPRC;

|PROCESO BuscaEtiIni;
     nLoc = 0;
     aEtiqueta = "<" + aEtiqueta;
     aAlfa = aLinea - aEtiqueta;
     |SI aAlfa ! aLinea;
          nLoc = 1;
     |FINSI;
|FPRC;

|PROCESO BuscaEtiIniFin;
     aValor = "";
     nLoc = 0;

     aEtiquetaIni = "<" + aEtiqueta + ">";
     aEtiquetaFin = "</" + aEtiqueta + ">";
     aAlfa = aLinea - aEtiquetaIni - aEtiquetaFin;
     |SI aAlfa ! aLinea;
          aValor = aAlfa;
          |SI aEtiqueta = "RazonSocial"; |O aEtiqueta = "EntidadAtEp";
          |O aEtiqueta = "DescripcionLRNT"; |O aEtiqueta = "DescripcionSumaIRNT";
               |QBF aValor;
          |SINO;
               |QBT aValor;
          |FINSI;
          nLoc = 1;
     |FINSI;
|FPRC;

|PROCESO LeeLinea;
     |XLEE nHandle, 250, aLinea;
     FLinea = FSalida;
     nNroLinea = nNroLinea + 1;
     aNroLinea = nNroLinea;
     aAlfa = " Linea " + aNroLinea + " de " + aTotalLineas;

     |ATRI R;
     aCadena = aAlfa; nEspacio = 35; |HAZ Centra;
     |PINTA 2223, aCadenaSal;
     |ATRI 0;
|FPRC;

|PROCESO BuscaTrabPorNaf;
     nTra = 0;
     aCentro = "";

     |SI nDiaDesdeTramo = 1; |Y nTipo = 02;
          nDiaBusca = 99;
     |SINO;
          nDiaBusca = nDiaDesdeTramo;
     |FINSI;

     |SI swAtrasos = 1;
          nMesClaveAnt = nMesClave;
          nMesClave = aMesDesdeTramo;
     |FINSI;

     |BUCLE BuscaPorNaf;

     |SI swAtrasos = 1;
          nMesClave = nMesClaveAnt;
     |FINSI;

     |QBF aNaf

     #labtraba#0 = nEmp;
     #labtraba#1 = nTra;
     |LEE 000#labtraba.=;

     || ya tengo nEmp y nTra
     || regrabo los centros
     #cretam3a#0 = nEmp;
     #cretam3a#1 = nAnyClave;
     #cretam3a#2 = nMesClave;
     #cretam3a#3 = nTipo;
     #cretam3a#4 = aCCC;
     #cretam3a#135 = nAnyAtrasos;
     |LEE 000#cretam3a.=;
     |SI FSalida = 0;
          aAlfa2 = #labtraba#77;
          |QBF aAlfa2;
          aAlfa2 = ("00"+ aAlfa2) % -102;

          aAlfa1 = #cretam3a#9; |QBF aAlfa1;
          |SI aAlfa1 = "";
               #cretam3a#7 = aAlfa2;
               #cretam3a#8 = aCentro;
               #cretam3a#9 = aAlfa2;
               |GRABA 020#cretam3a.a;
               |LIBERA #cretam3a;
          |SINO;
               aAlfa2 = aAlfa1;
               aAlfa2 = aAlfa1 - aAlfa2;
               |SI aAlfa2 = aAlfa1;
                    || centro nuevo, hay mas de un centro en este CCC !!
                    #cretam3a#7 = "00";
                    #cretam3a#8 = "Varios";
                    #cretam3a#9 = aAlfa1 + " " + aAlfa2;
                    |GRABA 020#cretam3a.a;
                    |LIBERA #cretam3a;
               |FINSI;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO GrabaCabeceraTramo;
     || Grabacion de Cabecera del tramo de Trabajador (el cretam3b)

     |DDEFECTO #cretam3b;
     #cretam3b#0 = nEmp;
     #cretam3b#1 = nAnyClave;
     #cretam3b#2 = nMesClave;
     #cretam3b#3 = nTipo;
     #cretam3b#4 = aCCC;
     #cretam3b#5 = aNaf;
     #cretam3b#6 = aDiaDesdeTramo;
     #cretam3b#139 = aAnyDesdeTramo;
     #cretam3b#140 = aMesDesdeTramo;

     |LEE 101#cretam3b.=;
     FEstado = FSalida;

     #cretam3b#7 = aFechaDesdeTramo;
     #cretam3b#8 = aFechaHastaTramo;
     #cretam3b#9 = aDias;

     aNaf = (aNaf + "                  ") % 115;

     |HAZ BuscaTrabPorNaf;
     #cretam3b#10 = nTra;
     #cretam3b#11 = #labtraba#2;
     #cretam3b#12 = #labtraba#7;
     #cretam3b#13 = aSoloFichero;
     #cretam3b#14 = aReferencia;

     #cretam3b#137 = nMesDesde;
     #cretam3b#138 = nMesHasta;

     |SI FEstado = 0;
          |GRABA 020#cretam3b.a;
     |SINO;
          |GRABA 020#cretam3b.n;
     |FINSI;

     |LIBERA #cretam3b;
|FPRC;

|PROCESO DatoCalculadoTramo;
     FLinea = 0;
     swAcaba = 0;

     nContConcepto = nContConcepto + 1;
     nPosicion = nContConcepto - 1;
     nCampo = 15 + (nPosicion * 6);

     FLinea = 0;
     swAcaba = 0;

     |PARA;
     |SI FLinea ] 0; |Y swAcaba = 0;
     |HACIENDO;
          FLinea = 0;
          |HAZ LeeLinea;

          aEtiqueta = "Codigo";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               nValor = aValor;
               #cretam3b#nCampo# = nValor;

               nCampo = nCampo + 1;
               #cretam19#0 = nValor;
               |LEE 000#cretam19.=;
               |SI FSalida = 0;
                    #cretam3b#nCampo# = #cretam19#1;
               |FINSI;
          |FINSI;

          aEtiqueta = "ValorBase";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               nCampo = nCampo + 1;
               nValor = aValor;
               #cretam3b#nCampo# = nValor / 100;
          |FINSI;

          aEtiqueta = "ValorCuotaTrabajador";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               nCampo = nCampo + 1;
               nValor = aValor;
               #cretam3b#nCampo# = nValor / 100;
          |FINSI;

          aEtiqueta = "ValorCuotaEmpresarial";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               nCampo = nCampo + 1;
               nValor = aValor;
               #cretam3b#nCampo# = nValor / 100;
          |FINSI;

          aEtiqueta = "ValorCuotaTotal";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               nCampo = nCampo + 1;
               nValor = aValor;
               #cretam3b#nCampo# = nValor / 100;
          |FINSI;

          aEtiqueta = "DatoCalculado";
          |HAZ BuscaEtiFin;
          |SI nLoc = 1;
               |SAL;
          |FINSI;
     |SIGUE;
|FPRC;

|PROCESO CalculosTramo;
     nContConcepto = 0;

     |DDEFECTO #cretam3b;
     #cretam3b#0 = nEmp;
     #cretam3b#1 = nAnyClave;
     #cretam3b#2 = nMesClave;
     #cretam3b#3 = nTipo;
     #cretam3b#4 = aCCC;
     #cretam3b#5 = aNaf;
     #cretam3b#6 = aDiaDesdeTramo;
     #cretam3b#139 = aAnyDesdeTramo;
     #cretam3b#140 = aMesDesdeTramo;

     |LEE 101#cretam3b.=;
     || entra siempre ya que lo acabo de grabar

     |PARA;
     |SI FLinea ] 0; |Y swAcaba = 0;
     |HACIENDO;
          FLinea = 0;
          |HAZ LeeLinea;
          aEtiqueta = "DatoCalculado";
          |HAZ BuscaEtiIni;
          |SI nLoc = 1;
               |HAZ DatoCalculadoTramo;
          |FINSI;

          aEtiqueta = "CalculosTramo";
          |HAZ BuscaEtiFin;
          |SI nLoc = 1;
               |GRABA 020#cretam3b.a;
               |LIBERA #cretam3b;
               |SAL;
          |FINSI;
     |SIGUE;
|FPRC;

|PROCESO Tramo;
     FLinea = 0;
     swAcaba = 0;

     |PARA;
     |SI FLinea ] 0; |Y swAcaba = 0;
     |HACIENDO;
          FLinea = 0;
          |HAZ LeeLinea;

          aEtiqueta = "FechaDesde";
          |HAZ BuscaEtiIni;
          |SI nLoc = 1;
              aCampoFecha = "FechaDesde";
              |HAZ TipoFecha;
              aDiaDesdeTramo = aDia;
              nDiaDesdeTramo = aDiaDesdeTramo;
              aMesDesdeTramo = aMes;
              aAnyDesdeTramo = aAny;
              aFechaDesdeTramo = aDiaDesdeTramo + "." + aMesDesdeTramo + "." + aAnyDesdeTramo;
          |FINSI;

          aEtiqueta = "FechaHasta";
          |HAZ BuscaEtiIni;
          |SI nLoc = 1;
              aCampoFecha = "FechaHasta";
              |HAZ TipoFecha;
              aDiaHastaTramo = aDia;
              aMesHastaTramo = aMes;
              aAnyHastaTramo = aAny;
              aFechaHastaTramo = aDiaHastaTramo + "." + aMesHastaTramo + "." + aAnyHastaTramo;
          |FINSI;

          aEtiqueta = "DiasCotizados";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               aDias = aValor;
          |FINSI;
          aEtiqueta = "CalculosTramo";
          |HAZ BuscaEtiIni;
          |SI nLoc = 1;
               |HAZ GrabaCabeceraTramo;
               |HAZ CalculosTramo;
          |FINSI;

          aEtiqueta = "Tramo";
          |HAZ BuscaEtiFin;
          |SI nLoc = 1;
               |SAL;
          |FINSI;
     |SIGUE;
|FPRC;

|PROCESO Tramos;
     FLinea = 0;
     swAcaba = 0;

     |PARA;
     |SI FLinea ] 0; |Y swAcaba = 0;
     |HACIENDO;
          FLinea = 0;
          |HAZ LeeLinea;

          aEtiqueta = "Tramo";
          |HAZ BuscaEtiIni;
          |SI nLoc = 1;
               |HAZ Tramo;
          |FINSI;

          aEtiqueta = "Tramos";
          |HAZ BuscaEtiFin;
          |SI nLoc = 1;
               |SAL;
          |FINSI;
     |SIGUE;
|FPRC;

|PROCESO Trabajador;
     FLinea = 0;
     swAcaba = 0;

     |PARA;
     |SI FLinea ] 0; |Y swAcaba = 0;
     |HACIENDO;
          FLinea = 0;
          |HAZ LeeLinea;

          aEtiqueta = "Naf";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               aNaf = aValor;
               aNaf = (aNaf + "                  ") % 115;
          |FINSI;
          aEtiqueta = "TipoIpf";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               aTipoIPF = aValor;
          |FINSI;

          aEtiqueta = "NumeroIpf";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               aNumeroIPF = aValor % -109;
          |FINSI;

          aEtiqueta = "Caf";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               aSiglas = aValor;
          |FINSI;

          aEtiqueta = "Tramos";
          |HAZ BuscaEtiIni;
          |SI nLoc = 1;
               |HAZ Tramos;
          |FINSI;

          aEtiqueta = "Trabajador";
          |HAZ BuscaEtiFin;
          |SI nLoc = 1;
               |SAL;
          |FINSI;
     |SIGUE;
|FPRC;

|PROCESO Trabajadores;
     FLinea = 0;
     swAcaba = 0;

     |PARA;
     |SI FLinea ] 0; |Y swAcaba = 0;
     |HACIENDO;
          FLinea = 0;
          |HAZ LeeLinea;

          aEtiqueta = "Trabajador";
          |HAZ BuscaEtiIni;
          |SI nLoc = 1;
               |HAZ Trabajador;
          |FINSI;

          aEtiqueta = "Trabajadores";
          |HAZ BuscaEtiFin;
          |SI nLoc = 1;
               |SAL;
          |FINSI;
     |SIGUE;
|FPRC;

|PROCESO GrabaCabecera;
     || primero grabo ya mi cabecera (el cretam3a)

     nEmp = 0;
     |HAZ BuscaPorCCC;

     swNoActiva = 0;
     #labempre#0 = nEmp;
     |LEE 000#labempre.=;
     |SI FSalida = 0; |Y #labempre#67 = "S";
          || esta activa
     |SINO;
          swNoActiva = 1;
          |ACABA;
     |FINSI;

     |DDEFECTO #cretam3a;
     #cretam3a#0 = nEmp;
     #cretam3a#1 = nAnyClave;
     #cretam3a#2 = nMesClave;
     #cretam3a#3 = nTipo;
     #cretam3a#4 = aCCC;
     #cretam3a#135 = nAnyAtrasos;
     |LEE 101#cretam3a.=;
     FEstado = FSalida;

     #cretam3a#6 = #labempre#2;
     #cretam3a#10 = aTipo;
     #cretam3a#11 = fFechaUlt;
     #cretam3a#12 = aHoraUlt;
     #cretam3a#13 = aAutorizado;
     #cretam3a#14 = aReferencia;
     #cretam3a#15 = aMesDesde;
     #cretam3a#16 = aAnyDesde;
     #cretam3a#17 = aMesHasta;
     #cretam3a#18 = aAnyHasta;
     #cretam3a#19 = aMesPres;
     #cretam3a#20 = aAnyPres;
     #cretam3a#21 = aNumLiq;
     #cretam3a#22 = aSoloFichero;
     #cretam3a#23 = aEstado;

     |SI nTipo = 90;
          nNume = aMesPres;
          |SI nNume = 01;
               nNume1 = 12;
               nNume2 = aAnyPres;
               nNume2 = nNume2 - 1;
          |SINO;
               nNume1 = nNume - 1;
               nNume2 = aAnyPres;
          |FINSI;
          #cretam3a#133 = nNume2;
          #cretam3a#134 = nNume1;
     |FINSI;

     |SI FEstado = 0;
          |GRABA 020#cretam3a.a;
     |SINO;
          |GRABA 020#cretam3a.n;
     |FINSI;

     |LIBERA #cretam3a;
|FPRC;

|PROCESO DatoCalculado;
     nContConcepto = nContConcepto + 1;
     nCampo = 25 + (nContConcepto * 5) + 1;

     FLinea = 0;
     swAcaba = 0;

     |PARA;
     |SI FLinea ] 0; |Y swAcaba = 0;
     |HACIENDO;
          FLinea = 0;
          |HAZ LeeLinea;

          aEtiqueta = "Codigo";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               nValor = aValor;
               #cretam3a#nCampo# = nValor;

               nCampo = nCampo + 1;
               #cretam19#0 = nValor;
               |LEE 000#cretam19.=;
               |SI FSalida = 0;
                    #cretam3a#nCampo# = #cretam19#1;
               |FINSI;
          |FINSI;

          aEtiqueta = "ValorCuotaTrabajador";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               nCampo = nCampo + 1;
               nValor = aValor;
               #cretam3a#nCampo# = nValor / 100;
          |FINSI;

          aEtiqueta = "ValorCuotaEmpresarial";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               nCampo = nCampo + 1;
               nValor = aValor;
               #cretam3a#nCampo# = nValor / 100;
          |FINSI;

          aEtiqueta = "ValorCuotaTotal";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               nCampo = nCampo + 1;
               nValor = aValor;
               #cretam3a#nCampo# = nValor / 100;
          |FINSI;

          aEtiqueta = "DatoCalculado";
          |HAZ BuscaEtiFin;
          |SI nLoc = 1;
               |SAL;
          |FINSI;
     |SIGUE;
|FPRC;

|PROCESO CalculosLiquidacion;
     |HAZ GrabaCabecera;
     |SI swNoActiva = 1;
          |ACABA;
     |FINSI;

     |DDEFECTO #cretam3a;
     #cretam3a#0 = nEmp;
     #cretam3a#1 = nAnyClave;
     #cretam3a#2 = nMesClave;
     #cretam3a#3 = nTipo;
     #cretam3a#4 = aCCC;
     #cretam3a#135 = nAnyAtrasos;
     |LEE 101#cretam3a.=;
     |SI FSalida = 0;
          || lo sera siempre, lo he grabado antes

          FLinea = 0;
          swAcaba = 0;

          |PARA;
          |SI FLinea ] 0; |Y swAcaba = 0;
          |HACIENDO;
               FLinea = 0;
               |HAZ LeeLinea;

               aEtiqueta = "DatoCalculado";
               |HAZ BuscaEtiIni;
               |SI nLoc = 1;
                    |HAZ DatoCalculado;
               |FINSI;

               aEtiqueta = "CalculosLiquidacion";
               |HAZ BuscaEtiFin;
               |SI nLoc = 1;
                    |GRABA 020#cretam3a.a;
                    |SAL;
               |FINSI;
          |SIGUE;
     |FINSI;
|FPRC;

|PROCESO Lectura;
     FLinea = 0;

     aAlfa = eaFichero;
     |XABRE "", aAlfa, nHandle; nHandle = FSalida;
     |SI FSalida ] 0;
          |PARA;
          |SI FLinea ] 0; |Y swAcaba = 0;
          |HACIENDO;
               FLinea = 0;
               |HAZ LeeLinea;

               aEtiqueta = "Autorizado";
               |HAZ BuscaEtiIniFin;
               |SI nLoc = 1;
                    aAutorizado = aValor;
               |FINSI;

               aEtiqueta = "ReferenciaExterna";
               |HAZ BuscaEtiIniFin;
               |SI nLoc = 1;
                    aReferencia = aValor;
               |FINSI;

               aEtiqueta = "Ccc";
               |HAZ BuscaEtiIni;
               |SI nLoc = 1;
                    |HAZ CCC;
               |FINSI;

               aEtiqueta = "PeriodoDesde";
               |HAZ BuscaEtiIni;
               |SI nLoc = 1;
                    aCampoFecha = "PeriodoDesde";
                    |HAZ TipoFecha;
                    aMesDesde = aMes;
                    nMesDesde = aMes;
                    aAnyDesde = aAny;
                    nAnyDesde = aAny;
               |FINSI;

               aEtiqueta = "PeriodoHasta";
               |HAZ BuscaEtiIni;
               |SI nLoc = 1;
                    aCampoFecha = "PeriodoHasta";
                    |HAZ TipoFecha;
                    aMesHasta = aMes;
                    aAnyHasta = aAny;
                    nMesHasta = aMes;
                    nAnyHasta = aAny;
               |FINSI;

               aEtiqueta = "Tipo";
               |HAZ BuscaEtiIniFin;
               |SI nLoc = 1;
                    swAtrasos = 0;
                    aTipo = aValor;
                    |SI aAlfa = "L00"; nTipo = 00; |FINSI;
                    |SI aAlfa = "L13"; nTipo = 02; |FINSI;
                    |SI aAlfa = "L90"; nTipo = 90; |FINSI;
                    |SI aAlfa = "L91"; nTipo = 91; |FINSI;

                    |SI aAlfa = "L00"; |O aAlfa = "L13";
                    |O aAlfa = "L90"; |O aAlfa = "L91";
                         nMesClave = nMesDesde;
                         nAnyClave = nAnyDesde;
                    |FINSI;

                    |SI aTipo = "L03";
                         swAtrasos = 1;
                         nTipo = 999;
                         || son atrasos, hay que buscar el nTipo
                         || pero esto lo hare despues en la fecha de control

                         nAnyAtrasos = nAnyDesde;
                    |FINSI;

                    || con el tipo a 999, en atrasos no borrara nada, lo
                    || hara despues

                    nDesdeAnyAtr = 0000; nHastaAnyAtr = 0000;
                    |BUCLE Borra_cretam3a;
                    |BUCLE Borra_cretam3b;
                    |SELECT #1#cretam3a;
                    |SELECT #1#cretam3b;
               |FINSI;

               aEtiqueta = "FechaControl";
               |HAZ BuscaEtiIni;
               |SI nLoc = 1;
                    aCampoFecha = "FechaControl";
                    |HAZ TipoFecha;

                    nMesControl = aMes;
                    nAnyControl = aAny;

                    nMesClave = nMesControl;
                    nAnyClave = nAnyControl;

                    || si encuentro este campo es que estoy en atrasos
                    || y ya tengo leido que es tipo L03, ahora ire a buscar que
                    || tipo de atrasos eran

                    |HAZ BuscaTipoAtrasos;

                    |SI nTipo = 00;
                         aAlfa = "    ATENCION. No se ha encontrado registro de Atrasos ";
                         aAlfa = aAlfa + " del mes " + aMesDesde + " al " + aMesHasta;
                         aAlfa = aAlfa + " de " + aAnyDesde + " en CCC: " + aCCC;
                         |MENAV aAlfa;
                    |FINSI;

                    || el bucle de borrado el cretam31 en atrasos es distinto
                    || porque en la cabecera se graba la fecha de control

                    || primero esto por si se me quedo algun a¤o 0000
                    nDesdeAnyAtr = 0000; nHastaAnyAtr = 0000;
                    |BUCLE Borra_cretam3a;
                    nDesdeAnyAtr = nAnyAtrasos; nHastaAnyAtr = nAnyAtrasos;
                    |BUCLE Borra_cretam3a;

                    |BUCLE Borra_cretam3b;
                    |SELECT #1#cretam3a;
                    |SELECT #1#cretam3b;
               |FINSI;

               aEtiqueta = "PeriodoPresentacion";
               |HAZ BuscaEtiIni;
               |SI nLoc = 1;
                    aCampoFecha = "PeriodoPresentacion";
                    |HAZ TipoFecha;
                    aMesPres = aMes;
                    aAnyPres = aAny;
               |FINSI;

               aEtiqueta = "NumeroLiquidacion";
               |HAZ BuscaEtiIniFin;
               |SI nLoc = 1;
                    aNumLiq = aValor;
               |FINSI;

               aEtiqueta = "Estado";
               |HAZ BuscaEtiIniFin;
               |SI nLoc = 1;
                    aEstado = aValor;
               |FINSI;

               aEtiqueta = "FechaUltimoCalculo";
               |HAZ BuscaEtiIni;
               |SI nLoc = 1;
                    |HAZ FechaUltimoCalculo;
               |FINSI;

               aEtiqueta = "CalculosLiquidacion";
               |HAZ BuscaEtiIni;
               |SI nLoc = 1;
                    |HAZ CalculosLiquidacion;
                    |SI swNoActiva = 1;
                         |ACABA;
                    |FINSI;
               |FINSI;

               aEtiqueta = "Trabajadores";
               |HAZ BuscaEtiIni;
               |SI nLoc = 1;
                    |HAZ Trabajadores;
               |FINSI;

               aEtiqueta = "Liquidacion";
               |HAZ BuscaEtiFin;
               |SI nLoc = 1;
                    |SAL;
               |FINSI;
          |SIGUE;
     |FINSI;

     |XCIERRA nHandle;
|FPRC;

|PROGRAMA;
     |ABRE #cretam3a;
     |ABRE #cretam3b;

     |ABRE #cretam19;

     |ABRE #labempre;
     |ABRE #labcentr;
     |ABRE #nomcenes;
     |ABRE #labtraba;

     |HAZ Lectura;

     |CIERRA #cretam3a;
     |CIERRA #cretam3b;

     |CIERRA #cretam19;

     |CIERRA #labempre;
     |CIERRA #labcentr;
     |CIERRA #nomcenes;
     |CIERRA #labtraba;
|FPRO;
