|FICHEROS;
     cretai45 #0;

     cretam45;

     labempre;

     cretam02;      || mto. liquidaciones CCC
     cretam22;      || respuestas fich. trab.-tram. CCC
     cretam27;      || respuestas fich. bases CCC
     cretam25;      || respuestas fich. bases NAF
     cretam31;      || RNT por CCC
     cretam34;      || DCL por CCC
     cretam36;      || RLC por CCC
     cretam3a;      || Calculos por CCC
     cretae02;      || Registro Ficheros Creados por  CCC
     cretam38;      || respuestas recogidas
|FIN;

|VARIABLES;
     &nError = 0;
     nCampo = 0;
     nCampo1 = 0;
     nCampo2 = 0;
     FEstado = 0;
     aAlfa = "";
     aAntCCC = "";

     fFecha = @;
     aFecha = "";
     nMes = 0;
     nAny = 0;
     nMesL13 = 0;
     nAnyL13 = 0;
     aReferencia = "";
     nNume = 0;
     nTramos = 0;
     nImpresion = 0;

     aTipo = "";
     swFormL13 = 0;
     swCCCOk = 0;
     nEnviados = 0;

     nMesControl = 0;
     nAnyControl = 0;
     swSigue = 0;
     nCampoError = 0;

     nAscii = 0;
     aCaracter = "";
     aCaracterSal = "";
     aCadenaSal = "";
     aCadena = "";
     aLongitud = "";
     nLongitud = 0;
     nPos = 0;
     nBusca = 0;
|FIN;

|PROCESO Defectos;
     fFecha = ~;
     aFecha = fFecha;
     aAlfa = aFecha % -104;
     nAny = aAlfa;

     aAlfa = aFecha % 402;
     nMes = aAlfa;
     nMes = nMes - 1;
     |SI nMes = 0;
          nMes = 12;
          nAny = nAny - 1;
     |FINSI;

     #0#2 = nAny;
     #0#3 = nMes;
     |PINTA #0#2;
     |PINTA #0#3;
|FPRC;

|PROCESO ElIMPRE;
     Impresora = "CrystalReport";
     |IMPRE -1;
     |SI FSalida = 0;
          |INFOR "cretai45";
     |FINSI;
|FPRC;

|PROCESO ElFINIMP;
     |PIEINF;
     |FININF;
     |FINIMP;
|FPRC;

|PROCESO sustituye2;
     nAscii = & aCaracter;
     aCaracterSal = aCaracter;
     |SI nAscii = 160; |O nAscii = 225;  || el caracter  
          aCaracterSal = "a";
     |FINSI;
     |SI nAscii = 130;             || el caracter ‚
          aCaracterSal = "e";
     |FINSI;
     |SI nAscii = 161; |O nAscii = 237; || el caracter ¡
          aCaracterSal = "i";
     |FINSI;
     |SI nAscii = 162; |O nAscii = 243;            || el caracter ¢
          aCaracterSal = "o";
     |FINSI;
     |SI nAscii = 163;             || el caracter £
          aCaracterSal = "u";
     |FINSI;
     /*
     |SI nAscii = 164;             || el caracter ¤
          aCaracterSal = "\361";
     |FINSI;
     |SI nAscii = 241;             || el caracter ¤ (otro codigo ¨?)
          aCaracterSal = "\361";
     |FINSI;
     |SI nAscii = 165;             || el caracter ¥
          aCaracterSal = "\321";
     |FINSI;
     |SI nAscii = 129;
          aCaracterSal = "\374";   || el caracter u con dieresis
     |FINSI;
     |SI nAscii = 154;
          aCaracterSal = "\334";   || el caracter U (mayuscula) con dieresis
     |FINSI;
     |SI nAscii = 167;
          aCaracterSal = "\272";   || el caracter §
     |FINSI;
     |SI nAscii = 166;
          aCaracterSal = "\252";   || el caracter ¦
     |FINSI;
     |SI aCaracter = "(";
          aCaracterSal = "\(";
     |FINSI;
     |SI aCaracter = ")";
          aCaracterSal = "\)";
     |FINSI;
     */
|FPRC;

|PROCESO sustituye;
     aCadenaSal = "";
     aLongitud = aCadena % 0;
     nLongitud = aLongitud;
     |PARA nPos = 1; |SI nPos [ nLongitud; |HACIENDO nPos = nPos + 1;
          nBusca = (nPos * 100) + 1;
          aCaracter = aCadena % nBusca;
          |HAZ sustituye2;
          aCadenaSal = aCadenaSal + aCaracterSal;
     |SIGUE;
|FPRC;

|PROCESO Tildes;
     |PARA nCampo = 24; |SI nCampo [ 28; |HACIENDO nCampo = nCampo + 1;
          aCadena = #cretam45#nCampo#;
          |QBF aCadena
          |SI aCadena ! "";
               |HAZ sustituye;
               #cretam45#nCampo# = aCadenaSal;
          |FINSI;
     |SIGUE;
|FPRC;

|PROCESO impresion;
     |SI #0#4 = "P";
          aAlfa = #cretam45#6;
          |QBF aAlfa;
          |SI aAlfa ! "Aceptada"; |Y aAlfa ! "Confirmada";
               |ACABA;
          |FINSI;
     |FINSI;

     |SI nImpresion = 0;
          |HAZ ElIMPRE;
     |FINSI;

     |HAZ Tildes;

     |PRINT;
     nImpresion = nImpresion + 1;
|FPRC;

|DEFBUCLE Impresion;
     #cretam45#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, impresion;
|FIN;

|PROCESO cretae02_bases;
     nEnviados = nEnviados + 1;
|FPRC;

|DEFBUCLE cretae02_bases;
     #cretae02#1;
     7;
     #cretam02#0, #cretam02#1, #cretam02#2, #cretam02#3, #cretam02#4, "        ", "B";
     #cretam02#0, #cretam02#1, #cretam02#2, #cretam02#3, #cretam02#4, "zzzzzzzz", "B";
     ;
     NULL, cretae02_bases;
|FIN;

|PROCESO cretae02;
     |DDEFECTO #cretam45;
     #cretam45#0 = #cretae02#0;
     #cretam45#1 = #cretae02#1;
     #cretam45#2 = #cretae02#2;
     #cretam45#3 = #cretae02#3;
     #cretam45#4 = #cretae02#4;
     #cretam45#23 = #cretae02#19;
     |SI #cretae02#3 < 5;     || esto deberia estar ya a cero ya que se deberia
          #cretam45#23 = 0;   || grabar a cero en tipo 00 o 02, pero se graba
     |FINSI;                  || con valores, se usa en otros sitios
     |SI #cretam45#3 = 90;
          |HAZ BuscaFechaControl;
          |SI nAnyControl ! #0#2; |O nMesControl ! #0#3;
               |ACABA;
          |FINSI;
     |FINSI;
     |LEE 101#cretam45.=;
     |SI FSalida ! 0;
          |ACABA;        || tiene que existir ya el registro ya que la
                         || liquidacion ha de estar creada para que se pueda
                         || haber enviado el fichero de TT o alguna otra
                         || solicitud cualquiera

          #labempre#0 = #cretae02#0;
          |LEE 000#labempre.=;
          #cretam45#5 = #labempre#2;

          |GRABA 020#cretam45.n;
          |LEE 101#cretam45.=;
     |FINSI;

     || solicitud Trabajadores y Tramos generada
     |SI #cretae02#7 = "T";
          #cretam45#8 = "Generada";

          || Si existe se marca Generado, dependiendo del valor del campo 8
          || estara enviado, aceptado o rechazado (con errores, esto es raro)

          |SI #cretae02#8 = "V"; #cretam45#8 = "Enviada Sol."; |FINSI;
          |SI #cretae02#8 = "C"; #cretam45#8 = "Recibido"; |FINSI;
          |SI #cretae02#8 = "E"; #cretam45#8 = "Rechazado"; |FINSI;
     |FINSI;

     || solicitud borrador generada
     |SI #cretae02#7 = "R";
          #cretam45#10 = "Generada";

          || Si existe se marca Generado, dependiendo del valor del campo 8
          || estara enviado, aceptado o rechazado (con errores, esto es raro)

          |SI #cretae02#8 ! " "; #cretam45#10 = "Enviada"; |FINSI;
     |FINSI;

     || solicitud confirmacion generada
     |SI #cretae02#7  = "F";
          #cretam45#11 = "Generada";

          || Si existe se marca Generado, dependiendo del valor del campo 8
          || estara enviado, aceptado o rechazado (con errores, esto es raro)

          |SI #cretae02#8 = "V"; #cretam45#11 = "Enviada"; |FINSI;
          |SI #cretae02#8 = "C"; #cretam45#11 = "Aceptada"; |FINSI;
          |SI #cretae02#8 = "E"; #cretam45#11 = "Rechazada"; |FINSI;

          ||HAZ Confirmacion;       || busqueda otros errores posteriores

     |FINSI;

     || solicitud calculos generada
     |SI #cretae02#7  = "C";
          #cretam45#12 = "Generada";

          || Si existe se marca Generado, dependiendo del valor del campo 8
          || estara enviado, aceptado o rechazado (con errores, esto es raro)

          |SI #cretae02#8 = "V"; #cretam45#12 = "Enviada"; |FINSI;
          |SI #cretae02#8 = "C"; #cretam45#12 = "Aceptada"; |FINSI;
          |SI #cretae02#8 = "E"; #cretam45#12 = "Rechazada"; |FINSI;
     |FINSI;

     |GRABA 020#cretam45.a;
     |LIBERA #cretam45;
|FPRC;

|DEFBUCLE cretae02;
     #cretae02#2;
     ;
     #0#0, #0#2, #0#3, 00, "               ", aTipo;
     #0#1, #0#2, #0#3, 99, "zzzzzzzzzzzzzzz", aTipo;
     ;
     NULL, cretae02;
|FIN;

|PROCESO cretam3a;
     |DDEFECTO #cretam45;
     #cretam45#0 = #cretam3a#0;
     #cretam45#1 = #cretam3a#1;
     #cretam45#2 = #cretam3a#2;
     #cretam45#3 = #cretam3a#3;
     #cretam45#4 = #cretam3a#4;
     #cretam45#23 = #cretam3a#135;

     |SI #cretam45#3 = 90;
          |HAZ BuscaFechaControl;
          |SI nAnyControl ! #0#2; |O nMesControl ! #0#3;
               |ACABA;
          |FINSI;
     |FINSI;

     |LEE 101#cretam45.=;
     |SI FSalida ! 0;
          #labempre#0 = #cretam3a#0;
          |LEE 000#labempre.=;
          #cretam45#5 = #labempre#2;

          |GRABA 020#cretam45.n;

          |LEE 101#cretam45.=;
     |FINSI;

     #cretam45#16 = "Disponible";
     |GRABA 020#cretam45.a;
     |LIBERA #cretam45;
|FPRC;

|DEFBUCLE cretam3a_L90;
     #cretam3a#1;
     ;
     #0#0, #0#2, #0#3, 00, "               ", 0000;
     #0#1, #0#2, #0#3, 99, "zzzzzzzzzzzzzzz", 0000;
     ;
     NULL, cretam3a;
|FIN;

|DEFBUCLE cretam3a;
     #cretam3a#1;
     ;
     #0#0, #0#2, #0#3, 00, "               ", 0000;
     #0#1, #0#2, #0#3, 99, "zzzzzzzzzzzzzzz", 2999;
     ;
     NULL, cretam3a;
|FIN;

|PROCESO cretam36;
     |DDEFECTO #cretam45;
     #cretam45#0 = #cretam36#0;
     #cretam45#1 = #cretam36#1;
     #cretam45#2 = #cretam36#2;
     #cretam45#3 = #cretam36#3;
     #cretam45#4 = #cretam36#4;
     #cretam45#23 = #cretam36#58;

     |SI #cretam45#3 = 90;
          |HAZ BuscaFechaControl;
          |SI nAnyControl ! #0#2; |O nMesControl ! #0#3;
               |ACABA;
          |FINSI;
     |FINSI;

     |LEE 101#cretam45.=;
     |SI FSalida ! 0;
          #labempre#0 = #cretam36#0;
          |LEE 000#labempre.=;
          #cretam45#5 = #labempre#2;

          |GRABA 020#cretam45.n;

          |LEE 101#cretam45.=;
     |FINSI;

     #cretam45#15 = "Disponible";
     |GRABA 020#cretam45.a;
     |LIBERA #cretam45;
|FPRC;

|DEFBUCLE cretam36_L90;
     #cretam36#3;
     ;
     #0#0, #0#2, #0#3, 00, "               ";
     #0#1, #0#2, #0#3, 99, "zzzzzzzzzzzzzzz";
     ;
     NULL, cretam36;
|FIN;

|DEFBUCLE cretam36;
     #cretam36#1;
     ;
     #0#0, #0#2, #0#3, 00, "               ", 0000;
     #0#1, #0#2, #0#3, 99, "zzzzzzzzzzzzzzz", 2999;
     ;
     NULL, cretam36;
|FIN;

|PROCESO cretam34;
     |DDEFECTO #cretam45;
     #cretam45#0 = #cretam34#0;
     #cretam45#1 = #cretam34#1;
     #cretam45#2 = #cretam34#2;
     #cretam45#3 = #cretam34#3;
     #cretam45#4 = #cretam34#4;
     #cretam45#23 = #cretam34#33;

     |SI #cretam45#3 = 90;
          |HAZ BuscaFechaControl;
          |SI nAnyControl ! #0#2; |O nMesControl ! #0#3;
               |ACABA;
          |FINSI;
     |FINSI;

     |LEE 101#cretam45.=;
     |SI FSalida ! 0;
          #labempre#0 = #cretam34#0;
          |LEE 000#labempre.=;
          #cretam45#5 = #labempre#2;

          |GRABA 020#cretam45.n;

          |LEE 101#cretam45.=;
     |FINSI;

     #cretam45#14 = "Disponible";
     |GRABA 020#cretam45.a;
     |LIBERA #cretam45;
|FPRC;

|DEFBUCLE cretam34_L90;
     #cretam34#3;
     ;
     #0#0, #0#2, #0#3, 00, "               ";
     #0#1, #0#2, #0#3, 99, "zzzzzzzzzzzzzzz";
     ;
     NULL, cretam34;
|FIN;

|DEFBUCLE cretam34;
     #cretam34#1;
     ;
     #0#0, #0#2, #0#3, 00, "               ", 0000;
     #0#1, #0#2, #0#3, 99, "zzzzzzzzzzzzzzz", 2999;
     ;
     NULL, cretam34;
|FIN;

|PROCESO BuscaFechaControl;
     nMesControl = 0;
     nAnyControl = 0;

     |DDEFECTO #cretam02;
     #cretam02#0 = #cretam45#0;
     #cretam02#1 = #cretam45#1;
     #cretam02#2 = #cretam45#2;
     #cretam02#3 = #cretam45#3;
     #cretam02#4 = #cretam45#4;
     |LEE 000#cretam02.=;
     |SI FSalida = 0;
          nAnyControl = #cretam02#21;
          nMesControl = #cretam02#20;

          #cretam45#1 = nAnyControl;
          #cretam45#2 = nMesControl;

          #cretam45#21 = #cretam02#2;
          #cretam45#22 = #cretam02#1;
     |FINSI;
|FPRC;

|PROCESO cretam31;
     |DDEFECTO #cretam45;
     #cretam45#0 = #cretam31#0;
     #cretam45#1 = #cretam31#1;
     #cretam45#2 = #cretam31#2;
     #cretam45#3 = #cretam31#3;
     #cretam45#4 = #cretam31#4;
     #cretam45#23 = #cretam31#83;

     |SI #cretam45#3 = 90;
          |HAZ BuscaFechaControl;
          |SI nAnyControl ! #0#2; |O nMesControl ! #0#3;
               |ACABA;
          |FINSI;
     |FINSI;

     |LEE 101#cretam45.=;
     |SI FSalida ! 0;
          #labempre#0 = #cretam31#0;
          |LEE 000#labempre.=;
          #cretam45#5 = #labempre#2;

          |GRABA 020#cretam45.n;

          |LEE 101#cretam45.=;
     |FINSI;

     #cretam45#13 = "Disponible";
     |GRABA 020#cretam45.a;
     |LIBERA #cretam45;
|FPRC;

|DEFBUCLE cretam31_L90;
     #cretam31#3;
     ;
     #0#0, #0#2, #0#3, 00, "               ";
     #0#1, #0#2, #0#3, 99, "zzzzzzzzzzzzzzz";
     ;
     NULL, cretam31;
|FIN;

|DEFBUCLE cretam31;
     #cretam31#1;
     ;
     #0#0, #0#2, #0#3, 00, "               ", 0000;
     #0#1, #0#2, #0#3, 99, "zzzzzzzzzzzzzzz", 2999;
     ;
     NULL, cretam31;
|FIN;

|PROCESO cretae02_atrasos;
     |DDEFECTO #cretam45;
     #cretam45#0 = #cretae02#0;
     #cretam45#1 = #cretae02#1;
     #cretam45#2 = #cretae02#2;
     #cretam45#3 = #cretae02#3;
     #cretam45#4 = #cretae02#4;
     #cretam45#23 = #cretae02#19;
     |SI #cretam45#3 = 90;
          |HAZ BuscaFechaControl;
          |SI nAnyControl ! #0#2; |O nMesControl ! #0#3;
               |ACABA;
          |FINSI;
     |FINSI;
     |LEE 101#cretam45.=;
     |SI FSalida ! 0;
          #labempre#0 = #cretae02#0;
          |LEE 000#labempre.=;
          #cretam45#5 = #labempre#2;
          #cretam45#6 = "Creada";
          #cretam45#9 = "Generado";

          #cretam45#17 = #cretae02#17;
          #cretam45#18 = #cretae02#18;
          #cretam45#19 = #cretae02#19;

          |GRABA 020#cretam45.n;

          |LEE 101#cretam45.=;
     |SINO;
          #cretam45#17 = #cretae02#17;
          #cretam45#18 = #cretae02#18;
          #cretam45#19 = #cretae02#19;
          |GRABA 020#cretam45.a;

          |LEE 101#cretam45.=;
     |FINSI;

     || ahora busco el estado de la liquidacion en el cretam02

     #cretam02#0 = #cretae02#0;
     #cretam02#1 = #cretae02#1;
     #cretam02#2 = #cretae02#17;   || el mes desde, por ejemplo, se graba el
     #cretam02#3 = #cretae02#3;    || estado en todos los meses de todos modos
     #cretam02#4 = #cretae02#4;
     swSigue = 0;
     |LEE 000#cretam02.=;
     |SI FSalida = 0;
          swSigue = 1;
     |SINO;
          |SI #cretam02#3 ] 5; |Y #cretam02#3 [ 20;
               || atrasos, pruebo a buscar cualquier mes del a¤o de liquidacion
               || y del a¤o de la fecha de control
               |PARA nNume = #cretae02#17; |SI nNume [ #cretae02#18;
               |HACIENDO nNume = nNume + 1;
                    #cretam02#1 = #cretae02#1;
                    #cretam02#2 = nNume;
                    |LEE 000#cretam02.=;
                    |SI FSalida = 0;
                         swSigue = 1;
                         |SAL;
                    |FINSI;
               |SIGUE;
               |SI swSigue = 0;
                    |PARA nNume = #cretae02#17; |SI nNume [ #cretae02#18;
                    |HACIENDO nNume = nNume + 1;
                         #cretam02#1 = #cretae02#19;
                         #cretam02#2 = nNume;
                         |LEE 000#cretam02.=;
                         |SI FSalida = 0;
                              swSigue = 1;
                              |SAL;
                         |FINSI;
                    |SIGUE;
               |FINSI;
          |FINSI;
     |FINSI;
     |SI swSigue = 1;
          |HAZ EstadoFichBases;

          |GRABA 020#cretam45.a;
          |LIBERA #cretam45;
     |FINSI;
|FPRC;

|DEFBUCLE cretae02_atrasos;
     #cretae02#2;
     ;
     #0#0, #0#2, #0#3, 05, "               ", "B";
     #0#1, #0#2, #0#3, 20, "zzzzzzzzzzzzzzz", "B";
     ;
     NULL, cretae02_atrasos;
|FIN;

|PROCESO cretam27;
     |SI #cretam27#14 = "R9529";
          swCCCOk = 1;
     |FINSI;
|FPRC;

|DEFBUCLE cretam27;
     #cretam27#1;
     ;
     #cretam02#0, #cretam02#1, #cretam02#2, #cretam02#3, #cretam02#4, "        ";
     #cretam02#0, #cretam02#1, #cretam02#2, #cretam02#3, #cretam02#4, "zzzzzzzz";
     ;
     NULL, cretam27;
|FIN;

|PROCESO CompRptaBases;
     || es posible que este marcado como erroneo cuando en verdad el CCC
     || esta aceptado, en este caso es por que habia algun trabajador
     || rechazado, pero en verdad la liquidacion esta calculada

     || vamos a comprobarlo

     |BUCLE cretam27;
|FPRC;

|PROCESO MeteError;
     |PARA nCampoError = 24; |SI nCampoError [ 28;
     |HACIENDO nCampoError = nCampoError + 1;
          aAlfa = #cretam45#nCampoError#;
          |QBF aAlfa;
          |SI aAlfa = "";
               #cretam45#nCampoError# = #cretam38#nCampo2#;
               |SAL;
          |FINSI;
     |SIGUE;
|FPRC;

|PROCESO cretam38;
     |PARA nCampo = 15; |SI nCampo [ 23; |HACIENDO nCampo = nCampo + 2;
          nCampo2 = nCampo + 1;
          aAlfa = #cretam38#nCampo#;
          |SI aAlfa = "A9762";
               |HAZ MeteError;
               #cretam45#6 = "Pendiente";
               #cretam45#11 = "Rechazada";
          |FINSI;
          |SI aAlfa = "R9523";
               |HAZ MeteError;
               #cretam45#6 = "Pendiente";
               #cretam45#11 = "Rechazada";
          |FINSI;
     |SIGUE;
|FPRC;

|DEFBUCLE cretam38;
     #cretam38#1;
     ;
     #cretam45#0, #cretam45#1, #cretam45#2, #cretam45#3, #cretam45#4, "F", "        ";
     #cretam45#0, #cretam45#1, #cretam45#2, #cretam45#3, #cretam45#4, "F", "zzzzzzzz";
     ;
     NULL, cretam38;
|FIN;

|PROCESO Confirmacion;
     || se da el caso de que desde que tesoreria envia el RNT y el RLC
     || para confirmar, hasta que despues se confirma, segun Tesoreria la
     || liquidacion ha cambiado, en este caso lo que hacen es que emiten
     || un nuevo RLC, y ojo porque ESTE HAY QUE CONFIRMARLO, si no, ellos no
     || lo confirman de oficio, lo que hacen es pasarlo a pago electronico
     || y hay que pagarlo en el banco

     || esto puede provocar que la liquidacion se quede sin confirmar, como
     || le paso a Lopez Andres (3923.1898)

     || vamos a buscar este tipo de respuesta en el cretam38 para contemplar
     || esto

     |ABRE #cretam38;

     |BUCLE cretam38;    || busco respuestas "F"

     |CIERRA #cretam38;
|FPRC;

|PROCESO EstadoFichBases;
     |SI #cretam02#7 = "G";
          #cretam45#9 = "Generado";
     |FINSI;
     |SI #cretam02#7 = "V";
          #cretam45#9 = "Enviado";
          #cretam45#6 = "Pendiente";

          || Vamos a buscar, en el caso de estar pendiente a ver si es el
          || primer envio o es otro, si es el segundo o posteriores, lo indico-
          || Asi si ya se acepto en el primero y tiene disponibles RNT y DLC
          || se sabe que ha sido un reenvio por lo que aparece pendiente.

          |SI #cretam45#3 < 5;           || solo en tipo 00
               nEnviados = 0;
               aTipo = "B";
               |BUCLE cretae02_bases;
               |SELECT #1#cretae02;
               |SI nEnviados > 1;
                    aAlfa = nEnviados;
                    |QBF aAlfa;
                    aAlfa = "Enviado (" + aAlfa + ")";
                    #cretam45#9 = aAlfa;
               |FINSI;
          |FINSI;
     |FINSI;
     |SI #cretam02#7 = "E";
          #cretam45#9 = "Rechazado";
          #cretam45#6 = "Rechazada";
          swCCCOk = 0;
          |HAZ CompRptaBases;
          |SI swCCCOk = 1;
               #cretam45#6 = "Aceptada";
               #cretam45#9 = "Aceptado";
               #cretam45#20 = 1;
          |FINSI;
     |FINSI;
     |SI #cretam02#7 = "C";
          #cretam45#6 = "Aceptada";
          #cretam45#9 = "Aceptado";
     |FINSI;
     |SI #cretam02#7 = "F";
          #cretam45#9 = "Aceptado";
          #cretam45#6 = "Confirmada";
     |FINSI;
     |SI #cretam02#7 = "O";
          #cretam45#9 = "Aceptado";
          #cretam45#6 = "Conf.Oficio";
     |FINSI;
|FPRC;

|PROCESO cretam02;
     |DDEFECTO #cretam45;
     #cretam45#0 = #cretam02#0;
     #cretam45#1 = #cretam02#1;
     #cretam45#2 = #cretam02#2;
     #cretam45#3 = #cretam02#3;
     #cretam45#4 = #cretam02#4;
     #cretam45#23 = #cretam02#19;
     |SI swFormL13 = 1;
          #cretam45#1 = #0#2;
          #cretam45#2 = #0#3;
     |FINSI;
     |SI #cretam02#3 = 90;
          #cretam45#1 = #cretam02#21;
          #cretam45#2 = #cretam02#20;

          #cretam45#21 = #cretam02#2;
          #cretam45#22 = #cretam02#1;
          #cretam45#23 = 0;   || este campo solo en atrasos L03
     |FINSI;
     |LEE 101#cretam45.=;
     |SI FSalida ! 0;
          #labempre#0 = #cretam02#0;
          |LEE 000#labempre.=;
          #cretam45#5 = #labempre#2;
          #cretam45#6 = "Creada";

          |GRABA 020#cretam45.n;

          |LEE 101#cretam45.=;
     |FINSI;

     #cretam45#7 = "Generado";

     || FICHERO DE TRABAJADORES Y TRAMOS

     |SI #cretam02#9 = "G"; #cretam45#8 = "Generada Sol."; |FINSI;
     |SI #cretam02#9 = "V"; #cretam45#8 = "Enviada Sol."; |FINSI;
     |SI #cretam02#9 = "C"; #cretam45#8 = "Recibido"; |FINSI;
     |SI #cretam02#9 = "E"; #cretam45#8 = "Rechazado"; |FINSI;

     || FICHERO DE BASES

     |HAZ EstadoFichBases;

     |GRABA 020#cretam45.a;
     |LIBERA #cretam45;
|FPRC;

|DEFBUCLE FormacionL13;
     #cretam02#1;
     11;
     #0#0, nAnyL13, nMesL13, 02, "               ", "S";
     #0#1, nAnyL13, nMesL13, 02, "zzzzzzzzzzzzzzz", "S";
     ;
     NULL, cretam02;
|FIN;

|DEFBUCLE cretam02_L90;
     #cretam02#1;
     20, 21;
     #0#0, 2000, 01, 90, "               ", #0#3, #0#2;
     #0#1, 2999, 12, 90, "zzzzzzzzzzzzzzz", #0#3, #0#2;
     ;
     NULL, cretam02;
|FIN;

|DEFBUCLE cretam02;
     #cretam02#1;
     ;
     #0#0, #0#2, #0#3, 00, "               ";
     #0#1, #0#2, #0#3, 02, "zzzzzzzzzzzzzzz";
     ;
     NULL, cretam02;
|FIN;

|PROCESO Seleccion;
     || empiezo por las liquidaciones
     |BUCLE cretam02;
     |BUCLE cretam02_L90;
/*
     || es no ya no es necesario
     nAnyL13 = #0#2;
     nMesL13 = #0#3 - 1;
     |SI nMesL13 = 00;
          nMesL13 = 12;
          nAnyL13 = #0#2 - 1;
     |FINSI;
     swFormL13 = 1;

     |BUCLE FormacionL13;
     swFormL13 = 0;
*/
     || para grabar en el temporal la liquidacion de atrasos
     || debo conocer la fecha de abono de los atrasos, y eso se guarda
     || al generar el fichero de bases

     |BUCLE cretae02_atrasos;

     |BUCLE cretam31;         || busqueda RNT
     |BUCLE cretam34;         || busqueda DLC
     |BUCLE cretam36;         || busqueda RCL
     |BUCLE cretam3a;         || busqueda Calculos

     |BUCLE cretam31_L90;         || busqueda RNT
     |BUCLE cretam34_L90;         || busqueda DLC
     |BUCLE cretam36_L90;         || busqueda RLC
     |BUCLE cretam3a_L90;         || busqueda Calculos

     aTipo = "T";             || vamos a buscar tambien la solicitud de trab.
                              || y tram. por si no se huibiera quedado grabada
                              || en el cretam02 cuando se cre¢
     |BUCLE cretae02;
     aTipo = "R";             || busqueda solicitud borrador
     |BUCLE cretae02;
     aTipo = "F";             || busqueda solicitud confirmacion
     |BUCLE cretae02;

     aTipo = "C";             || busqueda solicitud calculos
     |BUCLE cretae02;

|FPRC;

|PROGRAMA;
     |CLS;
     |PINPA #0#0;

     |ABRE #0;
     |DDEFECTO #0;
     |LEE 000#0p;
     |SI FSalida = 0;
          |BORRA 020#0a;
     |SINO;
          |HAZ Defectos;
     |FINSI;

     |PINDA #0#0;

     |ENDATOS #1#0;
     |SI FSalida = 0;
          aAlfa = Usuario; |QBT aAlfa; aAlfa = ("e" + aAlfa) % 108;
          |NOME_DAT #cretam45#aAlfa#;
          |DELINDEX #cretam45;

          |ABRE #cretam45;

          |ABRE #labempre;

          |ABRE #cretam02;    || mto. liquidaciones CCC
          |ABRE #cretam22;    || respuestas fich. trab.-tram. CCC
          |ABRE #cretam27;    || respuestas fich. bases CCC
          |ABRE #cretam25;    || respuestas fich. bases NAF
          |ABRE #cretam31;    || RNT por CCC
          |ABRE #cretam34;    || DCL por CCC
          |ABRE #cretam36;    || RLC por CCC
          |ABRE #cretae02;    || Registro Ficheros Creados por  CCC

          |HAZ Seleccion;

          ||CONSULTA_CLAVE #1#cretam45;

          |BUCLE Impresion;

          |CIERRA #cretae02;
          |CIERRA #cretam36;
          |CIERRA #cretam34;
          |CIERRA #cretam31;
          |CIERRA #cretam27;
          |CIERRA #cretam25;
          |CIERRA #cretam22;
          |CIERRA #cretam02;

          |CIERRA #labempre;

          |CIERRA #cretam45;


          |SI nImpresion ! 0;
               |HAZ ElFINIMP;
          |SINO;
               aAlfa = "    No se han encontrado datos de Liquidaciones para ";
               aAlfa = aAlfa + "la selección realizada.";
               |MENAV aAlfa;
          |FINSI;

          |DELINDEX #cretam45;
     |FINSI;

     |GRABA 000#0n;
     |CIERRA #0;
|FPRO;
