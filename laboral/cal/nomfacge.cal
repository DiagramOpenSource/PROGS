|FICHEROS;
     nomfacge #0;
     labempre #1;         || empresas
     nomcalca #2;         || calculos cabs.
     labempax #3;         || auxiliar empresas
     labregis #4;         || registro de contratos
     :/facturar/def/asnomdat #5;         || datos nominas (MINUTACION)
     :/facturar/def/facemp   #6;         || empresas (MINUTACION)
|FIN;

|VARIABLES;
    alfa1 = "";
    alfa2 = "";
    alfa3 = "";
    alfa4 = "";
    nume1 = 0;
    nume2 = 0;
    nume3 = 0;
    nume4 = 0;
    fech1 = @;
    fech2 = @;
    fech3 = @;
    fech4 = @;
    FEstado = 0;
    fecsys = @;
    p_mes = "";

    i_finiq = 0;
    i_nomin = 0;
    i_pagas = 0;
    i_tc2   = 0;
    i_tc21  = 0;
    i_impor = 0;
    i_contr = 0;
    i_prorr = 0;
    j_nomin = 0;
    j_finiq = 0;
    j_pagas = 0;
    j_tc2   = 0;
    j_tc21  = 0;
    j_impor = 0;
    j_contr = 0;
    j_prorr = 0;
    mes_ant = 0;
    any_ant = 0;
    dir_minuta = "";
    aPaquete = "facturar";
|FIN;

*****************************************************************************
                       CALCULOS DESDE CAMPOS
*****************************************************************************

|PROCESO defecto; |TIPO 7; ||calcula el a¤o por defecto segun fecha sistema
    fecsys = ~;
    alfa1 = fecsys;
    alfa1 = alfa1 % -102;
    #0Campo = alfa1;
|PINTA #0Campo;
|FPRC;

|PROCESO mirames; |TIPO 7; ||CALCULA EL MES POR DEFECTO SEGUN FECHA SISTEMA
    fecsys = ~;
    alfa1 = fecsys;
    alfa1 = alfa1 % 402;
    #0Campo = alfa1;
|PINTA #0Campo;
|FPRC;

|PROCESO pintames; |TIPO 0;      || PINTA EL MES LITERAL
|SI #0Campo =  1;  p_mes = "ENERO     ";  |FINSI;
|SI #0Campo =  2;  p_mes = "FEBRERO   ";  |FINSI;
|SI #0Campo =  3;  p_mes = "MARZO     ";  |FINSI;
|SI #0Campo =  4;  p_mes = "ABRIL     ";  |FINSI;
|SI #0Campo =  5;  p_mes = "MAYO      ";  |FINSI;
|SI #0Campo =  6;  p_mes = "JUNIO     ";  |FINSI;
|SI #0Campo =  7;  p_mes = "JULIO     ";  |FINSI;
|SI #0Campo =  8;  p_mes = "AGOSTO    ";  |FINSI;
|SI #0Campo =  9;  p_mes = "SEPTIEMBRE";  |FINSI;
|SI #0Campo = 10;  p_mes = "OCTUBRE   ";  |FINSI;
|SI #0Campo = 11;  p_mes = "NOVIEMBRE ";  |FINSI;
|SI #0Campo = 12;  p_mes = "DICIEMBRE ";  |FINSI;
|PINTA 1247, p_mes;
|FPRC;

|PROCESO path; |TIPO 8;
|FPRC;

|PROCESO Paquete; |TIPO 0;
     |SI #0#5 = "M";     aPaquete = "facturar";     |FINSI;
     |SI #0#5 = "A";     aPaquete = "facturar";   |FINSI;
     |HAZ path;
|FPRC;

*****************************************************************************
                    LECTURA Y GRABACION
*****************************************************************************

|PROCESO minutacion;
     |DBASS dir_minuta;
     alfa1 = dir_minuta + aPaquete + "/fich/";
     alfa2 = #0#4;  alfa2 = "00" + alfa2;  alfa2 = alfa2 % -102;
     alfa1 = alfa1 + alfa2 + "/";

     |PATH_DAT #5 alfa1;
     |DDEFECTO #5;

     |ABRE #5;
     #5#0 = #1#0;    || empresa
     #5#1 = mes_ant; || mes anterior
     #5#2 = any_ant; || a¤o anterior
     |LEE 000#5=;
     i_nomin = #5#6;
     i_finiq = #5#20;
     i_pagas = #5#8;
     i_tc2   = #5#10;
     i_tc21  = #5#12;               || guarda variables mes anterior
     i_contr = #5#14;
     i_prorr = #5#16;
     i_impor = #5#18;

     #5#0 = #1#0;    || empresa
     #5#1 = #0#2;    || mes anterior
     #5#2 = #0#3;    || a¤o anterior
     |LEE 000#5=;
     |SI FSalida = 0;
          alfa1 = #5#0; alfa1 = ("00000" + alfa1) % -105;
          alfa2 = #5#1; alfa2 = ("00" + alfa2) % -102;
          alfa3 = #5#2; alfa3 = ("00" + alfa3) % -102;
          alfa4 = "    ATENCION. La empresa " + alfa1 + ", mes " + alfa2 + ", periodo " + alfa3;
          alfa4 = alfa4 + " ya ha sido enlazada.";
          |MENAV alfa4;
          alfa4 = "    NSobreescribir datos existentes?"
          |CONFI alfa4;
          |SI FSalida = 0;
               |BORRA 020#5a;
          |SINO;
               |ACABA;
          |FINSI;
     |FINSI;

     #5#3 = #1#2;              || nombre empresa
     #5#4 = "N";               || facturado S/N
     #5#5 = i_nomin;           || nominas anterior
     #5#6 = j_nomin;           || nominas actual
     #5#7 = i_pagas;           || pagas anterior
     #5#8 = j_pagas;           || pagas actual
     #5#9 = i_tc2;             || tc`s2 anterior
     #5#10 = j_tc2;            || tc`s2 actual
     #5#11 = i_tc21;           || tc`s2/1 anterior
     #5#12 = j_tc21;           || tc`s2/1 actual
     #5#13 = i_contr;          || contratos anterior
     #5#14 = j_contr;          || contratos actual
     #5#15 = i_prorr;          || prorrogas anterior
     #5#16 = j_prorr;          || prorrogas actual
     #5#17 = i_impor;          || importe TC1 anterior
     #5#18 = j_impor;          || importe TC1 actual
     #5#19 = i_finiq;          || finiquitos anterior
     #5#20 = j_finiq;          || finiquitos actual
     |SI #5#5 ! 0;|O #5#6  ! 0;|O #5#7  ! 0;|O #5#8  ! 0;|O #5#9  ! 0;
                  |O #5#10 ! 0;|O #5#11 ! 0;|O #5#12 ! 0;|O #5#13 ! 0;
                  |O #5#14 ! 0;|O #5#15 ! 0;|O #5#16 ! 0;|O #5#17 ! 0;
                  |O #5#18 ! 0; |O #5#19 ! 0; |O #5#20 ! 0;
          |GRABA 020#5n;
     |FINSI;
     |CIERRA #5;
|FPRC;

|PROCESO lee_labora1;         || cuenta contratos y prorrogas
     alfa1 = #4#21;
     alfa2 = alfa1 %  402;  nume1 = alfa2;        || saca el mes
     alfa2 = alfa1 % -102;  nume2 = alfa2;        || saca el a¤o
|SI nume1 = #0#2;|Y nume2 = #0#3;
    |SI #4#15 = 0;  j_contr = j_contr + 1;  |FINSI;
    |SI #4#15 ! 0;  j_prorr = j_prorr + 1;  |FINSI;
|FINSI;
|FPRC;

|DEFBUCLE lee_labora;         || lee el registro de contratos
 #4#2;
 ;
 #1#0,     1;
 #1#0, 99999;
 e;
 NULL,  lee_labora1;
|FIN;

|PROCESO lee_seguri1;
    j_tc2 = j_tc2 + 1;             || uno por registro
    j_tc21 = j_tc21 + #3#17;       || nro. de TC's 2/1

    |SI #3#20 < 0; |Y #3#2 = 8;
        || no sumo Tique 3076_1622
    |SINO;
        j_impor = j_impor + #3#20;     || importe de TC's 1
    |FINSI;
|FPRC;

|DEFBUCLE lee_seguri;         || lee el auxiliar de empresas
 #3#1;
 ;
 #1#0,   1, #0#2,  0;
 #1#0, 999, #0#2, 19;
 e;
 NULL, lee_seguri1;
|FIN;

|PROCESO lee_nomina1;         || chequea bruto recibo y bruto extras
|SI #2#3  = 2;
   j_finiq = j_finiq + 1;
|SINO;
   j_nomin = j_nomin + 1;  || Tique 135 _5237 =|SI #2#46 ! 0; j_nomin = j_nomin + 1;  |FINSI;
   |SI #2#47 ! 0; |Y #1#25 = 1;  j_pagas = j_pagas + 1;  |FINSI;
|FINSI;
|FPRC;

|DEFBUCLE lee_nomina;         || lee el mantenimiento de calculos cabs.
 #2#1;
 ;
 #1#0,     1, #0#2, 0;
 #1#0, 99999, #0#2, 9;
 e;
 NULL, lee_nomina1;
|FIN;

|PROCESO limpieza;            || inicializa variables
    j_finiq = 0;
    j_nomin = 0;
    j_pagas = 0;
    j_tc2   = 0;
    j_tc21  = 0;
    j_impor = 0;
    j_contr = 0;
    j_prorr = 0;
    i_nomin = 0;
    i_finiq = 0;
    i_pagas = 0;
    i_tc2   = 0;
    i_tc21  = 0;
    i_impor = 0;
    i_contr = 0;
    i_prorr = 0;
|FPRC;

|PROCESO lee_empresa1;
|ATRI I;
|PINTA 2041, #1#0;
|ATRI 0;
|HAZ limpieza;                || inicializa variables
|BUCLE lee_nomina;            || lee calculos cabs.
|BUCLE lee_seguri;            || lee auxiliar empresas
|BUCLE lee_labora;            || lee registro de contratos
|HAZ minutacion;              || graba ficha en MINUTACION
|FPRC;

|DEFBUCLE lee_empresa;
 #1#1;
 ;
 #0#0;
 #0#1;
 e;
 NULL, lee_empresa1;
|FIN;

|PROCESO enlaza; |TIPO 3;
    mes_ant = #0#2 - 1;
    any_ant = #0#3;
|SI mes_ant = 0;  mes_ant = 12;  any_ant = any_ant - 1;  |FINSI;
|BUCLE lee_empresa;
|FPRC;
