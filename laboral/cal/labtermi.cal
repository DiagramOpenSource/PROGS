|FICHEROS;
    labtermi #0;
    labempre #1;
    labtraba #2;
    labcentr #3;
    labregis #4;
    labclaus #5;
    labcontr #10;
    labpanta #11;
    nomcenes #12;   || centros especiales
|FIN;

|VARIABLES;
    teclado = 0;
    mod = 0;
    diasfin = 0;
    verrfec = "0000ATENCION Fecha erronea";
    vdia1 = "";
    vme1 = "";
    vany1 = "";
    vdia = 0;
    vme = 0;
    vany = 0;
    vtope = 0;
    vsw = 0;
    vfec = "";
    vblanco = "          ";
    vpuntos = "..........";
    informe = "labtermi";
    mensa1 = "    No existe empresa";
    mensa2 = "    No existe trabajador";
    mensa3 = "    No existe centro trabajo";
    mensa4 = "    AVISO !!! No existe contrato inicial !!!";
    mensa5 = "    ATENCION !! Imposible prorroga sobre contrato INDEFINIDO !!";
    mensa6 = "    AVISO !!! Periodo acumulado superior a 48 meses o datos erroneos ...";
    mensa7 = "    ATENCION !! Prorroga no efectiva sobre contrato tipo 57";
    mensa8 = "    No existe clausula";
    mensaA = "    ATENCION! Dato erroneo, centro de trabajo distinto al del trabajador ...";
    &contador = 0;
    nume1 = 0;
    nume2 = 0;
    fecinic = @;
    fecreal = @;
    fecnume = 0;
    fecalfa = "";
    fectope = "";
    diaalfa = "";
    mesalfa = "";
    anyalfa = "";
    dianume = 0;
    mesnume = 0;
    anynume = 0;
    topemes = 0;
    diashas = 0;
    diastop = 0;
    diaprimer = 0;
    mesprimer = 0;
    duracio = 0;
    meses = " meses";
    dias = " dias";
    nada = "";
    z = 0;
    &coma = ",";
    &dia = "";             || fecha presentacion desglosada
    &mes = "";
    &anyo = "";
    dia1 = "";            || variables trabajo fecha
    any1 = "";
    fec1 = "";
    fec2 = 0;
    &prorro = " ";
    &duracion = " ";
    &contra = " ";
    and = " y ";
    c = "";
    &dura = "";
    direcc = "";
    numero = "";
    piso = "";
    cod1 = "";
    cod2 = "";
    locali = "";
    &domemp = "";
    &domtra = "";
    &domcen = "";
    linepunt = "";
    punt = ".";
    barra = "  /";
    compactado = "";
    check = 0;
    menos = 0;
    mensaB = "    No existen parametros de control !!! Usando defectos ...";
    mensaC = "    A IMPRIMIR PLAN DE FORMACION (HOJA 1) ...";
    mensaD = "    A IMPRIMIR PLAN DE FORMACION (HOJA 2) ...";
    mensaE = "    A IMPRIMIR PLAN DE FORMACION (HOJA 3) ...";
    mensaF = "    No existe Plan de Formacion !!!";
    aponer = "";
    fiac = "";
    relleno = "                                                 ";
    sistema = @;
    hojaplan = 0;
    &edadalum = 0;
    diasini = 0;
    diasven = 0;
    diasdur = 0;
    razon = 0;
    nacitra = 0;
    actutra = 0;
    edadtra = 0;
    anyotra = 0;
    fechtra = "";
    &x1 = "";
    &x2 = "";
    &x3 = "";
    &x4 = "";
    alfa1 = "";
    &ctacot="";
|FIN;

|INCLUYE pastil_i;

|PROCESO leeempre;|TIPO 0;    || CALCULO PARA ASEGURAR EFECTIVIDAD
|ABRET #1;                    || DE LA RELACION CON EL FICHERO DE
        #1#0 = #0#0;          || EMPRESAS
|LEE 040#1=;
|SI FSalida = 0;
    |PINTA 0723,#1#2;
|SINO;
    |MENAV mensa1;
        #0#0 = 0;
    |ERROR;
|FINSI;
|CIERRAT #1;
|FPRC;

|PROCESO leetraba;|TIPO 0;    || CALCULO PARA ASEGURAR EFECTIVIDAD
|ABRET #2;                    || EN LA RELACION CON EL FICHERO DE
    #2#0 = #0#0;              || TRABAJADORES
    #2#1 = #0#1;
|LEE 040#2=;
|SI FSalida = 0;
    |PINTA 0823,#2#2;
|SINO;
    |MENAV mensa2;
        #0#1 = 0;
    |ERROR;
|FINSI;
|CIERRAT #2;
|FPRC;

|PROCESO leecentr;|TIPO 0;     || CALCULO PARA ASEGURAR EFECTIVIDAD
|ABRET #3;                     || EN LA RELACION CON EL FICHERO DE
    #3#0 = #0#0;               || CENTROS DE TRABAJO
    #3#1 = #0#2;
|LEE 040#3=;
|SI FSalida = 0;
    |PINTA 0928,#3#2;
|SINO;
    |MENAV mensa3;
        #0#2 = 0;
    |ERROR;
|FINSI;
|CIERRAT #3;
|SI #0#2 ! #2#77;
    |MENAV mensaA;
    |ERROR;
|FINSI;
|FPRC;

|PROCESO leecontr; |TIPO 7;
|ABRE #10;
|LEE 040#10p;
|SI FSalida ! 0;
    |MENAV mensaB;
|SINO;
    #0#20 = #10#26; |PINTA #0#20;  || nro de copias
    #0#21 = #10#27; |PINTA #0#21;  || localidad firma
|FINSI;
|CIERRA #10;
|FPRC;

|PROCESO leeregis; |TIPO 0;
|ABRET #4;
|SELECT #2#4;
    #4#1 = #0#0;
    #4#2 = #0#1;
|LEE 040#4=;
|SI FSalida = 0;
        c = #4#4;
                contador = #4#0;
                #0#3 = #4#12;        || oficina empleo
                #0#4 = #4#15 + 1;    || nro de prorroga
                #0#9 = #4#7;         || fecha inicio 1er contrato
                #0#10 = #4#9;        || meses duracion
                #0#11 = #4#10;       || dias duracion
                #0#12 = #4#5;        || definicion decreto
                #0#13 = #4#6;        || real decreto
                #0#14 = #4#4;        || codigo contrato
                #0#15 = #4#12;       || oficina de empleo
                #0#16 = #4#11;       || presentacion 1er contrato
                #0#17 = #4#14;       || nro de inem
                #0#18 = #4#18;       || acumulado meses
                #0#21 = #4#13;       || localidad 1er contrato
                #0#22 = #4#22;       || acumulado dias
            |PINDA #0#0;
                diaprimer = #4#22;   || valor inicial acumulador
                mesprimer = #4#18;
|SINO;
    |MENAV mensa4;   || no existe contrato inicial
    |PTEC 807;
|FINSI;
|CIERRAT #4;
|FPRC;

|PROCESO CentroEspecial;
     |DDEFECTO #12;
     |ABRE #12;
     #12#0  = #2#0;   || empresa
     #12#13 = #2#77;  || centro
     |SI #2#45 = "097";|O #2#45 = "085";
          #12#1 = "87";
     |SINO;
          |SI #2#45 = "065";
               #12#1  = "64";
          |SINO;
               #12#1  = #2#45 % 2;
          |FINSI;
     |FINSI;
     |LEE 000#12=;
     |SI FSalida ! 0;
          |SI #2#45 = "087";|O #2#45 = "097";|O #2#45 = "085";
                            |O #2#45 = "064";|O #2#45 = "065";
               |MENAV "    AVISO! Centro Especial de trabajo INEXISTENTE ...";
          |FINSI;
     |FINSI;
     |CIERRA #12;
|FPRC;

|PROCESO Centro;
     |DDEFECTO #3;
     |ABRE #3;
     #3#0 = #2#0;
     #3#1 = #2#77;
     |LEE 000#3=;
     |SI FSalida ! 0;|Y #2#45 ! "064";|Y #2#45 ! "065";
                     |Y #2#45 ! "087";|Y #2#45 ! "097";|Y #2#45 ! "085";
          |MENAV "    AVISO! Centro de trabajo INEXISTENTE ...";
     |FINSI;
     |CIERRA #3;
|FPRC;


|PROCESO CuentaCotizacion;
     |SI #2#45 = "087";|O #2#45 = "097";|O #2#45 = "085";
                       |O #2#45 = "064";|O #2#45 = "065";
          ctacot = #12#10;
     |SINO;
          ctacot = #3#10;
     |FINSI;
|FPRC;


|PROCESO tipo3; |TIPO 3;
|HAZ Centro;        || conocer la cuenta cotizacion, esto es el cambio
|HAZ CentroEspecial;
|HAZ CuentaCotizacion;

|HAZ domicili;
|HAZ decodifi;
|HAZ diasmese;
|IMPRE 0;
    p_imp = #0#7;
|HAZ pastilla;
|SI FSalida = 0; |Y p_cen = 0;
    |PARA z = 1; |SI z [ #0#20; |HACIENDO z = z + 1;
        |INFOR informe;
        |PRINT;
        |PIEINF;
    |FININF;
    |SIGUE;
|FINSI;
|FINIMP;
    alfa1 = #3#12;
|QBF alfa1;
|SI alfa1 ! "NO EXISTE"; |Y alfa1 ! "";
    |PINPA #2#11;
    |AVISO;
    |PAUSA;
|FINSI;
|FPRC;

|PROCESO fechacon; |TIPO 0;
        vfec = #0Campo; vsw = 0;
|SI vfec ! vblanco; |Y vfec ! vpuntos; |Y vfec ! Aindefinido;
  vdia1 = vfec % A102;
  vme1 = vfec % A402;
  vany1 = vfec % A704;
  vdia = vdia1; vme = vme1; vany = vany1;
  |SI vdia ! 0; |Y vme ! 0; |Y vany ! 0;
     |SI vme [ 12; |Y vme ] 1;
       |SI vme = 1; |O vme = 3; |O vme = 5; |O vme = 7; |O vme = 8; |O vme = 10; |O vme = 12;
           vtope = 31;
       |SINO;
           |SI vme = 2;
               mod = vany $ 4;
               |SI mod = 0;
                   vtope = 29;
               |SINO;
                   vtope = 28;
               |FINSI;
            |SINO;
               vtope = 30;
            |FINSI;
        |FINSI;
        |SI vdia [ vtope;
            vsw = 1;
        |FINSI;
     |FINSI;
  |FINSI;
|SINO;
  vsw = 1;
|FINSI;
|SI vsw = 0;
    |MENAV verrfec;
    |ERROR;
|FINSI;
|FPRC;

|PROCESO siexiste; |TIPO 0;
|SI #0#31 ! 0;
    |ABRET #5;
        #5#1 = #0#31;
    |LEE 040#5=;
    |SI FSalida = 0;
            #0#25 = #5#3;
            #0#26 = #5#4;
            #0#27 = #5#5;
            #0#28 = #5#6;
            #0#29 = #5#7;
        |PINTA #0#25;
        |PINTA #0#26;
        |PINTA #0#27;
        |PINTA #0#28;
        |PINTA #0#29;
    |SINO;
            #0#25 = "";
            #0#26 = "";
            #0#27 = "";
            #0#28 = "";
            #0#29 = "";
        |PINTA #0#25;
        |PINTA #0#26;
        |PINTA #0#27;
        |PINTA #0#28;
        |PINTA #0#29;
        |MENAV mensa8;
        |ERROR;
    |FINSI;
    |CIERRA #5;
|SINO;
        #0#25 = "";
        #0#26 = "";
        #0#27 = "";
        #0#28 = "";
        #0#29 = "";
    |PINTA #0#25;
    |PINTA #0#26;
    |PINTA #0#27;
    |PINTA #0#28;
    |PINTA #0#29;
|FINSI;
|FPRC;

|PROCESO decodifi;             || RUTINA DE DEGLOSE FECHA PRESENTACION
|SI #0#19 ! vpuntos; |Y #0#19 ! vblanco;
        fec1 = #0#19;
        dia1 = fec1 % 102;
        dia  = dia1;
        any1 = fec1 % 704;
        anyo = any1;
        fec1 = fec1 % 402;
        fec2 = fec1;
    |SI fec2 = 1;   mes = "Enero ....";   |FINSI;
    |SI fec2 = 2;   mes = "Febrero ..";   |FINSI;
    |SI fec2 = 3;   mes = "Marzo ....";   |FINSI;
    |SI fec2 = 4;   mes = "Abril ....";   |FINSI;
    |SI fec2 = 5;   mes = "Mayo .....";   |FINSI;
    |SI fec2 = 6;   mes = "Junio ....";   |FINSI;
    |SI fec2 = 7;   mes = "Julio ....";   |FINSI;
    |SI fec2 = 8;   mes = "Agosto ...";   |FINSI;
    |SI fec2 = 9;   mes = "Septiembre";   |FINSI;
    |SI fec2 = 10;  mes = "Octubre ..";   |FINSI;
    |SI fec2 = 11;  mes = "Noviembre ";   |FINSI;
    |SI fec2 = 12;  mes = "Diciembre ";   |FINSI;
|SINO;
    dia = "..";
    mes = vpuntos;
    anyo = "....";
|FINSI;
|SI #0#4 = 1;   prorro = "primera";   |FINSI;
|SI #0#4 = 2;   prorro = "segunda";   |FINSI;
|SI #0#4 = 3;   prorro = "tercera";   |FINSI;
|SI #0#4 = 4;   prorro = "cuarta";    |FINSI;
|SI #0#4 = 5;   prorro = "quinta";    |FINSI;
|SI #0#4 = 6;   prorro = "sexta";     |FINSI;
|SI #0#4 = 7;   prorro = "s‚ptima";   |FINSI;
|SI #0#4 = 8;   prorro = "octava";    |FINSI;
|SI #0#4 = 9;   prorro = "novena";    |FINSI;
|SI #0#4 = 10;  prorro = "d‚cima";    |FINSI;
|SI #0#4 = 11;  prorro = "und‚cima";  |FINSI;
|SI #0#4 = 12;  prorro = "duod‚cima"; |FINSI;
    contra = "";
|SI #0#10 ! 0;
        contra = #0#10;
        contra = contra + meses;
|SINO;
        contra = "...........";
|FINSI;
|SI #0#11 ! 0;
    |SI #0#10 ! 0;
            contra = contra + and;
    |FINSI;
        contra = contra + #0#11;
        contra = contra + dias;
|SINO;
        contra = contra + "............";
|FINSI;
|FPRC;

|PROCESO diasmese;              || RUTINA PARA MONTAR ALFANUMERICO
    dura = "";                  || DE DURACION
    |SI #0#18 ! 0;
        dura = #0#18;
        dura = dura + meses;
    |SINO;
        dura = "...........";
    |FINSI;
    |SI #0#22 ! 0;
        |SI #0#18 ! 0;
            dura = dura + and;
        |FINSI;
        dura = dura + #0#22;
        dura = dura + dias;
    |SINO;
        dura = dura + "............";
    |FINSI;
|FPRC;

|PROCESO domicili;                    || RUTINA DEL CALCULO actualiz
    direcc = #1#3;                    || PARA CONSTRUCCION DOMICILIOS
    numero = #1#4;
    piso   = #1#5;
    cod1   = #1#7;
    cod2   = #1#8;
    locali = #1#6;
|HAZ compac;
    domemp = compactado;              || domicilio empresa
    direcc = #2#3;
    numero = #2#4;
    piso   = #2#5;
    cod1   = #2#8;
    cod2   = #2#9;
    locali = #2#6;
|HAZ compac;
    domtra = compactado;              || domicilio trabajador
    direcc = #3#3;
    numero = #3#4;
    piso   = #3#5;
    cod1   = #3#7;
    cod2   = #3#8;
    locali = #3#6;
|HAZ compac;
    domcen = compactado;              || domicilio centro trabajo
    locali = #0#21;
    |QBF locali;
    locali = "                         " + locali;
    locali = locali % -125;
    #0#21 = locali;
|FPRC;

|PROCESO compac;                      || SUBRUTINA DE RUTINA domicili
    compactado = "";                  || DE COMPACTACION DE CAMPOS
    |QBF direcc;                      || USADA TAMBIEN EN CONT. 05 Y 25
    |QBF numero;
    |QBF piso;
    |QBF cod1; cod1 = "00" + cod1; cod1 = cod1 % -102;
    |QBF cod2; cod2 = "000" + cod2; cod2 = cod2 % -103;
    |QBF locali;
    compactado = direcc + coma;
    compactado = compactado + numero;
    compactado = compactado + "  ";
    compactado = compactado + piso;
    compactado = compactado + " (";
    compactado = compactado + cod1;
    compactado = compactado + cod2;
    compactado = compactado + ") ";
    compactado = compactado + locali;
    punt = " ";
    linepunt = punt * 70;
    compactado = compactado + linepunt;
    compactado = compactado % 170;
|FPRC;

|PROCESO nomodif2; |TIPO 7;     || CONVIERTE A NO MODIFICABLES CAMPOS DE
|SI #0#31 = 0;                  || LAS CLAUSULAS ADICIONALES
    |HAZ nopasa;
|FINSI;
|FPRC;

|PROCESO nopasa;                   || SUBRUTINA PARA HACER <no modificable>
    aponer = Anono;                || LLAMADA EN EL <nomodif>
    fiac = Control % A1001;
    Control = Control + fiac;
    Control = Control + relleno;
    Control = Control % A120;
    Control = Control + aponer;
|FPRC;

|PROCESO edadanyo;              || CALCULA EDAD EN BASE A UNA FECHA
|SI fechtra ! vblanco; |Y fechtra ! vpuntos;
    sistema = fechtra;
    nacitra = sistema;
    sistema = ~;
    actutra = sistema;
    edadtra = actutra - nacitra;
    anyotra = edadtra $ 365;
    edadtra = edadtra - anyotra;
    anyotra = edadtra / 365;
|SINO;
    anyotra = 0;
|FINSI;
|FPRC;
