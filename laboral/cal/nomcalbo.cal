|FICHEROS;
     labempre;
     labtraba;

     nombonif;
     nomvarca;
     nomcontr;
     nomconst;
     nomhicca;
|FIN;

|VARIABLES;
     &runnom = "";
     &bisiesto = 0;
     nMesesBon = 0;
     aFechaCambioBon = "";
     fFechaCambioBon = @;
     aFechaIniBon = "";
     nDiasTramo1 = 0;
     nDiasTramo2 = 0;
     nBoni500 = 0;
     nAnysBon = 0;
     numer = 0;
     &anynum = 0;
     &topemes = 0;
     &mesnum = 0;
     aAlfa = "";
     fFechaIniBon = @;
     fFechaFinBon = @;
     nNume = 0;
     nNume1 = 0;
     nNume2 = 0;
     nNume3 = 0;
     nNume4 = 0;
     swTramoBon = 0;
     nCampo = 0;
     fFechaBaja = @;
     fFecha = @;
     fFechaAlta = @;
     &dias_vac = 0;
     &nMes = 0;
     &nElMes = 0;
     alfa2 = "";
     alfa3 = "";
     &nAny = 0;
     &nElAny = 0;
     fFechaCal = @;
     nume1 = 0;
     nume2 = 0;
     nDiasBon = 0;
     &swSigue = 0;
     &dbaja_3 = 0;
     &dcoti_0 = 0;
     &nCuantos = 0;
     &nPorBon1 = 0;
     &nPorBon2 = 0;
     &bon_0 = 0;
     &bon_3 = 0;
     &base_5 = 0;
     &base_6 = 0;
     &base_F = 0;
     &zbase_F = 0;
     &base_AB = 0;
     &zbase_AB = 0;
     &base_A = 0;
     &zbase_A = 0;
     &zbon_0 = 0;
     &zbon_3 = 0;
     nDiasMesTrab = 0;
     nCuotaCCDia = 0;
     nDiasAltaCot = 0;
     nDiasITCot = 0;
     nBaseCCAlta = 0;
     nBaseCCIT = 0;
     nCuotaCCIT = 0;
     aFechaFinBon = "";
     aFecha = "";
     alfa1 = "";
     fFechaIniPer = @;
     fFechaFinPer = @;
     &swCambiaCot = 0;
     nCuotaCC = 0;
     &tp = 0;
     aFechaFinBonTra = "";
     fFechaFinBonTra = @;
     &mes_x = 0;
|FIN;

|PROCESO ulti_dia_bo;  || halla el ultimo dia del mes
     bisiesto = 0;
     numer = anynum $ 4;
     |SI numer = 0;
          bisiesto = 1;
     |FINSI;
     topemes = 31;
     |SI mesnum= 2;
          topemes = 28 + bisiesto;
     |FINSI;
     |SI mesnum= 4;|O mesnum= 6;|O mesnum= 9;|O mesnum=11;
          topemes = 30;
     |FINSI;
|FPRC;

|PROCESO CambioTramoBon;
     aAlfa = fFechaIniBon % -104;
     nNume = aAlfa;
     nNume = nNume + (#nombonif#14 / 12);
     aAlfa = nNume;
     aFechaCambioBon = (aFechaIniBon % 106) + aAlfa;
     fFechaCambioBon = aFechaCambioBon;
|FPRC;

|PROCESO TramoBon;
     |SI fFechaCambioBon > fFechaFinBon;
          || la fecha del cambio de tramo no es este mes
          swTramoBon = 1;
     |FINSI;

     |SI fFechaIniBon [ fFechaCambioBon;
     |Y fFechaCambioBon [ fFechaFinBon;
          || la fecha del cambio de tramo es este mes !!!
          swTramoBon = 12;
     |FINSI;

     |SI fFechaCambioBon [ fFechaIniBon;
          || estoy en el segundo tramo !!!
          swTramoBon = 2;
     |FINSI;
|FPRC;

|PROCESO calboni_taripla;
     |SI runnom = "nomcalfi"; |Y dias_vac = 0;
          |ACABA;
     |FINSI;
     |SI runnom = "nomcalat";
          nElMes = mes_x
     |FINSI;

     nCuotaCC = 0;
     |SI #labtraba#42 = "X"; |O #labtraba#42 = "T";
          |SI tp ] 0.75;
               nCuotaCC = 75;
          |FINSI;
          |SI tp ] 0.50; |Y tp < 0.75;
               nCuotaCC = 50;
          |FINSI;
          |SI tp < 0.50;
               nCuotaCC = 0;
          |FINSI;
     |SINO;
          nCuotaCC = 100;
     |FINSI;

     || dia_2 son los dias naturales en alta del mes
     || topemes los dias que tiene el mes, como siempre

     swTramoBon = 0;
     nMesesBon = 0;
     fFechaIniBon = #labtraba#40;
     aFechaIniBon = #labtraba#40;
     aFechaFinBon = #labtraba#41;
     |QBF aFechaFinBon;
     |SI aFechaFinBon ! ""; |Y aFechaFinBon ! "..........";
          aFechaFinBon = #labtraba#41;
          fFechaFinBon = aFechaFinBon;
     |SINO;
          |PARA nCampo = 14; |SI nCampo [ 19; |HACIENDO nCampo = nCampo + 1;
               nMesesBon = nMesesBon + #nombonif#nCampo#;
          |SIGUE;
          nAnysBon = nMesesBon / 12;
          aAlfa = aFechaIniBon % -104;
          nNume = aAlfa;
          nNume = nNume + nAnysBon;
          aAlfa = nNume;
          aFechaFinBon = (aFechaIniBon % 106) + aAlfa;
          fFechaFinBon = aFechaFinBon;
          nNume = fFechaFinBon;
          nNume = nNume - 1;
          fFechaFinBon = nNume;
     |FINSI;

     |SI #nombonif#15 ! 0;
          |HAZ CambioTramoBon;
     |FINSI;

     || comprobaciones

     |SI runnom = "nomcalfi";
          |ABRE #nomvarca;
          #nomvarca#0 = #labtraba#0;
          #nomvarca#1 = #labtraba#1;
          #nomvarca#2 = nElMes;
          #nomvarca#3 = 0;
          |LEE 000#nomvarca.=;
          |CIERRA #nomvarca;
     |FINSI;
     aAlfa = #nomvarca#16;
     |SI aAlfa ! "          "; |Y aAlfa ! "..........";
          fFechaBaja = aAlfa;
     |SINO;
          aAlfa = #labtraba#37;
          |SI aAlfa ! "          "; |Y aAlfa ! "..........";
               fFechaBaja = aAlfa;
          |FINSI;
     |FINSI;

     || finiquitos, el primer dia del mes es el dia despues al de la baja
     || y el de baja es el de alta mas los dias de Vacaciones menos 1
     |SI runnom = "nomcalfi";
          nNume = fFechaBaja;
          nNume = nNume + 1;
          fFecha = nNume;
          fFechaAlta = nNume;
          nNume = nNume + dias_vac - 1;
     |FINSI;
     |SI runnom = "nomcalat";
          alfa1 = "01";
          alfa2 = nElMes; alfa2 = "00" + alfa2; alfa2 = alfa2 % -102;
          alfa3 = nElAny;
          aFecha = alfa1 + "." + alfa2 + "." + alfa3;
          fFecha = aFecha;
     |FINSI;

     |SI fFechaIniBon [ fFecha;
          fFechaIniBon = fFecha;
     |SINO;
          nMes = alfa2;
          nAny = alfa3;
          |SI nMes = nElMes; |Y nAny = nElAny;
               fFechaIniBon = fFechaIniBon;
          |FINSI;
     |FINSI;

     || finiquitos, el primer dia del mes es el dia despues al de la baja
     || y el de baja es el de alta mas los dias de Vacaciones
     |SI runnom = "nomcalfi";
          nNume = fFechaBaja;
          nNume = nNume;
          nNume = nNume + dias_vac;
          fFecha = nNume;
     |FINSI;
     |SI runnom = "nomcalat";
          anynum = nElAny;
          mesnum = nElMes;
          |HAZ ulti_dia_bo;

          alfa1 = topemes; alfa1 = "00" + alfa1; alfa1 = alfa1 % -102;
          alfa2 = nElMes; alfa2 = "00" + alfa2; alfa2 = alfa2 % -102;
          alfa3 = nElAny;
          aFecha = alfa1 + "." + alfa2 + "." + alfa3;
          fFecha = aFecha;
     |FINSI;

     fFechaCal = fFecha;
     |SI fFechaFinBon ] fFecha;
          fFechaFinBon = fFecha;
     |SINO;
          nMes = alfa2;
          nAny = alfa3;
          |SI nMes = nElMes; |Y nAny = nElAny;
               fFechaFinBon = fFechaFinBon;
          |FINSI;
     |FINSI;

     |SI aAlfa ! "          "; |Y aAlfa ! "..........";
          |SI fFecha [ fFechaFinBon;
               fFechaFinBon = fFecha;
          |FINSI;
     |FINSI;

                                        || esto para que si he copiado la
     |SI fFechaAlta ] fFechaIniBon;     || ficha del trabajador, la bonifica-
          fFechaIniBon = fFechaAlta;    || cion se aplique a partir de la fecha
     |FINSI;                            || de alta del trabajador, y no de la
                                        || de inicio de la bonif.

     || esto para saber en que tramo estoy
     swTramoBon = 0;
     |SI #nombonif#15 ! 0;
          |HAZ TramoBon;
     |FINSI;

     nume1 = fFechaIniBon;
     nume2 = fFechaFinBon;
     nDiasBon = nume2 - nume1 + 1;
     |SI nDiasBon [ 0;
          |ACABA;
     |FINSI;

     swSigue = 0;
     |SI nDiasBon < topemes;  || si no estoy de alta el mes completo
          swSigue = 1;        || tengo que proporcionar
     |FINSI;
     |SI nDiasBon = 30; |Y topemes = 31;
          || salvo en meses de 31 dias si estoy de alta 30 mensuales
          || y diarios 2884.2631
          |SI #nomcontr#18 ! 2;
               swSigue = 0;
          |SINO;
               || aunque tenga el "2" en el parametro, pero si es un mensual
               || en en mes de 31 dias con 30 de bonificacion, no tiene que
               || proporcionar, lo dejo asi 15.09.2015 - 779.622
               |SI #labtraba#42 ! "D"; |Y #labtraba#42 ! "T";
                    swSigue = 0;
               |FINSI;
          |FINSI;
     |FINSI;

     |SI nElMes = 2; |Y dbaja_3 = topemes;
          |SI nDiasBon < dcoti_0;
               nNume = dcoti_0 - nDiasBon;
               |SI nNume = 1; |O nNume = 2;
                    || mes de febrero normal o bisiesto
                    nDiasBon = nDiasBon + nNume;
               |FINSI;
          |FINSI;
     |FINSI;

     |SI swTramoBon = 12; |O swTramoBon = 2;
          swSigue = 1;
     |FINSI;

     |SI swSigue = 1;
          |SI #labtraba#42 ! "T"; |Y #labtraba#42 ! "D";
               |SI #nomcontr#18 = 2;
               |Y dbaja_3 = topemes; || pq. si no estoy el mes completo de alta
                                     || el tipo "2" cuenta los dias reales
                    |SI nElMes = 1; |O nElMes = 3; |O nElMes = 5; |O nElMes = 7;
                    |O nElMes = 8; |O nElMes = 10; |O nElMes = 12;
                         nDiasBon = nDiasBon - 1;
                    |FINSI;
                    |SI nElMes = 2;
                         nDiasBon = nDiasBon + 2 - bisiesto;
                    |FINSI;
               |FINSI;
          |FINSI;

          nCuotaCC = (nCuotaCC / 30) ! 2;
          |SI nDiasBon > dcoti_0; |Y nCuantos > 1;
               nDiasBon = dcoti_0;
          |FINSI;

          |SI #labtraba#39 = 501; |O #labtraba#39 = 502;
               |SI nDiasBon ! dcoti_0;
                    nPorBon1 = nDiasBon;
               |FINSI;
          |FINSI;

          |SI swTramoBon = 1;
               bon_0 = ((base_6 / dias_vac) * nDiasBon) ! 2;
               bon_3 = bon_0;
          |FINSI;
          |SI swTramoBon = 12; |O swTramoBon = 2;
               |SI nDiasBon > dcoti_0;
                    nDiasBon = dcoti_0;
               |FINSI;
          |FINSI;
          |SI swTramoBon = 12;
               nNume1 = fFechaIniBon;
               nNume2 = fFechaCambioBon;
               nDiasTramo1 = nNume2 - nNume1;
               nDiasTramo2 = nDiasBon - nDiasTramo1;
               nDiasBon = nDiasTramo1;
               bon_0 = ((base_6 / dcoti_0) * nDiasBon) ! 2;
               bon_3 = bon_0;
               nPorBon1 = nDiasTramo1;
          |FINSI;
          |SI swTramoBon = 2;
               nDiasTramo1 = 0;
               nDiasTramo2 = nDiasBon;
               nCuotaCC = 0;

               bon_0 = 0;
               nPorBon1 = 0;
               base_F = 0;
               base_AB = 0;
          |FINSI;

          nCuotaCC = nCuotaCC * nDiasBon;
     |SINO;
          nCuotaCC = nCuotaCC;

          nNume = nDiasMesTrab + nDiasBon;
          |SI nDiasMesTrab ! 0; |Y nNume = topemes;
               nCuotaCC = nCuotaCC - (nDiasMesTrab * ((nCuotaCC / 30) ! 2));
          |FINSI;
     |FINSI;

     |SI nCuotaCC ! 0; |O swTramoBon = 2;
          |SI runnom = "nomcalat";
               || esto es la tarifa plana por dia

               |SI #labtraba#42 = "X"; |O #labtraba#42 = "T";
                    |SI tp ] 0.75; nNume1 = 75 / 30; |FINSI;
                    |SI tp ] 0.50; |Y tp < 0.75;
                         nNume1 = (50 / 30) ! 2;
                    |FINSI;
                    |SI tp < 0.50; nNume1 = 0; |FINSI;
               |SINO;
                    nNume1 = (100 / 30) ! 2;
               |FINSI;

               || esto es lo que he pagado por dia
               nNume = ((#nomhicca#43 % 23.60) ! 2) / #nomhicca#8;
               |SI nNume < nNume1;
                    || aun tengo que pagar SS
                    nNume = nNume1 - nNume;
                    nCuotaCC = nNume * #nomhicca#8;
               |SINO;
                    nCuotaCC = 0;
               |FINSI;
          |FINSI;

          nBoni500 = ((bon_0 % #nomconst#6) ! 2) - nCuotaCC;

          |SI runnom = "nomcalat";
               |SI swTramoBon = 12; |O #labtraba#39 = 501;
                    || bonificada toda la parte del primer tramo
                    nBoni500 = ((bon_0 % #nomconst#6) ! 2);
               |FINSI;
               |SI swTramoBon = 2;
                    nBoni500 = 0;
               |FINSI;
          |FINSI;

          || N U E V O

          |SI swTramoBon ! 12; |Y swTramoBon ! 2;
               ||HAZ Dias;

               |SI base_5 ! 0;
                    nCuotaCCDia = ((nCuotaCC / 30) ! 2);
                    |SI nDiasAltaCot > 0; |Y nDiasITCot > 0;
                         nNume1 = ((nBaseCCAlta % #nomconst#6) ! 2) - (nDiasAltaCot * nCuotaCCDia);
                         nCuotaCCIT = nCuotaCC - (nDiasAltaCot * nCuotaCCDia);
                         nNume2 = ((nBaseCCIT % #nomconst#6) ! 2) - nCuotaCCIT;

                         nBoni500 = nNume1 + nNume2;
                    |SINO;
                         nBoni500 = ((bon_0 % #nomconst#6) ! 2) - nCuotaCC;
                    |FINSI;
               |FINSI;
          |FINSI;

          base_A = nBoni500;
          base_AB = base_A;
          base_F = bon_0;
     |FINSI;

     |SI swTramoBon = 12; |O swTramoBon = 2;
          |SI swTramoBon = 12;
               base_F = nCuotaCC;

               || ahora el tramo 2:

               zbon_0 = (base_6 - bon_0);
               zbon_3 = zbon_0;
               nPorBon2 = nDiasTramo2;
               nNume = ((((zbon_0 % #nomconst#6) ! 2)) % 50) ! 2;
               zbase_F = nNume;
               zbase_A = zbase_F;
               zbase_AB = zbase_A;
          |FINSI;
          |SI swTramoBon = 2;
               |SI runnom = "nomcalfi";
                    zbon_0 = ((base_6 / dias_vac) * nDiasBon) ! 2;
                    zbon_3 = zbon_0;
               |FINSI;
               |SI runnom = "nomcalat";
                    zbon_0 = ((base_6 / dcoti_0) * nDiasBon) ! 2;
                    zbon_3 = zbon_0;
               |FINSI;

               nNume = ((((zbon_0 % #nomconst#6) ! 2)) % 50) ! 2;
               zbase_F = nNume;
               nPorBon2 = nDiasTramo2;

               zbase_A = zbase_F;
               zbase_AB = zbase_A;
          |FINSI;
     |FINSI;
|FPRC;

/****************************************************************************/
/*** BONIFICACIONES DEL RDL 01/2015 - EXENCION DE 500 EUROS DE BASE DE CC ***/
/****************************************************************************/

|PROCESO BonifTP;
     |SI #labtraba#42 = "X"; |O #labtraba#42 = "T";
          |SI tp ] 0.50;
               nNume = tp ! 3;
               nCuotaCC = nCuotaCC * nNume;
          |SINO;
               nCuotaCC = 0;
          |FINSI;
     |SINO;
          nCuotaCC = nCuotaCC;
     |FINSI;
|FPRC;

|PROCESO calboni_500;
     |SI runnom = "nomcalfi"; |Y dias_vac = 0;
          |ACABA;
     |FINSI;

     swSigue = 0;
     |SI nElAny ] 2015;
          |SI nElAny = 2015;
               |SI nElMes ] 5;
                    swSigue = 1;
               |FINSI;
          |SINO;
               swSigue = 1;
          |FINSI;
     |FINSI;
     |SI swSigue = 0;
          |ACABA;
     |FINSI;

     nCuotaCC = #nombonif#39;
     |HAZ BonifTP;

     || dia_2 son los dias naturales en alta del mes
     || topemes los dias que tiene el mes, como siempre
     fFechaIniBon = #labtraba#40;
     aFechaIniBon = #labtraba#40;
     aFechaFinBon = #labtraba#41;
     |QBF aFechaFinBon;
     |SI aFechaFinBon ! ""; |Y aFechaFinBon ! "..........";
          aFechaFinBon = #labtraba#41;
     |SINO;
          aFechaFinBon = "31.12.2999";
     |FINSI;
     fFechaFinBon = aFechaFinBon;

     |SI #nombonif#15 ! 0;
          |HAZ CambioTramoBon;
     |FINSI;

     || comprobaciones
     alfa1 = "01";
     alfa2 = nElMes; alfa2 = "00" + alfa2; alfa2 = alfa2 % -102;
     alfa3 = nElAny;
     aFecha = alfa1 + "." + alfa2 + "." + alfa3;
     fFecha = aFecha;
     |SI fFechaIniBon [ fFecha;
          fFechaIniBon = fFecha;
     |SINO;
          nMes = alfa2;
          nAny = alfa3;
          |SI nMes = nElMes; |Y nAny = nElAny;
               fFechaIniBon = fFechaIniBon;
          |FINSI;
     |FINSI;

     anynum = nAny;
     mesnum = nMes;
     |HAZ ulti_dia_bo;

     alfa1 = topemes; alfa1 = "00" + alfa1; alfa1 = alfa1 % -102;
     alfa2 = nElMes; alfa2 = "00" + alfa2; alfa2 = alfa2 % -102;
     alfa3 = nElAny;
     aFecha = alfa1 + "." + alfa2 + "." + alfa3;
     fFecha = aFecha;
     fFechaCal = fFecha;
     |SI fFechaFinBon ] fFecha;
          fFechaFinBon = fFecha;
     |SINO;
          nMes = alfa2;
          nAny = alfa3;
          |SI nMes = nElMes; |Y nAny = nElAny;
               fFechaFinBon = fFechaFinBon;
          |FINSI;
     |FINSI;
     |SI runnom = "nomcalfi";
          |ABRE #nomvarca;
          #nomvarca#0 = #labtraba#0;
          #nomvarca#1 = #labtraba#1;
          #nomvarca#2 = nElMes;
          #nomvarca#3 = 0;
          |LEE 000#nomvarca.=;
          |CIERRA #nomvarca;
     |FINSI;
     aAlfa = #nomvarca#16;
     |SI aAlfa ! "          "; |Y aAlfa ! "..........";
          fFecha = aAlfa;
     |SINO;
          aAlfa = #labtraba#37;
          |SI aAlfa ! "          "; |Y aAlfa ! "..........";
               fFecha = aAlfa;
          |FINSI;
     |FINSI;

     || finiquitos, el dias de alta es el dia despues al de la baja
     || y el de baja es el de alta mas los dias de Vacaciones menos 1

     |SI dias_vac ! 0;
          nNume = fFecha;
          nNume = nNume + 1;

          fFechaAlta = nNume;
          nNume = nNume + dias_vac - 1;
          fFecha = nNume;
          aFechaFinBonTra = #labtraba#41;
          |QBF aFechaFinBonTra;
          |SI aFechaFinBonTra ! ""; |Y aFechaFinBonTra ! "..........";
               fFechaFinBonTra = aFechaFinBonTra;
               |SI fFecha [ fFechaFinBonTra;
                    fFechaFinBon = fFecha;
               |SINO;
                    fFechaFinBon = fFechaFinBonTra;
               |FINSI;
          |SINO;
               fFechaFinBon = fFecha;
          |FINSI;
     |FINSI;


                                        || esto para que si he copiado la
     |SI fFechaAlta ] fFechaIniBon;     || ficha del trabajador, la bonifica-
          fFechaIniBon = fFechaAlta;    || cion se aplique a partir de la fecha
     |FINSI;                            || de alta del trabajador, y no de la
                                             || de inicio de la bonif.

     || esto para saber en que tramo estoy
     swTramoBon = 0;
     |SI #nombonif#15 ! 0;
          |HAZ TramoBon;
          |SI swTramoBon = 2;
               nCuotaCC = #nombonif#40;
               |HAZ BonifTP;
          |FINSI;
     |FINSI;

     nume1 = fFechaIniBon;
     nume2 = fFechaFinBon;
     nDiasBon = nume2 - nume1 + 1;
     |SI nDiasBon [ 0;
          |ACABA;
     |FINSI;

     swSigue = 0;
     |SI nDiasBon < topemes;  || si no estoy de alta el mes completo
          swSigue = 1;        || tengo que proporcionar
     |FINSI;
     |SI nDiasBon = 30; |Y topemes = 31;
          || salvo en meses de 31 dias si estoy de alta 30 mensuales
          || y diarios 2884.2631
          |SI #nomcontr#18 ! 2;
               swSigue = 0;
          |FINSI;
     |FINSI;

     |SI fFechaIniBon [ fFechaIniPer; |Y fFechaFinBon ] fFechaFinPer;
          || sin duda, me bonifico todo el mes, pase lo que pase
          |SI nDiasBon < dcoti_0; |Y nElMes = 2; |Y swSigue = 1;
               || esto lo hago para que jamas, si tengo todos los dias bonificados
               || me bonifique menos dias de lo que toca
               || a ver, lo dejo de moemento solo para febrero, por si acaso
               || si hace falta otro mes, liberar

               nDiasBon = dcoti_0;
          |FINSI;
     |FINSI;

     |SI nDiasMesTrab > 0; |Y nDiasMesTrab = topemes;
          swSigue = 0;    || no he de proporcionar de momento
          || aunque no este de alta el mes completo
          || ya que he trabajado el mes completo entre las fichas que tenga

          || lo de nDiasMesTrab = topemes; no tiene sentido, de esa manera
          || no va a entrar nunca !!!
     |FINSI;

     |SI swSigue = 1;
          |SI #labtraba#42 ! "T"; |Y #labtraba#42 ! "D";
               |SI #nomcontr#18 = 2;
               |Y dbaja_3 = topemes; || pq. si no estoy el mes completo de alta
                                     || el tipo "2" cuenta los dias reales
                    |SI nElMes = 1; |O nElMes = 3; |O nElMes = 5; |O nElMes = 7;
                    |O nElMes = 8; |O nElMes = 10; |O nElMes = 12;
                         nDiasBon = nDiasBon - 1;
                    |FINSI;
                    |SI nElMes = 2;
                         nDiasBon = nDiasBon + 2 - bisiesto;
                    |FINSI;
               |FINSI;
          |FINSI;

          nCuotaCC = (nCuotaCC / 30) ! 2;
          |SI nDiasBon > dcoti_0; |Y nCuantos > 1; |Y swCambiaCot = 0;
               nDiasBon = dcoti_0;
          |FINSI;
          nCuotaCC = nCuotaCC * nDiasBon;
     |SINO;
          nCuotaCC = nCuotaCC;

          nNume = nDiasMesTrab + nDiasBon;
          |SI nDiasMesTrab ! 0; |Y nNume = topemes;
               nCuotaCC = nCuotaCC - (nDiasMesTrab * ((nCuotaCC / 30) ! 2));
          |FINSI;
     |FINSI;

     |SI nCuotaCC ! 0;
          nNume = (nCuotaCC % #nomconst#6) ! 2;

          bon_0 = nCuotaCC;
          base_A = nNume;
          base_AB = base_A;
          base_F = bon_0;
     |FINSI;
|FPRC;
