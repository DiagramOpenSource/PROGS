|FICHEROS;
     labempre #0;         || empresas
     labcentr #1;         || centros de trabajo

     :/basica/def/agifigen #11;        || clientes AGICLI
     :/basica/def/agihacie #12;        || datos hacienda AGICLI

     silcon01 #20;        || control AGIRED
     silcon06 #30;        || gestion empresas AGIRED
     nomcnaec;

     &haciend@ #706;
     &bancosc@ #709;
     nomconca;
     nombanco;
     nomcnahi;
     nomcontr;
     labclean;
     cretam00;
|FIN;

|VARIABLES;
     nume1 = 0;
     nume2 = 0;
     lugar = 0;
     nifnum = 0;
     resto = 0;
     mod = 0;
     corta = 0;
     nume3 = 0;
     jg = 0;
     sitio = 0;
     con = 0;
     swmod = 0;
     baja = 0;
     men1 = "0000ATENCION Fecha erronea";
     dia1 = "";
     mes1 = "";
     any1 = "";
     dia = 0;
     mes = 0;
     any = 0;
     tope = 0;
     sw = 0;
     fec = "";
     blanco = "          ";
     puntos = "..........";
     letra = "";
     alfa1 = "";
     alfa2 = "";
     alfa3 = "";
     alfa4 = "";
     final = "";
     x = "";
     men6 = "0000S Confirma el alta en Clientes General : ";
     men7 = "      D.N.I. del Cliente es distinto al D.N.I. del Cliente General";
     men8 = "      No existe Cliente General";
     aste = "";
     agi = "";
     agi1 = "";
     longi = "";
     num   = 0;
     agi2 = "";
     dni1 = "";
     dni2 = "";
     trabacod1 = "";
     trabacod2 = "";
     &em_borr = 0;
     &tr_borr = 0;
     FEstado = 0;
     runnom = "";
     ant_runnom = "";
     aAlfa1 = "";
     aAlfa2 = "";
     aLong  = "";
     nLong  = 0;
     nPos   = 0;

     &orden = "";
     &pasa = "";

     &enCodBanco = 0;
     &enCodProv  = 0;
     &enCodPobl  = 0;
     &eaVarDni   = "";
     &enCalcCif  = 0;
     &enEmpresa  = 0;
     nCampo = 0;
     nModo = 0;
     &enCodBanLab = 0;
     &eaDesBanLab = "";
     &eaCueBanLab = "";
     aAlfa = "";
     swIntegral = 0;
     aBase = "";
     nCanal = 0;
     fFecha = @;
     &nElMes = 0;
     &nElAny = 0;
     &nOpcionPLB = 0;
     &aCodPos = "";
     aCNAE = "";
     aDesCNAE = "";
     &eaVengoDe = "";
     swBotonSiNo = 0;
     nOpcion = 0;
     enBoton = 0;
     &eaEmail = "";
     &nArchi = 0;
     &eaProcedencia = "labempre";
     aPassword = "";
     &enVersionAgitool = 0;
     swSigue = 0;
     &eaMensaCad = "";
     &enErrorCad = 0;
|FIN;

|PROCESO letranif; |TIPO 6; || CALCULA LETRA PARA EL NIF CUANDO SE PULSA -F1-
     |SI FSalida = 10;
         eaVarDni  = #0Campo;
         enCalcCif = 1;
         |HAZ CalculoDNI;
         #0Campo = eaVarDni;
         |PINTA #0Campo;
         |ERROR;
     |FINSI;

     eaVarDni  = #0Campo;
     enCalcCif = 0;
     |HAZ CalculoDNI;
     aLong = #0Campo % 0;
     nLong = aLong;
     nPos  = nLong + 100;
     eaVarDni = (eaVarDni + (" " * nLong)) % nPos;
     |SI #0Campo ! eaVarDni;
         |CONFI "0000SEl Nif puede ser Erroneo. Desea Calcularlo.";
         |SI FSalida = 0;
             #0Campo = eaVarDni;  |PINTA #0Campo;
             |ERROR;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO fecempre; |TIPO 0;
        fec = #0Campo; sw = 0;
|SI fec ! blanco; |Y fec ! puntos;
  dia1 = fec % A102;
  mes1 = fec % A402;
  any1 = fec % A704;
  dia = dia1; mes = mes1; any = any1;
  |SI dia ! 0; |Y mes ! 0; |Y any ! 0;
     |SI mes [ 12; |Y mes ] 1;
       |SI mes = 1; |O mes = 3; |O mes = 5; |O mes = 7; |O mes = 8; |O mes = 10; |O mes = 12;
           tope = 31;
       |SINO ;           |SI mes = 2;
               mod = any $ 4;
               |SI mod = 0;
                   tope = 29;
               |SINO;
                   tope = 28;
               |FINSI;
            |SINO;
               tope = 30;
            |FINSI;
        |FINSI;
        |SI dia [ tope;
            sw = 1;
        |FINSI;
     |FINSI;
  |FINSI;
|SINO;
  sw = 1;
|FINSI;
|SI sw = 0;
    |MENAV men1;
    |ERROR;
|FINSI;
|FPRC;

|PROCESO control1; |TIPO 0;
|SI FSalida = 9;
        alfa1 = #0Campo;
    |QBF alfa1;
        alfa2 = alfa1 % 102; alfa3 = alfa1 % -107;
        alfa4 = alfa3 % 101;
    |SI alfa4 = "0";
            alfa3 = alfa3 % 206;
            alfa1 = alfa2 + alfa3;
    |FINSI;
        nume1 = alfa1;
        nume2 = nume1 / 97;
    |HAZ trunca1;
    |SI nume1 ] 0; |Y nume1 [ 99;
            alfa1 = #0Campo;
        |QBF alfa1;
            alfa2 = nume1;
            alfa2 = "00" + alfa2;
            alfa2 = alfa2 % -102;
            alfa1 = alfa1 + alfa2;
            #0Campo = alfa1;
        |PINTA #0Campo;
    |FINSI;
|FINSI;
|FPRC;

|PROCESO trunca1;
    corta = 0;
    alfa1 = nume2;  alfa2 = alfa1 % 0;  nume3 = alfa2;
|PARA jg = nume3; |SI jg ] 1;|Y corta = 0; |HACIENDO jg = jg - 1;
        sitio = (jg * 100) + 1;
        alfa2 = alfa1 % sitio;
    |SI alfa2 = ".";  corta = jg;  |FINSI;
|SIGUE;
|SI corta ! 0;
        sitio = 100 + corta - 1;
        alfa1 = alfa1 % sitio;
|FINSI;
    nume3 = alfa1;
    nume1 = nume1 - (nume3 * 97);
|FPRC;

/*
|PROCESO distinta; |TIPO 0;
|SI #0Campo ! "E";|Y #0Campo ! " ";
    |SI Campo = 37;
        |SI #0Campo = #0#38;
            |MENAV "    ATENCION!!! Claves iguales ...";
            |ERROR;
        |FINSI;
    |SINO;
        |SI #0Campo = #0#37;
            |MENAV "    ATENCION!!! Claves iguales ...";
            |ERROR;
        |FINSI;
    |FINSI;
|SINO;
    |SI #0Campo = "E";
        |MENAV "    ATENCION!!! Clave gestionada con conceptos 41? ...";
        |ERROR;
    |FINSI;
|FINSI;
|FPRC;
*/

|PROCESO tipo3; |TIPO 3;
    con = (FEntrada / 100) ! 0;
|HAZ grabacen;
|HAZ modicli;
|HAZ grabared;
|FPRC;

|PROCESO grabacen;
|SI con = 1;             || alta del centro numero 1
    |ABRE #1;
        #1#0 = #0#0;
        #1#1 = 1;
    |LEE 000#1=;
    |SI FSalida ! 0;
        |DDEFECTO #1;
            #1#0  =  #0#0;               || codigo empresa
            #1#1  =  1;                  || codigo centro
            #1#2  =  "PROPIA EMPRESA";   || descripcion centro
            #1#3  =  #0#3;               || domicilio
            #1#4  =  #0#4;               || numero
            #1#5  =  #0#5;               || piso
            #1#6  =  #0#6;               || localidad
            #1#7  =  #0#7;               || codigo postal 1
            #1#8  =  #0#8;               || codigo postal 2
            #1#9  =  #0#9;               || provincia
            #1#10 = #0#13;               || nro. S.S. centro/empresa
            #1#11 = #0#14;               || plantilla centro/empresa
            #1#13 = #0#19;               || clave entidad
            #1#14 = #0#20;               || entidad
        |GRABA 020#1n;
    |FINSI;
    |CIERRA #1;
|FINSI;
|FPRC;

|PROCESO grabared;
|SI con = 1;                       || alta en AGIRED
    |ABRE #20;
    |LEE 000#20p;
    |SI FSalida = 0;
        |CONFI "    NDesea Alta en AGIRED (S/N): ";
        |SI FSalida = 0;
            |DDEFECTO #30;
            |ABRE #30;
                #30#0 = #0#0;
            |LEE 000#30=;
            |SI FSalida ! 0;
                |PUSHV 05012380;
                |PINPA #0#30;
                |PINDA #0#30;
                |ENDATOS #1#30;
                |SI FSalida = 0;
                    |GRABA 020#30n;
                |FINSI;
                |POPV;
            |FINSI;
            |CIERRA #30;
        |FINSI;
    |FINSI;
    |CIERRA #20;
|FINSI;
|FPRC;
____________________________________/ GESTION DEL AGICLI
|PROCESO modicli;
|HAZ tipo8;
    con = (FEntrada / 100) ! 0;
|SI con = 2;|Y aste ! "*";         || modo modificacion (desde el tipo3)
    |ABRE #11;
        #11#0 = #0#0;
    |LEE 000#11=;
    |SI FSalida = 0;
            dni1 = #0#10;  |QBF dni1;
            dni2 = #11#13; |QBF dni2;
        |SI dni1 ! dni2;
            |MENAV men7;
        |FINSI;
    |SINO;
        |MENAV men8;
    |FINSI;
    |CIERRA #11;
|FINSI;
|FPRC;

|PROCESO bajacli; |TIPO 1;
|HAZ tipo8;
    con = (FEntrada / 100) ! 0;
|SI con = 3;|Y aste ! "*";         || modo baja
    |ABRE #11;
        #11#0 = #0#0;
    |LEE 101#11=;
    |SI FSalida = 0;
            #11#18 = "N";
        |GRABA 020#11a;
    |FINSI;
    |LIBERA #11;
    |CIERRA #11;
|FINSI;
|FPRC;

|PROCESO altacli; |TIPO 0;
|HAZ tipo8;
    con = (FEntrada / 100) ! 0;
|MODULO runnom;
|SI con = 1;|Y aste ! "*";|Y runnom ! ant_runnom; || modo alta
        ant_runnom = runnom;
    |ABRE #11;
        #11#0 = #0#0;
    |LEE 101#11=;
        FEstado = FSalida;
    |SI FEstado = 0;
        |HAZ carga_campos;
    |SINO;
        |CONFI men6;
        |SI FSalida = 0;
            |PUSHV 0501 2380;
            |PINPA #0#11;
            |DDEFECTO #11;
                #11#0 = #0#0;
            |PINDA #0#11;
            |ENDATOS #1#11;
            |SI FSalida = 0;
                |POPV;
                |HAZ postal;
                |HAZ carga_campos;
            |SINO;
                |ERROR;
                |POPV;
            |FINSI;
        |FINSI;
    |FINSI;
    |LIBERA #11;
    |CIERRA #11;
|FINSI;
|FPRC;

|PROCESO carga_campos;
    #11#18 = "S";
|SI FEstado ! 0;    |GRABA 020#11n;     |FINSI;
|SI FEstado = 0;    |GRABA 020#11a;     |FINSI;

    #0#2  = #11#1;       || nombre
    #0#3  = #11#42;      || domicilio
    #0#4  = #11#43;      || numero
    #0#5  = #11#45;      || piso
    #0#6  = #11#49;      || localidad
    #0#7  = #11#47;      || cod.postal 1
    #0#8  = #11#48;      || cod.postal 2
    #0#9  = #11#50;      || provincia
    #0#10 = #11#13;      || cif
    #0#21 = (#11#63 * 1000) + #11#64;   || cod.admon.hacienda
|HAZ lee_hacienda;
    #0#111= #11#14;      || telefono 1
    #0#112= #11#15;      || telefono 2 (fax)
|MODULO runnom;
|SI runnom = "labempre";
    |PINDA #0#0;
|FINSI;
|FPRC;

|PROCESO lee_hacienda;
     enCodProv = #11#63;
     enCodPobl = #11#64;
     |HAZ LeeHacienda;
     #0#22 = #706#3;       || administracion
     #0#24 = #706#2;       || delegacion

     |PINTA #0#22;
     |PINTA #0#24;
|FPRC;

|PROCESO postal;
    #11#16 = "";
    trabacod1 = #11#9;
    trabacod1 = "00" + trabacod1;
    trabacod1 = trabacod1 % -102;
    trabacod2 = #11#10;
    trabacod2 = "000" + trabacod2;
    trabacod2 = trabacod2 % -103;
    #11#16 = trabacod1 + trabacod2;
|FPRC;

|PROCESO tipo8; |TIPO 8;
     |QUE_SISTEMA alfa1;
     |CAMPO_ANCHO #nomconca#1, 1, 20;
|FPRC;

____________________________________/ BORRADO DE RELACIONES
|PROCESO tipo2; |TIPO 2;
    baja = FEntrada / 100;
    baja = baja ! 0;
|SI baja = 3;
    |HAZ aviso_em;
        em_borr = #0#0;
        tr_borr = 0;
    |SUB_EJECUTA "nomborr1.run";
    |SUB_EJECUTA "nomborr2.run";
    |HAZ e_aviso9;
|FINSI;
|FPRC;

|PROCESO aviso_em;
|PUSHV 05012380;
|HAZ e_color_f;
|CUADRO 09 30 11 71;
||            1234567890123456789012345678901234567890
|HAZ e_color_c;
|PINTA 1031, "         Borrando Relaciones ...        ";
|ATRI 0;
|FPRC;

|PROCESO e_color_c;        || activa el color caracter en avisos
|COLOR 14, 0;
|ATRI 0;
|FPRC;

|PROCESO e_color_f;        || activa el color graficos en avisos
|COLOR 4, 0;
|ATRI 0;
|FPRC;

|PROCESO e_aviso9;
|POPV;
|FPRC;

|PROCESO def45; |TIPO 7;
|SI #0#45 ! "S";|Y #0#45 ! "N";
        #0#45 = "N";
    |PINTA #0#45;
|FINSI;
|FPRC;

|PROCESO def46; |TIPO 7;
|SI #0#46 ! "S";|Y #0#46 ! "N";
        #0#46 = "N";
    |PINTA #0#46;
|FINSI;
|FPRC;

|PROCESO def47; |TIPO 7;
|SI #0#47 ! "1";|Y #0#47 ! "2";
        #0#47 = "1";
    |PINTA #0#47;
|FINSI;
|FPRC;

|PROCESO def48; |TIPO 7;
|SI #0#48 ! "1";|Y #0#48 ! "2";
        #0#48 = "2";
    |PINTA #0#48;
|FINSI;
|FPRC;

|PROCESO def49; |TIPO 7;
|SI #0#49 ! "S";|Y #0#49 ! "N";
        #0#49 = "S";
    |PINTA #0#49;
|FINSI;
|FPRC;

|PROCESO abreviado; |TIPO 0;
    alfa1 = Contenido;
|SI #0#45 = "S";|Y alfa1 ! #0Campo;
    |MENAV "    Revise las cuentas de cotizacion de los centros. Deberan ser unicas.";
|FINSI;
/*
|SI #0#49 = "S";|Y #0#45 = "S";|Y #0#46 = "N";|Y alfa1 ! #0Campo;
    |MENAV "    Si TC2 y TC2/1 se emite por centros y TC1 no: NO SE PODRA ABREVIAR EL TC2 ...";
|FINSI;
*/
|FPRC;

|PROCESO tc1_centros; |TIPO 7;
    #0#46 = #0#45;
    |PINTA #0#46;
    |C_M #0#46, 1, "N";
/*
|C_M #0#46, 1, "S";
|SI #0#45 = "N";
        #0#46 = "N";
    |PINTA #0#46;
    |C_M #0#46, 1, "N";
|FINSI;
*/
|FPRC;

|PROCESO tc1_atrasos; |TIPO 7;
|C_M #0#48, 1, "S";
|SI #0#47 = "2";
        #0#48 = "2";
    |PINTA #0#48;
    |C_M #0#48, 1, "N";
|FINSI;
|FPRC;

|PROCESO imagenes1; |TIPO 0;
     |SI FSalida = 13;
          aAlfa1 = #0#0; aAlfa1 = ("00000" + aAlfa1) % -105;
          aAlfa2 = "00000";
          orden = aAlfa1 + aAlfa2;
          pasa = "E";
          |SUB_EJECUTA "nombmp01";
     |FINSI;
|FPRC;

|PROCESO TipoIRPF; |TIPO 0;
     |SI #0#28 ! 0;|Y #0#28 ! 3;|Y #0#28 ! 4;
          |MENAV "    Error de entrada ...";
          |ERROR;
     |FINSI;
|FPRC;

|PROCESO CompBanco; |TIPO 7;
     enCodBanLab = 0;
     |HAZ BancoLab;

     |SI enCodBanLab ! 0;
          |CAMPO_MODIFICA #0#30, 1, "N";
          |CAMPO_MODIFICA #0#113, 1, "N";
/*
          aAlfa = #labempre#113;
          |QBF aAlfa;
          |QBF eaCueBanLab;

          |SI #labempre#30 ! enCodBanLab; |O aAlfa ! eaCueBanLab;
               #labempre#30 = enCodBanLab;
               #labempre#113 = eaCueBanLab;

               |PINTA #0#30
               |PINTA #0#113
          |FINSI;
*/
     |SINO;
          |CAMPO_MODIFICA #0#30, 1, "S";
          |CAMPO_MODIFICA #0#113, 1, "S";
     |FINSI;
|FPRC;

|PROCESO PintaBanco;  |TIPO 14;
     enCodBanco = #0#30;
     |HAZ LeeBancos;

     |ATRI I;  |PINTA 2130, #709#1;  |ATRI i;
|FPRC;

|PROCESO CuentaBanco;
     |ABRE #nombanco;
     #nombanco#0 = #0#30;
     |LEE 000#nombanco.=;
     |SI FSalida = 0;
          alfa1 = #nombanco#2;
          |QBF alfa1;
          |SI alfa1 ! "";
               #0#113 = alfa1;
               |PINTA #0#113;
          |FINSI;
     |FINSI;
     |CIERRA #nombanco;
|FPRC;

|PROCESO EnBlanco; |TIPO 7;
     |SI #0Campo ! "S";|Y #0Campo ! "N";
          #0Campo = "N";
          |PINTA #0Campo;
     |FINSI;
|FPRC;

|PROCESO Rellena; |TIPO 7;
     nCampo = Campo;
     |PARA nCampo = 53; |SI nCampo [ 60; |HACIENDO nCampo = nCampo + 1;
          |SI #0nCampo = " ";
               #0nCampo = "N"
               |PINTA #0nCampo;
          |FINSI;
     |SIGUE;
|FPRC;

|PROCESO RellenaAcc;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 1; |O #0Campo ! Contenido;
          #0#26 = #nomconca#145;
          #0#27 = #nomconca#143;
          #0#43 = #nomconca#146;
          #0#44 = #nomconca#147;
     |FINSI;
|FPRC;

|PROCESO AyudaPagas; |TIPO 7;
     |PUSHV 10281867;
     |ATRI R;
               ||  2 3        4         5         6
               ||  9012345678901234567890123456789012345
     |PINTA 1129, "ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂ"
     |PINTA 1229, "³ Configuracion del prorrateo de pa- ³";
     |PINTA 1329, "³ gas por empresa. Estos valores pa- ³";
     |PINTA 1429, "³ saran al trabajador cuando  se den ³";
     |PINTA 1529, "³ de alta pero  NO influyen directa- ³";
     |PINTA 1629, "³ mente en el calculo de nominas.    ³";
     |ATRI 0;
     |PINTA 1729, "ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´"
|FPRC;

|PROCESO CambioCNAE;
     nModo = (FEntrada / 100) ! 0;
     aAlfa = #0Campo;
     |QBF aAlfa;
     |SI nModo = 2; |Y aAlfa ! aCNAE;
          |ABRE #nomcnahi;
          #nomcnahi#0 = #0#0;
          #nomcnahi#2 = 2009;
          #nomcnahi#4 = 00;
          |LEE 000#nomcnahi.=;
          FEstado = FSalida;
          aAlfa = #nomcnahi#3;
          |QBF aAlfa;
          |SI FEstado ! 0; |O aAlfa = "";
               |PUSHV 13392275;
               |ATRI R;
                         ||  3 4        5         6         7
                         ||  9012345678901234567890123456789012345
               |PINTA 1339, " ATENCION: la modificación del  CNAE ";
               |PINTA 1439, " para adaptarlo a las tablas de 2009 ";
               |PINTA 1539, " ha de realizarse mediante el proce- ";
               |PINTA 1639, " ceso  [Maestros/Principales/Tarifas ";
               |PINTA 1739, " de  cotización  AT/EP/Tranformación ";
               |PINTA 1839, " a CNAE 2009] para guardar los valo- ";
               |PINTA 1939, " res de 2008. Si se  modifica direc- ";
               |PINTA 2039, " tamente  este campo se pueden gene- ";
               |PINTA 2139, " rar  errores al calcular  atrasos y ";
               |PINTA 2239, " seguros sociales desde el histórico ";
               |ATRI 0;

               |PAUSA;
               |POPV;
               #0Campo = aCNAE;
               |PINTA #0Campo;
               #0#129 = aDesCNAE;
               |PINTA #0#129;

               aAlfa1 = #0Campo;
               aAlfa1 = aAlfa1 % 102;
               #0#11 = aAlfa1;
          |FINSI;
     |FINSI;
     |CIERRA #nomcnahi;
|FPRC;

|PROCESO CNAE;
     |ABRE #nomcnaec;
     aAlfa1 = #0Campo;
     |QBF aAlfa1;
     |SI aAlfa1 = "";
          #0#129 = "";
          |PINTA #0#129;
          aAlfa1 = "    Es obligatorio introducir el CNAE ";
          aAlfa1 = aAlfa1 + "para la correcta elaboracion de los Seguros Sociales.";
          |MENAV aAlfa1;
          |ERROR;
     |SINO;
          /*
          |SI aAlfa1 = "01";
               aAlfa1 = "    ATENCION: el codigo 01 no tiene asociada cotizacion. "
               aAlfa1 = aAlfa1 + "Debe elegir una de sus actividades asociadas";
               |MENAV aAlfa1;
               |ERROR;
          |SINO;
          */
               #nomcnaec#0 = #0#128;
               |LEE 000#nomcnaec.=;
               |SI FSalida ! 0;
                    aAlfa1 = "    ATENCION: el CNAE introducido no es válido ";
                    aAlfa1 = aAlfa1 + "o no está vigente.";
                    |MENAV aAlfa1;
                    |ERROR;
               |SINO;
                    aAlfa1 = aAlfa1 % 102;
                    #0#11 = aAlfa1;
               |FINSI;
          ||FINSI;
     |FINSI;
     |CIERRA #nomcnaec;
|FPRC;

|PROCESO RellenaCNAE; |TIPO 7;
     |ABRE #nomcnaec;
     aAlfa1 = #0#128;
     |QBF aAlfa1;
     |SI aAlfa1 = "52.";
          aAlfa1 = "52";
          #0#128 = "52";
          |PINTA #0#128;
     |FINSI;
     aCNAE = #0#128;
     |QBF aCNAE;
     aDesCNAE = #0#129;
/*
     || quito esta parte que con el cmabio de CNAEs de 2009 puede dar
     || problemas
     #nomcnaec#0 = aAlfa1;
     |LEE 000#nomcnaec.=;
     |SI FSalida ! 0; |O aAlfa1 = "";
          |SI #0#11 > 0; |Y #0#11 < 100;
               aAlfa1 = #0#11;
               aAlfa1 = ("000" + aAlfa1) % -102;

               #nomcnaec#0 = aAlfa1;
               |LEE 000#nomcnaec.=;
               |SI FSalida = 0;
                    #0#128 = #nomcnaec#0;
                    #0#129 = #nomcnaec#1;
                    |PINTA #0#128;
                    |PINTA #0#129;
               |FINSI;
          |FINSI;
     |FINSI;
*/
     |CIERRA #nomcnaec;
|FPRC;

|PROCESO Actividad; |TIPO 66;
     |HAZ SacaVentana;
|FPRC;

|PROCESO PintaActiv;
     alfa1 = #0#12;
     |QBF alfa1;
     |SI alfa1 = "";
          #0#12 = #0#129 % 140;
          |PINTA #0#12;
     |FINSI;
|FPRC;

|PROCESO CompActiv;
/*
     alfa1 = #0#11;      || actividad
     alfa1 = "00" + alfa1;
     alfa1 = alfa1 % -102;

     alfa2 = #0#128;     || CNAE
     alfa2 = alfa2 % 102;

     |SI alfa1 ! alfa2;
          alfa3 =  "    ATENCION: La actividad de la empresa no puede per";
          alfa3 = alfa3 + "tenecer a un grupo distinto del CNAE";
          |MENAV alfa3;
          alfa3 = "    Pulse F11 y elija una actividad del grupo especificado";
          alfa3 = alfa3 + " en el CNAE de la empresa (" + alfa2 + ")";
          |MENAV alfa3;
          |ERROR;
     |FINSI;
*/
|FPRC;

|PROCESO Activa;
     |SI FSalida = 14;             || con F6
          |PUSHV 05012380;
          |CLS;
          |CUADRO 1218 1565;
          |PINTA 1320, "Introduzca contrase¤a:";
          E_inf = "";
          E_sup = "20";
          aPassword = 1342 ? -1;
          |SI aPassword = "<MYPASWD>     ";
          |O aPassword = "<MYPASWD>               ";
               |MENAV "     Activados modos Alta, Modificación y Baja.";
               swIntegral = -1;
          |FINSI;
          |POPV;
     |FINSI;
|FPRC;

|PROCESO Integral; |TIPO 7;
     |SI swIntegral = -1;
          |ACABA;
     |FINSI;
     swIntegral = 0;
     |DBASS aBase;   |QBT aBase;
     aBase = aBase + "integral/def/dsam0000.mas";
     |XABRE "E", aBase, nCanal;
     |SI FSalida ] 0;
          swIntegral = 1;
          |DBASS aBase;   |QBT aBase;
          aBase = aBase % -109;
          |SI aBase = "dsprofes/";
               swIntegral = 0;
          |FINSI;
     |FINSI;
     |SI swIntegral = 1;
          nModo = (FEntrada / 100) ! 0;
          |SI nModo ! 4;
               aAlfa = "    Operación no permitida desde este mantenimiento. ";
               aAlfa = aAlfa + "Sólo es posible el acceso en modo Consulta.";
               |MENAV aAlfa;
               aAlfa = "    Para Alta, Modificación o Baja, vaya a Mantenimiento y ";
               aAlfa = aAlfa + "Gestión de Clientes en el Menú Integrado.";
               |MENAV aAlfa;
               |PTEC 807;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO LimpiaCeroEmp;
     |ABRE #labempre;
     #labempre#0 = 00000;
     |LEE 101#labempre.=;
     |SI FSalida = 0;
          |BORRA 020#labempre.a;
     |FINSI;
     |LIBERA #labempre;
     |CIERRA #labempre;
|FPRC;

|PROCESO CompruebaAgitool;
     eaMensaCad = "9";
     |HAZ ControlCaducidad;
     |SI enErrorCad = 1;
         |PTEC 807;
         |ACABA;
     |FINSI;
|FPRC;

|PROCESO Tipo88emp; |TIPO 88;
/*
     |MODULO runnom;
     |SI runnom = "labacu11";
          |ACABA;
     |FINSI;
     |HAZ Tipo88;
     |HAZ ITIMSemp;
     ||HAZ LimpiaCeroEmp;
     |HAZ CompruebaAgitool;
*/
     |HAZ labclean_emp;
|FPRC;

|PROCESO ITIMSemp;
     |MODULO aAlfa;
     |QBF aAlfa;
     |SI aAlfa ! "labempre";
          |ACABA;
     |FINSI;

     fFecha = ~;
     aAlfa = fFecha; aAlfa = aAlfa % -104;
     nElAny = aAlfa;
     aAlfa = fFecha; aAlfa = aAlfa % 402;
     nElMes = aAlfa;
     |HAZ ValoresITIMS;
|FPRC;

|PROCESO ConsultaCal1; |TIPO 66;
     aAlfa1 = #0#7; |QBF aAlfa1; aAlfa1 = ("00" + aAlfa1) % -102;
     aAlfa2 = #0#8; |QBF aAlfa2; aAlfa2 = ("000" + aAlfa2) % -103;
     aCodPos = aAlfa1 + aAlfa2;
     nOpcionPLB = 0;
     |HAZ ConsCalenPLB;
     |SI nOpcionPLB ! 0;
          #0Campo = nOpcionPLB;
          |PINTA #0Campo;
          |ERROR;
     |FINSI;
|FPRC;

|PROCESO Activa64;
     |SI #0#127 > 0;
          |CAMPO_MODIFICA #0#64, 1, "S";
     |SINO;
          |CAMPO_MODIFICA #0#64, 1, "N";
     |FINSI;
|FPRC;

|PROCESO PintaCon_E; |TIPO 14;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 0;
          |PINTA 0737, #nomconca#1;
          |ACABA;
     |FINSI;
     |ABRE #nomconca;
     #nomconca#0 = #labempre#1;
     |LEE 000#nomconca.=;
     |ATRI I;
     |PINTA 0737, #nomconca#1;
     |ATRI I;
     |CIERRA #nomconca;
|FPRC;

|PROCESO SSOnlineEmp;
     aAlfa1 = #0#0; |QBF aAlfa1; aAlfa1 = ("00000" + aAlfa1) % -105;
     || aAlfa2 = #0#1; |QBF aAlfa2; aAlfa2 = ("00000" + aAlfa2) % -105;
     aAlfa2 = "00001";        || centro 1
     |SI nOpcion = 1;
          aAlfa = "labnct07;"+ "06" + aAlfa1 + aAlfa2;
     |FINSI;
     |SI nOpcion = 2;
          aAlfa = "labnct07;"+ "09" + aAlfa1 + aAlfa2;
     |FINSI;
     |SI nOpcion = 3;
          aAlfa = "labnct07;"+ "11" + aAlfa1 + aAlfa2;
     |FINSI;
     |SI nOpcion = 4;
          aAlfa = "labnct07;"+ "13" + aAlfa1 + aAlfa2;
     |FINSI;
     |SI nOpcion = 5;
          aAlfa = "labnct07;"+ "15" + aAlfa1 + aAlfa2;
     |FINSI;
     |SI nOpcion = 6;
          aAlfa = "labnct07;"+ "CO01" + aAlfa1 + aAlfa2;
     |FINSI;

     |SUB_EJECUTA aAlfa;
|FPRC;

|PROCESO MenuREDEmp;
     eaVengoDe = "labempre";
     |VARIABLES; {20} aPopup = ""; |FINSI;
     aPopup1 = "-1";  || Si fuera -1 en la posicion
     aPopup2 = 4;
     aPopup3 = "1. Situación de la empresa";
     aPopup4 = "2. Certificado situación de cotización";
     aPopup5 = "3. Afiliados en el Cod. Cta. Coticación";
     aPopup6 = "4. Consulta de trabajadores en alta";
     aPopup7 = "5. Consulta de Convenios";
     aPopup8 = "6. Datos de la Empresa en Contrat@";
     IaPopup = aPopup1  <-;
     |ESPECIFICA "TRACKPOPUP", aPopup;
     nOpcion = FSalida;
     |HAZ SSOnlineEmp;
|FPRC;

|PROCESO BotonREDEmp; |TIPO 8;
     |MODULO aAlfa;
     |QBF aAlfa;
     |SI aAlfa ! "labempre";
          |ACABA;
     |FINSI;

     ||CONTROL_SIMPLE 0, "BOTON,Seguridad Social Online", 0760, 0777, MenuREDEmp;
     |CONTROL_SIMPLE 0, "BOTON,Seguridad Social Online", 0760, 0777, ConectaSS;
     enBoton = FSalida;
     |ESTADO_CONTROL enBoton, "ENABLE";
     swBotonSiNo = 1;
|FPRC;

|PROCESO FinBotonREDEmp; |TIPO 9;
     |MODULO aAlfa;
     |QBF aAlfa;
     |SI aAlfa ! "labempre";
          |ACABA;
     |FINSI;

     |FIN_CONTROL_WINDOWS enBoton;
     |PULSATECLA;
     swBotonSiNo = 0;
|FPRC;

|PROCESO PonBotonREDEmp; |TIPO 14;
     |MODULO aAlfa;
     |QBF aAlfa;
     |SI aAlfa ! "labempre";
          |ACABA;
     |FINSI;

     |SI swBotonSiNo = 0;
          |HAZ BotonREDEmp;
     |FINSI;
|FPRC;

|PROCESO QuitaBotonEmp; |TIPO 14;
     |SI swBotonSiNo = 1;
          |HAZ FinBotonREDEmp;
     |FINSI;
|FPRC;

|PROCESO CompEmail_emp;
     eaEmail = #0Campo;
     |HAZ CompEmail_plb;
|FPRC;

|PROCESO CompDSArchi_emp;
     |HAZ MiraSiArchiva;
     |SI nArchi ! 1; |Y #0Campo = "S";
          aAlfa = "    Ha de tener instalada la aplicación DSARCHI y activar ";
          aAlfa = aAlfa + "la emisión de nóminas para poder usar esta opción";
          |MENAV aAlfa;

          #0Campo = "N";
          |PINTA #0Campo;
          |ACABA;
     |FINSI;
     |SI #0Campo = "S";
          nCampo = Campo - 1;
          aAlfa = #0nCampo;
          |QBF aAlfa;
          |SI aAlfa = "";
               aAlfa = "    ATENCION. La dirección de correo electrónico del ";
               aAlfa = aAlfa + "trabajador está en blanco. No se enviará la nómina.";
               |MENAV aAlfa;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Iniciando_emp;
     #labempre#68 = "N";
     |GRABA 020#labempre.a;
     |LIBERA #labempre;
|FPRC;

|DEFBUCLE Iniciando_emp;
     #labempre#1;
     ;
     INICIO;
     FINAL;
     be;
     NULL, Iniciando_emp;
|FIN;

|PROCESO labclean_emp;
     |MODULO aAlfa;
     |QBF aAlfa;
     |SI aAlfa ! "labempre";
          |ACABA;
     |FINSI;

     |PINTA 1327, "ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿"
     |PINTA 1427, "³   Inicializando nuevos campos.   ³";
     |PINTA 1527, "³ Por favor, espere unos segundos. ³";
     |PINTA 1627, "³                                  ³"
     |PINTA 1727, "ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ"

     |ABRE #labclean;

     #labclean#0 = "labempre";
     #labclean#1 = 68;
     |LEE 101#labclean.=;
     |SI FSalida ! 0;
          |BUCLE Iniciando_emp;

          #labclean#0 = "labempre";
          #labclean#1 = 68;
          #labclean#2 = "S";
          |GRABA 020#labclean.n;
          |LIBERA #labclean;

     |FINSI;

     |CIERRA #labclean;

     |PINTA 1327, "                                    ";
     |PINTA 1427, "                                    ";
     |PINTA 1527, "                                    ";
     |PINTA 1627, "                                    ";
     |PINTA 1727, "                                    ";
|FPRC;

|PROCESO Ayuda133; |TIPO 7;
     |PUSHV 12402279;
     |ATRI R;

               ||  4         5         6         7
               ||  0123456789012345678901234567890123456789
     |PINTA 1240, " ATENCION: este par metro  se usa  para ";
     |PINTA 1340, " EVITAR que en  trabajadores que  est n ";
     |PINTA 1440, " de alta en esta empresa Y EN OTRAS, se ";
     |PINTA 1540, " tengan en cuenta los ingresos  de esta ";
     |PINTA 1640, " empresa a la hora de regularizar.      ";
     |PINTA 1740, " SOLO si ‚sta es una empresa de pruebas ";
     |PINTA 1840, " y NO hay que tener en cuenta  sus tra- ";
     |PINTA 1940, " bajadores para el c lculo del IRPF, en ";
     |PINTA 2040, " ese caso hay que indicar 'S', para de- ";
     |PINTA 2140, " sactivar el c lculo  conjunto de  esta ";
     |PINTA 2240, " empresa en la regularizaci¢n de IRPF.  ";
|FPRC;

|PROCESO Activa67; |TIPO 14;
     swSigue = 0;
     |ABRE #cretam00;
     |LEE 000#cretam00.p;
     |SI FSalida = 0;
          |SI #cretam00#1 = "S";
               swSigue = 1;
          |FINSI;
     |FINSI;
     |CIERRA #cretam00;

     |SI swSigue = 1;
          |CAMPO_MODIFICA #0#67, 1, "S";
          |CAMPO_VISUAL #0#67, 1, "S";
          |ATRI R;
          |PINTA 1942, "Sistema Liquidaci¢n Dir.";
          |ATRI 0;
          |PINTA 2042, "SILTRA S/N ........:    ";
          |ATRI I;
          |PINTA #0#67;
          |ATRI I;
     |SINO;
          |CAMPO_MODIFICA #0#67, 1, "N";
          |CAMPO_VISUAL #0#67, 1, "N";
          |ATRI 0;
          |PINTA 1942, "                        ";
          |PINTA 2042, "                        ";
          |ATRI I;
     |FINSI;
|FPRC;
