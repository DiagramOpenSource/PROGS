|FICHEROS;
    nomdirec #0;    || def proceso
    nomconca #1;    || convenios cabeceras
    nomconli #2;    || convenios lineas
    nomtraca #3;    || aux.trab.cabs.
    nomtrali #4;    || aux.trab.lins.
    nomcatca #5;    || aux.cate.cabs.
    nomcatli #6;    || aux.cate.lins.
    nomtraac #7;    || trab.atra.cabs.
    nomtraal #8;    || trab.atra.lins.
    nomcatac #9;    || cate.atra.cabs.
    nomcatal #10;   || cate.atra.lins.
    labtraba #11;   || maestro trabajadores
    nomcateg #12;   || categorias (descrip.)
    labempre #13;   || maestro empresas
    nomantca;       || antiguedad por categorias de convenio
     nomantig;
     nomantil;
|FIN;

|VARIABLES;
    ya = 0;
    fuera = 0;
    detector = 0;
    FEstado = 0;
    alfa1 = "";
    alfa2 = "";
    alfa3 = "";
    alfa4 = "";
    xxxxfiac = "  ";
    xxaponer = "  ";
    xrelleno = "                              ";
     swActiva = 0;
     nCampo = 0;
     nCampo0 = 0;
     aAlfa = "";
     nNume = 0;
     swNoPidas = 0;
     nCampo1 = 0;
     nCampo2 = 0;
     nValor = 0;
     swDesdeAsis = 0;
     nCantidad = 0;
     nume1 = 0;
     nume2 = 0;
     nUltCat = 0;
     swNomantig = 0;
     swFIN102 = 0;
     swAntPorc = 0;
|FIN;

***************************************************************
     LLAMADAS DESDE CAMPOS
***************************************************************

|PROCESO inicio; |TIPO 3;
    ya = 1;
|SI #0#0 = "T";  |HAZ trabaja;  |FINSI;
|SI #0#0 = "C";  |HAZ categor;  |FINSI;
|FPRC;

|PROCESO asume; |TIPO 7;
     |SI #0#19 = "P";
          #0#10 = #0#8 > #0#17;
     |SINO;
          #0#10 = #0#17;
     |FINSI;
     |PINTA #0#10;
|FPRC;

|PROCESO noentres; |TIPO 7;
|SI ya = 0;
    xxaponer = Anono;
    xxxxfiac = Control % A1001;
    Control = Control + xxxxfiac;
    Control = Control + xrelleno;
    Control = Control % A120;
    Control = Control + xxaponer;
|FINSI;
|FPRC;

|PROCESO pulsaf1; |TIPO 0;
     |SI FSalida = 9;
          |SI #0#19 = "P"; |Y #0#18 = "N";
               #0#10 = #0#8 > #0#17;
          |SINO;
               #0#10 = #0#8 > #0#10;
          |FINSI;
     |FINSI;
     |PINTA #0#10;
|FPRC;

|PROCESO varia; |TIPO 7;
     #0#9 = #0#10 - #0#8;
     |PINTA #0#9;
|FPRC;

|PROCESO sinpregunta; |TIPO 7;
     |SI #0#17 ! 0;
          |C_M #0#18, 1, "S";
          |C_M #0#19, 1, "S";
     |SINO;
          #0#18 = "S";   |PINTA #0#18;
          |C_M #0#18, 1, "N";
          |C_M #0#19, 1, "N";
     |FINSI;
|FPRC;

|PROCESO des_emp; |TIPO 0;
|SI #0#0 = "C";
     |C_M #0#13, 1, "N";
     |C_M #0#14, 1, "N";
     |C_M #0#15, 1, "N";
     |C_M #0#16, 1, "N";
|SINO;
     |C_M #0#13, 1, "S";
     |C_M #0#14, 1, "S";
     |C_M #0#15, 1, "S";
     |C_M #0#16, 1, "S";
|FINSI;
|FPRC;

***************************************************************
      LECTURA DE TRABAJADORES
***************************************************************

|PROCESO trabaja2;  || lee el concepto en el convenio
|SI #4#2 ] #0#11;|Y #4#2 [ #0#12;
        #2#0 = #0#1;  || convenio
        #2#2 = #4#2;  || concepto
    |LEE 040#2=;
    |SI FSalida = 0;
            #0#02 = #2#2;  || concepto
            #0#03 = #2#3;  || calculo alfa
            #0#04 = #2#4;  || calculo nume
            #0#05 = #2#5;  || situacion
            #0#06 = #2#6;  || descripcion
            #0#07 = #4#3;  || unidades
            #0#08 = #4#4;  || valor anterior
            #0#09 =    0;  || variacion
            #0#10 = #4#4;  || valor nuevo (por defecto el mismo)
        |HAZ pide;
        |SI fuera ! 1;
            |HAZ gra_tra;
            |HAZ gra_atr;
        |FINSI;
    |FINSI;
|FINSI;
|LIBERA #4;
|FPRC;

|PROCESO trabaja1;  || lee el aux. de trabajadores
|HAZ activasino;
|SI swActiva = 1;
    #3#0 = #11#0;
    #3#1 = #11#1;
    |LEE 040#3=;
    |SI FSalida = 0;
        |ATRI I;
        |PINTA 0916, #11#0;
        |PINTA 0931, #11#1;
        |PINTA 0944, #11#2;
        |ATRI 0;
        |BUCLELIN 0trabaja2#3;
    |FINSI;
|FINSI;
|FPRC;

|DEFBUCLE nomdirec0;
 #11#1;
 87;
 #0#13, #0#15, #0#1;
 #0#14, #0#16, #0#1;
 ;
 NULL, trabaja1;
|FIN;

|PROCESO trabaja;
|ATRI P;  |PINTA 0907, "*";   |ATRI 0;
|PINTA 0908,"Empresa:    Trabajador:      Nombre: ";
|ABRE #02;
|ABRE #03;
|ABRE #07;
|ABRE #08;
|BUCLE nomdirec0;
|CIERRA #02;
|CIERRA #03;
|CIERRA #07;
|CIERRA #08;
|FPRC;

***************************************************************
      LECTURA DE CATEGORIAS
***************************************************************

|PROCESO nomantig;
     swNomantig = 1;
     swDesdeAsis = 1;
     || DESDE ASISTENTE DE ANTIGUEDAD (LO NUEVO)
     |PARA nCampo = 31; |SI nCampo [ 100; |Y #nomantig#nCampo# ! 0;
     |HACIENDO nCampo = nCampo + 1;
          |SI nCampo = 41; nCampo = 61; |FINSI;
          |SI nCampo = 71; nCampo = 91; |FINSI;
          nCampo0 = nCampo - 20;
          #0#02 = 102;   || concepto
          #0#03 = #2#3;  || calculo alfa
          #0#04 = #2#4;  || calculo nume
          #0#05 = #2#5;  || situacion
          #0#06 = #2#6;  || descripcion
          #0#07 = 0;     || unidades
          #0#08 = #nomantig#nCampo#;  || valor anterior
          #0#09 = 0;     || variacion
          #0#10 = #nomantig#nCampo#;  || valor nuevo (por defecto el mismo)
          /*
          |SI #0#19 = "P"; |Y swNoPidas = 0;
               nNume = #0#17; aAlfa = nNume; |QBF aAlfa;
               aAlfa = "    SConcepto 102 (Antiguedad): Aumentar el " + aAlfa;
               aAlfa = aAlfa + " % a todos los tramos de las categorias?";
               |CONFI aAlfa;
               |SI FSalida = 0;
                    swNoPidas = 1;
               |SINO;
                    swNoPidas = 2;
               |FINSI;
          |FINSI;
          */
          |SI #0#19 = "P"; |Y swNoPidas = 0; |Y #0#17 ! 0;
               swNoPidas = 1; || no pido nada, para eso es "3"
          |FINSI;

          |SI swNoPidas = 1;
               #0#10 = #0#8 > #0#17;
               #0#9 = #0#10 - #0#8;
               fuera = 2;
          |SINO;
               |HAZ pide;
          |FINSI;

          |SI fuera ! 1;
               |SI #nomantig#7 = 3;
                    || el mismo importe para todas las categorias

                    |HAZ bucle_gra_atc;
               |SINO;
                    |HAZ gra_atc;
               |FINSI;
               |HAZ gra_ant;
               |HAZ gra_nomantig;
          |FINSI;
     |SIGUE;
|FPRC;

|PROCESO nomantil;
     swDesdeAsis = 1;
     || DESDE ASISTENTE DE ANTIGUEDAD (LO NUEVO)
     |PARA nCampo = 31; |SI nCampo [ 100; |Y #nomantil#nCampo# ! 0;
     |HACIENDO nCampo = nCampo + 1;
          |SI nCampo = 41; nCampo = 61; |FINSI;
          |SI nCampo = 71; nCampo = 91; |FINSI;
          #0#02 = 102;   || concepto
          #0#03 = #2#3;  || calculo alfa
          #0#04 = #2#4;  || calculo nume
          #0#05 = #2#5;  || situacion
          #0#06 = #2#6;  || descripcion
          #0#07 = 0;     || unidades
          #0#08 = #nomantil#nCampo#;  || valor anterior
          #0#09 = 0;     || variacion
          #0#10 = #nomantil#nCampo#;  || valor nuevo (por defecto el mismo)
          |SI #0#19 = "P"; |Y swNoPidas = 0;
               nNume = #0#17; aAlfa = nNume; |QBF aAlfa;
               aAlfa = "    SConcepto 102 (Antiguedad): Aumentar el " + aAlfa;
               aAlfa = aAlfa + " % a todos los tramos de la categoria?";
               |CONFI aAlfa;
               |SI FSalida = 0;
                    swNoPidas = 1;
               |SINO;
                    swNoPidas = 2;
               |FINSI;
          |FINSI;
          |SI swNoPidas = 1;
               #0#10 = #0#8 > #0#17;
               #0#9 = #0#10 - #0#8;
               fuera = 2;
          |SINO;
               |HAZ pide;
          |FINSI;
          |SI fuera ! 1;
               |HAZ gra_atc;
               |HAZ gra_ant;
               |HAZ gra_nomantil;
          |FINSI;
     |SIGUE;
|FPRC;

|PROCESO GrabaConcepto;
     #2#0 = #6#0;  || convenio
     #2#2 = #6#2;  || concepto
     |LEE 040#2=;
     |SI FSalida = 0;
          #0#02 = #2#2;  || concepto
          #0#03 = #2#3;  || calculo alfa
          #0#04 = #2#4;  || calculo nume
          #0#05 = #2#5;  || situacion
          #0#06 = #2#6;  || descripcion
          #0#07 =    0;  || unidades
          #0#08 = #6#4;  || valor anterior
          #0#09 =    0;  || variacion
          #0#10 = #6#4;  || valor nuevo (por defecto el mismo)
          |HAZ pide;
          |SI fuera ! 1;
               |HAZ gra_cat;
               |HAZ gra_atc;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO categor1;  || lee el aux. de categorias
     swAntPorc = 0;
     swNoPidas = 0;
     #5#0 = #6#0;
     #5#1 = #6#1;
     |LEE 020#5=;
     |SI FSalida = 0;
          |DDEFECTO #12;
          #12#0 = #5#0;
          #12#1 = #5#1;
          |LEE 000#12=;
          |ATRI P;
          |PINTA 0907, "*";
          |ATRI 0;
          |ATRI I;
          |PINTA 0908,"Categoria:    Descripcion:                         ";
          |PINTA 0919, #6#1;
          |PINTA 0935, #12#2;
          |ATRI 0;
          |SI #6#2 ! 102; |O #nomconca#2 = 0;    || si no es antiguedad
               |HAZ GrabaConcepto;
          |SINO;
               swDesdeAsis = 0;
               #2#0 = #6#0;  || convenio
               #2#2 = #6#2;  || concepto
               |LEE 040#2=;
               |SI FSalida = 0;
                    #nomantig#0 = #5#0;      || convenio
                    |LEE 101#nomantig.=;

                    #nomantil#0 = #5#0;      || convenio
                    #nomantil#1 = #5#1;      || categoria
                    |LEE 101#nomantil.=;

                    |SI FSalida = 0;
                         |SI #nomantig#7 = 2;
                              |HAZ nomantil;
                         |FINSI;
                         |SI #nomantig#7 = 3; |Y swFIN102 = 0;
                              aAlfa = " - Se establece el mismo valor para todas las categor¡as - ";
                              |PINTA 0907, aAlfa;
                              |HAZ nomantig;
                              swFIN102 = 1;
                         |FINSI;
                    |SINO;
                         || DESDE ANTIGUEDAD POR CONVENIO/CATEGORIA (LO DE SIEMPRE)
                         swDesdeAsis = 0;

                         #nomantca#0 = #5#0;
                         #nomantca#1 = #5#1;
                         |LEE 000#nomantca.=;
                         |SI FSalida = 0;
                              |PARA nCampo = 14; |SI nCampo [ 63; |Y #nomantca#nCampo# ! 0;
                              |HACIENDO nCampo = nCampo + 1;
                                   #0#02 = 102;   || concepto
                                   #0#03 = #2#3;  || calculo alfa
                                   #0#04 = #2#4;  || calculo nume
                                   #0#05 = #2#5;  || situacion
                                   #0#06 = #2#6;  || descripcion
                                   #0#07 = 0;     || unidades
                                   #0#08 = #nomantca#nCampo#;  || valor anterior
                                   #0#09 = 0;     || variacion
                                   #0#10 = #nomantca#nCampo#;  || valor nuevo (por defecto el mismo)
                                   |SI #0#19 = "P"; |Y swNoPidas = 0;
                                        nNume = #0#17; aAlfa = nNume; |QBF aAlfa;
                                        aAlfa = "    SConcepto 102 (Antiguedad): Aumentar el " + aAlfa;
                                        aAlfa = aAlfa + " % a todos los tramos de la categoria?";
                                        |CONFI aAlfa;
                                        |SI FSalida = 0;
                                             swNoPidas = 1;
                                        |SINO;
                                             swNoPidas = 2;
                                        |FINSI;
                                   |FINSI;
                                   |SI swNoPidas = 1;
                                        #0#10 = #0#8 > #0#17;
                                        #0#9 = #0#10 - #0#8;
                                        fuera = 2;
                                   |SINO;
                                        |HAZ pide;
                                   |FINSI;
                                   |SI fuera ! 1;
                                        |HAZ gra_atc;
                                        |HAZ gra_ant;
                                        |HAZ gra_nomantil;
                                   |FINSI;
                                   |SI nCampo = 23; nCampo = nCampo + 10; |FINSI;
                              |SIGUE;
                         |SINO;
                              swAntPorc = 1;
                              |HAZ GrabaConcepto;
                         |FINSI;
                    |FINSI;
               |FINSI;
          |FINSI;
     |FINSI;
     |LIBERA #6;
|FPRC;

|DEFBUCLE nomdirec1;
#6#1;
;
#0#1, 001, #0#11;
#0#1, 999, #0#12;
be;
NULL, categor1;
|FIN;

|PROCESO nomcateg_ant;
     |ABRE #nomcateg;
     #nomcateg#0 = #0#1;
     #nomcateg#1 = 999;
     |LEE 000#nomcateg.];
     |SI FSalida = 0; |Y #nomcateg#0 = #0#1;
          nUltCat = #nomcateg#1;
     |SINO;
          |LEE 000#nomcateg.a;
          |SI #nomcateg#0 = #0#1;
               nUltCat = #nomcateg#1;
          |FINSI;
     |FINSI;
     |CIERRA #nomcateg;
|FPRC;

|PROCESO categor;
|ATRI P;  |PINTA 0907, "*";   |ATRI 0;
|PINTA 0908,"Categoria:    Descripcion: ";
|ABRE #02;
|ABRE #05;
|ABRE #09;
|ABRE #10;
|ABRE #12;
|ABRE #nomantca;
|ABRE #nomantig;
|ABRE #nomantil;
nCantidad = 0;
|HAZ nomcateg_ant;
|BUCLE nomdirec1;
|CIERRA #02;
|CIERRA #05;
|CIERRA #09;
|CIERRA #10;
|CIERRA #12;
|CIERRA #nomantil;
|CIERRA #nomantig;
|CIERRA #nomantca;
|FPRC;

***************************************************************
      RUTINAS COMUNES
***************************************************************

|PROCESO PintaPanAntig;
     |SI #0#2 = 102; |Y #0#0 ! "T"; |Y #nomconca#2 ! 0;
          |SI swDesdeAsis = 0;
               nCampo0 = nCampo - 10;
               aAlfa = #nomantca#nCampo0#;
          |SINO;
               nCampo0 = nCampo - 20;
               |SI #nomantig#7 = 2;
                    aAlfa = #nomantil#nCampo0#;
               |FINSI;
               |SI #nomantig#7 = 3;
                    aAlfa = #nomantig#nCampo0#;
               |FINSI;
          |FINSI;
          |PINTA 1408, "ÚÄÄÄÄ";
          |PINTA 1508, "³";
          |ATRI R;
          |PINTA 1509, "A¤os";
          |ATRI 0;
          |PINTA 1608, "ÃÄÄÄÄ";
          |ATRI I;
          aAlfa = "³ " + aAlfa + " ";
          |PINTA 1708, aAlfa;
          |ATRI 0;
          |PINTA 1808, "ÀÄÄÄÄ";
     |SINO;
          |PINTA 1408, "     ";
          |PINTA 1508, "     ";
          |PINTA 1608, "     ";
          |PINTA 1708, "     ";
          |PINTA 1808, "     ";
     |FINSI;
|FPRC;

|PROCESO pide;
     |SI swAntPorc = 0;
          |HAZ PintaPanAntig;
     |FINSI;
     |ATRI I;
     |PINDA #0#0;
     |ATRI 0;
     |ET varia;
     fuera = 0;
     detector = -1;
     |PARA; |SI detector ! 0; |HACIENDO;
          |SI #0#18 = "N";|Y #0#19 = "P";     |PTEC 823;     |FINSI;
          |SI #0#18 = "N";|Y #0#19 = "C";     |PTEC 802;     |FINSI;
          |ENCAMPO #10#0;
          |SI FSalida ! 1;|Y FSalida ! 7;|Y FSalida ! 9;
               detector = FSalida;
          |SINO;
               |SI FSalida ! 9;
                    fuera = 1;
               |FINSI;
               detector = 0;
          |FINSI;
     |SIGUE;
     fuera = 0;
     detector = -1;
     |PARA; |SI detector ! 0; |HACIENDO;
          |SI #0#18 = "N";     |PTEC 802;     |FINSI;
          |ENCAMPO #09#0;
          |SI FSalida ! 1;|Y FSalida ! 7;|Y FSalida ! 2;
               detector = FSalida;
          |SINO;
               |SI FSalida ! 2;
                    fuera = 1;
               |SINO;
                    fuera = 2;
               |FINSI;
               detector = 0;
          |FINSI;
     |SIGUE;
     |SI fuera = 2;  |VETE varia;  |FINSI;
|FPRC;

***************************************************************
      RUTINAS TRABAJADORES
***************************************************************

|PROCESO gra_tra;
    alfa1 = #0#10;
    alfa1 = "            " + alfa1;
    alfa1 = alfa1 % -112;
    #4#4 = alfa1;
|GRABA 020#4a;
|FPRC;

|PROCESO gra_atr;   || graba cabs. y lins. de atrasos trabajador
    #7#0 = #11#0;
    #7#1 = #11#1;
    #7#3 = #0#20;
|LEE 140#7=;
|SI FSalida ! 0;
        #7#0 = #11#0;
        #7#1 = #11#1;
        #7#2 = #11#2;
        #7#3 = #0#20;
    |GRABA 020#7n;
|FINSI;
|LIBERA #7;
    #8#0 = #11#0;
    #8#1 = #11#1;
    #8#2 = #4#2;
    #8#5 = #0#20;
|LEE 140#8=;
    FEstado = FSalida;
|DDEFECTO #8;
    #8#0 = #11#0;
    #8#1 = #11#1;
    #8#2 = #4#2;
    #8#5 = #0#20;
|SI #0#10 < #0#8;
        #8#3 = "-";
        #8#4 = #0#9 * (-1);
|SINO;
        #8#3 = "+";
        #8#4 = #0#9;
|FINSI;
|SI FEstado ! 0;  |GRABA 020#8n;  |FINSI;
|SI FEstado = 0;  |GRABA 020#8a;  |FINSI;
|LIBERA #8;
|FPRC;

***************************************************************
      RUTINAS TRABAJADORES
***************************************************************

|PROCESO gra_nomantig;
     |SI swDesdeAsis = 0;
          #nomantig#0 = #nomantca#0;
          |LEE 000#nomantig.=;
          |SI FSalida ! 0; |O #nomantig#8 ! "A";
               |ACABA;
          |FINSI;

          |PARA nCampo1 = 11; |SI nCampo1 [ 100; |HACIENDO nCampo1 = nCampo1 + 1;
               |SI nCampo1 ] 11; |Y nCampo1 [ 20;           || los a¤os
                    nCampo2 = nCampo1 - 7;
                    #nomantig#nCampo1# = #nomantca#nCampo2#;
               |FINSI;
               |SI nCampo1 ] 41; |Y nCampo1 [ 50;
                    nCampo2 = nCampo1 - 17;
                    #nomantig#nCampo1# = #nomantca#nCampo2#;
               |FINSI;
               |SI nCampo1 ] 71; |Y nCampo1 [ 80;
                    nCampo2 = nCampo1 - 27;
                    #nomantig#nCampo1# = #nomantca#nCampo2#;
               |FINSI;

               |SI nCampo1 ] 31; |Y nCampo1 [ 40;           || los importes
                    nCampo2 = nCampo1 - 17;
                    |SI #nomantig#8 = "A";
                         nValor = #nomantca#nCampo2#;
                         #nomantig#nCampo1# = nValor;
                    |FINSI;
               |FINSI;
               |SI nCampo1 ] 61; |Y nCampo1 [ 70;
                    nCampo2 = nCampo1 - 27;
                    |SI #nomantig#8 = "A";
                         nValor = #nomantca#nCampo2#;
                         #nomantig#nCampo1# = nValor;
                    |FINSI;
               |FINSI;
               |SI nCampo1 ] 91; |Y nCampo1 [ 100;
                    nCampo2 = nCampo1 - 37;
                    |SI #nomantig#8 = "A";
                         nValor = #nomantca#nCampo2#;
                         #nomantig#nCampo1# = nValor;
                    |FINSI;
               |FINSI;
          |SIGUE;

          |GRABA 020#nomantig.a;
     |SINO;
          |SI swNoPidas = 0;
               #nomantig#nCampo# = #0#10;    || ultima categoria del convenio
               |GRABA 020#nomantig.a;
          |FINSI;
          |SI #nomantca#1 = nUltCat;         || solo lo grabo una vez, en la
               #nomantig#nCampo# = #0#10;    || ultima categoria del convenio
               |GRABA 020#nomantig.a;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO gra_nomantil;
     |SI swDesdeAsis = 0;
          #nomantig#0 = #nomantca#0;
          |LEE 000#nomantig.=;
          |SI FSalida ! 0; |O #nomantig#8 ! "A";
               |ACABA;
          |FINSI;

          #nomantil#0 = #nomantca#0;
          #nomantil#1 = #nomantca#1;
          |LEE 101#nomantil.=;
          FEstado = FSalida;

          |PARA nCampo1 = 11; |SI nCampo1 [ 100; |HACIENDO nCampo1 = nCampo1 + 1;
               |SI nCampo1 ] 11; |Y nCampo1 [ 20;           || los a¤os
                    nCampo2 = nCampo1 - 7;
                    #nomantil#nCampo1# = #nomantca#nCampo2#;
               |FINSI;
               |SI nCampo1 ] 41; |Y nCampo1 [ 50;
                    nCampo2 = nCampo1 - 17;
                    #nomantil#nCampo1# = #nomantca#nCampo2#;
               |FINSI;
               |SI nCampo1 ] 71; |Y nCampo1 [ 80;
                    nCampo2 = nCampo1 - 27;
                    #nomantil#nCampo1# = #nomantca#nCampo2#;
               |FINSI;

               |SI nCampo1 ] 31; |Y nCampo1 [ 40;           || los importes
                    nCampo2 = nCampo1 - 17;
                    |SI #nomantig#8 = "A";
                         nValor = #nomantca#nCampo2#;
                         #nomantil#nCampo1# = nValor;
                    |FINSI;
               |FINSI;
               |SI nCampo1 ] 61; |Y nCampo1 [ 70;
                    nCampo2 = nCampo1 - 27;
                    |SI #nomantig#8 = "A";
                         nValor = #nomantca#nCampo2#;
                         #nomantil#nCampo1# = nValor;
                    |FINSI;
               |FINSI;
               |SI nCampo1 ] 91; |Y nCampo1 [ 100;
                    nCampo2 = nCampo1 - 37;
                    |SI #nomantig#8 = "A";
                         nValor = #nomantca#nCampo2#;
                         #nomantil#nCampo1# = nValor;
                    |FINSI;
               |FINSI;
          |SIGUE;
          |SI FEstado = 0;
               |GRABA 020#nomantil.a;
          |SINO;
               |GRABA 020#nomantil.n;
          |FINSI;
     |SINO;
          #nomantil#nCampo# = #0#10;
          |GRABA 020#nomantil.a;
     |FINSI;
|FPRC;

|PROCESO gra_nomantca3;
     #nomantca#0 = #nomcateg#0;
     #nomantca#1 = #nomcateg#1;
     || #nomantca#0 = #6#0;
     || #nomantca#1 = #6#1;
     |LEE 101#nomantca.=;
     FEstado = FSalida;
     |PARA nume1 = 4; |SI nume1 [ 53; |HACIENDO nume1 = nume1 + 1;
          |SI nume1 = 14; nume1 = 24; |FINSI;
          |SI nume1 = 34; nume1 = 44; |FINSI;
          |SI #nomantca#nume1# = 0;
               |SAL;
          |FINSI;
          |SI #nomantca#nume1# = #nomantig#nCampo0#;    || a¤os
               nume2 = nume1 + 10;
               |SI #nomantig#8 = "A";
                    nCantidad = #0#10;
               |FINSI;
               |SI #nomantig#8 = "S";
                    nCantidad = nCantidad + #0#10;
               |FINSI;
               #nomantca#nume2# = nCantidad;   || cantidad
               |SI FEstado = 0;
                    |GRABA 020#nomantca.a;
               |SINO;
                    |GRABA 020#nomantca.n;
               |FINSI;
          |FINSI;
     |SIGUE;
|FPRC;

|DEFBUCLE gra_nomantca3;
     #nomcateg#1;
     ;
     #0#1, 001;
     #0#1, 999;
     ;
     NULL, gra_nomantca3;
|FIN;

|PROCESO gra_ant;
     |SI swDesdeAsis = 0;
          #nomantca#nCampo# = #0#10;
          |GRABA 020#nomantca.a;
          |LIBERA #nomantca;
     |SINO;
          #nomantig#0 = #0#1;
          |LEE 000#nomantig.=;

          |SI #nomantig#7 = 2;
               #nomantca#0 = #nomantil#0;
               #nomantca#1 = #nomantil#1;
               |LEE 020#nomantca.=;
               |SI FSalida = 0;
                    |PARA nume1 = 4; |SI nume1 [ 53; |HACIENDO nume1 = nume1 + 1;
                         |SI nume1 = 14; nume1 = 24; |FINSI;
                         |SI nume1 = 34; nume1 = 44; |FINSI;
                         |SI #nomantca#nume1# = 0;
                              |SAL;
                         |FINSI;
                         |SI #nomantca#nume1# = #nomantil#nCampo0#;    || a¤os
                              nume2 = nume1 + 10;
                              |SI #nomantig#8 = "A";
                                   nCantidad = #0#10;
                              |FINSI;
                              |SI #nomantig#8 = "S";
                                   nCantidad = nCantidad + #0#10;
                              |FINSI;
                              #nomantca#nume2# = nCantidad;   || cantidad
                              |GRABA 020#nomantca.a;
                         |FINSI;
                    |SIGUE;
               |FINSI;
          |FINSI;
          |SI #nomantig#7 = 3;
               |BUCLE gra_nomantca3;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO gra_cat;
|SI #0#2 ! 102; |O #nomconca#2 = 0;
    alfa1 = #0#10;
    alfa1 = "            " + alfa1;
    alfa1 = alfa1 % -112;
    #6#4 = alfa1;
|SINO;
    #6#4 = "          0";
|FINSI;
|GRABA 020#6a;
|FPRC;

|PROCESO gra_atc;   || graba cabs. y lins. de atrasos trabajador
     #9#0 = #6#0;
     #9#1 = #6#1;
     #9#3 = #0#20;
     |LEE 140#9=;
     |SI FSalida ! 0;
          #9#0 = #6#0;
          #9#1 = #6#1;
          #9#2 = #5#2;
          #9#3 = #0#20;
          |GRABA 020#9n;
     |FINSI;
     |LIBERA #9;

     #10#0 = #6#0;
     #10#1 = #6#1;
     #10#2 = #6#2;
     #10#5 = #0#20;
     |LEE 140#10=;
     FEstado = FSalida;
     |DDEFECTO #10;
     #10#0 = #6#0;
     #10#1 = #6#1;
     #10#2 = #6#2;
     #10#5 = #0#20;
     |SI #10#2 = 102; |Y #nomconca#2 ! 0;
          #10#3 = "+";
          #10#4 = 0;
     |SINO;
          |SI #0#10 < #0#8;
               #10#3 = "-";
               #10#4 = #0#9 * (-1);
          |SINO;
               #10#3 = "+";
               #10#4 = #0#9;
          |FINSI;
     |FINSI;
     |SI FEstado ! 0;  |GRABA 020#10n;  |FINSI;
     |SI FEstado = 0;  |GRABA 020#10a;  |FINSI;
     |LIBERA #10;
|FPRC;

|PROCESO gra_atc_cat;   || graba cabs. y lins. de atrasos trabajador
     || para los convenios que estan introducidos por el asistente
     || y tienen el parametro 3 en el campo #nomantig#7, mismo importe
     || para todas las categorias en antiguedad

     || es similar al PROCESO gra_atc pero tomo como base el bucle sobre
     || el fichero de cabecera de categorias, que no se por que no lo
     || hace tambien en el PROCESO gra_atc porque para el caso hace lo
     || mismo, pero no lo cambio por lo liar, lo duplico

     #9#0 = #5#0;
     #9#1 = #5#1;
     #9#3 = #0#20;
     |LEE 140#9=;
     |SI FSalida ! 0;
          #9#0 = #5#0;
          #9#1 = #5#1;
          #9#2 = #5#2;
          #9#3 = #0#20;
          |GRABA 020#9n;
     |FINSI;
     |LIBERA #9;

     #10#0 = #5#0;
     #10#1 = #5#1;
     #10#2 = #6#2;
     #10#5 = #0#20;
     |LEE 140#10=;
     FEstado = FSalida;
     |DDEFECTO #10;
     #10#0 = #5#0;
     #10#1 = #5#1;
     #10#2 = #6#2;
     #10#5 = #0#20;
     |SI #10#2 = 102; |Y #nomconca#2 ! 0;
          #10#3 = "+";
          #10#4 = 0;
     |SINO;
          |SI #0#10 < #0#8;
               #10#3 = "-";
               #10#4 = #0#9 * (-1);
          |SINO;
               #10#3 = "+";
               #10#4 = #0#9;
          |FINSI;
     |FINSI;
     |SI FEstado ! 0;  |GRABA 020#10n;  |FINSI;
     |SI FEstado = 0;  |GRABA 020#10a;  |FINSI;
     |LIBERA #10;
|FPRC;

|DEFBUCLE gra_atc_cat;
     #5#1;
     ;
     #nomantig#0, 001;
     #nomantig#0, 999;
     ;
     NULL, gra_atc_cat;
|FIN;

|PROCESO bucle_gra_atc;
     |BUCLE gra_atc_cat;
|FPRC;

|PROCESO activasino;
     swActiva = 1;
     |ABRE #13;
     #13#0 = #11#0;
     |LEE 000#13=;
     |SI #13#50 = "S";
          swActiva = 0;  || empresa no activa
     |FINSI;
     |CIERRA #13;
|FPRC;
