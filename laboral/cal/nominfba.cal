|FICHEROS;
     nominfba #0;
     nomtmpba;
     nomtmpbb;
     nomtmpbc;

     labempre;
     labtraba;
     labcentr;
     nomcenes;

     nomcalca;
     nomhicca;
     labere23;

     nomcot02;
     nomcot03;
|FIN;

|VARIABLES;
     nRegistros = 0;
     nEmp = 0;
     nTra = 0;
     aAlfa = "";
     &nAny = 0;
     aAny = "";
     nAnyXX = 0;
     &nMes = 0;
     aMes = "";
     &nTopemes = 0;
     nTipo = 0;
     aOrigen = "";
     nBase = 0;
     nDias = 0;
     nMeses = 0;

     nDiasMax = 180;
     nNume = 0;
     nDiasInforme = 0;

     aFechaAlta = "";
     aFechaBaja = "";
     fFechaAlta = @;
     fFechaBaja = @;
     aMesAlta = "";
     aAnyAlta = "";
     nMesAlta = 0;
     nAnyAlta = 0;

     nAntEmp = 0;
     nAntTra = 0;
     aAntDNI = "";
     nFechaAlta = 0;
     fFechaMenosUno = @;
     fAntBaja = @;
     nAnyRef = 0;
     nMesRef = 0;
     swAlta = 0;
     swDesdeFuera = 0;
     &enEmpresa = 0;
     &enNroERE = 0;
     &enAny = 0;
     &enMes = 0;
     aParam = "";
     fFecha = @;
     swTemporal = 0;
     nDesdeEmp = 0;
     nHastaEmp = 0;
     nDesdeTra = 0;
     nHastaTra = 0;
     swERETP = 0;
     nCampo = 0;
     swSigue = 0;
|FIN;

|PROCESO imprime;
     |SI nRegistros = 0;
          Impresora = "CrystalReport";
          |IMPRE -1;
          |INFOR "nominfba";
     |FINSI;
     |PRINT;
     nRegistros = nRegistros + 1;
|FPRC;

|DEFBUCLE nomtmpba;
     #nomtmpba#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, imprime;
|FIN;

|PROCESO Impresion;
     |BUCLE nomtmpba;
|FPRC;

|PROCESO BorraNoCo;
     |BORRA 020#nomtmpba.a;
     |LIBERA #nomtmpba;
|FPRC;

|DEFBUCLE BorraNoCo;
     #nomtmpba#1;
     ;
     nAntEmp, nAntTra, 0000, 00,  0, " ";
     nAntEmp, nAntTra, 2099, 12, 99, "z";
     be;
     NULL, BorraNoCo;
|FIN;

|PROCESO nomtmpba_clave2;
     |SI aAntDNI = #nomtmpba#5;
     |Y nAntTra ! #nomtmpba#1;
          fFechaAlta = #nomtmpba#15;
          nFechaAlta = fFechaAlta;
          nFechaAlta = nFechaAlta - 1;
          fFechaMenosUno = nFechaAlta;

          || altas y bajas correlativas siempre para el mismo NIF
          || si no es asi, borro el codigo que no lo sea

          |SI fAntBaja ! fFechaMenosUno;
               |SALVA_FICHA #nomtmpba;
               |SELECT #1#nomtmpba;
               |BUCLE BorraNoCo;
               |SELECT #2#nomtmpba;
               |REPON_FICHA #nomtmpba;
          |FINSI;
     |FINSI;

     nAntEmp = #nomtmpba#0;
     nAntTra = #nomtmpba#1;

     aAntDNI = #nomtmpba#5;
     fAntBaja = #nomtmpba#16;
|FPRC;

|DEFBUCLE nomtmpba_clave2;
     #nomtmpba#2;
     ;
     INICIO;
     FINAL;
     ;
     NULL, nomtmpba_clave2;
|FIN;

|PROCESO NoSeguidos;
     |BUCLE nomtmpba_clave2;
|FPRC;

|PROCESO FechasAltaBaja;
     || fechas de alta y baja, solo las grabo si son durante el mes

     |SI aOrigen = "M";
          aFechaAlta = #nomcalca#4;
          aFechaBaja = #nomcalca#5;
     |FINSI;
     |SI aOrigen = "H";
          aFechaAlta = #nomhicca#4;
          aFechaBaja = #nomhicca#5;
     |FINSI;

     |SI aFechaAlta = ".........."; aFechaAlta = ""; |FINSI;
     |SI aFechaBaja = ".........."; aFechaBaja = ""; |FINSI;

     |QBF aFechaAlta;
     aMesAlta = aFechaAlta % 402;
     aAnyAlta = aFechaAlta % -104;
     nMesAlta = aMesAlta;
     nAnyAlta = aAnyAlta;

     |SI nMesAlta = nMes; |Y nAnyAlta = nAny;
          fFechaAlta = aFechaAlta;
     |FINSI;

     |QBF aFechaBaja;
     |SI aFechaBaja ! "";
          fFechaBaja = aFechaBaja;
     |FINSI;

     aMes = nMes; |QBF aMes; aMes = ("00" + aMes) % -102;
     aAny = nAny;

     |SI fFechaAlta = "01.01.0000";
          aFechaAlta = "01." + aMes + "." + aAny;
          fFechaAlta = aFechaAlta;
     |FINSI;
     |SI fFechaBaja = "01.01.0000";
          |HAZ topemes_plb;
          aAlfa = nTopemes;
          aFechaBaja = aAlfa + "." + aMes + "." + aAny;
          fFechaBaja = aFechaBaja;
     |FINSI;
|FPRC;

|PROCESO DatosTrab;
     aFechaAlta = "";
     fFechaAlta = "01.01.0000";
     aFechaBaja = "";
     fFechaBaja = "01.01.0000";

     #labempre#0 = nEmp;
     |LEE 000#labempre.=;

     #labtraba#0 = nEmp;
     #labtraba#1 = nTra;
     |LEE 000#labtraba.=;

     #labcentr#0 = nEmp;
     #labcentr#1 = #labtraba#77;
     |LEE 000#labcentr.=;

     |SI #labtraba#45 = 421; |O #labtraba#45 = 422;
          #nomcenes#0 = nEmp;
          #nomcenes#13 = 87;
          #nomcenes#1 = #labtraba#77;
          |LEE 000#nomcenes.=;
          |SI FSalida = 0;
               aAlfa = #nomcenes#10;
               |QBF aAlfa;
               |SI aAlfa ! "";
                    #labcentr#10 = #nomcenes#10;
               |FINSI;

               aAlfa = #nomcenes#2;
               |QBF aAlfa;
               |SI aAlfa ! "";
                    #labcentr#2 = #nomcenes#2;
               |FINSI;
          |FINSI;
     |FINSI;

     aAlfa = #labcentr#10;
     |QBF aAlfa;
     |SI aAlfa = "";
          #labcentr#10 = #labempre#13;
     |FINSI;

     |HAZ FechasAltaBaja;

|FPRC;

|PROCESO GrabaRegistro;
     |DDEFECTO #nomtmpba;
     |DDEFECTO #nomtmpbb;

     || campos clave
     #nomtmpba#0 = nEmp;
     #nomtmpba#1 = nTra;
     #nomtmpba#2 = nAny;
     #nomtmpba#3 = nMes;
     #nomtmpba#4 = nTipo;
     #nomtmpba#14 = aOrigen;

     || datos trabajador
     #nomtmpba#5 = #labtraba#7;         || DNI
     #nomtmpba#6 = #labtraba#77;
     #nomtmpba#7 = #labcentr#10;
     #nomtmpba#8 = #labtraba#75;
     #nomtmpba#9 = #labempre#2;
     #nomtmpba#10 = #labtraba#2;
     #nomtmpba#11 = #labcentr#2;

     |SI aFechaAlta !  "";
          #nomtmpba#15 = fFechaAlta;
     |FINSI;
     |SI aFechaBaja !  "";
          #nomtmpba#16 = fFechaBaja;
     |FINSI;

     || total dias informe
     #nomtmpbb#0 = #labtraba#7;
     |LEE 101#nomtmpbb.=;
     |SI FSalida = 0;
          |SI #nomtmpbb#1 < nDiasMax;
               nNume = #nomtmpbb#1 + nDias;
               |SI nNume > nDiasMax;
                    nDiasInforme = nDiasMax - #nomtmpbb#1;
                    nBase = ((nBase / nDias) ! 2) * nDiasInforme;
                    nDias = nDiasInforme;
               |FINSI;
          |SINO;
               nDias = 0;
          |FINSI;
          |SI nDias ! 0;
               #nomtmpbb#1 = #nomtmpbb#1 + nDias;
               |GRABA 020#nomtmpbb.a;
               |LIBERA #nomtmpbb;
          |FINSI;
     |SINO;
          #nomtmpbb#1 = nDias;
          |GRABA 020#nomtmpbb.n;
     |FINSI;

     |SI nDias ! 0;
          || datos nomina
          #nomtmpba#12 = nBase;
          #nomtmpba#13 = nDias;

          |GRABA 020#nomtmpba.n;
          |LIBERA #nomtmpba;
     |FINSI;
|FPRC;

|PROCESO EnAlta;
     swAlta = 1;
|FPRC;

|DEFBUCLE EnAlta;
     #nomtmpba#3;
     ;
     #labtraba#7, nAnyRef, nMesRef;
     #labtraba#7, nAnyRef, nMesRef;
     ;
     NULL, EnAlta;
|FIN;

|PROCESO TemporalOrden;
     |DDEFECTO #nomtmpbc;
     #nomtmpbc#0 = #labtraba#0;
     #nomtmpbc#1 = #labtraba#7;
     aAlfa = #labtraba#36;
     fFecha = aAlfa;
     #nomtmpbc#2 = fFecha;
     #nomtmpbc#3 = #labtraba#1;
     |LEE 101#nomtmpbc.=;
     |SI FSalida ! 0;
          |GRABA 020#nomtmpbc.n;
     |FINSI;
     |LIBERA #nomtmpbc;
|FPRC;

|PROCESO nomcot02;
     |SI #nomcot02#27 ! "ETP";
          |PARA nCampo = 0; |SI nCampo [ 7; |HACIENDO nCampo = nCampo + 1;
               #nomcot03#nCampo# = #nomcot02#nCampo#;
          |SIGUE;
          #nomcot03#10# = 500;
          |LEE 000#nomcot03.=;
          |SI FSalida = 0;
               nBase = nBase + #nomcot03#12;
               nDias = nDias + #nomcot02#22;
          |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE nomcot02;
     #nomcot02#1;
     ;
     nEmp, nAny, nMes, 0, "               ", "            ", 01, nTra;
     nEmp, nAny, nMes, 0, "zzzzzzzzzzzzzzz", "zzzzzzzzzzzz", 99, nTra;
     ;
     NULL, nomcot02;
|FIN;

|PROCESO MesERETP;
     nBase = 0;
     nDias = 0;
     swERETP = 0;

     |SI aOrigen = "M";
          swSigue = 0;
          |SI #nomcalca#27 = "D"; |Y #nomcalca#119 ! 100; swSigue = 1; |FINSI;
          |SI #nomcalca#28 = "D"; |Y #nomcalca#120 ! 100; swSigue = 1; |FINSI;
          |SI #nomcalca#29 = "D"; |Y #nomcalca#121 ! 100; swSigue = 1; |FINSI;
          |SI #nomcalca#30 = "D"; |Y #nomcalca#122 ! 100; swSigue = 1; |FINSI;

          |SI swSigue = 1;
               |ABRE #nomcot03;
               |BUCLE nomcot02;
               |CIERRA #nomcot03;

               #nomcalca#8 = nDias;
               #nomcalca#40 = nBase;
          |FINSI;
     |FINSI;
     |SI aOrigen = "H";
          swSigue = 0;
          |SI #nomhicca#27 = "D"; |Y #nomhicca#119 ! 100; swSigue = 1; |FINSI;
          |SI #nomhicca#28 = "D"; |Y #nomhicca#120 ! 100; swSigue = 1; |FINSI;
          |SI #nomhicca#29 = "D"; |Y #nomhicca#121 ! 100; swSigue = 1; |FINSI;
          |SI #nomhicca#30 = "D"; |Y #nomhicca#122 ! 100; swSigue = 1; |FINSI;

          |SI swSigue = 1;
               |ABRE #nomcot03;
               |BUCLE nomcot02;
               |CIERRA #nomcot03;

               #nomhicca#8 = nDias;
               #nomhicca#40 = nBase;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO nomcalca;
     |SI #nomcalca#184 ! nAny;
          |ACABA;
     |FINSI;

     nEmp = #nomcalca#0;
     nTra = #nomcalca#1;
     aOrigen = "M";
     nTipo = #nomcalca#3;

     |HAZ DatosTrab;

     |SI swTemporal = 1;
          |HAZ TemporalOrden;
          |ACABA;
     |FINSI;

     || si en el primer mes que pido no hay ninguna nomina calculada
     || del trabajador asumo que NO esta en alta y no sigo buscando
     || en el resto de meses

     |SI nMes ! nMesRef; |O nAny ! nAnyRef;
          swAlta = 0;
          |BUCLE EnAlta;
          |SI swAlta = 0;
               |ACABA;
          |FINSI;
     |FINSI;

     |SI #labtraba#42 = "H";
          |ACABA;
     |FINSI;

     || para sacar la base, adaptado del Certificado de Empresa

     |HAZ MesERETP;

     nBase = #nomcalca#40;         || base desempleo

     |SI #labtraba#42 ! "X"; |Y #labtraba#42 ! "T";
     |Y #labtraba#42 ! "I"; |Y #labtraba#42 ! "P";
     |Y swERETP = 0;     || en ERETP me traigo la base de comunes
          nBase = nBase - #nomcalca#41 - #nomcalca#42;     || menos horas extras
     |FINSI;

     nBase = nBase + #nomcalca#117;       || Mat/Pat
     nBase = nBase + #nomcalca#130;       || Riesgo
     nBase = nBase + #nomcalca#132;       || Control IT SS

     |SI #nomcalca#31 ! 0;
     |Y #nomcalca#132 = 0;    || si esta en el 132, ya lo he sumado
          |SI #labtraba#241 = "N"; |Y #labtraba#138 = "N";
               nBase = nBase + #nomcalca#80;
          |FINSI;
     |FINSI;
     |SI #labtraba#241 = "N";
     |Y #nomcalca#132 = 0; || porque si hay algo aqui en Ctrol. IT y ademas
                         || tiene prestaciones "N" lo sumaria dos veces
          nBase = nBase + #nomcalca#62;     || base desempleo
     |FINSI;

     nDias = #nomcalca#8;
     |SI #labtraba#42 = "E";
          nDias = #nomcalca#11 + #nomcalca#55;
     |FINSI;

     |HAZ GrabaRegistro;
|FPRC;

|PROCESO nomhicca;
     nEmp = #nomhicca#0;
     nTra = #nomhicca#1;
     aOrigen = "H";
     nTipo = #nomhicca#3;

     |HAZ DatosTrab;

     |SI swTemporal = 1;
          |HAZ TemporalOrden;
          |ACABA;
     |FINSI;

     |SI #labtraba#42 = "H";
          |ACABA;
     |FINSI;

     || si en el primer mes que pido no hay ninguna nomina calculada
     || del trabajador asumo que NO esta en alta y no sigo buscando
     || en el resto de meses

     |SI nMes ! nMesRef; |O nAny ! nAnyRef;
          swAlta = 0;
          |BUCLE EnAlta;
          |SI swAlta = 0;
               |ACABA;
          |FINSI;
     |FINSI;


     || para sacar la base, adaptado del Certificado de Empresa

     |HAZ MesERETP;
     nBase = #nomhicca#40;         || base desempleo

     |SI #labtraba#42 ! "X"; |Y #labtraba#42 ! "T";
     |Y #labtraba#42 ! "I"; |Y #labtraba#42 ! "P";
     |Y swERETP = 0;     || en ERETP me traigo la base de comunes
          nBase = nBase - #nomhicca#41 - #nomhicca#42;     || menos horas extras
     |FINSI;

     nBase = nBase + #nomhicca#159;       || Mat/Pat
     nBase = nBase + #nomhicca#172;       || Riesgo
     nBase = nBase + #nomhicca#174;       || Control IT SS

     || Control IT SS
     |SI #nomhicca#31 ! 0;
     |Y #nomhicca#174 = 0;    || si esta en el 174, ya lo he sumado
          |SI #labtraba#241 = "N"; |Y #labtraba#138 = "N";
               nBase = nBase + #nomhicca#122;
          |FINSI;
     |FINSI;

     |SI #labtraba#241 = "N";
     |Y #nomhicca#174 = 0;    || si esta en el 174, ya lo he sumado
                         || tiene prestaciones "N" lo sumaria dos veces
          nBase = nBase + #nomhicca#62;     || base desempleo
     |FINSI;

     nDias = #nomhicca#8;
     |SI #labtraba#42 = "E";
          nDias = #nomhicca#11 + #nomhicca#55;
     |FINSI;

     |HAZ GrabaRegistro;
|FPRC;

|DEFBUCLE nomhicca;
     #nomhicca#1;
     ;
     nDesdeEmp, nDesdeTra, nAnyXX, nMes, 0;
     nHastaEmp, nHastaTra, nAnyXX, nMes, 0;
     ;
     NULL, nomhicca;
|FIN;

|DEFBUCLE nomcalca;
     #nomcalca#1;
     ;
     nDesdeEmp, nDesdeTra, nMes, 0;
     nHastaEmp, nHastaTra, nMes, 0;
     ;
     NULL, nomcalca;
|FIN;

|PROCESO nomtmpbc;
     nMesRef = #0#5;
     nAnyRef = #0#4;
     nMes = #0#5 + 1;
     nAny = #0#4;

     |PARA nMeses = 1; |SI nMeses [ 7; |HACIENDO nMeses = nMeses + 1;
          nMes = nMes - 1;
          |SI nMes = 0;
               nAny = nAny - 1;
               nMes = 12;
          |FINSI;
          nAnyXX = nAny - 2000;

          nDesdeEmp = #nomtmpbc#0;
          nHastaEmp = #nomtmpbc#0;
          nDesdeTra = #nomtmpbc#3;
          nHastaTra = #nomtmpbc#3;

          |BUCLE nomcalca;
          |BUCLE nomhicca;
     |SIGUE;
|FPRC;

|DEFBUCLE nomtmpbc;
     #nomtmpbc#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, nomtmpbc;
|FIN;

|PROCESO Bases;
     nMesRef = #0#5;
     nAnyRef = #0#4;
     nMes = #0#5 + 1;
     nAny = #0#4;

     || en primer lugar hago un temporal previo, el nomtmpbc, en el que se
     || guardan los trabajadores ordenados por NIF y fecha de alta con la clave
     || en orden DESCENDENTE, ya que de otra manera, si en el ultimo mes que se
     || lee hay dos fichas, va a coger los dias que queden de la ficha primera
     || y eso seria incorrecto 1777.2254

     || el bucle despues se hara sobre ese temporal

     swTemporal = 1;
     |PARA nMeses = 1; |SI nMeses [ 7; |HACIENDO nMeses = nMeses + 1;
          nMes = nMes - 1;
          |SI nMes = 0;
               nAny = nAny - 1;
               nMes = 12;
          |FINSI;
          nAnyXX = nAny - 2000;

          nDesdeEmp = #0#0;
          nHastaEmp = #0#1;
          nDesdeTra = #0#2;
          nHastaTra = #0#3;

          |BUCLE nomcalca;
          |BUCLE nomhicca;
     |SIGUE;
     ||CONSULTA_CLAVE #1#nomtmpbc;

     || ahora ya hago leo este bucle, que lo hace por
     || empres, DNI, FECHA DE ALTA DESCENDENTE, trabajador
     || para grabar las bases

     swTemporal = 0;
     |BUCLE nomtmpbc;

     |HAZ NoSeguidos;
|FPRC;

|PROCESO CalculoBases;
     |ABRE #labempre;
     |ABRE #labtraba;
     |ABRE #labcentr;
     |ABRE #nomcenes;
     |ABRE #nomcalca;
     |ABRE #nomhicca;

     aAlfa = Usuario; |QBT aAlfa; aAlfa = "tm_nomtmpba_" + aAlfa;
     |NOME_DAT #nomtmpba#aAlfa#;
     |DELINDEX #nomtmpba;
     |ABRE #nomtmpba;

     aAlfa = Usuario; |QBT aAlfa; aAlfa = "tm_nomtmpbb_" + aAlfa;
     |NOME_DAT #nomtmpbb#aAlfa#;
     |DELINDEX #nomtmpbb;
     |ABRE #nomtmpbb;

     aAlfa = Usuario; |QBT aAlfa; aAlfa = "tm_nomtmpbc_" + aAlfa;
     |NOME_DAT #nomtmpbc#aAlfa#;
     |DELINDEX #nomtmpbc;
     |ABRE #nomtmpbc;

     |HAZ Bases;

     ||CONSULTA_CLAVE #1#nomtmpba;

     |SI swDesdeFuera = 0;
          |HAZ Impresion;

          |SI nRegistros ! 0;
               |PIEINF;
               |FININF;
               |FINIMP;
          |SINO;
               |MENAV "    No se han encontrado datos para la selección realizada.";
          |FINSI;
     |FINSI;

     |CIERRA #nomhicca;
     |CIERRA #nomcalca;
     |CIERRA #nomcenes;
     |CIERRA #labcentr;
     |CIERRA #labtraba;
     |CIERRA #labempre;

     |SI swDesdeFuera = 0;
          |CIERRA #nomtmpba;
          |DELINDEX #nomtmpba;
          |CIERRA #nomtmpbb;
          |DELINDEX #nomtmpbb;
          |CIERRA #nomtmpbc;
          |DELINDEX #nomtmpbc;
     |FINSI;
|FPRC;

|PROCESO Normal;
     |CLS;
     |PINPA #0#0;

     |ABRE #0;
     |DDEFECTO #0;
     |LEE 000#0p;
     |SI FSalida = 0;
          |BORRA 020#0a;
     |FINSI;

     |PINDA #0#0;

     |ENDATOS #1#0;
     |SI FSalida = 0;
          |HAZ CalculoBases;
     |FINSI;

     |GRABA 000#0n;
     |CIERRA #0;
|FPRC;

|| PROCESOS PARA CUANDO SE LLAMA DESDE EL MANTENIMIENTO DE ERES COVID-19

|PROCESO base_tmp;
     nBase = nBase + #nomtmpba#12;
     nDias = nDias + #nomtmpba#13;
|FPRC;

|DEFBUCLE base_tmp;
     #nomtmpba#3;
     ;
     #labere23#6, 0000, 00;
     #labere23#6, 2099, 12;
     ;
     NULL, base_tmp;
|FIN;

|PROCESO labere23;
     nBase = 0;
     nDias = 0;
     |BUCLE base_tmp;
     #labere23#18 = (nBase / nDias) ! 2;
     |GRABA 020#labere23.a;
     |LIBERA #labere23;
|FPRC;

|DEFBUCLE labere23;
     #labere23#1;
     ;
     enEmpresa, enNroERE, 00001;
     enEmpresa, enNroERE, 99999;
     be;
     NULL, labere23;
|FIN;

|PROGRAMA;
     swDesdeFuera = 0;
     aParam = PARAMETRO;
     |QBF aParam;
     |SI aParam = "";
          |HAZ Normal;
     |SINO;
          swDesdeFuera = 1;
          #0#0 = enEmpresa;
          #0#1 = enEmpresa;
          #0#2 = 00001;
          #0#3 = 99999;
          #0#4 = enAny;
          #0#5 = enMes;
          |HAZ CalculoBases;

          || ahora grabo por DNI el promedio
          |BUCLE labere23;

          |CIERRA #nomtmpba;
          |DELINDEX #nomtmpba;
          |CIERRA #nomtmpbb;
          |DELINDEX #nomtmpbb;
          |CIERRA #nomtmpbc
          |DELINDEX #nomtmpbc
     |FINSI;
|FPRO;
