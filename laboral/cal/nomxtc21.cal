|FICHEROS;
    nomxtc21 #0;    || emision TC2/1
    labempre #1;    || empresas
    labemphi #2;    || aux. empresas
    labtraba #5;    || trabajadores
    nomtrt22 #4;    || campos de impresion horas
    nomhicca #6;    || hist. calculos cabs. atrasos
    nomtrt21 #7;    || campos de impresion normal
    nombonif #8;    || bonificaciones
    nomepigr #9;    || epigrafes
    labcentr #10;   || centros de trabajo
    nomconst #11;   || constantes
    nomepitr #12;   || epigrafes/trabajador
    nomcenes #13;   || centros especiales
    nomcones #15;   || constantes especiales
    nomcontr #36;   || control aplicacion
|FIN;

|VARIABLES;
    para1 = 0;
    para2 = 0;
    para3 = 0;
    para4 = 0;
    FEstado = 0;
    botmes = 0;
    sgdonum = 0;
    topmes = 0;
    haentrat = 0;
    antempre = 0;
    antcentr = 0;
    antbonif = 0;
    antporce = 0;
    anttipo  = 0;
    sibonif = 0;
    endo = 0;
    mes = 0;
    estado = 0;
    informe1 = "nomtc21p";
    informe2 = "nomtc21s";
    mensaje1 = "    ATENCION !!! Error de Entrada ...";
    postal1 = "";   || Codigo postal1 alfa
    postal2 = "";   || Codigo postal2 alfa
    domi    = "";   || Variable para quitar espacios al domicilio
    afili   = "";   || Variable para quitar espacios al nro. de afiliacion
    sgdoalf = "";   || Variable para quitar espacios al nro. de afiliacion
    fecsys = @;
    alfa1 = "";
    alfa2 = "";
    alfa3 = "";
    alfa4 = "";
    p_mes = "";
    bot1 = "  ";         || topes para el DEFBUCLE 1
    top1 = "zz";         || del fichero temporal
    bot2 = "         ";
    top2 = "zzzzzzzzz";
    lite_mes = "";
    epi_tra = 0;
    epi_bas = 0;
    epig_ilt = 0;
    epig_ims = 0;
                        || Variables informes
    &t_bonif1 = 0;
    &t_bonif2 = 0;
    &t_porboni1 = 0;
    &t_porboni2 = 0;
    &t_cuotboni = 0;
    &t_reduccio = 0;
    &tablaepi1  = 0;
    &tablaepi2  = 0;
    &tablaepi3  = 0;
    &tablaepi4  = 0;
    &tablaepi5  = 0;
    &porcepig1  = 0;
    &porcepig2  = 0;
    &porcepig3  = 0;
    &porcepig4  = 0;
    &porcepig5  = 0;
    &basepigr1  = 0;
    &basepigr2  = 0;
    &basepigr3  = 0;
    &basepigr4  = 0;
    &basepigr5  = 0;
    &t_porcepi1 = 0;
    &t_porcepi2 = 0;
    &t_porcepi3 = 0;
    &t_porcepi4 = 0;
    &t_porcepi5 = 0;
    &t_porcepig = 0;
    &t_epigrafe = 0;
    &t_dfp      = 0;
    &t_porcdfp  = 0;
    &t_total    = 0;
    &t_cbonif   = 0;
    runnom = "nomxtc21";
    el_anyo = 0;
    sw_mater = 0;
    m_dias = 0;
    nume1 = 0;
    nume2 = 0;
    nume3 = 0;
    nume4 = 0;
    fech1 = @;
    fech2 = @;
    fech3 = @;
    fech4 = @;
    m_des = 0;
    m_has = 0;
    m_fec = "";
    m_top = "";
    f_mes = "";
    tip = 0;
    dtipo = 0;
    htipo = 0;
    dia_2 = 0;
    dia_3 = 0;
    dia_4 = 0;
    topemes = 0;
    siono_cen = "";
    formato = "";
    cuot2 = 0;
    porc1 = 0;
    porc2 = 0;
    bacc2 = 0;
    bacp2 = 0;
    bilt1 = 0;
    bilt2 = 0;
    bisi1 = 0;
    bisi2 = 0;
    &EURODB = 0;

    &nCConst = 0;
    &aCError = "";
    &fCFecha = @;
    aAlfa = "";
|FIN;

|INCLUYE i_cuadr;
|INCLUYE i_athis;

|| ***************************************************************************
|| ****** calculos de carga de fichero temporal ******************************
|| ***************************************************************************

|PROCESO Tipo88; |TIPO 88;
     |HAZ LeeEURODB;
|FPRC;

|PROCESO tmp_2;
|SI #8#11 = "S";   || si hay subvencion de horas
    |DDEFECTO #4;
    |ABRE #4;
        #4#0 = #1#0;                    || Codigo empresa
        #4#1 = #6#12;                   || Codigo Bonificacion
        #4#2 = #6#1;                    || Codigo trabajador
        #4#27 = tip;                    || tipo
    |LEE 101#4=;
        FEstado = FSalida;
    |SI #1#45 = "S";   afili = #10#10;  |FINSI;
    |SI #1#45 = "N";   afili = #01#13;  |FINSI;
    |QBF afili;
        sgdoalf = afili % 0;
        sgdonum = sgdoalf;
        sgdonum = sgdonum - 4;
        sgdonum = sgdonum + 300;        || para coger 2do. digito
        #4#3 = afili % 102;             || Primer digito S.S.
        #4#4 = afili % sgdonum;         || Segundo digito S.S.
        #4#5 = afili % -102;            || Tercer digito S.S.
        #4#6 = lite_mes;                || Mes literal
        #4#7 = #0#3;                    || A¤o
        #4#8 = #1#2;                    || Nombre empresa
    |SI #1#45 = "S";
            domi = #10#3;                    || Carga variable trabajo
        |QBF domi;                           || Quita espacios
            #4#9 = domi+" "+#10#4+" "+#10#5; || Construye domicilio
            #4#10 = #10#6;                   || Localidad
            #4#11 = #10#9;                   || Provincia
    |SINO;
            domi = #1#3;                    || Carga variable trabajo
        |QBF domi;                          || Quita espacios
            #4#9 = domi+" "+#1#4+" "+#1#5;  || Construye domicilio
            #4#10 = #1#6;                   || Localidad
            #4#11 = #1#9;                   || Provincia
    |FINSI;
        afili = #5#75;
    |QBF afili;
        sgdoalf = afili % 0;
        sgdonum = sgdoalf;
        sgdonum = sgdonum - 4;
        sgdonum = sgdonum + 300;        || para coger 2do. digito
        #4#12 = "SUBV.FORMAC. TEORICA"; || Concepto o modalidad
        #4#13 = "RD 1618/90";           || Disposicion
        #4#14 = 0;                      || Porcentaje
        #4#15 = afili % 102;            || Primer digito S.S.
        #4#16 = afili % sgdonum;        || Segundo digito S.S.
        #4#17 = afili % -102;           || Tercer digito S.S.
        #4#18 = #5#2;                   || Nombre Trabajador
        #4#19 = #5#40;                  || Fecha alta bonificacion
        #4#20 = #4#20 + #6#64;          || Cuota bonificacion 2
        #4#21 = #5#7;                   || DNI trabajador
    |SI #1#45 = "S";  #4#22 = #10#10;  |FINSI;
    |SI #1#45 = "N";  #4#22 = #01#13;  |FINSI;
        #4#23 = "";                     || INHIBIDO
        #4#24 = #0#8;                   || comentario
        #4#25 = #5#77;                  || centro de trabajo
        #4#26 = "";
        #4#28 = ~;                      || fecha impresion
    |SI #8#13 ! "   ";
            #4#31 = #8#13;              || clave TGSS (especifica)
    |SINO;
            alfa1 = #8#0;    alfa1 = ("000" + alfa1) % -103;
            #4#31 = alfa1;              || clave TGSS (codigo bonif.)
    |FINSI;
    |SI FEstado = 0;  |GRABA 020#4a;  |FINSI;
    |SI FEstado ! 0;  |GRABA 020#4n;  |FINSI;
    |LIBERA #4;
    |CIERRA #4;
|FINSI;
|FPRC;

|PROCESO tmp_12;
|DDEFECTO #7;
|ABRE #7;
    #7#29 = #1#45;                  || por centros S/N
    #7#0 = #1#0;                    || Codigo empresa
    #7#1 = #6#12 + 400;             || Codigo Bonificacion ---*---
    #7#14 = #6porc2;                || Porcentaje ---*---
    #7#2 = #6#1;                    || Codigo trabajador
    #7#27 = tip;                    || tipo
|LEE 101#7=;
    FEstado = FSalida;   || lee para acumular

|SI tip = 0;
    |SI #1#45 = "S";   afili = #10#10;  |FINSI;
    |SI #1#45 = "N";   afili = #01#13;  |FINSI;
|SINO;
    |HAZ cen_esp;
        afili = #13#10;
|FINSI;
|QBF afili;
    sgdoalf = afili % 0;
    sgdonum = sgdoalf;
    sgdonum = sgdonum - 4;
    sgdonum = sgdonum + 300;        || para coger 2do. digito
    #7#3 = afili % 102;             || Primer digito S.S.
    #7#4 = afili % sgdonum;         || Segundo digito S.S.
    #7#5 = afili % -102;            || Tercer digito S.S.
    #7#6 = lite_mes;                || Mes literal
    #7#7 = #0#3;                    || A¤o
    #7#8 = #1#2;                    || Nombre empresa
|SI #1#45 = "S";
        domi = #10#3;                    || Carga variable trabajo
    |QBF domi;                           || Quita espacios
        #7#9 = domi+" "+#10#4+" "+#10#5; || Construye domicilio
        #7#10 = #10#6;                   || Localidad
        #7#11 = #10#9;                   || Provincia
|SINO;
        domi = #1#3;                    || Carga variable trabajo
    |QBF domi;                          || Quita espacios
        #7#9 = domi+" "+#1#4+" "+#1#5;  || Construye domicilio
        #7#10 = #1#6;                   || Localidad
        #7#11 = #1#9;                   || Provincia
|FINSI;
    afili = #5#75;
|QBF afili;
    sgdoalf = afili % 0;
    sgdonum = sgdoalf;
    sgdonum = sgdonum - 4;
    sgdonum = sgdonum + 300;      || para coger 2do. digito
    #7#12 = #8#1;                 || Concepto o modalidad
    #7#13 = #8#2;                 || Disposicion
    #7#15 = afili % 102;          || Primer digito S.S.
    #7#16 = afili % sgdonum;      || Segundo digito S.S.
    #7#17 = afili % -102;         || Tercer digito S.S.
    #7#18 = #5#2;                 || Nombre Trabajador
    #7#19 = #5#40;                || Fecha alta bonificacion
    #7#20 = #7#20 + #6bacc2;      || Base CC bonificacion ---*---
    #7#21 = #7#21 + #6bacp2;      || Base CP bonificacion ---*---
    #7#22 = #5#7;                 || DNI trabajador
|SI #1#45 = "S";  #7#23 = #10#10;  |FINSI;
|SI #1#45 = "N";  #7#23 = #01#13;  |FINSI;
    #7#24 = #0#8;                 || comentario
    #7#25 = #5#77;                || centro de trabajo
    #7#26 = #7#26 + #6bilt2;      || base ILT bonificacion ---*---
    #7#28 = ~;                    || fecha impresion
|SI FEstado = 0;  |GRABA 020#7a;  |FINSI;
|SI FEstado ! 0;  |GRABA 020#7n;  |FINSI;
|LIBERA #7;
|CIERRA #7;
|FPRC;

|PROCESO tmp_11;
|DDEFECTO #7;
|ABRE #7;
    #7#29 = #1#45;                 || por centros S/N
    #7#0 = #1#0;                   || Codigo empresa
    #7#1 = #6#12;                  || Codigo Bonificacion
    #7#14 = #6porc1;               || Porcentaje
    #7#2 = #6#1;                   || Codigo trabajador
    #7#27 = tip + 10;              || tipo (desempleo)
|LEE 101#7=;
    FEstado = FSalida;   || lee para acumular

|SI tip = 0;
    |SI #1#45 = "S";   afili = #10#10;  |FINSI;
    |SI #1#45 = "N";   afili = #01#13;  |FINSI;
|SINO;
    |HAZ cen_esp;
        afili = #13#10;
|FINSI;
|QBF afili;
    sgdoalf = afili % 0;
    sgdonum = sgdoalf;
    sgdonum = sgdonum - 4;
    sgdonum = sgdonum + 300;        || para coger 2do. digito
    #7#3 = afili % 102;             || Primer digito S.S.
    #7#4 = afili % sgdonum;         || Segundo digito S.S.
    #7#5 = afili % -102;            || Tercer digito S.S.
    #7#6 = lite_mes;                || Mes literal
    #7#7 = #0#3;                    || A¤o
    #7#8 = #1#2;                    || Nombre empresa
|SI #1#45 = "S";
        domi = #10#3;                    || Carga variable trabajo
    |QBF domi;                           || Quita espacios
        #7#9 = domi+" "+#10#4+" "+#10#5; || Construye domicilio
        #7#10 = #10#6;                   || Localidad
        #7#11 = #10#9;                   || Provincia
|SINO;
        domi = #1#3;                    || Carga variable trabajo
    |QBF domi;                          || Quita espacios
        #7#9 = domi+" "+#1#4+" "+#1#5;  || Construye domicilio
        #7#10 = #1#6;                   || Localidad
        #7#11 = #1#9;                   || Provincia
|FINSI;
    afili = #5#75;
|QBF afili;
    sgdoalf = afili % 0;
    sgdonum = sgdoalf;
    sgdonum = sgdonum - 4;
    sgdonum = sgdonum + 300;      || para coger 2do. digito
    #7#12 = #8#1;                 || Concepto o modalidad
    #7#13 = #8#2;                 || Disposicion
    #7#15 = afili % 102;          || Primer digito S.S.
    #7#16 = afili % sgdonum;      || Segundo digito S.S.
    #7#17 = afili % -102;         || Tercer digito S.S.
    #7#18 = #5#2;                 || Nombre Trabajador
    #7#19 = #5#40;                || Fecha alta bonificacion
    #7#20 = #7#20 + (#6#119 * #5#242);  || Base bonificacion 1 (desempleo)
    #7#21 = #7#20;                      || Base bonificacion 2 (desempleo)
    #7#22 = #5#7;                 || DNI trabajador
|SI #1#45 = "S";  #7#23 = #10#10;  |FINSI;
|SI #1#45 = "N";  #7#23 = #01#13;  |FINSI;
    #7#24 = "E.R.Emp.Atrasos: " + #1#29 + "/" + #1#35; || nro.exp. (desempleo)
    #7#25 = #5#77;                || centro de trabajo
    #7#26 = #7#26 + #6#62;        || base ILT
    #7#28 = ~;                    || fecha impresion
|SI #8#13 ! "   ";
        #7#31 = #8#13;            || clave TGSS (especifica)
|SINO;
        alfa1 = #8#0;    alfa1 = ("000" + alfa1) % -103;
        #7#31 = alfa1;            || clave TGSS (codigo bonif.)
|FINSI;
|SI FEstado = 0;  |GRABA 020#7a;  |FINSI;
|SI FEstado ! 0;  |GRABA 020#7n;  |FINSI;
|LIBERA #7;
|CIERRA #7;
|FPRC;

|PROCESO tmp_1;
|DDEFECTO #7;
|ABRE #7;
    #7#29 = #1#45;                  || por centros S/N
    #7#0 = #1#0;                    || Codigo empresa
    #7#1 = #6#12;                   || Codigo Bonificacion
    #7#14 = #6porc1;                || Porcentaje
    #7#2 = #6#1;                    || Codigo trabajador
    #7#27 = tip;                    || tipo
|LEE 101#7=;
    FEstado = FSalida;   || lee para acumular

|SI tip = 0;
    |SI #1#45 = "S";   afili = #10#10;  |FINSI;
    |SI #1#45 = "N";   afili = #01#13;  |FINSI;
|SINO;
    |HAZ cen_esp;
        afili = #13#10;
|FINSI;
|QBF afili;
    sgdoalf = afili % 0;
    sgdonum = sgdoalf;
    sgdonum = sgdonum - 4;
    sgdonum = sgdonum + 300;        || para coger 2do. digito
    #7#3 = afili % 102;             || Primer digito S.S.
    #7#4 = afili % sgdonum;         || Segundo digito S.S.
    #7#5 = afili % -102;            || Tercer digito S.S.
    #7#6 = lite_mes;                || Mes literal
    #7#7 = #0#3;                    || A¤o
    #7#8 = #1#2;                    || Nombre empresa
|SI #1#45 = "S";
        domi = #10#3;                    || Carga variable trabajo
    |QBF domi;                           || Quita espacios
        #7#9 = domi+" "+#10#4+" "+#10#5; || Construye domicilio
        #7#10 = #10#6;                   || Localidad
        #7#11 = #10#9;                   || Provincia
|SINO;
        domi = #1#3;                    || Carga variable trabajo
    |QBF domi;                          || Quita espacios
        #7#9 = domi+" "+#1#4+" "+#1#5;  || Construye domicilio
        #7#10 = #1#6;                   || Localidad
        #7#11 = #1#9;                   || Provincia
|FINSI;
    afili = #5#75;
|QBF afili;
    sgdoalf = afili % 0;
    sgdonum = sgdoalf;
    sgdonum = sgdonum - 4;
    sgdonum = sgdonum + 300;      || para coger 2do. digito
    #7#12 = #8#1;                 || Concepto o modalidad
    #7#13 = #8#2;                 || Disposicion
    #7#15 = afili % 102;          || Primer digito S.S.
    #7#16 = afili % sgdonum;      || Segundo digito S.S.
    #7#17 = afili % -102;         || Tercer digito S.S.
    #7#18 = #5#2;                 || Nombre Trabajador
    #7#19 = #5#40;                || Fecha alta bonificacion
    #7#20 = #7#20 + #6#43;        || Base bonificacion 1 (*)
    #7#21 = #7#21 + #6#44;        || Base bonificacion 2 (*)
    #7#22 = #5#7;                 || DNI trabajador
|SI #1#45 = "S";  #7#23 = #10#10;  |FINSI;
|SI #1#45 = "N";  #7#23 = #01#13;  |FINSI;
    #7#24 = #0#8;                 || comentario
    #7#25 = #5#77;                || centro de trabajo
    #7#26 = #7#26 + #6bilt1;      || base ILT
    #7#28 = ~;                    || fecha impresion
|SI FEstado = 0;  |GRABA 020#7a;  |FINSI;
|SI FEstado ! 0;  |GRABA 020#7n;  |FINSI;
|LIBERA #7;
|CIERRA #7;

|SI #6cuot2 ! 0;    || bonificacion (segunda parte)
    |HAZ tmp_12;
|FINSI;

|SI #6#119 ! 0;|Y #8#0 < 100;|Y tip ! 6;
    |HAZ tmp_11;
|FINSI;
|FPRC;

|PROCESO cen_esp;
|ABRE #13;
|DDEFECTO #13;
    #13#0 = #5#0;        || empresa
    #13#13 = #5#77;      || centro
|SI tip = 6;   #13#1 = 87;    |FINSI;
|SI tip = 7;   #13#1 = 64;    |FINSI;
|LEE 000#13=;
|CIERRA #13;
|FPRC;

|PROCESO ini_auxil2;
    #2#11 = 0;       || Pone los acumulados de contingencias
    #2#12 = 0;       || y INEM a cero
    #2#21 = 0;       || compensacion maternidad
|SI PARAMETRO ! "AGRARIA";
        #2#17 = 0;   || codigo TC2/1
|FINSI;
    #2#20 = 0;       || importe TC1
|GRABA 020#2a;       || Graba el auxiliar de empresas
|FPRC;

|PROCESO ini_auxil;
    #2#2 = tip;
|PARA; |SI; |HACIENDO;
        #2#0 = #6#0;
        #2#1 = botmes;
    |SI #1#45 = "S";  #2#18 = #5#77;  |FINSI;
    |SI #1#45 = "N";  #2#18 =    99;  |FINSI;
        #2#16 = el_anyo;     || año
    |LEE 101#2=;             || Lee el auxiliar de empresas
    |SI FSalida = 0;
        |SI PARAMETRO = "AGRARIA";|Y #2#50 = "A"; |HAZ ini_auxil2; |FINSI;
        |SI PARAMETRO ! "AGRARIA";|Y #2#50 = "G"; |HAZ ini_auxil2; |FINSI;
    |FINSI;
    |LIBERA #2;
    |SI #2#2 = tip;
            #2#2 = #2#2 + 10;
    |SINO;
        |SAL;
    |FINSI;
|SIGUE;
|FPRC;

|PROCESO lee_centr;
|DDEFECTO #10;
|ABRE #10;
    #10#0 = #1#0;
    #10#1 = #5#77;          || lee el centro de trabajo
|LEE 000#10=;
|CIERRA #10;
|FPRC;

|PROCESO lee_traba;
|ABRE #5;
    #5#0 = #1#0;
    #5#1 = #6#1;
|LEE 000#5=;                || lee el trabajador
    FEstado = FSalida;
|CIERRA #5;
|FPRC;

|PROCESO constante;
     alfa1 = "01";
     alfa2 = #0#2;  alfa2 = "00" + alfa2;    alfa2 = alfa2 % -102;
     alfa3 = #0#3;
     alfa4 = alfa1 + "." + alfa2 + "." + alfa3;

     fCFecha = alfa4;         || primer dia de mes de calculo
     aCError = "";            || inicializo errores

     |HAZ LeeCons;

     |SI aCError ! "";
          |SI nCConst = 500;
               aAlfa = "0000No se ha encontrado la Constante Comun del ";
               aAlfa = aAlfa + " Regimen General (500)";
               |MENAV aAlfa;
          |SINO;
               alfa1 = #labtraba#0; alfa1 = "00000" + #labtraba#0; alfa1 = alfa1 % -105;
               alfa2 = #labtraba#1; alfa2 = "00000" + #labtraba#1; alfa2 = alfa2 % -105;
               aAlfa = "0000ATENCION: Trabajador: " + alfa1 + "." + alfa2;
               aAlfa = aAlfa + " La constante del trabajador no esta definida.";
               |MENAV aAlfa;
          |FINSI;
     |FINSI;
/*
|ABRE #11;
    #11#0 = #1#32;
|LEE 020#11=;                   || lee la constante
    FEstado = FSalida;
|CIERRA #11;

|DDEFECTO #15;
|ABRE #15;
    #15#0 = #1#32;
|LEE 000#15=;                   || lee la constante especial
    ||FEstado = FSalida;
|CIERRA #15;
*/
|FPRC;

|PROCESO lee_empre;
|ABRE #1;
    #1#0 = #6#0;
|LEE 000#1=;                  || lee la empresa
    FEstado = FSalida;
|CIERRA #1;

|SI #1#45 ! "S";|Y #1#45 ! "N";    #1#45 = "N";   |FINSI;
|SI #1#47 ! "1";|Y #1#47 ! "2";    #1#47 = "1";   |FINSI;
|FPRC;

|PROCESO el_tipo;
    tip = 5;                                                || resto
|SI #6#9 = 87;|O #6#9 = 97;|O #6#9 = 85;  tip = 6; |FINSI;  || aprend.
|SI #6#9 = 64;|O #6#9 = 65;               tip = 7; |FINSI;  || t.parcial
|FPRC;

|PROCESO temporal;
|HAZ lee_empre;     |SI FEstado ! 0;    |ACABA;   |FINSI;   || empresa
|HAZ lee_traba;     |SI FEstado ! 0;    |ACABA;   |FINSI;   || trabajador
nCConst = 500;
|HAZ constante;      |SI FEstado ! 0;    |ACABA;   |FINSI;   || constantes

|SI #5#45 = "087";|O #5#45 = "097";|O #5#45 = "085";
    ||SI #0#9 ! "T";|Y #0#9 ! "A";  |ACABA;   |FINSI;   || aprendices
    |ACABA;
|FINSI;
|SI #5#45 = "064";|O #5#45 = "065";
    |SI #0#9 ! "T";|Y #0#9 ! "P";  |ACABA;   |FINSI;   || t.parcial
|FINSI;
|SI #5#45 ! "087";|Y #5#45 ! "097";|Y #5#45 ! "085";
                  |Y #5#45 ! "064";|Y #5#45 ! "065";
    |SI #0#9 ! "T";|Y #0#9 ! "R";  |ACABA;   |FINSI;   || resto
|FINSI;
|SI #5#42 = "H";    |ACABA;   |FINSI;                  || autonomos
|SI #5#42 = "A";|O #5#42 = "B";|O #5#42 = "C";
    |SI PARAMETRO ! "AGRARIA";    |ACABA;   |FINSI;    || agrarios no
|FINSI;
|SI #5#42 ! "A";|Y #5#42 ! "B";|Y #5#42 ! "C";
    |SI PARAMETRO = "AGRARIA";    |ACABA;   |FINSI;    || agrarios solo
|FINSI;

|SI formato ! #1#47;     |ACABA;   |FINSI;             || separado/refundido

|HAZ la_mama;            || si tiene maternidad

|DDEFECTO #8;
    #8#0 = #6#12;
|LEE 000#8=;
|SI #8#0 ! 0; |Y FSalida = 0; |Y #6#9 ! 97;  || si existe y no es la cero
    |HAZ el_tipo;             || tipo de aux. empresas
    |HAZ lee_centr;           || lee centros
    |HAZ ini_auxil;           || inicializa aux. empresas
    |HAZ tmp_1;               || TC2/1 normal
    |HAZ tmp_2;               || TC2/1 subv. horas
|FINSI;
|SI sw_mater = 1;
    |HAZ el_tipo;
    |HAZ lee_centr;      || lee centros
    |HAZ bonif_mater;
    |HAZ calcu_mater;    || trastocan valores del nomhica
    |HAZ traba_mater;    || trastocan valores del labtraba
    |HAZ ini_auxil;
    |HAZ tmp_1;          || TC2/1 normal
|FINSI;
|FPRC;

|PROCESO la_mama;
    sw_mater = 0;
|SI #6#65 ! 0;|Y #0#3 ] 1995;      || bonificacion maternidad 1995

    |DDEFECTO #21;
    |ABRE #21;
        #21#0  = #6#0;        || empresa
        #21#1  = #6#1;        || trabajador
        #21#66 = (#0#3 $ 100);|| a¤o
        #21#2  = #6#2;        || mes
        #21#3  = 0;           || tipo
    |LEE 000#21=;
    |CIERRA #21;

        m_dias = #6#65;
    |SI #21#68 = "M";    m_dias = m_dias + #21#70; |FINSI;

        nume1 = 0;
    |SI #6#27 = "M";     nume1 = #6#23; |FINSI;
    |SI #6#28 = "M";     nume1 = #6#24; |FINSI;
    |SI #6#29 = "M";     nume1 = #6#25; |FINSI;
    |SI #6#30 = "M";     nume1 = #6#26; |FINSI;
    |SI nume1 = 0;       nume1 = #6#7;  |FINSI;

        alfa1 = nume1;       alfa1 = "00" + alfa1;    alfa1 = alfa1 % -102;
        alfa2 = #6#2;        alfa2 = "00" + alfa2;    alfa2 = alfa2 % -102;
        alfa3 = alfa1 + "." + alfa2 + "." + #0#3;
        fech1 = alfa3;            nume1 = fech1;
        fech2 = "01.01.1995";     nume2 = fech2;
        nume3 = nume1 - nume2 + 1;

    |SI m_dias [ nume3; sw_mater = 1;  |FINSI;
    |SI #6#2 ] 6;                  sw_mater = 1;  |FINSI;
|FINSI;
|FPRC;

|PROCESO bonif_mater;
|DDEFECTO #8;
|SI tip ! 7;
        #8#0  = 100;                             || codigo bonificacion
        #8#6  = #11#7;                           || linea 1 trabajador
        #8#1  = "SITUACION MATERNIDAD";          || descripcion
|SINO;
    ||SI #6#9 = 64;  #8#0 = 101;    #8#6 = #11#7 * #15#21;   |FINSI;
    ||SI #6#9 = 65;  #8#0 = 106;    #8#6 = #11#7 * #15#22;   |FINSI;
        #8#1  = "SIT.MATERNIDAD /" + #6#9;       || descripcion especial
|FINSI;
    #8#3  = 100;                             || % bonificacion
    #8#7  = #11#13+#11#15+#11#17+#11#19;     || linea 2 trabajador
    #8#8  = "N";                             || accidentes NO
    #8#10 = 3;                               || situacion TC1
|FPRC;

|PROCESO liquidados;
    bisi2 = 0;
    bisi1 = #0#3 $ 4;
|SI bisi1 = 0;|Y #36#9 = "S";
        bisi2 = 1;
|FINSI;

    topemes = 31;
|SI #6#2 = 2;
        topemes = 28 + bisi2;
|FINSI;
|SI #6#2 = 4;|O #6#2 = 6;|O #6#2 = 9;|O #6#2 = 11;
        topemes = 30;
|FINSI;

    dia_2 = topemes;     || dias que tiene el mes
    dia_3 = 30;          || dias cotizacion 'mensual'
|SI #5#42 = "D";|O #5#42 = "T";
        dia_3 = topemes; || dias cotizacion 'diario'
|FINSI;
    dia_4 = dia_3;

|SI nume1 > dia_4;  nume1 = dia_4;  |FINSI;
|SI nume1 = dia_2; |Y nume1<dia_4;  nume1 = dia_4;  |FINSI;
|FPRC;

|PROCESO calcu_mater;
    #6#12 = #8#0;             || codigo bonificacion
    nume1 = #6#65;
|HAZ liquidados;
    #6#43 = #6#35 * nume1;    || c.c.
    #6#44 = #6#36 * nume1;    || c.p.

    #6porc1 = #8#3;
|FPRC;

|PROCESO traba_mater;
|SI #6#27 = "M"; m_des = #6#19;    m_has = #6#23; |FINSI;
|SI #6#28 = "M"; m_des = #6#20;    m_has = #6#24; |FINSI;
|SI #6#29 = "M"; m_des = #6#21;    m_has = #6#25; |FINSI;
|SI #6#30 = "M"; m_des = #6#22;    m_has = #6#26; |FINSI;

|SI m_des ! 0;
        alfa1 = m_des;   alfa1 = "00" + alfa1;    alfa1 = alfa1 % -102;
        alfa2 = #6#2;    alfa2 = "00" + alfa2;    alfa2 = alfa2 % -102;
        m_fec = alfa1 + "." + alfa2 + "." + #0#3;
|SINO;
    |SI m_has ! 0;
            alfa1 = m_has;   alfa1 = "00" + alfa1;    alfa1 = alfa1 % -102;
            alfa2 = #6#2;    alfa2 = "00" + alfa2;    alfa2 = alfa2 % -102;
            m_top = alfa1 + "." + alfa2 + "." + #0#3;
    |SINO;
        |SI #6#2 =  1;    f_mes = "31.01.";   |FINSI;
        |SI #6#2 =  2;    f_mes = "28.02.";   |FINSI;
        |SI #6#2 =  3;    f_mes = "31.03.";   |FINSI;
        |SI #6#2 =  4;    f_mes = "30.04.";   |FINSI;
        |SI #6#2 =  5;    f_mes = "31.05.";   |FINSI;
        |SI #6#2 =  6;    f_mes = "30.06.";   |FINSI;
        |SI #6#2 =  7;    f_mes = "31.07.";   |FINSI;
        |SI #6#2 =  8;    f_mes = "31.08.";   |FINSI;
        |SI #6#2 =  9;    f_mes = "30.09.";   |FINSI;
        |SI #6#2 = 10;    f_mes = "31.10.";   |FINSI;
        |SI #6#2 = 11;    f_mes = "30.11.";   |FINSI;
        |SI #6#2 = 12;    f_mes = "31.12.";   |FINSI;
            m_top = f_mes + #0#3;
    |FINSI;
        fech1 = m_top;   nume1 = fech1;
        nume1 = nume1 - m_dias + 1;
        fech1 = nume1;
        m_fec = fech1;
|FINSI;

    #5#40 = m_fec;      || fecha inicio maternidad
|FPRC;

|PROCESO meslite;     || CALCULA MES/PERIODO LITERAL
|SI formato = "1";  |HAZ meslite1;   |FINSI;
|SI formato = "2";  |HAZ meslite2;   |FINSI;
|FPRC;

|PROCESO meslite1;     || CALCULA MES LITERAL
|SI botmes = 1;  lite_mes = "Enero     "; |FINSI;
|SI botmes = 2;  lite_mes = "Febrero   "; |FINSI;
|SI botmes = 3;  lite_mes = "Marzo     "; |FINSI;
|SI botmes = 4;  lite_mes = "Abril     "; |FINSI;
|SI botmes = 5;  lite_mes = "Mayo      "; |FINSI;
|SI botmes = 6;  lite_mes = "Junio     "; |FINSI;
|SI botmes = 7;  lite_mes = "Julio     "; |FINSI;
|SI botmes = 8;  lite_mes = "Agosto    "; |FINSI;
|SI botmes = 9;  lite_mes = "Septiembre"; |FINSI;
|SI botmes = 10; lite_mes = "Octubre   "; |FINSI;
|SI botmes = 11; lite_mes = "Noviembre "; |FINSI;
|SI botmes = 12; lite_mes = "Diciembre "; |FINSI;
|FPRC;

|PROCESO meslite2;     || CALCULA PERIODO LITERAL
    alfa1 = #0#2;  alfa1 = "00" + alfa1;  alfa1 = alfa1 % -102;
    alfa2 = #0#4;  alfa2 = "00" + alfa2;  alfa2 = alfa2 % -102;
    lite_mes = "De " + alfa1 + " a " + alfa2;
|FPRC;

|DEFBUCLE nomxtc210;
 #6#1;
 ;
 #0#0,     0, el_anyo, botmes, #0#10;
 #0#1, 99999, el_anyo, topmes, #0#10;
 e;
 meslite, temporal;
|FIN;

|| ***************************************************************************
|| ****** calculos de lectura e impresion ************************************
|| ***************************************************************************

|PROCESO impresion1;
    t_cbonif = t_cbonif + #4#20;
|PRINT;
|FPRC;

|DEFBUCLE nomxtc212;   || NOTA: lee segun lo seleccionado en el fichero 7
 #4#1;                 || CLAVE: empresa,bonificacion,trabajador
 ;
 antempre, antbonif,     1;
 antempre, antbonif, 99999;
 e;
 NULL, impresion1;
|FIN;

|DEFBUCLE centros;     || NOTA: lee segun lo seleccionado en el fichero 7
 #4#2;                 || CLAVE: empresa,centro,bonificacion,trabajador
 ;
 antempre, antcentr, antbonif,     1;
 antempre, antcentr, antbonif, 99999;
 e;
 NULL, impresion1;
|FIN;

|PROCESO ruptura2;
|INFOR informe1;
    antempre = #7#0;
    antcentr = #7#25;
    antbonif = #7#1;
    antporce = #7#14;
    anttipo  = #7#27 - dtipo;
|SI #7#1 > 400;
        #7#1 = #7#1 - 400;       || si es doble bonificacion
|FINSI;
|SI #7#1 [ 99;
    |DDEFECTO #8;
        #8#0 = #7#1;         || lee bonificacion
    |LEE 020#8=;
|SINO;
    |ABRE #1;  #1#0 = #7#0;   |LEE 020#1=;   |CIERRA #1;
    nCConst = 500;
    |HAZ constante;
        tip = #7#27;
    |HAZ bonif_mater;
        FSalida = 0;
|FINSI;
    sibonif = FSalida;
|FPRC;

|PROCESO ruptura1;
|HAZ pie;                  || Calculo pie de informe
|SI endo = 1;
    |PIEINF;
|FINSI;
|FININF;                   || cierra y abre un nuevo informe
|HAZ auxiliar;             || graba en el auxiliar de empresas
|HAZ segundo_tc;
|HAZ limpieza;             || inicializa variables acumuladoras
|FPRC;

|PROCESO segundo_tc;
|SI #8#11 = "S";|Y dtipo = 0;      || si hay subvenciones por horas
    |INFOR informe2;     || imprime el segundo tc2/1
    |SI siono_cen = "N";   |BUCLE nomxtc212;    |FINSI;
    |SI siono_cen = "S";   |BUCLE centros;      |FINSI;
    |PIEINF;
    |FININF;
    |HAZ auxiliar1;      || graba en el auxiliar de empresas
|FINSI;
|FPRC;

|PROCESO impresion;
    haentrat = 1;
|SI antempre = -1;  |HAZ ruptura2;  |FINSI;

|SI antempre ! #7#0;                    || RUPTURA POR EMPRESA
    |HAZ ruptura1;
    |HAZ ruptura2;
|SINO;
    |SI antcentr ! #7#25;|Y siono_cen = "S"; || RUPTURA POR CENTROS
        |HAZ ruptura1;
        |HAZ ruptura2;
    |SINO;
        |SI antbonif ! #7#1;            || RUPTURA POR BONIFICACION
            |HAZ ruptura1;
            |HAZ ruptura2;
        |SINO;
            |SI antporce ! #7#14;
                |HAZ ruptura1;
                |HAZ ruptura2;
            |FINSI;
        |FINSI;
    |FINSI;
|FINSI;

    t_bonif1 = t_bonif1 + #7#20;   || Acumulado contingencias comunes
    t_bonif2 = t_bonif2 + #7#21;   || Acumulado A.T y E.P

|SI #7#20 = 0;|Y #7#21 = 0;
        sibonif = 111;
|SINO;
        sibonif = 0;
|FINSI;

|SI #8#8 = "S";                || Si la bonificacion tiene accidentes
                          || ******* Tablas de epigrafes **********
    |ABRE #5;
    |ABRE #9;
    |ABRE #12;
        #5#0 = #7#0;
        #5#1 = #7#2;
    |LEE 000#5=;
    |SI FSalida = 0;
            epi_tra = #5#32;
            epi_bas = #7#21 - #7#26;
        |HAZ calc_epig;
        |SI #7#26 ! 0;        || si tiene base por ILT genera epig.enfer.
                epi_tra = #11#2;
                epi_bas = #7#26;
            |HAZ calc_epig;
        |FINSI;
    |FINSI;
    |CIERRA #5;
    |CIERRA #9;
    |CIERRA #12;
|FINSI;

|SI sibonif = 0; |Y #8#0 ! 0; |Y #7#14 ! 0;
        endo = 1;
    |SI #8#0 > 99; #8#3 =   0; #7#14 =   0; #7#31 = "";   |FINSI; || no en maternidad
    |PRINT;
    |SI #8#0 > 99; #8#3 = 100; #7#14 = 100; #7#31 = #8#0; |FINSI;
|FINSI;
|FPRC;

|PROCESO calc_epig;
|SI epi_tra ! #11#2;
    |DDEFECTO #9;
        #9#0 = epi_tra;
    |LEE 020#9=;
        epig_ilt = #9#1;           || porcentajes del epigrafe normal
        epig_ims = #9#2;
|SINO;
        epig_ilt = #11#3;          || porcentajes del epig.enf.
        epig_ims = #11#4;
|FINSI;

    #12#0 = #5#0;
    #12#1 = #5#1;
    #12#2 = epi_tra;
|LEE 000#12=;
|SI FSalida = 0;
        epig_ilt = #12#3;          || porcentajes del epigrafe/trabajador
        epig_ims = #12#4;
|FINSI;

    |SI tablaepi1  = 0; |O tablaepi1 = epi_tra;
            tablaepi1  = epi_tra;
            porcepig1  = epig_ilt + epig_ims;
            basepigr1  = basepigr1 + epi_bas;
    |SINO;
        |SI tablaepi2 = 0; |O tablaepi2 = epi_tra;
                tablaepi2 = epi_tra;
                porcepig2 = epig_ilt + epig_ims;
                basepigr2 = basepigr2 + epi_bas;
        |SINO;
            |SI tablaepi3 = 0; |O tablaepi3 = epi_tra;
                    tablaepi3 = epi_tra;
                    porcepig3 = epig_ilt + epig_ims;
                    basepigr3 = basepigr3 + epi_bas;
            |SINO;
                |SI tablaepi4 = 0; |O tablaepi4 = epi_tra;
                        tablaepi4 = epi_tra;
                        porcepig4 = epig_ilt + epig_ims;
                        basepigr4 = basepigr4 + epi_bas;
                |SINO;
                        tablaepi5 = epi_tra;
                        porcepig5  = epig_ilt + epig_ims;
                        basepigr5  = basepigr5 + epi_bas;
                |FINSI;
            |FINSI;
        |FINSI;
    |FINSI;
|FPRC;

|DEFBUCLE nomxtc211;   || NOTA: lee todo el fichero
 #7#1;                 || CLAVE: empresa,bonificacion,%,trabajador
 ;
 "N",     0,   0,    0,     1, dtipo;
 "N", 99999, 999, 9999, 99999, htipo;
 ;
 NULL, impresion;
|FIN;

|DEFBUCLE por_centros; || NOTA: lee todo el fichero
 #7#2;                 || CLAVE: empresa,centro,bonificacion,%,trabajador
 ;
 "S",     0,   0,   0,    0,     1, dtipo;
 "S", 99999, 999, 999, 9999, 99999, htipo;
 ;
 NULL, impresion;
|FIN;

|| ***************************************************************************
|| ****** calculos desde el mantenimiento ************************************
|| ***************************************************************************

|PROCESO control;
|DDEFECTO #36;
|ABRE #36;
|LEE 000#36p;
|SI FSalida ! 0;
    |MENAV "    No existe control, usando defectos ...";
|FINSI;
|CIERRA #36;
|FPRC;

|PROCESO impre; |TIPO 3;
    c_informe = "nomtc21p";
|HAZ cuadres;
|SI sw_cuadre = 0;
    |IMPRE 0;
    |SI FSalida = 0;
        |HAZ control;
            el_anyo = #0#3 $ 100;
        |HAZ el_hitmc;
        cuot2 = 123;
        porc1 = 124;
        porc2 = 125;
        bacc2 = 121;
        bacp2 = 122;
        bilt1 = 126;
        bilt2 = 127;
        formato = "1";   |HAZ impre1;   || separados
        formato = "2";   |HAZ impre2;   || refundidos
        |DELINDEX #21;
    |SINO;
        |MENAV "    Proceso ABORTADO !!!";
    |FINSI;
    |FINIMP;
|FINSI;
|FPRC;

|PROCESO carga;
    alfa1 = Usuario;  |QBT alfa1;
    alfa1 = (alfa1 + "xxxxxxx") % 107;
    alfa1 = "2" + alfa1;
|NOME_DAT #4 alfa1;
|DELINDEX #4;
    alfa1 = Usuario;  |QBT alfa1;
    alfa1 = (alfa1 + "xxxxxxx") % 107;
    alfa1 = "3" + alfa1;
|NOME_DAT #7 alfa1;
|DELINDEX #7;
|ABRE #2;      || aux. empresas
|ABRE #8;      || bonificaciones
|BUCLE nomxtc210;

    dtipo = 0; htipo = 9;
|PARA; |SI; |HACIENDO;

    |MENSA "2302Imprimiendo TC2/1 de empresas ...";
        antempre = -1;
        antcentr = -1;
        antbonif = -1;
        antporce = -1;
        haentrat = 0;   || switch para saber si lee
        siono_cen = "N";
    |BUCLE nomxtc211;
    |SI haentrat = 1;   || si ha impreso cierra el informe, totalizando
        |HAZ ruptura1;
    |FINSI;

    |MENSA "2302Imprimiendo TC2/1 de centros de trabajo ...";
        antempre = -1;
        antcentr = -1;
        antbonif = -1;
        antporce = -1;
        haentrat = 0;   || switch para saber si lee
        siono_cen = "S";
    |BUCLE por_centros;
    |SI haentrat = 1;   || si ha impreso cierra el informe, totalizando
        |HAZ ruptura1;
    |FINSI;

    |SI dtipo = 0;
            dtipo = 10;
            htipo = 19;
    |SINO;
        |SAL;
    |FINSI;
|SIGUE;
|CIERRA #2;
|CIERRA #8;
|DELINDEX #4;
|DELINDEX #7;
|FPRC;

|PROCESO impre2;    || FORMATO DE IMPRESION EN UN TC2/1
    botmes = #0#2;
    topmes = #0#4;
|HAZ carga;
|FPRC;

|PROCESO impre1;    || FORMATO DE IMPRESION EN UN TC2/1 CADA MES
|PARA mes = #0#2; |SI mes [ #0#4; |HACIENDO mes = mes + 1;
        botmes = mes;
        topmes = mes;
    |HAZ carga;
|SIGUE;
|FPRC;

|PROCESO defecto; |TIPO 7; ||calcula el periodo por defecto segun fecha sistema
    fecsys = ~;
    alfa1 = fecsys;
    alfa1 = alfa1 % -104;
    #0Campo = alfa1;
|PINTA #0Campo;
|FPRC;

|PROCESO pintames; |TIPO 0;
|SI #0Campo =  1;  p_mes = "ENERO     ";  |FINSI;
|SI #0Campo =  2;  p_mes = "FEBRERO   ";  |FINSI;
|SI #0Campo =  3;  p_mes = "MARZO     ";  |FINSI;
|SI #0Campo =  4;  p_mes = "ABRIL     ";  |FINSI;
|SI #0Campo =  5;  p_mes = "MAYO      ";  |FINSI;
|SI #0Campo =  6;  p_mes = "JUNIO     ";  |FINSI;
|SI #0Campo =  7;  p_mes = "JULIO     ";  |FINSI;
|SI #0Campo =  8;  p_mes = "AGOSTO    ";  |FINSI;
|SI #0Campo =  9;  p_mes = "SEPTIEMBRE";  |FINSI;
|SI #0Campo = 10;  p_mes = "OCTUBRE   ";  |FINSI;
|SI #0Campo = 11;  p_mes = "NOVIEMBRE ";  |FINSI;
|SI #0Campo = 12;  p_mes = "DICIEMBRE ";  |FINSI;
|SI Campo = 2;  |PINTA 1224, p_mes;  |FINSI;
|SI Campo = 4;  |PINTA 1324, p_mes;  |FINSI;
|FPRC;

|PROCESO hasmayor; |TIPO 0; || chequea que el hasta sea mayor que el desde
|SI #0Campo < #0#2;
    |MENAV mensaje1;
    |ERROR;
|FINSI;
|FPRC;

|PROCESO mirames; |TIPO 7; ||calcula el mes por defecto segun fecha sistema
    fecsys = ~;
    alfa1 = fecsys;
    alfa1 = alfa1 % 402;
    #0Campo = alfa1;
|PINTA #0Campo;
|FPRC;

|| ***************************************************************************
|| ****** calculos complementarios de trabajo en IMPRESION *******************
|| ***************************************************************************

|PROCESO limpieza;   || INICIALIZA ACUMULADORES
    t_bonif1   = 0;  t_bonif2   = 0;
    t_porboni1 = 0;  t_porboni2 = 0;
    t_cuotboni = 0;  t_reduccio = 0;
    tablaepi1  = 0;  tablaepi2  = 0;
    tablaepi3  = 0;  tablaepi4  = 0;
    tablaepi5  = 0;  porcepig1  = 0;
    porcepig2  = 0;  porcepig3  = 0;
    porcepig4  = 0;  porcepig5  = 0;
    basepigr1  = 0;  basepigr2  = 0;
    basepigr3  = 0;  basepigr4  = 0;
    basepigr5  = 0;  t_porcepi1 = 0;
    t_porcepi2 = 0;  t_porcepi3 = 0;
    t_porcepi4 = 0;  t_porcepi5 = 0;
    t_porcepig = 0;  t_epigrafe = 0;
    t_dfp      = 0;  t_porcdfp  = 0;
    t_total    = 0;  t_cbonif   = 0;
    endo = 0;
|FPRC;

|PROCESO auxiliar;  || ACTUALIZA FICHERO AUXILIAR DE EMPRESAS
    #2#00 = antempre;    || codigo empresa
    #2#01 = botmes;      || mes
    #2#02 = anttipo + dtipo;  || tipo
|SI siono_cen = "S";  #2#18 = antcentr;  |FINSI;
|SI siono_cen = "N";  #2#18 =       99;  |FINSI;
    #2#16 = el_anyo;     || a¤o
|LEE 101#2=;
    estado = FSalida;
|SI #8#10 = 1;
        #2#11 = #2#11 + t_reduccio;               || reducciones (arriba)
        #2#12 = #2#12 + t_epigrafe + t_porcdfp;   || bonificaciones (abajo)
|FINSI;
|SI #8#10 = 2;
        #2#12 = #2#12 + t_total;             || bonificaciones (abajo)
|FINSI;
|SI #8#10 = 3;
    |SI #8#0 > 99;
            #2#21 = #2#21 + t_total;         || reducciones(matern.) (arriba)
    |SINO;
            #2#11 = #2#11 + t_total;         || reducciones(aprend.) (arriba)
    |FINSI;
|FINSI;

|SI #8#0 > 99;|Y PARAMETRO ! "AGRARIA";
        #2#17 = 6;
|FINSI;

|SI estado ! 0;
    |GRABA 020#2n;
|SINO;
    |GRABA 020#2a;
|FINSI;
|LIBERA #2;
|FPRC;

|PROCESO auxiliar1;      || ACTUALIZA FICHERO AUXILIAR DE EMPRESAS
    #2#00 = antempre;    || codigo empresa
    #2#01 = botmes;      || mes
    #2#02 = 5 + dtipo;   || tipo
|SI siono_cen = "S";  #2#18 = antcentr;  |FINSI;
|SI siono_cen = "N";  #2#18 =       99;  |FINSI;
    #2#16 = el_anyo;     || a¤o
|LEE 101#2=;
    estado = FSalida;
    #2#12 = #2#12 + t_cbonif;
|SI estado ! 0;
    |GRABA 020#2n;
|SINO;
    |GRABA 020#2a;
|FINSI;
|LIBERA #2;
|FPRC;

|PROCESO pie;
    t_porboni1 = #8#4 + #8#6;            || Porcentaje contingencias
    t_porboni2 = #8#5 + #8#7;            || Porcentaje D-F-P
    t_cuotboni = (t_bonif1 % t_porboni1) ! EURODB;          || cuota conting.
    t_reduccio = (t_cuotboni % antporce) ! EURODB;              || bonif./reduccion

|SI #8#8 = "S";         || Si la bonificacion tiene accidentes
        t_porcepi1 = (basepigr1 % porcepig1) ! EURODB;
        t_porcepi2 = (basepigr2 % porcepig2) ! EURODB;
        t_porcepi3 = (basepigr3 % porcepig3) ! EURODB;
        t_porcepi4 = (basepigr4 % porcepig4) ! EURODB;
        t_porcepi5 = (basepigr5 % porcepig5) ! EURODB;
|FINSI;
                                                  || total base epigrafes
    t_porcepig = t_porcepi1 + t_porcepi2 + t_porcepi3;
    t_porcepig = t_porcepig + t_porcepi4 + t_porcepi5;
    t_epigrafe = (t_porcepig % antporce) ! EURODB;         || total % epigrafes
    t_dfp      = (t_bonif2 % t_porboni2) ! EURODB;     || base DFP
    t_porcdfp  = (t_dfp % antporce) ! EURODB;              || cuota DFP
    t_total    = t_reduccio + t_epigrafe + t_porcdfp;       || total
|FPRC;
