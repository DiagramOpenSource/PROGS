|FICHEROS;
     nompredi #0;
     labempre #1;         || empresas
     labtraba #2;         || trabajadores
     labtracu #22;        || cuentas trabajadores
     nomcalca #3;         || calculos cabs.
     nomhicca #4;         || historico cabs.
     nomcalac #5;         || atrasos cabs.
     nomgrubl #6;         || grupos bancos lins.
     nomrevca #7;         || transfer. cabs.
     nomrevli #8;         || transfer. lins.
     nomtminf #100;
|FIN;

|VARIABLES;
    alfa1 = "";
    alfa2 = "";
    alfa3 = "";
    alfa4 = "";
    nume1 = 0;
    nume2 = 0;
    nume3 = 0;
    nume4 = 0;
    para1 = 0;
    para2 = 0;
    para3 = 0;
    para4 = 0;
    fech1 = @;
    fech2 = @;
    fech3 = @;
    fech4 = @;
    elsw1 = 0;
    elsw2 = 0;
    elsw3 = 0;
    elsw4 = 0;
    FEstado = 0;
    seleccio = 0;
    modo = 0;
    sw_rup = 0;

    el_mes = 0;
    el_any = 0;
    devenga = 0;
    deducir = 0;
    &liquido = 0;

    swbaja = 0;
    esta = 0;
    fecalf = "";
    dia = 0;
    mes = 0;
    any = 0;
    confi = "";
    linea = 0;
    la_linea = 0;
    topemes = 0;
    bisiesto = 0;

     &nEmpPag = 0;
     &nAnyPag = 0;
     &nMesPag = 0;
     &nTipPag = 0;
     &aNorPag = "";
     &nTraPag = 0;
     &fFecPag = @;
     &aRegPag = "";
     &nLiqEnlace = 0;
     &swStop = 0;

     nBanco      = 0;
     nTipoCuenta = 0;
     nEmbargo    = 0;
     aCuenta     = "";

     aAlfa = "";
     aAlfa1 = "";
     aAlfa2 = "";
     el_tipo = 0;
     nAntEmp = 0;
     nAntReg = 0;
     nAntOrd = 0;
     nAntTra = 0;
     swMeSalgo = 0;

     nCaracter = 0;
     aCaracter = "";
     nLongitud = 0;
     nNume = 0;
     nCampo = 0;
     swError1 = 0;
     swError2 = 0;
     aValor = "";
     aIBAN = "";
     aBIC = "";
     nCuantos = 0;
|FIN;
____________________________________/
|PROCESO mayus; |TIPO 0;
    #0Campo = #0Campo > #0Campo;
|PINTA #0Campo;
|FPRC;

|PROCESO defecto; |TIPO 7; ||calcula el a¤o por defecto segun fecha sistema
    fech1 = ~;
    alfa1 = fech1;
    alfa1 = alfa1 % -104;
    #0Campo = alfa1;
|PINTA #0Campo;
|FPRC;

|PROCESO mirames; |TIPO 7;
    fech1 = ~;
    alfa1 = fech1;
    alfa1 = alfa1 % 402;
    #0Campo = alfa1;
|PINTA #0Campo;
|FPRC;

|PROCESO no_entres; |TIPO 7;
|SI #0#4 = "M";|O #0#4 = "H";
    |CAMPO_MODIFICA #0#8, 1, "S";
|SINO;
        #0#8 = "M"; |PINTA #0#8;
    |CAMPO_MODIFICA #0#8, 1, "N";
|FINSI;
|FPRC;

|PROCESO sel_banco; |TIPO 0;
|SI #0#12 = "G";
    |CAMPO_MODIFICA #0#10, 1, "N";
    |CAMPO_MODIFICA #0#11, 1, "N";
    |CAMPO_MODIFICA #0#13, 1, "S";
|SINO;
    |CAMPO_MODIFICA #0#10, 1, "S";
    |CAMPO_MODIFICA #0#11, 1, "S";
    |CAMPO_MODIFICA #0#13, 1, "N";
|FINSI;
|FPRC;

|PROCESO ultimo; |TIPO 7;
|SI elsw1 = 0;
        elsw1 = 1;
    |ABRE #7;
        #7#0 = #0#0;
        #7#1 = 999999;
    |LEE 000#7];
    |SI FSalida = 0;
        |LEE 000#7a;
    |SINO;
        |LEE 000#7u;
    |FINSI;
    |SI FSalida = 0;|Y #7#0 = #0#0;
            #0#9 = #7#1 + 1;
    |SINO;
            #0#9 = 1;
    |FINSI;
    |CIERRA #7;
|FINSI;
|FPRC;

|PROCESO si_esta; |TIPO 0;
|ABRE #7;
    #7#0 = #0#0;
    #7#1 = #0#9;
|LEE 101#7=;
|SI FSalida = 0;
    |MENSA "2300Registro existente, (I)nicializar - (A)cumular - (C) Cancelar: ";
    |ATRI I;
    |PINTA 2329, "I";
    |PINTA 2345, "A";
    |PINTA 2358, "C";
    |ATRI 0;
        E_sup = 1;
        E_inf = "IACiac";
    |AVISO;
        confi = 2371 ? 0;
    |SI confi = "I";|O confi = "i";
        |MENSA "2300Borrando ...";
        |BUCLELIN 1NULL#7;
        |BORRA 020#7a;
    |SINO;
        |SI confi = "C";|O confi = "c";
            |PTEC 807;
        |FINSI;
    |FINSI;
    |BLIN 23;
|FINSI;
|LIBERA #7;
|CIERRA #7;
|FPRC;

|PROCESO TipoRegis; |TIPO 0;
     |SI #0#4 = "A";
          #0#19 = 5;     |PINTA #0#19;
          #0#20 = 5;     |PINTA #0#20;
     |FINSI;
|FPRC;

____________________________________/ INFORME DE ERRORES

|PROCESO ImprimeErrores;
     |PRINT;
|FPRC;

|DEFBUCLE Informe;
     #100#0;
     ;
     INICIO;
     FINAL;
     ;
     NULL, ImprimeErrores;
|FIN;

|PROCESO Informe;
     |LEE 000#100p;
     |SI FSalida = 0;    || Hay algo
          aAlfa = "    ATENCION: existen cuentas bancarias erroneas. "
          aAlfa = aAlfa + "Pulse OK para ver el informe."
          |MENAV aAlfa;
          |IMPRE 0;
          |SI FSalida = 0;
               |INFOR "nompredi";
               |BUCLE Informe;
               |PIEINF;
               |FININF;
          |FINSI;
          |FINIMP;
     |SINO;
          |SI nCuantos ! 0;
               aAlfa = "    Se ha generado correctamente el registro con las transferencias ";
               aAlfa = aAlfa + "solicitadas.";
          |SINO;
               aAlfa = "    No se ha encontrado transferencias pendientes. No se generará ";
               aAlfa = aAlfa + "ningún registro";
          |FINSI;
          |MENAV aAlfa;
     |FINSI;
|FPRC;

____________________________________/ GRABACION TRANSFERENCIA
|PROCESO graba_cab;
     |DDEFECTO #7;
     #7#0 = #0#0;
     #7#1 = #0#9;
     |LEE 101#7=;
     |SI FSalida ! 0;
          |GRABA 020#7b;
     |FINSI;

     #8#0 = #7#0;
     #8#1 = #7#1;
     #8#2 = 99999;
     |LEE 000#8];
     |SI FSalida = 0;
          |LEE 000#8a;
     |SINO;
          |LEE 000#8u;
     |FINSI;
     |SI FSalida = 0;|Y #8#0 = #7#0;|Y #8#1 = #7#1;
          linea = #8#2 + 1;
     |SINO;
          linea = 1;
     |FINSI;
|FPRC;

|PROCESO mirando;
    esta = 1;
|FPRC;

|DEFBUCLE buscalo;
 #6#1;
 ;
 #0#13, nBanco;
 #0#13, nBanco;
 ;
 NULL, mirando;
|FIN;

|PROCESO CompLiq;
     |SI #8#0 ! nAntEmp; |O #8#3 ! nAntTra; |O swMeSalgo = 1;
          || es trabajador es distinto, voy a comprobar el liquido del anterior
          |SI swMeSalgo ! 1;       || hay que hacerlo asi porque en el ultimo
               |SALVA_FICHA #8;    || registro no hay que hacer el SALVA ya
          |FINSI;                  || que si es el que hay que borrar se lia
          #8#0 = nAntEmp;
          #8#1 = nAntReg;
          #8#2 = nAntOrd;
          |LEE 101#8=;
          |SI FSalida = 0;
               |SI #8#6 < 0;
                    |BORRA 020#8a;

                    #7#2 = #7#2 - #8#6;
                    #7#3 = #7#3 - 1;
                    |GRABA 020#7a;
               |FINSI;
          |FINSI;

          |SI swMeSalgo ! 1;
               |REPON_FICHA #8;
          |FINSI;
     |FINSI;

     nAntEmp = #8#0;
     nAntReg = #8#1;
     nAntOrd = #8#2;
     nAntTra = #8#3;
|FPRC;

|PROCESO Comprueba2;
     aAlfa = aValor;
     |PARA nCaracter = 1; |SI nCaracter [ nLongitud; |HACIENDO nCaracter = nCaracter + 1;
          nNume = (100 * nCaracter) + 1;
          aCaracter = aAlfa % nNume;
          |QBF aCaracter;
          |SI aCaracter ! "0"; |Y aCaracter ! "1"; |Y aCaracter ! "2"; |Y aCaracter ! "3";
          |Y aCaracter ! "4"; |Y aCaracter ! "5"; |Y aCaracter ! "6"; |Y aCaracter ! "7";
          |Y aCaracter ! "8"; |Y aCaracter ! "9";
               |SI aCaracter = "";
                    swError1 = 1;            || problemas con la longitud
               |SINO;
                    |SI aCaracter ! "*";
                         swError2 = 1;  || no numerico
                    |FINSI;
               |FINSI;
          |FINSI;
     |SIGUE;

     aAlfa = aValor;
     |QBF aAlfa;
     aAlfa = aAlfa % 0;
     nNume = aAlfa;
     |SI nNume > nLongitud;
          swError1 = 1;            || problemas con la longitud
     |FINSI;
|FPRC;

|PROCESO Comprueba;
     swError1 = 0;
     swError2 = 0;

     nLongitud = 20;
     aValor = aCuenta;

     |HAZ Comprueba2;
|FPRC;

|PROCESO graba_trans;
     |ABRE #22;
     #22#0 = #2#0;
     #22#1 = #2#1;
     #22#2 = nTipoCuenta;
     |LEE 000#22=;
     |SI FSalida = 0;
          nBanco = #22#3;
          aCuenta = #22#5 + #22#6 + #22#8 + #22#7;
          aIBAN = #22#12;
          aBIC  = #22#13;
     |SINO;
          nBanco = #2#71;
          aCuenta = #2#72;        || cta. cte.
          aIBAN = #2#154;
          aBIC  = #2#155;
     |FINSI;
     |CIERRA #22;

     |QBF aCuenta;
     |QBF aIBAN;
     swError1 = 0;
     swError2 = 0;
     |SI aCuenta = ""; |Y aIBAN ! "";
          aCuenta = aIBAN;
     |SINO;
          |HAZ Comprueba;
     |FINSI;

     |SI swError1 = 1; |O swError2 = 1;
          #100#0 = #2#0;
          #100#1 = #2#1;
          #100#2 = nTipoCuenta;
          #100#52 = #2#2;
          #100#53 = aCuenta;
          |GRABA 020#100n;
          |ACABA;
     |FINSI;

     esta = 0;
     |SI #0#12 = "S";
         |SI nBanco ] #0#10;  |Y nBanco [ #0#11;
             esta = 1;
         |FINSI;
     |SINO;
          |BUCLE buscalo;
     |FINSI;

     |SI #2#77 ] #0#17;|Y #2#77 [ #0#18;|Y esta = 1;
     |SINO;
            esta = 0;
     |FINSI;

     |SI esta = 0;  |ACABA;  |FINSI;

     |SELECT #2#8;
     #8#1 = #0#9;             || registro
     #8#0 = #0#0;             || empresa
     #8#3 = #2#1;             || trabajador
     #8#5 = aCuenta;          || Cuenta Trabajador
     |LEE 000#8=;
     |SI FSalida = 0;         || lectura por clave secundaria
          la_linea = #8#2;    || por si hay que acumular
     |SINO;
          la_linea = linea;
     |FINSI;

     nEmpPag = #0#0;
     nAnyPag = 2000 + el_any;
     nMesPag = el_mes;
     nTraPag = #2#1;
     swStop  = 0;

     |HAZ CompPagado;
     |SI swStop ! 0;
          |ACABA;
     |FINSI;

     |SELECT #1#8;
     |DDEFECTO #8;
     #8#0 = #0#0;         || empresa
     #8#1 = #0#9;         || registro
     #8#2 = la_linea;     || linea
     |LEE 101#8=;
     |SI FSalida ! 0;
         #8#0 = #0#0;         || empresa
         #8#1 = #0#9;         || registro
         #8#2 = la_linea;     || linea
         |SI nTipoCuenta = 1;  #8#7 = "NOMINA";  |FINSI;
         |SI nTipoCuenta = 2;  #8#7 = "PAGA";    |FINSI;
         |SI nTipoCuenta = 3;
               aAlfa = #22#10;
               |QBF aAlfa;
               |SI aAlfa ! "";
                    #8#7 = "EMBARGO-EXPD. " + aAlfa;
               |SINO;
                    #8#7 = "EMBARGO";
               |FINSI;
         |FINSI;
         |GRABA 020#8b;

          linea = linea + 1;    || contador de lineas
          #7#3 = #7#3 + 1;      || total transferencias
     |FINSI;

     #8#3 = #2#1;         || trabajador
     #8#4 = #2#7;         || dni
     #8#5 = aCuenta;      || cta. cte.
     #8#6 = #8#6 + liquido;    || liquido
     |SI nTipoCuenta ! 3;
          #8#8 = #2#2;         || apellidos y nombre
     |SINO;
          #8#8 = #22#9;
     |FINSI;
     #8#9  = aIBAN;      || IBAN
     #8#10 = aBIC;       || BIC

     #8#13 = #0#5;       || A¤o
     #8#14 = el_mes;     || Mes
     #8#15 = el_tipo;    || Tipo nomina
     #8#16 = #0#8;       || Tipo recibo

     |GRABA 020#8a;
     |LIBERA #8;

     #7#2 = #7#2 + liquido;    || total liquidos
     #7#4 = ~;                 || fecha registro

     #7#6 = #0#5;             || A¤o
     #7#7 = #0#6;             || Desde mes
     #7#8 = #0#7;             || Hasta mes
     #7#9 = #0#19;            || Desde Tipo
     #7#10 = #0#20;           || Hasta Tipo
     #7#11 = #0#4;            || Fuente (Mensual/His/Atr)
     #7#12 = #0#8;            || Tipo nominas (mes/pagas)

     |GRABA 020#7a;

     nCuantos = nCuantos + 1;

     |HAZ CompLiq;

     nEmpPag    = #8#0;
     nAnyPag    = 2000 + el_any;
     nMesPag    = el_mes;
     nTraPag    = #8#3;
     fFecPag    = "00.00.0000";
     aRegPag    = #nomrevca#1;
     aRegPag    = ("000000" + aRegPag) % -106;
     nLiqEnlace = liquido;
     |HAZ Pagado;
|FPRC;
____________________________________/ CALCULO LIQUIDOS
|PROCESO mensual;   || CALCULO LIQUIDO PARA CLAVE 'M'
     |SI #0#4 = "M";
          ||SI swbaja ! 1; |Y #1#25 ! 2; |Y #3#16 > 0;
          || si es baja o las pagas van junto al mes y tiene paga este mes

          nEmbargo = #3#104;
          |SI #3#16 > 0; |Y #3#104 > 0; || si hay paga y hay embargo
               |SI #1#25 = 1;
                    || paga y mes separados
                    nEmbargo = ((#3#104 * (#3#46 / (#3#46 + #3#47))) ! 2);
               |FINSI;
          |FINSI;

          devenga  = #3#46;
          deducir  = #3#48 +#3#49 +#3#50 +#3#51 +#3#52 +#3#53 +#3#54 +#3#75 +#3#89 + nEmbargo + #3#165;
          |||SI swbaja = 1;
          || si hay paga calculada y es paga junto mes la tendre que a¤adir
          || s¡ o s¡
          |SI #1#25 = 2;
               devenga = devenga + #3#16;  || si es baja este mes o la paga va
               deducir = deducir + #3#17;  ||/junto con el recibo, a¤ade paga
               deducir = deducir + #3#86;  || y anticipo de pagas extras
          |FINSI;
          nTipPag = #nomcalca#3;
     |FINSI;

     |SI #0#4 = "H";
          ||SI swbaja ! 1;|Y #1#25 ! 2; |Y #4#16 > 0;
          || si es baja o las pagas van junto al mes y tiene paga este mes

          nEmbargo = #4#146;
          |SI #4#16 > 0; |Y #4#146 > 0; || si hay paga y hay embargo
               |SI #1#25 = 1;
                    || paga y mes separados
               |FINSI;
          |FINSI;

          devenga  = #4#46;
          deducir  = #4#48 +#4#49 +#4#50 +#4#51 +#4#52 +#4#53 +#4#54 +#4#114 +#4#131 + nEmbargo + #4#207;
          |||SI swbaja = 1; |O #1#25 = 2;
          || si hay paga calculada y es paga junto mes la tendre que a¤adir
          || s¡ o s¡
          |SI #1#25 = 2;
               devenga = devenga + #4#16;  || si es baja este mes o la paga va
               deducir = deducir + #4#17;  ||/junto con el recibo, a¤ade paga
               deducir = deducir + #4#128; || y anticipos paga extra
          |FINSI;
          nTipPag = #nomhicca#3;
     |FINSI;
|SI #0#4 = "A";
        nEmbargo = 0;
        devenga = #5#46;
        deducir = #5#48 +#5#49 +#5#50 +#5#51 +#5#52 +#5#53 +#5#54 +#5#75 +#5#89;
    ||SI swbaja = 1; |O #1#25 = 2;
    |SI #1#25 = 2;
            devenga = devenga + #5#16;  || si la paga va
            deducir = deducir + #5#17;  ||/junto con el recibo, a¤ade paga
    |FINSI;
    nTipPag = #nomcalac#3;
|FINSI;
|FPRC;

|PROCESO pagas;   || CALCULO LIQUIDO PARA CLAVE 'P'
     nEmbargo = 0;
     ||SI swbaja ! 1; |Y #1#25 ! 2;       || si es baja o las pagas van junto al
                                          ||/mes no se incluyen

     || ¨por que no se va a incluir la paga, si hay importe calculado?
     || En paga y mes separados, me tiene que dar igual que este mes sea
     || o no sea baja. Si se calcula paga, pues tiene que ir a la transferencia

     |SI #1#25 = 1;
          |SI #0#4 = "M";
               devenga = #3#16;
               deducir = #3#17;
               nEmbargo = #3#104 - ((#3#104 * (#3#46 / (#3#46 + #3#47))) ! 2);
               || a¤ade anticipo pagas extras y embargo
               deducir = deducir + #3#86 + nEmbargo;
               nTipPag = #nomcalca#3;
          |FINSI;
          |SI #0#4 = "H";
               devenga = #4#16;
               deducir = #4#17;
               nEmbargo = #4#146 - ((#4#146 * (#4#46 / (#4#46 + #4#47))) ! 2);
               || a¤ade anticipo pagas extras y embargo
               deducir = deducir + #4#128 + nEmbargo;
               nTipPag = #nomhicca#3;
          |FINSI;
          |SI #0#4 = "A";
               devenga = 0;
               deducir = 0;
               nTipPag = #nomcalac#3;
          |FINSI;
     |SINO;
          devenga = 0;
          deducir = 0;
     |FINSI;
|FPRC;


|PROCESO desglosa;     || DESGLOSA EN DIA, MES Y ANYO UNA FECHA DD.MM.AAAA
    alfa1 = fecalf %  102;   dia = alfa1;
    alfa1 = fecalf %  402;   mes = alfa1;
    alfa1 = fecalf % -104;   any = alfa1;
|SI alfa1 = "....";
        dia = 0;
        mes = 0;
        any = 0;
|FINSI;
|FPRC;

|PROCESO topemes;
    nume1 = any $ 4;
|SI nume1 = 0;
        bisiesto = 1;
|SINO;
        bisiesto = 0;
|FINSI;
|SI el_mes =  1;  topemes = 31; |FINSI;
|SI el_mes =  2;  topemes = 28 + bisiesto; |FINSI;
|SI el_mes =  3;  topemes = 31; |FINSI;
|SI el_mes =  4;  topemes = 30; |FINSI;
|SI el_mes =  5;  topemes = 31; |FINSI;
|SI el_mes =  6;  topemes = 30; |FINSI;
|SI el_mes =  7;  topemes = 31; |FINSI;
|SI el_mes =  8;  topemes = 31; |FINSI;
|SI el_mes =  9;  topemes = 30; |FINSI;
|SI el_mes = 10;  topemes = 31; |FINSI;
|SI el_mes = 11;  topemes = 30; |FINSI;
|SI el_mes = 12;  topemes = 31; |FINSI;
|FPRC;

|PROCESO baja;
|| esta variable, este proceso, en teoria no se va a usar mas
|| la explicacion esta en el tiquet 001254.2213, 08/08/2018
     swbaja = 0;
     fecalf = #3#5;
     |HAZ desglosa;
     |HAZ topemes;
     |SI mes = el_mes; |Y any = #0#5; |Y dia ! topemes;
          swbaja = 1;
     |FINSI;
|FPRC;

|PROCESO proceso;
     |SI #0#4 = "M";     el_mes = #3#2; el_tipo = #3#3; |FINSI;
     |SI #0#4 = "H";     el_mes = #4#2; el_tipo = #4#3; |FINSI;
     |SI #0#4 = "A";     el_mes = #5#2; el_tipo = #5#3; |FINSI;

     esta = 0;
     |HAZ ordena;
     |SI #2#70 ! "T";  |ACABA;  |FINSI;
     |SI el_tipo = 2; |Y #2#48 = "N";
          aAlfa1 = #labtraba#0; aAlfa1 = ("00000" + aAlfa1) % -105;
          aAlfa2 = #labtraba#1; aAlfa2 = ("00000" + aAlfa2) % -105;
          aAlfa = "    ATENCION. El trabajador " + aAlfa1 + "." + aAlfa2;
          aAlfa = aAlfa + " tiene calculado finiquito teniendo en su ficha ";
          aAlfa = aAlfa + "Finiquito 'NO'";
          |MENAV aAlfa;
          aAlfa = "    El importe del finiquito NO se tendra en cuenta en la ";
          aAlfa = aAlfa + "transferencia. Revise sus datos si es necesario";
          |MENAV aAlfa;
          |ACABA;
     |FINSI;
     |HAZ baja;
     |SI swbaja = 0;|O #0#16 = "S";
          |SI #0#8 = "M"; nTipoCuenta = 1;  aNorPag = "N";  |HAZ mensual;  |FINSI;
          |SI #0#8 = "P"; nTipoCuenta = 2;  aNorPag = "P";  |HAZ pagas;    |FINSI;

          liquido = devenga - deducir;

          |SI liquido ! 0;
               #0#14 = #2#0;  |PINTA #0#14;
               #0#15 = #2#1;  |PINTA #0#15;
               seleccio = 1;
               |HAZ graba_trans;
          |FINSI;

          |SI nEmbargo > 0;
               nTipoCuenta = 3;
               |ABRE #22;
               #22#0 = #2#0;
               #22#1 = #2#1;
               #22#2 = nTipoCuenta;
               |LEE 000#22=;
               |SI FSalida = 0;
                    aNorPag     = "E";
                    liquido     = nEmbargo;

                    #0#14 = #2#0;  |PINTA #0#14;
                    #0#15 = #2#1;  |PINTA #0#15;
                    seleccio = 1;
                    |HAZ graba_trans;
               |FINSI;
               |CIERRA #22;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO ordena;   || LEE EL TRABAJADOR
|DDEFECTO #2;
|SI #0#4 = "M";     #2#0 = #3#0;   #2#1 = #3#1;   |FINSI;
|SI #0#4 = "H";     #2#0 = #4#0;   #2#1 = #4#1;   |FINSI;
|SI #0#4 = "A";     #2#0 = #5#0;   #2#1 = #5#1;   |FINSI;
|LEE 000#2=;
|FPRC;

|DEFBUCLE nomcalac;
 #5#1;
 ;
 #0#0, #0#2, #0#6, #0#19;
 #0#0, #0#3, #0#7, #0#20;
 ;
 NULL, ordena, NULL, NULL, NULL, proceso;
 #2#7, #2#0, #2#1;
|FIN;

|DEFBUCLE nomhicca;
 #4#1;
 ;
 #0#0, #0#2, el_any, #0#6, #0#19;
 #0#0, #0#3, el_any, #0#7, #0#20;
 ;
 NULL, ordena, NULL, NULL, NULL, proceso;
 #2#7, #2#0, #2#1;
|FIN;

|DEFBUCLE nomcalca;
 #3#1;
 ;
 #0#0, #0#2, #0#6, #0#19;
 #0#0, #0#3, #0#7, #0#20;
 ;
 NULL, ordena, NULL, NULL, NULL, proceso;
 #2#7, #2#0, #2#1;
|FIN;

|PROCESO tipo3; |TIPO 3;
     |CAMPO_VISUAL #0#14, 1, "S";
     |CAMPO_VISUAL #0#15, 1, "S";
     |PINTA 2203, "En proceso ...";
     alfa1 = #0#5;   alfa1 = alfa1 % -102;
     el_any = alfa1;

     |ABRE #1;
     |ABRE #2;
     |ABRE #7;
     |ABRE #8;

     aAlfa = Usuario; |QBF aAlfa;    aAlfa = "2" + aAlfa;
     |NOME_DAT #100 aAlfa;

     |DELINDEX #100;
     |ABRE #100;

     |HAZ graba_cab;
     #1#0 = #0#0;
     |LEE 020#1=;
     nCuantos = 0;
     |SI FSalida = 0;
          |SI #0#4 = "M";     |BUCLE nomcalca;    |FINSI;
          |SI #0#4 = "H";     |BUCLE nomhicca;    |FINSI;
          |SI #0#4 = "A";     |BUCLE nomcalac;    |FINSI;
          swMeSalgo = 1;
          |HAZ CompLiq;
          |HAZ Informe;
     |FINSI;

     |SELECT #1#8;
     #8#0 = #0#0;         || empresa
     #8#1 = #0#9;         || registro
     #8#2 = 1;            || linea
     |LEE 000#8];
     |SI FSalida ! 0;  |O  #8#0 ! #0#0;  |O #8#1 ! #0#9;
          |BORRA 020#7a;
     |FINSI;

     |SI seleccio = 0;
          |MENAV "    Seleccion vacia ...";
     |FINSI;
     |CIERRA #1;
     |CIERRA #2;
     |LIBERA #7;
     |CIERRA #7;
     |CIERRA #8;
     |CIERRA #100;
|FPRC;
