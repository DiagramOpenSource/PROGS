|FICHEROS;
     labdesmv #0;
     labdesmt #100,MANTE;
     labdesca;
     labdesli;
|FIN;

|VARIABLES;
 {-1}menu = "";
     menu1 = "2400";
     menu2 = "1";
     menu3 = " ";
     menu4 = "RPC";
     menu5 = " Repasar Lista ";
     menu6 = " Procesar";
     menu7 = " Cancelar ";
     opcion = 0;

     nCodAntiguo = 0;
     aDescripcion = "";
     nCodNuevo = 0;
     aAlfa1 = "";
     aAlfa2 = "";
     nNume1 = 0;
     nElUltimo = 0;
     nElAnterior = 0
     nRevisa1 = 0;
     nRevisa2 = 0;
|FIN;

|PROCESO labdesli;
     #labdesli#0 = nCodNuevo;
     |GRABA 020#labdesli.n;
     |SI FSalida = 0;
          #labdesli#0 = nCodAntiguo;
          |BORRA 020#labdesli.c
     |FINSI;
|FPRC;

|DEFBUCLE labdesli;
     #labdesli#1;
     ;
     nCodAntiguo, 01;
     nCodAntiguo, 14;
     e;
     NULL, labdesli;
|FIN;

|PROCESO graba_labdesca;
     #labdesca#0 = nCodAntiguo;
     |LEE 101#labdesca.=;
     #labdesca#0 = nCodNuevo;
     |GRABA 020#labdesca.n;
     |SI FSalida = 0;
          #labdesca#0 = nCodAntiguo;
          |LEE 101#labdesca.=;
          |BORRA 020#labdesca.a;
     |FINSI;
|FPRC;

|PROCESO ElProceso;
     nCodAntiguo = #labdesmt#0;
     aDescripcion = #labdesmt#1;
     nCodNuevo = #labdesmt#2;
     |HAZ graba_labdesca;
     |BUCLE labdesli;
|FPRC;

|DEFBUCLE ElBucle;
     #labdesmt#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, ElProceso;
|FIN;

|PROCESO mueve; |TIPO 3;
     |SI opcion = 3; |O opcion = 0; |O FSalida ! 0; |O FEntrada = 302;
          |ACABA;
     |SINO;
          |ABRE #labdesca;
          |ABRE #labdesli;
          |BUCLE ElBucle;
          |CIERRA #labdesli;
          |CIERRA #labdesca;
     |FINSI;
|FPRC;

|PROCESO revisa2;
     |SI #labdesmt#2 = nElAnterior;
          nRevisa2 = #labdesmt#2;
          |ERROR10;
     |FINSI;
     nElAnterior = #labdesmt#2;
|FPRC;

|DEFBUCLE revisa2;  || revisa que no se repita ninguno de los nuevos
     #labdesmt#2;
     ;
     INICIO;
     FINAL;
     ;
     NULL, revisa2;
|FIN;

|PROCESO revisa1;
     #labdesca#0 = #labdesmt#2;
     |LEE 000#labdesca.=;
     |SI FSalida = 0;
          nRevisa1 = #labdesmt#2;
          |ERROR10;
     |FINSI;
|FPRC;

|DEFBUCLE revisa1;  || revisa que ninguno de los nuevos exista
     #labdesmt#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, revisa1;
|FIN;

|PROCESO carga;
     #labdesmt#0 = #labdesca#0;
     #labdesmt#1 = #labdesca#1;
     #labdesmt#2 = #labdesca#0 + #labdesmv#1;
     |GRABA 020#labdesmt.n;
|FPRC;

|DEFBUCLE carga;
     #labdesca#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, carga;
|FIN;

|PROCESO minimo;
     #100#0 = 1;
|FPRC;

|PROCESO maximo;
     #100#0 = 99;
|FPRC;

|PROCESO lineal;
     |ABRE #labdesca;
     aAlfa1 = Usuario; |QBF aAlfa1;
     |NOME_DAT #100 aAlfa1;
     |SI #0#0 = "S";
          |DELINDEX #labdesmt;
          |ABRE #labdesmt;
          |BUCLE carga;
          |CIERRA #labdesmt;
     |SINO;
          |DELINDEX #labdesmt;
     |FINSI;
     |PARA; |SI; |HACIENDO;
          |ENTLINEAL #1#100, 1, 3, 21, 1, minimo, maximo;
          |MENU menu;
          opcion = FSalida;
          |SI opcion ! 1;
               |SI opcion = 2;
                    nElAnterior = 2.2; || me evito problemas con los codigos 0
                    nRevisa1 = 0;
                    nRevisa2 = 0;
                    |BUCLE revisa1;
                    |BUCLE revisa2;
                    |SI nRevisa1 ! 0;
                         aAlfa1 = nRevisa1;
                         aAlfa1 = "     ATENCION: El codigo " + aAlfa1 + " ya existe.";
                         aAlfa1 = aAlfa1 +  " Es necesario revisar la lista.";
                         |MENAV aAlfa1;
                    |FINSI;
                    |SI nRevisa2 ! 0;
                         aAlfa1 = nRevisa2;
                         aAlfa1 = "     ATENCION: El codigo " + aAlfa1 + " esta duplicado.";
                         aAlfa1 = aAlfa1 +  " Es necesario revisar la lista.";
                         |MENAV aAlfa1;
                    |FINSI;
                    |SI nRevisa1 = 0; |Y nRevisa2 = 0;
                         |SAL;
                    |FINSI;
               |SINO;
                    |SAL;
               |FINSI;
          |FINSI;
     |SIGUE;
     |CIERRA #labdesca;
|FPRC;

------------------------------------------------------------------------------
|PROCESO factor; |TIPO 7;
     |SI #0#0 = "S";
          |CAMPO_MODIFICA #0#1, 1, "S";
     |SINO;
          |CAMPO_MODIFICA #0#1, 1, "N";
     |FINSI;
|FPRC;

|PROCESO comprueba;
     #labdesca#0 = #labdesmt#0;
     |LEE 000#labdesca.=;
     |SI FSalida ! 0;
          aAlfa1 = #labdesca#0;
          aAlfa1 = "000" + aAlfa1;
          aAlfa1 = aAlfa1 % -103;
          aAlfa1 = "    ATENCION: La Planilla " + aAlfa1 + " no existe.";
          |MENAV aAlfa1;
          |ERROR;
     |FINSI;
|FPRC;

|PROCESO comprueba2;
     #labdesca#0 = #labdesmt#2;
     |LEE 000#labdesca.=;
     |SI FSalida = 0;
          aAlfa1 = #labdesca#0;
          aAlfa1 = "000" + aAlfa1;
          aAlfa1 = aAlfa1 % -103;
          aAlfa1 = "    ATENCION: La Planilla " + aAlfa1 + " ya existe.";
          |MENAV aAlfa1;
          |ERROR;
     |FINSI;
|FPRC;

|PROCESO descripcion;
     #labdesca#0 = #labdesmt#0;
     |LEE 000#labdesca.=;
     |SI FSalida = 0;
          #labdesmt#1 = #labdesca#1;
          |PINTA #labdesmt#1;
     |FINSI;
|FPRC;

|PROCESO elnuevo; |TIPO 7;
     |SI FEntrada ! 303;  || si no es modificacion
          #100Campo = #100#0;
          |PINTA #100Campo;
     |FINSI;
|FPRC;

|PROCESO comprueba0;
     |SI #0#0 = "S";
          |ABRE #labdesca;
          |LEE 000#labdesca.u;
          nElUltimo = #labdesca#0;
          |CIERRA #labdesca;

          nNume1 = nElUltimo + #0#1;
          |SI nNume1 > 99;
               nNume1 = 99 - nElUltimo;
               aAlfa2 = nNume1;
               aAlfa1 = "    El factor maximo segun la codificacion de Planilla ";
               aAlfa1 = aAlfa1 + "no puede superar el " + aAlfa2;
               |MENAV aAlfa1;
               |ERROR;
          |FINSI;
     |FINSI;
|FPRC;
