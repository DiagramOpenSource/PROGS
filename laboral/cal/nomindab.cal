|FICHEROS;
     nomindab #0;
     labtraba;
     nomtrcal #20;
     nomhicca #100;
     nomcalca #101;
     nomindtm #200;      || temporal por trabajador
     nomindtn #201;      || temporal por empresa/centro
     nomabsca;
     nomabsli;

     labempre;
     labcentr;
|FIN;

|VARIABLES;
     fFechaRef = @;
     aFechaRef = "";
     aDiaRef = "";
     aMesRef = "";
     aAnyRef = "";
     nDiaRef = 0;
     nMesRef = 0;
     nAnyRef = 0;

     aDiaBus = "";
     aMesBus = "";
     aAnyBus = "";
     nDiaBus = 0;
     nMesBus = 0;
     nAnyBus = 0;

     aFechaBus = "";
     fFechaBus = @;
     nFechaBus = 0;

     nAny = 0;
     nMes = 0;

     nume1 = 0;
     meslabo = "";
     dialabo = 0;
     diafest = 0;
     diasabse = 0;
     topemes1 = 0;
     topemes2 = 0;
     guarda = 0;
     mid = 0;
     nDiaDesde = 0;
     nDiaHasta = 0;
     nDiasAbs = 0;
     swNoEntra = 0;
     alfa1 = "";
     alfa2 = "";
     alfa3 = "";
     alfa4 = "";
     nIndice = 0;
     numer = 0;
     mesnum = 0;
     anynum = 0;
     bisiesto = 0;

     aFechaDesde = "";
     aFechaHasta = "";
     fFechaDesde = @;
     fFechaHasta = @;

     nNume1 = 0;
     topemes = 0;

     fFechaAlta = @;
     fFechaBaja = @;
     aFechaAlta = "";
     aFechaBaja = "";
     aDiaAlta = "";
     aMesAlta = "";
     aAnyAlta = "";
     aDiaBaja = "";
     aMesBaja = "";
     aAnyBaja = "";
     nDiaAlta = 0;
     nDiaBaja = 0;
     aVengoDe = "";
     nCampo1 = 0;
     nCampo2 = 0;
     nCampo3 = 0;
     aFuente = "";
     swSigue = 0;
     nDiasAbsMante = 0;

     nEmpAnt = 0;
     nCenAnt = 0;
     nTraAnt = 0;
     nAnyAnt = 0;
     nMesAnt = 0;
     nIndice1 = 0;
     nIndice2 = 0;
     aIndice1Per1 = "";
     aIndice1Per2 = "";
     aIndice2Per1 = "";
     aIndice2Per2 = "";
     aIndice2Per3 = "";
     aIndice2Per4 = "";
     aAny = "";
     aMes = "";
     aPeriodo = "";
     FEstado = 0;
     &aEmpresa = "";
     &aCentro = "";
     &aTrabajador = "";
|FIN;

|PROCESO Imprime;
     #labempre#0 = #200#0;
     |LEE 000#labempre.=;
     aEmpresa = #labempre#2;

     #labcentr#0 = #200#0;
     #labcentr#1 = #200#1;
     |LEE 000#labcentr.=;
     aCentro = #labcentr#2;

     #labtraba#0 = #200#0;
     #labtraba#1 = #200#2;
     |LEE 000#labtraba.=;
     aTrabajador = #labtraba#2;

     |PRINT;
|FPRC;

|DEFBUCLE Impresion;
     #200#1;
     16;
     00001, 001, 00001, 2000, 01, 1;
     99999, 999, 99999, 2999, 12, 1;
     ;
     NULL, Imprime;
|FIN;

|PROCESO Impresion;
     Impresora = "CrystalReport";
     |IMPRE -1;
     |SI FSalida = 0;
          |INFOR "nomindab";
          |BUCLE Impresion;
          |PIEINF;
          |FININF;
     |FINSI;
|FPRC;

|PROCESO CruzaDatos;
     #201#0 = #200#0;    || Empresa
     #201#1 = #200#1;    || Centro
     #201#2 = #200#3;    || A¤o
     #201#3 = #200#4;    || Mes
     |LEE 000#201=;
     |SI FSalida = 0;
          #200#12 = #201#4;
          #200#13 = #201#5;
          #200#14 = #201#6;
          |SI #200#14 ] 5;
               #200#15 = 1;
          |FINSI;
          #200#16 = 1;
          |GRABA 000#200a;
     |FINSI;
|FPRC;

|DEFBUCLE CruzaDatos;
     #200#1;
     ;
     nEmpAnt, nCenAnt, nTraAnt, 2000, 01;
     nEmpAnt, nCenAnt, nTraAnt, 2999, 12;
     ;
     NULL, CruzaDatos;
|FIN;

|PROCESO posible;
     |SALVA_FICHA #200;

     || ahora voy a buscar el indice de absentismo de la empresa
     || en cada periodo

     |BUCLE CruzaDatos;

     |REPON_FICHA #200;
|FPRC;

|PROCESO Candidatos;
     |SI nEmpAnt ! #200#0; |O nTraAnt ! #200#2;   || cambio de trabajador
          aIndice1Per1 = "";
          aIndice1Per2 = "";
          aIndice2Per1 = "";
          aIndice2Per2 = "";
          aIndice2Per3 = "";
          aIndice2Per4 = "";
          nIndice1 = 0;
          nIndice2 = 0;
     |FINSI;
     |SI #200#13 > 0;
          |ACABA;
     |FINSI;

     nAny = #200#3;
     nMes = #200#4;
     aAny = nAny;
     aMes = ("00" + aMes) % -102;
     aPeriodo = aAny + aMes;

     |SI #200#10 = 1;              || supero el 20%
          nIndice1 = nIndice1 + 1;
          |SI nIndice1 = 1;
               aIndice1Per1 = aPeriodo;
          |FINSI;
          |SI nIndice1 = 2;
               aIndice1Per2 = aPeriodo;
          |FINSI;
     |SINO;
          nIndice1 = 0;
          aIndice1Per1 = "";
          aIndice1Per2 = "";
     |FINSI;
     |SI #200#11 = 1;              || supero el 25%
          nIndice2 = nIndice2 + 1;
          |SI nIndice2 = 1;
               aIndice2Per1 = aPeriodo;
          |FINSI;
          |SI nIndice2 = 2;
               aIndice2Per2 = aPeriodo;
          |FINSI;
          |SI nIndice2 = 3;
               aIndice2Per3 = aPeriodo;
          |FINSI;
          |SI nIndice2 = 4;
               aIndice2Per4 = aPeriodo;
          |FINSI;
     |FINSI;

     |SI nIndice1 ] 2; || candidato;
          |SI nAnyAnt = #200#3;
               nNume1 = #200#4 - 1;
               |SI nMesAnt = nNume1;
                    |HAZ posible;
               |FINSI;
          |SINO;
               nNume1 = #200#3 - 1;
               |SI nAnyAnt = nNume1;
                    |SI nMesAnt = 12; |Y #200#4 = 1;
                         |HAZ posible;
                    |FINSI;
               |FINSI;
          |FINSI;
     |FINSI;
     |SI nIndice2 ] 4; || candidato;
          |HAZ posible;
     |FINSI;

     nEmpAnt = #200#0;
     nCenAnt = #200#1;
     nTraAnt = #200#2;
     nAnyAnt = #200#3;
     nMesAnt = #200#4;
|FPRC;

|DEFBUCLE Candidatos;
     #200#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, Candidatos;
|FIN;

|PROCESO Calculos;
     |BUCLE Candidatos;
     |HAZ Candidatos;
|FPRC;

|PROCESO Centros;
     |DDEFECTO #201;
     #201#0 = #200#0;    || Empresa
     #201#1 = #200#1;    || Centro
     #201#2 = #200#3;    || A¤o
     #201#3 = #200#4;    || Mes
     |LEE 000#201=;
     FEstado = FSalida;
     #201#4 = #201#4 + #200#7;    || Dias Absentismo
     #201#5 = #201#5 + #200#8;    || Dias Laborables
     #201#6 = #201#4 * 100 / #201#5;    || Indice Absentismo
     |SI FEstado ! 0;
          |GRABA 000#201n;
     |SINO;
          |GRABA 000#201a;
     |FINSI;
|FPRC;

|DEFBUCLE Empresa;
     #200#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, Centros;
|FIN;

|PROCESO CargaAbs;                 || CARGA LOS DIAS DE ABSENTISMO
     |DDEFECTO #20;
     |ABRE #20;
     #20#0  = #labtraba#156;
     #20#13 = anynum;
     |LEE 000#20=;
     |CIERRA #20;

     nume1 = nMes;
     meslabo = #20nume1;

     |PARA mid = topemes1; |SI mid [ topemes2; |HACIENDO mid = mid + 1;
          nume1 = (mid * 100) + 1;
          alfa1 = meslabo % nume1;
          |SI alfa1 = "0";
               |SI #nomabsli#6 = 0;
                    nDiasAbs = 1;     || cuenta dias absentismo
               |SINO;
                    nDiasAbs = ((#nomabsli#6 / nNume1) ! 3);
               |FINSI;
               diasabse = diasabse + nDiasAbs;     || cuenta dias absentismo
          |FINSI;
     |SIGUE;
|FPRC;

|PROCESO AbsDelDeAntes;
     |SI aFuente = "H";
          |PARA nCampo1 = 27; |SI nCampo1 [ 30; |HACIENDO nCampo1 = nCampo1 + 1;
               |SI #100nCampo1 = "F"; |O #100nCampo1 = "f";
                    nCampo2 = nCampo1 - 8;
                    nCampo3 = nCampo1 - 4;
                    topemes1 = #100nCampo2;
                    topemes2 = #100nCampo3;
                    #nomabsli#6 = 0;
                    |HAZ CargaAbs;
               |FINSI;
          |SIGUE;
          nNume1 = (#100#34 - 0.49) ! 0;         || la parte decimal
          nNume1 = #100#34 - nNume1;
          diasabse = diasabse + nNume1;
     |FINSI;
     |SI aFuente = "M";
          |PARA nCampo1 = 27; |SI nCampo1 [ 30; |HACIENDO nCampo1 = nCampo1 + 1;
               |SI #101nCampo1 = "F"; |O #101nCampo1 = "f";
                    nCampo2 = nCampo1 - 8;
                    nCampo3 = nCampo1 - 4;
                    topemes1 = #101nCampo2;
                    topemes2 = #101nCampo3;
                    #nomabsli#6 = 0;
                    |HAZ CargaAbs;
               |FINSI;
          |SIGUE;
          nNume1 = #101#34 ! 0;         || la parte decimal
          nNume1 = #101#34 - nNume1;
          diasabse = diasabse + nNume1;
     |FINSI;
|FPRC;

|PROCESO nomabsca;
     swNoEntra = 0;
     nNume1 = 8;

     |ABRE #nomabsca;
     #nomabsca#0 = #labtraba#0;
     #nomabsca#1 = #labtraba#1;
     |LEE 000#nomabsca.=;
     |SI FSalida = 0;
          nNume1 = #nomabsca#2;
          swNoEntra = 1;
     |FINSI;
     |CIERRA #nomabsca;
|FPRC;

|PROCESO Absentismos;
     alfa1 = #nomabsli#2;
     alfa1 = alfa1 % 102;
     topemes1 = alfa1;
     alfa2 = #nomabsli#3;
     alfa2 = alfa2 % 102;
     topemes2 = alfa2;

     |SI #nomabsli#4 = "A";
          |HAZ nomabsca;
          |HAZ CargaAbs;
     |FINSI;
|FPRC;

|DEFBUCLE Abs;
     #nomabsli#1;
     ;
     #labtraba#0, #labtraba#1, fFechaDesde;
     #labtraba#0, #labtraba#1, fFechaHasta;
     ;
     NULL, Absentismos;
|FIN;

|PROCESO ulti_dia;  || halla el ultimo dia del mes
     numer = anynum $ 4;
     |SI numer = 0;
             bisiesto = 1;
     |SINO;
             bisiesto = 0;
     |FINSI;
     |SI mesnum =  1;  topemes = 31;            |FINSI;
     |SI mesnum =  2;  topemes = 28 + bisiesto; |FINSI;
     |SI mesnum =  3;  topemes = 31;            |FINSI;
     |SI mesnum =  4;  topemes = 30;            |FINSI;
     |SI mesnum =  5;  topemes = 31;            |FINSI;
     |SI mesnum =  6;  topemes = 30;            |FINSI;
     |SI mesnum =  7;  topemes = 31;            |FINSI;
     |SI mesnum =  8;  topemes = 31;            |FINSI;
     |SI mesnum =  9;  topemes = 30;            |FINSI;
     |SI mesnum = 10;  topemes = 31;            |FINSI;
     |SI mesnum = 11;  topemes = 30;            |FINSI;
     |SI mesnum = 12;  topemes = 31;            |FINSI;
|FPRC;

|PROCESO GrabaTMP;
     |DDEFECTO #200;
     #200#0 = #labtraba#0;
     #200#1 = #labtraba#77;
     #200#2 = #labtraba#1;
     #200#3 = nAny + 2000;
     #200#4 = nMes;
     #200#5 = nDiaDesde;
     #200#6 = nDiaHasta;
     #200#7 = diasabse;
     #200#8 = dialabo;
     nIndice = diasabse / dialabo;
     #200#9 = (nIndice * 100) ! 2;
     |SI #200#9 ] 20;
          #200#10 = 1;
     |FINSI;
     |SI #200#9 ] 25;
          #200#11 = 1;
     |FINSI;
     |GRABA 020#200n;
     |LIBERA #200;
|FPRC;

|PROCESO CargaDias;                 || CARGA LOS DIAS LABORABLES
     |DDEFECTO #20;
     |ABRE #20;
     #20#0  = #labtraba#156;
     #20#13 = anynum;
     |LEE 000#20=;
     |CIERRA #20;

     nume1 = nMes;
     meslabo = #20nume1;

     dialabo = 0;

     |PARA mid = nDiaDesde; |SI mid [ nDiaHasta; |HACIENDO mid = mid + 1;
          nume1 = (mid * 100) + 1;
          alfa1 = meslabo % nume1;
          |SI alfa1 = "0";
               dialabo = dialabo + 1;     || cuenta dias laborables
          |FINSI;
     |SIGUE;
|FPRC;

|PROCESO LeeMante;
     swSigue = 0;

     #100#0 = #labtraba#0;
     #100#1 = #labtraba#1;
     #100#2 = nMes;
     #100#3 = 0;
     #100#66 = nAny;
     |LEE 000#100=;
     |SI FSalida = 0;
          aFuente = "H";
          nDiasAbsMante = #100#34;
          swSigue = 1;
     |SINO;
          |SI nMes [ #0#7;
               #101#0 = #labtraba#0;
               #101#1 = #labtraba#1;
               #101#2 = nMes;
               #101#3 = 0;
               |LEE 000#101=;
               |SI FSalida = 0;
                    aFuente = "M";
                    nDiasAbsMante = #101#34;
                    swSigue = 1;
               |FINSI;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO BuscaMes;
     nIndice = 0;
     dialabo = 0;
     diasabse = 0;
     nDiasAbsMante = 0;
     |HAZ LeeMante;
     |SI swSigue = 1;
          |SI aFuente = "M";
               nAny = #0#8 - 2000;
          |FINSI;
          mesnum = nMes;
          anynum = nAny + 2000;
          |HAZ ulti_dia;

          |SI nMes = nMesBus; |Y nAny = nAnyBus;
               alfa1 = nDiaBus;
               alfa1 = ("00" + alfa1) % -102;
          |SINO;
               alfa1 = "01";
          |FINSI;
          |SI nMes = nMesRef; |Y nAny = nAnyRef;
               alfa2 = nDiaRef;
               alfa2 = ("00" + alfa2) % -102;
          |SINO;
               alfa2 = topemes;
          |FINSI;
          alfa3 = mesnum; alfa3 = "00" + mesnum; alfa3 = alfa3 % -102;
          alfa4 = anynum;
          aFechaDesde = alfa1 + "." + alfa3 + "." + alfa4;
          aFechaHasta = alfa2 + "." + alfa3 + "." + alfa4;

          nDiaDesde = alfa1;
          nDiaHasta = alfa2;

          fFechaDesde = aFechaDesde;
          fFechaHasta = aFechaHasta;

          aFechaAlta = #labtraba#36;
          aFechaBaja = #labtraba#37;

          fFechaAlta = aFechaAlta;
          fFechaBaja = aFechaBaja;
          aDiaAlta = aFechaAlta % 102;
          aMesAlta = aFechaAlta % 402;
          aAnyAlta = aFechaAlta % -104;
          aDiaBaja = aFechaBaja % 102;
          aMesBaja = aFechaBaja % 402;
          aAnyBaja = aFechaBaja % -104;
          nDiaAlta = aDiaAlta;
          nDiaBaja = aDiaBaja;

          |SI aMesAlta = alfa3; |Y aAnyAlta = alfa4; |Y nDiaAlta > nDiaDesde;
               fFechaDesde = fFechaAlta;
               nDiaDesde = nDiaAlta;
          |FINSI;

          |SI aFechaBaja ! "          "; |Y aFechaBaja ! "..........";
               |SI aMesBaja = alfa3; |Y aAnyBaja = alfa4; |Y nDiaBaja < nDiaHasta;
                    fFechaHasta = fFechaBaja;
                    nDiaHasta = nDiaBaja;
               |FINSI;
          |FINSI;
          |HAZ CargaDias;

          |SI nDiasAbsMante > 0;    || tengo absentismo
               |BUCLE Abs;
               |HAZ AbsDelDeAntes;
          |FINSI;

          |HAZ GrabaTMP;
     |FINSI;
|FPRC;

|PROCESO labtraba;
     diasabse = 0;
     |PARA nMes = nMesBus; |SI nMes [ 12; |HACIENDO nMes = nMes + 1;
          nAny = nAnyBus - 2000;
          |HAZ BuscaMes;
     |SIGUE;
     |SI nAnyRef ! nAnyBus;
          |PARA nMes = 1; |SI nMes [ nMesRef; |HACIENDO nMes = nMes + 1;
               nAny = nAnyRef - 2000;
               |HAZ BuscaMes;
          |SIGUE;
     |FINSI;
|FPRC;

|DEFBUCLE labtraba;
     #labtraba#1;
     77;
     #0#0, #0#4, #0#2;
     #0#1, #0#5, #0#3;
     ;
     NULL, labtraba;
|FIN;

|PROGRAMA;
     |CLS;
     |PINPA #0#0;
     |PINDA #0#0;
     |ENDATOS #1#0;
     |SI FSalida = 0;

          nMesRef = #0#7;
          nAnyRef = #0#8;
          aAnyRef = nAnyRef;
          aMesRef = nMesRef;
          aMesRef = ("00" + aMesRef) % -102;

          mesnum = nMesRef;
          anynum = nAnyRef;
          |HAZ ulti_dia;
          nDiaRef = topemes;
          aDiaRef = nDiaRef;
          aDiaRef = ("00" + aDiaRef) % -102;

          aFechaRef = aDiaRef + "." + aMesRef + "." + aAnyRef;
          fFechaRef = aFechaRef;

/*
          fFechaRef = #0#6;
          aFechaRef = fFechaRef;
          aDiaRef = aFechaRef % 102;
          nDiaRef = aDiaRef;
          aMesRef = aFechaRef % 402;
          nMesRef = aMesRef;
          aAnyRef = aFechaRef % -104;
          nAnyRef = aAnyRef;
*/
          nAnyBus = nAnyRef - 1;
          aAnyBus = nAnyBus;
          aFechaBus = aDiaRef + "." + aMesRef + "." + aAnyBus;
          fFechaBus = aFechaBus;
          nFechaBus = fFechaBus;
          nFechaBus = nFechaBus + 1;
          fFechaBus = nFechaBus;        || fecha desde la que empiezo a buscar
                                        || justo un a¤o atras

          aFechaBus = fFechaBus;
          aDiaBus = aFechaBus % 102;
          aMesBus = aFechaBus % 402;
          aAnyBus = aFechaBus % -104;

          nDiaBus = aDiaBus;
          nMesBus = aMesBus;
          nAnyBus = aAnyBus;

          alfa1 = Usuario; |QBT alfa1;    alfa1 = ("l" + alfa1) % 108;
          |NOME_DAT #200 alfa1;
          |DELINDEX #200;

          alfa1 = Usuario; |QBT alfa1;    alfa1 = ("n" + alfa1) % 108;
          |NOME_DAT #201 alfa1;
          |DELINDEX #201;

          |ABRE #100;
          |ABRE #101;
          |ABRE #200;
          |ABRE #201;
          |ABRE #labempre;
          |ABRE #labcentr;
          |ABRE #labtraba;

          |BUCLE labtraba;
          |BUCLE Empresa;
          |BUCLE Candidatos;
          |HAZ Impresion;

          |CIERRA #labtraba;
          |CIERRA #labcentr;
          |CIERRA #labempre;
          |CIERRA #201;
          |CIERRA #200;
          |CIERRA #101;
          |CIERRA #100;
     |FINSI;
|FPRO;
