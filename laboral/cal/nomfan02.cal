|FICHEROS;
     nomconst #0,MANTE;
     nomfan02;
     nomregim;
     nomcopat;
     nompanta;
|FIN;

|VARIABLES;
     aAlfa = "";
     &nRegimen = 0;
     nCampo = 0;
     aParam = "";

     n82 = 0;
     n83 = 0;
     n84 = 0;
     n85 = 0;
     n86 = 0;
     n87 = 0;
     aModulo = "";
     &eaVengoDe = "";

     fDesdeFecha = @;
     fHastaFecha = @;

     fFecha = @;
|FIN;

|PROCESO GrabaForm;
     || Para que en ninguna constante de las antiguas queden sin grabarse las
     || cuotas de formacion, si no luego pueden salir mal algunos seguros sociales

     #nomconst#82 = n82;
     #nomconst#83 = n83;
     #nomconst#84 = n84;
     #nomconst#85 = n85;
     #nomconst#86 = n86;
     #nomconst#87 = n87;

     |GRABA 020#nomconst.a;
|FPRC;

|DEFBUCLE GrabaForm;
     #nomconst#2;
     ;
     001;
     499;
     be;
     NULL, GrabaForm;
|FIN;

|PROCESO ParcheAntFor;
     || AQUI ME QUEDARE CON LAS ULTIMAS CUOTAS DE FORMACION DE LA CONSTANTE 500

     |SI aParam = "ACTUALIZACION";
          |SI #nomcopat#0 = 500;
               n82 = #nomcopat#82;
               n83 = #nomcopat#83;
               n84 = #nomcopat#84;
               n85 = #nomcopat#85;
               n86 = #nomcopat#86;
               n87 = #nomcopat#87;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO copia500;
     |HAZ ParcheAntFor;
     |PARA nCampo = 0; |SI nCampo [ 93; |HACIENDO nCampo = nCampo + 1;
          #nomconst#nCampo# = #nomcopat#nCampo#;
     |SIGUE;
     |GRABA 020#nomconst.n;
|FPRC;

|DEFBUCLE copia500;
     #nomcopat#2;
     ;
     500;
     999;
     be;
     NULL, copia500;
|FIN;

|PROCESO borra500;
     |BORRA 020#nomconst.a;
|FPRC;

|DEFBUCLE borra500;
     #nomconst#2;
     ;
     500;
     999;
     be;
     NULL, borra500;
|FIN;

|PROCESO Actualizacion;
     |MODULO aModulo;
     |SI FSalida = 14; |O aParam = "ACTUALIZACION"; |O aParam = "ACTUALIZACIONRL";
          |SI aParam ! "ACTUALIZACIONRL";
          |Y eaVengoDe ! "nomcalcu"; |Y eaVengoDe ! "gomcalcu";
          |Y eaVengoDe ! "nompasec"; |Y eaVengoDe ! "labtraba";
               aAlfa = "     Desea actualizar las constantes de la aplicacion ";
               aAlfa = aAlfa + " (>500) segun la normativa legal?"
               |CONFI aAlfa;
          |SINO;
               FSalida = 0;
          |FINSI;
          |SI FSalida = 0;
               |BUCLE borra500;
               |ABRE #nomconst;
               |BUCLE copia500;
               |BUCLE GrabaForm;
               |CIERRA #nomconst;
               aAlfa = "     Se han actualizado las Constantes legales segun los tipos ";
               aAlfa = aAlfa + "de cotización PROVISIONALES para 2012";
               |SI aParam = "ACTUALIZACIONRL";
                    |PUSHV 05012380;
                    |BLANCO 10012380;
                    |PINPA #4#nompanta;
                    |AVISO;
                    |PAUSA;
                    |POPV;
               |SINO;
                    ||MENAV aAlfa;
                    || desactivo, que ahora se va a actualizar cada dos por tres
                    || hasta que la tesoreria se aclare
               |FINSI;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Tipo3; |TIPO 3;
     |SI FSalida = 0;
          nRegimen = #nomfan02#0;
          |SI nRegimen = 163;
               nRegimen = 613;
          |FINSI;
          |ENTLINEAL #3#0, -1, 2, 22, 1, inf, sup;
     |SINO;
          |ACABA;
     |FINSI;
|FPRC;

|PROCESO PintaReg;
     |ABRE #nomregim;
     #nomregim#0 = #nomfan02#0;
     |LEE 000#nomregim.=;
     |SI FSalida = 0;
          aAlfa = #nomregim#1;
          aAlfa = aAlfa % 124;
          |ATRI R;
          |PINTA 0556, aAlfa;
          |ATRI I;
     |SINO;
          aAlfa = "    Regimen inexistente";
          |MENAV aAlfa;
          |ERROR;
     |FINSI;
     |CIERRA #nomregim;
|FPRC;

|PROCESO inf;
     #0#0 = 001;
     #0#79 = fDesdeFecha;
     || #0#88 = #nomfan02#0;
     #0#88 = nRegimen;
|FPRC;

|PROCESO sup;
     #0#0 = 999;
     #0#79 = fHastaFecha;
     || #0#88 = #nomfan02#0;
     #0#88 = nRegimen;
|FPRC;

|PROCESO ActRegFec;
     |SI #0#88 = 0;
          |BORRA 020#nomconst.a;
          #0#88 = 111;
          #0#79 = "01.01.1900";
          |GRABA 020#nomconst.n;
     |FINSI;
     |LIBERA #nomconst;
|FPRC;

|DEFBUCLE ActRegFec;
     #nomconst#1;
     ;
     001, "01.01.1900";
     100, "31.12.2999";
     be;
     NULL, ActRegFec;
|FIN;

|PROGRAMA;
     aParam = PARAMETRO;
     |QBF aParam;
     aAlfa = aParam % 113;
     |SI aAlfa = "ACTUALIZACION"
          |BUCLE ActRegFec;
          |HAZ Actualizacion;
     |SINO;
          aAlfa = aParam % 104;

          |SI aAlfa = "SOLO";
               fFecha = ~;
               aAlfa = fFecha;
               aAlfa = aAlfa % -104;

               aAlfa = "01.01." + aAlfa;
               fDesdeFecha = aAlfa;
               fHastaFecha = "31.12.2999";
          |SINO;
               fDesdeFecha = "01.01.1900";
               fHastaFecha = "31.12.2999";
          |FINSI;
          |CLS;
          ||ENTLINEAL #1#0, -1, 2, 22, 1, inf, sup;
          |PINPA #0#nomconst;
          |PINPA #0#nomfan02;
          |ENDATOS #1#nomfan02;
     |FINSI;
|FPRO;
