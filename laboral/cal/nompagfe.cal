|FICHEROS;
     nompagfe #0;
|FIN;

|VARIABLES;
     swEsFecha = 0;
     swVacio = 0;
     aAlfa = "";
     aAlfa1 = "";
     aAlfa2 = "";
     aLon = "";
     nLon = 0;
     aMes = "";
     nMes = 0;
     aDia = "";
     nDia = 0;
     nTopeMes = 0;
     aPunto = "";
     swEsMes = 0;
|FIN;

|PROCESO TopeMes;
     |SI nMes = 1; |O nMes = 3; |O nMes = 5; |O nMes = 7; |O nMes = 8;
     |O nMes = 10; |O nMes = 12;
          nTopeMes = 31;
     |FINSI;
     |SI nMes = 4; |O nMes = 6; |O nMes = 9; |O nMes = 11;
          nTopeMes = 30;
     |FINSI;
     |SI nMes = 2;
          nTopeMes = 28;
     |FINSI;
|FPRC;

|PROCESO EsFecha;
     swEsFecha = 0;
     swEsMes = 0;
     swVacio = 0;
     aAlfa = #0Campo;
     |QBF aAlfa;
     |SI aAlfa = "";               || Vacio: no se ha metido nada
          swVacio = 1;
          |ACABA;
     |FINSI;
     aLon = aAlfa % 0;
     nLon = aLon;
     |SI nLon ! 5;                 || Error: Longitud cadena distinto a 5
          |ACABA;
     |FINSI;
     aPunto = aAlfa % 301;
     |SI aPunto ! ".";              || Error: Lo de en medio no es un punto
          |ACABA;
     |FINSI;
     aMes = aAlfa % -102;
     nMes = aMes;
     |SI nMes < 1; |O nMes > 12;   || Error: Mes no esta entre 1 y 12
          swEsMes = -1;  || fecha no valida
          |ACABA;
     |FINSI;
     |HAZ TopeMes;
     aDia = aAlfa % 102;
     nDia = aDia;
     |SI nDia > nTopeMes; |O nDia = 0;  || Error: Dia mayor que limite o = 0;
          swEsMes = -1;  || fecha no valida
          |ACABA;
     |FINSI;
     swEsMes = 1;
|FPRC;

|PROCESO Compruebalo;
     |HAZ EsFecha;
     |SI swEsMes ! 1; |Y swVacio ! 1;
          |SI swEsMes = 0;
               aAlfa = "    ATENCION. El formato de fecha valido es DD.MM"
          |FINSI;
          |SI swEsMes = -1;
               aAlfa = "    ATENCION. Fecha NO valida. Revise Dia/Mes"
          |FINSI;
          |MENAV aAlfa;
          |ERROR;
     |FINSI;
|FPRC;
