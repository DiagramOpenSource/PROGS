/*

|| PROCESOS QUE YA NO SE USAN Y QUE SE GUARDAN POR SI ACASO

|PROCESO DiasPago;
|| a ver, esta ley ya esta mas que caducada, todos los contratos temporales
|| a estas alturas (2018) son 12 dias por a¤o trabajado

     |HAZ BuscaModulo;
     |SI aFechaFIN ! "";
          |ACABA;        || no hagas esto con el informe de Sykes
     |FINSI;
     |MODULO aAlfa;

     nContrato = #labtraba#45;
     |SI nContrato ] 400; |Y #nomfinli#6 = "D"; |Y #nomfinli#7 = 12;
          || para los contratos de duracion determinada, los dias de indem-
          || nizacion estan estipulados segun la fecha de alta

          aAlfa = #labtraba#36;
          aAlfa = aAlfa % -104;
          nAnyAlta = aAlfa;

          |SI nAnyAlta [ 2011; nDiasLey = 8; |FINSI;
          |SI nAnyAlta = 2012; nDiasLey = 9; |FINSI;
          |SI nAnyAlta = 2013; nDiasLey = 10; |FINSI;
          |SI nAnyAlta = 2014; nDiasLey = 11; |FINSI;
          |SI nAnyAlta ] 2015; nDiasLey = 12; |FINSI;
     |FINSI;

     |SI nDiasLey ! #nomfinli#5;
          |SI nDiasLey = 8;
               aAlfa2 = " ocho";
               aAlfa3 = "hasta el 31 de diciem-";
               aAlfa4 = "bre de 2011.";
          |FINSI;
          |SI nDiasLey = 9;
               aAlfa2 = "nueve";
               aAlfa3 = "a partir del 1 de ene-";
               aAlfa4 = "ro de 2012.";
          |FINSI;
          |SI nDiasLey = 10;
               aAlfa2 = " diez";
               aAlfa3 = "a partir del 1 de ene-";
               aAlfa4 = "ro de 2013.";
          |FINSI;
          |SI nDiasLey = 11;
               aAlfa2 = " once";
               aAlfa3 = "a partir del 1 de ene-";
               aAlfa4 = "ro de 2014.";
          |FINSI;
          |SI nDiasLey = 12;
               aAlfa2 = " doce";
               aAlfa3 = "a partir del 1 de ene-";
               aAlfa4 = "ro de 2015.";
          |FINSI;

          |PUSHV 05012380;
          |BLANCO 10102271;
          |AVISO;
          |PINPA #5#nompanta;
          |ATRI S;
          |PINTA 1664, aAlfa2;
          |PINTA 1847, aAlfa3;
          |PINTA 1913, aAlfa4;
          |ATRI 0;
          |PAUSA;

          aAlfa1 = #nomfinli#5;
          aAlfa2 = nDiasLey;

          aAlfa = "    S¿Desea aplicar la indemnización de " + aAlfa2;
          aAlfa = aAlfa + " días (según Ley) en lugar de " + aAlfa1;
          aAlfa = aAlfa + " días (según Convenio)?";
          |CONFI aAlfa;

          |SI FSalida = 0;
               nDiasPago = nDiasLey;
          |FINSI;

          |POPV;
     |FINSI;
|FPRC;

|| *** PROCESOS PARA CONTROLAR LA BASE DE COTIZACION DE LOS FUNCIONARIOS ****
|| ** A PARTIR DEL MES DE JUNIO DE 2010 (HASTA DICIEMBRE 2010 DE MOMENTO) ***

|PROCESO BuscaBaseDiaria;
     |ABRE #nomred04;
     #nomred04#0 = #labtraba#0;
     #nomred04#1 = #labtraba#1;
     #nomred04#2 = nAnyRef;
     #nomred04#3 = nMesRef;
     |LEE 101#nomred04.=;
     |SI FSalida = 0;
          nRemunMayoDia = #nomred04#63;
          || nRemunMesRefApli = #nomred04#64;
          || remuneracion aplicada en el mes de referencia en verdad
          || lo comento, no lo usare de momento, ver comentario buscando
          || "475.1162"

          nRemunMayoTeor = #nomred04#19;
          || atencion, esto es nuevo, parece ser la solucion
          || si en diciembre use la remuneracion de Mayo, tengo que seguir
          || usando esa como base, ni quitar posibles nuevos aperiodicos
          || ni nada de nada
          || pero solo en este caso, si use la que salio real de la nomina
          || del mes tengo que volver a buscar y quitar aperiodicos si
          || hubiera alguno que no era en Diciembre
          || 475.1197 (que viene del 475.1162)
     |FINSI;
     |CIERRA #nomred04;
|FPRC;

|PROCESO RedFunc2010_Fin;
     |SI runnom = "nomcalfi"; |O runnom = "nomcalir";
          || no en finiquitos ni IRPF
          |ACABA;
     |FINSI;

     |ABRE #nomred04;
     #nomred04#0 = #labtraba#0;
     #nomred04#1 = #labtraba#1;
     #nomred04#2 = nElAny;
     #nomred04#3 = nElMes;
     |LEE 101#nomred04.=;
     |SI FSalida = 0;
          |ABRE #nomcalca;
          #nomcalca#0 = #labtraba#0;
          #nomcalca#1 = #labtraba#1;
          #nomcalca#2 = nElMes;
          #nomcalca#3 = 0;
          |LEE 000#nomcalca.=;
          |CIERRA #nomcalca;

          #nomred04#12 = #nomcalca#37;
          #nomred04#13 = #nomcalca#38;
          #nomred04#14 = #nomcalca#39 + #nomcalca#117;
          #nomred04#61 = #nomcalca#62;
          #nomred04#66 = #nomcalca#41 + #nomcalca#42;  || base horas extras
          |GRABA 020#nomred04.a;
          |LIBERA #nomred04;
     |FINSI;
     |CIERRA #nomred04;
|FPRC;

|PROCESO DiasMesRef;
     |ABRE #nomcaldl;
     #nomcaldl#0 = #labtraba#0;
     #nomcaldl#1 = #labtraba#1;
     #nomcaldl#2 = nAnyRef;
     #nomcaldl#3 = nMesRef;
     #nomcaldl#4 = 0;
     #nomcaldl#5 = 011000;
     |LEE 000#nomcaldl.=;
     |SI FSalida = 0;
          nDiasMayo = #nomcaldl#8;
     |FINSI;

     #nomcaldl#5 = 111000;
     |LEE 000#nomcaldl.=;
     |SI FSalida = 0;
          nDiasMayo = nDiasMayo + #nomcaldl#8;
     |FINSI;

     #nomcaldl#5 = 211000;
     |LEE 000#nomcaldl.=;
     |SI FSalida = 0;
          nDiasMayo = nDiasMayo + #nomcaldl#8;
     |FINSI;

     |CIERRA #nomcaldl;
|FPRC;

|PROCESO nomred03;
     |SI swPaso = 1;
          #nomhicli#0 = #labtraba#0;
          #nomhicli#1 = #labtraba#1;
          #nomhicli#12 = nAnyRef - 2000;
          #nomhicli#2 = nMesRef;
          #nomhicli#3 = 0;
          #nomhicli#4 = #nomred03#1;
          |LEE 000#nomhicli.=;
          |SI FSalida = 0; |Y #nomhicli#11 > 0;
               nRemunMayoAper = nRemunMayoAper + (#nomhicli#11 * #nomhicli#10);
               nCampo1 = 20;
               |PARA nCampo1 = 20; |SI nCampo1 [ 37; |HACIENDO nCampo1 = nCampo1 + 4;
                    |SI #nomred04#nCampo1# = 0;
                         nCampo2 = nCampo1 + 1;
                         nCampo3 = nCampo1 + 2;
                         nCampo4 = nCampo1 + 3;
                         #nomred04#nCampo1# = #nomhicli#4;
                         #nomred04#nCampo2# = #nomhicli#11;
                         #nomred04#nCampo3# = #nomhicli#10;
                         #nomred04#nCampo4# = #nomhicli#11 * #nomhicli#10;
                         |SAL;
                    |FINSI;
               |SIGUE;
          |FINSI;
     |FINSI;
     |SI swPaso = 2;
          #nomcalli#0 = #labtraba#0;
          #nomcalli#1 = #labtraba#1;
          #nomcalli#2 = nElMes;
          #nomcalli#3 = 0;
          #nomcalli#4 = #nomred03#1;
          |LEE 000#nomcalli.=;
          |SI FSalida = 0; |Y #nomcalli#11 > 0;
               nRemunMesAper = nRemunMesAper + (#nomcalli#11 * #nomcalli#10);
               |PARA nCampo1 = 40; |SI nCampo1 [ 57; |HACIENDO nCampo1 = nCampo1 + 4;
                    |SI #nomred04#nCampo1# = 0;
                         nCampo2 = nCampo1 + 1;
                         nCampo3 = nCampo1 + 2;
                         nCampo4 = nCampo1 + 3;
                         #nomred04#nCampo1# = #nomcalli#4;
                         #nomred04#nCampo2# = #nomcalli#11;
                         #nomred04#nCampo3# = #nomcalli#10;
                         #nomred04#nCampo4# = #nomcalli#11 * #nomcalli#10;
                         |SAL;
                    |FINSI;
               |SIGUE;
          |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE nomred03;
     #nomred03#1;
     ;
     #labtraba#87, 001;
     #labtraba#87, 999;
     ;
     NULL, nomred03;
|FIN;

|PROCESO Aperiodicos;
     || cuando toque

     |ABRE #nomhicli;

     swPaso = 1;
     |BUCLE nomred03;

     |CIERRA #nomhicli;

     |SALVA_FICHA #nomcalli;

     swPaso = 2;
     |BUCLE nomred03;

     |REPON_FICHA #nomcalli;
|FPRC;

|PROCESO MesReferencia;
     |ABRE #nomhicca;
     #nomhicca#0 = #labtraba#0;
     #nomhicca#1 = #labtraba#1;
     #nomhicca#66 = nAnyRef - 2000;
     #nomhicca#2 = nMesRef;
     #nomhicca#3 = 0;
     |LEE 000#nomhicca.=;
     |SI FSalida = 0;
          |SI nRemunMesRefApli ! 0;
               || para 2011, que me coja la cantidad que realmente formo
               || parte de la base de cotizacion en diciembre de 2010
               nRemunMayo = nRemunMesRefApli;
          |SINO;
               nRemunMayo = #nomhicca#37;
          |FINSI;

          #nomred04#4 = nRemunMayo;
          #nomred04#5 = #nomhicca#38;
          #nomred04#6 = #nomhicca#39 + #nomhicca#159;
          #nomred04#65 = #nomhicca#41 + #nomhicca#42;
          #nomred04#7 = nRemunMayo + #nomred04#65;
          #nomred04#60 = #nomhicca#62;
     |SINO;
          |SALVA_FICHA #nomcalca;

          #nomcalca#0 = #labtraba#0;
          #nomcalca#1 = #labtraba#1;
          #nomcalca#2 = nMesRef;
          #nomcalca#3 = 0;
          |LEE 000#nomcalca.=;
          |SI FSalida = 0;
               nRemunMayo = #nomcalca#37;

               #nomred04#4 = nRemunMayo;
               #nomred04#5 = #nomcalca#38;
               #nomred04#6 = #nomcalca#39 + #nomcalca#117;
               #nomred04#65 = #nomcalca#41 + #nomcalca#42;
               #nomred04#7 = nRemunMayo + #nomred04#65;
               #nomred04#61 = #nomcalca#62;
          |FINSI;

          |REPON_FICHA #nomcalca;
     |FINSI;
     |CIERRA #nomhicca;
|FPRC;

|PROCESO AvisaFun;
     aAlfa1 = #labtraba#0; |QBF aAlfa1; aAlfa1 = ("00000" + aAlfa1) % -105;
     aAlfa2 = #labtraba#1; |QBF aAlfa2; aAlfa2 = ("00000" + aAlfa2) % -105;
     aAlfa = aAlfa1 + "." + aAlfa2;

     |PUSHV 05012380;
     |BLANCO 10012380;
     |PINPA #7#nompanta;
     |ATRI R;
     |PINTA 1427, aAlfa;
     |ATRI 0;
     |AVISO;
     |PAUSA;
|FPRC;

|PROCESO RedFunc2010;
     nRemunMes = 0;
     nRemunMayo = 0;
     nDiasMayo = 0;
     nRemunMesAper = 0;
     nRemunMayoAper = 0;
     nRemunMesDia = 0;
     nRemunMayoDia = 0;
     nRemunMayoTeor = 0;
     nRemunMesOri = 0;
     swFuncRecorte = 0;
     nNuevaAcum10 = 0;
     nRemunMesRefApli = 0;

     |SI runnom = "nomcalfi"; || no en finiquitos
          |ACABA;
     |FINSI;

     |SI #labtraba#97 = 4; |Y nEstoyEnAjuste = 0;    || solo entrara aqui en
          |ACABA;             || caso de que este en el ajuste, antes entra
     |FINSI;                  || una vez desde el calcotiz que no debe entrar
                              || si no, empieza mal el ajuste
     swSigue = 0;
     |SI nElAny = 2010; |Y nElMes ] 06;
          nAnyRef = 2010
          nMesRef = 5;
          swSigue = 1;
     |FINSI;
     |SI nElAny ] 2011;
          nAnyRef = 2010
          nMesRef = 12;
          swSigue = 1;
          |HAZ BuscaBaseDiaria;
     |FINSI;
     |SI swSigue = 1;
          |ABRE #nomred04;
          #nomred04#0 = #labtraba#0;
          #nomred04#1 = #labtraba#1;
          #nomred04#2 = nElAny;
          #nomred04#3 = nElMes;
          |LEE 101#nomred04.=;
          |SI FSalida = 0;
               |SI runnom ! "nomcalir";
               || en calculos/estimaciones de IRPF deja el registro como esta
               || por favor
                    |BORRA 020#nomred04.a;
               |FINSI;
          |FINSI;
          |DDEFECTO #nomred04;

          |ABRE #nomred01;
          #nomred01#0 = #labtraba#0;
          #nomred01#1 = #labtraba#1;
          |LEE 000#nomred01.=;
          |SI FSalida = 0;
               swFuncRecorte = 1;

               || PRIMERO BUSCO LA REMUNERACION DE MAYO DE 2010 Y LA DEL
               || MES QUE ESTOY CALCULANDO

               |HAZ MesReferencia;

               nRemunMes = acum_10 + exce_smi;

               #nomred04#0 = #labtraba#0;
               #nomred04#1 = #labtraba#1;
               #nomred04#2 = nElAny;
               #nomred04#3 = nElMes;

               || AHORA DEBERIA QUITAR TANTO DE LA DEL MES DE REFERENCIA
               || COMO LA DEL MES DE CALCULO LOS CONCEPTOS APERIODICOS

               |HAZ Aperiodicos;

               #nomred04#7 = nRemunMayo + #nomred04#65;
               nRemunMayo = nRemunMayo - nRemunMayoAper;
               #nomred04#8 = nRemunMayoAper;
               #nomred04#9 = nRemunMayo + #nomred04#65;

               nRemunMesOri = nRemunMes;
               #nomred04#66 = dacum_30 + dacum_31;
               #nomred04#15 = nRemunMes + #nomred04#66;
               nRemunMes = nRemunMes - nRemunMesAper;
               #nomred04#16 = nRemunMesAper;
               #nomred04#17 = nRemunMes + #nomred04#66;

               || EN TERCER LUGAR BUSCO LOS DIAS COTIZABLES DE ALTA EN MAYO
               || DE 2010 PARA QUEDARME CON UNA BASE DIARIA, LAS DEL MES LAS
               || OBTENGO DIVIDIENDOLA POR LOS DIAS COTIZABLES EN ALTA

               |HAZ DiasMesRef;

               ||SI nElAny [ 2010;
               || en un principio esto no lo iba a hacer para 2011, lo que ha-
               || cia era recoger la base sin aperiodicos directamente de
               || lo que habia grabado de diciembre del nomred04
               || pero se da el caso de que si un concepto NO fue aperiodico
               || en diciembre, y ahora si lo es, ese calculo no es valido

               || a ver, otra vez igual ... lo dejo para que coja esa base
               || siempre, la que hubiera guardada, pero esto volvera a estar
               || mal caso de que haya conceptos aperiodicos ahora que en Diciembre
               || no lo fueron, si esto es asi, volver a pensar en esto
               || lo que hago es comentar el SI para que no pase por aqui en el
               || 2011, 475.1162

                    |SI nRemunMayo ! 0; |Y nDiasMayo ! 0;

                         |SI nRemunMayoTeor ! nRemunMayoDia;
                              nRemunMayoDia = nRemunMayoDia;

                              || me quedo con la que tenia, no usa la que he
                              || calculado de nuevo por si hubiera conceptos
                              || que comienzan a ser aperiodicos
                              || ver comentario donde se carga nRemunMayoTeor
                         |SINO;
                              nRemunMayoDia = (nRemunMayo + #nomred04#65) / nDiasMayo;
                         |FINSI;

                    |FINSI;
               ||FINSI;

               |SI nEl928 ! 0;               || por si lo metia manual
                    nRemunMayoDia = nEl928;
               |FINSI;

               |SI nDiasMayo = 0; |Y nEl928 = 0; |Y dcobr_0 ! 0;
               |Y runnom ! "nomcalir";
                    |SI nElAny ] 2011; |Y nRemunMayoDia ! 0;

                         || no avises porque aunque no tenga dias en diciembre
                         || tendre la base guardada
                    |SINO;
                         |HAZ AvisaFun;
                    |FINSI;
               |FINSI;

               #nomred04#10 = nDiasMayo;
               #nomred04#11 = nRemunMayoDia;

               nRemunMesDia = (nRemunMes + #nomred04#66) / dcobr_0;

               #nomred04#18 = dcobr_0;
               #nomred04#62 = dcobr_0;
               #nomred04#19 = nRemunMesDia;

               |SI nRemunMayoDia > nRemunMesDia;
                    #nomred04#63 = nRemunMayoDia;
               |SINO;
                    #nomred04#63 = nRemunMesDia;
               |FINSI;

               #nomred04#64 = #nomred04#63 * #nomred04#62;

               |SI runnom ! "nomcalir";
                    || no grabar en calculo de IRPF claro

                    |GRABA 020#nomred04.n;
                    |LIBERA #nomred04;
               |FINSI;

               || POR ULTIMO MODIFICO LAS BASES SI ES NECESARIO

               |SI nRemunMesDia < nRemunMayoDia;
                    acum_10 = dcobr_0 * nRemunMayoDia;
                    acum_10 = acum_10 + nRemunMesAper - #nomred04#66;
                    |SI nEstoyEnAjuste = 4;
                         acum_10 = acum_10 - nAjusteEBnor1 - nAjusteEBnor2 - nAjusteEBnor3;
                    |FINSI;
                    nNuevaAcum10 = acum_10;  || me la guardo que en los ajustes
                                             || ME PUEDE HACER FALTA
               |FINSI;
          |FINSI;

          |CIERRA #nomred04;
          |CIERRA #nomred01;
     |FINSI;
|FPRC;

|| ********************** FIN PROCESOS FUNCIONARIOS **************************

*/
