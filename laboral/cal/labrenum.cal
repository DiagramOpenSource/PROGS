|FICHEROS;
    labrenum #0;
    labregis #1;
    labimage #2;
|FIN;

|VARIABLES;
 inicio = 0;
 interv = 0;
 hasta = 0;
 desde = 0;
 campo = 0;
 primero = 0;
 ultimo = 0;
 limita = 0;
    mensa0 = "0000ATENCION! Superado el limite del contador ";
    mensa1 = "0000La Renumeracion provoca NUMEROS de CONTADOR DESBORDADOS !!!";
    mensa2 = "0000ATENCION! Error en datos ...";
    mensa3 = "0000AVISO! Hay registrados contratos con el CONTADOR DESBORDADO";
    mensa4 = "0000La Renumeracion DUPLICA el CONTRATO con CONTADOR -> ";
    mensaX = "";
    confi0 = "2320SContinuar con la Renumeracion S/N : ";
|FIN;

|PROCESO renumera; |TIPO 3;
    inicio = #0#3;
    interv = #0#2;   || inicializa los valores
    hasta = #0#1;
    desde = #0#0;

|ABRE #1;
|ET bucle_lee;
    |PINTA 2410, "Recorriendo : ";  |PINTA 2424, desde;
        #1#0 = desde;
    |LEE 040#1=;
    |SI FSalida = 0;

        |DDEFECTO #2;
        |PARA campo = 0;  |SI campo [ 26;  |HACIENDO campo = campo + 1;
            #2campo = #1campo;     || guarda en def el registro
        |SIGUE;

            #1#0 = inicio;
        |LEE 040#1=;
        |SI FSalida = 0;          || comprueba si existe el contador generado
                mensaX = #1#0;
                mensaX = mensa4 + mensaX;
            |MENSA mensaX;
            |AVISO;
            |ATRI P;
            |CONFI confi0;
            |ATRI 0;
            |SI FSalida ! 0;      || si no continua provoca el fin del bucle
                hasta = -1;
            |FINSI;
        |SINO;                    || si no existe borra el leido y graba
                #1#0 = desde;     || / el generado, recargandolo antes
            |BORRA 020#1c;
            |PARA campo = 0;  |SI campo [ 26;  |HACIENDO campo = campo + 1;
                #1campo = #2campo;
            |SIGUE;
                #1#0 = inicio;
            |HAZ recarga;
            |GRABA 020#1n;
            |PINTA 2445, "Grabando ...: ";  |PINTA 2460, inicio;
        |FINSI;
            inicio = inicio + interv;  || suma intervalo
        |SI inicio > 999999;
            |MENAV mensa1;
            |CONFI confi0;    || si se pasa de 999999, da mensaje y
            |SI FSalida ! 0;  || / pide confirmacion para seguir, sino
                hasta = -1;   || / provoca el fin del bucle y no graba
            |FINSI;
        |FINSI;

     |FINSI;

    desde = desde + 1;
|SI desde [ hasta;
    |VETE bucle_lee;
|FINSI;

|CIERRA #1;
|FPRC;



|PROCESO primero; |TIPO 7;
|ABRE #1;
|LEE 040#1p;
    |SI #1#0 < 1;
        |MENAV mensa3;
    |FINSI;
        primero = #1#0;
        #0#0 = #1#0;
    |PINTA #0#0;
|CIERRA #1;
|FPRC;


|PROCESO ultimo; |TIPO 7;
|ABRE #1;
|LEE 040#1u;
    |SI #1#0 > 999999;
        |MENAV mensa3;
    |FINSI;
        ultimo = #1#0;
        #0#1 = #1#0;
    |PINTA #0#1;
|CIERRA #1;
|FPRC;



|PROCESO mayor; |TIPO 0;
|SI #0#0 > #0#1;
    |MENAV mensa2;
    |ERROR;
|FINSI;
|SI #0#1 > ultimo;
    |MENAV mensa2;
        #0#1 = ultimo;
    |PINTA #0#1;
    |ERROR;
|FINSI;
|FPRC;



|PROCESO limite; |TIPO 0;
    limita = #0#2 + #0#3;
|SI limita > 999999;
    |MENAV mensa0;
    |ERROR;
|FINSI;
|FPRC;

|PROCESO recarga;
|SI #1#07 ! "..........";|Y #1#07 ! "          ";
        #1#23 = #1#07;
|SINO;
        #1#23 = "00.00.0000";
|FINSI;
|SI #1#08 ! "..........";|Y #1#08 ! "          ";
        #1#24 = #1#08;
|SINO;
        #1#24 = "00.00.0000";
|FINSI;
|SI #1#19 ! "..........";|Y #1#19 ! "          ";
        #1#25 = #1#19;
|SINO;
        #1#25 = "00.00.0000";
|FINSI;
|SI #1#20 ! "..........";|Y #1#20 ! "          ";
        #1#26 = #1#20;
|SINO;
        #1#26 = "00.00.0000";
|FINSI;
|FPRC;
