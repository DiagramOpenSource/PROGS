|FICHEROS;
     euro0101 #0;
     euro0001;
     euro0002;
     euro0003;

     euro0201;

     labempre;
     labtraba;
|FIN;

|VARIABLES;
     nEmp = 0;
     nTra = 0;
     nAny = 0;
     nMes = 0;
     nTip = 0;
     nDia = 0;
     aInf = "";
     nUni = 0;

     aLinea = "";
     aLon = "";
     nLon = 0;
     nColumna = 0;
     nPos = 0;
     nBusca = 0;
     aCaracter = "";
     aDato = "";
     aSeparador = "|";
     swLimpia = 0;

     aFichero = "";
     nHandle = 0;
     nLinea = 0;

     aRuta = "";
     aRuta0 = "";
     nSw = 0;
     i = 0;
     aFic = "";
     aAlfa = "";
     aAlfa1 = "";
     aDes = "";
     aPath = "";

     nLineaCon = 0;

     swAlguno = 0;
     FEstado = 0;
     nRegistros = 0;
     nTrabajadores = 0;
     swError1 = 0;
     swError2 = 0;
     swError3 = 0;
     nErrores = 0;
|FIN;

|PROCESO LimpiaLin;
     |BORRA 020#euro0002.a;
     |LIBERA #euro0002;
|FPRC;

|DEFBUCLE LimpiaLin;
     #euro0002#1;
     ;
     nEmp, nTra, nAny, nMes, 0, 01;
     nEmp, nTra, nAny, nMes, 0, 99;
     be;
     NULL, LimpiaLin;
|FIN;


|PROCESO GrabaError;
     |DDEFECTO #labempre;
     #labempre#0 = nEmp;
     |LEE 000#labempre.=;

     |DDEFECTO #labtraba;
     #labtraba#0 = nEmp;
     #labtraba#1 = nTra;
     |LEE 000#labtraba.=;

     #euro0201#0 = #labempre#0;
     #euro0201#1 = #labtraba#1;
     #euro0201#4 = 000;
     |LEE 000#euro0201.=;
     FEstado = FSalida;
     #euro0201#2 = #labempre#2;
     #euro0201#3 = #labtraba#2;

     |SI FEstado ! 0;
          #euro0201#4 = 000;
          #euro0201#5 = "";
          |GRABA 020#euro0201.n;
     |FINSI;

     |SI swError1 = 1;
          #euro0201#4 = 001;
          #euro0201#5 = "Empresa no encontrada";
          |LEE 000#euro0201.=;
          |SI FSalida ! 0;
               |GRABA 020#euro0201.n;
          |FINSI;
     |FINSI;
     |SI swError2 = 1;
          #euro0201#4 = 002;
          #euro0201#5 = "Trabajador no encontrado";
          |LEE 000#euro0201.=;
          |SI FSalida ! 0;
               |GRABA 020#euro0201.n;
          |FINSI;
     |FINSI;
     |SI swError3 = 1;
          #euro0201#4 = 003;
          #euro0201#5 = "Concepto " + aInf + " no encontrado";
          |LEE 000#euro0201.=;
          |SI FSalida ! 0;
               |GRABA 020#euro0201.n;
          |SINO;
               aAlfa = #euro0201#5;
               aAlfa1 = "Concepto " + aInf;
               aAlfa = aAlfa - aAlfa1;
               |SI FSalida ! 0;
                    aAlfa = aAlfa1 + ", " + aInf + " no encontrado";
               |FINSI;
               |GRABA 020#euro0201.a;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Graba;
     swError1 = 0;
     swError2 = 0;
     swError3 = 0;

     #labempre#0 = nEmp;
     |LEE 000#labempre.=;
     |SI FSalida ! 0;
          swError1 = 1;
     |FINSI;

     #labtraba#0 = nEmp;
     #labtraba#1 = nTra;
     |LEE 000#labtraba.=;
     |SI FSalida ! 0;
          swError2 = 1;
     |FINSI;

     #euro0001#0 = nEmp;
     #euro0001#1 = nTra;
     #euro0001#2 = nAny;
     #euro0001#3 = nMes;
     #euro0001#4 = 0;
     |LEE 101#euro0001.=;
     |SI FSalida ! 0;
          |SI swError1 = 0; |Y swError2 = 0;
               |GRABA 020#euro0001.n;
               nTrabajadores = nTrabajadores + 1;
          |FINSI;
     |FINSI;
     |LIBERA #euro0001;

     #euro0002#0 = nEmp;
     #euro0002#1 = nTra;
     #euro0002#2 = nAny;
     #euro0002#3 = nMes;
     #euro0002#4 = nTip;
     |PARA nLineaCon = 1; |SI nLineaCon [ 99; |HACIENDO nLineaCon = nLineaCon + 1;
          #euro0002#5 = nLineaCon;
          |LEE 000#euro0002.=;
          |SI FSalida ! 0;
               |SAL;
          |FINSI;
     |SIGUE;

     #euro0003#0 = nEmp;
     #euro0003#1 = aInf;
     |LEE 000#euro0003.=;
     |SI FSalida ! 0;
          swError3 = 1;
     |FINSI;

     #euro0002#5 = nLineaCon;
     #euro0002#6 = nDia;
     #euro0002#7 = aInf;
     #euro0002#8 = nUni;

     |SI swError1 = 0; |Y swError2 = 0; |Y swError3 = 0;
          |GRABA 020#euro0002.n;
     |SINO;
          nErrores = nErrores + 1;
     |FINSI;

     |LIBERA #euro0002;

     swAlguno = 1;
     nRegistros = nRegistros + 1;

     |HAZ GrabaError;
|FPRC;

|PROCESO Dato;
     |QBF aDato;
     |SI nColumna = 1; nEmp = aDato; |FINSI;
     |SI nColumna = 2; nTra = aDato; |FINSI;
     |SI nColumna = 3; nMes = aDato; |FINSI;
     |SI nColumna = 4; nTip = aDato; |FINSI;
     |SI nColumna = 5; nDia = aDato; |FINSI;
     |SI nColumna = 6; aInf = aDato; |FINSI;
     |SI nColumna = 7; nUni = aDato; |FINSI;
     |SI nColumna = 7;
          |HAZ Graba;
     |FINSI;
|FPRC;

|PROCESO LeeLinea;
     |QBF aLinea;
     aLon = aLinea % 0;
     nLon = aLon;
     nColumna = 1;
     aDato = "";
     |PARA nPos = 1; |SI nPos [ nLon; |HACIENDO nPos = nPos + 1;
          nBusca = (nPos * 100) + 1;
          aCaracter = aLinea % nBusca;
          aDato = aDato + aCaracter;
          |SI aCaracter = aSeparador;
               aDato = aDato - aSeparador;
               |HAZ Dato;
               aDato = "";
               nColumna = nColumna + 1;
               |SI swLimpia = 1; |Y nColumna = 5;
                    |BUCLE LimpiaLin;
                    nPos = 9999;
               |FINSI;
          |FINSI;
     |SIGUE;
     |SI swLimpia = 0;
          |HAZ Dato;     || el ultimo
     |FINSI;
|FPRC;

|PROCESO LeeFichero;
     |XABRE "C", aFichero, nHandle;
     |SI FSalida = 0;
          nLinea = 0;
          nAny = #0#2;
          |PARA; |SI FSalida ] 0; |HACIENDO;
               nLinea = nLinea + 1;
               |XLEE nHandle, 250, aLinea;
               |SI FSalida < 0;
                    |SAL;
               |FINSI;
               |SI nLinea ! 1;
                    |HAZ LeeLinea;
               |FINSI;
          |SIGUE;
     |SINO;
          |MENAV "    No se ha podido leer el fichero especificado";
     |FINSI;
     |XCIERRA nHandle;
|FPRC;

|PROCESO Imprime;
     |PRINT;
|FPRC;

|DEFBUCLE Imprime;
     #euro0201#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, Imprime;
|FIN;

|PROCESO Lectura;
     |ABRE #euro0001;
     |ABRE #euro0002;
     |ABRE #euro0003;
     |ABRE #labempre;
     |ABRE #labtraba;

     aAlfa = Usuario; |QBT aAlfa; aAlfa = "tm_euro0201_" + aAlfa;
     |NOME_DAT #euro0201#aAlfa#;
     |DELINDEX #euro0201;
     |ABRE #euro0201;

     swLimpia = 1;
     |HAZ LeeFichero;
     swLimpia = 0;
     |HAZ LeeFichero;

     |SI nErrores ! 0;
          Impresora = "CrystalReport";
          |IMPRE -1;
          |INFOR "euro0101";
          |BUCLE Imprime;
          |PIEINF;
          |FININF;
          |FINIMP;
     |FINSI;

     |CIERRA #euro0201;
     |DELINDEX #euro0201;
     |CIERRA #labempre;
     |CIERRA #labtraba;
     |CIERRA #euro0003;
     |CIERRA #euro0002;
     |CIERRA #euro0001;
|FPRC;

|PROCESO Manual; |TIPO 66;
     aRuta = "F";
     aFichero = "";
     |BROWSE aRuta;

     |SI aRuta ! "F";
          nSw = 0;
          aRuta0 = "";
          aLon = aRuta % A0;
          |PARA i = 1; |SI i < aLon; |HACIENDO i = i + 1;
               nPos = -1 * (i * 100 + 1);
               aAlfa = aRuta % nPos;
               |SI aAlfa = "/"; |O aAlfa = "\"; |O nSw = 1;
                    nSw = 1;
                    aRuta0 = aAlfa + aRuta0;
               |SINO;
                    aRuta0 = aAlfa + aRuta0;
                    aFic = aAlfa + aFic;
               |FINSI;
          |SIGUE;
          |QBT aFic;
          aFic = aFic < "";
          aDes = aPath + aFic;
          aDes = aDes < "";
          #0#1 = aRuta0;
          |PINTA #0#1;
     |FINSI;
|FPRC;

|PROGRAMA;
     |CLS;
     |PINPA #0#0;

     |ABRE #0;
     |DDEFECTO #0;
     |LEE 000#0p;
     |SI FSalida = 0;
          |BORRA 020#0a;
     |FINSI;

     |PINDA #0#0;

     |ENDATOS #1#0;
     |SI FSalida = 0;
          aFichero = #0#1;
          |QBF aFichero;
          |SI aFichero ! "";
               |HAZ Lectura;

               |SI swAlguno = 1;
                    |SI nErrores = 0;
                         aAlfa = nRegistros;
                         aAlfa = "    Se han importado " + aAlfa + " registros correctamente";
                         |MENAV aAlfa;
                    |FINSI;
               |SINO;
                    |MENAV "    No se han encontrado registros para importar";
               |FINSI;
          |FINSI;
     |FINSI;

     |GRABA 000#0n;
     |CIERRA #0;
|FPRO;
