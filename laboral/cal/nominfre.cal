|FICHEROS;
     nominfre #0;
     :integral/dsam0001 #1;
     labtraba #2;
     labempre #3;
     nominfrt #100;
     copcalcu@ #200;
|FIN;

|VARIABLES;
     FEstado = 0;
     fFecha = @;
     aFecha = "";
     aParam = "";
     aAlfa = "";
     aEstoyEn = "";
     &aPobProv = "";
     &aDirEmp = "";
     &aDomEmp = "";
     &aDia = "";
     &aMes = "";
     &aAny = "";
     nMes = 0;
     nAny = 0;
     nCopias = 0;
|FIN;

|PROCESO CargaDefectos;
     fFecha = ~;
     aFecha = fFecha;
     aMes = aFecha % 402;
     aAny = aFecha % -104;
     #0#15 = aMes;
     #0#16 = aAny;
     #0#12 = fFecha;
     #0#13 = fFecha;
     #0#17 = aParam;
|FPRC;

|PROCESO LeeDatosDespacho;
     |ABRE #dsam0001;
     #dsam0001#0 = 1;
     |LEE 000#dsam0001.=;
     |SI FSalida = 0;
          #nominfre#1 = #dsam0001#1;    || nombre
          #nominfre#2 = #dsam0001#3;    || domicilio
          #nominfre#3 = #dsam0001#4;    || poblacion
          #nominfre#4 = #dsam0001#5;    || provincia
          #nominfre#6 = #dsam0001#6;    || telefono 1
          #nominfre#7 = #dsam0001#7;    || telefono 2 (fax)
     |FINSI;
     |CIERRA #dsam0001;
|FPRC;

|PROCESO impresion;
     |PRINT;
|FPRC;

|DEFBUCLE impresion;
     #100#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, impresion;
|FIN;

|PROCESO mesaletra;
     |SI nMes = 1;  aMes = "Enero";          |FINSI;
     |SI nMes = 2;  aMes = "Febrero";        |FINSI;
     |SI nMes = 3;  aMes = "Marzo";          |FINSI;
     |SI nMes = 4;  aMes = "Abril";          |FINSI;
     |SI nMes = 5;  aMes = "Mayo";           |FINSI;
     |SI nMes = 6;  aMes = "Junio";          |FINSI;
     |SI nMes = 7;  aMes = "Julio";          |FINSI;
     |SI nMes = 8;  aMes = "Agosto";         |FINSI;
     |SI nMes = 9;  aMes = "Septiembre";     |FINSI;
     |SI nMes = 10; aMes = "Octubre";        |FINSI;
     |SI nMes = 11; aMes = "Noviembre";      |FINSI;
     |SI nMes = 12; aMes = "Diciembre";      |FINSI;
|FPRC;

|PROCESO VariablesImp;
     |ABRE #labempre;
     #labempre#0 = #0#14;
     |LEE 000#labempre.=;

     aPobProv = #0#3;
     |QBF aPobProv;
     aPobProv = aPobProv + " (" + #0#4;
     |QBF aPobProv;
     aPobProv = aPobProv + ")";

     aDomEmp = #labempre#3;
     |QBF aDomEmp;
     aDomEmp = aDomEmp + ", " + #labempre#4 + " - " + #labempre#5;

     aDirEmp = #labempre#6;
     |QBF aDirEmp;
     aDirEmp = aDirEmp + " (" + #labempre#9;
     |QBF aDirEmp;
     aDirEmp = aDirEmp + ")";

     aAlfa = #0#12;
     aDia = aAlfa % 102;
     aMes = aAlfa % 402;
     nMes = aMes;
     |HAZ mesaletra;
     aAny = aAlfa % -104;
     |CIERRA #labempre;
|FPRC;

|PROCESO Informe;
     |IMPRE 0;
     |SI FSalida = 0;
          |HAZ VariablesImp;
          |PARA nCopias = 1; |SI nCopias [ #0#18; |HACIENDO nCopias = nCopias + 1;
               |INFOR "nominfre";
               |BUCLE impresion;
               |PIEINF;
               |FININF;
          |SIGUE;
     |FINSI;
     |FINIMP;
|FPRC;

|PROCESO inferior;
     #100#1 = 00001;
     #100#3 = "          ";
|FPRC;

|PROCESO superior;
     #100#1 = 99999;
     #100#3 = "zzzzzzzzzz";
|FPRC;

|PROCESO Muestra;
     |ENTLINEAL #1#100, -1, 2, 22, 1, inferior, superior;
|FPRC;

|PROCESO GrabaTemporal;
     #100#0 = #200#0;
     #100#1 = #200#1;

     #labtraba#0 = #100#0;
     #labtraba#1 = #100#1;
     |LEE 000#labtraba.=;
     #100#2 = #labtraba#2;
     |SI #200#3 = 2;
          #100#3 = "Finiquito";
          |GRABA 020#100n;
     |SINO;
          #100#3 = "N¢mina";
          |GRABA 020#100n;
          |SI #200#16 ! 0;
               #100#3 = "Paga Extra";
               |GRABA 020#100n;
          |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE nomhicca;
     #200#1005;
     ;
     #nominfre#14, 00001, nAny, #nominfre#15, 0;
     #nominfre#14, 99999, nAny, #nominfre#15, 2;
     ;
     NULL, GrabaTemporal;
|FIN;

|DEFBUCLE nomcalca;
     #200#1004;
     ;
     #nominfre#14, 00001, #nominfre#15, 0;
     #nominfre#14, 99999, #nominfre#15, 2;
     ;
     NULL, GrabaTemporal;
|FIN;

|PROCESO Carga;
     aAlfa = Usuario;    |QBT aAlfa;    aAlfa = ("l" + aAlfa) % 108;
     |NOME_DAT #100 aAlfa;
     |DELINDEX #100;
     |ABRE #100;
     |ABRE #labtraba;

     |SI aParam = "M";
          |DBASE aAlfa; |QBF aAlfa; aAlfa = aAlfa + "def/nomcalca.mas";
          aEstoyEn = "";
          |CARGA_DEF aAlfa, copcalcu@;
          |SI FSalida = 0;
               aEstoyEn = "M";
               |BUCLE nomcalca;
          |SINO;
               aAlfa = "    ATENCION: Error en CARGA_DEF: " + aAlfa;
               |MENAV aAlfa;
          |FINSI;
          |DESCARGA_DEF #copcalcu@;
     |SINO;
          |DBASE aAlfa; |QBF aAlfa; aAlfa = aAlfa + "def/nomhicca.mas";
          aEstoyEn = "";
          |CARGA_DEF aAlfa, copcalcu@;
          |SI FSalida = 0;
               aEstoyEn = "H";
               nAny = #0#16 - 2000;
               |BUCLE nomhicca;
          |SINO;
               aAlfa = "    ATENCION: Error en CARGA_DEF: " + aAlfa;
               |MENAV aAlfa;
          |FINSI;
          |DESCARGA_DEF #copcalcu@;
     |FINSI;

     |CIERRA #labtraba;
     |CIERRA #100;
|FPRC;

|PROCESO ElProceso;
     |HAZ Carga;
     |HAZ Muestra;
     |HAZ Informe;
|FPRC;

|PROGRAMA;
     aParam = PARAMETRO;
     |QBF aParam;

     |CLS;
     |PINPA #0#0;

     |ABRE #nominfre;
     |LEE 101#0p;
     |SI FSalida ! 0;
          |HAZ LeeDatosDespacho;
          |HAZ CargaDefectos;
          |PINDA #0#0;
          |ENDATOS #1#0;
          FEstado = FSalida;
          |SI FSalida = 0;
               |GRABA 020#0n;
          |FINSI;
     |SINO;
          |HAZ CargaDefectos;
          |PINDA #0#0;
          |ENDATOS #2#0;
          FEstado = FSalida;
          |SI FSalida = 0;
               |GRABA 020#0a;
          |FINSI;
     |FINSI;
     |CIERRA #nominfre;
     |SI FEstado = 0;
          |HAZ ElProceso;
     |FINSI;
|FPRO;
