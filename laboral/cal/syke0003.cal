 |FICHEROS;
     syke0003  #0;
     syke0004  #100;     ||Tmp. Datos
     syke0006  #200;     ||Tmp. Horas, para la cuenta de columnas
     nomauxi1;
     nomauxi2;
     syke0014;
     labtraba;
     labcentr;
     ref0001@  #900;
|FIN;

|VARIABLES;
     {1}aVersion      = "";
     aPath = "";
     aLongitud = "";
     nLongitud = 0;
     nPos = 0;
     nCaracter = 0;
     aCaracter = "";

     fTmp = @;
     fTmp1 = @;
     fTmp2 = @;
     nAcumHC = 0;
     nAcum39 = 0;
     nAcumFT = 0;
     nPrime = 0;

     fecsys = @;
     aAlfa = "";
     aAlfa1 = "";
     aArea = "";
     aAreaAnt = "";
     aSeccion = "";
     aEmpre = "";
     aCodTra = "";
     aCentro = "";
     nCalc = 0;
     nCalc1 = 0;
     nHoras = 0;
     nResul = 0;
     nLinea = 0;
     nCuenta = 0;
     aDirBase = "";
     aBase = "";
     aOrigen = "";
     aDestino = "";
     aDestinoTrab = "";
     aIPRemoto = "";
     nCodAscii = 0;
     aColumna = "";
     aResultado = "";
|FIN;

|PROCESO mirames; |TIPO 7; ||calcula el mes por defecto segun fecha sistema
     fecsys = ~;
     aAlfa = fecsys;
     aAlfa = aAlfa % 402;
     #0Campo = aAlfa;
     |PINTA #0Campo;
|FPRC;

|PROCESO miraanyo; |TIPO 7; ||calcula el periodo por defecto segun fecha sistema
     fecsys = ~;
     aAlfa = fecsys;
     aAlfa = aAlfa % -104;
     #0Campo = aAlfa;
     |PINTA #0Campo;
|FPRC;

|PROCESO labtraba;
     aAlfa = "30.";
     aAlfa1 = "00" + #0#2;
     aAlfa1 = aAlfa1 % -102;
     aAlfa = aAlfa + aAlfa1;
     aAlfa1 = "0000" +  #0#3;
     aAlfa1 = aAlfa1 % -104;
     aAlfa = aAlfa + "." + aAlfa1;
     fTmp = aAlfa;

     aAlfa = #labtraba#36;    |QBF aAlfa;
     fTmp1 = aAlfa;

     aAlfa = #labtraba#37;    |QBF aAlfa;
     fTmp2 = aAlfa;

     |SI fTmp1 > fTmp;
          |ACABA;
     |SINO;
          aAlfa = #labtraba#37;    |QBF aAlfa;
          |SI aAlfa ! ""; |Y fTmp > fTmp2;
               |ACABA;
          |FINSI;
     |FINSI;


     #nomauxi1#0 = #labtraba#62;
     |LEE 000#nomauxi1.=;
     |SI FSalida = 0;
          aArea = #nomauxi1#0;
     |FINSI;

     #nomauxi2#0 = #labtraba#62;
     #nomauxi2#1 = #labtraba#63;
     |LEE 000#nomauxi2.=;
     |SI FSalida = 0;
          ||aArea = #nomauxi2#2;
          ||SISCO. 2.1.2007 Lo he cambiado a Seccion ponia aArea y aSeccion no se inicializaba en ningun sitio
          aSeccion = #nomauxi2#1;
     |FINSI;

     aEmpre = #labtraba#0;
     |QBF aEmpre;
     aEmpre = "00000" + aEmpre;
     aEmpre = aEmpre % -105;

     aCodTra = #labtraba#1;
     |QBF aCodTra;
     aCodTra = "00000" + aCodTra;
     aCodTra = aCodTra % -105;

     nHoras = #labtraba#234;

     #labcentr#0 = #labtraba#0;
     #labcentr#1 = #labtraba#77;
     |LEE 000#labcentr.=;
     |SI FSalida = 0;
          aCentro = #labcentr#2;
     |FINSI;

     #syke0014#0 = #labtraba#0;
     #syke0014#1 = #labtraba#1;
     #syke0014#2 = #0#3;
     #syke0014#3 = #0#2;
     #syke0014#4 = 1;
     |LEE 000#syke0014.];
     |SI FSalida = 0; |Y #syke0014#0 = #labtraba#0; |Y #syke0014#1 = #labtraba#1; |Y #syke0014#2 = #0#3; |Y #syke0014#3 = #0#2;
          |PARA; |SI FSalida = 0; |Y #syke0014#0 = #labtraba#0; |Y #syke0014#1 = #labtraba#1; |Y #syke0014#2 = #0#3; |Y #syke0014#3 = #0#2; |HACIENDO;

               #100#0 = #syke0014#7;
               #100#1 = #syke0014#9;
               #100#2 = aEmpre;
               #100#3 = aCodTra;
               #100#5 = #syke0014#5;
               |SI nHoras = 0; |Y #syke0014#11 ! 1;
                    nHoras = 39;   ||SISCO: Esto hay que sacarlo de algun lado?
               |FINSI;
               nCalc = nHoras * #syke0014#11;
               #100#4 = nCalc;
               |GRABA 020#100n;
               #200#0 = nCalc;
               |LEE 000#200=;
               |SI FSalida ! 0;
                    |GRABA 020#200n;
               |FINSI;

               |LEE 000#syke0014.s;
          |SIGUE;
     |SINO;
          #syke0014#0 = #labtraba#0;
          #syke0014#1 = #labtraba#1;
          #syke0014#2 = #0#3;
          #syke0014#3 = 99;
          #syke0014#4 = 1;
          |LEE 000#syke0014.];
          |SI FSalida = 0; |Y #syke0014#0 = #labtraba#0; |Y #syke0014#1 = #labtraba#1; |Y #syke0014#2 = #0#3; |Y #syke0014#3 = 99;
               |PARA; |SI FSalida = 0; |Y #syke0014#0 = #labtraba#0; |Y #syke0014#1 = #labtraba#1; |Y #syke0014#2 = #0#3; |Y #syke0014#3 = 99; |HACIENDO;
                    #100#0 = #syke0014#7;
                    #100#1 = #syke0014#9;
                    #100#2 = aEmpre;
                    #100#3 = aCodTra;
                    #100#5 = #syke0014#5;
                    |SI nHoras = 0; |Y #syke0014#11 ! 1;
                         nHoras = 39;   ||SISCO: Esto hay que sacarlo de algun lado?
                    |FINSI;
                    nCalc = nHoras * #syke0014#11;
                    #100#4 = nCalc;
                    |GRABA 020#100n;
                    #200#0 = nCalc;
                    |LEE 000#200=;
                    |SI FSalida ! 0;
                         |GRABA 020#200n;
                    |FINSI;

                    |LEE 000#syke0014.s;
               |SIGUE;
          |SINO;
               ||GRABAR SEGUN FICHA TRABAJADOR
               #100#0 = aArea;
               #100#1 = aSeccion;
               #100#2 = aEmpre;
               #100#3 = aCodTra;
               #100#4 = nHoras;
               #100#5 = #labtraba#77;
               |GRABA 020#100n;

               #200#0 = nHoras;
               |LEE 000#200=;
               |SI FSalida ! 0;
                    |GRABA 020#200n;
               |FINSI;
          |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE labtraba;
     #labtraba#1;
     44;
     #0#0, 00001, "A";
     #0#1, 99999, "A";
     ;
     NULL, labtraba;
|FIN;


|PROCESO ElProceso;
     |ABRE #labtraba;
     |ABRE #labcentr;
     |ABRE #nomauxi1;
     |ABRE #nomauxi2;
     |ABRE #syke0014;

     aAlfa1 = Usuario; |QBT aAlfa1;    aAlfa1 = ("s" + aAlfa1) % 108;
     |NOME_DAT #100 aAlfa1;
     |DELINDEX #100;

     aAlfa1 = Usuario; |QBT aAlfa1;    aAlfa1 = ("h" + aAlfa1) % 108;
     |NOME_DAT #200 aAlfa1;
     |DELINDEX #200;


     |ABRE #100;
     |ABRE #200;
     |BUCLE labtraba;

     ||CONSULTA_CLAVE #1#200;
     ||CONSULTA_CLAVE #1#100;

     |CIERRA #200;
     |CIERRA #100;
     |CIERRA #labtraba;
     |CIERRA #labcentr;
     |CIERRA #nomauxi1;
     |CIERRA #nomauxi2;
     |CIERRA #syke0014;
|FPRC;

|PROCESO syke0004w1;
     nResul = nResul + 1;
|FPRC;

|DEFBUCLE syke0004w1;
     #900#1005;
     4;
     #100#5,#100#0, #100#1, 00001, 00001, nCuenta;
     #100#5,#100#0, #100#1, 99999, 99999, nCuenta;
     ;
     NULL, syke0004w1;
|FIN;

|DEFBUCLE syke0004wS;
     #900#1005;
     4;
     #100#5, aAreaAnt, "    ", 00001, 00001, nCuenta;
     #100#5, aAreaAnt, "zzzz", 99999, 99999, nCuenta;
     ;
     NULL, syke0004w1;
|FIN;

|DEFBUCLE syke0004wT;
     #900#1005;
     ;
     #100#5, #100#0, #100#1, 00001, 00001;
     #100#5, #100#0, #100#1, 99999, 99999;
     ;
     NULL, syke0004w1;
|FIN;

|PROCESO CuentaTrab;
     nResul = 0;
     |BUCLE syke0004w1;
|FPRC;

|PROCESO CuentaTrabT;
     nResul = 0;
     |BUCLE syke0004wT;
|FPRC;

|PROCESO CuentaTrabwS;
     nResul = 0;
     |BUCLE syke0004wS;
|FPRC;

|PROCESO syke0006;
     |SI #200#0 = 0;     |ACABA;   |FINSI;
     nCodAscii = &aColumna;
     nCodAscii = nCodAscii + 1;
     aColumna = &nCodAscii;

     nCuenta = #200#0;
     |HAZ CuentaTrab;
     aAlfa = aColumna + nLinea + "," + nResul; |ALFACALLDLL "agixl97.dll","poke_dato", aAlfa;
     nCalc1 = nResul * #200#0;
     nCalc = nCalc + nCalc1;

     nResul = #200#1 + nResul;
     #200#1 = nResul;
     |GRABA 020#200a;
|FPRC;

|PROCESO syke0006wS;
     |SI #200#0 = 0;     |ACABA;   |FINSI;
     nCodAscii = &aColumna;
     nCodAscii = nCodAscii + 1;
     aColumna = &nCodAscii;

     nCuenta = #200#0;
     |HAZ CuentaTrabwS;
     aAlfa = aColumna + nLinea + "," + nResul; |ALFACALLDLL "agixl97.dll","poke_dato", aAlfa;
     nCalc1 = nResul * #200#0;
     nCalc = nCalc + nCalc1;
|FPRC;


|DEFBUCLE syke0006;
     #200#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, syke0006;
|FIN;

|DEFBUCLE syke0006wS;
     #200#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, syke0006wS;
|FIN;

|PROCESO syke0006w2;
     |SI #200#0 = 0;     |ACABA;   |FINSI;
     nCodAscii = &aColumna;
     nCodAscii = nCodAscii + 1;
     aColumna = &nCodAscii;

     aAlfa = aColumna + nLinea + "," + #200#0; |ALFACALLDLL "agixl97.dll","poke_dato", aAlfa;
|FPRC;

|DEFBUCLE syke0006w2;
     #200#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, syke0006w2;
|FIN;

|PROCESO syke0006w3;
     |SI #200#0 = 0;     |ACABA;   |FINSI;
     nCodAscii = &aColumna;
     nCodAscii = nCodAscii + 1;
     aColumna = &nCodAscii;

     aAlfa = aColumna + nLinea + "," + #200#1; |ALFACALLDLL "agixl97.dll","poke_dato", aAlfa;
|FPRC;

|DEFBUCLE syke0006w3;
     #200#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, syke0006w3;
|FIN;

|PROCESO Impresion;
     nLinea = nLinea + 1;
/*
     aArea = #100#0;     |QBF aArea;
     |SI aArea ! aAreaAnt; |Y nPrime = 1;
          aAlfa = "B" + nLinea + "," + "TOTAL " + aAreaAnt; |ALFACALLDLL "agixl97.dll","poke_dato", aAlfa;
          aColumna = "B";
          |BUCLE syke0006wS;
          nLinea = nLinea + 2;
     |FINSI;
*/

     nPrime = 1;

     ||Centro
     #labcentr#0 = #100#2;
     #labcentr#1 = #100#5;
     |LEE 000#labcentr.=;
     |SI FSalida = 0;
          aAlfa = "A" + nLinea + "," + #100#5 + " - " + #labcentr#2; |ALFACALLDLL "agixl97.dll","poke_dato", aAlfa;
     |SINO;
          aAlfa = "A" + nLinea + "," + #100#5; |ALFACALLDLL "agixl97.dll","poke_dato", aAlfa;
     |FINSI;

     ||Area
     #nomauxi1#0 = #100#0;
     |LEE 000#nomauxi1.=;
     |SI FSalida = 0;
          aAlfa = "B" + nLinea + "," + #100#0 + " - " + #nomauxi1#1; |ALFACALLDLL "agixl97.dll","poke_dato", aAlfa;
     |SINO;
          aAlfa = "B" + nLinea + "," + #100#0; |ALFACALLDLL "agixl97.dll","poke_dato", aAlfa;
     |FINSI;

     ||Seccion
     #nomauxi2#0 = #100#0;
     #nomauxi2#1 = #100#1;
     |LEE 000#nomauxi2.=;
     |SI FSalida = 0;
          aAlfa = "C" + nLinea + "," + #100#1 + " - " + #nomauxi2#2; |ALFACALLDLL "agixl97.dll","poke_dato", aAlfa;
     |SINO;
          aAlfa = "C" + nLinea + "," + #100#1; |ALFACALLDLL "agixl97.dll","poke_dato", aAlfa;
     |FINSI;

     ||HC Cuenta de Trabajadores
     |HAZ CuentaTrabT;
     aAlfa = "D" + nLinea + "," + nResul; |ALFACALLDLL "agixl97.dll","poke_dato", aAlfa;
     nAcumHC = nAcumHC + nResul;

     ||Jornada Completa
     nCuenta = 0;
     |HAZ CuentaTrab;
     aAlfa = "E" + nLinea + "," + nResul; |ALFACALLDLL "agixl97.dll","poke_dato", aAlfa;
     ||SISCO. 39 = JornadaCompleta
     nCalc = 39 * nResul;
     nAcum39 = nAcum39 + nResul;


     ||DESGLOSE HORAS
     aColumna = "E";
     |BUCLE syke0006;

     ||FULLTIME
     nCodAscii = &aColumna;
     nCodAscii = nCodAscii + 1;
     aColumna = &nCodAscii;

     nCalc = nCalc / 39;
     aAlfa = aColumna + nLinea + "," + nCalc; |ALFACALLDLL "agixl97.dll","poke_dato", aAlfa;
     nAcumFT = nAcumFT + nCalc;


     aAreaAnt = aArea;
|FPRC;

|DEFBUCLE Impresion;
     #100#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, Impresion;
|FIN;

|PROCESO Cabecera;
     aAlfa = "E" + nLinea + "," + "39"; |ALFACALLDLL "agixl97.dll","poke_dato", aAlfa;
     aColumna = "E";
     |BUCLE syke0006w2;
     nCodAscii = &aColumna;
     nCodAscii = nCodAscii + 1;
     aColumna = &nCodAscii;

     aAlfa = aColumna + nLinea + "," + "FT"; |ALFACALLDLL "agixl97.dll","poke_dato", aAlfa;
|FPRC;

|PROCESO Totales;
     nLinea = nLinea + 1;
     aAlfa = "C" + nLinea + "," + "TOTAL STAFF"; |ALFACALLDLL "agixl97.dll","poke_dato", aAlfa;
     aAlfa = "D" + nLinea + "," + nAcumHC; |ALFACALLDLL "agixl97.dll","poke_dato", aAlfa;
     aAlfa = "E" + nLinea + "," + nAcum39; |ALFACALLDLL "agixl97.dll","poke_dato", aAlfa;
     aColumna = "E";
     |BUCLE syke0006w3;

     nCodAscii = &aColumna;
     nCodAscii = nCodAscii + 1;
     aColumna = &nCodAscii;
     aAlfa = aColumna + nLinea + "," + nAcumFT; |ALFACALLDLL "agixl97.dll","poke_dato", aAlfa;
|FPRC;

|PROCESO ElInforme;
     |DBASE aDirBase;    |QBF aDirBase;
     aAlfa = aDirBase + "def/syke0004.mas";
     |CARGA_DEF aAlfa, ref0001@;
     |SI FSalida ! 0;    |ACABA;   |FINSI;
     aAlfa1 = Usuario; |QBT aAlfa1;    aAlfa1 = ("s" + aAlfa1) % 108;
     |NOME_DAT #900 aAlfa1;

     |ABRE #100;
     |CARGADLL "agixl97.dll";
     |SI FSalida < 0;
          |MENAV "0000No se puede usar la DLL de Excel.";
          |ACABA;
     |FINSI;

     |DBASE aBase;  |QBF aBase;
     aOrigen = aBase + "documentos/summary_report.xls";
     aDestinoTrab = "C:\DOCUMENTOS";         |RMKDIR aDestino;
     aDestino = aDestinoTrab + "\trabajo.xls";

     aIPRemoto = "";
     |IP_REMOTO aIPRemoto;  |QBT aIPRemoto;
     |SI aIPRemoto ! "";
          |ENVIA_FICHERO aOrigen, aDestino;
     |SINO;
          |COPIA_FICHERO aOrigen, aDestino;
     |FINSI;

     |ALFACALLDLL "agixl97.dll", "carga_book", aDestino;
     |ALFACALLDLL "agixl97.dll", "selecciona_hoja", "SUMMARY REPORT";

     nLinea = 8;
     |HAZ Cabecera;
     aAlfa = "B5," + #0#2 +" / " + #0#3; |ALFACALLDLL "agixl97.dll","poke_dato", aAlfa;

     |ABRE #nomauxi1;
     |ABRE #nomauxi2;
     |ABRE #labcentr;

     |BUCLE Impresion;

     |CIERRA #labcentr;
     |CIERRA #nomauxi1;
     |CIERRA #nomauxi2;

     |HAZ Totales;

     |DESCARGA_DEF #900;
     aAlfa   = aDestinoTrab + "\summary_report.xls";
     |RFBORRA aAlfa;

     |ALFACALLDLL "agixl97.dll", "fichero_destino", aAlfa;
     aResultado = aAlfa;

     |ALFACALLDLL "agixl97.dll", "fin_book", "salva";
     |DESCARGADLL "agixl97.dll";

     aDestino   = aDestinoTrab + "\trabajo.xls";
     |RFBORRA aDestino;

     aVersion1 = "Excel.Sheet";
     |ESPECIFICA "VERSION_PROGRAMA", aVersion;
     |HAZ ExtraePath;
     |SI aPath = "NO LOCALIZADO";
         |MENAV "0000 No puedo encontrar automaticamente la Excel";
         |ACABA;
     |FINSI;

     |QBF aPath;

     || aAlfa  = "cmd " + &34 + "/c START /WAIT " + aPath + " " + aAlfa + &34;
     aAlfa  = "cmd " + &34 + "/c START /WAIT " + aResultado + &34;
     |RSYSTEM aAlfa;

     |CIERRA #100;
|FPRC;

|PROCESO ExtraePath;
     aPath     = aVersion1;
     aLongitud = aPath % 0;
     nLongitud = aLongitud;
     |PARA nPos = 1; |SI nPos [ nLongitud; |HACIENDO nPos = nPos + 1;
           nCaracter = (nPos * 100) + 1;
           aCaracter = aPath % nCaracter;
           |SI aCaracter = ",";
               nPos = nLongitud - nPos;
               nPos = nPos + 100;
               nPos = (-1) * nPos;
               aPath = aPath % nPos;
               |SAL;
          |FINSI;
     |SIGUE;

     aPath = aPath - "[";
     aPath = aPath - "]";
     |QBF aPath;
|FPRC;

|PROGRAMA;
     |CLS;
     |PINPA #0#0;
     |PINDA #0#0;
     |ENDATOS #1#0;
     |SI FSalida = 0;
          |HAZ ElProceso;
          |HAZ ElInforme;
     |FINSI;
|FPRO;
