|FICHEROS;
     nomasigc #0;
     nomasigt #100,MANTE;
     nomcnaec;
     labempre;
|FIN;

|VARIABLES;
     aAlfa1 = "";
     aAlfa2 = "";
     swSigue = 0;

     {-1}menu = "";
     menu1 = "2400";
     menu2 = "1";
     menu3 = "Elegir Opcion:";
     menu4 = "RAC";
     menu5 = "Revisar";
     menu6 = "Actualizar";
     menu7 = "Cancelar";
     nOpcion = 0;

     nModo = 0;
|FIN;

|PROCESO NoAlta; |TIPO 0;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 1;
          |ERROR;
     |FINSI;
|FPRC;

|PROCESO labempre;
     swSigue = 1;
     |DDEFECTO #100;
     #100#0 = #labempre#0;
     #100#1 = #labempre#2;
     #100#2 = #labempre#11;
     #100#3 = #labempre#12;
     #100#4 = #labempre#128;
     #100#5 = #labempre#129;

     aAlfa2 = #labempre#128;
     |QBF aAlfa2;
     |SI #0#3 = "T";
          swSigue = 1;
     |SINO;
          |SI aAlfa2 = "";
               swSigue = 1;
          |SINO;
               swSigue = 0;
          |FINSI;
     |FINSI;

     |SI swSigue = 1;
          |SI #0#2 = "S"; |Y aAlfa2 = "";
               |SI #labempre#11 > 0; |Y #labempre#11 < 100;
                    aAlfa1 = "";
                    aAlfa1 = #labempre#11;
                    aAlfa1 = ("000" + aAlfa1) % -102;

                    #nomcnaec#0 = aAlfa1;
                    |LEE 000#nomcnaec.=;
                    |SI FSalida = 0;
                         #100#4 = #nomcnaec#0;
                         #100#5 = #nomcnaec#1;
                    |FINSI;
               |FINSI;
          |FINSI;

          |GRABA 101#100n;
          |LIBERA #100;
     |FINSI;
|FPRC;

|DEFBUCLE labempre;
     #labempre#1;
     ;
     #0#0;
     #0#1;
     ;
     NULL, labempre;
|FIN;

|PROCESO asignacion;
     #labempre#0 = #100#0;
     |LEE 101#labempre.=;
     |SI FSalida = 0;
          aAlfa1 = #100#4;
          |QBF aAlfa1;
          |SI aAlfa1 ! "";
               #labempre#11 = #100#4;
          |FINSI;
          #labempre#128 = #100#4;
          #labempre#129 = #100#5;
          |GRABA 020#labempre.a;
     |FINSI;
     |LIBERA #labempre;
|FPRC;

|DEFBUCLE asignacion;
     #100#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, asignacion;
|FIN;

|PROCESO Actualiza;
     |ABRE #labempre;
     |BUCLE asignacion;
     |MENAV "    Proceso de asignacion terminado con exito";
     |CIERRA #labempre;
|FPRC;

|PROCESO minimo;
     #100#0 = 00001;
|FPRC;

|PROCESO maximo;
     #100#0 = 99999;
|FPRC;

|PROGRAMA;
     |CLS;
     |PINPA #0#0;
     |PINDA #0#0;
     |ENDATOS #1#0;
     |SI FSalida = 0;
          aAlfa1 = Usuario; |QBT aAlfa1;    aAlfa1 = ("t" + aAlfa1) % 108;
          |NOME_DAT #100 aAlfa1;
          |DELINDEX #100;
          |ABRE #100;
          |ABRE #nomcnaec;
          |BUCLE labempre;
          |CIERRA #nomcnaec;
          |CIERRA #100;

          |PARA; |SI; |HACIENDO;
               |ENTLINEAL #1#100, -1, 2, 22, 1, minimo, maximo;
               |MENU menu;
               nOpcion = FSalida;
               |SI nOpcion = 2;
                    |HAZ Actualiza;
                    |SAL;
               |FINSI;
               |SI nOpcion = 3;
                    |SAL;
               |FINSI;
          |SIGUE;
     |FINSI;
|FPRO;
