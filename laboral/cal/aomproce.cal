|FICHEROS;
    aomproce #0;
    aomhorca #1;    || entrada horas cabs.
    aomhorli #2;    || entrada horas lins.
    aomcateg #3;    || categorias
    aomconce #4;    || conceptos
    aomcatli #5;    || categorias lins.

    labtraba #10;   || trabajadores
    nomtrcal #11;   || calendario
    aomentra #12;   || entrada definitiva
|FIN;

|VARIABLES;
    alfa1 = "";
    alfa2 = "";
    alfa3 = "";
    alfa4 = "";
    nume1 = 0;
    nume2 = 0;
    nume3 = 0;
    nume4 = 0;
    para1 = 0;
    para2 = 0;
    para3 = 0;
    para4 = 0;
    fech1 = @;
    fech2 = @;
    fech3 = @;
    fech4 = @;
    elsw1 = 0;
    elsw2 = 0;
    elsw3 = 0;
    elsw4 = 0;
    FEstado = 0;
    seleccio = 0;
    modo = 0;
    sw_rup = 0;

    bisiesto = 0;
    topemes = 0;
    p_mes = "";
    desde = @;
    hasta = @;
    emp_ant = 0;
    tra_ant = 0;
    obr_ult = 0;
    sub_ult = 0;
    cat_ult = 0;
    fec_pri = @;
    fec_ult = @;
    calendar = "";
    eldia = 0;
    cadia = "";
    tipod = 0;
    tipot = 0;
    atipo = "";
    horas = 0;
    codigo = 0;
    importe = 0;
    festiv = 0;
    extras = 0;
    normal = 0;
    fecha = @;
    nobra = 0;
    nsubo = 0;
    ncate = 0;
|FIN;
____________________________________/
|PROCESO mayus; |TIPO 0;
    #0Campo = #0Campo > #0Campo;
|PINTA #0Campo;
|FPRC;

|PROCESO defecto; |TIPO 7;
    fech1 = ~;
    alfa1 = fech1;
    alfa1 = alfa1 % -104;
    #0Campo = alfa1;
|PINTA #0Campo;
|FPRC;

|PROCESO mirames; |TIPO 7;
    fech1 = ~;
    alfa1 = fech1;
    alfa1 = alfa1 % 402;
    #0Campo = alfa1;
|PINTA #0Campo;
|FPRC;

|PROCESO pintames; |TIPO 0;
    bisiesto = #0#0 $ 4;
|SI bisiesto = 0;  bisiesto = 1; |SINO; bisiesto = 0;  |FINSI;

|SI #0Campo =  1;  p_mes = "ENERO     ";  topemes = 31; |FINSI;
|SI #0Campo =  2;  p_mes = "FEBRERO   ";  topemes = 28+bisiesto; |FINSI;
|SI #0Campo =  3;  p_mes = "MARZO     ";  topemes = 31; |FINSI;
|SI #0Campo =  4;  p_mes = "ABRIL     ";  topemes = 30; |FINSI;
|SI #0Campo =  5;  p_mes = "MAYO      ";  topemes = 31; |FINSI;
|SI #0Campo =  6;  p_mes = "JUNIO     ";  topemes = 30; |FINSI;
|SI #0Campo =  7;  p_mes = "JULIO     ";  topemes = 31; |FINSI;
|SI #0Campo =  8;  p_mes = "AGOSTO    ";  topemes = 31; |FINSI;
|SI #0Campo =  9;  p_mes = "SEPTIEMBRE";  topemes = 30; |FINSI;
|SI #0Campo = 10;  p_mes = "OCTUBRE   ";  topemes = 31; |FINSI;
|SI #0Campo = 11;  p_mes = "NOVIEMBRE ";  topemes = 30; |FINSI;
|SI #0Campo = 12;  p_mes = "DICIEMBRE ";  topemes = 31; |FINSI;
|PINTA 1140, p_mes;

    alfa1 = #0#1;        alfa1 = ("00" + alfa1) % -102;
    alfa2 = topemes;     alfa2 = ("00" + alfa2) % -102;

    alfa3 = "01." + alfa1 + "." + #0#0;           desde = alfa3;
    alfa3 = alfa2 + "." + alfa1 + "." + #0#0;     hasta = alfa3;
|FPRC;
____________________________________/
|PROCESO mensajes;
|SI FSalida = 100;
        alfa1 = "    Movimiento [" +#12#0+"-"+#12#2+"-"+#12#3+"-"+#12#1+"-"+#12#7+"-"+#12#4+"] duplicado ...";
    |MENAV alfa1;
|FINSI;
|SI FSalida ! 0;|Y FSalida ! 100;
        alfa1 = "    Error grabando [" +#12#0+"-"+#12#2+"-"+#12#3+"-"+#12#1+"-"+#12#7+"-"+#12#4+"] ...";
    |MENAV alfa1;
|FINSI;
|FPRC;

|PROCESO vacaciones;
|SI #2#7 > 0;
        #4#0 = #2#9;
    |LEE 000#4=;
    |SI FSalida = 0;|Y #4#3 = "M";
        |DDEFECTO #12;
            #12#0 = #2#2;    || fecha
            #12#1 = #2#0;    || obra
            #12#7 = #2#1;    || subobra
            #12#2 = #2#4;    || empresa
            #12#3 = #2#5;    || trabajador
            #12#4 = #2#9;    || tipo de hora
            #12#5 = #2#7;    || nro. de horas

            #5#0 = #2#0; #5#1 = #2#1; #5#2 = #2#8; #5#3 = #4#0; |LEE 000#5=;
        |SI FSalida ! 0;
                #12#6 = #2#7 * #4#2;   || ptas. destajo (conceptos)
        |SINO;
                #12#6 = #2#7 * #5#5;   || ptas. destajo (categorias)
        |FINSI;

        |GRABA 000#12n;
        |HAZ mensajes;
    |FINSI;
        fec_ult = #2#2;
        obr_ult = #2#0;
        sub_ult = #2#1;
        cat_ult = #2#8;
|FINSI;
|FPRC;

|PROCESO pluses;
|SI horas > 0;
    |DDEFECTO #12;
        #12#0 = #2#2;      || fecha
        #12#1 = #2#0;      || obra
        #12#7 = #2#1;      || subobra
        #12#2 = #2#4;      || empresa
        #12#3 = #2#5;      || trabajador
        #12#4 = #2#9;      || tipo de hora
        #12#5 = horas;     || nro. de horas
        #12#6 = horas * #4#2;     || ptas. destajo
    |GRABA 000#12n;
    |HAZ mensajes;
|FINSI;
|FPRC;

|PROCESO busca_categ;
|SI horas > 0;
    |SI atipo = "N";     codigo = 4;    |FINSI;   || normales
    |SI atipo = "E";     codigo = 5;    |FINSI;   || extras
    |SI atipo = "F";     codigo = 6;    |FINSI;   || festivos
    |SI atipo = "D";     codigo = 7;    |FINSI;   || dietas
    |SI atipo = "T";     codigo = 8;    |FINSI;   || medias dietas
    |SI atipo = "L";     codigo = 9;    |FINSI;   || desplazamiento
    |SI #3codigo ! 0;
            importe = codigo + 12;
        |SI #3importe ! 0;
            |DDEFECTO #12;
                #12#0 = fecha;     || fecha
                #12#1 = nobra;     || obra
                #12#7 = nsubo;     || subobra
                #12#2 = emp_ant;   || empresa
                #12#3 = tra_ant;   || trabajador
                #12#4 = #3codigo;  || tipo de hora
                #12#5 = horas;     || nro. de horas
                #12#6 = horas * #3importe;   || ptas. destajo
            |GRABA 000#12n;
            |HAZ mensajes;
        |FINSI;
    |FINSI;
|FINSI;
|FPRC;

|PROCESO calcula;
    #3#0 = #2#0;
    #3#1 = #2#1;
    #3#2 = #2#8;
|LEE 000#3=;
|SI FSalida = 0;
        festiv = 0;      extras = 0;    normal = 0;
        nobra = #2#0;    nsubo = #2#1;  fecha = #2#2;  ncate = #2#8;

        alfa1 = #2#2 % 102;   eldia = alfa1; eldia = (eldia * 100) + 1;
        cadia = calendar % eldia;
    |SI cadia = "1";
            festiv = #2#7;
    |SINO;
            extras = #2#7 - 8;
        |SI extras < 0;  extras = 0;    |FINSI;
            normal = #2#7 - extras;
    |FINSI;
        horas = festiv;  atipo = "F";   |HAZ busca_categ;   || festivos
        horas = extras;  atipo = "E";   |HAZ busca_categ;   || extras
        horas = normal;  atipo = "N";   |HAZ busca_categ;   || normales

    |SI #2#9 ! 0;
            #4#0 = #2#9;
        |LEE 000#4=;
        |SI FSalida = 0;
            |SI #4#3 = "D";|O #4#3 = "T";|O #4#3 = "L";
                    alfa1 = "    Plus [" + #4#0 + "] erroneamente informado ...";
                |MENAV alfa1;
            |SINO;
                |SI #4#3 = "N";    horas = normal;     |FINSI;
                |SI #4#3 = "E";    horas = extras;     |FINSI;
                |SI #4#3 = "F";    horas = festiv;     |FINSI;
                    atipo = #4#3;
                |HAZ pluses;
            |FINSI;
        |FINSI;
    |FINSI;

        tipot = tipot + 1;
|SINO;
        alfa1 = "    Categoria [" + #3#0 + "-" + #3#1 + "-" + #3#2 + "] inexistente ...";
    |MENAV alfa1;
|FINSI;

    fec_ult = #2#2;
    obr_ult = #2#0;
    sub_ult = #2#1;
    cat_ult = #2#8;
|FPRC;

|PROCESO rup_tra;
|SI sw_rup ! 0;
        nobra = obr_ult; nsubo = sub_ult; fecha = fec_ult; ncate = cat_ult;
        #3#0 = nobra;
        #3#1 = nsubo;
        #3#2 = #10#17;
    |LEE 000#3=;
    |SI FSalida = 0;
            nume1 = fec_pri; nume2 = fec_ult;
            tipod = nume2 - nume1 + 1;
            horas = tipod;    atipo = "D";   |HAZ busca_categ;   || dietas

            horas = tipot;    atipo = "T";   |HAZ busca_categ;   || 1/2 dietas

            horas = tipot;    atipo = "L";   |HAZ busca_categ;   || desplazamiento
    |SINO;
            alfa1 = "    Categoria [" + #3#0 + "-" + #3#1 + "-" + #3#2 + "] inexistente ...";
        |MENAV alfa1;
    |FINSI;
|FINSI;

    emp_ant = #2#4;
    tra_ant = #2#5;
    fec_pri = #2#2;
    tipod = 0;
    tipot = 0;
|DDEFECTO #10; #10#0 = #2#4;    #10#1 = #2#5;  |LEE 000#10=;   || trabajador
|DDEFECTO #11; #11#0 = #10#156; #11#13= #0#0;  |LEE 000#11=;   || calendario
    nume1 = #0#1;
    calendar = #11nume1;
|FPRC;

|PROCESO entrada1;
|SI sw_rup = 0;
    |HAZ rup_tra;
        sw_rup = 1;
|FINSI;

|SI emp_ant ! #2#4;|O tra_ant ! #2#5;
    |HAZ rup_tra;
|FINSI;

    #4#0 = #2#9;
|LEE 000#4=;
|SI #4#3 ! "M";
    |HAZ calcula;
|SINO;
    |HAZ vacaciones;
|FINSI;
|FPRC;

|DEFBUCLE entrada;
 #2#2;
 ;
 #0#2, #0#4, desde, #0#6, #0#8;
 #0#3, #0#5, hasta, #0#7, #0#9;
 ;
 NULL, entrada1;
|FIN;

|PROCESO borrado1;
|BORRA 020#12a;
|LIBERA #12;
|FPRC;

|DEFBUCLE borrado;
 #12#1;
 ;
 desde, #0#2, #0#4, #0#6, #0#8,  0;
 hasta, #0#3, #0#5, #0#7, #0#9, 99;
 be;
 NULL, borrado1;
|FIN;

|PROCESO tipo3; |TIPO 3;
|SI #0#10 = "SI";
    |ATRI P;   |MENSA "    Borrando anteriores ...";   |ATRI 0;
    |BUCLE borrado;
    |ATRI P;   |MENSA "    Procesando actuales ...";   |ATRI 0;
    |ABRE #3;
    |ABRE #4;
    |ABRE #5;
    |ABRE #10;
    |ABRE #11;
    |ABRE #12;
    |BUCLE entrada;
    |SI sw_rup ! 0;
        |HAZ rup_tra;
    |SINO;
        |MENAV "    Seleccion vacia ...";
    |FINSI;
    |CIERRA #3;
    |CIERRA #4;
    |CIERRA #5;
    |CIERRA #10;
    |CIERRA #11;
    |CIERRA #12;
|FINSI;
|FPRC;
