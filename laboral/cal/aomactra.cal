|FICHEROS;
    aomactra #0;
    aomentra #1;
    labtraba #2;
    aomhoras #3;
|FIN;

|VARIABLES;
    mes = 0;
    any = 0;
    nume1 = 0;
    bisiesto = 0;
    topemes = 0;
    seleccio = 0;
    ptashora = 0;
    destajo = 0;
    importe = 0;
    alfa1 = "";
    alfa2 = "";
    alfa3 = "";
    alfa4 = "";
    desfec = @;
    hasfec = @;
    fecsys = @;
    actual0 = "";
    anteri0 = "";
    anteri1 = 0;
    anteri2 = 0;
|FIN;

***************************************************************************
   RUTINA DE GESTION DE PANTALLA
***************************************************************************

|PROCESO defecto;   || CARGA EL A¥O DE LA FECHA DEL SISTEMA
    fecsys = ~;
    alfa1 = fecsys;
    alfa1 = alfa1 % 402;
    mes = alfa1;

    fecsys = ~;
    alfa1 = fecsys;
    alfa1 = alfa1 % -104;
    any = alfa1;

    nume1 = any $ 4;
|SI nume1 = 0;
        bisiesto = 1;
|SINO;
        bisiesto = 0;
|FINSI;

|SI mes =  1;  topemes = 31;             |FINSI;
|SI mes =  2;  topemes = 28 + bisiesto;  |FINSI;
|SI mes =  3;  topemes = 31;             |FINSI;
|SI mes =  4;  topemes = 30;             |FINSI;
|SI mes =  5;  topemes = 31;             |FINSI;
|SI mes =  6;  topemes = 30;             |FINSI;
|SI mes =  7;  topemes = 31;             |FINSI;
|SI mes =  8;  topemes = 31;             |FINSI;
|SI mes =  9;  topemes = 30;             |FINSI;
|SI mes = 10;  topemes = 31;             |FINSI;
|SI mes = 11;  topemes = 30;             |FINSI;
|SI mes = 12;  topemes = 31;             |FINSI;
|FPRC;

|PROCESO fechas; |TIPO 7;
|HAZ defecto;

    alfa1 = topemes; alfa1 = "00" + alfa1;   alfa1 = alfa1 % -102;
    alfa2 = mes;     alfa2 = "00" + alfa2;   alfa2 = alfa2 % -102;
    alfa3 = any;

    alfa4 = "01."       + alfa2 + "." + alfa3;  desfec = alfa4; || desde fecha
    alfa4 = alfa1 + "." + alfa2 + "." + alfa3;  hasfec = alfa4; || hasta fecha

    #0#0 = desfec;  |PINTA #0#0;
    #0#1 = hasfec;  |PINTA #0#1;
|FPRC;

***************************************************************************
   RUTINAS DEL BUCLE DE LECTURA DE LOS MOVIMIENTO DE HORAS
***************************************************************************

|PROCESO bucle5;   || PINTA EN PANTALLA EL REGISTRO ACTUALMENTE PROCESADO
    #0#06 = #1#0;   |PINTA #0#06;
    #0#07 = #1#2;   |PINTA #0#07;
    #0#08 = #1#3;   |PINTA #0#08;
    #0#09 = #1#1;   |PINTA #0#09;
    #0#11 = #1#7;   |PINTA #0#11;
    #0#10 = #1#4;   |PINTA #0#10;
|FPRC;

|PROCESO bucle4;   || PROVOCA REGRABACION DEL ULTIMO TRABAJADOR
    actual0 = "xxxxxxxx";
|HAZ bucle2;
|FPRC;

|PROCESO bucle3;   || LEE IMPORTE HORA Y ACUMULA PTAS.
|HAZ bucle2;
|DDEFECTO #3;
    #3#0 = #1#4;
|LEE 020#3=;
    ptashora = #3#1 * #1#5;                     || ptas. horas
    destajo  = #1#6;                            || ptas. destajo
    importe  = importe + (ptashora + destajo);  || acumula el importe
|FPRC;

|PROCESO bucle2;   || REGRABA EL TRABAJADOR
|SI anteri0 ! actual0;
        #2#0 = anteri1;  || codigo empresa
        #2#1 = anteri2;  || codigo trabajador
    |LEE 101#2=;
    |SI FSalida = 0;
            #2#98 = importe;
        |GRABA 020#2a;
        |SI #2#97 = 0;
                alfa1 = "    Trabajador [" + #2#0 + "-" + #2#1 + "] con tipo de ajuste CERO ...";
            |MENAV alfa1;
        |FINSI;
    |SINO;
            alfa1 = "    Trabajador [" + #2#0 + "-" + #2#1 + "] INEXISTENTE ...";
        |MENAV alfa1;
    |FINSI;
    |LIBERA #2;
        anteri0 = actual0;
        anteri1 = #1#2;
        anteri2 = #1#3;
        importe = 0;
|FINSI;
|FPRC;

|PROCESO bucle1;   || CONSTRUYE VARIABLES PARA RUPTURA POR TRABAJADOR
    seleccio = 1;
|HAZ bucle5;
    alfa1 = #1#2;  alfa1 = "00000" + alfa1;  alfa1 = alfa1 % -105;
    alfa2 = #1#3;  alfa2 = "00000" + alfa2;  alfa2 = alfa2 % -105;
    actual0 = alfa1 + alfa2;
|SI anteri0 = "xxxxxxxxxx";   || solo la primera vez
        anteri0 = actual0;
        anteri1 = #1#2;
        anteri2 = #1#3;
|FINSI;
|FPRC;

|PROCESO bucle0;
    importe = 0;
    anteri0 = "xxxxxxxxxx";
|FPRC;

|DEFBUCLE aomactra0;
 #1#2;
 ;
 #0#2, #0#4, #0#0,      1,  0,  1;
 #0#3, #0#5, #0#1, 999999, 99, 99;
 ;
 bucle0, bucle1, NULL, bucle3, bucle4;
|FIN;

***************************************************************************
   RUTINA DE INICIO DE PROCESO
***************************************************************************

|PROCESO proceso; |TIPO 3;
|ABRE #2;
|ABRE #3;
|BUCLE aomactra0;
|SI seleccio = 0;
    |ATRI P;
    |MENAV "    Seleccion vacia ...";
    |ATRI 0;
|FINSI;
|CIERRA #2;
|CIERRA #3;
|FPRC;
