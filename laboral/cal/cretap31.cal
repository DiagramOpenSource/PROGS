|FICHEROS;
     labempre;
     labtraba;
     labcentr;
     nomcenes;

     cretam3e;
     silcon23;
|FIN;

|VARIABLES;
     nEmp = 0;
     nTra = 0;
     nPos = 0;
     aPos = "";
     nLon = 0;
     aLon = "";
     aAlfa = "";
     &eaFichero = "";

     aLinea = "";
     aCadena = "";
     aCadenaSal = "";
     aValor = "";
     nValor = 0;
     fValor = @;
     nEspacio = 0;
     aSegmento = "";
     swAcaba = 0;

     nCampo = 0;
     nHandle = 0;
     FLinea = 0;
     nNroLinea = 0;
     aNroLinea = "";
     &aTotalLineas = "";
     nUltimo = 0;
     aFechaAlta = "";
     fFechaAlta = @;
     aNif = "";
     aSit = "";
     aDesdeSit = "";
     aHastaSit = "";

     nBlancos = 0;
     nBlancosIzd = 0;
     nBlancosDer = 0;
     aBlancosIzd = "";
     aBlancosDer = "";
     nNume = 0;

     nNroCampos = 0;
     nNroTrab = 0;
     aSoloFichero = "";
     nPos1 = 0;
     aUltimo = "";
     nRepetido = 0;
     aDescripcion = "";
     aDesc8 = "";

     aFechaAltaTrab = "";
     fFechaAltaTrab = @;
     aFechaBajaTrab = "";
     fFechaBajaTrab = @;
     aNifFIE = "";
     fFechaRef = @;
     aAntSegmento = "";
     nPasada = 0;

     fFechaGen = @;
     aHoraGen = "";
|FIN;

|PROCESO SoloFichero;
     aSoloFichero = "";
     |PARA nPos = 1; |SI swAcaba = 0; |HACIENDO nPos = nPos + 1;
          nPos1 = ((nPos * 100) + 1) * (-1);
          aUltimo = eaFichero % nPos1;
          |SI aUltimo ! "/";
               aSoloFichero = aUltimo + aSoloFichero;
          |SINO;
               swAcaba = 1;
          |FINSI;
     |SIGUE;
|FPRC;

|PROCESO cretam3e;
     nUltimo = nUltimo + 1;

     aAlfa = #cretam3e#30;
     |QBF aAlfa;
     |SI aNifFIE = #cretam3e#6;
     |Y aAlfa = aSoloFichero;      || ojo, he encontrado un registro con el
          nRepetido = #cretam3e#2; || mismo trabajador y el mismo nombre
          |BORRA 020#cretam3e.a;   || de fichero ... lo borro y lo machaco
          |LIBERA #cretam3e;
     |FINSI;
|FPRC;

|DEFBUCLE cretam3e_0000;
     #cretam3e#1;
     ;
     00000, 00000, 00001;
     00000, 00000, 99999;
     ;
     NULL, cretam3e;
|FIN;

|DEFBUCLE cretam3e;
     #cretam3e#3;
     ;
     aNifFIE, 00001;
     aNifFIE, 99999;
     ;
     NULL, cretam3e;
|FIN;

|PROCESO labtraba_NIF;
     aFechaAltaTrab = #labtraba#36;
     aFechaBajaTrab = #labtraba#37;
     |QBF aFechaBajaTrab;
     |SI aFechaBajaTrab = ""; |O aFechaBajaTrab = "..........";
          aFechaBajaTrab = "31.12.2099";
     |FINSI;
     fFechaAltaTrab = aFechaAltaTrab;
     fFechaBajaTrab = aFechaBajaTrab;

     |SI fFechaAltaTrab [ fFechaRef; |Y fFechaRef [ fFechaBajaTrab;
          nEmp = #labtraba#0;
          nTra = #labtraba#1;
     |FINSI;
|FPRC;

|DEFBUCLE labtraba_NIF;
     #labtraba#2;
     44;
     aNif, aDesdeSit;
     aNif, aHastaSit;
     ;
     NULL, labtraba_NIF;
|FIN;

|PROCESO Localizador;
     |PARA nPasada = 1; |SI nPasada [ 10; |HACIENDO nPasada = nPasada + 1;
          |SI nEmp = 0; |Y nTra = 0;
               fFechaRef = "01.01.0000";
               |SI nPasada = 1;
                    fFechaRef = #cretam3e#33;    || fecha generacion movimiento
               |FINSI;
               |SI nPasada = 2;
                    fFechaRef = #cretam3e#27;    || fecha fin IT
               |FINSI;
               |SI nPasada = 3;
                    fFechaRef = #cretam3e#14;    || fecha inicio IT
               |FINSI;
               |SI nPasada = 4;
                    fFechaRef = #cretam3e#35;    || fecha inicio pago dir
               |FINSI;
               |SI nPasada = 5;
                    fFechaRef = #cretam3e#51;    || fecha propuesta IP
               |FINSI;
               |SI nPasada = 6;
                    fFechaRef = #cretam3e#70;    || fecha ini pago otras prest.
               |FINSI;
               |SI nPasada = 7;
                    fFechaRef = #cretam3e#74;    || fecha nacimiento
               |FINSI;
               |SI nPasada = 8;
                    fFechaRef = #cretam3e#65;    || fecha ini pago jubilacion
               |FINSI;
               |SI fFechaRef ! "01.01.0000"
                    |BUCLE labtraba_NIF;
               |FINSI;
               |SI nEmp ! 0; |O nTra ! 0;
                    |SAL;
               |FINSI;
          |FINSI;
     |SIGUE;
|FPRC;

|PROCESO Finaliza;
     || ahora vamos a buscar los datos que no se cogen del FIE
     || primero me quedo con los datos que ya tengo
     |SALVA_DATOS #cretam3e;

     aNifFIE = #cretam3e#6;

     aNif = aNifFIE;
     |QBT aNif;
     aNif = (aNif + "            ") % 112;
     fFechaAlta = #cretam3e#11;
     aFechaAlta = fFechaAlta;

     nEmp = 0;
     nTra = 0;
     aDesdeSit = "A";
     aHastaSit = "A";
     |HAZ Localizador;

     |SI nEmp = 0; |Y nTra = 0;
          || no es un trab. en alta, a ver si es uno que est  en baja

          aDesdeSit = "B";
          aHastaSit = "B";
          |HAZ Localizador;

          |SI nEmp = 0; |Y nTra = 0;
               || esto ya no deberia pasar, esto es un error
               || lo grabare con el codigo 00000.00000 para saber que algo raro hay
          |FINSI;
     |FINSI;

     ||SI nEmp ! 0; || trabajador localizado
                    || o no, lo grabo siempre, aunque sea a cero
     nUltimo = 0;
     nRepetido = 0;

     |SI nEmp = 00000; |Y nTra = 00000;      || no encontrado
          |BUCLE cretam3e_0000;
     |SINO;
          |BUCLE cretam3e;
     |FINSI;

          |REPON_DATOS #cretam3e;
          #cretam3e#0 = nEmp;
          #cretam3e#1 = nTra;
          |SI nRepetido ! 0;
               #cretam3e#2 = nRepetido;
          |SINO;
               #cretam3e#2 = nUltimo + 1;
          |FINSI;
          |GRABA 020#cretam3e.c;

          || ahora el resto de datos

          #labempre#0 = nEmp;
          |LEE 000#labempre.=;

          #labtraba#0 = nEmp;
          #labtraba#1 = nTra;
          |LEE 000#labtraba.=;

          |SI #labtraba#45 = "421"; |O #labtraba#45 = "422";
               #nomcenes#0 = nEmp;
               #nomcenes#1 = #labtraba#77;
               |LEE 000#nomcenes.=;
               |SI FSalida = 0;
                    #labcentr#2 = #nomcenes#2;
               |FINSI;
          |SINO;
               #labcentr#0 = nEmp;
               #labcentr#1 = #labtraba#77;
               |LEE 000#labcentr.=;
          |FINSI;

          |SI nEmp ! 0; |Y nTra ! 0;
               #cretam3e#4 = #labempre#2;
               #cretam3e#5 = #labtraba#2;
               #cretam3e#8 = #labtraba#77;
               #cretam3e#9 = #labcentr#2;
          |SINO;
               || si no los he localizado, al menos el nombre de la empresa y
               || el del trabajador los coge de lo que me venga en el fichero
          |FINSI;

          || mas campos

          #cretam3e#30 = aSoloFichero;
          #cretam3e#31 = ~;
          |HORA aAlfa;
          |QBF aAlfa;
          #cretam3e#32 = aAlfa;

          |GRABA 020#cretam3e.a;

          |LIBERA #cretam3e;

          nNroTrab = nNroTrab + 1;

     ||SINO;
          || NAF no localizado
     ||FINSI;
|FPRC;

|PROCESO GuardaDatosGen;
     |SI aSegmento = "ETI";
          |SI #silcon23#1 = 110;        || igual para todos, fecha de generacion
               fFechaGen = aValor;
          |FINSI;
          |SI #silcon23#1 = 120;        || igual para todos, hora de generacion
               aHoraGen = aValor;
          |FINSI;
          || me los quedo
     |FINSI;
|FPRC;

|PROCESO silcon23;
     |SI #silcon23#1 = 320;        || inicio, segmento EMP
          |DDEFECTO #cretam3e;
          nNroCampos = 0;
          nNroTrab = 0;
          #cretam3e#33 = fFechaGen;
          #cretam3e#34 = aHoraGen;
     |FINSI;

     nCampo = #silcon23#8;
     nPos = #silcon23#4;
     aPos = nPos;
     nLon = #silcon23#5;
     aLon = nLon;
     aDescripcion = #silcon23#9;
     |QBF aDescripcion;
     aDesc8 = aDescripcion % 108;

     nPos = (nPos * 100) + nLon;
     aValor = aLinea % nPos;

     |SI nCampo = 6;               || problema NIF con relleno que
          aAlfa = aValor % 104;    || vino mal
          |SI aAlfa = "0000";
               aValor = aValor % 509;
          |SINO;
               aValor = aValor % 109;
          |FINSI;
     |FINSI;

     |SI #silcon23#3 = "F";
          aValor = (aValor % 702) + "." + (aValor % 502) + "." + (aValor % 104);
          |SI aValor = "00.00.0000";
               aValor = "01.01.0000";
          |FINSI;
          fValor = aValor;
     |FINSI;
     |SI #silcon23#3 = "H";
          aValor = (aValor % 102) + ":" + (aValor % 304);
     |FINSI;
     |SI #silcon23#3 = "N";
          nValor = aValor;
          |SI aDesc8 = "BASE REG";
               aValor = (aValor % 104) + "," + (aValor % 502);
               nValor = aValor;
          |FINSI;
     |FINSI;

     |SI #silcon23#3 = "A"; |O #silcon23#3 = "H";
          #cretam3e#nCampo# = aValor;
     |FINSI;
     |SI #silcon23#3 = "N";
          #cretam3e#nCampo# = nValor;
     |FINSI;
     |SI #silcon23#3 = "F";
          #cretam3e#nCampo# = fValor;
     |FINSI;

     nNroCampos = nNroCampos + 1;

     |HAZ GuardaDatosGen;
|FPRC;

|DEFBUCLE silcon23;
     #silcon23#2;
     ;
     "FIE", aSegmento, 1;
     "FIE", aSegmento, 80;
     ;
     NULL, silcon23;
|FIN;

|PROCESO BuscaValores;
     || primero me traigo los valores del fichero
     |BUCLE silcon23;
|FPRC;

|PROCESO Centra;
     aLon = aCadena % 0;
     nLon = aLon;
     nBlancos = nEspacio - nLon;
     nNume = nBlancos / 2;
     nBlancosIzd = nNume ! 0;
     nBlancosDer = nBlancos - nBlancosIzd;
     aBlancosIzd = " " * nBlancosIzd;
     aBlancosDer = " " * nBlancosDer;
     aCadenaSal = aBlancosIzd + aCadena + aBlancosDer;
|FPRC;

|PROCESO LeeLinea;
     |XLEE nHandle, 250, aLinea;
     FLinea = FSalida;
     nNroLinea = nNroLinea + 1;
     aNroLinea = nNroLinea;
     aAlfa = " Linea " + aNroLinea + " de " + aTotalLineas;

     |ATRI R;
     aCadena = aAlfa; nEspacio = 35; |HAZ Centra;
     |PINTA 2223, aCadenaSal;
     |ATRI 0;
|FPRC;

|PROCESO Lectura;
     FLinea = 0;

     |HAZ SoloFichero;
     aAlfa = eaFichero;
     swAcaba = 0;

     |XABRE "", aAlfa, nHandle; nHandle = FSalida;
     |SI FSalida ] 0;
          |PARA;
          |SI FLinea ] 0; |Y swAcaba = 0;
          |HACIENDO;
               FLinea = 0;
               |HAZ LeeLinea;

               aSegmento = aLinea % 103;
               |SI aSegmento = "ETF";        || fin de fichero
                    |HAZ Finaliza;
               |SINO;
                    |SI aSegmento = "EMP";   || fin de trabajador
                         |SI aAntSegmento ! "ETI";
                              |HAZ Finaliza; || si es ETI es que era el primero
                         |FINSI;

                         |HAZ BuscaValores;  || comienza trabajador
                    |SINO;
                         |HAZ BuscaValores;  || linea de trabajador
                    |FINSI;
               |FINSI;
               aAntSegmento = aSegmento;
          |SIGUE;
     |FINSI;

     |XCIERRA nHandle;
|FPRC;

|PROGRAMA;
     |ABRE #cretam3e;

     |ABRE #labempre;
     |ABRE #labcentr;
     |ABRE #nomcenes;
     |ABRE #labtraba;

     ||eaFichero = "/u/dsprofes/laboral/tmpsiltra/1430885303.msj";
     |QBF eaFichero;

     |HAZ Lectura;

     |CIERRA #cretam3e;

     |CIERRA #labempre;
     |CIERRA #labcentr;
     |CIERRA #nomcenes;
     |CIERRA #labtraba;
|FPRO;
