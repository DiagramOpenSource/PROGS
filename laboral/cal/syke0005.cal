|FICHEROS;
   syke0005 #0;

   nomvarca #100;
   nomvarli #101;
   nomabsca #102;
   nomabsli #103;
|FIN;

|VARIABLES;
   &eaIncid = "";

   nHandle = 0;
   nEmpre  = 0;
   nTraba  = 0;
   nMes    = 0;
   nAnyo   = 0;
   nTipoB  = 0;
   nDDia   = 0;
   nHDia   = 0;
   nHoras  = 0;
   nDesem  = 0;
   nCont   = 0;
   nCalc   = 0;

   nSwErr  = 0;
   nNulo   = 0;

   fFecha  = @;

   aTipoB  = "";
   aDto    = "";
   aHospi  = "";
   aRecai  = "";
   aAlfa    = "";
   aDestino = "";
|FIN;

|PROCESO Importacion;
   |ABRE #nomvarca; |ABRE #nomvarli;
   |ABRE #nomabsca; |ABRE #nomabsli;

   |CARGADLL "agixl97.dll";
   |SI FSalida < 0;
      |MENAV "    No se puede usar agixl97.dll"; |ACABA;
   |FINSI;

   |MENSA "    Cargando documento";

   aDestino = #syke0005#0; |QBF aDestino;
   |ALFACALLDLL "agixl97.dll", "carga_book", aDestino;
   |ALFACALLDLL "agixl97.dll", "selecciona_hoja", "Hoja1";

   |MENSA "    Ejecutando importacion";

   nNulo = 0;
   |PARA nCont = 1; |SI nNulo < 10; |HACIENDO nCont = nCont + 1;
      aAlfa = "A" + nCont; |ALFACALLDLL "agixl97.dll", "peek_dato", aAlfa; nEmpre = aAlfa;
      aAlfa = "B" + nCont; |ALFACALLDLL "agixl97.dll", "peek_dato", aAlfa; nTraba = aAlfa;
      aAlfa = "C" + nCont; |ALFACALLDLL "agixl97.dll", "peek_dato", aAlfa; nMes   = aAlfa;
      aAlfa = "D" + nCont; |ALFACALLDLL "agixl97.dll", "peek_dato", aAlfa; nAnyo  = aAlfa;
      aAlfa = "E" + nCont; |ALFACALLDLL "agixl97.dll", "peek_dato", aAlfa; aTipoB = aAlfa;
      aAlfa = "F" + nCont; |ALFACALLDLL "agixl97.dll", "peek_dato", aAlfa; nDDia  = aAlfa;
      aAlfa = "G" + nCont; |ALFACALLDLL "agixl97.dll", "peek_dato", aAlfa; nHDia  = aAlfa;
      aAlfa = "H" + nCont; |ALFACALLDLL "agixl97.dll", "peek_dato", aAlfa; nHoras = aAlfa;
      aAlfa = "I" + nCont; |ALFACALLDLL "agixl97.dll", "peek_dato", aAlfa; aDto   = aAlfa;
      aAlfa = "J" + nCont; |ALFACALLDLL "agixl97.dll", "peek_dato", aAlfa; aHospi = aAlfa;
      aAlfa = "K" + nCont; |ALFACALLDLL "agixl97.dll", "peek_dato", aAlfa; aRecai = aAlfa;
      aAlfa = "L" + nCont; |ALFACALLDLL "agixl97.dll", "peek_dato", aAlfa; nDesem = aAlfa;
      |SI nEmpre ! 0; |O nTraba ! 0;
         |SI aTipoB = "E"; |O aTipoB = "M"; |O aTipoB = "A"; |O aTipoB = "D";
            #nomvarca#0 = nEmpre;
            #nomvarca#1 = nTraba;
            #nomvarca#2 = nMes;
            #nomvarca#3 = 0;
            |LEE 101#nomvarca.=;
            |SI FSalida ! 0;
               |DDEFECTO #nomvarca;
               #nomvarca#0 = nEmpre;
               #nomvarca#1 = nTraba;
               #nomvarca#2 = nMes;
               #nomvarca#3 = 0;
               |GRABA 020#nomvarca.b;

               |DDEFECTO #nomvarli;
               #nomvarli#0 = nEmpre;
               #nomvarli#1 = nTraba;
               #nomvarli#2 = nMes;
               #nomvarli#3 = 0;
               #nomvarli#4 = 999;
               |GRABA 020#nomvarli.n;
            |FINSI;
            nSwErr = 0;
            |SI #nomvarca#12 = " ";
               #nomvarca#12 = aTipoB;
               #nomvarca#4  = nDDia;
               #nomvarca#8  = nHDia;
               |SI #nomvarca#12 = "E"; |O #nomvarca#12 = "A"; #nomvarca#31 = aHospi; |FINSI;
               |SI #nomvarca#12 = "E";                        #nomvarca#35 = aRecai; |FINSI;
               |SI #nomvarca#12 = "D";                        #nomvarca#27 = #nomvarca#27 + nDesem; |FINSI;
            |SINO;
               |SI #nomvarca#13 = " ";
                  #nomvarca#13 = aTipoB;
                  #nomvarca#5  = nDDia;
                  #nomvarca#9  = nHDia;
                  |SI #nomvarca#13 = "E"; |O #nomvarca#13 = "A"; #nomvarca#32 = aHospi; |FINSI;
                  |SI #nomvarca#13 = "E";                        #nomvarca#36 = aRecai; |FINSI;
                  |SI #nomvarca#13 = "D";                        #nomvarca#27 = #nomvarca#27 + nDesem; |FINSI;
               |SINO;
                  |SI #nomvarca#14 = " ";
                     #nomvarca#14 = aTipoB;
                     #nomvarca#6  = nDDia;
                     #nomvarca#10 = nHDia;
                     |SI #nomvarca#14 = "E"; |O #nomvarca#14 = "A"; #nomvarca#33 = aHospi; |FINSI;
                     |SI #nomvarca#14 = "E";                        #nomvarca#37 = aRecai; |FINSI;
                     |SI #nomvarca#14 = "D";                        #nomvarca#27 = #nomvarca#27 + nDesem; |FINSI;
                  |SINO;
                     |SI #nomvarca#15 = " ";
                        #nomvarca#15 = aTipoB;
                        #nomvarca#7  = nDDia;
                        #nomvarca#11 = nHDia;
                        |SI #nomvarca#15 = "E"; |O #nomvarca#15 = "A"; #nomvarca#34 = aHospi; |FINSI;
                        |SI #nomvarca#15 = "E";                        #nomvarca#38 = aRecai; |FINSI;
                        |SI #nomvarca#15 = "D";                        #nomvarca#27 = #nomvarca#27 + nDesem; |FINSI;
                     |SINO;
                        nSwErr = 1;
                        || Poner aviso de revisar manualmente este registro PEPE
                        eaIncid = "No quedan campos en la ficha de variaciones para mas datos.";
                        eaIncid = eaIncid + " Revisar manualmente la clave Empresa ";
                        eaIncid = eaIncid + (("00000" + nEmpre) % -105) + ", Trab. " + (("00000" + nTraba) % -105);
                        eaIncid = eaIncid + ", Mes " + (("00" + nMes) % -102); |PRINT;
                     |FINSI;
                  |FINSI;
               |FINSI;
            |FINSI;
            |SI nSwErr = 0;
               |GRABA 020#nomvarca.a;
            |FINSI;
            |LIBERA #nomvarca;
         |SINO;
            #nomabsca#0 = nEmpre;
            #nomabsca#1 = nTraba;
            |LEE 101#nomabsca.=;
            |SI FSalida ! 0;
               |DDEFECTO #nomabsca;
               #nomabsca#0 = nEmpre;
               #nomabsca#1 = nTraba;
               |GRABA 020#nomabsca.b;
            |FINSI;
            #nomabsli#0 = nEmpre;
            #nomabsli#1 = nTraba;
            |SI nDDia ! 0;
               fFecha = ~; aAlfa = fFecha; |QBF aAlfa; aAlfa = aAlfa % -104;
               aAlfa = (("00" + nDDia) % -102) + "." + (("00" + nMes) % -102) + "." + (("0000" + nAnyo) % -104);
            |SINO;
               aAlfa = "01." + (("00" + nMes) % -102) + "." + (("0000" + nAnyo) % -104);
            |FINSI;
            #nomabsli#2 = aAlfa;
            |LEE 000#nomabsli.=;
            |SI FSalida ! 0;
               |DDEFECTO #nomabsli;
               #nomabsli#0 = nEmpre;
               #nomabsli#1 = nTraba;
               #nomabsli#2 = aAlfa;
               |SI nHDia ! 0;
                  fFecha = ~; aAlfa = fFecha; |QBF aAlfa; aAlfa = aAlfa % -104;
                  aAlfa = (("00" + nHDia) % -102) + "." + (("00" + nMes) % -102) + "." + (("0000" + nAnyo) % -104);
               |SINO;
                  nCalc = nMes; nCalc = nCalc + 1;
                  aAlfa = "01." + (("00" + nCalc) % -102) + "." + (("0000" + nAnyo) % -104);
                  fFecha = aAlfa; nCalc = fFecha; nCalc = nCalc - 1; fFecha = nCalc;
                  aAlfa = fFecha;
               |FINSI;
               #nomabsli#3 = aAlfa;
               |SI aTipoB = "F"; aTipoB = "A"; |FINSI;
               #nomabsli#4 = aTipoB;
               #nomabsli#5 = aDto;
               #nomabsli#6 = nHoras;
               |GRABA 020#nomabsli.n;
            |SINO;
               nSwErr = 1;
               eaIncid = "Se ha intentado duplicar la linea de absentismo/huelga/vacaciones de clave Empresa ";
               eaIncid = eaIncid + (("00000" + nEmpre) % -105) + ", Trab. " + (("00000" + nTraba) % -105);
               eaIncid = eaIncid + ", Fecha " + aAlfa; |PRINT;
            |FINSI;
         |FINSI;
         nNulo = 0;
      |SINO;
         nNulo = nNulo + 1;
      |FINSI;
   |SIGUE;

   |ALFACALLDLL "agixl97.dll", "fin_book", "salva";
   |MENSA "    Cerrando documento";
   |DESCARGADLL "agixl97.dll";

   |BLIN 4;
   |CIERRA #nomvarca; |CIERRA #nomvarli;
   |CIERRA #nomabsca; |CIERRA #nomabsli;
|FPRC;

|PROCESO Browse;
   |SI FSalida = 10;
      aAlfa = "F"; |BROWSE aAlfa;
      aAlfa = aAlfa % 0299; |QBF aAlfa;
      |SI aAlfa ! "";
         #syke0005#0 = aAlfa; |PINTA #syke0005#0;
      |FINSI;
      |ERROR;
      |ACABA;
   |FINSI;

   aAlfa = #0#0; |QBF aAlfa;
   |XABRE "EC", aAlfa, nHandle;
   |SI FSalida < 0;
      |MENAV "    No encuentro el fichero"; |ERROR;
   |FINSI;
|FPRC;

|PROGRAMA;
   |CLS;
   |CABEZA "Importacion excel variaciones IT";

   |PINPA #0#0; |PINDA #0#0;
   |ENDATOS #1#0;
   |SI FSalida = 0;
      |IMPRE 0;
      |SI FSalida = 0;
         |INFOR "syke0005";
         |SI FSalida = 0;
            |HAZ Importacion;
            |SI nSwErr = 0;
               eaIncid = " La captura se ha efectuado sin incidencias"; |PRINT;
            |FINSI;
            |PIEINF; |FININF;
         |FINSI;
         |FINIMP;
      |FINSI;
   |FINSI;
|FPRO;
