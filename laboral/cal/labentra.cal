|FICHEROS;
    labentra #0;
    labregis #1;
    labempre #2;
    labtraba #3;
|FIN;

|VARIABLES;
    mod = 0;
    verrfec = "0000ATENCION Fecha erronea";
    vdia1 = "";
    vme1 = "";
    vany1 = "";
    vdia = 0;
    vme = 0;
    vany = 0;
    vtope = 0;
    vsw = 0;
    vfec = "";
    vblanco = "          ";
    vpuntos = "..........";
    cab = "ENTRADA NRO. INEM";             || recoge titulo en cabecera
    nada = "                         ";    || verificador de campo vacio
    blanco = "";                           || limpiador de pantalla
    mensa1 = "    NO EXISTE EN REGISTRO";
    mensa2 = "EMPRESA BORRADA                                 ";
    mensa3 = "TRABAJADOR BORRADO                              ";
    {-1}menu = "";
       menu1 = "2400";
       menu2 = "1";
       menu3 = "Eliga tipo lectura: ";
       menu4 = "DS";
       menu5 = " Directa ";
       menu6 = " Secuencial ";
       opcio = 0;
       fuera = 0;
       error = 0;
|FIN;

|PROGRAMA;
|CLS;
|PINPA #0#0;
|CABEZA cab;
|ABRET #1;
|ABRET #2;
|ABRET #3;
|ET almenu;
|DDEFECTO #0;
|PINDA #0#0; blanco = nada * 4;
|PINTA 1030, blanco; |PINTA 1130, blanco;
|PINTA 1230, blanco; |PINTA 1330, blanco;
    fuera = 0;
|MENU menu;
    opcio = FSalida;
|SI opcio > 0;
    |SI opcio = 1;
        |HAZ directa;
        |VETE almenu;
    |SINO;
        |HAZ secuenc;
        |VETE almenu;
    |FINSI;
|FINSI;
|CIERRAT #1;
|CIERRAT #2;
|CIERRAT #3;
|FPRO;

|PROCESO directa;
|ENCAMPO #0#0;                || pide codigo contador
|SI FSalida = 0;
    #1#0 = #0#0;              || lee en registro
    |LEE 101#1=;
    |SI FSalida = 0;
        |HAZ proceso;         || procesa si existe
    |SINO;
        |MENAV mensa1;        || da mensaje de error y limpia si no
        #0#0 = 0;
        |PINTA #0#0;
    |FINSI;
|FINSI;
|FPRC;

|PROCESO secuenc;
|LEE 101#1p;
|SI FSalida = 0;
    |SI #1#14 = nada;          || chequea primer registro
        |HAZ proceso;          || y procesa si es descargado
    |FINSI;
    |LIBERA #1;
    |ET aleer;
    |SI fuera = 0; |O fuera = 2; |O fuera = 3;
        |SI fuera = 0; |O fuera = 3;
            |LEE 101#1s;
            error = FSalida;
        |FINSI;
        |SI fuera = 2;
            |LEE 101#1a;
            error = FSalida;
        |FINSI;
        |SI error = 0;
            |SI #1#14 = nada;  || chequeando registros secuencialmente
                |HAZ proceso;
            |FINSI;
            |LIBERA #1;
            |VETE aleer;       || vuelve a leer si no eof
        |FINSI;
    |FINSI;
|FINSI;
|FPRC;

|PROCESO proceso;
    #0#1 = #1#14;
    #0#2 = #1#11;
|PINTA #0#1;
|PINTA #0#2;
    #2#0 = #1#1;
|LEE 040#2=;                   || lee empresa y pinta datos
|SI FSalida = 0;
    |PINTA 1030, #2#0;
    |PINTA 1037, #2#2;
|SINO;
    |PINTA 1037, mensa2;
|FINSI;
    #3#0 = #1#1;
    #3#1 = #1#2;
|LEE 040#3=;                   || lee trabajador y pinta datos
|SI FSalida = 0;
    |PINTA 1130, #3#1;
    |PINTA 1137, #3#2;
|SINO;
    |PINTA 1137, mensa3;
|FINSI;
    #0#0 = #1#0;
|PINTA #0#0;                   || pinta datos del registro y codigo entrado
|PINTA 1230, #1#3;
|PINTA 1330, #1#5;
|ENCAMPO #1#0;                 || entrar nuevo nro. inem
|ENCAMPO #2#0;                 || entrar nueva fecha
    fuera = FSalida;
|SI fuera = 0;
        #1#11 = #0#2;
        #1#14 = #0#1;
    |GRABA 020#1a;             || graba si todo va bien
|FINSI;
|FPRC;

|PROCESO fechacon; |TIPO 0;
        vfec = #0Campo; vsw = 0;
|SI vfec ! vblanco; |Y vfec ! vpuntos;
  vdia1 = vfec % A102;
  vme1 = vfec % A402;
  vany1 = vfec % A704;
  vdia = vdia1; vme = vme1; vany = vany1;
  |SI vdia ! 0; |Y vme ! 0; |Y vany ! 0;
     |SI vme [ 12; |Y vme ] 1;
       |SI vme = 1; |O vme = 3; |O vme = 5; |O vme = 7; |O vme = 8; |O vme = 10; |O vme = 12;
           vtope = 31;
       |SINO;
           |SI vme = 2;
               mod = vany $ 4;
               |SI mod = 0;
                   vtope = 29;
               |SINO;
                   vtope = 28;
               |FINSI;
            |SINO;
               vtope = 30;
            |FINSI;
        |FINSI;
        |SI vdia [ vtope;
            vsw = 1;
        |FINSI;
     |FINSI;
  |FINSI;
|SINO;
  vsw = 1;
|FINSI;
|SI vsw = 0;
    |MENAV verrfec;
    |ENCAMPO #2#0;
|FINSI;
|FPRC;
