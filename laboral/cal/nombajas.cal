|FICHEROS;
     nombajas;
     nombajtm;
     nomhicca;
     nomabsca;
     nomabsli;
     nomvarca;
     nomcalca;

     labtraba;
     labempre;
     labcentr;
     nomcenes;
|FIN;

|VARIABLES;
     fFecha = @;
     aFecha = "";
     aAny = "";

     nAny = 0;
     nAnyDesde = 0;
     nAnyHasta = 0;
     aAlfa = "";
     aAlfa1 = "";
     aAlfa2 = "";
     aAlfa3 = "";
     nNume = 0;
     nNume1 = 0;
     nNume2 = 0;

     nume1 = 0;
     anyo = 0;
     bisiesto = 0;
     topemes = 0;
     nMes = 0;
     aTipo = "";
     aTipoB = "";
     nDiaDesde = 0;
     nMesDesde = 0;
     nDiaHasta = 0;
     nMesHasta = 0;
     nDesde = 0;
     nHasta = 0;
     aFechaDesde = "";
     aFechaHasta = "";
     aFechaAnt = "";
     aFechaAntEnf = "";
     aFechaAntAcc = "";
     aFechaAntMat = "";
     aFechaAntPat = "";
     aFechaAntCon = "";
     aDia = "";
     aMes = "";
     nCampo = 0;
     aEstoyEn = "";
     nDesdeMes = 0;
     nHastaMes = 0;
     nElAnyo = 0;
     nDiasAntEnf = 0;
     nDiasAntAcc = 0;
     nDiasAntMat = 0;
     nDiasAntPat = 0;
     nDiasAntCon = 0;
     swSigue = 0;
     aParam = "";
     fFechaDesde = @;
     fFechaHasta = @;
     nFecha1 = 0;
     nFecha2 = 0;
     nDiasAbs = 0;
     nDesde2 = 0;
     nHasta2 = 0;
     nTipo = 0;
     nTipo2 = 0;
     nDesde3 = 0;
     nHasta3 = 0;
     swHayAlgo = 0;
     nReca1 = 0;
     nReca2 = 0;

     mesnum = 0;
     nContador = 0;
     nAcumBus = 0;
     swSal = 0;
     nDesdeBus = 0;
     nHastaBus = 0;
     nMesBus = 0;
     nAnyBus = 0;
     nCampoBus = 0;
     aAlfa0 = "";
     aFechaAntDescon = "";
     nMesDesdeBuc = 0;
     nMesHastaBuc = 0;
     aCCC = "";
     nEmpBus2 = 0;
     nAnyBus2 = 0;
     nAnyBus21 = 0;
     nAnyBus22 = 0;
     nMesBus2 = 0;
     aNifBus2 = "";
     fFecBus2 = @;
     swEncontrado = 0;
     nDiasIT = 0;

     nDiaIni = 0;
     nDiaFin = 0;
     nAnyBusca = 0;
     nMesBusca = 0;
     nEmp = 0;
     nTra = 0;
     fFechaIni = @;
     fFechaFin = @;
     nAnyLimite = 0;
     fFechaPte = @;
     aTipoPte = "";
     nDia = 0;
     nElMes = 0;
     nUltimoMesHis = 0;
     nUltimoAnyHis = 0;
     FEstado = 0;
     swMesAnterior = 0;
     aSigue = "";

     nDEmpre             = 0;
     nHEmpre             = 0;
     nOpcion             = 0;

     aEmp                = "";
     aNomEmp             = "";

     {-1}aMenu           = "";
         aMenu1          = "2400";
         aMenu2          = "1";
         aMenu3          = "Archivo documental activado. Elija opci¢n: ";
         aMenu4          = "IAB";
         aMenu5          = "Imprimir";
         aMenu6          = "Archivar";
         aMenu7          = "Ambos";

     &eaVengoDe          = "";
     &eaPeriodos         = "";
     &enDesdeMes         = 0;
     &enAny              = 0;
     &nArchi             = 0;
     &swErrorDSArchi     = 0;
|FIN;

|PROCESO Defectos; |TIPO 7;
     |SI #nombajas#6 = 2004; |O #nombajas#7 = 2004;
          fFecha = ~;
          aFecha = fFecha;
          aAny = aFecha % -104;
          nAny = aAny;

          #nombajas#6 = nAny;
          #nombajas#7 = nAny;
          #nombajas#8 = 1;
          #nombajas#9 = 12;
          |PINTA #nombajas#6;
          |PINTA #nombajas#7;
          |PINTA #nombajas#8;
          |PINTA #nombajas#9;
     |FINSI;
|FPRC;

|PROCESO impresion;
     |PRINT;
|FPRC;

|DEFBUCLE impresion;
     #nombajtm#3;
     ;
     INICIO;
     FINAL;
     ;
     NULL, impresion;
|FIN;

|PROCESO MiraTopeMes; |TIPO 0;
     nume1 = anyo $ 4;
     |SI nume1 = 0;
          bisiesto = 1;
     |SINO;
          bisiesto = 0;
     |FINSI;

     |SI mesnum = 1;   topemes = 31;  |FINSI;
     |SI mesnum = 2;   topemes = 28 + bisiesto;  |FINSI;
     |SI mesnum = 3;   topemes = 31;  |FINSI;
     |SI mesnum = 4;   topemes = 30;  |FINSI;
     |SI mesnum = 5;   topemes = 31;  |FINSI;
     |SI mesnum = 6;   topemes = 30;  |FINSI;
     |SI mesnum = 7;   topemes = 31;  |FINSI;
     |SI mesnum = 8;   topemes = 31;  |FINSI;
     |SI mesnum = 9;   topemes = 30;  |FINSI;
     |SI mesnum = 10;  topemes = 31;  |FINSI;
     |SI mesnum = 11;  topemes = 30;  |FINSI;
     |SI mesnum = 12;  topemes = 31;  |FINSI;
|FPRC;

|PROCESO DiasIT;
                    |SI aTipo = "E"; #nombajtm#8 = nDiasIT; |FINSI;
                    |SI aTipo = "A"; #nombajtm#9 = nDiasIT; |FINSI;
                    |SI aTipo = "M"; #nombajtm#10 = nDiasIT; |FINSI;
                    |SI aTipo = "R"; #nombajtm#10 = nDiasIT; |FINSI;
                    |SI aTipo = "P"; #nombajtm#10 = nDiasIT; |FINSI;
                    |SI aTipo = "F"; #nombajtm#11 = nDiasIT; |FINSI;
                    |SI aTipo = "V"; #nombajtm#12 = nDiasIT; |FINSI;
                    |SI aTipo = "D"; #nombajtm#13 = nDiasIT; |FINSI;
                    |SI aTipo = "H"; #nombajtm#14 = nDiasIT; |FINSI;
                    |SI aTipo = "C"; #nombajtm#26 = nDiasIT; |FINSI;
|FPRC;

|PROCESO nomcalca;
     || incluimos este proceso para que si una IT que viene del mes anterior
     || y continua este mes y tiene la nomina calculada, ponga como fecha fin
     || el mes que haya encontrado en el moncalca

     |ABRE #nomcalca;

     #nomcalca#0 = #nombajtm#0;
     #nomcalca#1 = #nombajtm#1;
     nNume = nMes + 1;
     |SI nNume = 13;
          nNume = 1;
     |FINSI;
     #nomcalca#2 = nNume;
     #nomcalca#3 = 0;
     |LEE 000#nomcalca.=;
     |SI FSalida = 0;
          |PARA nCampo = 27; |SI nCampo [ 30; |HACIENDO nCampo = nCampo + 1;
               aTipo = #nomcalca#nCampo#;
               nDesde = nCampo - 8;
               nHasta = nCampo - 4;
               |QBF aTipo;
               |SI aTipo = #nombajtm#29;
               |Y #nomcalca#nDesde# = 0; |Y #nomcalca#nHasta# = 0;
                    nMes = #nomcalca#2;
                    nAny = #nombajtm#4;
               |FINSI;
          |SIGUE;
     |FINSI;

     |CIERRA #nomcalca;
|FPRC;

|PROCESO BuscaHisFin;
     nUltimoMesHis = 0;
     nUltimoAnyHis = 0;
     swSigue = 1;
     nMesBusca = nMes;
     |PARA nAnyBusca = nAny;
     |SI nAnyBusca [ nAnyLimite; |Y swSigue = 1;
     |HACIENDO nAnyBusca = nAnyBusca + 1;
          |SI nMesBusca = 13;
               nMesBusca = 1;
          |FINSI;
          |PARA nMesBusca = nMesBusca;
          |SI nMesBusca [ 12; |Y swSigue = 1;
          |HACIENDO nMesBusca = nMesBusca + 1;
               #nomhicca#0 = #nombajtm#0;
               #nomhicca#1 = #nombajtm#1;
               #nomhicca#66 = nAnyBusca - 2000;
               #nomhicca#2 = nMesBusca;
               #nomhicca#3 = 0;
               |LEE 000#nomhicca.=;
               |SI FSalida = 0;
                    nUltimoMesHis = #nomhicca#2;
                    nUltimoAnyHis = #nomhicca#66 + 2000;
                    |PARA nCampo = 27; |SI nCampo [ 30; |HACIENDO nCampo = nCampo + 1;
                         aTipo = #nomhicca#nCampo#;
                         nDesde = nCampo - 8;
                         nHasta = nCampo - 4;
                         |QBF aTipo;
                         |SI aTipo = #nombajtm#29;
                              |SI #nomhicca#nDesde# = 0; |Y #nomhicca#nHasta# ! 0;
                                   aDia = #nomhicca#nHasta#; aDia = "00" + aDia; aDia = aDia % -102;
                                   aMes = #nomhicca#2; aMes = "00" + aMes; aMes = aMes % -102;
                                   nAnyHasta = #nomhicca#66 + 2000;
                                   aAny = nAnyHasta; aAny = "0000" + aAny; aAny = aAny % -104;
                                   aFechaHasta = aDia + "." + aMes + "." + aAny;
                                   nDiaHasta = aDia;
                                   swSigue = 0;
                                   |SAL;
                              |FINSI;
                         |FINSI;
                    |SIGUE;
               |FINSI;
          |SIGUE;
     |SIGUE;
|FPRC;

|PROCESO BuscaHisIni;
     swSigue = 1;
     nMesBusca = nMes;
     |PARA nAnyBusca = nAny;
     |SI nAnyBusca ] nAnyLimite; |Y swSigue = 1;
     |HACIENDO nAnyBusca = nAnyBusca - 1;
          |SI nMesBusca = 0;
               nMesBusca = 12;
          |FINSI;
          |PARA nMesBusca = nMesBusca;
          |SI nMesBusca ] 1; |Y swSigue = 1;
          |HACIENDO nMesBusca = nMesBusca - 1;
               #nomhicca#0 = #nombajtm#0;
               #nomhicca#1 = #nombajtm#1;
               #nomhicca#66 = nAnyBusca - 2000;
               #nomhicca#2 = nMesBusca;
               #nomhicca#3 = 0;
               |LEE 000#nomhicca.=;
               |SI FSalida = 0;
                    nUltimoMesHis = #nomhicca#2;
                    nUltimoAnyHis = #nomhicca#66 + 2000;
                    |PARA nCampo = 27; |SI nCampo [ 30; |HACIENDO nCampo = nCampo + 1;
                         aTipo = #nomhicca#nCampo#;
                         nDesde = nCampo - 8;
                         nHasta = nCampo - 4;
                         |QBF aTipo;
                         |SI aTipo = #nombajtm#29;
                              |SI #nomhicca#nDesde# ! 0; |Y #nomhicca#nHasta# = 0;
                                   aDia = #nomhicca#nDesde#; aDia = "00" + aDia; aDia = aDia % -102;
                                   aMes = #nomhicca#2; aMes = "00" + aMes; aMes = aMes % -102;
                                   nAnyDesde = #nomhicca#66 + 2000;
                                   aAny = nAnyDesde; aAny = "0000" + aAny; aAny = aAny % -104;
                                   aFechaDesde = aDia + "." + aMes + "." + aAny;
                                   nDiaDesde = aDia;
                                   swSigue = 0;
                                   |SAL;
                              |FINSI;
                         |FINSI;
                    |SIGUE;
               |FINSI;
          |SIGUE;
     |SIGUE;
|FPRC;

|PROCESO nombajtm_his;
     |SI #nombajtm#27 = 0;
          nMes = #nombajtm#3 - 1;
          nAny = #nombajtm#4;
          nAnyLimite = #nombajtm#4 - 2;
          |SI nMes = 0;
               nAny = nAny - 1;
               nMes = 12;
          |FINSI;

          aFechaDesde = "";
          nDiaDesde = 0;
          |HAZ BuscaHisIni;

          |SI nDiaDesde ! 0;
               #nombajtm#15 = aFechaDesde;
               #nombajtm#27 = nDiaDesde;

               fFechaDesde = #nombajtm#15;
               fFechaHasta = #nombajtm#16;
               |SI nAnyDesde < #nombajas#6;
                    aMes = #nombajas#8; |QBF aMes; aMes = ("00" + aMes) % -102;
                    aAlfa = #nombajas#6; aAlfa = "01." + aMes + "." + aAlfa;
                    fFechaDesde = aAlfa;
               |FINSI;
               nDiasIT = fFechaHasta - fFechaDesde + 1;
               |HAZ DiasIT;

               |GRABA 020#nombajtm.a;
               |LIBERA #nombajtm;
          |SINO;
               aDia = "01";
               nMes = #nombajas#8;
               nAny = #nombajas#6;
               |SI nUltimoMesHis ! 0;
                    nMes = nUltimoMesHis;
                    nAny = nUltimoAnyHis;
               |FINSI;
               aMes = nMes; |QBF aMes;  aMes = ("00" + aMes) % -102;
               aAny = nAny; |QBF aAny;  aAny = ("0000" + aAny) % -104;
               aAlfa = aDia + "." + aMes + "." + aAny;
               fFechaDesde = aAlfa;
               fFechaHasta = #nombajtm#16;
               nDiasIT = fFechaHasta - fFechaDesde + 1;
               aTipo = #nombajtm#29;
               |HAZ DiasIT;

               #nombajtm#15 = fFechaDesde;
               #nombajtm#25 = "(*)";

               |GRABA 020#nombajtm.a;
               |LIBERA #nombajtm;
          |FINSI;
     |FINSI;
     |SI #nombajtm#28 = 0;
          nMes = #nombajtm#3 + 1;
          nAny = #nombajtm#4;
          nAnyLimite = #nombajtm#4 + 2;
          |SI nMes = 13;
               nAny = nAny + 1;
               nMes = 1;
          |FINSI;

          aFechaHasta = "";
          nDiaHasta = 0;
          |HAZ BuscaHisFin;

          |SI nDiaHasta ! 0;
               #nombajtm#16 = aFechaHasta;
               #nombajtm#28 = nDiaHasta;

               fFechaDesde = #nombajtm#15; nNume1 = fFechaDesde
               fFechaHasta = #nombajtm#16; nNume2 = fFechaHasta;

               aAlfa = fFechaHasta;
               aAny = (aAlfa % -104)
               nAnyHasta = aAny;
               |SI nAnyHasta > #nombajas#7;
                    anyo = #nombajas#7;
                    mesnum = #nombajas#9;
                    |HAZ MiraTopeMes;
                    aDia = topemes; |QBF aDia;  aDia = ("00" + aDia) % -102;
                    aMes = #nombajas#9; |QBF aMes;  aMes = ("00" + aMes) % -102;
                    aAlfa = #nombajas#7; aAlfa = aDia + "." + aMes + "." + aAlfa;
                    fFechaHasta = aAlfa;
                    #nombajtm#28 = topemes;
               |FINSI;
               nDiasIT = fFechaHasta - fFechaDesde + 1;
               |HAZ DiasIT;

               |GRABA 020#nombajtm.a;
               |LIBERA #nombajtm;
          |SINO;
               nMes = #nombajtm#3;
               nAny = #nombajtm#4;

               |SI nUltimoMesHis ! 0;
                    nMes = nUltimoMesHis;
                    nAny = nUltimoAnyHis;
               |FINSI;

               |HAZ nomcalca;

               aMes = nMes; |QBF aMes;  aMes = ("00" + aMes) % -102;
               aAny = nAny; |QBF aAny;  aAny = ("0000" + aAny) % -104;

               anyo = nAny;
               mesnum = nMes;
               |HAZ MiraTopeMes;
               aDia = topemes; |QBF aDia;  aDia = ("00" + aDia) % -102;

               aAlfa = aDia + "." + aMes + "." + aAny;
               fFechaDesde = #nombajtm#15;
               fFechaHasta = aAlfa;
               nDiasIT = fFechaHasta - fFechaDesde + 1;
               aTipo = #nombajtm#29;
               |HAZ DiasIT;
               #nombajtm#16 = fFechaHasta;
               #nombajtm#24 = "(*)";

               |GRABA 020#nombajtm.a;
               |LIBERA #nombajtm;
          |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE nombajtm_his;
     #nombajtm#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, nombajtm_his;
|FIN;

|PROCESO nombajtm_lim;
     |BORRA 020#nombajtm.a;
     |LIBERA #nombajtm;
|FPRC;

|DEFBUCLE nombajtm_lim;
     #nombajtm#1;
     30;
     00001, 00001, 1999,  1, 0, " ", 1;
     99999, 99999, 2999, 12, 0, "z", 1;
     be;
     NULL, nombajtm_lim;
|FIN;

|PROCESO nombajtm_rec;
     |SI #nombajtm#27 = 0; |Y #nombajtm#28 = 0; |Y #nombajtm#30 = 0;
          |BORRA 020#nombajtm.a;
          |LIBERA #nombajtm;
          |ACABA;
     |FINSI;
     |SI #nombajtm#27 ! 0;
          fFechaPte = "";
          aTipoPte = "";
     |FINSI;
     |SI #nombajtm#28 = 0;         || baja desde X hasta 0, no tengo fecha
                                   || fin en el mes
          aTipoPte = #nombajtm#29;
          fFechaPte = #nombajtm#15;
     |FINSI;
     |SI #nombajtm#27 = 0;              || baja desde 0 hasta X, dia fin de
                                        || una que venia de mes/es anterior/es
          |SI aTipoPte = #nombajtm#29;
               |SALVA_FICHA #nombajtm;
               fFechaFin = #nombajtm#16;
               nDiaFin = #nombajtm#28;

               #nombajtm#0 = #nombajtm#0;
               #nombajtm#1 = #nombajtm#1;
               aAlfa = fFechaPte; aAlfa = aAlfa % -104; nAny = aAlfa;
               aAlfa = fFechaPte; aAlfa = aAlfa % 402; nMes = aAlfa;
               aAlfa = fFechaPte; aAlfa = aAlfa % 102; nDia = aAlfa;
               #nombajtm#4 = nAny;
               #nombajtm#3 = nMes;
               #nombajtm#27 = nDia;
               #nombajtm#29 = aTipoPte;
               |LEE 101#nombajtm.=;
               FEstado = FSalida;
               |SI FSalida = 0;
                    #nombajtm#16 = fFechaFin;
                    #nombajtm#28 = nDiaFin;
                    nNume1 = #nombajtm#15;
                    nNume2 = #nombajtm#16;
                    #nombajtm#24 = "";

                    aAlfa = fFechaFin;
                    aAny = (aAlfa % -104)
                    nAnyHasta = aAny;
                    |SI nAnyHasta > #nombajas#7;
                         anyo = #nombajas#7;
                         mesnum = #nombajas#9;
                         |HAZ MiraTopeMes;
                         aDia = topemes; |QBF aDia;  aDia = ("00" + aDia) % -102;
                         aMes = nMes; |QBF aMes;  aMes = ("00" + aMes) % -102;
                         aAlfa = #nombajas#7; aAlfa = aDia + "." + aMes + "." + aAlfa;
                         fFechaHasta = aAlfa;
                         nNume2 = fFechaHasta;
                    |FINSI;
                    nDiasIT = nNume2 - nNume1 + 1;
                    aTipo = #nombajtm#29;
                    |HAZ DiasIT;

                    |GRABA 020#nombajtm.a;
                    |LIBERA #nombajtm;
               |FINSI;
               |REPON_FICHA #nombajtm;       || una que venia de mes/es anterior/es

               |SI FEstado = 0;              || si la he encontrado
                    |BORRA 020#nombajtm.a;
                    |LIBERA #nombajtm;
               |FINSI;
          |FINSI;
     |FINSI;
/*
     |SI #nombajtm#28 ! 0;              || baja desde 0 hasta X, dia fin de
          || borro restos "desde 0 hasta 0" del mismo tipo de IT
          || de meses anteriores

          |SALVA_FICHA #nombajtm;

          |PARA nMesBusca = nMes; |SI nMesBusca ] 1; |HACIENDO nMesBusca = nMesBusca - 1;
               #nombajtm#0 = #nombajtm#0;
               #nombajtm#1 = #nombajtm#1;
               #nombajtm#4 = #nombajtm#4;
               #nombajtm#3 = nMesBusca;
               #nombajtm#27 = 0;
               #nombajtm#29 = aTipoPte;
               |SI FSalida = 0;
                    |BORRA 020#nombajtma;
                    |LIBERA #nombajtm;
               |FINSI;
          |SIGUE;

          ||BUCLE BorraRestos;
          |REPON_FICHA #nombajtm;
     |FINSI;
*/
|FPRC;

|DEFBUCLE nombajtm_rec;
     #nombajtm#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, nombajtm_rec;
|FIN;

|PROCESO nombajtm_lim2;
     |ABRE #nombajtm;

     |LEE 000#nombajtm.p;
     |PARA; |SI; |HACIENDO;
          nEmp = #nombajtm#0;
          nTra = #nombajtm#1;
          nDia = #nombajtm#27;
          aTipo = #nombajtm#29;
          aSigue = #nombajtm#24;
          |LEE 000#nombajtm.s;

          |SI FSalida ! 0;
               |SAL;
          |FINSI;

          |SI nEmp = #nombajtm#0; |Y nTra = #nombajtm#1; |Y nDia = #nombajtm#27; |Y aTipo = #nombajtm#29;
          |Y aSigue = "(*)";
               || es el mismo que el anterior, borro el anterior

               |LEE 000#nombajtm.a;
               |BORRA 000#nombajtm.a;
               |LEE 000#nombajtm.s;
          |FINSI;
     |SIGUE;

     |CIERRA #nombajtm;
|FPRC;

|PROCESO FiltraTMP;
     |ABRE #nombajtm;
     |ABRE #nomhicca;

     fFecha = ~;
     aFecha = fFecha;
     aAny = aFecha % -104;
     nAnyLimite = aAny;

     |BUCLE nombajtm_rec;
     |BUCLE nombajtm_his;
     |BUCLE nombajtm_lim;
     |HAZ nombajtm_lim2;

     |CIERRA #nomhicca;
     |CIERRA #nombajtm;
|FPRC;

|PROCESO grababajas;
     |SI aParam = "";
          #labtraba#0 = #nomhicca#0;
          #labtraba#1 = #nomhicca#1;
          |LEE 000#labtraba.=
     |FINSI;

     |SI #labtraba#77 ] #nombajas#2; |Y #labtraba#77 [ #nombajas#3;  || compruebo centro
          #labempre#0 = #nomhicca#0;
          |LEE 000#labempre.=
          #labcentr#0 = #labtraba#0;
          #labcentr#1 = #labtraba#77;
          |LEE 000#labcentr.=;
          aCCC = #labcentr#10;

          |SI #labtraba#45 = "085"; |O #labtraba#45 = "421";
               #nomcenes#0 = #labtraba#0;
               #nomcenes#13 = #labtraba#77;
               #nomcenes#1 = "87";
               |LEE 000#nomcenes.=;
               |SI FSalida = 0;
                    aCCC = #nomcenes#10;
               |FINSI;
          |FINSI;

          |DDEFECTO #nombajtm;
          #nombajtm#0 = #nomhicca#0;    || empresa
          #nombajtm#1 = #nomhicca#1;    || trabajador
          #nombajtm#3 = #nomhicca#2;    || mes
          |SI aEstoyEn = "H";
               |SI #nomhicca#66 < 80;
                    #nombajtm#4 = #nomhicca#66 + 2000;   || a¤o
               |SINO;
                    #nombajtm#4 = #nomhicca#66 + 1900;   || a¤o
               |FINSI;
          |SINO;
               #nombajtm#4 = nElAnyo;          || a¤o
          |FINSI;
          #nombajtm#2 = #labtraba#77;   || centro
          #nombajtm#5 = #labempre#2;    || nombre empresa
          #nombajtm#6 = #labtraba#2;    || nombre trabajador
          #nombajtm#7 = #labcentr#2;    || nombre centro

          #nombajtm#18 = #labtraba#7;   || NIF trabajador
          #nombajtm#19 = #labtraba#75;  || NAFSS trabajador
          #nombajtm#20 = #labempre#10;  || CIF empresa
          #nombajtm#21 = aCCC;          || CCC empresa

          anyo = #nombajtm#4;
          mesnum = nMes;
          |HAZ MiraTopeMes;

          nNume1 = #nomhicca#nDesde#;
          |SI nNume1 = 0;
               nNume1 = 1;
          |FINSI;
          nNume2 = #nomhicca#nHasta#;
          |SI nNume2 = 0;
               nNume2 = topemes;
          |FINSI;
          nDiasIT = nNume2 - nNume1 + 1;

          |SI aTipo ! "H"; |Y aTipo ! "F"; |Y aTipo ! "V"; |Y aTipo ! "f";
               |SI aTipo = "E";
                    #nombajtm#8 = nDiasIT;   || dias enfermedad
                    #nombajtm#17 = "Enfermedad";
               |FINSI;
               |SI aTipo = "A";
                    #nombajtm#9 = nDiasIT;   || dias accidente
                    #nombajtm#17 = "Accidente";
                    nNume = nCampo + 107;
                    |SI #nomhicca#nNume# = "X";
                         #nombajtm#17 = "Enf. profesional";
                    |FINSI;
               |FINSI;
               |SI aTipo = "M";
                    #nombajtm#10 = nDiasIT;   || dias maternidad
                    #nombajtm#17 = "Maternidad";
               |FINSI;
               |SI aTipo = "R";
                    #nombajtm#10 = nDiasIT;   || dias Riesgo
                    #nombajtm#17 = "Riesgo Emb.";
               |FINSI;
               |SI aTipo = "P";
                    #nombajtm#10 = nDiasIT;   || dias paternidad
                    #nombajtm#17 = "Paternidad";
               |FINSI;
               |SI aTipo = "V";
                    #nombajtm#12 = nDiasIT;   || dias vacaciones
                    #nombajtm#17 = "Vacaciones";
               |FINSI;
               |SI aTipo = "D";
                    #nombajtm#13 = nDiasIT;   || dias desempleo
                    #nombajtm#17 = "Desempleo";
               |FINSI;
               |SI aTipo = "C";
                    #nombajtm#26 = nDiasIT;   || dias control IT SS
                    #nombajtm#17 = "Control IT SS";
               |FINSI;

               nAny = #nombajtm#4;                  || a¤o
               nDiaDesde = #nomhicca#nDesde#;     || dia desde
               nDiaHasta = #nomhicca#nHasta#;     || dia hasta

               nMesDesde = nMes;                  || mes
               nMesHasta = nMes;                  || mes

               #nombajtm#27 = nDiaDesde;
               #nombajtm#28 = nDiaHasta;
               #nombajtm#29 = aTipo;

               aFechaDesde = "";

               |SI nDiaDesde ! 0;
                    aDia = nDiaDesde;   aDia = "00" + aDia;  aDia = aDia % -102;
                    aMes = nMesDesde;   aMes = "00" + aMes;  aMes = aMes % -102;
                    aAny = nAny;
                    aFechaDesde = aDia + "." + aMes + "." + aAny;
                    #nombajtm#15 = aFechaDesde;   || fecha desde
               |FINSI;
               |SI nDiaHasta ! 0;
                    aDia = nDiaHasta;   aDia = "00" + aDia;  aDia = aDia % -102;
                    aMes = nMesHasta;   aMes = "00" + aMes;  aMes = aMes % -102;
                    aAny = nAny;
                    aFechaHasta = aDia + "." + aMes + "." + aAny;
                    #nombajtm#16 = aFechaHasta;   || fecha hasta
               |FINSI;
               |SI nDiaHasta = 0; |Y aEstoyEn = "VV";
                    anyo = #nombajtm#4;
                    mesnum = #nomhicca#2;
                    |HAZ MiraTopeMes;
                    aDia = topemes;   aDia = "00" + aDia;  aDia = aDia % -102;
                    aMes = mesnum;   aMes = "00" + aMes;  aMes = aMes % -102;
                    aAny = nAny;
                    aFechaHasta = aDia + "." + aMes + "." + aAny;
                    #nombajtm#16 = aFechaHasta;   || fecha hasta
                    #nombajtm#28 = topemes;
                    #nombajtm#24 = "(*)";
               |FINSI;
               |SI swMesAnterior = 1;   || para irme despues a buscar el
                    #nombajtm#24 = "(*)";    || ultimo dia del mes que pido
                    |SI #nombajtm#27 = 0; |Y #nombajtm#28 = 0;
                         #nombajtm#30 = 1;
                    |FINSI;
               |FINSI;
               |GRABA 020#nombajtm.n;
          |SINO;
               |DDEFECTO #nomabsca;
               |ABRE #nomabsca;
               #nomabsca#0 = #nomabsli#0;
               #nomabsca#1 = #nomabsli#1;
               |LEE 000#nomabsca.=;
               |CIERRA #nomabsca;

               nFecha1 = #nomabsli#2; || Fecha Desde
               nFecha2 = #nomabsli#3; || Fecha Hasta
               nDiasAbs = nFecha2 - nFecha1 + 1;
               |SI #nomabsli#6 = 0;
                    #nomabsli#6 = #labtraba#47;
               |FINSI;
               |SI aTipo = "F"; |O aTipo = "f";
                    |SI nDiasAbs > 0;
                         #nombajtm#11 = nDiasAbs;
                         |SI #nomabsli#6 = 0;
                              #nombajtm#22 = nDiasAbs * #nomabsca#2; || horas
                         |SINO;
                              #nombajtm#22 = nDiasAbs * #nomabsli#6; || horas
                         |FINSI;
                    |SINO;
                         #nombajtm#11 = ((#nomabsli#6 / #labtraba#47) ! 2);   || dias
                         #nombajtm#22 = #nomabsli#6; || horas
                    |FINSI;
                    #nombajtm#17 = "Absentismo";
               |FINSI;
               |SI aTipo = "H";
                    #nombajtm#14 = nDiasAbs;
                    |SI #nomabsli#7 > 0;
                         #nombajtm#14 = #nombajtm#14 + (#nomabsli#7 / 100);
                    |FINSI;
                    |SI #nomabsli#6 > 0;
                         |SI #nombajtm#14 = 1;    || un dia de huelga, pero
                              #nombajtm#14 = 0;   || solo horas
                         |FINSI;
                         #nombajtm#23 = #nombajtm#23 + #nomabsli#6;
                    |FINSI;
                    |SI #nomabsli#7 > 0;
                         #nombajtm#23 = #nombajtm#23 + (#nombajtm#14 * #nomabsca#2);
                         |SI #nombajtm#14 = 0; || solo horas
                              #nombajtm#23 = #nombajtm#23 + ((#nomabsli#7 / 100) * #nomabsca#2);
                         |FINSI;
                    |FINSI;
                    #nombajtm#17 = "Huelga";
               |FINSI;
               |SI aTipo = "V";
                    #nombajtm#12 = nDiasAbs;
                    #nombajtm#17 = "Vacaciones";
               |FINSI;
               nAny = #nombajtm#4;                  || me hace falta para despues
               aFechaDesde = #nomabsli#2;   || fecha desde
               aFechaHasta = #nomabsli#3;   || fecha hasta

               #nombajtm#15 = aFechaDesde;   || fecha desde
               #nombajtm#16 = aFechaHasta;   || fecha hasta

               aAlfa = aFechaDesde % 102;
               #nombajtm#27 = aAlfa;              || Dia Desde
               aAlfa = aFechaHasta % 102;
               #nombajtm#28 = aAlfa;              || Dia Hasta
               #nombajtm#29 = aTipo;
               |GRABA 020#nombajtm.n;
          |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE nomabsli;
     #nomabsli#1;
     4;
     #labtraba#0, #labtraba#1, fFechaDesde, aTipoB;
     #labtraba#0, #labtraba#1, fFechaHasta, aTipoB;
     ;
     NULL, grababajas;
|FIN;

|PROCESO buscabajas;
     swHayAlgo = 1;
     |SI aEstoyEn = "VA";
          #nomhicca#0 = #nomabsli#0;
          #nomhicca#1 = #nomabsli#1;
          #nomhicca#2 = nMes;
          #nomhicca#3 = 0;
     |FINSI;
     nMes = #nomhicca#2;
     |SI aEstoyEn = "H";
          |SI #nomhicca#66 < 80;
               anyo = #nomhicca#66 + 2000;
          |SINO;
               anyo = #nomhicca#66 + 1900;
          |FINSI;
     |FINSI;

     |PARA nCampo = 27; |SI nCampo [ 30; |HACIENDO nCampo = nCampo + 1;
          aTipo = #nomhicca#nCampo#;
          nDesde = nCampo - 8;
          nHasta = nCampo - 4;
          |QBF aTipo;
          swSigue = 0;
          |SI aTipo ! "";
               |SI aTipo = "E"; |Y #nombajas#10 = "X"; swSigue = 1; |FINSI;
               |SI aTipo = "A"; |Y #nombajas#11 = "X"; swSigue = 1; |FINSI;
               |SI aTipo = "M"; |Y #nombajas#12 = "X"; swSigue = 1; |FINSI;
               |SI aTipo = "P"; |Y #nombajas#12 = "X"; swSigue = 1; |FINSI;
               |SI aTipo = "R"; |Y #nombajas#12 = "X"; swSigue = 1; |FINSI;
               |SI aTipo = "V"; |Y #nombajas#14 = "X"; swSigue = 1; |FINSI;
               |SI aTipo = "D"; |Y #nombajas#15 = "X"; swSigue = 1; |FINSI;
               |SI aTipo = "C"; |Y #nombajas#17 = "X"; swSigue = 1; |FINSI;

               || esto es para que no me entre una baja del mes anterior al
               || intervalo que he pedido si no se continua en el mes que si
               || que he pedido

               |SI swMesAnterior = 1; |Y #nomhicca#nHasta# ! 0;
                    swSigue = 0;
               |FINSI;

              |SI swSigue = 1;
                    |HAZ grababajas;
               |FINSI;
          |FINSI;
     |SIGUE;

     |SI #nomhicca#2 ] #nombajas#8; |Y #nombajas#6 = anyo;
     || solo las que correspondan al intervalo de meses pedido
          aMes = #nomhicca#2;        || desde mes
          aMes = "00" + aMes;
          aMes = aMes % -102;
          aAny = anyo;        || desde a¤o
          aFecha = "01" + "." + aMes + "." + aAny;
          fFechaDesde = aFecha;

          nMes = #nomhicca#2;
          mesnum = nMes;
          |HAZ MiraTopeMes;
          aDia = topemes;
          aMes = nMes;   aMes = "00" + aMes;      aMes = aMes % -102;
          aAny = anyo;
          aFecha = aDia + "." + aMes + "." + aAny;
          fFechaHasta = aFecha;

          |SI #nombajas#13 = "X";
               aTipo = "F";   || tipo para buscar en el mto.
               aTipoB = "A";  || tipo para el bucle
               swSigue = 1;
               |BUCLE nomabsli;
          |FINSI;
          |SI #nombajas#14 = "X";
               aTipo = "V";   || tipo para buscar en el mto.
               aTipoB = "V";  || tipo para el bucle
               swSigue = 1;
               |BUCLE nomabsli;
          |FINSI;
          |SI #nombajas#16 = "X";
               aTipo = "H";   || tipo para buscar en el mto.
               aTipoB = "H";  || tipo para el bucle
               swSigue = 1;
               |BUCLE nomabsli;
          |FINSI;
     |FINSI;
     nDesdeMes = #nomhicca#2;      || para saber con que mes seguir por el nomcalca
|FPRC;

|PROCESO buscavar;
     |DDEFECTO #nomhicca;
     #nomhicca#0 = #nomvarca#0;
     #nomhicca#1 = #nomvarca#1;
     #nomhicca#2 = #nomvarca#2;
     #nomhicca#3 = #nomvarca#3;
     |PARA nCampo = 12; |SI nCampo [ 15; |HACIENDO nCampo = nCampo + 1;
          nDesde = nCampo - 8;
          nHasta = nCampo - 4;
          aTipo = #nomvarca#nCampo#;
          |QBF aTipo;
          |SI aTipo = "E"; |O aTipo = "A"; |O aTipo = "V"; |O aTipo = "D";
          |O aTipo = "M"; |O aTipo = "P"; |O aTipo = "R"; |O aTipo = "C";
               nDesde2 = nDesde + 15;
               nHasta2 = nHasta + 15;
               nTipo2 = nCampo + 15;
               nReca1 = nCampo + 23;
               nReca2 = nCampo + 122;
               nMes = #nomvarca#2;

               |SI aParam ! "";
                    fFecha = ~;
                    aFecha = fFecha;
                    aAlfa = aFecha % -104;
                    anyo = aAlfa;
                    nElAnyo = anyo; || me hara falta despues
               |SINO;
                    anyo = nElAnyo;
               |FINSI;
               mesnum = nMes;
               |HAZ MiraTopeMes;

               nDesde3 = #nomvarca#nDesde#;
               nHasta3 = #nomvarca#nHasta#;
               |SI nDesde3 = 0;
                    nDesde3 = 1;
               |FINSI;
               |SI nHasta3 = 0;
                    nHasta3 = topemes;
               |FINSI;
               #nomhicca#nDesde2# = #nomvarca#nDesde#;
               #nomhicca#nHasta2# = #nomvarca#nHasta#;
               #nomhicca#nTipo2#  = aTipo;

               |SI aTipo = "E";
                    #nomhicca#31 = nHasta3 - nDesde3 + 1;
               |FINSI;
               |SI aTipo = "A";
                    #nomhicca#32 = nHasta3 - nDesde3 + 1;
                    #nomhicca#nReca2# = #nomvarca#nReca1#;
               |FINSI;
               |SI aTipo = "M"; |O aTipo = "P"; |O aTipo = "R";
                    #nomhicca#65 = nHasta3 - nDesde3 + 1;
               |FINSI;
               |SI aTipo = "V";
                    #nomhicca#66 = nHasta3 - nDesde3 + 1;
               |FINSI;
               |SI aTipo = "D";
                    #nomhicca#78 = nHasta3 - nDesde3 + 1;
               |FINSI;
               |SI aTipo = "C";
                    #nomhicca#131 = nHasta3 - nDesde3 + 1;
               |FINSI;
          |FINSI;
     |SIGUE;
     |HAZ buscabajas;
|FPRC;

|DEFBUCLE variaciones;
     #nomvarca#1;
     ;
     #labtraba#0, #labtraba#1, nDesdeMes, 0;
     #labtraba#0, #labtraba#1, nHastaMes, 0;
     ;
     NULL, buscavar;
|FIN;

|DEFBUCLE nomhicca;
     #nomhicca#1;
     ;
     #labtraba#0, #labtraba#1, nAnyDesde, nMesDesdeBuc, 0;
     #labtraba#0, #labtraba#1, nAnyHasta, nMesHastaBuc, 0;
     ;
     NULL, buscabajas;
|FIN;

|PROCESO ElInforme;
     |LEE 000#nombajtm.p;
     |SI FSalida ! 0;
         |MENAV "0000Selección vacía";
         |ACABA;
     |FINSI;

     Impresora = "CrystalReport";
     |IMPRE -1;
     |SI aParam = "";
          |INFOR "nombajas";
     |SINO;
          |INFOR "nombaja2";
     |FINSI;

     |BUCLE impresion;

     |PIEINF;
     |FININF;
     |FINIMP;
|FPRC;

|PROCESO ElProceso;
     aFechaAnt = "";
     nDiasAntEnf = 0;
     nDiasAntAcc = 0;
     nDiasAntMat = 0;

     ||SI aParam = "";
          nDesdeMes = 0;
          |SI #nombajas#6 ! #nombajas#7;
               nMesDesdeBuc = #nombajas#8;
               nMesHastaBuc = 12;

               nAnyDesde = #nombajas#6 - 2000;
               nAnyHasta = #nombajas#6 - 2000;
               aEstoyEn = "H";
               |BUCLE nomhicca;

               nNume = #nombajas#7 - #nombajas#6;
               |SI nNume > 1;
                    nMesDesdeBuc = 01;
                    nMesHastaBuc = 12;

                    nAnyDesde = #nombajas#6 - 2000 + 1;
                    nAnyHasta = #nombajas#7 - 2000 - 1;
                    aEstoyEn = "H";
                    |BUCLE nomhicca;
               |FINSI;

               nMesDesdeBuc = 01;
               nMesHastaBuc = #nombajas#9;

               nAnyDesde = #nombajas#7 - 2000;
               nAnyHasta = #nombajas#7 - 2000;
               aEstoyEn = "H";
               |BUCLE nomhicca;
          |SINO;
               nMesDesdeBuc = #nombajas#8;
               nMesHastaBuc = #nombajas#9;

               nAnyDesde = #nombajas#6 - 2000;
               nAnyHasta = #nombajas#7 - 2000;

               aEstoyEn = "H";
               |BUCLE nomhicca;
          |FINSI;

          || busco mes anterior a lo pedido para grabar posibles ITs que
          || comenzaran el mes pasado y sigan en curso si fecha de baja
          || en variaciones

          nMesDesdeBuc = #nombajas#8 - 1;
          nAnyDesde = #nombajas#7 - 2000;
          |SI nMesDesdeBuc = 0;
               nMesDesdeBuc = 12;
               nAnyDesde = #nombajas#7 - 2000 - 1;
          |FINSI;
          nMesHastaBuc = nMesDesdeBuc;
          nAnyHasta = nAnyDesde;
          aEstoyEn = "H";
          swMesAnterior = 1;
          |BUCLE nomhicca;
          swMesAnterior = 0;

     fFecha = ~;
     aFecha = fFecha;
     aAlfa = aFecha % 402;
     nElMes = aAlfa;
     aAlfa = aFecha % -104;
     nElAnyo = aAlfa;
     nHastaMes = #nombajas#9;
     |SI nDesdeMes = 12;
          nDesdeMes = 0;
     |FINSI;

     swSigue = 0;
     |SI nElAnyo = #nombajas#7;      || miro variaciones si el a¤o hasta es el a¤o
          nDesdeMes = #nombajas#8;   || en curso, desde el mes hasta, hasta el mes 12
          nHastaMes = 12;
          swSigue = 1;
     |FINSI;

     nNume = nElAnyo - 1;
     |SI nNume = #nombajas#7; |Y nElMes = 1; |Y nHastaMes = 12;
          || he pedido un informe del a¤o 2012 hasta el mes 12 por ejemplo
          || hago esto para mirar las variaciones de 2013 (con enero bastaria
          || pero mirare de enero a diciembre)
          nDesdeMes = 1;
          nHastaMes = 12;
          swSigue = 1;
     |FINSI;

     |SI aParam ! "";         || si no desde el proceso por nif
          nAny = nElAnyo;     || no miraba en el nomvarca
          swSigue = 1;
     |FINSI;

     |SI swSigue = 1;
          aEstoyEn = "VV";         || busca nomvarca
          |SI #labtraba#44 = "A";
               |BUCLE variaciones;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO PorDNI;
     #nombajas#0 = #labtraba#0;      || empresa desde
     #nombajas#1 = #labtraba#0;      || empresa hasta
     #nombajas#4 = #labtraba#1;      || trabajador desde
     #nombajas#5 = #labtraba#1;      || trabajador hasta
     #nombajas#2 = 001;              || centro desde
     #nombajas#3 = 999;              || centro hasta
     #nombajas#6 = 2000;             || a¤o desde
     #nombajas#7 = 2099;             || a¤o hasta
     #nombajas#8 = 01;               || mes desde
     #nombajas#9 = 12;               || mes hasta

     fFecha = ~;
     aFecha = fFecha;
     aAny = aFecha % -104;
     nAny = aAny;

     |HAZ ElProceso;
|FPRC;

|DEFBUCLE PorDNI;
     #labtraba#7;
     ;
     aParam, nDEmpre, 00001;
     aParam, nHEmpre, 99999;
     ;
     NULL, PorDNI;
|FIN;

|DEFBUCLE DesdeHasta;
     #labtraba#7;
     77;
     "            ", nDEmpre, #nombajas#4, #nombajas#2;
     "zzzzzzzzzzzz", nHEmpre, #nombajas#5, #nombajas#3;
     ;
     NULL, ElProceso;
|FIN;

|PROCESO Papel;
     aAlfa = Usuario; |QBT aAlfa; aAlfa = ("b" + aAlfa) % 108;
     |NOME_DAT #nombajtm#aAlfa#;
     |DELINDEX #nombajtm;

     |ABRET #nombajtm;

     |SI aParam ! "";
         |BUCLE PorDNI;
     |SINO;
         |BUCLE DesdeHasta;
     |FINSI;

     |HAZ FiltraTMP;
     |HAZ ElInforme;

     |CIERRAT #nombajtm;
     |DELINDEX #nombajtm;
|FPRC;

|PROCESO Mensaje;
     aEmp = "                                             ";

     aAlfa   = ("00000" + #labempre#0) % -105;
     aNomEmp = #labempre#2;
     aNomEmp = aNomEmp % 126;
     aEmp = "Empresa: " + aAlfa + " - " + aNomEmp;
     aEmp = " " + aEmp + " ";

     |CUADRO 1217 1619;
     |ATRI R;
     |PINTA 1318, "       - Procesando archivo de absentismos -        ";
     |PINTA 1418, "                                                    ";
     |PINTA 1518, "                                                    ";
     |PINTA 1518, aEmp;
     |ATRI 0;
|FPRC;

|PROCESO labempre;
     aAlfa = Usuario; |QBT aAlfa; aAlfa = ("b" + aAlfa) % 108;
     |NOME_DAT #nombajtm#aAlfa#;
     |DELINDEX #nombajtm;

     |ABRET #nombajtm;

     nDEmpre = #labempre#0;
     nHEmpre = #labempre#0;

     |SI aParam ! "";
         |BUCLE PorDNI;
     |SINO;
         |BUCLE DesdeHasta;
     |FINSI;

     |HAZ FiltraTMP;

     |LEE 000#nombajtm.p;
     |SI FSalida ! 0;
         |ACABA;
     |FINSI;

     |HAZ Mensaje;
     |HAZ IMPREArchi;

     |BUCLE impresion;

     |PIEINF;
     |FININF;

     |SI swErrorDSArchi = 0;
         |HAZ Archivando;
     |FINSI;

     |CIERRAT #nombajtm;
     |DELINDEX #nombajtm;
|FPRC;

|DEFBUCLE labempre;
     #labempre#1;
     ;
     #nombajas#0;
     #nombajas#1;
     ;
     NULL, labempre;
|FIN;

|PROCESO Archivado;
     eaPeriodos = ("00" + #nombajas#8) % -102;
     eaPeriodos = eaPeriodos + "/" + #nombajas#6;
     eaPeriodos = eaPeriodos + " a  " + (("00" + #nombajas#9) % -102);
     eaPeriodos = eaPeriodos + "/" + #nombajas#7;

     enDesdeMes = #nombajas#8;
     enAny      = #nombajas#6;

     |BUCLE labempre;
|FPRC;

|PROCESO T3;  |TIPO 3;
     nDEmpre = #nombajas#0;
     nHEmpre = #nombajas#1;

     |ABRET #labempre;
     |ABRET #labtraba;
     |ABRET #labcentr;
     |ABRET #nomcenes;

     eaVengoDe = "nombajas";
     |SI aParam ! "";
         eaVengoDe = "nombaja2";
     |FINSI;

     |HAZ MiraSiArchiva;
     |SI nArchi = 0;
         |HAZ Papel;

         |CIERRAT #nomcenes;
         |CIERRAT #labcentr;
         |CIERRAT #labtraba;
         |CIERRAT #labempre;

         |ACABA;
     |FINSI;

     |MENU aMenu;
     nOpcion = FSalida;
     |SI nOpcion = 1;  |HAZ Papel;      |FINSI;
     |SI nOpcion = 2;  |HAZ Archivado;  |FINSI;
     |SI nOpcion = 3;  |HAZ Papel;      |FINSI;
     |SI nOpcion = 3;  |HAZ Archivado;  |FINSI;

     |CIERRAT #nomcenes;
     |CIERRAT #labcentr;
     |CIERRAT #labtraba;
     |CIERRAT #labempre;
|FPRC;

|PROGRAMA;
     aParam = PARAMETRO;
     |QBF aParam;
     |SI aParam = "";
          |CLS;
          |PINPA #0#nombajas

          |ABRE #nombajas;
          |DDEFECTO #nombajas;
          |LEE 000#nombajas.p;
          |SI FSalida = 0;  |BORRA 020#nombajas.a;  |FINSI;
          |PINDA #0#nombajas;
          |ENDATOS #1#nombajas;
     |SINO;
          |HAZ T3;
     |FINSI;

     |SI aParam = "";
          |GRABA 000#nombajas.n;
          |CIERRA #nombajas;
     |FINSI;
|FPRO;
