|FICHEROS;
  nomxvenc #0;      || Fichero informe vencimientos
  labtraba #1;      || Fichero de datos trabajadores
      dsnetm00@ #100;    || parametros dsnetcom
     nombotra;
     nomvarca;
|FIN;

|VARIABLES;
 desde = 0;
 hasta = 0;
 mirar = 0;
   p_mes = "";
   fecsys = @;
   alfa1 = "";
   informe = "nomxvenc";
   error1 = "     Proceso abortado !!!";
   error2 = "     No existe informe";
   swprint = 0;                || Variable para saber si ha imprimido o no
   &fecantic = "";             || Variable informe
   &fecplus  = "";             || Variable informe
   &fecbonif = "";             || Variable informe
   swantic = 0;                || sw para saber si imprime o no
   swbonif = 0;                || sw para saber si imprime o no
   swplus  = 0;                || sw para saber si imprime o no
   mes = "";                   || Recoge el mes alfanumericamente
   anyo = "";                  || Recoge el anyo alfanumericamente
   mesnum = 0;                 || Recoge el mes numericamente
   anyonum = 0;                || Recoge el anyo numericamente

     aDirDes = "";
     aRutaDes = "";
     aRuta = "";
     aRutaDatos = "";
     nCanal = 0;
     aArchivo = "";
     aCabecera = "";
     aTipoResumen = "";
     aTipoNomina = "";
     aEmpresa = "";
     aAnyoDesde = "";
     aAnyoHasta = "";
     aMesDesde = "";
     aMesHasta = "";
     aOrdenacion = "";
     aAnyo = "";
     aDireccion = "";
     aTrabajadorDesde = "";
     aTrabajadorHasta = "";
     nEmpresa = 0;
     nAnyo = 0;
     nMesDesde = 0;
     nMesHasta = 0;
     nOrdenacion = 0;
     nDireccion = 0;
     nTrabajadorDesde = 0;
     nTrabajadorHasta = 0;

     cParam         = "";
     cParte         = "";
     &cOrigen        = "";
     &cDestino       = "";
     cDirBase       = "";
     cUsuario       = "";
     cIp            = "";
     cDirUsu        = "";
     cNumUsu        = "";
     aAlfa = "";
     aAlfa1 = "";
     aTabla = "";
     aDir = "";
     &nErrorInf = 0;
     aSystem = "";
     aBaseBin = "";
     aDirOri = "";

     nLongitud = 0;
     aLongitud = "";
     &swDesdeWeb = 0;
     nBonif = 0;
     nMes = 0;
|FIN;

|PROCESO inicia;
    desde = (#0#5 * 100) + #0#4;
    hasta = (#0#8 * 100) + #0#7;
|FPRC;

|PROCESO nombotra;
     nBonif = #nombotra#2;
     |ERROR10;
|FPRC;

|DEFBUCLE nombotra;
     #nombotra#1;
     ;
     #labtraba#0, #labtraba#1, 001;
     #labtraba#0, #labtraba#1, 999;
     ;
     NULL, nombotra;
|FIN;

|PROCESO imprimir;
    swantic = 0;
    swplus  = 0;
    swbonif = 0;
    fecantic = "";
    fecplus  = "";
    fecbonif = "";

|SI #1#35 ! "..........";|Y #1#35 ! "          ";   || fecha de antiguedad
        alfa1 = #1#35;
    |HAZ desmenuza;
    |SI desde [ mirar;|Y mirar [ hasta;
            fecantic = #1#35;
            swantic  = 1;
    |FINSI;
|FINSI;

alfa1 = "";
|SI #1#37 ! "..........";|Y #1#37 ! "          ";   || fecha de baja
        alfa1 = #1#37;
|FINSI;
|SI alfa1 = "";
     |PARA nMes = #0#4; |SI nMes [ #0#7; |HACIENDO nMes = nMes + 1;
          #nomvarca#0 = #1#0;
          #nomvarca#1 = #1#1;
          #nomvarca#2 = nMes;
          #nomvarca#3 = 0;
          |LEE 000#nomvarca.=;
          |SI FSalida = 0;
               alfa1 = #nomvarca#16;
          |FINSI;
     |SIGUE;
|FINSI;
|QBF alfa1;

|SI alfa1 ! "";
    |HAZ desmenuza;
    |SI desde [ mirar;|Y mirar [ hasta;
            fecplus = alfa1;
            swplus  = 1;
    |FINSI;
|FINSI;
|SI #1#41 ! "..........";|Y #1#41 ! "          ";   || fecha vto. bonif.
        alfa1 = #1#41;
    |HAZ desmenuza;
    |SI desde [ mirar;|Y mirar [ hasta;
            fecbonif = #1#41;
            swbonif  = 1;
    |FINSI;
|FINSI;

     nBonif = 0;
     |BUCLE nombotra;

     |SI nBonif ! 0;
          alfa1 = #nombotra#5;
          |HAZ desmenuza;
          |SI desde [ mirar; |Y mirar [ hasta;
               fecbonif = #nombotra#5;
               swbonif = 1;
          |FINSI;
     |FINSI;

|SI swantic = 1; |O swplus = 1; |O swbonif = 1;
    |PRINT;
        swprint = 1;
|FINSI;
|FPRC;

|PROCESO desmenuza;
    mes  = alfa1 % 402;
    anyo = alfa1 % 704;
    mesnum  = mes;
    anyonum = anyo;
    mirar = (anyonum * 100) + mesnum;
|FPRC;

|DEFBUCLE nomxvenc0;
#1#1;
;
#0#0,#0#2;
#0#1,#0#3;
e;
inicia, imprimir;
|FIN;

|PROCESO impre;|TIPO 3;
     swprint = 0;
     |SI swDesdeWeb = 0;
          |IMPRE 0;
     |SINO;
          |HAZ prepara_impresion;
     |FINSI;
     |SI FSalida = 0;
          |INFOR informe;
          |SI FSalida = 0;
               |ABRE #nomvarca;
               |BUCLE nomxvenc0;
               |CIERRA #nomvarca;
               |SI swprint = 1;
                    |PIEINF;
               |FINSI;
               |FININF;
          |SINO;
               |MENAV error2;
          |FINSI;
     |SINO;
     |MENAV error1;
|FINSI;
|FINIMP;
|FPRC;

|PROCESO defecto; |TIPO 7; ||calcula el periodo por defecto segun fecha sistema
    fecsys = ~;
    alfa1 = fecsys;
    alfa1 = alfa1 % -104;
    #0Campo = alfa1;
|PINTA #0Campo;
|FPRC;

|PROCESO pintames; |TIPO 0;
|SI #0Campo =  1;  p_mes = "ENERO     ";  |FINSI;
|SI #0Campo =  2;  p_mes = "FEBRERO   ";  |FINSI;
|SI #0Campo =  3;  p_mes = "MARZO     ";  |FINSI;
|SI #0Campo =  4;  p_mes = "ABRIL     ";  |FINSI;
|SI #0Campo =  5;  p_mes = "MAYO      ";  |FINSI;
|SI #0Campo =  6;  p_mes = "JUNIO     ";  |FINSI;
|SI #0Campo =  7;  p_mes = "JULIO     ";  |FINSI;
|SI #0Campo =  8;  p_mes = "AGOSTO    ";  |FINSI;
|SI #0Campo =  9;  p_mes = "SEPTIEMBRE";  |FINSI;
|SI #0Campo = 10;  p_mes = "OCTUBRE   ";  |FINSI;
|SI #0Campo = 11;  p_mes = "NOVIEMBRE ";  |FINSI;
|SI #0Campo = 12;  p_mes = "DICIEMBRE ";  |FINSI;
|PINTA 1422, p_mes;
|FPRC;

|PROCESO mirames; |TIPO 7; ||calcula el mes por defecto segun fecha sistema
    fecsys = ~;
    alfa1 = fecsys;
    alfa1 = alfa1 % 402;
    #0Campo = alfa1;
|PINTA #0Campo;
|FPRC;

|PROCESO masuno; |TIPO 0;   || mes desde mayor que mes hasta y a¤o igual
|SI #0#4 > #0#7;|Y #0#5 = #0#8;
    |MENAV "    ATENCION!!! Error de entrada ...";
    |ERROR;
|FINSI;
|FPRC;

|PROCESO masdos; |TIPO 0;   || a¤o desde mayor que a¤o hasta
|SI #0#5 > #0#8;
    |MENAV "    ATENCION!!! Error de entrada ...";
    |ERROR;
|FINSI;
|FPRC;

-------------------------------------------------------------------------------
                    ||   PROCESOS COMUNES

|PROCESO prepara_impresion;
     aDir = "/tmp/";
     Impresora = aDir + Usuario;
     |PINTA 2001, Usuario;
     |PINTA 2101, Impresora;
     |IMPRE -77;
     |PINTA 2201, FSalida;
     |SI FSalida ! 0;
          nErrorInf = 1;
     |FINSI;
     aAlfa1 = "chmod 777 " + Impresora;;
     |SYSTEM aAlfa1;
|FPRC;

|PROCESO traete_fichero;
     |FINIMP;
     cOrigen = aDir + Usuario;
     cDestino = aDirDes;
     |QBF cDestino;
     aArchivo = aArchivo - ".txt";
     cDestino = cDestino + aArchivo;
     |COPIA_FICHERO cOrigen, cDestino;
     |SI FSalida ! 0;
          nErrorInf = 0;
     |FINSI;
     aAlfa1 = "chmod 777 " + cDestino;
     |SYSTEM aAlfa1;
|FPRC;

|PROCESO ConviertePdf;
     |DBASS aBaseBin;  |QBF aBaseBin;
     aBaseBin = aBaseBin + "bin/topdf.sh ";

     aSystem = aBaseBin + cDestino;
     |SYSTEM aSystem;

     aSystem = "chmod 777 " + cDestino + ".pdf";
     |SYSTEM aSystem;

     |FBORRA cDestino;

     ||SI #0#6 = "S";  |HAZ AlaWeb;  |FINSI;
     ||SI #0#5 = "S";  |HAZ Correo;  |FINSI;

     ||FBORRA aPdf;
|FPRC;
-------------------------------------------------------------------------------
|PROCESO LeeParam;
     |DBASS aRutaDes;
     |DBASS aRutaDatos;
     |QBF aRutaDes;
     |QBF aRutaDatos;
     aRutaDes = aRutaDes + "dsnetcom/def/dsnetm00.mas";
     |CARGA_DEF aRutaDes, dsnetm00@;
     |SI FSalida ! 0;
          |MENAV "    Error en CARGADEF: dsnetm00";
     |SINO;
          aRutaDatos = aRutaDatos + "dsnetcom/fich/";
          |PATH_DAT #100 aRutaDatos;
          |ABRE #100;
          |LEE 000#100p;
          |SI FSalida = 0;
               aDirDes = #100#7;
               aDirOri = aDirDes;
          |SINO;
               |MENAV "    Error de datos en fichero de configuracion dsnetm00";
          |FINSI;
          |CIERRA #100;
     |FINSI;
     |DESCARGA_DEF #dsnetm00@;
|FPRC;

|PROCESO LeeOrigen;

     aArchivo = PARAMETRO;
     |QBF aDirOri;
     aRuta = aDirOri + aArchivo;

     |XABRE "U", aRuta, nCanal;
     |SI FSalida ] 0;
          |XLEE nCanal, 100, aCabecera;
          |XLEE nCanal,  10, aEmpresa;
          |XLEE nCanal,  10, aMesDesde;
          |XLEE nCanal,  10, aMesHasta;
          |XLEE nCanal,  10, aAnyo;
     |FINSI;
     |XCIERRA nCanal;
     |QBF aCabecera;
     |QBF aEmpresa;
     |QBF aMesDesde;
     |QBF aMesHasta;
     |QBF aAnyo;
     nEmpresa = aEmpresa;
     nMesDesde = aMesDesde;
     nMesHasta = aMesHasta;
     nAnyo = aAnyo;
|FPRC;

|PROCESO PasaParametros;
     #0#0 = nEmpresa;
     #0#1 = nEmpresa;
     #0#2 = 1;           || desde trabajador
     #0#3 = 99999;       || hasta trabajador
     #0#4 = nMesDesde;
     #0#5 = nAnyo;
     #0#7 = nMesHasta;
     #0#8 = nAnyo;
     #0#6 = ~;
|FPRC;

|PROGRAMA;
     |CLS;
     |CABEZA "INFORME DE VENCIMIENTOS";
     |PINPA #0#0;
     |PINDA #0#0;
     |SI PARAMETRO ! "";
          |HAZ LeeParam;
          |HAZ LeeOrigen;
          |HAZ PasaParametros;
          swDesdeWeb = 1;
          |HAZ impre;
          |HAZ traete_fichero;
          |HAZ ConviertePdf;
     |SINO;
          |ENDATOS #1#0;
     |FINSI;
|FPRO;
