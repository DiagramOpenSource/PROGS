|FICHEROS;
     labere01 #1;
     labere02 #2;
     labere03 #3,MANTE;

     labempre;
|FIN;

|VARIABLES;
     nVentana         = -1;
     nVent03          = -1;
     nBtnAlta         = -1;
     nBtnBorr         = -1;
     nBtnAfec         = -1;
     nBtnNoAf         = -1;
     nBtnEmis         = -1;
     nBtnImpo         = -1;
     nRango           = -1;
     nLabel           = -1;
     nGraba           = -1;

     aEsc1            = "";
     aEsc2            = "";
     aRetu            = "";
     aTecla           = "";
     aAlfa            = "";

     {-1}aVent        = "";
         aVent1       = 0;
         aVent2       = 0;
         aVent3       = 0;
         aVent4       = 0;
         aVent5       = "";

     &enGrid01        = -1;
     &enGrid02        = -1;
     &eaTipo          = "";
|FIN;

|PROCESO Tipo12Fere03;  |TIPO 12;
     |C_M #3#0, 0, aAlfa;

     |SI aAlfa = "S";
         #1#0 = #3#0;
         #1#1 = #3#1;
         |LEE 000#1=;
         |SI FSalida = 0;
             |MENAV "0000Ya existe el registro.";
             |ERROR;
             |ACABA;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO Tipo0C0Fere03;  |TIPO 0;
     |C_M #3#0, 0, aAlfa;
     |SI aAlfa = "N"; |ACABA;  |FINSI;

     |ABRE #labempre;
     #labempre#0 = #labere03#0;
     |LEE 000#labempre.=;
     |SI FSalida ! 0;  |DDEFECTO #labempre;  |FINSI;
     |CIERRA #labempre;
|FPRC;

|PROCESO Baja02;
     #2#0 = #1#0;
     #2#1 = #1#1;
     #2#5 = eaTipo;
     #2#6 = 1;
|FPRC;

|PROCESO Alta02;
     #2#0 = #1#0;
     #2#1 = #1#1;
     #2#5 = eaTipo;
     #2#6 = 9999999999;
|FPRC;

|PROCESO Evento02;
     |SI FSalida = 1;  |O FSalida = 2;
         |LEE 000#2=;

         |ESTADO_CONTROL nBtnEmis, "ENABLE";
     |FINSI;
|FPRC;

|PROCESO Evento01;
     |SI FSalida = 1;  |O FSalida = 2;
         |LEE 000#01=;

         |ESTADO_CONTROL nBtnBorr, "ENABLE";

         |SI nLabel ! -1;
             |FIN_CONTROL_WINDOWS nLabel;
         |FINSI;

         aAlfa = "LABEL,{{13}}Solicitud: " + #1#2 + " " + #1#1;   |QBF aAlfa;
         nLabel = FSalida;

         |CONTROL_SIMPLE 0, aAlfa, 1902, 1980, NULL;

         |ESTADO_CONTROL nBtnImpo, "ENABLE";
         |ESTADO_CONTROL nBtnEmis, "DISABLE";

         |REFRESCACONTROL enGrid02;

         |PULSATECLA;
     |FINSI;
|FPRC;

|PROCESO BtnAlta;
     |VENTANA 0501, 1390, -1, 16, "Nueva solicitud";
     nVent03 = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVent03;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |C_M #3#0, 1, "S";
     |C_M #3#1, 1, "S";

     |DDEFECTO #3;
     |PINPA #0#3;
     |PINDA #0#3;

     nGraba = 0;
     |ENDATOS #1#3;
     |SI FSalida = 0;
         #1#0 = #3#0;
         #1#1 = #3#1;
         #1#2 = #3#2;
         #1#3 = #3#3;
         #1#4 = #3#4;
         |GRABA 020#1n;
         |LIBERA #1;

         nGraba = 1;
     |FINSI;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVent03;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA nVent03;

     |SI nGraba = 1;
         |REFRESCACONTROL enGrid01;
     |FINSI;

     |FOCOTECLADO enGrid01;
     |PULSATECLA;
|FPRC;

|PROCESO labere02B;
     |BORRA 020#2a;
     |LIBERA #2;
|FPRC;

|DEFBUCLE labere02B;
     #2#1;
     ;
     #1#0, #1#1, 00001;
     #1#0, #1#1, 99999;
     be;
     NULL, labere02B;
|FIN;

|PROCESO BtnBorr;
     |LEE 101#1=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |CONFI "0000NSeguro que quiere borrar el expediente seleccionado";
     |SI FSalida ! 0;
         |LIBERA #1;
         |ACABA;
     |FINSI;

     |BUCLE labere02B;

     |BORRA 020#1a;
     |LIBERA #1;

     |REFRESCACONTROL enGrid01;
     |REFRESCACONTROL enGrid02;
|FPRC;

|PROCESO BtnAfect;
     eaTipo = "1";
     |ESTADO_CONTROL nBtnAfec, "DISABLE";
     |ESTADO_CONTROL nBtnNoAf, "ENABLE";
     |ESTADO_CONTROL nBtnEmis, "DISABLE";

     |REFRESCACONTROL enGrid02;
|FPRC;

|PROCESO BtnNoAfec;
     eaTipo = "2";
     |ESTADO_CONTROL nBtnAfec, "ENABLE";
     |ESTADO_CONTROL nBtnNoAf, "DISABLE";
     |ESTADO_CONTROL nBtnEmis, "DISABLE";

     |REFRESCACONTROL enGrid02;
|FPRC;

|PROGRAMA;
     |ABRET #1;
     |ABRET #2;

     |VENTANA 0501, 3599, -1, 0, "";
     nVentana = FSalida;

     aAlfa = "LABEL,{{15}}SOLICITUD DE EXPEDIENTE DE REGULACION DE EMPLEO VINCULADO";
     |CONTROL_SIMPLE 0, aAlfa, 0502, 0598, NULL;

     |CONTROL_SIMPLE 0, "BOTON,{{193}}Alta ", 1876, 1886, BtnAlta;
     nBtnAlta = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,{{199}}Borrar", 1888, 1898, BtnBorr;
     nBtnBorr = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,{{197}}Afectados", 2002, 2015, BtnAfect;
     nBtnAfec = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,{{197}}No afectados", 2018, 2031, BtnNoAfec;
     nBtnNoAf = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,{{197}}Gestionar trabajadores", 2078, 2098, BtnImportar;
     nBtnImpo = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,{{205}}Emitir documento", 3302, 3320, BtnEmitir;
     nBtnEmis = FSalida;

     nRango = 4 + 8 + 16 + 32 + 128;
     eaTipo = "1";

     |LINEAL_SIMPLE #2#2, nRango, 2102, 3298, Baja02, Alta02, Evento02;
     enGrid02 = FSalida;

     |LINEAL_SIMPLE #1#1, nRango, 0602, 1798, NULL, NULL, Evento01;
     enGrid01 = FSalida;

     |ESTADO_CONTROL nBtnBorr, "DISABLE";
     |ESTADO_CONTROL nBtnAfec, "DISABLE";
     |ESTADO_CONTROL nBtnEmis, "DISABLE";
     |ESTADO_CONTROL nBtnImpo, "DISABLE";

     aEsc1  = &255 + "806";
     aEsc2  = &255 + "807";
     aRetu  = &255 + "802";
     |PARA; |SI;  |HACIENDO;
         FSalida = "::{{001}}Salir," + enGrid01;
         |LEETECLA aTecla;
         |SI aTecla = aEsc1; |SAL;  |FINSI;
         |SI aTecla = aEsc2; |SAL;  |FINSI;
         |SI aTecla = aRetu; |SAL;  |FINSI;
     |SIGUE;

     |FIN_CONTROL_WINDOWS nBtnAlta;
     |FIN_CONTROL_WINDOWS nBtnBorr;

     |FIN_CONTROL_WINDOWS nBtnAfec;
     |FIN_CONTROL_WINDOWS nBtnNoAf;
     |FIN_CONTROL_WINDOWS nBtnEmis;
     |FIN_CONTROL_WINDOWS nBtnImpo;

     |DESTRUYECONTROL enGrid01;
     |DESTRUYECONTROL enGrid02;

     |FINVENTANA nVentana;

     |CIERRAT #1;
     |CIERRAT #2;
|FPRO;
