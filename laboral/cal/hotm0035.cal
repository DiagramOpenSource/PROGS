|FICHEROS;
     hotm0035;
     nomvarca;
     nomvarli;
     hotm0033;
     hotm0034;
|FIN;

|VARIABLES;
     aAlfa1 = "";
     aAlfa2 = "";
     aAlfa3 = "";
     aAlfa4 = "";
     nNume1 = 0;
     nNume2 = 0;
     nNume3 = 0;
     nNume4 = 0;
     nPara1 = 0;
     nPara2 = 0;
     nPara3 = 0;
     nPara4 = 0;
     fFech1 = @;
     fFech2 = @;
     fFech3 = @;
     fFech4 = @;
     nFEstado = 0;
     nSeleccio = 0;
     nModo = 0;

     aP_mes = "";
     nBisiesto = 0;
     nTopeMes = 0;
     fDesde = @;
     fHasta = @;
     nImporte = 0;
     nCto = 0;
     nAntes = 0;
     &EURODB = 0;
|FIN;
____________________________________/
|PROCESO Defecto; |TIPO 7;    || calcula el a¤o por defecto segun sistema
     fFech1 = ~;
     aAlfa1 = fFech1;
     aAlfa1 = aAlfa1 % -104;
     #hotm0035#Campo# = aAlfa1;
     |PINTA #hotm0035#Campo#;

     nBisiesto = #hotm0035#4 $ 4;
     |SI nBisiesto ! 0;
          nBisiesto = 0;
     |SINO;
          nBisiesto = 1;
     |FINSI;
|FPRC;

|PROCESO Mirames; |TIPO 7;    || calcula el mes por defecto segun sistema
     fFech1 = ~;
     aAlfa1 = fFech1;
     aAlfa1 = aAlfa1 % 402;
     #hotm0035#Campo# = aAlfa1;
     |PINTA #hotm0035#Campo#;
|FPRC;

|PROCESO Pintames; |TIPO 0;   || pinta el nombre del mes
     |SI #hotm0035#Campo# =  1; aP_mes = "ENERO     ";  nTopeMes = 31;  |FINSI;
     |SI #hotm0035#Campo# =  2; aP_mes = "FEBRERO   ";  nTopeMes = 28 + nBisiesto;  |FINSI;
     |SI #hotm0035#Campo# =  3; aP_mes = "MARZO     ";  nTopeMes = 31;  |FINSI;
     |SI #hotm0035#Campo# =  4; aP_mes = "ABRIL     ";  nTopeMes = 30;  |FINSI;
     |SI #hotm0035#Campo# =  5; aP_mes = "MAYO      ";  nTopeMes = 31;  |FINSI;
     |SI #hotm0035#Campo# =  6; aP_mes = "JUNIO     ";  nTopeMes = 30;  |FINSI;
     |SI #hotm0035#Campo# =  7; aP_mes = "JULIO     ";  nTopeMes = 31;  |FINSI;
     |SI #hotm0035#Campo# =  8; aP_mes = "AGOSTO    ";  nTopeMes = 31;  |FINSI;
     |SI #hotm0035#Campo# =  9; aP_mes = "SEPTIEMBRE";  nTopeMes = 30;  |FINSI;
     |SI #hotm0035#Campo# = 10; aP_mes = "OCTUBRE   ";  nTopeMes = 31;  |FINSI;
     |SI #hotm0035#Campo# = 11; aP_mes = "NOVIEMBRE ";  nTopeMes = 30;  |FINSI;
     |SI #hotm0035#Campo# = 12; aP_mes = "DICIEMBRE ";  nTopeMes = 31;  |FINSI;
     |PINTA 1435, aP_mes;
|FPRC;
____________________________________/
|PROCESO nomvarli;
     |DDEFECTO #nomvarli;
     #nomvarli#0 = #hotm0034#0;         || empresa
     #nomvarli#1 = #hotm0034#1;         || trabajador
     #nomvarli#2 = #hotm0035#5;         || mes
     #nomvarli#3 = 0;                   || tipo
     #nomvarli#4 = nCto;                || concepto
     |LEE 101#nomvarli.=;
     |SI FSalida ! 0;    |GRABA 020#nomvarli.b;   |FINSI;

     nNume1 = #nomvarli#5;
     nNume2 = #nomvarli#6;
     nAntes = (nNume1 * nNume2) ! EURODB;

     nImporte = nImporte + nAntes;

     #nomvarli#5 = "         1";
     |SI nImporte = 0;
          #nomvarli#6 = "";
     |SINO;
          aAlfa1 = nImporte;
          aAlfa1 = ("            " + aAlfa1) % -112;
          #nomvarli#6 = aAlfa1;
     |FINSI;

     |GRABA 020#nomvarli.a;
     |LIBERA #nomvarli;
|FPRC;

|PROCESO nomvarca;
     |DDEFECTO #nomvarca;
     #nomvarca#0 = #hotm0034#0;         || empresa
     #nomvarca#1 = #hotm0034#1;         || trabajador
     #nomvarca#2 = #hotm0035#5;         || mes
     #nomvarca#3 = 0;                   || tipo
     |LEE 000#nomvarca.=;
     |SI FSalida ! 0;
          |GRABA 020#nomvarca.n;
     |FINSI;
|FPRC;

|PROCESO hotm0034;
     #hotm0033#0 = #hotm0034#0;
     #hotm0033#1 = #hotm0034#1;
     |LEE 000#hotm0033.=;
     |SI FSalida = 0;              || si existe la cabecera
          |HAZ nomvarca;
          nCto = 906;    nImporte = #hotm0034#3;  |HAZ nomvarli;
          nCto = 907;    nImporte = #hotm0034#4;  |HAZ nomvarli;
          nCto = 908;    nImporte = #hotm0034#5;  |HAZ nomvarli;
          nSeleccio = 1;
     |FINSI;
|FPRC;

|DEFBUCLE hotm0034;
     #hotm0034#1;
     ;
     #hotm0035#0, #hotm0035#2, fDesde;
     #hotm0035#1, #hotm0035#3, fHasta;
     ;
     NULL, hotm0034;
|FIN;

|PROCESO nomvarli_1;
     |BORRA 020#nomvarli.a;
     |LIBERA #nomvarli;
|FPRC;

|DEFBUCLE nomvarli;
     #nomvarli#1;
     ;
     #hotm0035#0, #hotm0035#2, #hotm0035#5, 0, 906;
     #hotm0035#1, #hotm0035#3, #hotm0035#5, 0, 908;
     be;
     NULL, nomvarli_1;
|FIN;

|PROCESO Tipo3; |TIPO 3;
     aAlfa1 = ("00" + #hotm0035#5) % -102;

     aAlfa2 = "01" + "." + aAlfa1 + "."  + #hotm0035#4;
     fDesde = aAlfa2;

     aAlfa2 = nTopeMes + "." + aAlfa1 + "."  + #hotm0035#4;
     fHasta = aAlfa2;

     |BUCLE nomvarli;         || borra
     |ABRE #nomvarca;
     |ABRE #nomvarli;
     |ABRE #hotm0033;
     |BUCLE hotm0034;
     |CIERRA #nomvarca;
     |CIERRA #nomvarli;
     |CIERRA #hotm0033;

     |SI nSeleccio = 0;
          |MENAV "0000Seleccion vacia.";
     |FINSI;
|FPRC;
