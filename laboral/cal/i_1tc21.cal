|FICHEROS;
    labempre #1;    || empresas
    labtraba #5;    || trabajadores
    nomtrt22 #4;    || temporal 1
    nomtrt21 #7;    || temporal 2
    nombonif #8;    || bonificaciones
    nomepigr #9;    || epigrafes
    labcentr #10;   || centros de trabajo
    nomconst #11;   || constantes
    nomepitr #12;   || epigrafes/trabajador
    nomcenes #13;   || centros especiales 18/94-10/94
    nomcones #15;   || constantes especiales 18/94-10/94
    nomcontr #36;   || control aplicacion
|FIN;

|VARIABLES;
    FEstado = 0;
    sgdonum = 0;
    haentrat = 0;
    antempre = 0;
    antcentr = 0;
    antbonif = 0;
    antporce = 0;
    anttipo  = 0;
    sibonif = 0;
    endo = 0;
    menos = 0;
    estado = 0;
    informe1 = "nomtc21p";
    informe2 = "nomtc21s";
    postal1 = "";   || Codigo postal1 alfa
    postal2 = "";   || Codigo postal2 alfa
    domi    = "";   || Variable para quitar espacios al domicilio
    afili   = "";   || Variable para quitar espacios al nro. de afiliacion
    sgdoalf = "";   || Variable para quitar espacios al nro. de afiliacion
    fecsys = @;
    nume1 = 0;
    nume2 = 0;
    nume3 = 0;
    nume4 = 0;
    alfa1 = "";
    alfa2 = "";
    alfa3 = "";
    alfa4 = "";
    fech1 = @;
    fech2 = @;
    fech3 = @;
    fech4 = @;
    p_mes = "";
    bot1 = "  ";         || topes para el DEFBUCLE 1
    top1 = "zz";         || del fichero temporal
    bot2 = "         ";
    top2 = "zzzzzzzzz";
    lite_mes = "";
    epi_tra = 0;
    epi_bas = 0;
    epig_ilt = 0;
    epig_ims = 0;
    anyonum = 0;
                        || Variables informes
    &t_bonif1 = 0;
    &t_bonif2 = 0;
    &t_porboni1 = 0;
    &t_porboni2 = 0;
    &t_cuotboni = 0;
    &t_reduccio = 0;
    &tablaepi1  = 0;
    &tablaepi2  = 0;
    &tablaepi3  = 0;
    &tablaepi4  = 0;
    &tablaepi5  = 0;
    &porcepig1  = 0;
    &porcepig2  = 0;
    &porcepig3  = 0;
    &porcepig4  = 0;
    &porcepig5  = 0;
    &basepigr1  = 0;
    &basepigr2  = 0;
    &basepigr3  = 0;
    &basepigr4  = 0;
    &basepigr5  = 0;
    &t_porcepi1 = 0;
    &t_porcepi2 = 0;
    &t_porcepi3 = 0;
    &t_porcepi4 = 0;
    &t_porcepi5 = 0;
    &t_porcepig = 0;
    &t_epigrafe = 0;
    &t_dfp      = 0;
    &t_porcdfp  = 0;
    &t_total    = 0;
    &t_cbonif   = 0;
    siono_cen = "";
    runnom = "";

    tip = 0;
    sw_mater = 0;
    f_mes = "";
    m_dias = 0;
    m_des = 0;
    m_has = 0;
    m_fec = "";
    m_top = "";
    dtipo = 0;
    htipo = 0;
    dia_2 = 0;
    dia_3 = 0;
    dia_4 = 0;
    topemes = 0;
    sw_abierto = 0;
    Fbonif = 0;
    desem = 0;
    cuot2 = 0;
    porc1 = 0;
    porc2 = 0;
    bacc2 = 0;
    bacp2 = 0;
    bilt1 = 0;
    bilt2 = 0;
    &EURODB = 0;

    &nCConst = 0;
    &aCError = "";
    &fCFecha = @;
    aAlfa = "";
|FIN;

|| ***************************************************************************
|| ****** calculos de carga de fichero temporal ******************************
|| ***************************************************************************

|PROCESO arreglo;
          ||  si emitimos desde el historico y el campo porcentaje esta vacio
          || /lo rellenamos para evitar que tengan que recalcular
     |SI runnom = "nomwtc21";|Y #7#14 = 0;|Y #6#63 ! 0;
          #7#14 = #8#3;
     |FINSI;
|FPRC;

|PROCESO tmp_2;
|SI #8#11 = "S";
    |DDEFECTO #4;
    |ABRE #4;
        #4#0 = #1#0;                    || Codigo empresa
        #4#1 = #6#12;                   || Codigo Bonificacion
        #4#2 = #6#1;                    || Codigo trabajador
    |SI tip = 0;
        |SI #1#45 = "N";   afili = #01#13;  |FINSI;
        |SI #1#45 = "S";   afili = #10#10;  |FINSI;
    |SINO;
            afili = #13#10;
    |FINSI;
    |QBF afili;
        sgdoalf = afili % 0;
        sgdonum = sgdoalf;
        sgdonum = sgdonum - 4;
        sgdonum = sgdonum + 300;        || para coger 2do. digito
        #4#3 = afili % 102;             || Primer digito S.S.
        #4#4 = afili % sgdonum;         || Segundo digito S.S.
        #4#5 = afili % -102;            || Tercer digito S.S.
        #4#6 = lite_mes;                || Mes literal
        #4#7 = #0#3;                    || A¤o
        #4#8 = #1#2;                    || Nombre empresa
    |SI tip = 0;
        |SI #1#45 = "S";
                domi = #10#3;
            |QBF domi;
                #4#9 = domi+" "+#10#4+" "+#10#5;  || domicilio
                #4#10 = #10#6;                    || localidad
                #4#11 = #10#9;                    || provincia
        |SINO;
                domi = #1#3;
            |QBF domi;
                #4#9 = domi+" "+#1#4+" "+#1#5;
                #4#10 = #1#6;
                #4#11 = #1#9;
        |FINSI;
    |SINO;
            domi = #13#3;
        |QBF domi;
            #4#9 = domi+" "+#13#4+" "+#13#5;
            #4#10 = #13#6;
            #4#11 = #13#9;
    |FINSI;
        afili = #5#75;
    |QBF afili;
        sgdoalf = afili % 0;
        sgdonum = sgdoalf;
        sgdonum = sgdonum - 4;
        sgdonum = sgdonum + 300;        || para coger 2do. digito
        #4#12 = "CT FORMAC. TEORICA";   || Concepto o modalidad
        #4#13 = "O.M. 14-07-98";        || Disposicion
        #4#14 = 0;                      || Porcentaje
        #4#15 = afili % 102;            || Primer digito S.S.
        #4#16 = afili % sgdonum;        || Segundo digito S.S.
        #4#17 = afili % -102;           || Tercer digito S.S.
        #4#18 = #5#2;                   || Nombre Trabajador
        #4#19 = #5#40;                  || Fecha alta bonificacion
        #4#20 = #6#64;                  || Cuota bonificacion 2
        #4#21 = #5#7;                   || DNI trabajador
    |SI tip = 0;
        |SI #1#45 = "N";  #4#22 = #01#13;  |FINSI;
        |SI #1#45 = "S";  #4#22 = #10#10;  |FINSI;
    |SINO;
            #4#22 = #13#10;
    |FINSI;
        #4#23 = "";                     || vacio
        #4#24 = "";                     || literal fijo
        #4#25 = #5#77;                  || centro trabajo
        #4#26 = "";
        #4#27 = tip;                    || tipo
        #4#28 = ~;                      || fecha impresion
    |SI #8#13 ! "   ";
            #4#31 = #8#13;              || clave TGSS (especifica)
    |SINO;
            alfa1 = #8#0;    alfa1 = ("000" + alfa1) % -103;
            #4#31 = alfa1;              || clave TGSS (codigo bonif.)
    |FINSI;
    |SI #6#64 ! 0;
        |GRABA 020#4n;
    |FINSI;
    |CIERRA #4;
|FINSI;
|FPRC;

|PROCESO tmp_12;
|DDEFECTO #7;
|ABRE #7;
    #7#29 = #1#45;       || por centros S/N
    #7#0 = #1#0;         || empresa
    #7#1 = #6#12;        || bonificacion
    #7#2 = #6#1;         || trabajador
    #7#27 = tip;         || tipo
|LEE 101#7=;
|SI FSalida = 0;
        #7#20 = #7#20 + #6#43;     || Base bonificacion 1 (acumula finiquito)
        #7#21 = #7#21 + #6#44;     || Base bonificacion 2 (acumula finiquito)
    |GRABA 020#7a;
|FINSI;
|LIBERA #7;
|CIERRA #7;
|FPRC;

|PROCESO tmp_1;
|DDEFECTO #7;
|ABRE #7;
    #7#0 = #1#0;        || empresa
    #7#1 = #6#12;       || bonificacion
    #7#2 = #6#1;        || trabajador
|SI tip = 0;
    |SI #1#45 = "N";   afili = #01#13;  |FINSI;
    |SI #1#45 = "S";   afili = #10#10;  |FINSI;
|SINO;
    |HAZ cen_esp;
        afili = #13#10;
|FINSI;
|QBF afili;
    sgdoalf = afili % 0;
    sgdonum = sgdoalf;
    sgdonum = sgdonum - 4;
    sgdonum = sgdonum + 300;        || para coger 2do. digito
    #7#3 = afili % 102;             || Primer digito S.S.
    #7#4 = afili % sgdonum;         || Segundo digito S.S.
    #7#5 = afili % -102;            || Tercer digito S.S.
    #7#6 = lite_mes;                || Mes literal
    #7#7 = anyonum;                 || A¤o
    #7#8 = #1#2;                    || Nombre empresa
|SI tip = 0;
    |SI #1#45 = "S";
            domi = #10#3;
        |QBF domi;
            #7#9 = domi+" "+#10#4+" "+#10#5; || domicilio
            #7#10 = #10#6;                   || localidad
            #7#11 = #10#9;                   || provincia
    |SINO;
            domi = #1#3;
        |QBF domi;
            #7#9 = domi+" "+#1#4+" "+#1#5;
            #7#10 = #1#6;
            #7#11 = #1#9;
    |FINSI;
|SINO;
        domi = #13#3;
    |QBF domi;               || domicilio empresa
        #7#9 = domi+" "+#13#4+" "+#13#5;
        #7#10 = #13#6;
        #7#11 = #13#9;
|FINSI;
    afili = #5#75;
|QBF afili;
    sgdoalf = afili % 0;
    sgdonum = sgdoalf;
    sgdonum = sgdonum - 4;
    sgdonum = sgdonum + 300;      || para coger 2do. digito
    #7#12 = #8#1;                 || Concepto o modalidad
    #7#13 = #8#2;                 || Disposicion
    #7#14 = #6porc1;              || Porcentaje (1)
|HAZ arreglo;
    #7#15 = afili % 102;          || Primer digito S.S.
    #7#16 = afili % sgdonum;      || Segundo digito S.S.
    #7#17 = afili % -102;         || Tercer digito S.S.
    #7#18 = #5#2;                 || Nombre Trabajador
    #7#19 = #5#40;                || Fecha alta bonificacion
    #7#20 = #6#43;                || Base CC bonificacion (1)
    #7#21 = #6#44;                || Base CP bonificacion (1)
    #7#22 = #5#7;                 || DNI trabajador
|SI tip = 0;
    |SI #1#45 = "N";  #7#23 = #01#13;  |FINSI;
    |SI #1#45 = "S";  #7#23 = #10#10;  |FINSI;
|SINO;
        #7#23 = #13#10;
|FINSI;
    #7#24 = "";                   || Literal fijo
    #7#25 = #5#77;                || centro trabajo
    #7#26 = #6bilt1;              || base ILT bonificacion (1)
    #7#27 = tip;                  || tipo
    #7#28 = ~;                    || fecha impresion
    #7#29 = #1#45;                || por centros S/N
    #7#30 = #6#9;                 || codigo contrato (para bonif_mater)
|SI #8#13 ! "   ";
        #7#31 = #8#13;            || clave TGSS (especifica)
|SINO;
        alfa1 = #8#0;    alfa1 = ("000" + alfa1) % -103;
        #7#31 = alfa1;            || clave TGSS (codigo bonif.)
|FINSI;
|SI #6#63 ! 0;|O #6#64 ! 0;   || bonificacion (primera parte y subv.horas)
    |GRABA 020#7n;
|FINSI;

|SI #6cuot2 ! 0;    || bonificacion (segunda parte)
    #7#1 = #7#1 + 400;
    #7#14 = #6porc2;          || Porcentaje (2)
    #7#20 = #6bacc2;          || Base CC bonificacion (2)
    #7#21 = #6bacp2;          || Base CP bonificacion (2)
    #7#26 = #6bilt2;          || base ILT bonificacion (2)
    |GRABA 020#7n;
|FINSI;

|SI #6desem ! 0;|Y #8#0 < 100;|Y tip ! 8;
        #7#20 = #6desem * #5#242;       || Base bonificacion 1
        #7#21 = #7#20;                  || Base bonificacion 2
        #7#24 = "Exp.Reg.Empleo: " + #1#29 + "/" + #1#35;  || nro.exp.
        #7#27 = #7#27 + 10;             || tipo
    |GRABA 020#7n;
|FINSI;
|CIERRA #7;
|FPRC;

|PROCESO cen_esp;
|ABRE #13;
|DDEFECTO #13;
    #13#0 = #5#0;        || empresa
    #13#13 = #5#77;      || centro
|SI tip = 8;   #13#1 = 87;    |FINSI;
|SI tip = 9;   #13#1 = 64;    |FINSI;
|LEE 000#13=;
|CIERRA #13;
|FPRC;

|PROCESO ini_auxil;
     |SI #2#50 = "G";|Y PARAMETRO = "AGRARIA";    |ACABA;   |FINSI;
     |SI #2#50 = "A";|Y PARAMETRO ! "AGRARIA";    |ACABA;   |FINSI;

||    #2#11 = 0;      || compensaciones/reducciones 209
||    #2#12 = 0;      || bonificaciones/subvenciones 601
||    #2#21 = 0;      || compensaciones maternidad 201
|SI PARAMETRO ! "AGRARIA";
        #2#17 = 0;  || clave de TC2/1
|FINSI;
    #2#20 = 0;      || importe a pagar TC1
|GRABA 020#2a;      || Graba el auxiliar de empresas
|LIBERA #2;
|FPRC;

|PROCESO lee_centr;
|DDEFECTO #10;
|ABRE #10;
    #10#0 = #1#0;
    #10#1 = #5#77;          || lee el centro de trabajo
|LEE 000#10=;
|CIERRA #10;
|FPRC;

|PROCESO lee_traba;
|ABRE #5;
    #5#0 = #1#0;
    #5#1 = #6#1;
|LEE 000#5=;                || lee el trabajador
    FEstado = FSalida;
|CIERRA #5;
|FPRC;

|PROCESO constante;
     alfa1 = "01";
     alfa2 = #0#2;  alfa2 = "00" + alfa2;    alfa2 = alfa2 % -102;
     alfa3 = #0#3;
     alfa4 = alfa1 + "." + alfa2 + "." + alfa3;

     fCFecha = alfa4;         || primer dia de mes de calculo
     aCError = "";            || inicializo errores

     |HAZ LeeCons;

     |SI aCError ! "";
          |SI nCConst = 500; |O nCConst = 700;
               |SI nCConst = 500;
                    aAlfa = "0000No se ha encontrado la Constante Comun del ";
                    aAlfa = aAlfa + " Regimen General (500)";
                    |MENAV aAlfa;
               |FINSI;
               |SI nCConst = 700;
                    aAlfa = "0000No se ha encontrado la Constante Comun del ";
                    aAlfa = aAlfa + " Regimen Agrario (700)";
                    |MENAV aAlfa;
               |FINSI;
          |SINO;
               alfa1 = #labtraba#0; alfa1 = "00000" + #labtraba#0; alfa1 = alfa1 % -105;
               alfa2 = #labtraba#1; alfa2 = "00000" + #labtraba#1; alfa2 = alfa2 % -105;
               aAlfa = "0000ATENCION: Trabajador: " + alfa1 + "." + alfa2;
               aAlfa = aAlfa + " La constante del trabajador no esta definida.";
               |MENAV aAlfa;
          |FINSI;
     |FINSI;

/*
|ABRE #11;
    #11#0 = #1#32;
|LEE 020#11=;                   || lee la constante
    FEstado = FSalida;
|CIERRA #11;

|DDEFECTO #15;
|ABRE #15;
    #15#0 = #1#32;
|LEE 000#15=;                   || lee la constante especial
|CIERRA #15;
*/
|FPRC;

|PROCESO lee_empre;
|ABRE #1;
    #1#0 = #6#0;
|LEE 000#1=;                       || lee la empresa
    FEstado = FSalida;
|CIERRA #1;

|SI #1#45 ! "S";|Y #1#45 ! "N";    #1#45 = "N";   |FINSI;
|FPRC;

|PROCESO el_tipo;
    tip = 0;                                                || resto
|SI #6#9 = 87;|O #6#9 = 97;|O #6#9 = 85;  tip = 8; |FINSI;  || aprend.
|SI #6#9 = 64;|O #6#9 = 65;               tip = 9; |FINSI;  || t.parcial
|FPRC;

|PROCESO temporal;
|HAZ lee_empre;     |SI FEstado ! 0;    |ACABA;   |FINSI;   || empresa
|HAZ lee_traba;     |SI FEstado ! 0;    |ACABA;   |FINSI;   || trabajador
|SI #labtraba#247 = 613; |O #labtraba#247 = 163;
     nCConst = 700;
|SINO;
     nCConst = 500;
|FINSI;
|HAZ constante;      |SI FEstado ! 0;    |ACABA;   |FINSI;   || constantes

|SI #5#45 = "087";|O #5#45 = "097";|O #5#45 = "085";
    |SI #0#6 ! "T";|Y #0#6 ! "A";  |ACABA;   |FINSI;   || aprendices
|FINSI;
|SI #5#45 = "064";|O #5#45 = "065";
    |SI #0#6 ! "T";|Y #0#6 ! "P";  |ACABA;   |FINSI;   || t.parcial
|FINSI;
|SI #5#45 ! "087";|Y #5#45 ! "097";|Y #5#45 ! "085";
                  |Y #5#45 ! "064";|Y #5#45 ! "065";
    |SI #0#6 ! "T";|Y #0#6 ! "R";  |ACABA;   |FINSI;   || resto
|FINSI;
|SI #5#42 = "H";    |ACABA;   |FINSI;                  || autonomos
|SI #5#42 = "A";|O #5#42 = "B";|O #5#42 = "C";
    |SI PARAMETRO ! "AGRARIA";    |ACABA;   |FINSI;    || agrarios no
|FINSI;
|SI #5#42 ! "A";|Y #5#42 ! "B";|Y #5#42 ! "C";
    |SI PARAMETRO = "AGRARIA";    |ACABA;   |FINSI;    || agrarios solo
|FINSI;

|HAZ la_mama;            || si tiene maternidad

|DDEFECTO #8;  #8#0 = #6#12;  |LEE 000#8=;
    Fbonif = FSalida;

|SI #8#0 ! 0;|Y Fbonif = 0;|Y #6#9 ! 97;    || si existe, no es cero, no 97
    |HAZ el_tipo;
    |HAZ lee_centr;
    |SI #6#3 = 0;
        |HAZ tmp_1;           || TC2/1 normal
    |SINO;
        |HAZ tmp_12;          || TC2/1 normal (finiquitos)
    |FINSI;
    |HAZ tmp_2;               || TC2/1 subvencion de horas
|FINSI;
|SI #8#0 ! 0;|Y Fbonif = 0;|Y #6#9 = 97;
    |HAZ el_tipo;
    |HAZ bonif_97;
    |HAZ tmp_1;               || TC2/1 aprendices minusvalidos
    |HAZ tmp_2;               || TC2/1 subvencion de horas
|FINSI;
|SI sw_mater = 1;|Y #6#3 = 0;
    |HAZ el_tipo;
    |HAZ lee_centr;
    |HAZ bonif_mater;
    |HAZ calcu_mater;
    |HAZ traba_mater;
    |HAZ tmp_1;               || TC2/1 normal (maternidad)
|FINSI;
|FPRC;

|PROCESO la_mama;
    sw_mater = 0;
|SI #6#65 ! 0;|Y anyonum ] 1995;        || bonificacion maternidad 1995

        m_dias = #6#65;
    |SI runnom = "nomytc21";|Y #5#60 = "M"; m_dias = m_dias + #5#89; |FINSI;
    |SI runnom = "nomwtc21";|Y #6#68 = "M"; m_dias = m_dias + #6#70; |FINSI;

        nume1 = 0;
    |SI #6#27 = "M";     nume1 = #6#23; |FINSI;
    |SI #6#28 = "M";     nume1 = #6#24; |FINSI;
    |SI #6#29 = "M";     nume1 = #6#25; |FINSI;
    |SI #6#30 = "M";     nume1 = #6#26; |FINSI;
    |SI nume1 = 0;       nume1 = #6#7;  |FINSI;

        alfa1 = nume1;       alfa1 = "00" + alfa1;    alfa1 = alfa1 % -102;
        alfa2 = #6#2;        alfa2 = "00" + alfa2;    alfa2 = alfa2 % -102;
        alfa3 = alfa1 + "." + alfa2 + "." + anyonum;
        fech1 = alfa3;            nume1 = fech1;
        fech2 = "01.01.1995";     nume2 = fech2;
        nume3 = nume1 - nume2 + 1;

    |SI m_dias [ nume3; sw_mater = 1;  |FINSI;
    |SI #6#2 ] 6;                  sw_mater = 1;  |FINSI;
|FINSI;
|FPRC;

|PROCESO bonif_mater;
     || 100 -> maternidad sobre contratos normales
     || 101 -> maternidad sobre contratos 64
     || 104 -> maternidad sobre contratos 87
     || 105 -> maternidad sobre contratos 85
     || 106 -> maternidad sobre contratos 65
|DDEFECTO #8;
|SI tip ! 9;|Y tip ! 8;
        #8#0  = 100;                             || codigo bonificacion
        #8#6  = #11#7;                           || linea 1 trabajador
        #8#1  = "SITUACION MATERNIDAD";          || descripcion
|SINO;
        #8#1  = "SIT.MATERNIDAD /" + #6#9;       || descripcion especial
|FINSI;
|SI tip = 8;
    |SI #6#9 = 87;  #8#0 = 104;    |FINSI;
    |SI #6#9 = 85;  #8#0 = 105;    |FINSI;
|FINSI;
|SI tip = 9;
    ||SI #6#9 = 64;  #8#0 = 101;    #8#6 = #11#7 * #15#21;   |FINSI;
    ||SI #6#9 = 65;  #8#0 = 106;    #8#6 = #11#7 * #15#22;   |FINSI;
|FINSI;

    #8#3  = 100;                             || % bonificacion
    #8#7  = #11#13+#11#15+#11#17+#11#19;     || linea 2 trabajador
    #8#8  = "N";                             || accidentes NO
    #8#10 = 3;                               || situacion TC1
|FPRC;

|PROCESO liquidados;
    dia_2 = topemes;     || dias que tiene el mes
    dia_3 = 30;          || dias cotizacion 'mensual'
|SI #5#42 = "D";|O #5#42 = "T";
        dia_3 = topemes; || dias cotizacion 'diario'
|FINSI;
    dia_4 = dia_3;

|SI nume1 > dia_4;  nume1 = dia_4;  |FINSI;
|SI nume1 = dia_2; |Y nume1<dia_4;  nume1 = dia_4;  |FINSI;
|FPRC;

|PROCESO calcu_mater;
    #6#12 = #8#0;             || codigo bonificacion
    nume1 = #6#65;
|HAZ liquidados;
    #6#43 = #6#35 * nume1;    || c.c.
    #6#44 = #6#36 * nume1;    || c.p.

    #6porc1 = #8#3;           || para que imprima el TC2/1
    #6#63 = 1;                || para que grabe el temporal
|FPRC;

|PROCESO bonif_97;
     || 102 -> bonificacion sobre contratos 97 antes del 17.05.1997
     || 103 -> bonificacion sobre contratos 97 despues del 17.05.1997

    fech1 = #6#4;
|SI fech1 < "17.05.1997";
      #8#0 = 200 + #8#0;      || en 'ruptura2' se necesita el cod.de bonif.
|SINO;
      #8#0 = 300 + #8#0;
|FINSI;

    #6#12 = #8#0;
|FPRC;

|PROCESO traba_mater;
|SI #6#27 = "M"; m_des = #6#19;    m_has = #6#23; |FINSI;
|SI #6#28 = "M"; m_des = #6#20;    m_has = #6#24; |FINSI;
|SI #6#29 = "M"; m_des = #6#21;    m_has = #6#25; |FINSI;
|SI #6#30 = "M"; m_des = #6#22;    m_has = #6#26; |FINSI;

|SI m_des ! 0;
        alfa1 = m_des;   alfa1 = "00" + alfa1;    alfa1 = alfa1 % -102;
        alfa2 = #6#2;    alfa2 = "00" + alfa2;    alfa2 = alfa2 % -102;
        m_fec = alfa1 + "." + alfa2 + "." + anyonum;
|SINO;
    |SI m_has ! 0;
            alfa1 = m_has;   alfa1 = "00" + alfa1;    alfa1 = alfa1 % -102;
            alfa2 = #6#2;    alfa2 = "00" + alfa2;    alfa2 = alfa2 % -102;
            m_top = alfa1 + "." + alfa2 + "." + anyonum;
    |SINO;
            m_top = f_mes + anyonum;
    |FINSI;
        fech1 = m_top;   nume1 = fech1;
        nume1 = nume1 - m_dias + 1;
        fech1 = nume1;
        m_fec = fech1;
|FINSI;

    #5#40 = m_fec;      || fecha inicio maternidad
|FPRC;

|PROCESO meslite;
|SI #0#2 = 1;  lite_mes = "Enero     "; f_mes = "31.01."; |FINSI;
|SI #0#2 = 2;  lite_mes = "Febrero   "; f_mes = "28.02."; |FINSI;
|SI #0#2 = 3;  lite_mes = "Marzo     "; f_mes = "31.03."; |FINSI;
|SI #0#2 = 4;  lite_mes = "Abril     "; f_mes = "30.04."; |FINSI;
|SI #0#2 = 5;  lite_mes = "Mayo      "; f_mes = "31.05."; |FINSI;
|SI #0#2 = 6;  lite_mes = "Junio     "; f_mes = "30.06."; |FINSI;
|SI #0#2 = 7;  lite_mes = "Julio     "; f_mes = "31.07."; |FINSI;
|SI #0#2 = 8;  lite_mes = "Agosto    "; f_mes = "31.08."; |FINSI;
|SI #0#2 = 9;  lite_mes = "Septiembre"; f_mes = "30.09."; |FINSI;
|SI #0#2 = 10; lite_mes = "Octubre   "; f_mes = "31.10."; |FINSI;
|SI #0#2 = 11; lite_mes = "Noviembre "; f_mes = "30.11."; |FINSI;
|SI #0#2 = 12; lite_mes = "Diciembre "; f_mes = "31.12."; |FINSI;
|FPRC;

|| ***************************************************************************
|| ****** calculos de lectura e impresion ************************************
|| ***************************************************************************

|PROCESO cierra_informe;
||SI sw_abierto = 1;
    ||PIEINF;
    ||FININF;
||FINSI;
|FPRC;

|PROCESO el_print;
||SI sw_abierto = 1;
    ||PRINT;
||FINSI;
|FPRC;

|PROCESO impresion1;
    t_cbonif = t_cbonif + #4#20;
|HAZ el_print;
|FPRC;

|DEFBUCLE nomytc212;   || NOTA: lee segun lo seleccionado en el fichero 7
 #4#1;                 || CLAVE: empresa,bonificacion,trabajador
 ;
 antempre, antbonif,     1;
 antempre, antbonif, 99999;
 ;
 NULL, impresion1;
|FIN;

|DEFBUCLE centros;     || NOTA: lee segun lo seleccionado en el fichero 7
 #4#2;                 || CLAVE: empresa,centro,bonificacion,trabajador
 ;
 antempre, antcentr, antbonif,     1;
 antempre, antcentr, antbonif, 99999;
 ;
 NULL, impresion1;
|FIN;

|PROCESO segundo_tc;
|SI #8#11 = "S";|Y dtipo = 0;      || si hay subvenciones por horas
    |SI sw_abierto = 1;            ||/imprime el segundo TC2/1
        ||INFOR informe2;
    |FINSI;
    |SI siono_cen = "N";   |BUCLE nomytc212;   |FINSI;
    |SI siono_cen = "S";   |BUCLE centros;     |FINSI;
    |HAZ cierra_informe;
    |HAZ auxiliar1;      || graba en el auxiliar de empresas
|FINSI;
|FPRC;

|PROCESO ruptura1;
|HAZ pie;                || calculo pie de informe
|SI sw_abierto = 1;
    |SI endo ! 0;
        ||PIEINF;
    |FINSI;
    ||FININF;
|FINSI;
|HAZ auxiliar;           || graba en el auxiliar de empresas
|HAZ segundo_tc;
|HAZ limpieza;           || inicializa variables acumuladoras
|FPRC;

|PROCESO abreviado;      || LEE AUX. DE EMPRESAS PARA SABER SI ES ABREVIADO
    #2#00 = antempre;                                  || codigo empresa
    #2#01 = #0#2;                                      || mes
    #2#02 = anttipo + dtipo;                           || tipo
|SI runnom = "nomwtc21";      #2#16 = #0#3;  |FINSI;   || a¤o (historico)
|SI siono_cen = "S";  #2#18 = antcentr;  |FINSI;       || centro trabajo
|SI siono_cen = "N";  #2#18 =       99;  |FINSI;
|LEE 000#2=;
|SI FSalida = 0;|Y #2#22 = "S";
        sw_abierto = 1;
|SINO;
        sw_abierto = 0;
|FINSI;
|FPRC;

|PROCESO ruptura2;
    antempre = #7#0;               || lee la empresa para la constante y para
    antcentr = #7#25;              ||/el abreviado S/N
    antbonif = #7#1;
    antporce = #7#14;
    anttipo  = #7#27 - dtipo;
|HAZ abreviado;
|SI sw_abierto = 1;
    ||INFOR informe1;
|FINSI;
|DDEFECTO #8;
|SI #7#1 > 400;               || cuando es doble bonificacion
        #7#1 = #7#1 - 400;
        #8#0 = #7#1;
|FINSI;
|SI #7#1 < 200;
        #8#0 = #7#1;
|SINO;
    |SI #7#1 < 300;
            #8#0 = #7#1 - 200;     || contratos 97 antes de 17.05.1997
            #7#1 = 102;
    |SINO;
            #8#0 = #7#1 - 300;     || contratos 97 despues de 17.05.1997
            #7#1 = 103;
    |FINSI;
|FINSI;
|LEE 000#8=;
|SI #7#1 > 99;
    |ABRE #1;  #1#0 = #7#0;   |LEE 020#1=;   |CIERRA #1;
    nCConst = 500;
    |HAZ constante;
    |SI #7#1 = 100;|O #7#1 = 101;|O #7#1 = 104;|O #7#1 = 105;|O #7#1 = 106;
            tip = #7#27;
            #6#9 = #7#30;     || cod.contrato
        |HAZ bonif_mater;
    |FINSI;
    |SI #7#1 = 102;|O #7#1 = 103;
            #8#0 = #7#1;
    |FINSI;
|FINSI;
|FPRC;

|PROCESO impresion;
    haentrat = 1;
|SI antempre = -1;  |HAZ ruptura2;  |FINSI;
|SI antempre ! #7#0;                         || ruptura por empresa
    |HAZ ruptura1;
    |HAZ ruptura2;
|SINO;
    |SI antcentr ! #7#25;|Y siono_cen = "S"; || ruptura por centro
        |HAZ ruptura1;
        |HAZ ruptura2;
    |SINO;
        |SI antbonif ! #7#1;                 || ruptura por bonificacion
            |HAZ ruptura1;
            |HAZ ruptura2;
        |SINO;
            |SI antporce ! #7#14;
                |HAZ ruptura1;
                |HAZ ruptura2;
            |FINSI;
        |FINSI;
    |FINSI;
|FINSI;

    t_bonif1 = t_bonif1 + #7#20;   || Acumulado contingencias comunes
    t_bonif2 = t_bonif2 + #7#21;   || Acumulado A.T y E.P

|SI #7#20 = 0;|Y #7#21 = 0;
        sibonif = 111;
|SINO;
        sibonif = 0;
|FINSI;

|SI #8#8 = "S";                || Si la bonificacion tiene accidentes
                               || ******* Tablas de epigrafes **********
    |ABRE #5;
    |ABRE #9;
    |ABRE #12;
        #5#0 = #7#0;
        #5#1 = #7#2;
    |LEE 000#5=;
    |SI FSalida = 0;
            epi_tra = #5#32;
            epi_bas = #7#21 - #7#26;
        |HAZ calc_epig;
        |SI #7#26 ! 0;        || si tiene base por ILT genera epig.enfer.
                epi_tra = #11#2;
                epi_bas = #7#26;
            |HAZ calc_epig;
        |FINSI;
    |FINSI;
    |CIERRA #5;
    |CIERRA #9;
    |CIERRA #12;
|FINSI;

|SI sibonif = 0; |Y #8#0 ! 0; |Y #7#14 ! 0;
        endo = endo + 1;

    |SI #8#0 = 100;|O #8#0 = 101;|O #8#0 = 104;|O #8#0 = 105;|O #8#0 = 106;
            #8#3 = 0;    #7#14 = 0;     #7#31 = "";    || no en maternidad
    |FINSI;
    |HAZ el_print;
    |SI #8#0 = 100;|O #8#0 = 101;|O #8#0 = 104;|O #8#0 = 105;|O #8#0 = 106;
            #8#3 = 100;  #7#14 = 100;   #7#31 = #8#0;  || lo repone
    |FINSI;
|FINSI;
|FPRC;

|PROCESO calc_epig;
|SI epi_tra ! #11#2;
    |DDEFECTO #9;
        #9#0 = epi_tra;
    |LEE 020#9=;
        epig_ilt = #9#1;           || porcentajes del epigrafe normal
        epig_ims = #9#2;
|SINO;
        epig_ilt = #11#3;          || porcentajes del epig.enf.
        epig_ims = #11#4;
|FINSI;

    #12#0 = #5#0;
    #12#1 = #5#1;
    #12#2 = epi_tra;
|LEE 000#12=;
|SI FSalida = 0;
        epig_ilt = #12#3;          || porcentajes del epigrafe/trabajador
        epig_ims = #12#4;
|FINSI;

|SI tablaepi1  = 0; |O tablaepi1 = epi_tra;
        tablaepi1  = epi_tra;
        porcepig1  = epig_ilt + epig_ims;
        basepigr1  = basepigr1 + epi_bas;
|SINO;
    |SI tablaepi2 = 0; |O tablaepi2 = epi_tra;
            tablaepi2 = epi_tra;
            porcepig2 = epig_ilt + epig_ims;
            basepigr2 = basepigr2 + epi_bas;
    |SINO;
        |SI tablaepi3 = 0; |O tablaepi3 = epi_tra;
                tablaepi3 = epi_tra;
                porcepig3 = epig_ilt + epig_ims;
                basepigr3 = basepigr3 + epi_bas;
        |SINO;
            |SI tablaepi4 = 0; |O tablaepi4 = epi_tra;
                    tablaepi4 = epi_tra;
                    porcepig4 = epig_ilt + epig_ims;
                    basepigr4 = basepigr4 + epi_bas;
            |SINO;
                    tablaepi5 = epi_tra;
                    porcepig5  = epig_ilt + epig_ims;
                    basepigr5  = basepigr5 + epi_bas;
            |FINSI;
        |FINSI;
    |FINSI;
|FINSI;
|FPRC;

|DEFBUCLE por_empresa;   || NOTA: lee todo el fichero
 #7#1;                   || CLAVE: empresa,bonificacion,%,trabajador,tipo
 ;
 "N",     0,   0,    0,     1, dtipo;
 "N", 99999, 999, 9999, 99999, htipo;
 ;
 NULL, impresion;
|FIN;

|DEFBUCLE por_centros;   || NOTA: lee todo el fichero
 #7#2;                   || CLAVE: empresa,centro,bonificacion,%,trabajador,tipo
 ;
 "S",     0,   0,   0,    0,     1, dtipo;
 "S", 99999, 999, 999, 9999, 99999, htipo;
 ;
 NULL, impresion;
|FIN;

|| ***************************************************************************
|| ****** calculos desde el mantenimiento ************************************
|| ***************************************************************************

|PROCESO el_bucle;
    dtipo = 0;   htipo = 9;
|PARA; |SI; |HACIENDO;

    |MENSA "2302Imprimiendo TC2/1 de empresas ...";
        antempre = -1;
        antcentr = -1;
        antbonif = -1;     || inicializa variables para rupturas
        antporce = -1;
        haentrat = 0;      || switch para saber si lee
        siono_cen = "N";
    |BUCLE por_empresa;
    |SI haentrat = 1;      || si ha impreso cierra el informe, totalizando
        |HAZ ruptura1;
    |FINSI;

    |MENSA "2302Imprimiendo TC2/1 de centros de trabajo ...";
        antempre = -1;
        antcentr = -1;
        antbonif = -1;     || inicializa variables para rupturas
        antporce = -1;
        haentrat = 0;      || switch para saber si lee
        siono_cen = "S";
    |BUCLE por_centros;
    |SI haentrat = 1;      || si ha impreso cierra el informe, totalizando
        |HAZ ruptura1;
    |FINSI;

    |SI dtipo = 0;
            dtipo = 10;   htipo = 19;
    |SINO;
        |SAL;
    |FINSI;
|SIGUE;
|FPRC;

|PROCESO impre;
    c_informe = "nomtc21p";
||HAZ cuadres;
|SI sw_cuadre = 0;
    ||IMPRE 0;
    |SI FSalida = 0;
        |SI runnom = "nomytc21";
            PARAMETRO = "AGRARIA";
            desem = 78;
            cuot2 = 81;
            porc1 = 82;
            porc2 = 83;
            bacc2 = 79;
            bacp2 = 80;
            bilt1 = 84;
            bilt2 = 85;
        |FINSI;
        |SI runnom = "nomwtc21";
            PARAMETRO = "AGRARIA";
            desem = 119;
            cuot2 = 123;
            porc1 = 124;
            porc2 = 125;
            bacc2 = 121;
            bacp2 = 122;
            bilt1 = 126;
            bilt2 = 127;
        |FINSI;

        |MENSA "2302Preparando temporales ...";
            alfa1 = Usuario;  |QBT alfa1;
            alfa1 = (alfa1 + "zzzzzzz") % 107;
            alfa1 = "2" + alfa1;
        |NOME_DAT #4 alfa1;
        |DELINDEX #4;
            alfa1 = Usuario;  |QBT alfa1;
            alfa1 = (alfa1 + "zzzzzzz") % 107;
            alfa1 = "3" + alfa1;
        |NOME_DAT #7 alfa1;
        |DELINDEX #7;
        |ABRET #2;       || aux. empresas
        |ABRE #8;        || bonificaciones
        |HAZ temporal0;  || carga temporal
        |HAZ el_bucle;
        |CIERRAT #2;
        |CIERRA #8;
        |DELINDEX #4;
        |DELINDEX #7;
    |SINO;
        |MENAV "    Proceso ABORTADO !!!";
    |FINSI;
    |FINIMP;
|FINSI;
|FPRC;

|| ***************************************************************************
|| ****** calculos complementarios de trabajo en IMPRESION *******************
|| ***************************************************************************

|PROCESO limpieza;   || INICIALIZA ACUMULADORES
    t_bonif1   = 0;  t_bonif2   = 0;
    t_porboni1 = 0;  t_porboni2 = 0;
    t_cuotboni = 0;  t_reduccio = 0;
    tablaepi1  = 0;  tablaepi2  = 0;
    tablaepi3  = 0;  tablaepi4  = 0;
    tablaepi5  = 0;  porcepig1  = 0;
    porcepig2  = 0;  porcepig3  = 0;
    porcepig4  = 0;  porcepig5  = 0;
    basepigr1  = 0;  basepigr2  = 0;
    basepigr3  = 0;  basepigr4  = 0;
    basepigr5  = 0;  t_porcepi1 = 0;
    t_porcepi2 = 0;  t_porcepi3 = 0;
    t_porcepi4 = 0;  t_porcepi5 = 0;
    t_porcepig = 0;  t_epigrafe = 0;
    t_dfp      = 0;  t_porcdfp  = 0;
    t_total    = 0;  t_cbonif   = 0;
    endo = 0;
|FPRC;

|PROCESO auxiliar;      || ACTUALIZA FICHERO AUXILIAR DE EMPRESAS
    #2#00 = antempre;                                  || codigo empresa
    #2#01 = #0#2;                                      || mes
    #2#02 = anttipo + dtipo;                           || tipo
|SI runnom = "nomwtc21";      #2#16 = #0#3;  |FINSI;   || a¤o (historico)
|SI siono_cen = "S";  #2#18 = antcentr;  |FINSI;       || centro trabajo
|SI siono_cen = "N";  #2#18 =       99;  |FINSI;
|LEE 101#2=;
    estado = FSalida;
/*
|SI #8#10 = 1;
        #2#11 = #2#11 + t_reduccio;               || reducciones 209
        #2#12 = #2#12 + t_epigrafe + t_porcdfp;   || bonificaciones 601
|FINSI;
|SI #8#10 = 2;
        #2#12 = #2#12 + t_total;                  || bonificaciones 601
|FINSI;
|SI #8#10 = 3;
    |SI #8#0 ! 102;|Y #8#0 ! 103;
            #2#21 = #2#21 + t_total;    || reducciones(matern.) 201
    |SINO;
            #2#11 = #2#11 + t_total;    || reducciones(aprend.) 209
    |FINSI;
|FINSI;
*/
|SI PARAMETRO ! "AGRARIA";
    |SI #8#0 = 100;|O #8#0 = 101;|O #8#0 = 104;|O #8#0 = 105;|O #8#0 = 106;
            #2#17 = 6;                  || clave maternidad
    |FINSI;
|FINSI;

|SI estado ! 0;
    |GRABA 020#2n;
|SINO;
    |GRABA 020#2a;
|FINSI;
|LIBERA #2;
|FPRC;

|PROCESO auxiliar1;      || ACTUALIZA FICHERO AUXILIAR DE EMPRESAS
    #2#00 = antempre;    || codigo empresa
    #2#01 = #0#2;        || mes
    #2#02 = anttipo + dtipo;  || tipo
|SI runnom = "nomwtc21";      #2#16 = #0#3;  |FINSI;   || a¤o (historico)
|SI siono_cen = "S";  #2#18 = antcentr;  |FINSI;
|SI siono_cen = "N";  #2#18 =       99;  |FINSI;
|LEE 101#2=;
    estado = FSalida;

||    #2#12 = #2#12 + t_cbonif;      || bonif./subven. 601

|SI estado ! 0;
    |GRABA 020#2n;
|SINO;
    |GRABA 020#2a;
|FINSI;
|LIBERA #2;
|FPRC;

|PROCESO pie;
|SI anttipo ! 8;
        t_porboni1 = #8#4 + #8#6;            || Porcentaje contingencias
        t_porboni2 = #8#5 + #8#7;            || Porcentaje D-F-P
        t_cuotboni = (t_bonif1 % t_porboni1) ! EURODB;      || cuota conting.
        t_reduccio = (t_cuotboni % antporce) ! EURODB;      || bonif./reduccion

    |SI #8#8 = "S";         || Si la bonificacion tiene accidentes
            t_porcepi1 = (basepigr1 % porcepig1) ! EURODB;
            t_porcepi2 = (basepigr2 % porcepig2) ! EURODB;
            t_porcepi3 = (basepigr3 % porcepig3) ! EURODB;
            t_porcepi4 = (basepigr4 % porcepig4) ! EURODB;
            t_porcepi5 = (basepigr5 % porcepig5) ! EURODB;
    |FINSI;
                                                       || total bases epigrafes
        t_porcepig = t_porcepi1 + t_porcepi2 + t_porcepi3;
        t_porcepig = t_porcepig + t_porcepi4 + t_porcepi5;
        t_epigrafe = (t_porcepig % antporce) ! EURODB;      || total % epigrafes
        t_dfp      = (t_bonif2 % t_porboni2) ! EURODB;      || cuota DFP
        t_porcdfp  = (t_dfp % antporce) ! EURODB;           || reduccion DFP
|SINO;
    |SI #8#0 = 102;      || aprendices antes del 17.05.1997
    /*
            t_cuotboni = #15#10 * endo;              || cuota con.comunes
            t_reduccio = (t_cuotboni % antporce) ! EURODB;|| reduccion con.comunes
            t_dfp      = #15#12 * endo;              || cuota DFP
            t_porcdfp  = (t_dfp % antporce) ! EURODB;     || reduccion DFP
            t_porcepig = (#15#11 + #15#9) * endo;    || cuota epigrafes
            t_epigrafe = (t_porcepig % antporce);    || reduccion epigrafes
    */
    |FINSI;
    |SI #8#0 = 103;      || aprendices despues del 17.05.1997
            t_cuotboni = #11#82 * endo;              || cuota con.comunes
            t_reduccio = (t_cuotboni % antporce) ! EURODB;|| reduccion con.comunes
            t_dfp      = #11#84 * endo;              || cuota DFP
            t_porcdfp  = (t_dfp % antporce) ! EURODB;     || reduccion DFP
            t_porcepig = (#11#86 + #11#87) * endo;   || cuota epigrafes
            t_epigrafe = (t_porcepig % antporce);    || reduccion epigrafes
    |FINSI;
    |SI #8#0 = 104;      || maternidad para los 87
    /*
            t_cuotboni = #15#13 * endo;           || cuota con.comunes
            t_reduccio = (t_cuotboni % 100) ! EURODB;  || reduccion con.comunes
            t_dfp      = #15#5 * endo;            || cuota DFP
            t_porcdfp  = (t_dfp % antporce) ! EURODB;  || reduccion DFP
            t_porcepig = 0;                       || cuota epigrafes
            t_epigrafe = 0;                       || reduccion epigrafes
    */
    |FINSI;
    |SI #8#0 = 105;      || maternidad para los 85
            t_cuotboni = #11#83 * endo;           || cuota con.comunes
            t_reduccio = (t_cuotboni % 100) ! EURODB;  || reduccion con.comunes
            t_dfp      = #11#85 * endo;           || cuota DFP
            t_porcdfp  = (t_dfp % 100) ! EURODB;       || reduccion DFP
            t_porcepig = 0;                       || cuota epigrafes
            t_epigrafe = 0;                       || reduccion epigrafes
    |FINSI;
        t_porboni1 = (t_cuotboni * 100) / t_bonif1;
        t_porboni2 = (t_dfp      * 100) / t_bonif2;
|FINSI;
    t_total    = t_reduccio + t_epigrafe + t_porcdfp;  || total reducc.
|FPRC;
