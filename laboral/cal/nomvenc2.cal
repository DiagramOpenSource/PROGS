|FICHEROS;
     nomvenc2;
     labempre;
     labtraba;
     labcentr;
     nomcateg;
     nomtmp03;
     labregis;
     nomconca;
     nomconax;
     nomantca;
     nomvarca;
     nomcalca;
     nombotra;
|FIN;

|VARIABLES;
     aFecha = "";
     fFecha = @;
     fFecha2 = @;
     fFechaLis = @;
     aMes = "";
     nMes = 0;
     nTope = 0;
     aTope = "";
     nAny = 0;
     aAny = "";
     mod = 0;
     fRefDesde = @;
     fRefHasta = @;
     aVencCat = "";
     nTipo = 0;
     aAlfa = "";
     nNume = 0;
     nCampo = 0;
     nCampo1 = 0;
     aDia = "";
     swExisteAnt = 0;
     swNoSigas = 0;
     swSigue = 0;
     aMesBaja = "";
     nMesBaja = 0;

     aFechaDesde = "";
     aFechaHasta = "";
     aMesDesde = "";
     aMesHasta = "";
     nMesDesde = 0;
     nMesHasta = 0;
     nMes1 = 0;
     nMes2 = 0;
     aNif = "";
     aNuevaFecha = "";
     fNuevaFecha = @;
     nNuevaFecha = 0;
     nFechaRef = 0;
     fNuevaMenosUno = @;
     fech1 = @;
     fech2 = @;
     aDuracion = "";
     aDiaAlta = "";
     aMesAlta = "";
     aAnyAlta = "";
     aDiaBaja = "";
     aAnyBaja = "";
     nDiaAlta = 0;
     nMesAlta = 0;
     nAnyAlta = 0;
     nDiaBaja = 0;
     nAnyBaja = 0;
     nDias = 0;
     nMeses = 0;
     nAnys = 0;
     nume1 = 0;
     xmesnume = 0;
     xanynume = 0;
     xtopemes = 0;
     aAnys = "";
     aMeses = "";
     aDias = "";
     aAlfa1 = "";
     aAlfa2 = "";
     aAlfa3 = "";
     nDesdeTra = 0;
     nHastaTra = 0;
     aDesdeEstado = "";
     aHastaEstado = "";
     swSigueReg = 0;
     swSigueVenc = 0;
     fFecha1 = @;
     aFecha1 = "";
     aNuevoCont = "";
     nEmp = 0;
     aFech1 = "";
     aFech2 = "";

     nDEmpre             = 0;
     nHEmpre             = 0;
     nOpcion             = 0;

     aEmp                = "";
     aNomEmp             = "";

     {-1}aMenu           = "";
         aMenu1          = "2400";
         aMenu2          = "1";
         aMenu3          = "Archivo documental activado. Elija opci¢n: ";
         aMenu4          = "IAB";
         aMenu5          = "Imprimir";
         aMenu6          = "Archivar";
         aMenu7          = "Ambos";

     &eaVengoDe          = "";
     &eaPeriodos         = "";
     &enDesdeMes         = 0;
     &enAny              = 0;
     &nArchi             = 0;
     &swErrorDSArchi     = 0;

     nPaga = 0;
|FIN;

|PROCESO topemes;
     |SI nMes = 1; |O nMes = 3; |O nMes = 5; |O nMes = 7; |O nMes = 8; |O nMes = 10; |O nMes = 12;
           nTope = 31;
     |SINO;
          |SI nMes = 2;
               aAny = aFecha % -104;
               nAny = aAny;
               mod = nAny $ 4;
               |SI mod = 0;
                    nTope = 29;
               |SINO;
                    nTope = 28;
               |FINSI;
          |SINO;
               nTope = 30;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Defectos; |TIPO 7;
|ACABA;
     fFecha = ~;
     aFecha = fFecha;
     aFecha = aFecha % -108;
     aFecha = "01" + aFecha;
     fFecha = aFecha;
     #nomvenc2#2 = fFecha;
     |PINTA #nomvenc2#2;

     fFecha = ~;
     aFecha = fFecha;
     aMes = aFecha % 402;
     nMes = aMes;
     |HAZ topemes;
     aFecha = aFecha % -108;
     aTope = nTope;
     aFecha = aTope + aFecha;
     fFecha = aFecha;
     #nomvenc2#3 = fFecha;
     |PINTA #nomvenc2#3;
|FPRC;

|PROCESO Imprime;
     |PRINT;
|FPRC;

|DEFBUCLE Imprime;
     #nomtmp03#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, Imprime;
|FIN;

|PROCESO Duracion;
     fech1 = aFech1;
     fech2 = aFech2;

     aDuracion = "";

     aDiaAlta = (aFech1 % 102);
     aMesAlta = (aFech1 % 402);
     aAnyAlta = (aFech1 % -104);
     aDiaBaja = (aFech2 % 102);
     aMesBaja = (aFech2 % 402);
     aAnyBaja = (aFech2 % -104);

     nDiaAlta = aDiaAlta;
     nMesAlta = aMesAlta;
     nAnyAlta = aAnyAlta;
     nDiaBaja = aDiaBaja;
     nMesBaja = aMesBaja;
     nAnyBaja = aAnyBaja;

     nAnys = nAnyBaja - nAnyAlta;

     nMeses = nMesBaja - nMesAlta;
     |SI nMeses [ 0;
          |SI nAnys ! 0;
               nAnys = nAnys - 1;
               nMeses = 12 + nMeses;
          |FINSI;
     |FINSI;

     nDias = nDiaBaja - nDiaAlta + 1;
     nume1 = nDiaAlta - 1;
     xmesnume = nMesBaja;
     xanynume = nAnyBaja;
     aFecha = aFech2;
     nMes = nMesBaja;
     |HAZ topemes;
     xtopemes = nTope;
     |SI nDiaBaja = nume1;
          || nMeses = nMeses + 1;
          nDias = 0;
     |SINO;
          |SI nDiaAlta = 1; |Y nDiaBaja = xtopemes;
               nMeses = nMeses + 1;
               nDias = 0;
          |FINSI;
     |FINSI;
     |SI nDias < 0;
          xmesnume = nMesAlta;
          xanynume = nAnyAlta;
          aFecha = aFech1;
          nMes = nMesAlta;
          |HAZ topemes;
          xtopemes = nTope;
          nDias = xtopemes - nDiaAlta + 1;
          nDias = nDias + nDiaBaja;
          nMeses = nMeses - 1;
     |FINSI;
     |SI nMeses = 12;
          nAnys = nAnys + 1;
          nMeses = 0;
     |FINSI;

     aAnys = nAnys;
     aMeses = nMeses;
     aDias = nDias;

     |SI nAnys > 0;
          aDuracion = aAnys + " a¤o";
          |SI nAnys > 1;
               aDuracion = aDuracion + "s";
          |FINSI;
     |FINSI;
     |SI nMeses > 0;
          |SI aDuracion ! "";
               aDuracion = aDuracion + ", ";
          |FINSI;
          aDuracion = aDuracion + aMeses + " mes";
          |SI nMeses > 1;
               aDuracion = aDuracion + "es";
          |FINSI;
     |FINSI;
     |SI nDias > 0;
          |SI aDuracion ! "";
               aDuracion = aDuracion + ", ";
          |FINSI;
          aDuracion = aDuracion + aDias + " d¡a";
          |SI nDias > 1;
               aDuracion = aDuracion + "s";
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO GrabaLinea;
     #labempre#0 = #labtraba#0;
     |LEE 000#labempre.=;

     #nomtmp03#0 = #labtraba#0;
     #nomtmp03#1 = #labtraba#1;
     #nomtmp03#2 = #labtraba#2;
     #nomtmp03#3 = nTipo;
     #nomtmp03#4 = fFecha;
     #nomtmp03#9 = #labempre#2;
     #nomtmp03#10 = aNuevoCont;

     |SI nTipo = 8;
     |O nTipo = 81; |O nTipo = 82;
     |O nTipo = 86; |O nTipo = 87;
          |SI swSigue = 3;     || Vencimiento de contrato
               |SI nTipo = 8;
                    |SI swSigueReg = 1;
                         #nomtmp03#4 = fFecha;
                    |FINSI;
                    |SI swSigueVenc = 1;
                         #nomtmp03#4 = fFecha1;
                    |FINSI;
               |FINSI;
               |SI nTipo = 81;
                    #nomtmp03#4 = fFecha;
               |FINSI;
               |SI nTipo = 82;
                    #nomtmp03#4 = fFecha1;
               |FINSI;
               #nomtmp03#5 = #labregis#4;         || Contrato
               #nomtmp03#6 = #labregis#7;         || Fecha inicio Contrato
               aAlfa1 = #labregis#18; |QBF aAlfa1;
               aAlfa2 = #labregis#22; |QBF aAlfa2;
               |SI aAlfa1 ! "0";
                    aAlfa1 = aAlfa1 + " meses";
               |FINSI;
               |SI aAlfa2 ! "0";
                    |SI aAlfa1 ! "0";
                         aAlfa2 = ", " + aAlfa2 + " d¡as";
                    |SINO;
                         aAlfa2 = aAlfa2 + " d¡as";
                    |FINSI;
               |FINSI;
               |SI aAlfa1 = "0"; aAlfa1 = ""; |FINSI;
               |SI aAlfa2 = "0"; aAlfa2 = ""; |FINSI;
               aAlfa = aAlfa1 + aAlfa2;
               |QBF aAlfa;
               |SI aAlfa ! "";
                    #nomtmp03#7 = aAlfa;               || Duracion Acumulada
               |SINO;
                    || duracion de registro a cero, la calculo yo
                    aFech1 = #labtraba#36;
                    aFech2 = #nomtmp03#4;
                    |HAZ Duracion;
                    #nomtmp03#7 = aDuracion;                || Duracion Acumulada
               |FINSI;
               #nomtmp03#8 = #labregis#15;        || Nro. Prorrogas
          |SINO;
               #nomtmp03#5 = #labtraba#45;         || Contrato
               #nomtmp03#6 = #labtraba#36;         || Fecha inicio Contrato
               aFech1 = #labtraba#36;
               aFech2 = #labtraba#177;
               |QBF aFech2;
               |SI aFech2 = "";
                    aFech2 = #labtraba#37;
               |FINSI;
               |HAZ Duracion;
               #nomtmp03#7 = aDuracion;                || Duracion Acumulada
          |FINSI;
     |FINSI;
     |SI nTipo ] 41; |Y nTipo [ 48;
          #nomtmp03#8 = nMes;                || Mes Paga
     |FINSI;

     #nomtmp03#11 = #labtraba#77;
     #nomtmp03#12 = #labcentr#2;

     |GRABA 020#nomtmp03.n;
|FPRC;

|PROCESO Antig_Trab;
     fFecha = #labtraba#35;
     |SI fFecha ] fRefDesde ; |Y fFecha [ fRefHasta;
          nTipo = 10;          || Antiguedad Trabajdor
          |HAZ GrabaLinea;
     |FINSI;
|FPRC;

|PROCESO CompNomantca;
     swExisteAnt = 0;
     aFecha = #labtraba#34;
     fFecha = aFecha;

     |SI #nomantca#nCampo# = 0;
          fFecha = "31.12.2999";
          |ACABA;
     |FINSI;

     nNume = (#nomantca#nCampo# / 1000000);
     fFecha = fFecha + nNume;
     |SI #nomantca#3 = 0;
          fFecha = "01.01.1900";
     |SINO;
          aFecha = fFecha;
          aDia = aFecha % 102;
          aMes = aFecha % 402;
          aAny = aFecha % -104;
     |FINSI;

     |SI #nomantca#3 = 1;
          fFecha = fFecha;
          aFecha = fFecha;
          aFecha = aFecha % -108;
          aFecha = "01" + aFecha;
          fFecha = aFecha;
     |FINSI;
     |SI #nomantca#3 = 2;    || mes siguiente
          fFecha = fFecha + 0.001000;
          aFecha = fFecha;
          aFecha = aFecha % -108;
          aFecha = "01" + aFecha;
          fFecha = aFecha;
     |FINSI;
     |SI #nomantca#3 = 3;    || mismo trimestre
          fFecha = fFecha;
          nMes = aMes;
          |SI nMes ] 1; |Y nMes [ 3; aMes = "01"; |FINSI;
          |SI nMes ] 4; |Y nMes [ 6; aMes = "04"; |FINSI;
          |SI nMes ] 7; |Y nMes [ 9; aMes = "07"; |FINSI;
          |SI nMes ] 10; |Y nMes [ 12; aMes = "10"; |FINSI;
          aFecha = fFecha;
          aFecha = aFecha % -104;
          aFecha = "01" + "." + aMes + "." + aFecha;
          fFecha = aFecha;
     |FINSI;
     |SI #nomantca#3 = 4;    || siguiente trimestre
          fFecha = fFecha;
          nMes = aMes;
          |SI nMes ] 1; |Y nMes [ 3; aMes = "04"; |FINSI;
          |SI nMes ] 4; |Y nMes [ 6; aMes = "07"; |FINSI;
          |SI nMes ] 7; |Y nMes [ 9; aMes = "10"; |FINSI;
          |SI nMes ] 10; |Y nMes [ 12;
                aMes = "01"
                nAny = nAny + 1;
                aAny = nAny;
          |FINSI;
          aFecha = fFecha;
          aFecha = "01" + "." + aMes + "." + aAny;
          fFecha = aFecha;
     |FINSI;
     |SI #nomantca#3 = 5;    || mismo semestre
          fFecha = fFecha;
          nMes = aMes;
          |SI nMes ] 1; |Y nMes [ 6; aMes = "01"; |FINSI;
          |SI nMes ] 7; |Y nMes [ 12; aMes = "07"; |FINSI;
          aFecha = fFecha;
          aFecha = aFecha % -104;
          aFecha = "01" + "." + aMes + "." + aFecha;
          fFecha = aFecha;
     |FINSI;
     |SI #nomantca#3 = 6;    || siguiente semestre
          fFecha = fFecha;
          nMes = aMes;
          |SI nMes ] 1; |Y nMes [ 6; aMes = "07"; |FINSI;
          |SI nMes ] 7; |Y nMes [ 12;
                aMes = "01"
                nAny = nAny + 1;
                aAny = nAny;
          |FINSI;
          aFecha = fFecha;
          aFecha = "01" + "." + aMes + "." + aAny;
          fFecha = aFecha;
     |FINSI;
     |SI #nomantca#3 = 7;    || mismo a¤o
          fFecha = fFecha;
          aMes = "01";
          aFecha = fFecha;
          aFecha = aFecha % -104;
          aFecha = "01" + "." + aMes + "." + aFecha;
          fFecha = aFecha;
     |FINSI;
     |SI #nomantca#3 = 8;    || siguiente a¤o
          fFecha = fFecha;
          aMes = "01"
          nAny = nAny + 1;
          aAny = nAny;
          aFecha = fFecha;
          aFecha = "01" + "." + aMes + "." + aAny;
          fFecha = aFecha;
     |FINSI;
|FPRC;

|PROCESO nomantca;
     #nomantca#0 = #labtraba#87;
     #nomantca#1 = #labtraba#17;
     |LEE 000#nomantca.=;
     |SI FSalida ! 0;
          fFecha = "01.01.1900";
          |ACABA;
     |FINSI;
     |PARA nCampo = 4; |SI nCampo [ 13; |HACIENDO nCampo = nCampo + 1;
          |HAZ CompNomantca;
          |SI fFecha ] fRefDesde ; |Y fFecha [ fRefHasta;
               swExisteAnt = 1;
               |SAL;
          |FINSI;
     |SIGUE;
     |SI swExisteAnt = 1;     |ACABA;        |FINSI;
     |PARA nCampo = 24; |SI nCampo [ 33; |HACIENDO nCampo = nCampo + 1;
          |HAZ CompNomantca;
          |SI fFecha ] fRefDesde ; |Y fFecha [ fRefHasta;
               swExisteAnt = 1;
               |SAL;
          |FINSI;
     |SIGUE;
     |SI swExisteAnt = 1;     |ACABA;        |FINSI;
     |PARA nCampo = 44; |SI nCampo [ 53; |HACIENDO nCampo = nCampo + 1;
          |HAZ CompNomantca;
          |SI fFecha ] fRefDesde ; |Y fFecha [ fRefHasta;
               swExisteAnt = 1;
               |SAL;
          |FINSI;
     |SIGUE;
|FPRC;

|PROCESO nomconax;
     #nomconax#0 = #labtraba#87;
     |LEE 000#nomconax.=;
     |SI FSalida ! 0;
          |ACABA;
     |FINSI;

     |PARA nCampo = 65; |SI nCampo [ 79; |HACIENDO nCampo = nCampo + 1;
          swExisteAnt = 0;
          aFecha = #labtraba#34;
          fFecha = aFecha;
          |SI #nomconax#nCampo# ! 0;
               nNume = (#nomconax#nCampo# / 1000000);
               fFecha = fFecha + nNume;
               |SI #nomconax#95 = 0;
                    fFecha = "01.01.1900";
               |SINO;
                    aFecha = fFecha;
                    aDia = aFecha % 102;
                    aMes = aFecha % 402;
                    aAny = aFecha % -104;
               |FINSI;

               |SI #nomconax#95 = 1;
                    fFecha = fFecha;
                    aFecha = fFecha;
                    aFecha = aFecha % -108;
                    aFecha = "01" + aFecha;
                    fFecha = aFecha;
               |FINSI;
               |SI #nomconax#95 = 2;    || mes siguiente
                    fFecha = fFecha + 0.001000;
                    aFecha = fFecha;
                    aFecha = aFecha % -108;
                    aFecha = "01" + aFecha;
                    fFecha = aFecha;
               |FINSI;
               |SI #nomconax#95 = 3;    || mismo trimestre
                    fFecha = fFecha;
                    nMes = aMes;
                    |SI nMes ] 1; |Y nMes [ 3; aMes = "01"; |FINSI;
                    |SI nMes ] 4; |Y nMes [ 6; aMes = "04"; |FINSI;
                    |SI nMes ] 7; |Y nMes [ 9; aMes = "07"; |FINSI;
                    |SI nMes ] 10; |Y nMes [ 12; aMes = "10"; |FINSI;
                    aFecha = fFecha;
                    aFecha = aFecha % -104;
                    aFecha = "01" + "." + aMes + "." + aFecha;
                    fFecha = aFecha;
               |FINSI;
               |SI #nomconax#95 = 4;    || siguiente trimestre
                    fFecha = fFecha;
                    nMes = aMes;
                    |SI nMes ] 1; |Y nMes [ 3; aMes = "04"; |FINSI;
                    |SI nMes ] 4; |Y nMes [ 6; aMes = "07"; |FINSI;
                    |SI nMes ] 7; |Y nMes [ 9; aMes = "10"; |FINSI;
                    |SI nMes ] 10; |Y nMes [ 12;
                          aMes = "01"
                          nAny = nAny + 1;
                          aAny = nAny;
                    |FINSI;
                    aFecha = fFecha;
                    aFecha = "01" + "." + aMes + "." + aAny;
                    fFecha = aFecha;
               |FINSI;
               |SI #nomconax#95 = 5;    || mismo semestre
                    fFecha = fFecha;
                    nMes = aMes;
                    |SI nMes ] 1; |Y nMes [ 6; aMes = "01"; |FINSI;
                    |SI nMes ] 7; |Y nMes [ 12; aMes = "07"; |FINSI;
                    aFecha = fFecha;
                    aFecha = aFecha % -104;
                    aFecha = "01" + "." + aMes + "." + aFecha;
                    fFecha = aFecha;
               |FINSI;
               |SI #nomconax#95 = 6;    || siguiente semestre
                    fFecha = fFecha;
                    nMes = aMes;
                    |SI nMes ] 1; |Y nMes [ 6; aMes = "07"; |FINSI;
                    |SI nMes ] 7; |Y nMes [ 12;
                          aMes = "01"
                          nAny = nAny + 1;
                          aAny = nAny;
                    |FINSI;
                    aFecha = fFecha;
                    aFecha = "01" + "." + aMes + "." + aAny;
                    fFecha = aFecha;
               |FINSI;
               |SI #nomconax#95 = 7;    || mismo a¤o
                    fFecha = fFecha;
                    aMes = "01";
                    aFecha = fFecha;
                    aFecha = aFecha % -104;
                    aFecha = "01" + "." + aMes + "." + aFecha;
                    fFecha = aFecha;
               |FINSI;
               |SI #nomconax#95 = 8;    || siguiente a¤o
                    fFecha = fFecha;
                    aMes = "01"
                    nAny = nAny + 1;
                    aAny = nAny;
                    aFecha = fFecha;
                    aFecha = "01" + "." + aMes + "." + aAny;
                    fFecha = aFecha;
               |FINSI;
               |SI fFecha ] fRefDesde ; |Y fFecha [ fRefHasta;
                    swExisteAnt = 1;
                    |SAL;
               |FINSI;
          |FINSI;
     |SIGUE;
|FPRC;

|PROCESO Antiguedad;
     #nomconca#0 = #labtraba#87;
     |LEE 000#nomconca.=;
     |SI #nomconca#2 ] 2;
          fFecha = #labtraba#34;
     |SINO;
          |ACABA;
     |FINSI;

     |DDEFECTO #nomconax;
     |SI #nomconca#2 = 2;
          #nomconax#0 = #labtraba#87;
          |LEE 000#nomconax.=;
     |FINSI;

     |SI #nomconca#2 = 2; |Y #nomconax#95 = 9;
          |HAZ nomantca;
     |SINO;
          |HAZ nomconax;
     |FINSI;
     |SI swExisteAnt = 1;
          nTipo = 9;
          |HAZ GrabaLinea;
     |FINSI;
|FPRC;

|PROCESO labregis;
     swSigue = 0;

     |SI #labtraba#177 = #labregis#8; |Y #labregis#8 ! "          ";
          swSigue = swSigue + 1;
          || un trabajador con el mismo DNI
          || y con la misma fecha de vencimiento que la del fin de contrato
          || del labregis puede corresponder con el
          || que estoy buscando
     |SINO;
          |SI #labtraba#177 = #labregis#20; |Y #labregis#20 ! "          ";
          || si no, mira la prorroga
               swSigue = swSigue + 1;
          |FINSI;
     |FINSI;

     |SI #labtraba#0 = #labregis#1;
          || misma empresa, claro
          swSigue = swSigue + 1;
     |FINSI;

     |SI swSigue = 2;
          aFecha = #labregis#20;
          |QBF aFecha;
          |SI aFecha ! ""; |Y aFecha ! "..........";
               fFecha = aFecha;
          |SINO;
               aFecha = #labregis#8;
               |QBF aFecha;
               |SI aFecha ! ""; |Y aFecha ! "..........";
                    fFecha = aFecha;
               |FINSI;
          |FINSI;
          |SI aFecha ! ""; |Y aFecha ! "..........";
               |SI fFecha ] fRefDesde ; |Y fFecha [ fRefHasta; |Y fFecha ! fNuevaMenosUno;
                    swSigue = swSigue + 1;
                    nTipo = 8;
                    |HAZ GrabaLinea;
               |FINSI;
          |FINSI;

          |ERROR10;
     |FINSI;
|FPRC;

|DEFBUCLE labregis;
     #labregis#3;
     ;
     #labtraba#7;
     #labtraba#7;
     ;
     NULL, labregis;
|FIN;

|PROCESO Contrato;
     aFecha = "";
     fFecha = "01.01.0000";
     aFecha1 = "";
     fFecha1 = "01.01.0000";
     |SELECT #2#labregis;
     #labregis#1 = #labtraba#0;
     #labregis#2 = #labtraba#1;
     |LEE 000#labregis.=;
     |SI FSalida = 0;
          swSigue = 0;
          aFecha = #labregis#20;
          |QBF aFecha;
          |SI aFecha ! ""; |Y aFecha ! "..........";
               fFecha = aFecha;
          |SINO;
               aFecha = #labregis#8;
               |QBF aFecha;
               |SI aFecha ! ""; |Y aFecha ! "..........";
                    fFecha = aFecha;
               |FINSI;
          |FINSI;

          aFecha1 = #labtraba#177;
          |QBF aFecha1;
          |SI aFecha1 ! ""; |Y aFecha1 ! "..........";
               fFecha1 = aFecha1;
          |FINSI;

          swSigueReg = 0;
          swSigueVenc = 0;

          || segun la fecha del registro
          |SI aFecha ! ""; |Y aFecha ! "..........";
               |SI fFecha ] fRefDesde ; |Y fFecha [ fRefHasta; |Y fFecha ! fNuevaMenosUno;
                    swSigueReg = 1;
               |FINSI;
          |FINSI;

          || segun la fecha de vto. del trabajador
          |SI aFecha1 ! ""; |Y aFecha1 ! "..........";
               |SI fFecha1 ] fRefDesde ; |Y fFecha1 [ fRefHasta; |Y fFecha ! fNuevaMenosUno;
                    swSigueVenc = 1;
               |FINSI;
          |FINSI;

          |SI swSigueReg = 1; |O swSigueVenc = 1;
               swSigue = 3;
               nTipo = 8;
               |SI swSigueReg = 1; |Y fFecha ! fFecha1;
               |Y aFecha1 ! ""; |Y aFecha1 ! "..........";
                    nTipo = 81;    || vto. segun fecha registro, distinta
               |FINSI;             || a la de vto. del trabajador
               |SI swSigueVenc = 1; |Y fFecha1 ! fFecha;
               |Y aFecha ! ""; |Y aFecha ! "..........";
                    nTipo = 82;    || vto. segun fecha vto. trab., distinta
               |FINSI;             || a la del registro
               |HAZ GrabaLinea;
          |FINSI;
     |SINO;
          || vamos a ver, soy un trabajador sin contrato eso es porque vengo
          || de otro trabajador y he tenido un cambio de algo (p.ej. de cate-
          || goria o algo asi) y no me ha hecho falta contrato nuevo pero s¡
          || nueva ficha

          swSigue = 0;

          |BUCLE labregis;

          |SI swSigue ! 3;
               aFecha = #labtraba#177;
               |QBF aFecha;
               |SI aFecha ! ""; |Y aFecha ! "..........";
                    fFecha = aFecha;
                    nTipo = 86;
               |SINO;
                    aFecha = #labtraba#37;
                    |QBF aFecha;
                    |SI aFecha ! ""; |Y aFecha ! "..........";
                         fFecha = aFecha;
                         nTipo = 87;
                    |FINSI;
               |FINSI;
               |SI aFecha ! ""; |Y aFecha ! "..........";
                    |SI fFecha ] fRefDesde ; |Y fFecha [ fRefHasta; |Y fFecha ! fNuevaMenosUno;
                         |HAZ GrabaLinea;
                    |FINSI;
               |FINSI;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO BonifMay60;
     fFecha = #labtraba#11;
     fFecha = fFecha + 0.000060;

     |SI #labtraba#212 = 1;        || segun fecha antiguedad
          fFecha2 = #labtraba#34;
     |FINSI;
     |SI #labtraba#212 = 2;        || segun fecha alta
          fFecha2 = #labtraba#36;
     |FINSI;
     fFecha2 = fFecha2 + 0.000005;

     |SI fRefHasta ] fFecha; |Y fRefHasta ] fFecha2;
          |SI fFecha ] fRefDesde ; |Y fFecha [ fRefHasta;
               nTipo = 6;          || CUMPLE 60 A¥OS
               |HAZ GrabaLinea;
          |FINSI;
          |SI fFecha2 ] fRefDesde ; |Y fFecha2 [ fRefHasta;
               nTipo = 7;          || CUMPLE 5 A¥OS ANTIGUEDAD
               |HAZ GrabaLinea;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO PagasMes;
     #nomconca#0 = #labtraba#87;
     |LEE 000#nomconca.=;
     |SI FSalida = 0;
          fFecha = #nomvenc2#2; aAlfa = fFecha; aAlfa = aAlfa % 402; nMes1 = aAlfa;
          fFecha = #nomvenc2#3; aAlfa = fFecha; aAlfa = aAlfa % 402; nMes2 = aAlfa;
          fFecha = #nomvenc2#2; aAlfa = fFecha; aAlfa = aAlfa % -104; nAny = aAlfa;
          |PARA nPaga = 1; |SI nPaga [ 8; |HACIENDO nPaga = nPaga + 1;
               nCampo = nPaga + 19;
               |SI nMes1 [ #nomconca#nCampo#; |Y #nomconca#nCampo# [ nMes2;
                    nCampo1 = nPaga + 112;
                    |SI #labtraba#nCampo1# ! "S";
                         nMes = #nomconca#nCampo#;

                         aAlfa1 = "01";
                         aAlfa2 = nMes; |QBF aAlfa2; aAlfa2 = ("00" + aAlfa2) % -102;
                         aAlfa3 = nAny;
                         aAlfa = aAlfa1 + "." + aAlfa2 + "." + aAlfa3;

                         fFecha = aAlfa;
                         nTipo = 40 + nPaga;          || HAY QUE PAGAR PAGA

                         |HAZ GrabaLinea;
                    |FINSI;
               |FINSI;
          |SIGUE;
     |FINSI;
|FPRC;

|PROCESO AutCumple50;
     |SI #labtraba#247 = 521;      || autonomos
          fFecha = #labtraba#11;
          fFecha = fFecha + 0.000050
          |SI fFecha ] fRefDesde ; |Y fFecha [ fRefHasta;
               nTipo = 5;          || CUMPLE 50 A¥OS
               |HAZ GrabaLinea;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Permiso;
     fFecha = #labtraba#209;
     |SI fFecha ] fRefDesde ; |Y fFecha [ fRefHasta;
          nTipo = 4;          || BONIFICACION
          |HAZ GrabaLinea;
     |FINSI;
|FPRC;

|PROCESO nombotra;
     fFecha = #nombotra#5;
     |SI fFecha ] fRefDesde ; |Y fFecha [ fRefHasta;
          nTipo = 11;          || BONIFICACION
          |HAZ GrabaLinea;
          |ERROR10;
     |FINSI;
|FPRC;

|DEFBUCLE nombotra;
     #nombotra#1;
     ;
     #labtraba#0, #labtraba#1, 001;
     #labtraba#0, #labtraba#1, 999;
     ;
     NULL, nombotra;
|FIN;

|PROCESO Bonif_Fomento;
     |BUCLE nombotra;
|FPRC;

|PROCESO Bonificacion;
     fFecha = #labtraba#41;
     |SI fFecha ] fRefDesde ; |Y fFecha [ fRefHasta;
          nTipo = 3;          || BONIFICACION
          |HAZ GrabaLinea;
     |FINSI;
|FPRC;

|PROCESO Edad;
     #nomcateg#0 = #labtraba#87;
     #nomcateg#1 = #labtraba#17;
     |LEE 000#nomcateg.=;
     |SI FSalida = 0;
          nNume = #nomcateg#6 / 1000000;
          fFecha = #labtraba#11;
          fFecha = fFecha + nNume;
          |SI fFecha ] fRefDesde ; |Y fFecha [ fRefHasta;
               nTipo = 2;          || EDAD
               |HAZ GrabaLinea;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Categoria;
     #nomcateg#0 = #labtraba#87;
     #nomcateg#1 = #labtraba#17;
     |LEE 000#nomcateg.=;
     |SI FSalida = 0; |Y #nomcateg#7 ! 0;
          nNume = #nomcateg#7 / 1000000;
          fFecha = #labtraba#253;
          fFecha = fFecha + nNume;
          |SI fFecha ] fRefDesde ; |Y fFecha [ fRefHasta;
               nTipo = 1;          || CATEGORIA
               |HAZ GrabaLinea;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO labtraba_2;
     || fFecha es la fecha de baja del trabajador

     aNuevaFecha = #labtraba#36;
     fNuevaFecha = aNuevaFecha;
     nNuevaFecha = fNuevaFecha;
     nFechaRef = fFecha;

     nNuevaFecha = nNuevaFecha - 1;
     fNuevaMenosUno = nNuevaFecha;
     |SI nFechaRef = nNuevaFecha;
     ||Y #labtraba#248 = 500; no solo ind, todos
          aAlfa1 = #labtraba#0; |QBF aAlfa1; aAlfa1 = ("00000" + aAlfa1) % -105;
          aAlfa2 = #labtraba#1; |QBF aAlfa2; aAlfa2 = ("00000" + aAlfa2) % -105;
          aNuevoCont = aAlfa1 + "." + aAlfa2;
     |FINSI;
|FPRC;

|DEFBUCLE labtraba_2;
     #labtraba#5;
     77;
     nEmp, aNif, #nomvenc2#17;
     nEmp, aNif, #nomvenc2#18;
     ;
     NULL, labtraba_2;
|FIN;

|PROCESO Comprueba;
     |DDEFECTO #nomvarca;
     #nomvarca#0 = #labtraba#0;    || primero miro si hay en el nomvarca
     #nomvarca#1 = #labtraba#1;    || baja por un motivo que no 08, 09, 10
     #nomvarca#2 = nMes;           || si es asi me quedo con la fecha
     #nomvarca#3 = 0;
     |LEE 000#nomvarca.=;
     |SI FSalida = 0; |Y #nomvarca#16 ! "          "; |Y #nomvarca#16 ! "..........";
          |SI #nomvarca#46 ! 08; |Y #nomvarca#46 ! 09; |Y #nomvarca#46 ! 10;
               aFecha = #nomvarca#16;
          |FINSI;
     |FINSI;

     |SI #nomvenc2#16 = "S";
          |ACABA;
     |FINSI;

     |PARA nTipo = 0; |SI nTipo [ 2; |HACIENDO nTipo = nTipo + 2;
          #nomcalca#0 = #labtraba#0;    || ademas esta calculada la nomina en
          #nomcalca#1 = #labtraba#1;    || el proceso mensual, confirmo que me
          #nomcalca#2 = nMes;           || quedo con esa fecha
          #nomcalca#3 = nTipo;
          |LEE 000#nomcalca.=;
          |SI FSalida = 0; |Y #nomcalca#5 ! "          "; |Y #nomcalca#5  ! "..........";
               |SI #nomvarca#46 ! 08; |Y #nomvarca#46 ! 09; |Y #nomvarca#46 ! 10;
                    aFecha = #nomcalca#5;
                    aMesBaja = aFecha % 402;
                    nMesBaja = aMesBaja;     || si tengo nomina calculada en el
                    |SI nMesBaja = nMes;     || mensual con fecha de baja y esta se
                         swNoSigas = 1;        || produce el mes calculado y es por algun
                    |FINSI;                  || motivo que no sea 08, 09, 10 entonces
               |FINSI;                       || NO lo incluyo
          |FINSI;
     |SIGUE;
|FPRC;

|PROCESO MiraFechaBaja;
     fFechaLis = ~;
     aFecha = fFechaLis;
     aMes = aFecha % 402;
     nMes = aMes;
     aMesBaja = "";
     nMesBaja = 0;

     aFecha = "";
     aFechaDesde = #nomvenc2#2;
     aFechaHasta = #nomvenc2#3;
     aMesDesde = aFechaDesde % 402;
     aMesHasta = aFechaHasta % 402;
     nMesDesde = aMesDesde;
     nMesHasta = aMesHasta;

     || aFecha = #labtraba#37;
     fFecha = #labtraba#37;

     |SI nMesDesde [ nMesHasta;
          nMes1 = nMesDesde;
          nMes2 = nMesHasta;
          |PARA nMes = nMes1; |SI nMes [ nMes2; |Y swNoSigas = 0; |HACIENDO nMes = nMes + 1;
               |HAZ Comprueba
          |SIGUE;
     |SINO;
          nMes1 = nMesDesde;
          nMes2 = 12;
          |PARA nMes = nMes1; |SI nMes [ nMes2; |Y swNoSigas = 0; |HACIENDO nMes = nMes + 1;
               |HAZ Comprueba
          |SIGUE;
          nMes1 = 1;
          nMes2 = nMesHasta;
          |PARA nMes = nMes1; |SI nMes [ nMes2; |HACIENDO nMes = nMes + 1;
               |HAZ Comprueba
          |SIGUE;
     |FINSI;

     |SI aFecha ! "";
          fFecha = aFecha;              || si la fecha de baja es menor a la
          |SI fFecha < fFechaLis;       || del listado (la del dia que se imprime)
               swNoSigas = 1;           || NO lo incluyo
          |FINSI;
     |FINSI;

     aAlfa = #labtraba#37;
     |QBF aAlfa;
     |SI aAlfa ! "";
          fFecha = aAlfa;
          |SI fFecha < #nomvenc2#2;     || baja antes de las fechas del listado
               swNoSigas = 1;
          |FINSI;
     |FINSI;

     || fFecha es la fecha de baja del trabajador

     || vamos a comprobar ahora si el trabajador esta de alta con otro NIF
     || con fecha de alta el dia siguente a la fecha de baja, si es asi NO
     || debe entrar

     |SALVA_FICHA #labtraba;
     aNif = #labtraba#7;
     fNuevaMenosUno = "";
     nEmp = #labtraba#0;
     |BUCLE labtraba_2;
     |REPON_FICHA #labtraba;
|FPRC;

|PROCESO labtraba;
     swNoSigas = 0;
     aNuevoCont = "";
     |HAZ MiraFechaBaja;

     #labempre#0 = #labtraba#0;
     |LEE 000#labempre.=;
     |SI #labempre#50 = "S";
          swNoSigas = 1;
     |FINSI;

     |DDEFECTO #labcentr;
     #labcentr#0 = #labtraba#0;
     #labcentr#1 = #labtraba#77;
     |LEE 000#labcentr.=;

     |SI swNoSigas = 1;
          |ACABA;
     |FINSI;

     |SI #nomvenc2#4 = "X";     |HAZ Categoria;          |FINSI;
     |SI #nomvenc2#5 = "X";     |HAZ Edad;               |FINSI;
     |SI #nomvenc2#6 = "X";     |HAZ Bonificacion;       |FINSI;
     |SI #nomvenc2#7 = "X";     |HAZ Permiso;            |FINSI;
     |SI #nomvenc2#8 = "X";     |HAZ AutCumple50;        |FINSI;
     |SI #nomvenc2#9 = "X";     |HAZ PagasMes;           |FINSI;
     |SI #nomvenc2#10 = "X";    |HAZ Contrato;           |FINSI;
     |SI #nomvenc2#11 = "X";    |HAZ Antiguedad;         |FINSI;
     |SI #nomvenc2#13 = "X";    |HAZ Antig_Trab;         |FINSI;
     |SI #nomvenc2#15 = "X";    |HAZ Bonif_Fomento;      |FINSI;
|FPRC;

|DEFBUCLE labtraba;
     #labtraba#1;
     77,44;
     nDEmpre, nDesdeTra, #nomvenc2#17, aDesdeEstado;
     nHEmpre, nHastaTra, #nomvenc2#18, aHastaEstado;
     ;
     NULL, labtraba;
|FIN;

|PROCESO Papel;
     aAlfa = Usuario; |QBT aAlfa; aAlfa = "tm_nomtmp03_" + aAlfa;
     |NOME_DAT #nomtmp03#aAlfa#;
     |ABRE #nomtmp03;  |CIERRA #nomtmp03;   |DELINDEX #nomtmp03;

     |ABRET #nomtmp03;

     |BUCLE labtraba;

     |LEE 000#nomtmp03.p;
     |SI FSalida ! 0;
         |MENAV "0000Selección vacía.";
         |ACABA;
     |FINSI;

     Impresora = "CrystalReport";
     |IMPRE -1;
     |SI FSalida ! 0;
         |ACABA;
     |FINSI;

     |INFOR "nomvenc2";

     |BUCLE Imprime;

     |PIEINF;
     |FININF;
     |FINIMP;

     |CIERRAT #nomtmp03;
     |DELINDEX #nomtmp03;
|FPRC;

|PROCESO Mensaje;
     aEmp = "                                             ";

     aAlfa   = ("00000" + #labempre#0) % -105;
     aNomEmp = #labempre#2;
     aNomEmp = aNomEmp % 126;
     aEmp = "Empresa: " + aAlfa + " - " + aNomEmp;
     aEmp = " " + aEmp + " ";

     |CUADRO 1217 1619;
     |ATRI R;
     |PINTA 1318, "       - Procesando archivo de vencimientos -       ";
     |PINTA 1418, "                                                    ";
     |PINTA 1518, "                                                    ";
     |PINTA 1518, aEmp;
     |ATRI 0;
|FPRC;

|PROCESO labempre;
     nDEmpre = #labempre#0;
     nHEmpre = #labempre#0;

     aAlfa = Usuario;    |QBF aAlfa;    aAlfa = ("c" + aAlfa) % 108;
     |NOME_DAT #nomtmp03#aAlfa#;
     |ABRE #nomtmp03;  |CIERRA #nomtmp03;   |DELINDEX #nomtmp03;

     |ABRET #nomtmp03;

     |BUCLE labtraba;

     |LEE 000#nomtmp03.p;
     |SI FSalida ! 0;
         |ACABA;
     |FINSI;

     |HAZ Mensaje;
     |HAZ IMPREArchi;

     |BUCLE Imprime;

     |PIEINF;
     |FININF;

     |SI swErrorDSArchi = 0;
         |HAZ Archivando;
     |FINSI;

     |CIERRAT #nomtmp03;
     |DELINDEX #nomtmp03;
|FPRC;

|DEFBUCLE labempre;
     #labempre#1;
     ;
     #nomvenc2#0;
     #nomvenc2#1;
     ;
     NULL, labempre;
|FIN;

|PROCESO Archivado;
     eaPeriodos = #nomvenc2#2 + " a " +  #nomvenc2#3;

     aAlfa = #nomvenc2#2 % 0402;  enDesdeMes = aAlfa;
     aAlfa = #nomvenc2#2 % -104;  enAny      = aAlfa;

     |BUCLE labempre;
|FPRC;

|PROCESO T3;  |TIPO 3;
     nDEmpre = #nomvenc2#0;
     nHEmpre = #nomvenc2#1;

     fRefDesde = #nomvenc2#2;
     fRefHasta = #nomvenc2#3;
     nDesdeTra = 00001;
     nHastaTra = 99999;

     |SI #nomvenc2#14 = "A"; |O #nomvenc2#14 = "B";
         aDesdeEstado = #nomvenc2#14;
         aHastaEstado = #nomvenc2#14;
     |SINO;
         aDesdeEstado = "A";
         aHastaEstado = "B";
     |FINSI;

     |ABRET #labempre;
     |ABRE #nomcateg;
     |ABRE #labcentr;
     |ABRE #nomconca;
     |ABRE #nomconax;
     |ABRE #nomantca;
     |ABRE #labregis;
     |ABRE #nomvarca;
     |ABRE #nomcalca;

     eaVengoDe = "nomvenc2";
     |HAZ MiraSiArchiva;
     |SI nArchi = 0;
         |HAZ Papel;

         |CIERRAT #labempre;
         |CIERRA #nomcalca;
         |CIERRA #nomvarca;
         |CIERRA #nomantca;
         |CIERRA #nomconax;
         |CIERRA #nomconca;
         |CIERRA #nomcateg;
         |CIERRA #labcentr;
         |CIERRA #labregis;

         |ACABA;
     |FINSI;

     |MENU aMenu;
     nOpcion = FSalida;
     |SI nOpcion = 1;  |HAZ Papel;      |FINSI;
     |SI nOpcion = 2;  |HAZ Archivado;  |FINSI;
     |SI nOpcion = 3;  |HAZ Papel;      |FINSI;
     |SI nOpcion = 3;  |HAZ Archivado;  |FINSI;

     |CIERRAT #labempre;
     |CIERRA #nomcalca;
     |CIERRA #nomvarca;
     |CIERRA #nomantca;
     |CIERRA #nomconax;
     |CIERRA #nomconca;
     |CIERRA #nomcateg;
     |CIERRA #labcentr;
     |CIERRA #labregis;

     |SI nOpcion ! 1;
         aAlfa = ":dsarchi/dpntz001;CLI";
         |SUB_EJECUTA aAlfa;
     |FINSI;
|FPRC;


|PROGRAMA;
     |CLS;
     |PINPA #0#nomvenc2;
     |ABRE #nomvenc2;
     |DDEFECTO #nomvenc2;
     |LEE 000#nomvenc2.p;
     |SI FSalida = 0;
          |BORRA 020#nomvenc2.a;
     |FINSI;
     |CIERRA #nomvenc2;
     |PINDA #0#nomvenc2;
     |ENDATOS #1#nomvenc2;

     |ABRE #nomvenc2; |GRABA 000#nomvenc2.n; |CIERRA #nomvenc2;
|FPRO;
