|FICHEROS;
     mod00266 #0;
     mod00265;

     mod00269;      || cabecera
     mod00267;      || lineas

     mod00244;

     labempre;
     labtraba;
|FIN;

|VARIABLES;
     fFechaDesde = @;
     fFechaHasta = @;

     nTopeMes = 0;
     nMes = 0;
     nAny = 0;
     nDia = 0;
     fFecha = @;
     nAntEmp = 0;
     nAntTra = 0;

     aAlfa = "";
     aAlfa1 = "";
     aAlfa2 = "";
     aAlfa3 = "";
     nNume = 0;
     swLlega = 0;
     nCalendario = 0;
     nMargen = 0;
     nMargen1 = 0;
     nMargen2 = 0;
     nMinutos = 0;
     nDescuadre = 0;

     ||Aleatorio
     rnd        = 0;
     r_semilla  = 0;
     r_vuelta   = 0;
     r_bucle    = 0;
     r_hora     = "";
     r_beta     = "";
     r_punto    = "";
     r_pi       = 3.141592;
     r_producto = 0.000000;
|FIN;

||-------------------- Proceso que genera aleatorios (variable 'rnd')-------
|| El numero aleatorio se devuelve en la variable 'rnd'
|| Ej: obtener un numero comprendido entre 1 y 100:
||  |HAZ Random;  rnd = rnd $  100; rnd = rnd + 1;  --->   1 ] rnd [ 100

|PROCESO Random;
     r_vuelta = r_vuelta + 1;
     |SI r_semilla [ 0; |O r_vuelta > 200;
          |HORA r_hora;
          r_beta = r_hora % 702;
          r_semilla = r_beta;
          r_vuelta = 0;
     |FINSI;
     r_producto = r_semilla / r_pi;
     r_beta = r_producto;
     |PARA r_bucle = 101;|SI r_bucle < 1000;|HACIENDO r_bucle = r_bucle + 100;
          r_punto = r_beta % r_bucle;
          |SI r_punto = "."; |SAL; |FINSI;
     |SIGUE;
     r_bucle = r_bucle + 104;
     r_beta = r_beta % r_bucle;
     r_semilla = r_beta;
     rnd = r_semilla;
|FPRC;

|PROCESO Defecto;
     |DDEFECTO #mod00269;
     #mod00269#0 = #mod00244#0;
     #mod00269#1 = #mod00244#1;
     #mod00269#2 = nAny;
     #mod00269#3 = nMes;
     |LEE 101#mod00269.=;
     |SI FSalida ! 0;
          |GRABA 020#mod00269.n;
     |FINSI;
     |LIBERA #mod00269;

     |PARA nDia = 1; |SI nDia [ nTopeMes; |HACIENDO nDia = nDia + 1;
          #mod00267#0 = #mod00244#0;
          #mod00267#1 = #mod00244#1;
          aAlfa1 = nDia; |QBF aAlfa1; aAlfa1 = ("00" + aAlfa1) % -102;
          aAlfa2 = nMes; |QBF aAlfa2; aAlfa2 = ("00" + aAlfa2) % -102;
          aAlfa3 = nAny;
          aAlfa = aAlfa1 + "." + aAlfa2 + "." + aAlfa3;
          fFecha = aAlfa;
          #mod00267#15 = nAny;
          #mod00267#16 = nMes;
          #mod00267#2 = fFecha;
          |LEE 101#mod00267.=;
          |SI FSalida = 0;
               |BORRA 020#mod00267.a;
          |FINSI;

          |DDEFECTO #mod00267;
          #mod00267#0 = #mod00244#0;
          #mod00267#1 = #mod00244#1;
          #mod00267#15 = nAny;
          #mod00267#16 = nMes;
          aAlfa1 = nDia; |QBF aAlfa1; aAlfa1 = ("00" + aAlfa1) % -102;
          aAlfa2 = nMes; |QBF aAlfa2; aAlfa2 = ("00" + aAlfa2) % -102;
          aAlfa3 = nAny;
          aAlfa = aAlfa1 + "." + aAlfa2 + "." + aAlfa3;
          fFecha = aAlfa;
          #mod00267#2 = fFecha;
          #mod00267#13 = #labempre#2;
          #mod00267#14 = #labtraba#2;
          |GRABA 020#mod00267.n;
          |LIBERA #mod00267;
     |SIGUE;
|FPRC;

|PROCESO Margenes;
          || primero hay que saber si llega antes o despues de la hora
          |HAZ Random;  rnd = rnd $ 2; rnd = rnd + 0;
          swLlega = rnd;

          || ahora cuantos minutos
          |HAZ Random;  rnd = rnd $ #mod00265#14; rnd = rnd + 0;
          nMinutos = rnd;
|FPRC;

|PROCESO mod00265;
     |SI #mod00265#3 [ nMes; |Y nMes [ #mod00265#5;
     |Y #mod00244#5 = #mod00265#15;
          nCalendario = #mod00265#0;
     |FINSI;
|FPRC;

|DEFBUCLE mod00265;
     #mod00265#1;
     ;
     01;
     99;
     ;
     NULL, mod00265;
|FIN;

|PROCESO mod00244;
     #labempre#0 = #mod00244#0;
     |LEE 000#labempre.=;

     #labtraba#0 = #mod00244#0;
     #labtraba#1 = #mod00244#1;
     |LEE 000#labtraba.=;

     |SI #0#4 = 2018; |Y #0#5 = 08;
          |SI #mod00244#7 = "I";
               || adelante
          |SINO;
               |SI #mod00244#5 [ 9; |Y #labtraba#142 = "S";
                    || adelante
               |SINO;
                    |ACABA;
               |FINSI;
          |FINSI;
     |FINSI;

     nCalendario = 0;
     |BUCLE mod00265;

     |SI #labtraba#142 = "N";
          || nada
     |SINO;
          |SI nAntEmp ! #mod00244#0; |O nAntTra ! #mod00244#1;
               || nuevo trabajador
               |HAZ Defecto;
          |FINSI;

          #mod00265#0 = nCalendario;
          |LEE 000#mod00265.=;

          nDescuadre = 0;

          || hora de entrada ma¤ana

          || swLlega = 0 -> llega antes
          || swLlega = 1 -> llega tarde

          |HAZ Margenes;
          swLlega = 0; || llega antes siempre
          |SI swLlega = 0;
               #mod00265#7 = #mod00265#7 - nMinutos;
               |SI #mod00265#7 < 0;
                    #mod00265#7 = 60 - nMinutos;
                    #mod00265#6 = #mod00265#6 - 1;
               |FINSI;
               nDescuadre = nDescuadre + nMinutos;
          |SINO;
               #mod00265#7 = #mod00265#7 + nMinutos;
               |SI #mod00265#7 ] 60;
                    #mod00265#7 = nMinutos - 60;
                    #mod00265#6 = #mod00265#6 + 1;
               |FINSI;
               nDescuadre = nDescuadre - nMinutos;
          |FINSI;

          || hora de salida ma¤ana

          |HAZ Margenes;

          || swLlega = 0 -> sale antes
          || swLlega = 1 -> sale tarde

          swLlega = 1;   ||  sale tarde siempre
          |SI swLlega = 0;
               #mod00265#9 = #mod00265#9 - nMinutos;
               |SI #mod00265#9 < 0;
                    #mod00265#9 = 60 - nMinutos;
                    #mod00265#8 = #mod00265#8 - 1;
               |FINSI;
               nDescuadre = nDescuadre - nMinutos;
          |SINO;
               #mod00265#9 = #mod00265#9 + nMinutos;
               |SI #mod00265#9 ] 60;
                    #mod00265#9 =  nMinutos - 60;
                    #mod00265#8 = #mod00265#8 + 1;
               |FINSI;
               nDescuadre = nDescuadre + nMinutos;
          |FINSI;

          || LO MISMO PERO PARA LA TARDE

          |HAZ Margenes;

          || hora de entrada tarde

          swLlega = 0;    || llega antes
          |SI swLlega = 0;
               #mod00265#11 = #mod00265#11 - nMinutos;
               |SI #mod00265#11 < 0;
                    #mod00265#11 = 60 - nMinutos;
                    #mod00265#10 = #mod00265#10 - 1;
               |FINSI;
               nDescuadre = nDescuadre + nMinutos;
          |SINO;
               #mod00265#11 = #mod00265#11 + nMinutos;
               |SI #mod00265#11 > 60;
                    #mod00265#11 = nMinutos - 60;
                    #mod00265#10 = #mod00265#10 + 1;
               |FINSI;
               nDescuadre = nDescuadre - nMinutos;
          |FINSI;

          || hora de salida tarde

          |HAZ Margenes;

          swLlega = 1; ||  sale tarde siempre
          |SI swLlega = 1;
               #mod00265#13 = #mod00265#13 + nMinutos;
               |SI #mod00265#13 ] 60;
                    #mod00265#13 =  nMinutos - 60;
                    #mod00265#12 = #mod00265#12 + 1;
               |FINSI;
               nDescuadre = nDescuadre + nMinutos;
          |FINSI;
/*
          #mod00265#13 = #mod00265#13 - nDescuadre;
          |SI #mod00265#13 < 0;
               #mod00265#13 = 60 - nDescuadre;
               #mod00265#12 = #mod00265#12 - 1;
          |FINSI;
          |SI #mod00265#13 > 60;
               #mod00265#13 = nDescuadre - 60;
               #mod00265#12 = #mod00265#12 + 1;
          |FINSI;
*/
          #mod00267#0 = #mod00244#0;
          #mod00267#1 = #mod00244#1;
          #mod00267#2 = #mod00244#2;
          |LEE 000#mod00267.=;

          || tiene que existir

          #mod00267#3 = #mod00244#5;

          #mod00267#4 = #mod00265#6;
          #mod00267#5 = #mod00265#7;
          #mod00267#6 = #mod00265#8;
          #mod00267#7 = #mod00265#9;
          #mod00267#8 = #mod00265#10;
          #mod00267#9 = #mod00265#11;
          #mod00267#10 = #mod00265#12;
          #mod00267#11 = #mod00265#13;

          |GRABA 020#mod00267.a;
          |LIBERA #mod00267;

          nAntEmp = #mod00244#0;
          nAntTra = #mod00244#1;
     |FINSI;
|FPRC;

|DEFBUCLE mod00244;
     #mod00244#1;
     7;
     #0#0, #0#2, fFechaDesde, " ", "I";
     #0#1, #0#3, fFechaHasta, "z", "I"
     ;
     NULL, mod00244;
|FIN;

|DEFBUCLE mod00244_agosto18;
     #mod00244#1;
     7;
     #0#0, #0#2, fFechaDesde, " ", " ";
     #0#1, #0#3, fFechaHasta, "z", "z"
     ;
     NULL, mod00244;
|FIN;

|PROCESO topemes;
     nTopeMes = 31;
     |SI nMes = 4; |O nMes = 6; |O nMes = 9; |O nMes = 11;
          nTopeMes = 30;
     |FINSI;
     |SI nMes = 2;
          nTopeMes = 28;
          nNume = nAny $ 4;
          |SI nNume = 0;
               nTopeMes = 29;
          |FINSI;
     |FINSI;
|FPRC;

|PROGRAMA;
     |CLS;
     |CABEZA "Informe Horas Trabajadas";
     |PINPA #0#0;
     |ABRE #0;
     |LEE 001#0p;
     |SI FSalida ! 0;
          |DDEFECTO #0;
          |GRABA 020#0n;
     |FINSI;
     |LEE 101#0=;
     |PINDA #0#0;

     |ENDATOS #1#0;
     |SI FSalida = 0;
          |ABRE #labempre;
          |ABRE #labtraba;
          |ABRE #mod00265;

          |ABRE #mod00269;
          |ABRE #mod00267;

          nAny = #0#4;
          nMes = #0#5;
          |HAZ topemes;

          aAlfa1 = "01";
          aAlfa2 = nMes; |QBF aAlfa2; aAlfa2 = ("00" + aAlfa2) % -102;
          aAlfa3 = nAny;
          aAlfa = aAlfa1 + "." + aAlfa2 + "." + aAlfa3;
          fFechaDesde = aAlfa;

          aAlfa1 = nTopeMes;
          aAlfa = aAlfa1 + "." + aAlfa2 + "." + aAlfa3;
          fFechaHasta = aAlfa;

          |SI #0#4 = 2018; |Y #0#5 = 08;
               |BUCLE mod00244_agosto18;
          |SINO;
               |BUCLE mod00244;
          |FINSI;

          |CIERRA #mod00267
          |CIERRA #mod00269
          |CIERRA #mod00265;
          |CIERRA #labtraba;
          |CIERRA #labempre;
     |FINSI;

     |GRABA 020#0a;

     |LIBERA #0;
     |CIERRA #0;
|FPRO;
