|FICHEROS;
     laborn02;            || este def el del lineal
     nomvarca;
     nomvarli;
     nomvarl1;
     nomcontr;

     labor001;           || empresas labornet
     labtraba;
     nompmail;
     labornx2;
     laborn22,MANTE;
     nomhicca;
     motibaja;

     gomdetjr;
     mod00262;
     nomcont1;
     taiclaoc;
|FIN;

|VARIABLES;
     &eaDirDest  = "";
     &eaAsunto   = "";
     &eaTexto    = "";
     &eaFichero  = "";

     &aParam = "";
     &eaUsuario = "";
     &eaFecha = "";
     &efFecha = @;
     &eaHora = "";
     aTipo = "";
     sw = 0;
     aAlfa  = "";
     aAlfa1 = "";
     aAlfa2 = "";
     FEstado = 0;
     nMes = 0;
     nAny = 0;
     aUsuarioD = "";
     aUsuarioH = "";
     &nEmpExt = 0;
     &nTraExt = 0;
     &eaVengoDe = "laborn02";
     &eaEmpresa = "";
     &nEmpAUTO = 0;
     &nTraAUTO = 0;
     &nMesAUTO = 0;
     &nAnyAUTO = 0;
     &nTipAUTO = 0;
     &aFechaBaja = "";
     &fFechaBaja = @;
     &nCausaBaja = 0;
     &aCausaBaja = "";
     &aTipoContr = "";
     nNume = 0;
     aSiEsX = "";

     &eaParam       = "";
     &enFSalida     = 0;
     &enBorrado     = 0;
     &eaEstado      = "";
     &eaMotiv       = "";
     &enActualizado = 0;

     &enEmpresa = 0;
     &enTrabajador = 0;
     &enDiasVac = 0;
     &nLabornet = 0;
     nCampo = 0;
     nCampo1 = 0;
     swSigue = 0;
     &nCon1_Labornet = 0;
     &nCon2_Labornet = 0;
     &nCon3_Labornet = 0;
     &nVal1_Labornet = 0;
     &nVal2_Labornet = 0;
     &nVal3_Labornet = 0;
     &nPreavisoAbo = 0;
     &nPreavisoDto = 0;
     nDiasInd = 0;
|FIN;

|PROCESO labtraba;
     |ABRE #labtraba;
     #labtraba#0 = #laborn02#5;
     #labtraba#1 = #laborn02#6;
     |LEE 000#labtraba.=;
     |SI FSalida = 0;
          #labtraba#37 = #laborn02#8;
          #labtraba#184 = #laborn02#9;
          aAlfa = #laborn02#30;
          |QBF aAlfa;
          |SI aAlfa ! "";
               #labtraba#49 = #laborn02#30;
          |FINSI;
          aAlfa = #laborn02#38;
          |QBF aAlfa;
          |SI aAlfa ! "";
               #labtraba#185 = aAlfa;

               |ABRE #taiclaoc;
               aAlfa = (aAlfa + "          ") % 110;
               #taiclaoc#0 = aAlfa;
               |LEE 000#taiclaoc.=;
               |SI FSalida = 0;
                    #labtraba#19 = #taiclaoc#1;
               |FINSI;
               |CIERRA #taiclaoc;
          |FINSI;

          |GRABA 020#labtraba.a;
          |LIBERA #labtraba;
     |FINSI;
     |CIERRA #labtraba;
|FPRC;

|PROCESO nomvarli_nomina;
     aAlfa = #laborn02#8;
     aAlfa = aAlfa % 402;
     nMes = aAlfa;

     |ABRE #nomcontr;
     |LEE 000#nomcontr.p;
     |CIERRA #nomcontr;

     |ABRE #nomvarli;

     |SI #laborn02#13 < 0;    || para que las vacaciones negativas entren
                              || en la nomina, ya que se han de calcular ahi
          #nomvarli#0 = #laborn02#5;
          #nomvarli#1 = #laborn02#6;
          #nomvarli#2 = nMes;
          #nomvarli#3 = 0;
          #nomvarli#4 = 125;
          |LEE 000#nomvarli.=;
          FEstado = FSalida;

          aAlfa = #laborn02#13;
          aAlfa = ("          " + aAlfa) % -110;
          #nomvarli#5 = aAlfa;
          |SI FEstado ! 0;
               |GRABA 020#nomvarli.n;
          |SINO;
               |GRABA 020#nomvarli.a;
          |FINSI;
          |LIBERA #nomvarli;
     |FINSI;

     || dias preaviso abono y descuento
     nNume = #laborn02#28 - #laborn02#29;
     |SI nNume ! 0;
          #nomvarli#0 = #laborn02#5;
          #nomvarli#1 = #laborn02#6;
          #nomvarli#2 = nMes;
          #nomvarli#3 = 0;
          #nomvarli#4 = 965;
          |LEE 000#nomvarli.=;
          FEstado = FSalida;

          aAlfa = nNume;
          |QBF aAlfa;
          aAlfa = ("          " + aAlfa) % -110;
          #nomvarli#5 = aAlfa;
          #nomvarli#6 = "";
          |SI FEstado ! 0;
               |GRABA 020#nomvarli.n;
          |SINO;
               |GRABA 020#nomvarli.a;
          |FINSI;
          |LIBERA #nomvarli;
     |FINSI;

     |CIERRA #nomvarli;
|FPRC;

|PROCESO BorraNomvarli;
     |BORRA 020#nomvarli.a;
     |LIBERA #nomvarli;
|FPRC;

|DEFBUCLE BorraNomvarli;
     #nomvarli#1;
     ;
     #nomvarca#0, #nomvarca#1, #nomvarca#2, #nomvarca#3, 101;
     #nomvarca#0, #nomvarca#1, #nomvarca#2, #nomvarca#3, 999;
     be;
     NULL, BorraNomvarli;
|FIN;

|PROCESO Linea902;
     || creacion automatica de la linea 902 en variaciones
     nDiasInd = 0;
     |SI #nomvarca#46 ! 0; |Y #nomcontr#13 = "A";
          |ABRE #motibaja;
          #motibaja#0 = #nomvarca#46;
          |LEE 000#motibaja.=;
          |SI FSalida = 0;
               nDiasInd = #motibaja#2;
          |FINSI;

          |SI nDiasInd ! 0;
               #nomvarli#0 = #laborn02#5;
               #nomvarli#1 = #laborn02#6;
               #nomvarli#2 = nMes;
               #nomvarli#3 = 0;
               #nomvarli#4 = 902;
               |LEE 101#nomvarli.=;
               |SI FSalida = 0;
                    || si ya existia borro el 902 que hubiera
                    |BORRA 020#nomvarli.a;
               |FINSI;
               aAlfa = nDiasInd; |QBF aAlfa;
               |QBF aAlfa;
               aAlfa = ("          " + aAlfa) % -110;

               #nomvarli#5 = aAlfa;
               #nomvarli#6 = "Indemniz.A¤o";
               |GRABA 020#nomvarli.n;
               |LIBERA #nomvarli;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO nomvarca;
     aAlfa = #laborn02#8;
     aAlfa = aAlfa % 402;
     nMes = aAlfa;

     |ABRE #nomvarca;
     #nomvarca#0 = #laborn02#5;
     #nomvarca#1 = #laborn02#6;
     #nomvarca#2 = nMes;
     #nomvarca#3 = 0;
     |LEE 101#nomvarca.=;
     |SI FSalida = 0;
          |BUCLE BorraNomvarli;
          |BORRA 020#nomvarca.a;
     |FINSI;

     #nomvarca#0 = #laborn02#5;
     #nomvarca#1 = #laborn02#6;
     #nomvarca#2 = nMes;
     #nomvarca#3 = 0;

     #nomvarca#16 = #laborn02#8;
     aAlfa = #laborn02#9;
     aAlfa = aAlfa % 102;
     #nomvarca#46 = aAlfa;

     |GRABA 020#nomvarca.n;

     |LIBERA #nomvarca;
     |CIERRA #nomvarca;
|FPRC;

|PROCESO jornadas;
     |SI #mod00262#5 ! 0;
          nCampo = #mod00262#4;
          nCampo = nCampo + 3;
          #gomdetjr#nCampo# = "X";
     |FINSI;
|FPRC;

|DEFBUCLE jornadas;
     #mod00262#1;
     ;
     #laborn02#5, #laborn02#6, nAny, nMes, 01;
     #laborn02#5, #laborn02#6, nAny, nMes, 31;
     ;
     NULL, jornadas;
|FIN;

|PROCESO calendario;
     |ABRE #gomdetjr;

     |DDEFECTO #gomdetjr;
     #gomdetjr#0 = #laborn02#5;
     #gomdetjr#1 = #laborn02#6;
     #gomdetjr#2 = nAny;
     #gomdetjr#3 = nMes;
     |LEE 101#gomdetjr.=;
     FEstado = FSalida;
     |SI FSalida = 0;
          |PARA nCampo = 4; |SI nCampo [ 34; |HACIENDO nCampo = nCampo + 1;
               #gomdetjr#nCampo# = "";
          |SIGUE;
     |FINSI;

     |BUCLE jornadas;

     |SI FEstado = 0;
          |GRABA 020#gomdetjr.a;
     |SINO;
          |GRABA 020#gomdetjr.n;
     |FINSI;
     |LIBERA #gomdetjr;

     |CIERRA #gomdetjr;
|FPRC;

|PROCESO nomvarli;
     aAlfa = #laborn02#8;
     aAlfa = aAlfa % 402;
     nMes = aAlfa;

     |ABRE #nomcontr;
     |LEE 000#nomcontr.p;
     |CIERRA #nomcontr;

     |ABRE #nomvarli;

     |SI #laborn02#13 ] 0;
          #nomvarli#0 = #laborn02#5;
          #nomvarli#1 = #laborn02#6;
          #nomvarli#2 = nMes;
          #nomvarli#3 = 0;
          #nomvarli#4 = 125;
          |LEE 000#nomvarli.=;
          FEstado = FSalida;

          aAlfa = #laborn02#13;
          aAlfa = ("          " + aAlfa) % -110;
          #nomvarli#5 = aAlfa;
          |SI FEstado ! 0;
               |GRABA 020#nomvarli.n;
          |SINO;
               |GRABA 020#nomvarli.a;
          |FINSI;
          |LIBERA #nomvarli;
     |FINSI;

     aAlfa = eaUsuario; |QBF aAlfa;
     aAlfa1 = eaUsuario % 105;
     swSigue = 0;
     |SI #laborn02#14 ! 0;
          |SI aAlfa ! "WEB"; |Y aAlfa1 ! "NUBEA";
               swSigue = 1;
          |FINSI;
     |FINSI;
     |SI swSigue = 1;
          || para los que vengan de laborbox, se guarda en el nomvarl1
          || si no, la indemnizacion sale en la nomina del mes y en el
          || finiquito
          #nomvarli#0 = #laborn02#5;
          #nomvarli#1 = #laborn02#6;
          #nomvarli#2 = nMes;
          #nomvarli#3 = 0;

          aAlfa = #laborn02#9;
          |QBF aAlfa;
          aAlfa = aAlfa % 102;
          nNume = aAlfa;
          |ABRE #motibaja;
          #motibaja#0 = nNume;
          |LEE 000#motibaja.=;
          |SI FSalida = 0;
               aAlfa = #motibaja#4;
          |SINO;
               aAlfa = "S";
          |FINSI;
          |CIERRA #motibaja;
          |SI aAlfa = "S";
               #nomvarli#4 = 410;
          |SINO;
               #nomvarli#4 = 415;
          |FINSI;
          |LEE 000#nomvarli.=;
          FEstado = FSalida;

          #nomvarli#5 = "         1";
          aAlfa = #laborn02#14;
          aAlfa = ("            " + aAlfa) % -112;
          #nomvarli#6 = aAlfa;
          |SI FEstado ! 0;
               |GRABA 020#nomvarli.n;
          |SINO;
               |GRABA 020#nomvarli.a;
          |FINSI;
          |LIBERA #nomvarli;
     |FINSI;

     |SI #laborn02#17 ! 0;
          #nomvarli#0 = #laborn02#5;
          #nomvarli#1 = #laborn02#6;
          #nomvarli#2 = nMes;
          #nomvarli#3 = 0;
          #nomvarli#4 = 903;
          |LEE 000#nomvarli.=;
          FEstado = FSalida;

          aAlfa = #laborn02#17;
          aAlfa = ("          " + aAlfa) % -110;
          #nomvarli#5 = aAlfa;
          #nomvarli#6 = "Jornadas";
          |SI FEstado ! 0;
               |GRABA 020#nomvarli.n;
          |SINO;
               |GRABA 020#nomvarli.a;
          |FINSI;
          |LIBERA #nomvarli;

          |ABRE #nomcont1; |LEE 000#nomcont1.=; |CIERRA #nomcont1;
          |SI #nomcont1#9 = 006554;
               #nomvarli#0 = #laborn02#5;
               #nomvarli#1 = #laborn02#6;
               #nomvarli#2 = nMes;
               #nomvarli#3 = 0;
               #nomvarli#4 = 101;
               |LEE 000#nomvarli.=;
               FEstado = FSalida;

               aAlfa = #laborn02#17;
               aAlfa = ("          " + aAlfa) % -110;
               #nomvarli#5 = aAlfa;
               #nomvarli#6 = "";
               |SI FEstado ! 0;
                    |GRABA 020#nomvarli.n;
               |SINO;
                    |GRABA 020#nomvarli.a;
               |FINSI;
               |LIBERA #nomvarli;
          |FINSI;
     |FINSI;

     |SI #laborn02#18 ! 0;
          #nomvarli#0 = #laborn02#5;
          #nomvarli#1 = #laborn02#6;
          #nomvarli#2 = nMes;
          #nomvarli#3 = 0;
          #nomvarli#4 = 922;
          |LEE 000#nomvarli.=;
          FEstado = FSalida;

          aAlfa = #laborn02#18;
          aAlfa = ("          " + aAlfa) % -110;
          #nomvarli#5 = aAlfa;
          #nomvarli#6 = "Jornadas IT";
          |SI FEstado ! 0;
               |GRABA 020#nomvarli.n;
          |SINO;
               |GRABA 020#nomvarli.a;
          |FINSI;
          |LIBERA #nomvarli;
     |FINSI;

     || calendario de jornadas si las jornadas me vienen del modulo

     |ABRE #nomcont1; |LEE 000#nomcont1.=; |CIERRA #nomcont1;
     |SI #nomcont1#9 = 006554;
          |SI #laborn02#17 ! 0;
               aAlfa = #laborn02#8; aAlfa = aAlfa % 402; nMes = aAlfa;
               aAlfa = #laborn02#8; aAlfa = aAlfa % -104; nAny = aAlfa;

               |HAZ calendario;
          |FINSI;
     |FINSI;

     || Otros conceptos

     |PARA nCampo = 32; |SI nCampo [ 37; |HACIENDO nCampo = nCampo + 2;
          nCampo1 = nCampo + 1;
          |SI #laborn02#nCampo# ! 0; |Y #laborn02#nCampo1# ! 0;
               #nomvarli#0 = #laborn02#5;
               #nomvarli#1 = #laborn02#6;
               #nomvarli#2 = nMes;
               #nomvarli#3 = 0;
               #nomvarli#4 = #laborn02#nCampo#;
               |LEE 000#nomvarli.=;
               FEstado = FSalida;

               #nomvarli#5 = "         1";
               aAlfa = #laborn02#nCampo1#;
               aAlfa = ("            " + aAlfa) % -112;
               #nomvarli#6 = aAlfa;

               |SI FEstado ! 0;
                    |GRABA 020#nomvarli.n;
               |SINO;
                    |GRABA 020#nomvarli.a;
               |FINSI;
               |LIBERA #nomvarli;

               |SI nCampo = 32;
                    nCon1_Labornet = #laborn02#nCampo#;
                    nVal1_Labornet = #laborn02#nCampo1#;
               |FINSI;
               |SI nCampo = 34;
                    nCon2_Labornet = #laborn02#nCampo#;
                    nVal2_Labornet = #laborn02#nCampo1#;
               |FINSI;
               |SI nCampo = 36;
                    nCon3_Labornet = #laborn02#nCampo#;
                    nVal3_Labornet = #laborn02#nCampo1#;
               |FINSI;
          |FINSI;
     |SIGUE;

     |HAZ Linea902;

     |CIERRA #nomvarli;
|FPRC;

|PROCESO nomvarl1;
     aAlfa = #laborn02#8;
     aAlfa = aAlfa % 402;
     nMes = aAlfa;

     |ABRE #nomcontr;
     |LEE 000#nomcontr.p;
     |CIERRA #nomcontr;

     |ABRE #nomvarl1;

     |SI #laborn02#14 ! 0;
          #nomvarl1#0 = #laborn02#5;
          #nomvarl1#1 = #laborn02#6;
          #nomvarl1#2 = nMes;
          #nomvarl1#3 = 2;
          |SI #nomcontr#13 = "S";
               #nomvarl1#4 = 410;
          |SINO;
               #nomvarl1#4 = 415;
          |FINSI;
          |LEE 000#nomvarl1.=;
          FEstado = FSalida;

          #nomvarl1#5 = "         1";
          aAlfa = #laborn02#14;
          aAlfa = ("            " + aAlfa) % -112;
          #nomvarl1#6 = aAlfa;
          |SI FEstado ! 0;
               |GRABA 020#nomvarl1.n;
          |SINO;
               |GRABA 020#nomvarl1.a;
          |FINSI;
          |LIBERA #nomvarl1;
     |FINSI;
     |CIERRA #nomvarl1;
|FPRC;

|PROCESO Email;
     || Emite correo electronico al usuario de que la baja del trabajador
     ||    se ha hecho efectiva
     aAlfa = ("00000" + #laborn02#6) % -105;
     eaTexto = &13 + &10 + "El administrador ha hecho efectiva la baja del trabajador (" + aAlfa + ") " + #laborn02#16; |QBF eaTexto;
     aAlfa = ("00000" + #laborn02#5) % -106;
     eaTexto = eaTexto + " de la empresa (" + aAlfa + ") " + #laborn02#15; |QBF eaTexto;

     eaTexto = eaTexto + &13 + &10 + &13 + &10;
     eaTexto = eaTexto + " Actualizacion con fecha " + #laborn02#3 + " a las " + #laborn02#4 + &13 + &10;

     aAlfa  = ("00000" + #laborn02#5) % -106;
     aAlfa1 = ("00000" + #laborn02#6) % -105;
     eaAsunto  = "Efectivo baja trabajador (" + aAlfa + "/" + aAlfa1 + ") " + #laborn02#16; |QBF eaAsunto;
     eaFichero = "";
     eaDirDest = "";

     aAlfa = #laborn02#5; |QBF aAlfa;
     eaEmpresa = ("00000" + aAlfa) % -105;
     |HAZ ConsEmail2;

/*
     |DBASS aAlfa; |QBF aAlfa;
     aAlfa = aAlfa % -109;
     |SI aAlfa = "dsprofes/";       || estoy en produccion, sin duda
          aAlfa = "/u/dsmedida/gecochek/def/gecom014.mas";
     |SINO;
          |DBASS aAlfa; |QBF aAlfa;
          aAlfa = aAlfa + "gecochek/def/gecom014.mas";
     |FINSI;

     |CARGA_DEF aAlfa, gecom014@;
     |SI FSalida = 0;
          |DBASS aAlfa; |QBF aAlfa;
          aAlfa = aAlfa % -109;
          |SI aAlfa = "dsprofes/";       || estoy en produccion, sin duda
               aAlfa = "/u/dsmedida/gecochek/fich/";
          |SINO;
               |DBASS aAlfa; |QBF aAlfa;
               aAlfa = aAlfa + "/gecochek/fich/";
          |FINSI;

          |PATH_DAT #gecom014@#aAlfa#;
          |ABRE #gecom014@; |SELECT #2#gecom014@;
          #gecom014@#1 = #laborn02#1;
          |LEE 000#gecom014@.=;
          |SI FSalida  = 0;
               eaDirDest = #gecom014@#6;
          |FINSI;
          |SELECT #1#gecom014@; |CIERRA #gecom014@; |DESCARGA_DEF #gecom014@;
     |FINSI;

     |HAZ SendMail;
*/
|FPRC;

|PROCESO Parametros;
     |PUSHV 10241756;
     |ABRE #laborn22;
     |LEE 000#laborn22.p;
     |SI #labtraba#247 = 613; |O #labtraba#247 = 163;
          #laborn22#2 = "N";
     |FINSI;
     #laborn22#5 = "N";       || me cargo el TA2 y el mov. AFI de baja
     |SI FSalida ! 0;
          |GRABA 000#laborn22.n;
     |FINSI;
     |PINPA #0#laborn22;
     |PINDA #0#laborn22;
     |ENDATOS #1#laborn22;
     |SI FSalida = 0;
          |GRABA 000#laborn22.a;
     |FINSI;
     |CIERRA #laborn22;
     |POPV;
|FPRC;

|PROCESO Actualiza;
     swSigue = 0;
     nLabornet = 1;

     aAlfa = "    ATENCION: se va a iniciar el proceso de actualizacion";
     aAlfa = aAlfa + " de los datos del trabajador solicitado";
     |MENAV aAlfa;
     aAlfa = "    S¿Desea continuar?";
     |CONFI aAlfa;
     |SI FSalida = 0;
          swSigue = 1;
     |FINSI;
     |SI swSigue = 1;
          |ABRE #nomhicca;
          #nomhicca#0 = #laborn02#5;
          #nomhicca#1 = #laborn02#6;
          aAlfa = #laborn02#8; aAlfa = aAlfa % 402; nMes = aAlfa;
          aAlfa = #laborn02#8; aAlfa = aAlfa % -104; nAny = aAlfa;
          #nomhicca#66 = nAny - 2000;
          #nomhicca#2 = nMes;
          #nomhicca#3 = 0;
          |LEE 000#nomhicca.=;
          |SI FSalida = 0;
               swSigue = 0;
               aAlfa = "    ATENCION. La nómina del trabajador ya está ";
               aAlfa = aAlfa + "calculada y actualizada al Histórico. ";
               |MENAV aAlfa;

               aAlfa = "    No es posible realizar el proceso. ";
               aAlfa = aAlfa + "Compruebe los datos del trabajador.";
               |MENAV aAlfa;
          |SINO;
               swSigue = 1;
          |FINSI;
          |CIERRA #nomhicca;
     |FINSI;
     |SI swSigue = 1;
          |ABRE #labtraba;
          #labtraba#0 = #laborn02#5;
          #labtraba#1 = #laborn02#6;
          |LEE 000#labtraba.=;
          |CIERRA #labtraba;

          |HAZ Parametros;
          |SI #laborn22#4 = "S";
               swSigue = 1;
          |SINO;
               swSigue = 0;
          |FINSI;
     |FINSI;

     |SI swSigue = 1;
          |HAZ nomvarca;      || actualizo fecha baja en variaciones

          |HAZ labtraba;      || actualizo fecha baja en trabajador

          |SI #labtraba#48 = "N"; |O #labtraba#247 = 613; |O #labtraba#247 = 163;
                                   || sin finiquito
               |HAZ nomvarli;      || actualizo variaciones antes de
          |FINSI;                       || calcular nomina

          |HAZ nomvarli_nomina;    || en principio para 965
                                   || tambien para 125 negativo

          || ahora a calcular la nomina
          |SI #laborn22#1 = "S";
               nEmpAUTO = #laborn02#5;
               nTraAUTO = #laborn02#6;
               aAlfa = #laborn02#8; aAlfa = aAlfa % 402;
               nMesAUTO = aAlfa;
               aAlfa = #laborn02#8; aAlfa = aAlfa % -104;
               nAnyAUTO = aAlfa;

               |MENSA "    Calculando nomina ... ";
               |SI #labtraba#247 ! 613; |Y #labtraba#247 ! 163;
                    |SUB_EJECUTA "nomcalcu;NOMSICOSR";
               |SINO;
                    |SUB_EJECUTA "gomcalcu;NOMSICOSR";
               |FINSI;

               |MENSA "    Generando recibo ... ";
               nTipAUTO = 0;
               |SUB_EJECUTA "nomrecib;AUTO";
          |FINSI;

          || ahora el finiquito

          |SI #labtraba#48 = "S";       || con finiquito
               |HAZ nomvarli;      || actualizo variaciones despues de
               aAlfa = eaUsuario; |QBF aAlfa;
               aAlfa1 = eaUsuario % 105;
               |SI aAlfa = "WEB"; |O aAlfa1 = "NUBEA";
                    |HAZ nomvarl1;      || finiquito
               |FINSI;

               |SI #laborn22#2 = "S";
                    |MENSA "    Preparando finiquito ...";

                    aParam = "";
                    aAlfa = #laborn02#5; aAlfa = ("00000" + aAlfa) % -105;
                    aParam = aAlfa;
                    aAlfa = #laborn02#6; aAlfa = ("00000" + aAlfa) % -105;
                    aParam = aParam + aAlfa;
                    aAlfa = #laborn02#8; aAlfa = aAlfa % 402;
                    aParam = aParam + aAlfa;
                    aAlfa = #laborn02#8; aAlfa = aAlfa % -104;
                    aParam = aParam + aAlfa;
                    aParam = aParam + "CPAN";     || para que me muestre la pantalla
                                                  || de Datos

                    aParam = "nomcalfi;" + aParam;
                    |SUB_EJECUTA aParam;
                    nEmpAUTO = #laborn02#5;
                    nTraAUTO = #laborn02#6;
                    aAlfa = #laborn02#8; aAlfa = aAlfa % 402;
                    nMesAUTO = aAlfa;
                    aAlfa = #laborn02#8; aAlfa = aAlfa % -104;
                    nAnyAUTO = aAlfa;
                    nTipAUTO = 2;
                    |SUB_EJECUTA "nomrecib;AUTO";
               |FINSI;
          |FINSI;

          |SI #laborn22#5 = "S";
               || Emision TA2
               nEmpExt = #laborn02#5;
               nTraExt = #laborn02#6;
               fFechaBaja = #laborn02#8;
               aTipoContr = #labtraba#45;
               aCausaBaja = #laborn02#9;
               |QBF aCausaBaja;
               aAlfa = aCausaBaja % 102;
               nNume = aAlfa;
               |SI nNume > 08; |Y nNume < 99;
                    nCausaBaja = nNume;
                    aAlfa = aCausaBaja % 105;
                    aCausaBaja = aCausaBaja - aAlfa;
                    |QBF aCausaBaja;
               |FINSI;
               |SI #labtraba#247 ! 613; |Y #labtraba#247 ! 163;
                    |SUB_EJECUTA "labparta;BAJA";
               |SINO;
                    |SUB_EJECUTA "labta611;BAJA";
               |FINSI;
          |FINSI;

          |SI #laborn22#3 = "S";
               |SI #labtraba#247 ! 613; |Y #labtraba#247 ! 163;
                    aParam = "labcerti;labornet";
               |SINO;
                    aParam = "labcerag;labornet";
               |FINSI;

               aAlfa = #laborn02#5; aAlfa = ("00000" + aAlfa) % -105;
               aParam = aParam + aAlfa;
               aAlfa = #laborn02#6; aAlfa = ("00000" + aAlfa) % -105;
               aParam = aParam + aAlfa;
               aAlfa = #laborn02#8;
               aFechaBaja = aAlfa;
               |SUB_EJECUTA aParam;
          |FINSI;

          |SI #laborn22#6 = "S";
               aAlfa1 = #labtraba#0;  aAlfa1 = "00000" + aAlfa1; aAlfa1 = aAlfa1 % -105;
               aAlfa2 = #labtraba#1;  aAlfa2 = "00000" + aAlfa2; aAlfa2 = aAlfa2 % -105;
               aAlfa = aAlfa1 + aAlfa2;
               enDiasVac = #laborn02#13;
               aAlfa = "labnct07;"+ "02" + aAlfa;
               |SUB_EJECUTA aAlfa;
          |FINSI;

          || primero me paso todos los campos del registro que voy a borrar
          || a un temporal, ya que ha habido varios fallos en los que al
          || borrar el registro pierde la clave, asi me aseguro

          |DDEFECTO #labornx2;
          |PARA nCampo = 0; |SI nCampo [ 38; |HACIENDO nCampo = nCampo + 1;
               #labornx2#nCampo# = #laborn02#nCampo#;
          |SIGUE;

          |BORRA 020#laborn02.a;

          |PARA nCampo = 0; |SI nCampo [ 38; |HACIENDO nCampo = nCampo + 1;
               #laborn02#nCampo# = #labornx2#nCampo#;
          |SIGUE;

          #laborn02#0 = "A";
          #laborn02#19 = Usuario % 0810;
          #laborn02#20 = ~;
          |HORA aAlfa;
          #laborn02#21 = aAlfa;
          |GRABA 020#laborn02.n;

          aAlfa = "    Se han realizado los procesos asociados a la baja.";
          |MENAV aAlfa;

          enEmpresa = #labtraba#0;
          enTrabajador = #labtraba#1;
          |HAZ ActualizaLabornet;

          enActualizado = 1;

          |ABRE #nompmail; |LEE 000#nompmail.p; |CIERRA #nompmail;
          |SI #nompmail#11 = "S";
               |HAZ Email;
          |FINSI;

/*
          || sincronizacion de esta empresa
          nEmpExt = #laborn02#5;
          aAlfa = nEmpExt;
          aAlfa = "labor001;" + aAlfa;
          |SUB_EJECUTA aAlfa;
*/
     |FINSI;
     |PONCONTROL 23, "SI";
     |PTEC 808;
|FPRC;

|PROCESO Consulta;
     eaUsuario = #laborn02#1;      || permito modificar
     eaFecha = #laborn02#3;
     eaHora = #laborn02#4;
     |SUB_EJECUTA "laborn12;CONSUL";
     |ACABA;
|FPRC;

|PROCESO Modificacion;
     eaUsuario = #laborn02#1;      || permito modificar
     eaFecha = #laborn02#3;
     eaHora = #laborn02#4;
     |SUB_EJECUTA "laborn12;MODIF";
     |ACABA;
|FPRC;

|PROCESO Opciones; |TIPO 0;
     |SI enFSalida ! 0;
          FSalida = enFSalida;
     |FINSI;

     || consulta de datos introducidos
     |SI FSalida = 10;
          |SI aParam = "USUARIO"; |O aParam = "ADMIN";  || consulta el usuario los datos que
               |HAZ Modificacion;
          |FINSI;
          |SI aParam = "HISTO"; |O aParam = "HISTOUSU";
               |HAZ Consulta;           || consulta de datos introducidos
          |FINSI;                       || o ya actualizados, solo visualizar
          |ACABA;
     |FINSI;

     |SI FSalida = 11;  |Y #laborn02#0 = "P"; |Y aParam = "ADMIN";
          |HAZ Modificacion;
          |LEE 000#laborn02.=;
          |HAZ Actualiza;
          |ACABA;
     |FINSI;

     |SI FSalida = 12;  |Y #laborn02#0 = "P";
/*
          enBorrado = 1;
          |CONFI "0000N¿Está seguro de que desea rechazar esta solicitud?";
          |SI FSalida ! 0;
              enBorrado = 0;
              |ERROR;
              |ACABA;
          |FINSI;
*/
          |LEE 101#laborn02.=;
          |BORRA 020#laborn02.a;
          |LIBERA #laborn02;

          #laborn02#0   = "R";
          #laborn02#24  = eaMotiv;
          |GRABA 020#laborn02.n;
          |LIBERA #laborn02;
     |FINSI;
|FPRC;

|PROCESO Mensa;  |TIPO 7;

     |SI #laborn02#0 = "P";        || pendientes de actualizar
          |SI aParam = "USUARIO";
               |MENSA "0000 <F2> Ver/modificar Datos. <F4> Borrar registro.";
          |SINO;
               |MENSA "0000 <F2> Ver datos. <F3> Actualizar baja. <F4> Borrar registro.";
          |FINSI;
     |SINO;                   || ya actualizados
          |MENSA "0000 <F2> Consultar Datos.";
     |FINSI;
|FPRC;

|PROCESO inferior;
     #laborn02#0 = aTipo;
     #laborn02#1 = aUsuarioD;
     #laborn02#2 = "";
     #laborn02#3 = "01.01.0000";
     #laborn02#4 = "";
|FPRC;

|PROCESO superior;
     #laborn02#0 = aTipo;
     #laborn02#1 = aUsuarioH;
     #laborn02#2 = "zzzzzzzzzz";
     #laborn02#3 = "31.12.2999";
     #laborn02#4 = "zzzzzzzz";
|FPRC;

|PROCESO inferiora;
     #laborn02#0 = aTipo;
     #laborn02#1 = aUsuarioD;
     #laborn02#2 = "";
     #laborn02#3 = "01.01.0000";
     #laborn02#4 = "";
     #laborn02#5 = 0;
|FPRC;

|PROCESO superiora;
     #laborn02#0 = aTipo;
     #laborn02#1 = aUsuarioH;
     #laborn02#2 = "zzzzzzzzzz";
     #laborn02#3 = "31.12.2999";
     #laborn02#4 = "zzzzzzzz";
     #laborn02#5 = 999999;
|FPRC;

|PROCESO RecogeCampos;
     #laborn02#1 = eaUsuario;
     #laborn02#3 = efFecha;
     #laborn02#4 = eaHora;
     #laborn02#0 = eaEstado;
|FPRC;

|PROGRAMA;
     |SI enFSalida ! 12;
          |CLS;
     |FINSI;
     |CABEZA "Bajas";

     aAlfa = Usuario % 0810;
     |SI aParam = "ADMIN"; |O aParam = "HISTO";
          aUsuarioD = "";
          aUsuarioH = "zzzzzzzzzz";
     |FINSI;
     |SI aParam = "USUARIO"; |O aParam = "HISTOUSU";
          aUsuarioD = aAlfa;
          aUsuarioH = aAlfa;
     |FINSI;

     |ABRE #laborn02;
     sw = 1;
     |PARA; |SI sw ! 0; |HACIENDO;
          sw = 0;
          |SI aParam = "ADMIN"; |O aParam = "USUARIO";
               aTipo = "P";   || los pendientes de actualizar
               |SI eaParam = "";
                    |ENTLINEAL #1#laborn02, -2, 4, 22, 1, inferior, superior;
               |SINO;
                    |HAZ RecogeCampos;
                    |HAZ Opciones;
               |FINSI;
          |FINSI;
          |SI aParam = "HISTO"; |O aParam = "HISTOUSU";
               aTipo = "A";   || consulta del historico, los ya actualizados
               |SI eaParam = "";
                    |ENTLINEAL #2#laborn02, -4, 4, 22, 1, inferiora, superiora;
               |SINO;
                    |HAZ RecogeCampos;
                    |LEE 000#laborn02.=;
                    |HAZ Opciones;
               |FINSI;
          |FINSI;
     |SIGUE;

     |CIERRA #laborn02;
|FPRO;

|PROCESO Tipo80n02;  |TIPO 80;
     |SI PARAMETRO = "";  |ACABA;  |FINSI;

     aSiEsX = PARAMETRO % 101;
     |SI aSiEsX = "X";
         aAlfa     = PARAMETRO % 202;   enFSalida = aAlfa;
         eaEstado  = PARAMETRO % 401;
         eaUsuario = PARAMETRO % 510;
         aAlfa     = PARAMETRO % 1510;  efFecha = aAlfa;
         eaHora    = PARAMETRO % 2508;
         eaParam   = PARAMETRO % 3310;  |QBF eaParam;
         aParam    = eaParam;
     |SINO;
          aParam = PARAMETRO;  |QBF aParam;
          |SI eaParam ! "";
              eaParam = aParam;
          |FINSI;
     |FINSI;
|FPRC;
