|FICHEROS;
     nomyrpac #0;
     nomhicca #1;        || historico
     labtraba #2;
     labempre #3;
     nomcalca #6;        || mensual
     nomcalcu #40;       || calculo mensual
     dsnetm00@ #100;     || parametros dsnetcom
     nomgruec;
     nomgruel;
     nomtmpre #200;
     nomyrtmp #201;
     nomtmprf #202;
     labcentr;
     nomauxi1;
     nomcontr;
     nomimpr1;
     nomdesam;
     nomcalli;
     nomhicli;
     nomconli;
|FIN;

|VARIABLES;
    nModoRepeticion = 0;
    seleccio = 0;
    centro = 0;
    sw = 0;
    empresa = 0;
    tinf_tc1 = 0;
    tinf_sss = 0;
    tinf_dse = 0;
    tinf_enf = 0;
    ginf_bru = 0;
    ginf_ded = 0;
    ginf_liq = 0;
    cinf_bru = 0;
    cinf_ded = 0;
    cinf_liq = 0;
    ginf_dto = 0;
    cinf_dto = 0;
    nume1 = 0;
    nume2 = 0;
    fecsys = @;
    alfa1 = "";
    error0       = "     Proceso ABORTADO !!! ";
    &informe = "nomyrweb";          || el informe de siempre
    informeweb = "nomyrweb";
    informecrys = "nomyrenm";      || el informe para Crystal
    informecrys2 = "nomyren2";      || el informe para Crystal
    informecrys3 = "nomyren3";     || el informe para Crystal
                                   || con descuentos desglosados
    informeexcel = "nomyrexc";      || el informe para Excel
    anyo = "";
    fechasys = @;
    &ano = 0;
    &p_mes = " ";
    &swlin = 0;
    &varinf1 = "";
    &varinf2 = "";

    &inf_bru = 0;
    &inf_tan = 0;
    &inf_ded = 0;
    &inf_liq = 0;
    &inf_dto = 0;
     inf_antp = 0;
     inf_emb = 0;
    &inf_dias = 0;

    &pinf_bru = 0;
    &pinf_ded = 0;
    &pinf_liq = 0;
    &pinf_dto = 0;

    &tinf_bru = 0;
    &tinf_ded = 0;
    &tinf_liq = 0;
    &tinf_dto = 0;

    tip = "";
    &fecha = @;
    &cabeza = "";
    grupo = " ";

     aDirDes = "";
     aRutaDes = "";
     aRuta = "";
     aRutaDatos = "";
     nCanal = 0;
     aArchivo = "";
     aCabecera = "";
     aTipoResumen = "";
     aTipoNomina = "";
     aEmpresa = "";
     aAnyoDesde = "";
     aAnyoHasta = "";
     aMesDesde = "";
     aMesHasta = "";
     aOrdenacion = "";
     nEmpresa = 0;
     nAnyoDesde = 0;
     nAnyoHasta = 0;
     nMesDesde = 0;
     nMesHasta = 0;
     nOrdenacion = 0;

     cParam         = "";
     cParte         = "";
     &cOrigen        = "";
     &cDestino       = "";
     cDirBase       = "";
     cUsuario       = "";
     cIp            = "";
     cDirUsu        = "";
     cNumUsu        = "";
     aAlfa = "";
     aAlfa1 = "";
     aAlfa2 = "";
     aTabla = "";
     aDir = "";
     &nErrorInf = 0;
     aSystem = "";
     aBaseBin = "";
     aDirOri = "";
     swDesdeWeb = 0;
     &aParametro = "";
     nNume = 0;
     nCampo = 0;
     nGrupo = 0;
     nDesdeEmp = 0;
     nHastaEmp = 0;
     nDesdeCen = 0;
     nHastaCen = 0;
     &nOrdenEmp = 0;
     &nOrdenTra = 0;
     &aRupAreas = "";

     &resumen = 1;
     &enTipoInf = 0;
     &eaFuente = "";
     &enDesdeMes = 0;
     &enHastaMes = 0;
     &enAny = 0;
     &aNomPag = "";

     FEstado = 0;
     FEstado2 = 0;
     FEstadoDef = 0;
     &swHayAlgo = 0;
     &enCuadre = 0;
     &swPagasSN = 0;
     nAnyTmp = 0;

     &nRegistro = 0;
     &fFechaEmision = @;
     &aHoraEmision = "";
     nHandle = 0;
     aOrigen = "";
     aDestino = "";
     aRutaFich = "";
     aRutaDestino = "";

 {-1}aMenu  = "";
     aMenu1 = "2400";
     aMenu2 = "1";
     aMenu3 = "Archivo documental activado. Elija opci¢n: ";
     aMenu4 = "IAM";
     aMenu5 = "Imprimir";
     aMenu6 = "Archivar";
     aMenu7 = "aMbos";
     &nOpcion = 0;
     &nArchi = 0;
     &eaVengoDe = "";
     &enSwDesde9 = 1;
     &enSwMeteGrupoSubTipo = 0;
     &swErrorDSArchi = 0;
     &eaInforme = "";
     swSigue = 0;
     aEmp = "";
     aCen = "";
     aTra = "";
     aNomEmp = "";
     emp_rupc = 0;
     cen_rupc = 0;
     nEmbargoPaga = 0;
     nDesdeEmpSel = 0;
     nHastaEmpSel = 0;
     nConcepto = 0;
     nUnidades = 0;
     nValor = 0;
     nImporteCon = 0;
     aDescripcion = "";
     aTipoConcepto = "";
     FEstadoC = 0;
     &aTotalGrupo = "";
     &enPorEmp  = 0;
     &enPorCen  = 0;
|FIN;

|INCLUYE i_yreno;

|PROCESO ConsultaGrupos; |TIPO 66;
     |ABRE #nomgruec;
     |CONSULTA_CLAVE #1#nomgruec;
     |SI FSalida = 0;
          #0Campo = #nomgruec#0;
          |PINTA #0Campo;
     |FINSI;
     |CIERRA #nomgruec;
|FPRC;

|PROCESO Ayuda; |TIPO 7;
     |PUSHV 09481479;
     |ATRI R;
     ||            4 5         6         7
     ||            8901234567890123456789012345678
     |PINTA 0948, "          - Opciones -         ";
     |PINTA 1048, "00. Normal                     ";
     ||PINTA 1148, "01. Impresión a Excel (con NIF)";
     |PINTA 1148, "                               ";
     |PINTA 1248, "02. Excel                      ";
     |PINTA 1348, "10. Fichero XML                ";
     |ATRI 0;
|FPRC;

|PROCESO Restaura;
     |POPV;
|FPRC;

|PROCESO PorGrupos;
     nCampo = Campo;
     |SI #0nCampo = 00000;
          aAlfa = "N";
     |SINO;
          aAlfa = "S";
     |FINSI;
     |PARA nCampo = nCampo + 1; |SI nCampo [ 16; |HACIENDO nCampo = nCampo + 1;
          |CAMPO_MODIFICA #0nCampo, 1, aAlfa;
          |SI aAlfa = "N";
               #0nCampo = 0;
               |PINTA #0nCampo;
          |FINSI;
     |SIGUE;
|FPRC;

|PROCESO PParametro; |TIPO 7;
     |SI PARAMETRO="M"; |O PARAMETRO="H";
          |CAMPO_MODIFICA #0#10,1,"N";
          #0#10=PARAMETRO;
     |FINSI;
|FPRC;

|PROCESO Cuadre; |TIPO 0;
     |SI #0#20 = 2;
          |SI #0#0 ! #0#1; |Y FSalida ! 2;
               aAlfa = "    ATENCION: en el informe en EXCEL sólo se puede imprimir ";
               aAlfa = aAlfa + " una empresa. Corrija la selección."
               |MENAV aAlfa;
               |ERROR;
          |FINSI;
     |FINSI;
|FPRC;

******************************************************************************
   LLAMADAS DE CAMPOS
******************************************************************************

|PROCESO FuenteDatos;|TIPO 0;
     |SI #0#10="M";
          alfa1="MENSUAL  ";
          ||CAMPO_MODIFICA #0#5,1,"N";
     |SINO;
          alfa1="HISTORICO";
          |CAMPO_MODIFICA #0#5,1,"S";
     |FINSI;

     |PINTA 0744,alfa1;
     |PINTA #0#10;
|FPRC;


|PROCESO mirames; |TIPO 7; ||CALCULA EL MES POR DEFECTO SEGUN FECHA SISTEMA
    fecsys = ~;
    alfa1 = fecsys;
    alfa1 = alfa1 % 402;
    #0Campo = alfa1;
|PINTA #0Campo;
|FPRC;

|PROCESO pintames; |TIPO 0;      || PINTA EL MES LITERAL
|SI #0Campo =  1;  p_mes = "ENERO     ";  |FINSI;
|SI #0Campo =  2;  p_mes = "FEBRERO   ";  |FINSI;
|SI #0Campo =  3;  p_mes = "MARZO     ";  |FINSI;
|SI #0Campo =  4;  p_mes = "ABRIL     ";  |FINSI;
|SI #0Campo =  5;  p_mes = "MAYO      ";  |FINSI;
|SI #0Campo =  6;  p_mes = "JUNIO     ";  |FINSI;
|SI #0Campo =  7;  p_mes = "JULIO     ";  |FINSI;
|SI #0Campo =  8;  p_mes = "AGOSTO    ";  |FINSI;
|SI #0Campo =  9;  p_mes = "SEPTIEMBRE";  |FINSI;
|SI #0Campo = 10;  p_mes = "OCTUBRE   ";  |FINSI;
|SI #0Campo = 11;  p_mes = "NOVIEMBRE ";  |FINSI;
|SI #0Campo = 12;  p_mes = "DICIEMBRE ";  |FINSI;
|ATRI I;
|PINTA 1345, p_mes;
|ATRI 0;
|FPRC;

|PROCESO pintanyo;|TIPO 7;
     ||SI #0#10="H";
        fechasys = ~;
        anyo = fechasys %  - 102;
        #0#5 = anyo;
        |PINTA #0#5;
        ano = #0#5;
     ||FINSI;
|FPRC;
******************************************************************************
    CARGA DE VARIABLES
******************************************************************************

|PROCESO ElPRINT;
     |SI #0#20 ! 02; |Y #0#20 ! 03; |Y #0#20 ! 10;
     |Y informe ! "nomyren2";
          |PRINT;
     |FINSI;
|FPRC;

|PROCESO Area;
     #200#21 = "";
     #200#22 = "";
     |SI #0#6 = "S";
          #nomauxi1#0 = #labtraba#62;
          |LEE 000#nomauxi1.=;
          |SI FSalida = 0;
                #200#21 = #nomauxi1#0;
                #200#22 = #nomauxi1#1;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO EmbargoPaga;
     nEmbargoPaga = 0;

     |ABRE #nomimpr1;

     #nomimpr1#0 = #labtraba#0;
     #nomimpr1#1 = #labtraba#1;
     #nomimpr1#2 = #200#19;
     #nomimpr1#3 = 0;
     #nomimpr1#4 = #200#41;
     |LEE 000#nomimpr1.=;
     |SI FSalida = 0; |Y #nomimpr1#10 ! 0;
          nEmbargoPaga = #nomimpr1#10;
     |FINSI;

     |CIERRA #nomimpr1;
|FPRC;

|PROCESO grabatmp;
     swHayAlgo = 1;
     |DDEFECTO #200;
     #200#0 = #labtraba#0;    || codigo empresa
     #200#1 = #labtraba#1;    || codigo trabajador
     #200#2 = #labtraba#77;   || codigo centro
     #200#19 = #0#2;          || mensual, hco y atrasos refundidos
     #200#23 = 0;
     #200#41 = nAnyTmp;

     |HAZ Area;

     |LEE 101#200=;
     FEstado2 = FSalida;

     #200#3 = #labtraba#7;    || NIF

     #labempre#0 = #labtraba#0;
     |LEE 000#labempre.=;
     #200#4 = #labempre#2;    || nombre empresa
     #200#43 = #labempre#13;  || ccc empresa

     #200#5 = #labtraba#2;    || nombre trabajador

     #labcentr#0 = #labtraba#0;
     #labcentr#1 = #labtraba#77;
     |LEE 000#labcentr.=;
     #200#6 = #labcentr#2;    || nombre centro
     #200#44 = #labcentr#10;  || CCC centro

     |HAZ Area;

     nCampo = 6;
     || prestaciones    - Campo 7
     nCampo = nCampo + 1; || #200nCampo = #200nCampo + inf_enf;
     || coste trabajad. - Campo 8
     nCampo = nCampo + 1; || #200nCampo = #200nCampo + inf_sub;
     || Importe bruto   - Campo 9
     nCampo = nCampo + 1; #200nCampo = #200nCampo + inf_bru;
     || Dto.varios      - Campo 10
     nCampo = nCampo + 1; #200nCampo = #200nCampo + inf_dto;
     || Porcentaje      - Campo 11
     nCampo = nCampo + 1; #200nCampo = #200nCampo + inf_tan;
     || Deduccion       - Campo 12
     nCampo = nCampo + 1; #200nCampo = #200nCampo + inf_ded;
     || notaria         - Campo 13
     nCampo = nCampo + 1; #200nCampo = #200nCampo + ded_not;
     || Deduccion S.S   - Campo 14
     nCampo = nCampo + 1; || #200nCampo = #200nCampo + inf_dse;
     || Liquido         - Campo 15
     nCampo = nCampo + 1; #200nCampo = #200nCampo + inf_liq;
     || bonif           - Campo 16
     nCampo = nCampo + 1; || #200nCampo = #200nCampo + inf_bon;
     || S.S CGO         - Campo 17
     nCampo = nCampo + 1; || #200nCampo = #200nCampo + inf_sss;
     || Base IRPF exenta- Campo 18
     nCampo = nCampo + 1; || #200nCampo = #200nCampo + inf_exe;
     |SI #0#10 = "M";
          || #200#24 = #200#24 + #6#52;    || anticipos 1
          || #200#25 = #200#25 + #6#53;    || anticipos 2
          || #200#26 = #200#26 + #6#89;    || anticipos 3
          #200#27 = #200#27 + #6#86;    || anticipos Paga Extra
          || #200#28 = #200#28 + #6#54;    || especie
          |SI #6#47 = 0;
               #200#29 = #200#29 + #6#104;   || embargo
          |SINO;
               nEmbargoPaga = 0;
               |HAZ EmbargoPaga;
               |SI nEmbargoPaga ! 0;
                    #200#29 = #200#29 + nEmbargoPaga;
               |SINO;
                    #200#29 = #200#29 + (#6#104 - ((#6#104 * (#6#46 / (#6#46 + #6#47))) ! 2));
               |FINSI;
          |FINSI;
     |FINSI;

     |SI #0#10 = "H";
          || #200#24 = #200#24 + #1#52;    || anticipos 1
          || #200#25 = #200#25 + #1#53;    || anticipos 2
          || #200#26 = #200#26 + #1#131;    || anticipos 3
          #200#27 = #200#27 +  inf_antp;    || anticipos Paga Extra
          || #200#28 = #200#28 + #1#54;    || especie
          |SI #1#47 = 0;
               #200#29 = #200#29 + inf_emb;   || embargo
          |SINO;
               nEmbargoPaga = 0;
               |HAZ EmbargoPaga;
               |SI nEmbargoPaga ! 0;
                    #200#29 = #200#29 + nEmbargoPaga;
               |SINO;
                    #200#29 = #200#29 + (inf_emb - ((inf_emb * (#1#46 / (#1#46 + #1#47))) ! 2));
               |FINSI;
          |FINSI;
     |FINSI;
     #200#36 = #labtraba#36;         || fecha de alta
     #200#37 = #labtraba#31;         || numero de referencia
     #200#41 = nAnyTmp;
     #200#51 = inf_dias;

     |SI FEstado2 = 0;
          |GRABA 020#200a;
     |SINO;
          #200#0 = #labtraba#0;    || codigo empresa
          #200#1 = #labtraba#1;    || codigo trabajador
          #200#2 = #labtraba#77;   || codigo centro
          #200#19 = #0#2;          || mensual, hco y atrasos refundidos
          #200#23 = 0;
          #200#41 = nAnyTmp;
          |HAZ Area;

          |GRABA 020#200n;
     |FINSI;
     |LIBERA #200;
|FPRC;

|PROCESO Descripcion;
     #nomdesam#0 = #labtraba#87;
     #nomdesam#1 = nConcepto;
     |LEE 000#nomdesam.=;
     |SI FSalida = 0;
          aAlfa = #nomdesam#2;
          |QBF aAlfa;
          |SI aAlfa ! "";
               aDescripcion = #nomdesam#2;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO GrabaConcepto;
     swSigue = 0;

     |SI nConcepto ] 200; |Y nConcepto [ 299;
          |SI aTipoConcepto ! "P";
               swSigue = 1;
          |FINSI;
          |SI nValor = 1;
               nValor = 0;
          |FINSI;
          |SI nImporteCon > 0; |Y swSigue = 1;
               inf_dias = nUnidades;
          |FINSI;
     |FINSI;

     |SI nImporteCon > 0;
          || swSigue = 1;
     |FINSI;

     |SI swSigue = 0;  |ACABA;  |FINSI;

     #nomtmprf#0  = #labtraba#0;    || codigo empresa
     #nomtmprf#2  = #labtraba#77;   || codigo centro
     #nomtmprf#21 = "";             || area
     #nomtmprf#1  = #labtraba#1;    || codigo trabajador
     #nomtmprf#41 = nAnyTmp;
     #nomtmprf#19 = #0#2;
     #nomtmprf#23 = 0;
     #nomtmprf#7  = nConcepto;
     |LEE 101#nomtmprf.=;
     |SI FSalida ! 0;
         |DDEFECTO #nomtmprf;
         #nomtmprf#0  = #labtraba#0;    || codigo empresa
         #nomtmprf#2  = #labtraba#77;   || codigo centro
         #nomtmprf#21 = "";             || area
         #nomtmprf#1  = #labtraba#1;    || codigo trabajador
         #nomtmprf#41 = nAnyTmp;
         #nomtmprf#19 = #0#2;
         #nomtmprf#23 = 0;
         #nomtmprf#7  = nConcepto;
         |GRABA 020#nomtmprf.b;
     |FINSI;

     |HAZ Descripcion;
     #nomtmprf#8  = aDescripcion;
     #nomtmprf#9  = nUnidades;
     #nomtmprf#10 = nValor;
     #nomtmprf#11 = nImporteCon;

     |GRABA 020#nomtmprf.a;
     |LIBERA #nomtmprf;
|FPRC;

|PROCESO PrepConceptos;
     |SI #0#10 = "M";
          nConcepto     = #nomcalli#4;
          nUnidades     = #nomcalli#11;
          nValor        = #nomcalli#10;
          nImporteCon   = #nomcalli#11 * #nomcalli#10;
          aDescripcion  = #nomcalli#8;
          aTipoConcepto = #nomcalli#5;
     |FINSI;

     |SI #0#10 = "H";
          nConcepto     = #nomhicli#4;
          nUnidades     = #nomhicli#11;
          nValor        = #nomhicli#10;
          nImporteCon   = #nomhicli#11 * #nomhicli#10;
          aDescripcion  = #nomhicli#8;
          aTipoConcepto = #nomhicli#5;
     |FINSI;

     |HAZ GrabaConcepto;
|FPRC;

|DEFBUCLE nomhicli;
     #nomhicli#1;
     ;
     #nomhicca#0, #nomhicca#1, #nomhicca#66, #nomhicca#2, #nomhicca#3, 101;
     #nomhicca#0, #nomhicca#1, #nomhicca#66, #nomhicca#2, #nomhicca#3, 999;
     ;
     NULL, PrepConceptos;
|FIN;

|DEFBUCLE nomcalli;
     #nomcalli#1;
     ;
     #nomcalca#0, #nomcalca#1, #nomcalca#2, #nomcalca#3, 101;
     #nomcalca#0, #nomcalca#1, #nomcalca#2, #nomcalca#3, 999;
     ;
     NULL, PrepConceptos;
|FIN;

|PROCESO nomconli;
     nConcepto     = #nomconli#2;

     |SI nConcepto = 116; |O nConcepto = 117; |O nConcepto = 118;
          |ACABA;
     |FINSI;

     #nomtmprf#0  = #labtraba#0;    || codigo empresa
     #nomtmprf#2  = #labtraba#77;   || codigo centro
     #nomtmprf#21 = "";             || area
     #nomtmprf#1  = #labtraba#1;    || codigo trabajador
     #nomtmprf#41 = nAnyTmp;
     #nomtmprf#19 = #0#2;
     #nomtmprf#23 = 0;
     #nomtmprf#7  = #nomconli#2;
     |LEE 101#nomtmprf.=;
     |SI FSalida ! 0;
         |DDEFECTO #nomtmprf;
         #nomtmprf#0  = #labtraba#0;    || codigo empresa
         #nomtmprf#2  = #labtraba#77;   || codigo centro
         #nomtmprf#21 = "";             || area
         #nomtmprf#1  = #labtraba#1;    || codigo trabajador
         #nomtmprf#41 = nAnyTmp;
         #nomtmprf#19 = #0#2;
         #nomtmprf#23 = 0;
         #nomtmprf#7  = #nomconli#2;
         |GRABA 020#nomtmprf.b;
     |FINSI;

     |SI #nomconli#2 = 431;
          aDescripcion = "ABSENTISMO";
     |SINO;
          aDescripcion = #nomconli#6;
          |HAZ Descripcion;
     |FINSI;

     #nomtmprf#8  = aDescripcion;
     #nomtmprf#9  = 0;
     #nomtmprf#10 = 0;
     #nomtmprf#11 = 0;
     |GRABA 020#nomtmprf.a;
     |LIBERA #nomtmprf;
|FPRC;

|DEFBUCLE nomconli;
     #nomconli#1;
     ;
     #labtraba#87,   0;
     #labtraba#87, 999;
     ;
     NULL, nomconli;
|FIN;

|PROCESO CreaConceptosCero;
     |BUCLE nomconli;

     #nomconli#2 = 431;
     #nomconli#6 = "ABSENTISMO";
     |HAZ nomconli;
|FPRC;

|PROCESO DesgloseConceptosXML;
     |ABRE #nomdesam;

     |HAZ CreaConceptosCero;

     |SI #0#10 = "M";
          |BUCLE nomcalli;
     |FINSI;

     |SI #0#10 = "H";
          |BUCLE nomhicli;
     |FINSI;

     |CIERRA #nomdesam;
|FPRC;

|PROCESO detalleH;
    inf_bru = #1#16;                              || Importe bruto
    inf_tan = #1#129; inf_tan = inf_tan ! 2;      || Porcentaje
    inf_dto = #1#128;                             || Anticipos P.E.
    |SI #1#146 ! 0;
          |SI #1#47 = 0;
               inf_dto = inf_dto + #1#146;
          |SINO;
               nEmbargoPaga = 0;
               #200#19 = #1#2;
               #200#41 = #1#66 + 2000;
               |HAZ EmbargoPaga;
               |SI nEmbargoPaga ! 0;
                    inf_dto = inf_dto + nEmbargoPaga;
               |SINO;
                    inf_dto = inf_dto + (#1#146 - ((#1#146 * (#1#46 / (#1#46 + #1#47))) ! 2));
               |FINSI;
          |FINSI;
     |FINSI;
    inf_ded = #1#17;                              || Deduccion
    inf_antp = #1#128;
    inf_emb = #1#146;
    inf_liq = inf_bru - inf_ded - inf_dto;        || Liquido
     nAnyTmp = #1#66 + 2000;
     |SI #0#20 = 10;
          |HAZ DesgloseConceptosXML;
     |FINSI;
|FPRC;

|PROCESO grupos;
    ginf_bru = ginf_bru + inf_bru;       || Importe bruto
    ginf_ded = ginf_ded + inf_ded;       || Deduccion
    ginf_liq = ginf_liq + inf_liq;       || Liquido
    ginf_dto = ginf_dto + inf_dto;       || Anticipos P.E.
|FPRC;

|PROCESO centros;
    cinf_bru = cinf_bru + inf_bru;       || Importe bruto
    cinf_ded = cinf_ded + inf_ded;       || Deduccion
    cinf_liq = cinf_liq + inf_liq;       || Liquido
    cinf_dto = cinf_dto + inf_dto;       || Anticipos P.E.
|FPRC;

|PROCESO &totales;
    tinf_bru = tinf_bru + inf_bru;        || Importe bruto
    tinf_ded = tinf_ded + inf_ded;        || Deduccion
    tinf_liq = tinf_liq + inf_liq;        || Liquido
    tinf_dto = tinf_dto + inf_dto;        || Anticipos P.E.
|FPRC;

|PROCESO puecentro;
    pinf_bru = cinf_bru;
    pinf_ded = cinf_ded;
    pinf_liq = cinf_liq;
    pinf_dto = cinf_dto;
|FPRC;

|PROCESO puegrupos;
    pinf_bru = ginf_bru;
    pinf_ded = ginf_ded;
    pinf_liq = ginf_liq;
    pinf_dto = ginf_dto;
|FPRC;


|PROCESO limpgrupos;
    ginf_bru = 0;
    ginf_ded = 0;
    ginf_liq = 0;
    ginf_dto = 0;
|FPRC;

|PROCESO limparcial;
    cinf_bru = 0;
    cinf_ded = 0;
    cinf_liq = 0;
    cinf_dto = 0;
|FPRC;

|PROCESO limptotal;
    tinf_bru = 0;
    tinf_ded = 0;
    tinf_liq = 0;
    tinf_dto = 0;
|FPRC;
|PROCESO centra;         || CENTRA TITULO DEL INFORME
    alfa1 = cabeza % 0;
    nume1 = alfa1;
    nume2 = ((79 - nume1) / 2) - 0.5;
    nume2 = nume2 ! 0;
    alfa1 = " " * nume2;
    cabeza = alfa1 + cabeza;
|FPRC;

******************************************************************************
   historico
******************************************************************************

|PROCESO imprimirH;
     #3#0 = #2#0;
     |LEE 020#3=;
     |SI FSalida ! 0;  |DDEFECTO #3;  |FINSI;
     |SI #3#36 ! "N";
          #1#0 = #2#0;   || Empresa
          #1#1 = #2#1;   || Trabajador
          #1#2 = #0#2;   || Mes
          #1#3 = #0#4;   || Tipo
          #1#66 = #0#5;  || Anyo
          |LEE 040#1=;
          |SI FSalida = 0; |Y #1#16 ! 0;
               |SI sw = 0;
                    centro = #2#77;
                    sw = 1;
                    grupo = #2#62;
               |FINSI;
               empresa = #2#0;
               seleccio = 1;
               /*
               |SI empresa ! #2#0;                     || ruptura por empresa
                    |HAZ rup_areas;
                    |HAZ rup_centr;
                    |HAZ rup_empre;
               |SINO;
               */
                    |SI centro ! #2#77;                || ruptura por centro
                         |HAZ rup_areas;
                         |HAZ rup_centr;
                    |SINO;
                         |SI grupo ! #2#62;            || ruptura por areas
                              |HAZ rup_areas;
                         |FINSI;
                    |FINSI;
               ||FINSI;

               |HAZ detalleH;

               |HAZ ElPRINT;
               |HAZ grabatmp;
               |HAZ centros;
               |HAZ grupos;
               ||HAZ totales;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO rup_empre;
     swlin = 2;
     ||FSalida = "PAGINA";
     |HAZ ElPRINT;
     |SI #0#20 ! 02; |Y #0#20 ! 03; |Y #0#20 ! 10;
     |Y informe ! "nomyren2";
          || solo para el .inf
          |PIEINF;
     |FINSI;
     |HAZ limptotal;
     empresa = #2#0;
     swlin = 0;
|FPRC;

|PROCESO rup_centr;
           emp_rupc = empresa;     || me quedo con el ultimo centro por el
           cen_rupc = centro;      || que he roto
           |HAZ lee_centro;
           swlin = 1;
           varinf1 = "Centro: " + #4#1 + "/" + #4#2;
           |HAZ puecentro;
           |HAZ ElPRINT;
           |HAZ limparcial;
           centro = #2#77;
           swlin = 0;
|FPRC;

|PROCESO rup_areas;
|SI #0#6 = "S";
    |HAZ lee_area;
        swlin = 1;
        varinf1 = "Area: " + #5#0 + "/" +  #5#1;
    |HAZ puegrupos;
    |HAZ ElPRINT;
    |HAZ limpgrupos;
        grupo = #2#62;
        swlin = 0;
|FINSI;
|FPRC;

|DEFBUCLE nomyrpah0;
     #2#1;
     77;
     nDesdeEmp,     1, nDesdeCen;
     nHastaEmp, 99999, nHastaCen;
     ;
     NULL, NULL, NULL, NULL, NULL, imprimirH;
     #2#0, #2#77, #2#62, #2#1;
|FIN;

|DEFBUCLE nomyrpah1;
     #2#1;
     77;
     nDesdeEmp,     1, nDesdeCen;
     nHastaEmp, 99999, nHastaCen;
     ;
     NULL, NULL, NULL, NULL, NULL, imprimirH;
     #2#0, #2#77, #2#1;
|FIN;

|DEFBUCLE nomyrpah2;
     #2#1;
     77;
     nDesdeEmp,     1, nDesdeCen;
     nHastaEmp, 99999, nHastaCen;
     ;
     NULL, NULL, NULL, NULL, NULL, imprimirH;
     #2#0, #2#77, #2#62, #2#2, #2#1;
|FIN;

|DEFBUCLE nomyrpah3;
     #2#1;
     77;
     nDesdeEmp,     1, nDesdeCen;
     nHastaEmp, 99999, nHastaCen;
     ;
     NULL, NULL, NULL, NULL, NULL, imprimirH;
     #2#0, #2#77, #2#2, #2#1;
|FIN;

|DEFBUCLE nomyrpah4;
     #2#1;
     77;
     nDesdeEmp,     1, nDesdeCen;
     nHastaEmp, 99999, nHastaCen;
     ;
     NULL, NULL, NULL, NULL, NULL, imprimirH;
     #2#0, #2#77, #2#62, #2#31, #2#1;
|FIN;

|DEFBUCLE nomyrpah5;
     #2#1;
     77;
     nDesdeEmp,     1, nDesdeCen;
     nHastaEmp, 99999, nHastaCen;
     ;
     NULL, NULL, NULL, NULL, NULL, imprimirH;
     #2#0, #2#77, #2#31, #2#1;
|FIN;

|PROCESO ResHistorico;
     seleccio = 0;
     sw = 0;
     |SI #0#7 = 1;
          |SI #0#6 = "S";  |BUCLE nomyrpah0;  |FINSI;
          |SI #0#6 = "N";  |BUCLE nomyrpah1;  |FINSI;
     |FINSI;
     |SI #0#7 = 2;
          |SI #0#6 = "S";  |BUCLE nomyrpah2;  |FINSI;
          |SI #0#6 = "N";  |BUCLE nomyrpah3;  |FINSI;
     |FINSI;
     |SI #0#7 = 3;
          |SI #0#6 = "S";  |BUCLE nomyrpah4;  |FINSI;
          |SI #0#6 = "N";  |BUCLE nomyrpah5;  |FINSI;
     |FINSI;
     |SI seleccio = 1;
          |HAZ rup_areas;
          |SI empresa ! emp_rupc; |O centro ! cen_rupc;
               || que no vuelva a imprimir los totales de un centro que ya
               || haya totalizado
               |HAZ rup_centr;
          |FINSI;
          |HAZ rup_empre;
     |FINSI;
|FPRC;

|PROCESO PHistorico;
     |SI #0#4 = 0; tip = "MENSUAL - ";   |FINSI;
     |SI #0#4 = 2; tip = "FINIQUITO - "; |FINSI;
     |SI #0#4 = 5; tip = "ATRASOS - ";   |FINSI;
     fecha = #0#3;
     cabeza = "RESUMEN HISTORICO PAGAS " + tip + p_mes;
     |HAZ centra;
     |ABRE #1;
     |ABRE #3;
     |SI swDesdeWeb = 0;
          ||HAZ ElCuadre;
     |SINO;
          || desde la web
          informe = informecrys;
          |HAZ prepara_impresion;
     |FINSI;
     |SI FSalida = 0;
          |SI #0#20 ! 02; |Y #0#20 ! 10;
          |Y nOpcion ! 2; |Y informe ! "nomyrweb";
               |INFOR informe;
          |FINSI;
          seleccio = 0;

          nDesdeEmp = nEmpresa;
          nHastaEmp = nEmpresa;
          nDesdeCen = #0#8;
          nHastaCen = #0#9;
          |HAZ ResHistorico;
     |SINO;
          |MENAV error0;
     |FINSI;
     |SI #0#20 = 02; |O #0#20 = 03; |O #0#20 = 10;
     |O informe = "nomyren2";
          enPorEmp = 1;      || de momento asi 14/11/2018
          |HAZ ElInforme;
     |SINO;
          |SI nOpcion = 1; |O nOpcion = 3;
               |SI informe ! "nomyrweb";
                    |FINIMP;
               |FINSI;
          |FINSI;
     |FINSI;
     |CIERRA #1;
     |CIERRA #3;
|FPRC;

******************************************************************************
   mensual
******************************************************************************

|PROCESO imprimirM;
     varinf1 = "SUMA Y SIGUE CENTRO TRABAJO .....";
     varinf2 = "SUMA Y SIGUE EMPRESA ............";
     #3#0 = #2#0;
     |LEE 020#3=;
     |SI FSalida ! 0;  |DDEFECTO #3;  |FINSI;
     |SI #3#36 ! "N";   || si el switch de impresion no es "N"
          #6#0 = #2#0;   || Empresa
          #6#1 = #2#1;   || Trabajador
          #6#2 = #0#2;   || Mes
          #6#3 = #0#4;   || Tipo
          |LEE 040#6=;
          |SI FSalida = 0; |Y #6#16 ! 0;
               |SI sw = 0;
                    centro = #2#77;
                    sw = 1;
                    grupo = #2#62;
               |FINSI;
               empresa = #2#0;
               seleccio = 1;
               /*
               |SI empresa ! #2#0;                    || ruptura por empresa
                   |HAZ rup_areas;
                   |HAZ rup_centr;
                   |HAZ rup_empre;
               |SINO;
               */
                   |SI centro ! #2#77;                        || ruptura por centro
                       |HAZ rup_areas;
                       |HAZ rup_centr;
                   |SINO;
                       |SI grupo ! #2#62;                     || ruptura por area
                           |HAZ rup_areas;
                       |FINSI;
                   |FINSI;
               ||FINSI;

               |HAZ detalleM;

               |HAZ ElPRINT;
               |HAZ grabatmp;
               |HAZ centros;
               |HAZ grupos;
               ||HAZ totales;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO detalleM;
    inf_bru = #6#16;                              || Importe bruto
    inf_tan = #6#87; inf_tan = inf_tan ! 2;       || Porcentaje
    inf_ded = #6#17;                              || Deduccion
    inf_dto = #6#86;                              || Anticipos P.E.
    |SI #6#104 ! 0;
          |SI #6#47 = 0;
               inf_dto = inf_dto + #6#104;
          |SINO;
               nEmbargoPaga = 0;
               #200#19 = #6#2;
               #200#41 = #0#5 + 2000;
               |HAZ EmbargoPaga;
               |SI nEmbargoPaga ! 0;
                    inf_dto = inf_dto + nEmbargoPaga;
               |SINO;
                    inf_dto = inf_dto + (#6#104 - ((#6#104 * (#6#46 / (#6#46 + #6#47))) ! 2))
               |FINSI;
          |FINSI;
    |FINSI;
    inf_liq = inf_bru - inf_ded - inf_dto;        || Liquido
     nAnyTmp = #0#5 + 2000;
     |SI #0#20 = 10;
          |HAZ DesgloseConceptosXML;
     |FINSI;
|FPRC;

|DEFBUCLE nomyrpan0;
     #2#1;
     77;
     nDesdeEmp,     1, nDesdeCen;
     nHastaEmp, 99999, nHastaCen;
     ;
     NULL, NULL, NULL, NULL, NULL, imprimirM;
     #2#0, #2#77, #2#62, #2#1;
|FIN;

|DEFBUCLE nomyrpan1;
     #2#1;
     77;
     nDesdeEmp,     1, nDesdeCen;
     nHastaEmp, 99999, nHastaCen;
     ;
     NULL, NULL, NULL, NULL, NULL, imprimirM;
     #2#0, #2#77, #2#1;
|FIN;

|DEFBUCLE nomyrpan2;
     #2#1;
     77;
     nDesdeEmp,     1, nDesdeCen;
     nHastaEmp, 99999, nHastaCen;
     ;
     NULL, NULL, NULL, NULL, NULL, imprimirM;
     #2#0, #2#77, #2#62, #2#2, #2#1;
|FIN;

|DEFBUCLE nomyrpan3;
     #2#1;
     77;
     nDesdeEmp,     1, nDesdeCen;
     nHastaEmp, 99999, nHastaCen;
     ;
     NULL, NULL, NULL, NULL, NULL, imprimirM;
     #2#0, #2#77, #2#2, #2#1;
|FIN;

|DEFBUCLE nomyrpan4;
     #2#1;
     77;
     nDesdeEmp,     1, nDesdeCen;
     nHastaEmp, 99999, nHastaCen;
     ;
     NULL, NULL, NULL, NULL, NULL, imprimirM;
     #2#0, #2#77, #2#62, #2#31, #2#1;
|FIN;

|DEFBUCLE nomyrpan5;
     #2#1;
     77;
     nDesdeEmp,     1, nDesdeCen;
     nHastaEmp, 99999, nHastaCen;
     ;
     NULL, NULL, NULL, NULL, NULL, imprimirM;
     #2#0, #2#77, #2#31, #2#1;
|FIN;

|PROCESO ResMensual;
     seleccio = 0;
     sw = 0;
     |SI #0#7 = 1;
          |SI #0#6 = "S";  |BUCLE nomyrpan0;  |FINSI;
          |SI #0#6 = "N";  |BUCLE nomyrpan1;  |FINSI;
     |FINSI;
     |SI #0#7 = 2;
          |SI #0#6 = "S";  |BUCLE nomyrpan2;  |FINSI;
          |SI #0#6 = "N";  |BUCLE nomyrpan3;  |FINSI;
     |FINSI;
     |SI #0#7 = 3;
          |SI #0#6 = "S";  |BUCLE nomyrpan4;  |FINSI;
          |SI #0#6 = "N";  |BUCLE nomyrpan5;  |FINSI;
     |FINSI;
     |SI seleccio = 1;
          |HAZ rup_areas;
          |SI empresa ! emp_rupc; |O centro ! cen_rupc;
               || que no vuelva a imprimir los totales de un centro que ya
               || haya totalizado
               |HAZ rup_centr;
          |FINSI;
          |HAZ rup_empre;
     |FINSI;
|FPRC;

|PROCESO ElCuadre;
     |SI #0#20 ! 02; |Y #0#20 ! 10;
          || aqui preguntamos si DsArchi si o no
          eaVengoDe = "nomyrpac";

          nArchi = 0;
          |HAZ MiraSiArchiva;

          |SI nArchi ! 1;
               || no tengo dsarchi, sigo como siempre
               nOpcion = 1;
          |SINO;
               || tengo dsarchi, presento menu
               |MENU aMenu;
               nOpcion = FSalida;
               |SI FSalida = 0;
                    |ACABA;
               |FINSI;
          |FINSI;

          |SI nOpcion = 1; |O nOpcion = 3;
               |IMPRE 0;
          |FINSI;
          |SI nOpcion = 2;
               Impresora = "CrystalReport";
          |FINSI;

          |SI Impresora = "CrystalReport";
               |SI #0#20 = 03;
                    informe = informecrys3;
               |SINO;
                    informe = informecrys2;
               |FINSI;
          |SINO;
               |SI #0#20 = 0;
                    informe = informe;
               |FINSI;
               |SI #0#20 = 1;
                    informe = informeexcel;
               |FINSI;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO graba_dbfs2;
     aAlfa = nRegistro ; |QBF aAlfa;
     aAlfa = ("0000000" + aAlfa) % -107;
     aDestino = aAlfa + "_";
     aAlfa = fFechaEmision; |QBF aAlfa;
     aAlfa = (aAlfa % 102) + (aAlfa % 402) + (aAlfa % -104);
     aDestino = aDestino + aAlfa;
     aAlfa = aHoraEmision; |QBF aAlfa;
     aAlfa = (aAlfa % 102) + (aAlfa % 402) + (aAlfa % 702);
     aDestino = aDestino + "_" + aAlfa;
     |SI aOrigen = "c:\ds\crystal\cabecera.dbf";
     |O aOrigen = "c:\dspi\crystal\cabecera.dbf";
          aDestino = aDestino + "_cab.dbf";
     |SINO;
          aDestino = aDestino + "_det.dbf";
     |FINSI;
     aDestino = aRutaDestino + "/" + aDestino;

     |RECIBE_FICHERO aOrigen, aDestino;
|FPRC;

|PROCESO graba_dbfs;
     |DFICE aRutaFich;
     |QBF aRutaFich;
     aRutaDestino = aRutaFich + "reg_res";
     |RMKDIR aRutaDestino;

     aAlfa = "c:\ds\crystal\cabecera.dbf";
     |XABRE "CE", aAlfa, nHandle;
     |SI FSalida ] 0;
          aOrigen = aAlfa;
     |SINO;
          aAlfa = "c:\dspi\crystal\cabecera.dbf";
          |XABRE "CE", aAlfa, nHandle;
          |SI FSalida ] 0;
               aOrigen = aAlfa;
          |FINSI;
     |FINSI;
     |QBF aOrigen;

     |HAZ graba_dbfs2;

     aAlfa = "c:\ds\crystal\detalle.dbf";
     |XABRE "CE", aAlfa, nHandle;
     |SI FSalida ] 0;
          aOrigen = aAlfa;
     |SINO;
          aAlfa = "c:\dspi\crystal\detalle.dbf";
          |XABRE "CE", aAlfa, nHandle;
          |SI FSalida ] 0;
               aOrigen = aAlfa;
          |FINSI;
     |FINSI;
     |QBF aOrigen;

     |HAZ graba_dbfs2;
|FPRC;


|PROCESO PMensual;
     fecha = #0#3;
     nNume = #0#5;
     nNume = 2000 + nNume;
     aAlfa = nNume;
     cabeza = "RESUMEN MENSUAL PAGAS - " + p_mes + "/ " + aAlfa;
     |HAZ centra;
     |ABRE #6;
     |ABRE #3;

     |SI FSalida = 0;
          |SI #0#20 ! 02; |Y #0#20 ! 10;
          |Y nOpcion = 1; |O nOpcion = 3;
               |INFOR informe;
          |FINSI;
          seleccio = 0;

          nDesdeEmp = nEmpresa;
          nHastaEmp = nEmpresa;
          nDesdeCen = #0#8;
          nHastaCen = #0#9;
          |HAZ ResMensual;
     |SINO;
         |MENAV error0;
     |FINSI;
     |SI #0#20 = 02; |O #0#20 = 03; |O #0#20 = 10;
     |O informe = "nomyren2";
          enPorEmp = 1;      || de momento asi 14/11/2018
          |HAZ ElInforme;
     |SINO;
          |SI nOpcion = 1; |O nOpcion = 3;
               |SI informe ! "nomyrweb";
                   |FINIMP;
               |FINSI;
          |FINSI;
     |FINSI;
     |CIERRA #6;
     |CIERRA #3;
|FPRC;

|PROCESO ResumenEmpresa;
     alfa1 = Usuario;  |QBF alfa1;  alfa1 = "X" + alfa1;
     |NOME_DAT #200 alfa1;
     |DELINDEX #200;
     |ABRE #200;
     |ABRE #labcentr;
     |ABRE #nomauxi1;

     alfa1 = Usuario;  |QBF alfa1;  alfa1 = "Y" + alfa1;
     |NOME_DAT #202 alfa1;
     |DELINDEX #202;
     |ABRE #202;

     swHayAlgo = 0;
     |SI nOpcion = 2;
          eaInforme = informe;
          |HAZ IMPREArchi;
     |FINSI;

     nEmpresa = #201#0;

     |SI #0#10="M";
          |HAZ PMensual;
     |FINSI;
     |SI #0#10="H";
          |HAZ PHistorico;
     |FINSI;
     |SI nOpcion = 2; |Y swHayAlgo = 1;
          |PIEINF;
          |FININF;
          |HAZ Mensaje;           || mensaje de envio al dsarchi
          |HAZ Archivando;
     |FINSI;
     |CIERRA #nomauxi1;
     |CIERRA #labcentr;
     |CIERRA #200;
     |DELINDEX #202;
     |CIERRA #202;
|FPRC;

|DEFBUCLE ResumenEmpresa;
     #201#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, ResumenEmpresa;
|FIN;

|PROCESO TmpHistorico;
     #201#0 = #1#0;
     |LEE 000#201=;
     |SI FSalida ! 0;
          |GRABA 000#201n;
     |FINSI;
|FPRC;

|DEFBUCLE TmpHistorico;
     #1#1;
     ;
     nDesdeEmpSel,     1, #0#5, #0#2, 0;
     nHastaEmpSel, 99999, #0#5, #0#2, 0;
     ;
     NULL, TmpHistorico;
|FIN;

|PROCESO TmpMensual;
     #201#0 = #6#0;
     |LEE 000#201=;
     |SI FSalida ! 0;
          |GRABA 000#201n;
     |FINSI;
|FPRC;

|DEFBUCLE TmpMensual;
     #6#1;
     ;
     nDesdeEmpSel,     1, #0#2, 0;
     nHastaEmpSel, 99999, #0#2, 0;
     ;
     NULL, TmpMensual;
|FIN;

|PROCESO TmpGrupo;
     nDesdeEmpSel = #nomgruel#1; nHastaEmpSel = #nomgruel#1;

     |SI #0#10 = "M";
          |BUCLE TmpMensual;
     |FINSI;
     |SI #0#10 = "H";
          |BUCLE TmpHistorico;
     |FINSI;
|FPRC;

|DEFBUCLE TmpGrupo;
     #nomgruel#1;
     ;
     nGrupo, 00001;
     nGrupo, 99999;
     ;
     NULL, TmpGrupo;
|FIN;

|PROCESO CargaTmpEmp;
     |SI #0#11 ! 0;
          |PARA nCampo = 11; |SI nCampo [ 18; |HACIENDO nCampo = nCampo + 1;
               |SI #0nCampo ! 0;
                    nGrupo = #0nCampo;
                    |ABRE #nomgruec;
                    #nomgruec#0 = nGrupo;
                    |LEE 000#nomgruec.=;
                    aTotalGrupo = #nomgruec#2;
                    |BUCLE TmpGrupo;
                    |CIERRA #nomgruec;
               |FINSI;
          |SIGUE;
     |SINO;
          nDesdeEmpSel = #0#0; nHastaEmpSel = #0#1;

          |SI #0#10 = "M";
               |BUCLE TmpMensual;
          |FINSI;
          |SI #0#10 = "H";
               |BUCLE TmpHistorico;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO imprime;|TIPO 3;
     informe = "nomyrweb";          || el informe de siempre
     nOrdenEmp = #0#21;
     nOrdenTra = #0#7;
     swPagasSN = 1;
     aRupAreas = #0#6;
     fecha = #0#3;
     aNomPag = "P";

     enTipoInf = 2;
     eaFuente = 10;
     enDesdeMes = #0#2;
     enHastaMes = #0#2;
     enAny = #0#5 + 2000;
     enCuadre = #0#20;

     |SI nModoRepeticion = 1;
          nOpcion = 2;
          Impresora = "CrystalReport";
          |SI #0#20 = 03;
               informe = informecrys3;
          |SINO;
               informe = informecrys2;
          |FINSI;
     |SINO;
          |SI #0#10 = "M";
               |HAZ ElCuadre;
          |FINSI;
          |SI #0#10 = "H"; |Y swDesdeWeb = 0;
               |HAZ ElCuadre;
          |FINSI;
     |FINSI;

     || temporal para guardar las empresa de las que hay datos para resumenes

     alfa1 = Usuario;  |QBF alfa1;  alfa1 = "t" + alfa1;
     |NOME_DAT #201 alfa1;
     |DELINDEX #201;
     |ABRE #201;

     |HAZ CargaTmpEmp;

     |SI #0#20 ! 02; |Y #0#20 ! 03; |Y #0#20 ! 10;
     |Y informe ! "nomyren2";
          || para impresion del .inf
          |INFOR informe;
     |FINSI;

     |BUCLE ResumenEmpresa;

     |SI nOpcion = 2;
          |SI swHayAlgo = 1;
               aAlfa = ":dsarchi/dpntz001;CLI";
               |SUB_EJECUTA aAlfa;
          |FINSI;
     |FINSI;

     |SI informe = "nomyren2";
          |PIEINF;
     |FINSI;

     |SI #0#20 = 00;
          |SI informe = "nomyren2";
               |ABRE #nomcontr; |LEE 000#nomcontr.p; |CIERRA #nomcontr;
               |SI #nomcontr#56 = "S"; |O #nomyrpac#22 = "S";
                    |HAZ graba_dbfs;
               |FINSI;
          |FINSI;
     |FINSI;

     |SI #0#20 ! 02; |Y #0#20 ! 03; |Y #0#20 ! 10;
     |Y informe ! "nomyren2";
          || solo para el .inf
          |FININF;
     |FINSI;

     |FININF;

     |SI nOpcion = 1; |O nOpcion = 3;
          |FINIMP;
     |FINSI;

     |CIERRA #201;
     |DELINDEX #201;
/*
     |SI nOpcion = 1; |O nOpcion = 3;
          |CIERRA #nomauxi1;
          |CIERRA #labcentr;
          |CIERRA #200;
     |FINSI;
*/

     |SI nOpcion = 3;
          nModoRepeticion = 1;
          |HAZ imprime;
     |SINO;
          nModoRepeticion = 0;
     |FINSI;
|FPRC;

-------------------------------------------------------------------------------
                    ||   PROCESOS COMUNES

|PROCESO prepara_impresion;
     aDir = "/tmp/";
     Impresora = aDir + Usuario;
     |PINTA 2001, Usuario;
     |PINTA 2101, Impresora;
     |IMPRE -77;
     |PINTA 2201, FSalida;
     |SI FSalida ! 0;
          nErrorInf = 1;
     |FINSI;
     aAlfa1 = "chmod 777 " + Impresora;;
     |SYSTEM aAlfa1;
|FPRC;

|PROCESO traete_fichero;
     |FINIMP;
     cOrigen = aDir + Usuario;
     cDestino = aDirDes;
     |QBF cDestino;
     aArchivo = aArchivo - ".txt";
     cDestino = cDestino + aArchivo;
     |COPIA_FICHERO cOrigen, cDestino;
     |SI FSalida ! 0;
          nErrorInf = 0;
     |FINSI;
     aAlfa1 = "chmod 777 " + cDestino;
     |SYSTEM aAlfa1;
|FPRC;

|PROCESO ConviertePdf;
     |DBASS aBaseBin;  |QBF aBaseBin;
     aBaseBin = aBaseBin + "bin/topdf.sh ";

     aSystem = aBaseBin + cDestino;
     |SYSTEM aSystem;

     aSystem = "chmod 777 " + cDestino + ".pdf";
     |SYSTEM aSystem;

     |FBORRA cDestino;

     ||SI #0#6 = "S";  |HAZ AlaWeb;  |FINSI;
     ||SI #0#5 = "S";  |HAZ Correo;  |FINSI;

     ||FBORRA aPdf;
|FPRC;
-------------------------------------------------------------------------------
|PROCESO LeeParam;
     |DBASS aRutaDes;
     |DBASS aRutaDatos;
     |QBF aRutaDes;
     |QBF aRutaDatos;
     aRutaDes = aRutaDes + "dsnetcom/def/dsnetm00.mas";
     |CARGA_DEF aRutaDes, dsnetm00@;
     |SI FSalida ! 0;
          |MENAV "    Error en CARGADEF: dsnetm00";
     |SINO;
          aRutaDatos = aRutaDatos + "dsnetcom/fich/";
          |PATH_DAT #100 aRutaDatos;
          |ABRE #100;
          |LEE 000#100p;
          |SI FSalida = 0;
               aDirDes = #100#7;
               aDirOri = aDirDes;
          |SINO;
               |MENAV "    Error de datos en fichero de configuracion dsnetm00";
          |FINSI;
          |CIERRA #100;
     |FINSI;
     |DESCARGA_DEF #dsnetm00@;
|FPRC;

|PROCESO LeeOrigen;

     aArchivo = PARAMETRO;
     |QBF aDirOri;
     aRuta = aDirOri + aArchivo;

     |XABRE "U", aRuta, nCanal;
     |SI FSalida ] 0;
          |XLEE nCanal, 100, aCabecera;
          |XLEE nCanal,   5, aTipoResumen;
          |XLEE nCanal,   5, aTipoNomina;
          |XLEE nCanal,  10, aEmpresa;
          |XLEE nCanal,  10, aAnyoDesde;
          |XLEE nCanal,  10, aAnyoHasta;
          |XLEE nCanal,  10, aMesDesde;
          |XLEE nCanal,  10, aMesHasta;
          |XLEE nCanal,   5, aOrdenacion;
     |FINSI;
     |XCIERRA nCanal;
     |QBF aCabecera;
     |QBF aTipoResumen;
     |QBF aTipoNomina;
     |QBF aEmpresa;
     |QBF aAnyoDesde;
     |QBF aAnyoHasta;
     |QBF aMesDesde;
     |QBF aMesHasta;
     |QBF aOrdenacion;
     nEmpresa = aEmpresa;
     nAnyoDesde = aAnyoDesde;
     nAnyoHasta = aAnyoHasta;
     nMesDesde = aMesDesde;
     nMesHasta = aMesHasta;
     nOrdenacion = aOrdenacion;
|FPRC;

|PROCESO PasaParametros;

     #0#0 = nEmpresa;
     #0#1 = nEmpresa;
     #0#2 = nMesDesde;
     #0#3 = ~;
     #0#4 = 0;           || tipo de nomina: el cero, imagino
     |SI nAnyoDesde ] 2000;
          #0#5 = nAnyoDesde - 2000;
     |SINO;
          #0#5 = nAnyoDesde - 1900;
     |FINSI;
     #0#6 = "N";
     #0#7 = nOrdenacion;
     #0#8 =  1;          || desde centro
     #0#9 = 99;          || hasta centro
     #0#10 = "H";        || Fuente de Datos: Historico
     #0#11 = "EUROS  ";

|FPRC;

|PROCESO CamposRegistro;
     |ABRE #nomcontr; |LEE 000#nomcontr.p; |CIERRA #nomcontr;
     |SI #nomcontr#56 = "P";
          |CAMPO_MODIFICA #0#22, 1, "S";
          |CAMPO_VISUAL #0#23, 1, "S";
          #0#23 = "";
          |PINTA 2216, "³ Guardar emisi¢n .....:                       ³";
          |PINTA #0#22;
          |PINTA #0#23;
     |SINO;
          |CAMPO_MODIFICA #0#22, 1, "N";
          |CAMPO_VISUAL #0#23, 1, "N";
          #0#22 = "N";
          #0#23 = "";
          |PINTA 2216, "³                                              ³";
     |FINSI;
|FPRC;

|PROCESO Activa22; |TIPO 7;
     |ABRE #nomcontr; |LEE 000#nomcontr.p; |CIERRA #nomcontr;
     |SI #nomcontr#56 = "P";
          |SI #0#20 = 00; |O #0#20 = 03;
               |CAMPO_MODIFICA #0#22, 1, "S";
          |SINO;
               #0#22 = "N";
               |PINTA #0#22;
               |CAMPO_MODIFICA #0#22, 1, "N";
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Activa23; |TIPO 7;
     |SI #0#22 = "S";
          |CAMPO_MODIFICA #0#23, 1, "S";
     |SINO;
          #0#23 = "";
          |PINTA #0#23;
          |CAMPO_MODIFICA #0#23, 1, "N";
     |FINSI;
|FPRC;

|PROGRAMA;
     |SI aParametro ! "";
          PARAMETRO = aParametro;
     |FINSI;
     |SI PARAMETRO ! "M"; |Y PARAMETRO ! "H";
          |HAZ LeeParam;
          |HAZ LeeOrigen;
          |HAZ PasaParametros;
          swDesdeWeb = 1;
          |HAZ imprime;
          |HAZ traete_fichero;
          |HAZ ConviertePdf;
     |SINO;
          |CLS;
          |CABEZA "INFORME RESUMEN DE PAGAS EXTRAS POR CENTROS";

          |ABRE #0;
          |DDEFECTO #0;
          |LEE 000#0p;
          FEstadoDef = FSalida;

          #0#3 = ~;
          |PINPA #0#0;
          |PINDA #0#0;
          |HAZ CamposRegistro;
          |ENDATOS #1#0;

          |SI FEstadoDef = 0;
               |GRABA 020#0a;
          |SINO;
               |GRABA 020#0n;
          |FINSI;
          |CIERRA #0;
     |FINSI;
|FPRO;
