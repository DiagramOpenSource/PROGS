|FICHEROS;
     cretap11 #0;
     silcon01;
     labempre;
     labtraba;
     labcentr;
     nomcenes;
     silcon06;
     silcon07;
     silcon15;

     tsilco14,MANTE;     || tmp parrilla empresas
     tsilco16,MANTE;     || tmp parrilla centros
     cretam00;
     cretam01;
     cretam02;
     cretam03;
     cretam04;
     cretat02;
     cretat08;

     cretae02;

     cretam51;
     cretam52;

     cretam53;
     cretat53,MANTE;
|FIN;

|VARIABLES;
     nDesdeMes = 0;
     nHastaMes = 0;
     &nMes = 0;
     &aMes = "";
     &nAny = 0;
     aTmp_tsilco14 = "";
     &aTmp_tsilco16 = "";
     aTmp_cretat02 = "";

     aHora = "";
     fFecha = @;
     aFecha = "";
     aFichero = "";
     &eaFichero = "";
     &aHoraEntrada = "";
     &aFechaEntrada = "";

     aAlfa = "";

     &enSwAlta = 0;
     &enMes = 0;
     &enAny = 0;

     aCentro = "";
     &eaRegimen = "";

     &aCadena = "";
     nNivel = 0;
     swSinReturn = 0;
     nHandle = 0;
     aValor = "";
     aTipoFichero = "";
     aOrigen = "";
     &eaTipoXML = "";
     &eaReferencia = "";
     aCadenaSal = "";
     aLongitud = "";
     nLongitud = 0;
     nPos = 0;
     nBusca = 0;
     aBusca = "";
     aCaracter = "";
     aCambia = "";
     aRuta = "";
     aDestino = "";
     aIP = "";

     nNume1 = 0;
     nNume2 = 0;

     nEmpresas = 0;
     nCentros = 0;
     nCCCs = 0;
     nAntCentros = 0;

     aAlfa0 = "";
     aAlfa1 = "";
     aAlfa2 = "";
     aAlfa3 = "";
     aAlfa4 = "";
     alfa4 = "";
     nNume = 0;
     aLon = "";
     nLon = 0;
     nEspacio = 0;
     aEspacio = "";
     nBlancos = 0;
     nBlancosIzd = 0;
     nBlancosDer = 0;
     aBlancosIzd = "";
     aBlancosDer = "";
     aLinea = "";

     &efFecha = @;
     &eaHora = "";
     FEstado = 0;
     aParam = "";
     aCCC = "";

     &enEmp = 0;
     &enCen = 0;
     swLabcentr = 0;
     swNomcenes = 0;
     &enTipo = 0;

     aAutorizado = "";
     swCompAut = 0;
     aAutEmp = "";
     aAntAut = "";
     nEmpOtraAut = 0;
     swAvisoAut = 0;

     nDesdeMesAtrasos = 0;
     nHastaMesAtrasos = 0;
     nAnyAtrasos = 0;
     aIPF = "";
     aEsp = "";
     aExt = "";

     nCampo = 0;
     swSal = 0;
     nMesSilcon = 0;
     nAnySilcon = 0;
     swAlguno = 0;
     nAntEmp = 0;
     nAntEmpAtr = 0;
     swSigue = 0;
     swStop = 0;
     &aFechaIniParam = "";
     &aFechaFinParam = "";
     swBorradorL13For = 0;
|FIN;

/****************************************************************************/
/********************   PROCESOS DESDE LOS CAMPOS DE PANTALLA ***************/
/****************************************************************************/

|PROCESO AyudaTipos; |TIPO 7;
     |PUSHV 08542380;
     |ATRI R;
     ||            5     6         7
     ||            45678901234567890123456789
     |PINTA 0854, "       - Opciones -       ";
     |PINTA 0954, " 00 :Normal               ";
     |PINTA 1054, " 02 :Finiquitos           ";
     |PINTA 1154, "5-20:Atrasos              ";
     |PINTA 1254, " 90 :Complementarias      ";
     |PINTA 1354, "                          ";
     |PINTA 1454, "                          ";
     |ATRI 0;
|FPRC;

|PROCESO TipoSolicitud; |TIPO 7;
|ACABA;
     |PUSHV 07541780;
     |ATRI R;
               ||  5     6         7        8
               ||  56789012345678901234567890
     |PINTA 0855, "   TIPOS DE SOLICITUDES   ";
     |PINTA 0955, "   --------------------   ";
     |PINTA 1055, " 1. Trabajadores y tramos ";
     |PINTA 1155, " 2. Borrador              ";
     |PINTA 1255, " 3. Confirmaci¢n          ";
     |PINTA 1355, " 4. C lculos              ";
     |PINTA 1455, "                          ";
     |ATRI 0;
|FPRC;

|PROCESO Restaura;
     |POPV;
|FPRC;

|PROCESO PintaSol;
     |ATRI R;
     |SI #0#5 = " ";
          aAlfa = "                                  ";
     |FINSI;
     ||                      1         2         3
     ||             123456789012345678901234567890123456789
     |SI #0#5 = "1";
          aAlfa = " SOLICITUD DE TRABAJADORES Y TRAMOS ";
          |PINTA 0621, aAlfa;
          |CAMPO_MODIFICA #0#6, 1, "N";
          |ATRI 0;
          |PINTA 0921, "                          ";
     |FINSI;
     |SI #0#5 = "2";
          aAlfa = " SOLICITUD DE BORRADOR ";
          |PINTA 0628, aAlfa;
     |FINSI;
     |SI #0#5 = "3";
          aAlfa = " SOLICITUD DE CONFIRMACION ";
          |PINTA 0626, aAlfa;
     |FINSI;
     |SI #0#5 = "4";
          aAlfa = " SOLICITUD DE CALCULOS ";
          |PINTA 0628, aAlfa;
     |FINSI;
     |SI #0#5 = "5";
          aAlfa = " COMUNICACION DE DATOS BANCARIOS ";
          |PINTA 0622, aAlfa;
          |CAMPO_MODIFICA #0#0, 1, "N";
          |CAMPO_MODIFICA #0#1, 1, "N";
          |CAMPO_MODIFICA #0#6, 1, "N";
          |ATRI 0;
          |PINTA 0818, "                                          ";
          |PINTA 0918, "                                          ";
          |PINTA 1018, "                                          ";
          |PINTA 1118, " ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿";
     |FINSI;
     |ATRI 0;
|FPRC;

|PROCESO PintaDescAtrasos;
     |SI #0#5 = "5";
          |ACABA;
     |FINSI;
     |SI #0#6 ] 5; |Y #0#6 [ 20;
          |PINTA 0921, "FECHA DE CONTROL.A¤o:";
          |PINTA 1021, "FECHA DE CONTROL.Mes:";
          |PINTA 1121, "A¤o Atrasos ........:";
          |CAMPO_MODIFICA #0#7, 1, "S";
          |CAMPO_VISUAL #0#7, 1, "S";
          #0#7 = #0#0;
          |PINTA #0#7;
     |SINO;
          |PINTA 0921, "A¤o ................:";
          |PINTA 1021, "Mes ................:";
          |CAMPO_MODIFICA #0#7, 1, "N";
          |CAMPO_VISUAL #0#7, 1, "N";
          |PINTA 1121, "                                 ";
     |FINSI;
|FPRC;

|PROCESO AyudaFecha; |TIPO 7;
     |SI #0#6 ] 5; |Y #0#6 [ 20;
          |SI Campo = 0;
               |MENSA "    Introduzca el año de la FECHA DE CONTROL con la que se envió la liquidación";
          |FINSI;
          |SI Campo = 1;
               |MENSA "    Introduzca el mes de la FECHA DE CONTROL con la que se envió la liquidación";
          |FINSI;
     |SINO;
          |SI Campo = 0;
               |MENSA "    Año de la liquidación de la que se realiza la solicitud";
          |FINSI;
          |SI Campo = 1;
               |MENSA "    Mes del que se realiza la solicitud";
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO PintaAyuda11;
||             0      1         2         3         4         5         6         7
||             3456789012345678901234567890123456789012345678901234567890123456789012345678
     |SI #0#5 = "1";
     aAlfa1 =       "l Fichero de Trabajadores y Tramos, que el usuario enviar  a trav‚s de";
     aAlfa2 = "SILTRA para obtener la informaci¢n de la que la TGSS dispone de los trabaja-";
     aAlfa3 = "dores que se incluir n en la liquidaci¢n.                                   ";
     |FINSI;
     |SI #0#5 = "2";
     aAlfa1 =       "l Borrador de la liquidaci¢n. El usuario enviar  este fichero a trav‚s";
     aAlfa2 = "de SILTRA para obtener el borrador y la liquidaci¢n, que podr  ser confirma-";
     aAlfa3 = "da posteriormente a trav‚s de la Solicitud de Confirmaci¢n si es v lida.    ";
     |FINSI;
     |SI #0#5 = "3";
     aAlfa1 =       " Confirmaci¢n de la liquidaci¢n. El usuario enviar  esta solicitud me-";
     aAlfa2 = "diante SILTRA para confirmar el pago de las liquidaciones a Tesorer¡a de los";
     aAlfa3 = "seguros sociales.                                                           ";
     |FINSI;
     |SI #0#5 = "4";
     aAlfa1 =       " C lculos de las liquidaciones.  El usuario enviar  esta solicitud me-";
     aAlfa2 = "diante SILTRA para  solicitar el detalle por  trabajador de los c lculos que";
     aAlfa3 = "se han realizado por parte de Tesorer¡a para elaborar las liquidaciones.    ";
     |FINSI;
     |SI #0#5 = "5";
     aAlfa0 =                                                                      "Comuni-";
     aAlfa1 = "caci¢n de Datos Bancarios. Una vez generado el fichero XML,el usuario lo en-";
     aAlfa2 = "viar  mediante SILTRA para comunicar las cuentas de los CCCs que usen adeudo";
     aAlfa3 = "en cuenta como sistema de pago de los seguros sociales.                     ";
     |FINSI;
     aAlfa4 = "ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ";

     nNume = #0#5;
     |SI nNume [ 4;
          |PINTA 2009, aAlfa1;
          |PINTA 2103, aAlfa2;
          |PINTA 2203, aAlfa3;
          |PINTA 2301, aAlfa4;
     |SINO;
          |PINTA 1972, aAlfa0
          |PINTA 2003, aAlfa1;
          |PINTA 2103, aAlfa2;
          |PINTA 2203, aAlfa3;
          |PINTA 2301, aAlfa4;
     |FINSI;
|FPRC;

|PROCESO Defectos;
     fFecha = ~;
     aAlfa = fFecha;
     aAlfa = aAlfa % 402;
     nMes = aAlfa;

     aAlfa = fFecha;
     aAlfa = aAlfa % -104;
     nAny = aAlfa;

     |SI nMes = 1;
          nAny = nAny - 1;
          nMes = 12;
     |SINO;
          nMes = nMes - 1;
     |FINSI;

     #0#0 = nAny;
     #0#1 = nMes;

     |PINTA #0#0;
     |PINTA #0#1;
|FPRC;

|PROCESO pintames;
     |SI #0#5 = "5";
          |ACABA;
     |FINSI;
     nMes = #0Campo;
     |HAZ MesLetra_plb;
     |ATRI I;
     |PINTA 1046, aMes;
     |ATRI 0;
|FPRC;

|PROCESO DiaHora;
     |HORA aHora;
     fFecha = ~;
     aFecha = fFecha;

     efFecha = fFecha;
     eaHora = aHora;

     aHoraEntrada = aHora - ":" - ":";
     aFechaEntrada = aFecha - "." - ".";
|FPRC;

|PROCESO Fichero;
     aAlfa = "";
     |SI #0#5 = "1";
          aAlfa = "SOLTRA";
     |FINSI;
     |SI #0#5 = "2";
          aAlfa = "SOLBOR";
     |FINSI;
     |SI #0#5 = "3";
          aAlfa = "SOLCON";
     |FINSI;
     |SI #0#5 = "4";
          aAlfa = "SOLCAL";
     |FINSI;
     |SI #0#5 = "5";
          aAlfa = "COMDAT";
     |FINSI;

     |SI aAlfa = "";
          aAlfa = "    Debe elegir un tipo de solicitud para poder continuar.";
          |MENAV aAlfa;
          |ERROR;
          |ACABA;
     |FINSI;

     aAlfa1 = #0#1;
     |QBF aAlfa1;
     aAlfa1 = ("00" + aAlfa1) % -102;
     aAlfa2 = #0#0;

     |SI #0#5 = "5";
          aFichero = aAlfa + "_" + aFechaEntrada + "_" + aHoraEntrada + ".xml"
     |SINO;
          aFichero = aAlfa + "_" + aAlfa2 + aAlfa1 + "_" + aFechaEntrada + "_" + aHoraEntrada + ".xml"
     |FINSI;
     eaFichero = aFichero;

     #0#3 = aFichero;
     |PINTA #0#3;
|FPRC;

/****************************************************************************/
/*************  PROCESOS PARA ELABORAR LA PARRILLA DE SELECCION  ************/
/****************************************************************************/

|PROCESO EmpresasTMP_Ctas;
     #labempre#0 = #cretam53#0;
     |LEE 000#labempre.=;
     |SI FSalida = 0;
          |DDEFECTO #tsilco14;
          #tsilco14#0 = #cretam53#0;
          #tsilco14#1 = #cretam53#2;
          #tsilco14#2 = "";
          |LEE 101#tsilco14.=;
          |SI FSalida ! 0
               |GRABA 020#tsilco14.n;
          |FINSI;
          |LIBERA #tsilco14;
     |FINSI;
|FPRC;

|PROCESO BuscaLiq;
     enSwAlta = 1;
     |ERROR10;
|FPRC;

|DEFBUCLE BuscaLiq;
     #cretam04#1;
     ;
     #cretam01#0, #0#0, #0#1, enTipo, "               ", "            ", 01;
     #cretam01#0, #0#0, #0#1, enTipo, "zzzzzzzzzzzzzzz", "zzzzzzzzzzzz", 99;
     ;
     NULL, BuscaLiq;
|FIN;

|PROCESO CompTraEmp;
     enSwAlta = 0;
     |BUCLE BuscaLiq;
|FPRC;

|PROCESO EmpresasTMP;
     nDesdeMes = #0#1;
     nHastaMes = #0#1;
     nAny = #0#0;

     |SI enTipo < 05; |O enTipo = 90;
          #labempre#0 = #cretam01#0;
     |SINO;
          #labempre#0 = #cretam02#0;
     |FINSI;
     |LEE 000#labempre.=;
     |SI FSalida = 0;
          |SI enTipo < 05; |O enTipo = 90;
               |HAZ CompTraEmp;    || doble comprobacion, tampoco esta de mas
                                   || en atrasos paso, de hecho no hace falta
          |SINO;
               enSwAlta = 1;
          |FINSI;
          |SI enSwAlta = 1;
               |DDEFECTO #tsilco14;
               #tsilco14#0 = #labempre#0;
               #tsilco14#1 = #labempre#2;
               |LEE 101#tsilco14.=;
               |SI FSalida ! 0
                    #tsilco14#2 = " ";
                    |SI 05 [ enTipo; |Y enTipo [ 20;
                         #tsilco14#5 = #cretam02#17;
                         #tsilco14#6 = #cretam02#18;
                         #tsilco14#7 = #cretam02#19;
                    |FINSI;
                    |GRABA 020#tsilco14.n;
               |FINSI;
          |FINSI;
          |LIBERA #tsilco14;
     |FINSI;
|FPRC;

|DEFBUCLE EmpresasTMP_Ctas;
     #cretam53#1;
     ;
     00001, "               ";
     99999, "zzzzzzzzzzzzzzz";
     ;
     NULL, EmpresasTMP_Ctas;
|FIN;

|| la fecha de control me la guardo en el cretam02 y 03, asi que en atrasos el
|| bucle va sobre el 02

|DEFBUCLE EmpresasTMP_Atr;
     #cretam02#1;
     19, 20, 21;
     00001, 2000, 01, enTipo, "               ", #0#7, #0#1, #0#0;
     99999, 2999, 12, enTipo, "zzzzzzzzzzzzzzz", #0#7, #0#1, #0#0;
     ;
     NULL, EmpresasTMP;
|FIN;

|DEFBUCLE EmpresasTMP;
     #cretam01#1;
     ;
     00001, #0#0, #0#1, enTipo;
     99999, #0#0, #0#1, enTipo;
     ;
     NULL, EmpresasTMP;
|FIN;

|PROCESO min14;
    #tsilco14#0 = 00001;
|FPRC;

|PROCESO max14;
    #tsilco14#0 = 99999;
|FPRC;

|PROCESO parrilla;
     enTipo = #0#6;
     |SI FSalida = 0; |Y #0#2 = "S";
          |ATRI P;   |MENSA "    Cargando seleccion ...";    |ATRI 0;

          |SI enTipo < 05; |O enTipo = 90;
               |SI #0#5 = "5";
                    |BUCLE EmpresasTMP_Ctas;
               |SINO;
                    |BUCLE EmpresasTMP;
               |FINSI;
          |SINO;
               |BUCLE EmpresasTMP_Atr;
          |FINSI;

          |ATRI R;
          |BLIN 4;
          |ATRI 0;

          |PUSHV 0501 2380;
          |ENTLINEAL #1#tsilco14, 1, 4, 22, 1, min14, max14;
          |POPV;
     |FINSI;
|FPRC;

/****************************************************************************/
/******        GRABACION DEL TEMPORAL DE CCCs A ENVIAR   ********************/
/****************************************************************************/

|PROCESO cretam03;
     enSwAlta = 1;
     nDesdeMesAtrasos = #cretam03#17;
     nHastaMesAtrasos = #cretam03#18;
     nAnyAtrasos = #cretam03#19;
     |ERROR10;
|FPRC;

|DEFBUCLE cretam03_Atr;
     #cretam03#1;
     19, 20, 21;
     #labempre#0, 2000, 01, enTipo, aCCC, "            ", #0#7, #0#1, #0#0;
     #labempre#0, 2999, 12, enTipo, aCCC, "zzzzzzzzzzzz", #0#7 ,#0#1, #0#0;
     ;
     NULL, cretam03;
|FIN;

|DEFBUCLE cretam03;
     #cretam03#1;
     ;
     #labempre#0, #0#0, #0#1, enTipo, aCCC, "            ";
     #labempre#0, #0#0, #0#1, enTipo, aCCC, "zzzzzzzzzzzz";
     ;
     NULL, cretam03;
|FIN;

|PROCESO CargaCtas;
     |PARA nCampo = 0; |SI nCampo [ 11; |HACIENDO nCampo = nCampo + 1;
          #cretat53#nCampo# = #cretam53#nCampo#;
          |SI nCampo = 1;
               aAlfa1 = #cretam53#10; |QBF aAlfa1; aAlfa1 = ("0000" + aAlfa1) % -104;

               |SI aAlfa1 = "0613";
                    aAlfa1 = "0163";
               |FINSI;

               aAlfa2 = #cretam53#1; |QBF aAlfa2;
               aAlfa = aAlfa1 + aAlfa2;
               #cretat53#nCampo# = aAlfa;
          |FINSI;
     |SIGUE;
     |GRABA 020#cretat53.n;
     |LIBERA 020#cretat53;

     |SI #0#2 = "N";
          || si no, si pongo CON seleccion pasa dos veces
          |SI nAntEmp ! #cretat53#0;
               nEmpresas = nEmpresas + 1;
          |FINSI;
     |FINSI;
     |SI #0#2 = "N";
          || lo mismo
          nCCCs = nCCCs + 1;
     |FINSI;
     nCentros = nCentros + 1;
     nAntEmp = #cretat53#0;
|FPRC;

|PROCESO centros;
/*
     || esto es de antes, de cuando se hacia el bucle desde el centro
     || no tiene sentido, las liquidaciones las busco en el cretam02
     || antes lo que haciamos era buscar por todos los centros, y dentro
     || de ellos buscaba en el cretam03 (desde labcentr y nomcenes)

     enSwAlta = 0;
     |SI swLabcentr = 1;
          enEmp = #labcentr#0;
          enCen = #labcentr#1;
          aAlfa = #labcentr#10;
     |FINSI;
     |SI swNomcenes = 1;
          enEmp = #nomcenes#0;
          enCen = #nomcenes#13;
          aAlfa = #nomcenes#10;
     |FINSI;

     |QBT aAlfa;

     |SI aAlfa = "";
          aAlfa = #labempre#13;
          |QBT aAlfa;
     |FINSI;
     aAlfa = aAlfa % -111;

     enMes = #0#1;
     enAny = #0#0;

     |HAZ RegimenCCC_plb;
     aCCC = eaRegimen + aAlfa;
*/

     |SI #0#2 = "S";                    || seleccion CI
     |Y #tsilco14#8 = "*";              || he entrado a seleccion centros
          #tsilco16#0 = #cretam02#0;
          #tsilco16#1 = #cretam02#5;
          |LEE 000#tsilco16.=;
          |SI FSalida = 0; |Y #tsilco16#3 = "*";
               || adelante
          |SINO;
               |ACABA;
          |FINSI;
     |FINSI;

     enEmp = #cretam02#0;
     enCen = #cretam02#5;
     aCCC = #cretam02#4;

     || para comprobar si tengo trabajadores con la liquidacion calculada
     || para mandar la solicitud

     enSwAlta = 0;
     |SI enTipo < 5; |O enTipo = 90;
          |BUCLE cretam03;
     |SINO;
          |BUCLE cretam03_Atr;
     |FINSI;

     |SI #0#5 = "5";
          || envio de cuentas, para que lo cuente siempre
          enSwAlta = 1;
     |FINSI;

     |SI enSwAlta = 1;
          aCentro = enCen;
          |QBF aCentro;
          aCentro = ("000" + aCentro) % -103;

          #cretat02#0 = enEmp;
          #cretat02#1 = aCCC;
          #cretat02#7 = enTipo;
          |LEE 101#cretat02.=;
          |SI FSalida = 0;
               aAlfa = #cretat02#3;
               |QBF aAlfa;
               aAlfa1 = aAlfa - aCentro;
               |SI aAlfa1 = aAlfa;
                    || quiere decir que el centro no estaba en la lista
                    #cretat02#3 = aAlfa + " " + aCentro;
               |SINO;
                    || para que no lo cuente de mas despues
                    nCentros = nCentros - 1;
               |FINSI;
               |GRABA 020#cretat02.a;
          |SINO;
               #cretat02#2 = #labempre#2;
               #cretat02#3 = aCentro;
               |SI enTipo < 05; |O enTipo = 90;
                    || si no es atrasos tambien se usa despues, no quitar
                    #cretat02#4 = #0#1;
                    #cretat02#5 = #0#1;
                    #cretat02#6 = #0#0;
               |SINO;
                    #cretat02#4 = nDesdeMesAtrasos;
                    #cretat02#5 = nHastaMesAtrasos;
                    #cretat02#6 = nAnyAtrasos;
               |FINSI;
               |GRABA 020#cretat02.n;
               nCCCs = nCCCs + 1;
          |FINSI;
          nCentros = nCentros + 1;
          |LIBERA #cretat02;
     |FINSI;
|FPRC;

|DEFBUCLE cretam02_atrasos;
     #cretam02#1;
     19, 20, 21;
     #labempre#0, 2000, 01, enTipo, "               ", #0#7, #0#1, #0#0;
     #labempre#0, 2999, 12, enTipo, "zzzzzzzzzzzzzzz", #0#7, #0#1, #0#0;
     ;
     NULL, centros;
|FIN;

|DEFBUCLE cretam02_normal;
     #cretam02#1;
     ;
     #labempre#0, #0#0, #0#1, enTipo, "               ";
     #labempre#0, #0#0, #0#1, enTipo, "zzzzzzzzzzzzzzz";
     ;
     NULL, centros;
|FIN;

|DEFBUCLE nomcenes;
     #nomcenes#1;
     ;
     #labempre#0, 001, "87";
     #labempre#0, 999, "87";
     ;
     NULL, centros;
|FIN;

|DEFBUCLE labcentr;
     #labcentr#1;
     ;
     #labempre#0, 001;
     #labempre#0, 999;
     ;
     NULL, centros;
|FIN;

|DEFBUCLE ConSeleccion_Ctas;
     #cretam53#1;
     9;
     #tsilco14#0, "               ", "S";
     #tsilco14#0, "zzzzzzzzzzzzzzz", "S";
     ;
     NULL, CargaCtas;
|FIN;

|PROCESO Empresa;
     |SI #0#2 = "S";
          #labempre#0 = #tsilco14#0;
     |SINO;
          |SI enTipo ] 5; |Y enTipo ! 90;
               |SI nAntEmpAtr = #cretam02#0;
                    |ACABA;
               |FINSI;
          |FINSI;

          |SI #0#5 = "5";
               #labempre#0 = #cretam53#0;
          |SINO;
               |SI enTipo ] 5; |Y enTipo ! 90;
                    #labempre#0 = #cretam02#0;
                    nAntEmpAtr = #cretam02#0;
               |SINO;
                    #labempre#0 = #cretam01#0;
               |FINSI;
          |FINSI;
     |FINSI;
     |LEE 000#labempre.=;
     nEmpresas = nEmpresas + 1;

     |SI #0#5 = "5";
          |BUCLE ConSeleccion_Ctas;

          || se hace diferente en el envio de cuentas, logicamente, ya que aqui
          || no hay liquidaciones calculadas de ningun mes

          swLabcentr = 1; swNomcenes = 0;
          |BUCLE labcentr;
          swLabcentr = 0; swNomcenes = 1;
          |BUCLE nomcenes;
     |SINO;
          |SI enTipo ] 5; |Y enTipo ! 90;
               |BUCLE cretam02_atrasos;
          |SINO;
               |BUCLE cretam02_normal;
          |FINSI;
     |FINSI;

     /*
     || esto es de antes, de cuando se hacia el bucle desde el centro
     || no tiene sentido, las liquidaciones las busco en el cretam02
     || antes lo que haciamos era buscar por todos los centros, y dentro
     || de ellos buscaba en el cretam03 (desde labcentr y nomcenes)
     || estos son los bucles

     migue
     swLabcentr = 1; swNomcenes = 0;
     |BUCLE labcentr;
     swLabcentr = 0; swNomcenes = 1;
     |BUCLE nomcenes;
     */

     |SI nCentros = nAntCentros;
          || el numero de centros no ha aumentado tras mirar que centros habia
          || que meter, con lo que de esta empresa no habia ningun centro
          || a incluir en el XML

          || no para fichero de datos bancarios
          |SI #0#5 ! "5";
               nEmpresas = nEmpresas - 1;
          |FINSI;
     |FINSI;

     nAntCentros = nCentros;
|FPRC;

|DEFBUCLE ConSeleccion;
     #tsilco14#1;
     2;
     00001, "*";
     99999, "*";
     ;
     NULL, Empresa;
|FIN;

|DEFBUCLE SinSeleccion_Ctas;
     #cretam53#1;
     9;
     00001, "               ", "S";
     99999, "zzzzzzzzzzzzzzz", "S";
     ;
     NULL, CargaCtas;
|FIN;

|DEFBUCLE SinSeleccion_Atr;
     #cretam02#1;
     19, 20, 21;
     00001, 2000, 01, enTipo, "               ", #0#7, #0#1, #0#0;
     99999, 2999, 12, enTipo, "zzzzzzzzzzzzzzz", #0#7, #0#1, #0#0;
     ;
     NULL, Empresa;
|FIN;

|DEFBUCLE SinSeleccion;
     #cretam01#1;
     ;
     00001, #0#0, #0#1, enTipo;
     99999, #0#0, #0#1, enTipo;
     ;
     NULL, Empresa;
|FIN;

/****************************************************************************/
/***************** TRASPASO DE CUENTAS BANCARIAS AL CRETAM53 ****************/
/****************************************************************************/

|PROCESO ipf_p11;
     aAlfa1 = aValor;
     aAlfa1 = aAlfa1 % 101;
     |SI aAlfa1 ] "0";|Y aAlfa1 [ "9";
          aIPF = "1";            || tipo identificacion DNI
     |SINO;
          |SI aAlfa1 ! "X"; |Y aAlfa1 ! "Y"; |Y aAlfa1 ! "Z";
               aIPF = "9";        || tipo identificacion CIF
          |SINO;
               aIPF = "6";        || tipo identificacion NIE
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO VerificaIBANCCC;
     aAlfa1 = ""; aAlfa2 = "";
     aAlfa1 = #labempre#135;
     |SI swLabcentr = 1;
          aAlfa2 = #labcentr#28;
     |FINSI;
     |SI swNomcenes = 1;
          #labcentr#0 = #nomcenes#0;
          #labcentr#1 = #nomcenes#13;
          |LEE 000#labcentr.=;
          |SI FSalida = 0;
               aAlfa2 = #labcentr#28;
          |FINSI;
     |FINSI;
     |QBT aAlfa1; |QBT aAlfa2;
     |SI aAlfa1 = ""; |Y aAlfa2 = "";
          swStop = 1;
     |FINSI;

     aAlfa = "";
     |SI swLabcentr = 1; aAlfa = #labcentr#10; |FINSI;
     |SI swNomcenes = 1; aAlfa = #nomcenes#10; |FINSI;
     |QBT aAlfa;
     |SI aAlfa = "";
          aAlfa = #labempre#13;
          |QBT aAlfa;
     |FINSI;
     |SI aAlfa = "";
          swStop = 1;
     |FINSI;
|FPRC;

|PROCESO ActCuenta;
     aAlfa1 = ""; aAlfa2 = ""; aAlfa3 = "";

     aAlfa1 = #cretam53#8;
     |QBF aAlfa1;

     |SI swNomcenes = 1;
          #labcentr#0 = #nomcenes#0;
          #labcentr#1 = #nomcenes#13;
          |LEE 000#labcentr.=;
          |SI FSalida = 0;
               aAlfa2 = #labcentr#28;
               |QBF aAlfa2;
          |FINSI;
     |SINO;
          aAlfa2 = #labcentr#28;
          |QBF aAlfa2;
     |FINSI;

     aAlfa3 = #labempre#135;
     |QBF aAlfa3;

     |SI aAlfa2 ! "";
          #cretam53#8 = aAlfa2;
          #cretam53#11 = "C";
     |SINO;
/*
          || esto no entiendo por que estaba, era un caso de ADADE
          |SI aAlfa1 = "";
               || si no estaba en blanco no lo machaco con el de la empresa
               || 2884.3425
          |FINSI;
*/
          |SI aAlfa3 ! "";
          |Y #cretam53#11 ! "C";        || nunca machaques con el IBAN de la
                                        || empresa, uno que estuviera grabado
                                        || de un centro
               #cretam53#8 = aAlfa3;
               #cretam53#11 = "E";
          |FINSI;
     |FINSI;

     |GRABA 020#cretam53.a;
|FPRC;

|PROCESO centros_cuentas;
     |SI swLabcentr = 1;
          enEmp = #labcentr#0;
          enCen = #labcentr#1;
          aAlfa = #labcentr#10;
     |FINSI;
     |SI swNomcenes = 1;
          enEmp = #nomcenes#0;
          enCen = #nomcenes#13;
          aAlfa = #nomcenes#10;
     |FINSI;
     |QBT aAlfa;

     |SI aAlfa = "";
          aAlfa = #labempre#13;
          |QBT aAlfa;
     |FINSI;
     aAlfa = aAlfa % -111;
     aCCC = aAlfa;

     eaRegimen = "0000";

     || primero el silcon06
     #silcon06#0= enEmp;
     |LEE 000#silcon06.=;
     |SI FSalida = 0;
          eaRegimen = #silcon06#2;
          |QBF eaRegimen;
          eaRegimen = ("0000" + eaRegimen) % -104;
     |FINSI;

     || despues el silcon15
     #silcon15#0 = enEmp;
     #silcon15#1 = enCen;
     |LEE 000#silcon15.=;
     |SI FSalida = 0;
          eaRegimen = #silcon15#2;
          |QBF eaRegimen;
          eaRegimen = ("0000" + eaRegimen) % -104;
     |FINSI;

     || o si no, busco regimen de trab. en alta los ultimos 3 a¤os
     |SI eaRegimen = "0000";
          fFecha = ~;
          aFechaFinParam = fFecha;

          nNume = fFecha;
          nNume = nNume - 1095;           || los ultimos tres a¤os desde hoy
          fFecha = nNume;
          aFechaIniParam = fFecha;

          enMes = nMes;
          enAny = nAny;

          |HAZ RegimenCCC_plb;
     |FINSI;

     |SI eaRegimen = "0613";
          eaRegimen = "0163";
     |FINSI;

     |SI eaRegimen = "0000";
          eaRegimen = "0111";
     |FINSI;

     || aCCC = eaRegimen + aCCC;
     || el Regimen DESPUES

     enSwAlta = 1;

     |SI enSwAlta = 1;
          aCentro = enCen;
          |QBF aCentro;
          aCentro = ("00" + aCentro) % -102;
          |SI swNomcenes = 1;
               aCentro = aCentro + "F";
          |FINSI;

          |DDEFECTO #cretam53;
          #cretam53#0 = enEmp;
          #cretam53#1 = aCCC;
          |LEE 101#cretam53.=;
          |SI FSalida = 0;
               aAlfa = #cretam53#3;
               |QBF aAlfa;
               aAlfa1 = aAlfa - aCentro;
               |SI aAlfa1 = aAlfa;
                    || quiere decir que el centro no estaba en la lista
                    #cretam53#3 = aAlfa + " " + aCentro;
               |SINO;
                    || ya tengo a este centro en la lista,
                    || compruebo que no haya cambiado nada y me salgo

                    |HAZ ActCuenta;
                    |LIBERA #cretam53;

                    |ACABA;
               |FINSI;
               #cretam53#4 = "Varios Centros";
               |GRABA 020#cretam53.a;
          |SINO;
               #cretam53#2 = #labempre#2;
               #cretam53#3 = aCentro;
               #cretam53#4 = #labempre#2;

               |SI swLabcentr = 1;
                    aAlfa = #labcentr#2;
                    |QBF aAlfa;
                    |SI aAlfa ! "";
                         #cretam53#4 = aAlfa;
                    |FINSI;
               |FINSI;
               |SI swNomcenes = 1;
                    aAlfa = #nomcenes#2;
                    |QBF aAlfa;
                    |SI aAlfa ! "";
                         #cretam53#4 = aAlfa;
                    |FINSI;
               |FINSI;

               |GRABA 020#cretam53.n;
          |FINSI;

          |LEE 101#cretam53.=;     || leo otra vez (no quiero GRABA.c)

          swStop = 0;
          |HAZ VerificaIBANCCC;
          |SI swStop = 1;
               |BORRA 020#cretam53.a;
               |LIBERA #cretam53;
               |ACABA;
          |FINSI;

          aValor = #labempre#10;
          |HAZ ipf_p11;
          #cretam53#5 = aIPF;
          #cretam53#6 = #labempre#10;
          #cretam53#7 = #labempre#2;

          |HAZ ActCuenta;

          #cretam53#9 = "S";
          #cretam53#10 = eaRegimen;

          |GRABA 020#cretam53.a;
          |LIBERA #cretam53;
     |FINSI;
|FPRC;

|DEFBUCLE Carga_nomcenes;
     #nomcenes#1;
     ;
     #labempre#0, 001, "87";
     #labempre#0, 999, "87";
     ;
     NULL, centros_cuentas;
|FIN;

|DEFBUCLE Carga_labcentr;
     #labcentr#1;
     ;
     #labempre#0, 001;
     #labempre#0, 999;
     ;
     NULL, centros_cuentas;
|FIN;

|PROCESO labempre;
     || de momento todas que SI, son las que tienen Creta "SI"
     swSigue = 1;
     |DDEFECTO #silcon06;
     #silcon06#0= #labempre#0;
     |LEE 000#silcon06.=;
     |SI FSalida = 0;
          |SI #silcon06#7 ! "C";
               swSigue = 0;
          |FINSI;
     |FINSI;

     |SI swSigue = 1;
          swLabcentr = 1; swNomcenes = 0;
          |BUCLE Carga_labcentr;
          swLabcentr = 0; swNomcenes = 1;
          |BUCLE Carga_nomcenes;
     |FINSI;
|FPRC;

|DEFBUCLE labempre;
     #labempre#1;
     67;
     00001, "S";
     99999, "S";
     ;
     NULL, labempre;
|FIN;

|PROCESO cretam53;
     aAlfa = #cretam53#1;
     |QBF aAlfa
     aLon = aAlfa % 0;
     nLon = aLon;
     |SI nLon = 15;
          |BORRA 020#cretam53.a;
          |LIBERA #cretam53;

          aAlfa = #cretam53#1;
          aAlfa = aAlfa % 104;
          #cretam53#10 = aAlfa;

          aAlfa = #cretam53#1;
          aAlfa = aAlfa % -111;
          #cretam53#1 = aAlfa;

          |GRABA 020#cretam53.n;
          |LIBERA #cretam53;
     |FINSI;
|FPRC;

|DEFBUCLE cretam53;
     #cretam53#1;
     ;
     00001, "               ";
     99999, "zzzzzzzzzzzzzzz";
     be;
     NULL, cretam53;
|FIN;

|PROCESO TraspasoCuentas;
     |ABRE #cretam53;
     |ABRE #labempre;
     |ABRE #silcon06;
     |ABRE #silcon15;

     |BUCLE cretam53;

     |BUCLE labempre;

     |CIERRA #silcon15;
     |CIERRA #silcon06;
     |CIERRA #labempre;
     |CIERRA #cretam53;
|FPRC;

/****************************************************************************/
/**************            CREACION DEL XML             *********************/
/****************************************************************************/

|PROCESO Linea;
     |QBF aAlfa;
     aCadena = aAlfa;
     |HAZ QuitaRaros;
     |SI nNivel = 1;
          aCadena = aCadena;
     |FINSI;
     |SI nNivel = 2;
          || aCadena = &09 + aCadena;
          aCadena = "  " + aCadena;
     |FINSI;
     |SI nNivel = 3;
          aCadena = "  " + "  " + aCadena;
     |FINSI;
     |SI nNivel = 4;
          aCadena = "  " + "  " + "  " + aCadena;
     |FINSI;
     |SI nNivel = 5;
          aCadena = "  " + "  " + "  " + "  " + aCadena;
     |FINSI;
     |SI nNivel = 6;
          aCadena = "  " + "  " + "  " + "  " + "  " + aCadena;
     |FINSI;
     |SI nNivel = 7;
          aCadena = "  " + "  " + "  " + "  " + "  " + "  " + aCadena;
     |FINSI;
     |SI nNivel = 8;
          aCadena = "  " + "  " + "  " + "  " + "  " + "  " + "  " + aCadena;
     |FINSI;
     |SI nNivel = 9;
          aCadena = "  " + "  " + "  " + "  " + "  " + "  " + "  " + "  " + aCadena;
     |FINSI;
     |SI nNivel = 10;
          aCadena = "  " + "  " + "  " + "  " + "  " + "  " + "  " + "  " + "  " + aCadena;
     |FINSI;
     |SI swSinReturn = 0;
          aCadena = aCadena + &13 + &10;
     |FINSI;
     aAlfa = aCadena;
     |XGRABA nHandle, aAlfa;
|FPRC;

|PROCESO RegistroCCC;
     |DDEFECTO #cretae02;

     |ABRE #cretae02;
     #cretae02#0 = #cretat02#0;
     #cretae02#1 = #0#0;
     #cretae02#2 = #0#1;
     #cretae02#3 = enTipo;
     |SI aParam = "cretat08";      || solo cuando vengo de un borrador provo-
          #cretae02#3 = #cretat02#7;    || cado por formacion sin horas (becarios
     |FINSI;                       || o L13)
     #cretae02#4 = #cretat02#1;
     #cretae02#6 = eaReferencia;
     |LEE 101#cretae02.=;
     FEstado = FSalida;

     #cretae02#7 = eaTipoXML;
     #cretae02#9 = #cretat02#3;

     #cretae02#11 = #0#3;
     #cretae02#12 = efFecha;
     #cretae02#13 = (eaHora % 102) + ":" + (eaHora % 402);

     #cretae02#17 = #cretat02#4;
     #cretae02#18 = #cretat02#5;
     #cretae02#19 = #cretat02#6;

     |SI FEstado = 0;
          |GRABA 020#cretae02.a;
     |SINO;
          |GRABA 020#cretae02.n;
     |FINSI;

     |LIBERA #cretae02;
     |CIERRA #cretae02;

     |DDEFECTO #cretam02;

     |ABRE #cretam02;
     #cretam02#0 = #cretat02#0;
     #cretam02#1 = #0#0;
     #cretam02#2 = #0#1;
     #cretam02#3 = enTipo;
     #cretam02#4 = #cretat02#1;
     |LEE 101#cretam02.=;
     |SI FSalida = 0;
          |SI #0#5 = 1;
               #cretam02#9 = "G";
               #cretam02#10 = eaReferencia;
               |GRABA 020#cretam02.a;
          |FINSI;
          |SI #0#5 = 2;
               #cretam02#13 = "G";
               #cretam02#14 = eaReferencia;
               |GRABA 020#cretam02.a;
          |FINSI;
     |FINSI;

     |LIBERA #cretam02;
     |CIERRA #cretam02;
|FPRC;

|PROCESO ChequeaAut;
     #cretam52#0 = #cretat02#0;
     |LEE 000#cretam52.=;
     |SI FSalida = 0;
          aAutEmp = #cretam52#2;
          |QBF aAutEmp;
          aAutEmp = ("00000000" + aAutEmp) % -108;     || autoriz. por emp.

          |SI aAntAut ! "";
               |SI aAutEmp ! aAntAut;             || si el nro de autoriz. por emp
                    nEmpOtraAut = #cretat02#0;    || es distinto al que venga usando
               |FINSI;                            || me guardo la empresa
          |FINSI;
     |SINO;
          aAutEmp = #cretam51#1;             || si no tiene autor. por emp.
          |QBF aAutEmp;                      || me quedo con el nro. general
          aAutEmp = ("00000000" + aAutEmp) % -108;
     |FINSI;

     aAutorizado = aAutEmp;

     |SI aAntAut ! "";
          |SI aAntAut ! aAutEmp;             || aviso, si hay alguno distinto
               swAvisoAut = 1;               || al anterior
          |FINSI;
     |FINSI;

     aAntAut = aAutEmp;
|FPRC;

|PROCESO AccionDatosBancarios;
     |SI swCompAut = 1;
          |HAZ ChequeaAut;
          |ACABA;
     |FINSI;

     nNivel = 2;
     aAlfa = "<AccionDatosBancarios>";
     |HAZ Linea;

     nNivel = 3;
     aAlfa = #cretat53#0;
     |QBF aAlfa;
     aAlfa = ("00000" + aAlfa) % -105;
     aAlfa = aAlfa + "  " + #cretat53#2;
     aAlfa = "<!-- " + aAlfa + " -->";
     |HAZ Linea;

     aAlfa = #cretat53#3;
     |QBF aAlfa;
     aAlfa = "<!-- CENTRO: " + aAlfa + " -->";
     |HAZ Linea;

     aAlfa = "<Ccc>";
     |HAZ Linea;
     |HAZ RegistroCCC;

     nNivel = 4;
     aValor = #cretat53#1 % 0104;
     aAlfa = "<Regimen>" + aValor + "</Regimen>";
     |HAZ Linea;
     aValor = #cretat53#1 % 0502;
     aAlfa = "<Provincia>" + aValor + "</Provincia>";
     |HAZ Linea;
     aValor = #cretat53#1 % 0709;
     aAlfa = "<Numero>" + aValor + "</Numero>";
     |HAZ Linea;

     nNivel = 3;
     aAlfa = "</Ccc>";
     |HAZ Linea;

     nNivel = 3;
     aValor = "A";
     aAlfa = "<TipoMovimiento>" + aValor + "</TipoMovimiento>";
     |HAZ Linea;

     nNivel = 3;
     aValor = "1";
     aAlfa = "<TipoAccion>" + aValor + "</TipoAccion>";
     |HAZ Linea;

     nNivel = 3;
     aAlfa = "<DatosBancarios>";
     |HAZ Linea;

     nNivel = 4;
     aValor = #cretat53#8;
     |QBF aValor;
     aAlfa = "<IBAN>" + aValor + "</IBAN>";
     |HAZ Linea;

     nNivel = 4;
     aAlfa = "<Titular>";
     |HAZ Linea;

     nNivel = 5;
     aAlfa = "<IpfTitular>";
     |HAZ Linea;

     nNivel = 6;
     aValor = #cretat53#5;
     |QBF aValor;
     aAlfa = "<TipoIpf>" + aValor + "</TipoIpf>";
     |HAZ Linea;

     nNivel = 6;
     aValor = #cretat53#6;
     |QBF aValor;
     aValor = ("0000000000" + aValor) % -110;
     aAlfa = "<NumeroIpf>" + aValor + "</NumeroIpf>";
     |HAZ Linea;

     nNivel = 5;
     aAlfa = "</IpfTitular>";
     |HAZ Linea;

     nNivel = 5;
     aValor = #cretat53#7;
     |QBF aValor;
     aAlfa = "<NombreTitular>" + aValor + "</NombreTitular>";
     |HAZ Linea;

     nNivel = 4;
     aAlfa = "</Titular>";
     |HAZ Linea;

     nNivel = 3;
     aAlfa = "</DatosBancarios>";
     |HAZ Linea;

     nNivel = 2;
     aAlfa = "</AccionDatosBancarios>";
     |HAZ Linea;
|FPRC;

|PROCESO GeneraXML;
     nNivel = 2;

     aAlfa = "<Liquidacion>";
     |HAZ Linea;

     nNivel = 3;

     aAlfa = #cretat02#0;
     |QBF aAlfa;
     aAlfa = ("00000" + aAlfa) % -105;
     aAlfa = aAlfa + "  " + #cretat02#2;
     aAlfa = "<!-- " + aAlfa + " -->";
     |HAZ Linea;

     aAlfa = #cretat02#3;
     |QBF aAlfa;
     aAlfa = "<!-- CENTRO: " + aAlfa + " -->";
     |HAZ Linea;

     aAlfa = "<Ccc>";
     |HAZ Linea;
     |HAZ RegistroCCC;

     nNivel = 4;
     aValor = #cretat02#1 % 0104;
     aAlfa = "<Regimen>" + aValor + "</Regimen>";
     |HAZ Linea;
     aValor = #cretat02#1 % 0502;
     aAlfa = "<Provincia>" + aValor + "</Provincia>";
     |HAZ Linea;
     aValor = #cretat02#1 % 0709;
     aAlfa = "<Numero>" + aValor + "</Numero>";
     |HAZ Linea;

     nNivel = 3;
     aAlfa = "</Ccc>";
     |HAZ Linea;

     nNivel = 3;
     aAlfa = "<PeriodoDesde>";
     |HAZ Linea;

     nNivel = 4;
     nNume = #cretat02#4;
     |SI swBorradorL13For = 1;
          nNume = nNume + 1;
          |SI nNume = 13;
               nNume = 1;
          |FINSI;
     |FINSI;
     aValor = nNume;
     |QBF aValor;
     aValor = ("00" + aValor) % -102;
     aAlfa = "<Mes>" + aValor + "</Mes>";
     |HAZ Linea;

     nNivel = 4;
     nNume = #cretat02#6;
     |SI swBorradorL13For = 1; |Y #cretat02#4 = 12;
          nNume = nNume + 1;
     |FINSI;
     aValor = nNume;
     |QBF aValor;
     aAlfa = "<Anho>" + aValor + "</Anho>";
     |HAZ Linea;

     nNivel = 3;
     aAlfa = "</PeriodoDesde>";
     |HAZ Linea;

     nNivel = 3;
     aAlfa = "<PeriodoHasta>";
     |HAZ Linea;

     nNivel = 4;
     nNume = #cretat02#5;
     |SI swBorradorL13For = 1;
          nNume = nNume + 1;
          |SI nNume = 13;
               nNume = 1;
          |FINSI;
     |FINSI;
     aValor = nNume;
     |QBF aValor;
     aValor = ("00" + aValor) % -102;
     aAlfa = "<Mes>" + aValor + "</Mes>";
     |HAZ Linea;

     nNivel = 4;
     nNume = #cretat02#6;
     |SI swBorradorL13For = 1; |Y #cretat02#5 = 12;
          nNume = nNume + 1;
     |FINSI;
     aValor = nNume;
     |QBF aValor;
     aAlfa = "<Anho>" + aValor + "</Anho>";
     |HAZ Linea;

     nNivel = 3;
     aAlfa = "</PeriodoHasta>";
     |HAZ Linea;

     nNivel = 3;
     |SI aParam = "cretat08";      || solo cuando vengo de un borrador provo-
          enTipo = #cretat02#7;    || cado por formacion sin horas (becarios
     |FINSI;                       || o L13)
     |SI enTipo = 00;
          aAlfa = "L00";
     |FINSI;
     |SI enTipo = 02;
          aAlfa = "L13";
     |FINSI;
     |SI enTipo ] 05; |Y enTipo [ 20;
          aAlfa = "L03";
     |FINSI;
     |SI enTipo = 90;
          aAlfa = "L90";
     |FINSI;

     aAlfa = "<Tipo>" + aAlfa + "</Tipo>";
     |HAZ Linea;

     |SI enTipo ] 05; |Y enTipo [ 20;
          nNivel = 3;
          aAlfa = "<FechaControl>";
          |HAZ Linea;

          nNivel = 4;
          aValor = #0#1;
          |QBF aValor;
          aValor = ("00" + aValor) % -102;
          aAlfa = "<Mes>" + aValor + "</Mes>";
          |HAZ Linea;

          nNivel = 4;
          aValor = #0#0;
          |QBF aValor;
          aAlfa = "<Anho>" + aValor + "</Anho>";
          |HAZ Linea;

          nNivel = 3;
          aAlfa = "</FechaControl>";
          |HAZ Linea;
     |FINSI;

     |SI #0#5 = "2";
          || aAlfa = "<AceptarBasesAnteriores>" + "S" + "</AceptarBasesAnteriores>";
     |FINSI;

     |SI #0#5 = "4";
          nNivel = 3;
          aAlfa = "<PeriodoPresentacion>";
          |HAZ Linea;

          |SI #0#6 = 90;
               nNume1 = #cretam02#20 + 1;
          |SINO;
               nNume1 = #0#1 + 1;
          |FINSI;

          |SI nNume1 = 13;
               nNume1 = 1;
               |SI #0#6 = 90;
                    nNume2 = #cretam02#21 + 1;
               |SINO;
                    nNume2 = #0#0 + 1;
               |FINSI;
          |SINO;
               |SI #0#6 = 90;
                    nNume2 = #cretam02#21;
               |SINO;
                    nNume2 = #0#0;
               |FINSI;
          |FINSI;

          nNivel = 4;
          aValor = nNume1;
          |QBF aValor;
          aValor = ("00" + aValor) % -102;
          aAlfa = "<Mes>" + aValor + "</Mes>";
          |HAZ Linea;

          nNivel = 4;
          aValor = nNume2;
          |QBF aValor;
          aAlfa = "<Anho>" + aValor + "</Anho>";
          |HAZ Linea;

          nNivel = 3;
          aAlfa = "</PeriodoPresentacion>";
          |HAZ Linea;

          nNivel = 4;
          aValor = "S";
          |QBF aValor;
          aAlfa = "<IndicadorCalculosDesglosados>" + aValor + "</IndicadorCalculosDesglosados>";
          |HAZ Linea;
     |FINSI;

     nNivel = 2;
     aAlfa = "</Liquidacion>";
     |HAZ Linea;
|FPRC;

|PROCESO Liquidacion;
     |SI swCompAut = 1;
          |HAZ ChequeaAut;
          |ACABA;
     |FINSI;

     |HAZ GeneraXML;

     |SI aParam = "cretat08"; |Y #cretat02#7 = 2;
          || es un CCC de formacion, de cual estoy pidiendo la L13
          || esto es porque existe linea 99, entonces tengo que hacer la
          || solicitud de un borrador del mes actual y otro del mes siguiente

          swBorradorL13For = 1;

          |HAZ GeneraXML;
     |FINSI;

     swBorradorL13For = 0;
|FPRC;

|PROCESO Cabecera;
     |ABRE #silcon01;
     |DDEFECTO #silcon01;
     |LEE 000#silcon01.p;
     |CIERRA #silcon01;

     |SI #0#5 = "1";
          aTipoFichero = "SolicitudTrabajadoresTramos";
     |FINSI;
     |SI #0#5 = "2";
          aTipoFichero = "SolicitudBorrador";
     |FINSI;
     |SI #0#5 = "4";
          aTipoFichero = "SolicitudCalculos";
     |FINSI;
     |SI #0#5 = "3";
          aTipoFichero = "SolicitudConfirmacion";
     |FINSI;
     |SI #0#5 = "5";
          aTipoFichero = "ComunicacionDatosBancarios";
     |FINSI;

     |XABRE "WB", aOrigen, nHandle;
     |SI FSalida ] 0;
          nNivel = 1;
          aAlfa = "<?xml version= " + &34 + "1.0" + &34 + " encoding=" + &34 + "ISO-8859-1" + &34 + "?>";
          |HAZ Linea;
          swSinReturn = 1;
          aAlfa = "<" + aTipoFichero + " xmlns:xsi=" + &34 + "http://www.w3.org/2001/XMLSchema-instance" + &34;
          |HAZ Linea;
          aAlfa = " xsi:schemaLocation=" + &34 + "http://www.seg-social.es/creta/esquemas/V100/" + aTipoFichero + " " + aTipoFichero + ".xsd" + &34;
          |HAZ Linea;
          aAlfa = " xmlns=" + &34 + "http://www.seg-social.es/creta/esquemas/V100/" + aTipoFichero + &34 + ">";
          swSinReturn = 0;
          |HAZ Linea;

          aValor = aAutorizado;
          nNivel = 2;
          aAlfa = "<Autorizado>" + aValor + "</Autorizado>";
          |HAZ Linea;

          |SI #0#5 = "1";
               || CLAVE T - solicitud de trabajadores y tramos
               eaTipoXML = "T";
          |FINSI;
          |SI #0#5 = "2";
               || CLAVE R - solicitud de borrador
               eaTipoXML = "R";
          |FINSI;
          |SI #0#5 = "3";
               || CLAVE F - solicitud de confirmacion
               eaTipoXML = "F";
          |FINSI;
          |SI #0#5 = "4";
               || CLAVE C - solicitud de calculos
               eaTipoXML = "C";
          |FINSI;
          |SI #0#5 = "5";
               || CLAVE D - comunicacion datos bancarios
               eaTipoXML = "D";
          |FINSI;

          enMes = #0#1;
          enAny = #0#0;
          |HAZ RefCreta_plb;

          aValor = eaReferencia;
          aAlfa = "<ReferenciaExterna>" + aValor + "</ReferenciaExterna>";
          |HAZ Linea;
     |FINSI;
|FPRC;

|PROCESO Pie;
     nNivel = 1;
     aAlfa = "</" + aTipoFichero + ">";
     |HAZ Linea;

     |XCIERRA nHandle;
|FPRC;

|DEFBUCLE Cuentas;
     #cretat53#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, AccionDatosBancarios;
|FIN;

|DEFBUCLE Liquidacion;
     #cretat02#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, Liquidacion;
|FIN;

|PROCESO sustituye;
     || aCadena es la cadena a buscar un caracter
     || aBusca es el caracter a buscar
     || aCambia es por el que lo queremos cambiar

     aCadenaSal = "";
     aLongitud = aCadena % 0;
     nLongitud = aLongitud;
     |PARA nPos = 1; |SI nPos [ nLongitud; |HACIENDO nPos = nPos + 1;
          nBusca = (nPos * 100) + 1;
          aCaracter = aCadena % nBusca;
          |SI aCaracter = aBusca;
               aCadenaSal = aCadenaSal + aCambia;
          |SINO;
               aCadenaSal = aCadenaSal + aCaracter;
          |FINSI;
     |SIGUE;
|FPRC;

|PROCESO Ruta;
     aCadena = #cretam00#2;
     aBusca = "/";
     aCambia = "\";
     |HAZ sustituye;
     aRuta = aCadenaSal;

     |QBF aRuta;
     aAlfa = aRuta % -101;
     |SI aAlfa ! "\";
          aRuta = aRuta + "\";
     |FINSI;
     aRuta = aRuta + "SOLICITUDES\";

     |SI #0#5 = "1";
          aRuta = aRuta + "TRABAJADORES\";
     |FINSI;
     |SI #0#5 = "2";
          aRuta = aRuta + "BORRADOR\";
     |FINSI;
     |SI #0#5 = "4";
          aRuta = aRuta + "CALCULOS\";
     |FINSI;
     |SI #0#5 = "3";
          aRuta = aRuta + "CONFIRMACION\";
     |FINSI;
     |SI #0#5 = "5";
          aRuta = aRuta + "DATOSBANCARIOS\";
     |FINSI;

     |RMKDIR aRuta;

     aFichero = #0#3;
     |QBF aFichero;
     aAlfa = aFichero % -104;
     |SI aAlfa ! ".xml";
          aFichero = aFichero + ".xml";
     |FINSI;

     aRuta = aRuta + aFichero;
|FPRC;

|PROCESO Alinea;
     aAlfa = nNume; |QBF aAlfa;
     |SI nNume < 10;
          aAlfa = "  " + aAlfa;
     |SINO;
          |SI nNume < 100;
               aAlfa = " " + aAlfa;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Centra;
     aLon = aCadena % 0;
     nLon = aLon;
     nBlancos = nEspacio - nLon;
     nNume = nBlancos / 2;
     nBlancosIzd = nNume ! 0;
     nBlancosDer = nBlancos - nBlancosIzd;
     aBlancosIzd = " " * nBlancosIzd;
     aBlancosDer = nBlancosDer * " ";
     aCadenaSal = aBlancosIzd + aCadena + aBlancosDer;
|FPRC;

|PROCESO Mensaje;
     |BLANCO 08101770;
     |CUADRO 08101770;
               ||  1       2         3         4         5         6
               ||  234567890123456789012345678901234567890123456789012345678
     |SI #0#5 = 1;
          aAlfa = "trabajadores y tramos";
     |FINSI;
     |SI #0#5 = 2;
          aAlfa = "borrador";
     |FINSI;
     |SI #0#5 = 3;
          aAlfa = "confirmaci¢n";
     |FINSI;
     |SI #0#5 = 4;
          aAlfa = "c lculos";
     |FINSI;
     |SI #0#5 = 5;
          aAlfa = "datos bancarios";
     |FINSI;
     aLinea = "El fichero de solicitud de " + aAlfa + " se ha";
     aCadena = aLinea; nEspacio = 57; |HAZ Centra;
     |PINTA 0912, aCadenaSal;
     aLinea = "grabado en la carpeta: ";
     aCadena = aLinea; nEspacio = 57; |HAZ Centra;
     |PINTA 1012, aCadenaSal;
     aAlfa = aDestino - aFichero;
     aCadena = aAlfa; nEspacio = 57; |HAZ Centra;
     |ATRI S;
     |PINTA 1112, aCadenaSal;
     |ATRI 0;
     |SI aParam = "cretat08";
          |SI #cretat08#5 = 1;
               |PINTA 1212, "      para los CCCs de formaci¢n cuyos trabajadores      ";
               |PINTA 1312, "       no tienen horas de formaci¢n subvencionadas       ";
          |SINO;
               |PINTA 1212, "            para los CCCs sin bases a declarar           ";
               |PINTA 1312, "                                                         ";
          |FINSI;
     |SINO;
          |PINTA 1212, "       Se realiza la solicitud para un total de:         ";
          nNume = nEmpresas; |HAZ Alinea
          aLinea = "                    " + aAlfa + " Empresas                         ";
          |PINTA 1312, aLinea;
          |SI #0#5 ! 5;
               nNume = nCentros; |HAZ Alinea
               aLinea = "                    " + aAlfa + " Centros                          ";
               |PINTA 1412, aLinea;
          |FINSI;
          nNume = nCCCs; |HAZ Alinea
          aLinea = "                    " + aAlfa + " CCCs                             ";
          |PINTA 1512, aLinea;

          |PINTA 1612, "                                                         ";
     |FINSI;

     |PAUSA;
|FPRC;

|PROCESO Autorizacion;
     || en primer lugar dejo en aAutorizado el numero de autorizacion
     || si existe en cretam51 el que haya, si no, me lo traigo del silcon01
     || y alli lo dejo grabado

     |ABRE #cretam51;
     |DDEFECTO #cretam51;
     |LEE 000#cretam51.p;
     |SI FSalida ! 0;
          |ABRE #silcon01;
          |DDEFECTO #silcon01;
          |LEE 000#silcon01.p;
          |SI FSalida = 0;
               #cretam51#1 = #silcon01#0;
               |GRABA 020#cretam51.n;
               |LIBERA #cretam51;
          |FINSI;
          |CIERRA #silcon01;
     |FINSI;
     |CIERRA #cretam51;

     aAlfa = #cretam51#1;
     |QBF aAlfa;
     aAutorizado = ("00000000" + aAlfa) % -108;

     |SI #cretam51#2 ! "S";    || si solo tengo el codigo de autorizacion
          |ACABA;             || principal no tengo nada mas que mirar
     |FINSI;

     || si puede que tenga mas de una autorizacion
     || ahora tengo que recorrer la misma seleccion de empresas que he hecho
     || en busca de los codigos de autorizacion, por si hay alguna que tenga
     || alguno distinto

     swCompAut = 1;      || para que el programa sepa  que voy a comprobar autorizaciones
     swAvisoAut = 0;     || indicador de que hay que dar aviso al final

     |ABRE #cretam52;

     |BUCLE Liquidacion;

     |CIERRA #cretam52;

     swCompAut = 0;

     |SI swAvisoAut = 1;
          aAlfa = nEmpOtraAut; |QBF aAlfa; aAlfa = ("00000" + aAlfa) % -105;
          aAlfa = "    ATENCION. La empresa " + aAlfa + " tiene un número de autorizado ";
          aAlfa = aAlfa + "distinto al principal.";
          |MENAV aAlfa;

          aAlfa = "    Para esta empresa se ha de realizar una Solicitud independiente";
          |MENAV aAlfa;
     |FINSI;
|FPRC;

|PROCESO CreacionXML;
     |HAZ Ruta;

     |DFICE aOrigen;
     aOrigen = aOrigen + aFichero;
     aDestino = aRuta;
     |QBF aDestino;
     aDestino = aRuta;

     |HAZ Autorizacion;

     |HAZ Cabecera;

     |SI #0#5 ! 5;
          |BUCLE Liquidacion;
     |SINO;
          |BUCLE Cuentas;
     |FINSI;

     |HAZ Pie;

     |IP_REMOTO aIP;
     |SI aIP ! "";
          |ENVIA_FICHERO aOrigen, aDestino;
     |SINO;
          |COPIA_FICHERO aOrigen, aDestino;
     |FINSI;

     |DBASE aAlfa; |QBF aAlfa;
     aAlfa = aAlfa + "tmpsiltra/XECR";
     |MKDIR aAlfa;
     aAlfa = aAlfa + "/copia";
     |MKDIR aAlfa;
     aAlfa = aAlfa + "/" + aFichero;
     |COPIA_FICHERO aOrigen, aAlfa;

     |SI FSalida = 0;
          |HAZ Mensaje;
          aAlfa = "    A continuación, puede enviar el fichero creado a la TGSS a través ";
          aAlfa = aAlfa + "de la aplicación SILTRA.";
          |MENAV aAlfa;
     |FINSI;

     |FBORRA aOrigen;
|FPRC;

|PROCESO ElProceso;
     |SI #0#2 = "S";
          |BUCLE ConSeleccion;
     |SINO;
          |SI #0#5 = "5";
               |BUCLE SinSeleccion_Ctas;
          |SINO;
               |SI #0#6 ] 5; |Y #0#6 [ 20;
                    |BUCLE SinSeleccion_Atr;
               |SINO;
                    |BUCLE SinSeleccion;
               |FINSI;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO cretat08;
     nCCCs = nCCCs + 1;
     #cretat02#0 = #cretat08#0;
     #cretat02#1 = #cretat08#1;
     #cretat02#2 = #cretat08#2;
     #cretat02#3 = #cretat08#3;
     #cretat02#4 = #0#1;
     #cretat02#5 = #0#1;
     #cretat02#6 = #0#0;
     #cretat02#7 = #cretat08#4;

     |GRABA 020#cretat02.n;
     |LIBERA #cretat02;
|FPRC;

|DEFBUCLE cretat08;
     #cretat08#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, cretat08;
|FIN;

|PROCESO TraspasoCCCsEnvio;
     alfa4 = Usuario; |QBT alfa4;    alfa4 = ("c3" + alfa4) % 108;
     |NOME_DAT #cretat08#alfa4#;

     |BUCLE cretat08;

     |CIERRA #cretat08;
     |DELINDEX #cretat08;
|FPRC;

|PROCESO imprime;
     |PRINT;
|FPRC;

|DEFBUCLE cretat53;
     #cretat53#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, imprime;
|FIN;

|PROGRAMA;
     |CLS;

     aParam = PARAMETRO;
     |QBF aParam;

     |ABRE #labcentr;

     |SI aParam = "5";          || traspado inicial a Mto. Ctas: cretam53
          ||SUB_EJECUTA "cretaz03";

          |CLS;
          |PINTA 1019, "ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿";
          |PINTA 1119, "³ CARGANDO FICHERO DE CUENTAS BANCARIAS³";
          |PINTA 1219, "³                                      ³";
          |PINTA 1319, "ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ";

          |BUCLE cretam53;
          |HAZ TraspasoCuentas;
     |FINSI;

     |SI aParam = "cretat08";
          #0#5 = 2;

          #0#0 = enAny;
          #0#1 = enMes;

          |HAZ DiaHora;
          |HAZ Fichero;
     |SINO;
          #0#5 = aParam;

          |PINPA #0#0;
          |PINDA #0#0;

          |HAZ Defectos;
          Campo = 1;
          |HAZ pintames;

          |HAZ PintaSol;
          |HAZ DiaHora;
          |HAZ Fichero;
          |HAZ PintaAyuda11;
     |FINSI;

     aTmp_tsilco14 = Usuario; |QBT aTmp_tsilco14;    aTmp_tsilco14 = ("t" + aTmp_tsilco14) % 108;
     |NOME_DAT #tsilco14#aTmp_tsilco14#;
     |DELINDEX #tsilco14;

     aTmp_tsilco16 = Usuario; |QBT aTmp_tsilco16;    aTmp_tsilco16 = ("tmp_tsilco16_" + aTmp_tsilco16) % 121;
     |NOME_DAT #tsilco16#aTmp_tsilco16#;
     |DELINDEX #tsilco16;

     aTmp_cretat02 = Usuario; |QBT aTmp_cretat02;    aTmp_cretat02 = ("c2" + aTmp_cretat02) % 108;
     |NOME_DAT #cretat02#aTmp_cretat02#;
     |DELINDEX #cretat02;

     aAlfa = Usuario; |QBT aAlfa; aAlfa = "tm_cretat53_" + aAlfa;
     |NOME_DAT #cretat53#aAlfa#;
     |DELINDEX #cretat53;

     |ABRE #cretat02;
     |ABRE #cretam53;
     |ABRE #cretat53;
     |ABRE #labempre;
     |ABRE #labtraba;
     |ABRE #tsilco14;
     |ABRE #tsilco16;
     |ABRE #silcon06;
     |ABRE #silcon07;

     |DDEFECTO #cretam00;
     |ABRE #cretam00; |LEE 000#cretam00.p; |CIERRA #cretam00;

     |SI aParam = "cretat08";
          #0#2 = "S";
          |HAZ TraspasoCCCsEnvio;
          FSalida = 0;
     |SINO;
          |ENDATOS #1#0;
     |FINSI;

     |SI FSalida = 0;
          enTipo = #0#6;

          |HAZ ElProceso;

          |SI #0#5 = "5"; |Y nCCCs ! 0;
               aAlfa = "    A continuación se emitirá un listado de las cuentas";
               aAlfa = aAlfa + " que se comunicarán en el fichero";
               |MENAV aAlfa;

               Impresora = "CrystalReport";
               |IMPRE -1;
               |SI FSalida = 0;
                    |INFOR "cretat53";
                    |BUCLE cretat53;
                    |PIEINF;
                    |FININF;
               |FINSI;
               |FINIMP;

               aAlfa = "    S¿Desea continuar con la creación del fichero de ";
               aAlfa = aAlfa + "comunicación de cuentas?";
               swSigue = 1;
               |CONFI aAlfa;
               |SI FSalida ! 0;
                    swSigue = -1;
                    nCCCs = 0;
               |FINSI;
          |FINSI;

          |SI nCCCs ! 0;
               |HAZ CreacionXML;
          |SINO;
               |SI swSigue = -1;
                    aAlfa = "    Puede desactivar las cuentas que no desee incluir, ";
                    aAlfa = aAlfa + " en la Consulta de Datos Bancarios por Empresa/CCC";
                    |MENAV aAlfa;
               |SINO;
                    aAlfa = "    No se han encontrado datos para realizar la solicitud";
                    aAlfa = aAlfa + " con la selección realizada."
                    |MENAV aAlfa;
               |FINSI;
          |FINSI;
     |FINSI;

     |DELINDEX #tsilco16;
     |DELINDEX #tsilco14;
     |DELINDEX #cretat02;
     |DELINDEX #cretat53;

     |CIERRA #silcon06;
     |CIERRA #silcon07;
     |CIERRA #cretat53;
     |CIERRA #cretam53;
     |CIERRA #cretat02;
     |CIERRA #tsilco16;
     |CIERRA #tsilco14;
     |CIERRA #labtraba;
     |CIERRA #labcentr;
     |CIERRA #labempre;
|FPRO;
