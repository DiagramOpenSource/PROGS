|FICHEROS;
     laborn0a;           || este def el del lineal
     nomvarca;
     nomvarli;
     nomcontr;

     labtraba;
     nompmail;
     laborn1a;
     labornxa;

     :integral/dsam0103;
     :modelos/dsmom004;
     :modelos/dsm11101;
|FIN;

|VARIABLES;
     &eaDirDest  = "";
     &eaAsunto   = "";
     &eaTexto    = "";
     &eaFichero  = "";

     &aParam = "";
     &eaUsuario = "";
     &eaFecha = "";
     &efFecha = @;
     &eaHora = "";
     aTipo = "";
     sw = 0;
     aAlfa  = "";
     aAlfa1 = "";
     aAlfa2 = "";
     FEstado = 0;
     nMes = 0;
     aUsuarioD = "";
     aUsuarioH = "";
     &nEmpExt = 0;
     &nTraExt = 0;
     &eaVengoDe = "laborn0a";
     &eaEmpresa = "";
     &nEmpAUTO = 0;
     &nTraAUTO = 0;
     &nMesAUTO = 0;
     &nAnyAUTO = 0;
     &nTipAUTO = 0;
     &aFechaBaja = "";
     &fFechaBaja = @;
     &nCausaBaja = 0;
     &aCausaBaja = "";
     &aTipoContr = "";
     nNume = 0;
     aSiEsX = "";

     &eaParam       = "";
     &enFSalida     = 0;
     &enBorrado     = 0;
     &eaEstado      = "";
     &eaMotiv       = "";
     &enActualizado = 0;

     &enEmpresa = 0;
     &enTrabajador = 0;
     &enDiasVac = 0;
     nCampo = 0;
     nRegistro = 0;
     swEmbargo = 0;
     fFecha = @;
     nAny = 0;
     nPeriodo = 0;

     || para el SUB_EJECUTA A modelos

     &enModelo = 0;
     &enPeriodo = 0;
     &enEjer = 0;
     &enComplem = 0;
     &eaNombreEmp = "";
     &enActividad = 0;
|FIN;

|PROCESO Actualizacion;
     || actualizacion en la aplicacion

     |ABRE #dsam0103;
     #dsam0103#0 = #laborn0a#7;
     |LEE 101#dsam0103.=;
     |SI FSalida ! 0;
          #dsam0103#0 = #laborn0a#7;
          #dsam0103#1 = #laborn0a#9;
          #dsam0103#4 = #laborn0a#10;
          #dsam0103#7 = #laborn0a#11;
          |GRABA 020#dsam0103.n;
     |FINSI;
     |LIBERA #dsam0103;
     |CIERRA #dsam0103;

     nPeriodo = 0;
     |ABRE #dsmom004;
     #dsmom004#0 = #laborn0a#5;
     #dsmom004#1 = #laborn0a#12;
     #dsmom004#2 = #laborn0a#13;
     #dsmom004#3 = 110;
     #dsmom004#4 = 00;
     |LEE 000#dsmom004.=;
     |SI FSalida = 0;
          || 110 mensual
          nPeriodo = #dsmom004#2;
     |SINO;
          || 110 trimestral o no existe planning
          |SI #laborn0a#13 ] 01; |Y #laborn0a#13 [ 03;
               #dsmom004#2 = 03;
          |FINSI;
          |SI #laborn0a#13 ] 04; |Y #laborn0a#13 [ 06;
               #dsmom004#2 = 06;
          |FINSI;
          |SI #laborn0a#13 ] 07; |Y #laborn0a#13 [ 09;
               #dsmom004#2 = 09;
          |FINSI;
          |SI #laborn0a#13 ] 10; |Y #laborn0a#13 [ 12;
               #dsmom004#2 = 12;
          |FINSI;
          |LEE 000#dsmom004.=;
          |SI FSalida = 0;
               nPeriodo = #dsmom004#2;
          |FINSI;
     |FINSI;
     |CIERRA #dsmom004;

     |SI nPeriodo ! 0;
          |ABRE #dsm11101;
          |DDEFECTO #dsm11101;
          #dsm11101#0 = #laborn0a#5;
          #dsm11101#1 = #laborn0a#12;
          #dsm11101#2 = nPeriodo;
          #dsm11101#3 = 110;
          #dsm11101#4 = 00;
          #dsm11101#5 = 99;
          #dsm11101#6 = #laborn0a#7;
          #dsm11101#7 = 0000;
          #dsm11101#8 = #laborn0a#13;
          #dsm11101#9 = #laborn0a#14;
          #dsm11101#10 = #laborn0a#15;
          #dsm11101#11 = 0;
          |LEE 000#dsm11101.=;
          FEstado = FSalida;

          #dsm11101#21 = #laborn0a#9;
          #dsm11101#12 = #dsm11101#12 + #laborn0a#16;
          #dsm11101#13 = #dsm11101#13 + #laborn0a#17;
          |SI FEstado = 0;
               |GRABA 020#dsm11101.a;
          |SINO;
               |GRABA 020#dsm11101.n;
          |FINSI;

          |LIBERA #dsm11101;
          |CIERRA #dsm11101;

          enModelo    = 110;
          enPeriodo   = nPeriodo;
          enEjer      = #laborn0a#12;
          enEmpresa   = #laborn0a#5;
          eaNombreEmp = #laborn0a#8;
          enComplem   = 00;
          enActividad = 99;
          |SUB_EJECUTA ":modelos/dsmozpas;labornet";

     |SINO;
          aAlfa = #laborn0a#12;

          aAlfa = "    No se ha encontrado el planning para el 110 del año " + aAlfa + ". ";
          aAlfa = aAlfa + "El proceso se interrumpirá.";
          |MENAV aAlfa;
     |FINSI;
|FPRC;

|PROCESO Email;
     || Emite correo electronico al usuario de que la baja del trabajador
     ||    se ha hecho efectiva
     aAlfa = ("00000" + #laborn0a#6) % -105;
     eaTexto = &13 + &10 + "El administrador ha hecho efectivo el embargo del trabajador (" + aAlfa + ") " + #laborn0a#9; |QBF eaTexto;
     aAlfa = ("00000" + #laborn0a#5) % -106;
     eaTexto = eaTexto + " de la empresa (" + aAlfa + ") " + #laborn0a#8; |QBF eaTexto;

     eaTexto = eaTexto + &13 + &10 + &13 + &10;
     eaTexto = eaTexto + " Actualizacion con fecha " + #laborn0a#3 + " a las " + #laborn0a#4 + &13 + &10;

     aAlfa  = ("00000" + #laborn0a#5) % -106;
     aAlfa1 = ("00000" + #laborn0a#6) % -105;
     eaAsunto  = "Efectiva solicitud de retenciones trabajador (" + aAlfa + "/" + aAlfa1 + ") " + #laborn0a#9; |QBF eaAsunto;
     eaFichero = "";
     eaDirDest = "";

     aAlfa = #laborn0a#5; |QBF aAlfa;
     eaEmpresa = ("00000" + aAlfa) % -105;
     |HAZ ConsEmail2;
|FPRC;

|PROCESO Actualiza;
     aAlfa = "    ATENCION: se procederá a la actualización de la ";
     aAlfa = aAlfa + "solicitud de retenciones seleccionada";
     |MENAV aAlfa;
     aAlfa = "    S¿Desea continuar?";
     |CONFI aAlfa;
     |SI FSalida = 0;
          |HAZ Actualizacion;

          |SI nPeriodo = 0;
               |ACABA;
               |PONCONTROL 23, "SI";
               |PTEC 808;
          |FINSI;

          || primero me paso todos los campos del registro que voy a borrar
          || a un temporal, ya que ha habido varios fallos en los que al
          || borrar el registro pierde la clave, asi me aseguro

          |DDEFECTO #labornxa;
          |PARA nCampo = 0; |SI nCampo [ 28; |HACIENDO nCampo = nCampo + 1;
               #labornxa#nCampo# = #laborn0a#nCampo#;
          |SIGUE;

          |BORRA 020#laborn0a.a;

          |PARA nCampo = 0; |SI nCampo [ 28; |HACIENDO nCampo = nCampo + 1;
               #laborn0a#nCampo# = #labornxa#nCampo#;
          |SIGUE;

          #laborn0a#0 = "A";
          #laborn0a#21 = Usuario % 0810;
          #laborn0a#22 = ~;
          |HORA aAlfa;
          #laborn0a#23 = aAlfa;
          |GRABA 020#laborn0a.n;

          enActualizado = 1;

          |ABRE #nompmail; |LEE 000#nompmail.p; |CIERRA #nompmail;
          |SI #nompmail#11 = "S";
               |HAZ Email;
          |FINSI;

          aAlfa = "    La solicitud de Retención a Profesionales se ha actualizado ";
          aAlfa = aAlfa + "con éxito.";
          |MENAV aAlfa;
     |FINSI;
     |PONCONTROL 23, "SI";
     |PTEC 808;
|FPRC;

|PROCESO Consulta;
     eaUsuario = #laborn0a#1;      || permito modificar
     eaFecha = #laborn0a#3;
     eaHora = #laborn0a#4;
     |SUB_EJECUTA "laborn1a;CONSUL";
     |ACABA;
|FPRC;

|PROCESO Modificacion;
     eaUsuario = #laborn0a#1;      || permito modificar
     eaFecha = #laborn0a#3;
     eaHora = #laborn0a#4;
     |SUB_EJECUTA "laborn1a;MODIF";
     |ACABA;
|FPRC;

|PROCESO Opciones; |TIPO 0;
     |SI enFSalida ! 0;
          FSalida = enFSalida;
     |FINSI;

     || consulta de datos introducidos
     |SI FSalida = 10;
          |SI aParam = "USUARIO"; |O aParam = "ADMIN";  || consulta el usuario los datos que
               |HAZ Modificacion;
          |FINSI;
          |SI aParam = "HISTO"; |O aParam = "HISTOUSU";
               |HAZ Consulta;           || consulta de datos introducidos
          |FINSI;                       || o ya actualizados, solo visualizar
          |ACABA;
     |FINSI;

     |SI FSalida = 11;  |Y #laborn0a#0 = "P"; |Y aParam = "ADMIN";
          |HAZ Modificacion;
          |LEE 000#laborn0a.=;
          |HAZ Actualiza;
          |ACABA;
     |FINSI;

     |SI FSalida = 12;  |Y #laborn0a#0 = "P";
          enBorrado = 1;

          |LEE 101#laborn0a.=;
          |BORRA 020#laborn0a.a;
          |LIBERA #laborn0a;

          #laborn0a#0 = "R";
          #laborn0a#25 = eaMotiv;

          |GRABA 020#laborn0a.n;
          |LIBERA #laborn0a;
     |FINSI;
|FPRC;

|PROCESO RecogeCampos;
     #laborn0a#1 = eaUsuario;
     #laborn0a#3 = efFecha;
     #laborn0a#4 = eaHora;
     #laborn0a#0 = eaEstado;
|FPRC;

|PROGRAMA;
     |SI enFSalida ! 12;
          |CLS;
     |FINSI;
     |CABEZA "Retenciones Profesionales";

     aAlfa = Usuario % 0810;

     |ABRE #laborn0a;
     |SI aParam = "ADMIN"; |O aParam = "USUARIO";
          aTipo = "P";   || los pendientes de actualizar
          |HAZ RecogeCampos;
          |HAZ Opciones;
     |FINSI;
     |SI aParam = "HISTO"; |O aParam = "HISTOUSU";
          aTipo = "A";   || consulta del historico, los ya actualizados
          |HAZ RecogeCampos;
          |LEE 000#laborn0a.=;
          |HAZ Opciones;
     |FINSI;
     |CIERRA #laborn0a;
|FPRO;

|PROCESO Tipo80n08;  |TIPO 80;
     |SI PARAMETRO = "";  |ACABA;  |FINSI;

     aSiEsX = PARAMETRO % 101;
     |SI aSiEsX = "X";
         aAlfa     = PARAMETRO % 202;   enFSalida = aAlfa;
         eaEstado  = PARAMETRO % 401;
         eaUsuario = PARAMETRO % 510;
         aAlfa     = PARAMETRO % 1510;  efFecha = aAlfa;
         eaHora    = PARAMETRO % 2508;
         eaParam   = PARAMETRO % 3310;  |QBF eaParam;
         aParam    = eaParam;
     |SINO;
          aParam = PARAMETRO;  |QBF aParam;
          |SI eaParam ! "";
              eaParam = aParam;
          |FINSI;
     |FINSI;
|FPRC;
