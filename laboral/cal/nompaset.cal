|FICHEROS;
     nompaset #0;
     labempre;
     labtraba;
     nomtraca;
|FIN;

|VARIABLES;
     &nCConst = 0;
     &fCFecha = @;
     &aCError = "";
     fFechaHoy = @;
     nCte = 0;
     &aEmp = "";
     &aTra = "";
     &aSit = "";
     &aCte = "";
     &aErr = "";
     alfa1 = "";
     alfa2 = "";
     alfa3 = "";
     alfa4 = "";
|FIN;

|PROCESO constante;
     fFechaHoy = ~;
     alfa1 = "01";
     alfa2 = fFechaHoy;  alfa2 = alfa2 % 402;
     alfa3 = fFechaHoy;  alfa3 = alfa3 % -104;
     alfa4 = alfa1 + "." + alfa2 + "." + alfa3;

     fCFecha = alfa4;         || primer dia de mes de calculo
     aCError = "";            || inicializo errores
     |HAZ LeeCons;
|FPRC;

|PROCESO labtraba;
     nCte = 0;
     aEmp = "";
     aTra = "";
     aSit = "";
     aCte = "";
     aErr = "";
     #nomtraca#0 = #labtraba#0;
     #nomtraca#1 = #labtraba#1;
     |LEE 000#nomtraca.=;
     |SI FSalida = 0; |Y #nomtraca#3 ! 0;
          nCte = #nomtraca#3;
     |SINO;
          #labempre#0 = #labtraba#0;
          |LEE 000#labempre.=;
          |SI FSalida ! 0;
               nCte = -1;          || imposible: no hay ficha en nomtraca
          |SINO;                   || y la empresa del trabajador no existe
               nCte = #labempre#32;
          |FINSI;
     |FINSI;
     aEmp = #labtraba#0;
     aEmp = "00000" + aEmp;
     aEmp = aEmp % -105;
     aTra = #labtraba#1;
     aTra = "00000" + aTra;
     aTra = aTra % -105;
     aSit = #labtraba#44;
     aCte = nCte;
     aCte = "000" + nCte;
     aCte = aCte % -103;
     |SI nCte ] 0;
          nCConst = nCte;
          |HAZ constante;
          |SI aCError ! "";
               aErr = "No se ha encontrado la constante o el periodo no es correcto";
               |PRINT;
          |SINO;
               #labtraba#248 = nCte;
               |GRABA 020#labtraba.a;
          |FINSI;
     |SINO;
          aErr = " El trabajador no pertenece a ninguna empresa";
          |PRINT;
     |FINSI;
|FPRC;

|DEFBUCLE labtraba;
     #labtraba#1;
     ;
     #0#0, #0#2;
     #0#1, #0#3;
     ;
     NULL, labtraba;
|FIN;

|PROCESO Tipo3; |TIPO 3;
     |SI FSalida = 0;
          |IMPRE 0;
          |SI FSalida = 0;
               |ABRE #labempre;
               |ABRE #labtraba;
               |ABRE #nomtraca;
               |INFOR "nompaset";
               |BUCLE labtraba;
               |PIEINF;
               |FININF;
               |CIERRA #labempre;
               |CIERRA #labtraba;
               |CIERRA #nomtraca;
               |FINIMP;
          |SINO;
               |MENAV "    Proceso abortado";
          |FINSI;
     |SINO;
          |MENAV "    Proceso abortado";
     |FINSI;
|FPRC;
