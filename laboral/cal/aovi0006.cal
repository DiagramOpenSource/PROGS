|FICHEROS;
     aovi0006 #0;
     nomvarca;
     nomvarli;
     aomparam;

     enthoras@ #100;

     aovi0001;
     aovi0011;
     aovi0002;      || max por convenio/categoria cabecera
     aovi0003;      || max por convenio/categoria lineas
     aovi0005;
     aovi0007;
     aovi0008;
     aovi0012;      || max por empresa/trabajador cabecera
     aovi0013;      || max por empresa/trabajador lineas
     labtraba;
     nomcalca;
     aomj0002;
     aomj0003;
     aovi0014;
|FIN;

|VARIABLES;
     fFecha = @;
     aFecha = "";
     aAny = "";
     aMes = "";
     nAny = 0;
     nMes = 0;
     &nEmpAUTO_D = 0;
     &nEmpAUTO_H = 0;
     &nTraAUTO_D = 0;
     &nTraAUTO_H = 0;
     &nMesAUTO = 0;
     &nAnyAUTO = 0;

     numer = 0;
     anynum = 0;
     bisiesto = 0;
     mesnum = 0;
     topemes = 0;

     nPrecio1 = 0;
     nPrecio2 = 0;
     nPrecio3 = 0;
     nNomina = 0;
     nMaximo = 0;
     nRecibo = 0;
     nTotal = 0;
     nVariac = 0;
     nBruto = 0;
     FEstado = 0;
     aAlfa = "";
     fFechaDesde = @;
     fFechaHasta = @;
     aDia = "";

     nRestante = 0;
     nGraba = 0;
     nLimite = 0;
     nAntici = 0;
     swVacac = 0;
     nImporteVac = 0;

     nHandle = 0;
     aDef = "";
     aRutaDef = "";
     aRutaDatos = "";
     aDefCopia = "";

     nPrecioEnt1 = 0;
     nPrecioEnt2 = 0;
     nPrecioMed1 = 0;
     nPrecioMed2 = 0;
     nConceptoEnt = 0;
     nConceptoMed = 0;
     nDiferencia = 0;
     nBrutoConDietas = 0;
     nConcepto = 0;
     nDietasEnt = 0;
     nDietasMed = 0;
     nHoras = 0;
     swEntera = 0;
     swMedia = 0;
     nTipoHora = 0;
     nUnidades = 0;
     swBorra = 0;
     nImporte = 0;
|FIN;

|PROCESO Defectos; |TIPO 7;
     fFecha = ~;
     aFecha = fFecha;
     aAny = aFecha % -104;
     aMes = aFecha % 402;
     nAny = aAny;
     nMes = aMes;
     #0#4 = nAny;
     #0#5 = nMes;
     |PINTA #0#4;
     |PINTA #0#5;
|FPRC;

|PROCESO GrabaAntici;
     |ABRE #nomvarca;
     |DDEFECTO #nomvarca;

     #nomvarca#0 = #aovi0008#0;         || empresa
     #nomvarca#1 = #aovi0008#3;         || trabajador
     #nomvarca#2 = #0#5;                || mes
     #nomvarca#3 = 0;                   || tipo
     |LEE 101#nomvarca.=;
     |SI #aovi0014#4 = 1;
          #nomvarca#19 = nAntici;
          |SI #nomvarca#20 = nAntici;
               #nomvarca#20 = 0;
          |FINSI;
          |SI #nomvarca#44 = nAntici;
               #nomvarca#44 = 0;
          |FINSI;
     |FINSI;
     |SI #aovi0014#4 = 2;
          #nomvarca#20 = nAntici;
          |SI #nomvarca#19 = nAntici;
               #nomvarca#19 = 0;
          |FINSI;
          |SI #nomvarca#44 = nAntici;
               #nomvarca#44 = 0;
          |FINSI;
     |FINSI;
     |SI #aovi0014#4 = 3;
          #nomvarca#44 = nAntici;
          |SI #nomvarca#19 = nAntici;
               #nomvarca#19 = 0;
          |FINSI;
          |SI #nomvarca#20 = nAntici;
               #nomvarca#20 = 0;
          |FINSI;
     |FINSI;
     |SI FSalida ! 0;
          |GRABA 020#nomvarca.n;
     |SINO;
          |GRABA 020#nomvarca.a;
     |FINSI;

     |LIBERA #nomvarca;
     |CIERRA #nomvarca;
|FPRC;

|PROCESO GrabaVar2;
     |SI nRestante = 0;
          |ACABA;
     |FINSI;

     nLimite = #nomcalca#55 * #aovi0003#4;
     |SI nRestante > nLimite;
          nRestante = nRestante - nLimite;
          nGraba = nLimite / #nomcalca#55;
     |SINO;
          nGraba = nRestante / #nomcalca#55;
          nRestante = 0;
     |FINSI;

     |ABRE #nomvarca;
     |ABRE #nomvarli;

     |DDEFECTO #nomvarca;
     #nomvarca#0 = #aovi0008#0;         || empresa
     #nomvarca#1 = #aovi0008#3;         || trabajador
     #nomvarca#2 = #0#5;                || mes
     #nomvarca#3 = 0;                   || tipo
     |LEE 101#nomvarca.=;
     |SI FSalida ! 0;
          |GRABA 020#nomvarca.n;
          |LIBERA #nomvarca;
     |FINSI;

     #nomvarli#0 = #aovi0008#0;         || empresa
     #nomvarli#1 = #aovi0008#3;         || trabajador
     #nomvarli#2 = #0#5;                || mes
     #nomvarli#3 = 0;                   || tipo
     #nomvarli#4 = #aovi0003#2;        || concepto
     |LEE 101#nomvarli.=;
     FEstado = FSalida;

     |SI nVariac = 0;
          |SI FEstado = 0;
               |BORRA 020#nomvarli.a;
          |FINSI;
     |SINO;
          || aAlfa = 1;
          || aAlfa = "          " + aAlfa;
          || aAlfa = aAlfa % -110;
          || #nomvarli#5 = aAlfa;          || unidades

          aAlfa = nGraba;
          aAlfa = "          " + aAlfa;
          aAlfa = aAlfa % -112;
          #nomvarli#6 = aAlfa;          || unidades

          |SI FEstado = 0;
               |GRABA 020#nomvarli.a;
          |SINO;
               #nomvarli#0 = #aovi0008#0;         || empresa
               #nomvarli#1 = #aovi0008#3;         || trabajador
               #nomvarli#2 = #0#5;                || mes
               #nomvarli#3 = 0;                   || tipo
               #nomvarli#4 = #aovi0003#2;         || concepto
               |GRABA 020#nomvarli.n;
          |FINSI;
     |FINSI;

     |LIBERA #nomvarli;

     |CIERRA #nomvarli;
     |CIERRA #nomvarca;
|FPRC;

|DEFBUCLE GrabaVar2;
     #aovi0003#1;
     ;
     #labtraba#87, #labtraba#17, 101;
     #labtraba#87, #labtraba#17, 499;
     ;
     NULL, GrabaVar2;
|FIN;

|PROCESO GrabaVar1;
     |SI nRestante = 0;
          |ACABA;
     |FINSI;

     nLimite = #nomcalca#55 * #aovi0013#4;
     |SI nRestante > nLimite;
          nRestante = nRestante - nLimite;
          nGraba = nLimite / #nomcalca#55;
     |SINO;
          nGraba = nRestante / #nomcalca#55;
          nRestante = 0;
     |FINSI;

     |ABRE #nomvarca;
     |ABRE #nomvarli;

     |DDEFECTO #nomvarca;
     #nomvarca#0 = #aovi0008#0;         || empresa
     #nomvarca#1 = #aovi0008#3;         || trabajador
     #nomvarca#2 = #0#5;                || mes
     #nomvarca#3 = 0;                   || tipo
     |LEE 101#nomvarca.=;
     |SI FSalida ! 0;
          |GRABA 020#nomvarca.n;
          |LIBERA #nomvarca;
     |FINSI;

     #nomvarli#0 = #aovi0008#0;         || empresa
     #nomvarli#1 = #aovi0008#3;         || trabajador
     #nomvarli#2 = #0#5;                || mes
     #nomvarli#3 = 0;                   || tipo
     #nomvarli#4 = #aovi0013#2;        || concepto
     |LEE 101#nomvarli.=;
     FEstado = FSalida;

     |SI nVariac = 0;
          |SI FEstado = 0;
               |BORRA 020#nomvarli.a;
          |FINSI;
     |SINO;
          || aAlfa = 1;
          || aAlfa = "          " + aAlfa;
          || aAlfa = aAlfa % -110;
          || #nomvarli#5 = aAlfa;          || unidades

          aAlfa = nGraba;
          aAlfa = "          " + aAlfa;
          aAlfa = aAlfa % -112;
          #nomvarli#6 = aAlfa;          || unidades

          |SI FEstado = 0;
               |GRABA 020#nomvarli.a;
          |SINO;
               #nomvarli#0 = #aovi0008#0;         || empresa
               #nomvarli#1 = #aovi0008#3;         || trabajador
               #nomvarli#2 = #0#5;                || mes
               #nomvarli#3 = 0;                   || tipo
               #nomvarli#4 = #aovi0013#2;         || concepto
               |GRABA 020#nomvarli.n;
          |FINSI;
     |FINSI;

     |LIBERA #nomvarli;

     |CIERRA #nomvarli;
     |CIERRA #nomvarca;
|FPRC;

|DEFBUCLE GrabaVar1;
     #aovi0013#1;
     ;
     #labtraba#0, #labtraba#1, 101;
     #labtraba#0, #labtraba#1, 499;
     ;
     NULL, GrabaVar1;
|FIN;

|PROCESO MaximoCat;
     nMaximo = nMaximo + #aovi0003#4;
|FPRC;

|DEFBUCLE MaximoCat;
     #aovi0003#1;
     ;
     #labtraba#87, #labtraba#17, 101;
     #labtraba#87, #labtraba#17, 499;
     ;
     NULL, MaximoCat;
|FIN;

|PROCESO PreciosDietas;
     || busca importe de dieta para el modulo de MARCOS JIMENEZ

     #aomj0002#0 = #labtraba#87;
     #aomj0002#1 = #labtraba#17;
     |LEE 000#aomj0002.=;
     |SI FSalida = 0;
          nPrecioEnt1 = #aomj0002#5;
          nPrecioEnt2 = #aomj0002#4;
          nPrecioMed1 = #aomj0002#9;
          nPrecioMed2 = #aomj0002#8;
          nConceptoEnt = #aomj0002#2;
          nConceptoMed = #aomj0002#6;
     |FINSI;

     #aomj0003#0 = #labtraba#0;
     #aomj0003#1 = #labtraba#1;
     |LEE 000#aomj0003.=;
     |SI FSalida = 0;
          nPrecioEnt1 = #aomj0003#5;
          nPrecioEnt2 = #aomj0003#4;
          nPrecioMed1 = #aomj0003#9;
          nPrecioMed2 = #aomj0003#8;
          nConceptoEnt = #aomj0003#2;
          nConceptoMed = #aomj0003#6;
     |FINSI;
|FPRC;

|PROCESO GrabaVar;
     |ABRE #nomvarca;
     |ABRE #nomvarli;
     |DDEFECTO #nomvarca;

     #nomvarca#0 = #aovi0008#0;         || empresa
     #nomvarca#1 = #aovi0008#3;         || trabajador
     #nomvarca#2 = #0#5;                || mes
     #nomvarca#3 = 0;                   || tipo
     |LEE 101#nomvarca.=;
     |SI FSalida ! 0;
          |GRABA 020#nomvarca.n;
          |LIBERA #nomvarca;
     |FINSI;

     #nomvarli#0 = #aovi0008#0;         || empresa
     #nomvarli#1 = #aovi0008#3;         || trabajador
     #nomvarli#2 = #0#5;                || mes
     #nomvarli#3 = 0;                   || tipo
     #nomvarli#4 = nConcepto;           || concepto
     |LEE 101#nomvarli.=;
     FEstado = FSalida;

     |SI FEstado = 0;
          |BORRA 020#nomvarli.a;
     |FINSI;
     |DDEFECTO #nomvarli;

     aAlfa = nUnidades;
     aAlfa = "          " + aAlfa;
     aAlfa = aAlfa % -110;
     #nomvarli#5 = aAlfa;          || unidades

     |SI nImporte > 0;
          aAlfa = nImporte;
          aAlfa = "            " + aAlfa;
          aAlfa = aAlfa % -112;
          #nomvarli#6 = aAlfa;          || importe
     |FINSI;

     #nomvarli#0 = #aovi0008#0;         || empresa
     #nomvarli#1 = #aovi0008#3;         || trabajador
     #nomvarli#2 = #0#5;                || mes
     #nomvarli#3 = 0;                   || tipo
     #nomvarli#4 = nConcepto;           || concepto
     |GRABA 020#nomvarli.n;

     |LIBERA #nomvarli;

     |CIERRA #nomvarli;
     |CIERRA #nomvarca;
|FPRC;

|PROCESO CalculaDietas;
     || nBruto es el bruto nomina

     || nTotal es el total horas + dietas a precio 1 (a pagar)

     || nBrutoConDietas es el total horas + dietas a precio 2 (precio dieta nomina)
     nBrutoConDietas = nBruto;
     nBrutoConDietas = nBrutoConDietas + (nDietasEnt * nPrecioEnt2);
     nBrutoConDietas = nBrutoConDietas + (nDietasMed * nPrecioMed2);

     nDiferencia = nBrutoConDietas - nTotal;
     |SI nDiferencia [ 0;
          || esto quiere decir que lo que pago en nomina es menor (o igual)
          || que lo que debe cobrar por las horas + dietas a precio 1
          || entonces le meto en variaciones todas las dietas y aparte
          || genero un recibo por la diferencia (si es igual, no habra recibo)
          nRecibo = (-1) * nDiferencia;
          nDiferencia = 0;
          |SI nConceptoEnt > 0; |Y nDietasEnt > 0
               nConcepto = nConceptoEnt;
               nUnidades = nDietasEnt;
               |HAZ GrabaVar;
          |FINSI;
          |SI nConceptoMed > 0; |Y nDietasMed > 0
               nConcepto = nConceptoMed;
               nUnidades = nDietasMed;
               |HAZ GrabaVar;
          |FINSI;
          |ACABA;
     |FINSI;
     |SI nDiferencia > 0;
          || esto es porque lo que cobro en nomina es superior a lo que
          || debo cobrar segun las horas que hecho, por tanto para no tener
          || que generar anticipo, voy quitando dietas empezando por las enteras
          || hasta que ajuste las cantidades

          nDietasEnt = #aovi0008#16;

          nDietasMed = #aovi0008#20;

          |SI nDietasEnt ] 1;      || tengo dietas enteras
               |PARA; |SI nDiferencia > 0; |HACIENDO;
                    |SI nDietasEnt ] 1;
                         nDietasEnt = nDietasEnt - 1;
                         nDiferencia = nDiferencia - (1 * nPrecioEnt2);
                    |SINO;
                         || me he quedado sin dietas enteras y aun tengo diferencia
                         |SAL;
                    |FINSI;
               |SIGUE;

               |SI nDiferencia < 0;
                    || me he pasado, la ultima la vuelvo a poner y empeizo a quitar
                    || medias dietas
                    nDietasEnt = nDietasEnt + 1;
                    nDiferencia = nDiferencia + (1 * nPrecioEnt2);
               |FINSI;
          |FINSI;

          |SI nDietasMed ] 1;
               |PARA; |SI nDiferencia > 0; |HACIENDO;
                    |SI nDietasMed ] 1;
                         nDietasMed = nDietasMed - 1;
                         nDiferencia = nDiferencia - (1 * nPrecioMed2);
                    |SINO;
                         || me he quedado sin dietas enteras y aun tengo diferencia
                         |SAL;
                    |FINSI;
               |SIGUE;
          |FINSI;

          || ahora grabo las dietas en variaciones mes

          |SI nConceptoEnt > 0; |Y nDietasEnt > 0
               nConcepto = nConceptoEnt;
               nUnidades = nDietasEnt;
               |HAZ GrabaVar;
          |FINSI;
          |SI nConceptoMed > 0; |Y nDietasMed > 0
               nConcepto = nConceptoMed;
               nUnidades = nDietasMed;
               |HAZ GrabaVar;
          |FINSI;
          |SI nDiferencia < 0;
               || esta diferencia es la que me llevaria al recibo

               nRecibo = (-1) * nDiferencia;
          |SINO;
               |SI nDiferencia > 0;
                    || no he tenido bastante, el trabajador deberia cobrar entre
                    || horas y dietas a precio 2 menos aun de lo que cobra entre
                    || bruto y dietas a precio 1 aun quitandole las dietas
                    || con lo que le genero un anticipo para que le quite dinero
                    || de su bruto

                    nAntici = nDiferencia;
               |FINSI;
          |FINSI;

     |FINSI;
|FPRC;

|PROCESO aovi0008;
     nPrecio1 = 0;
     nPrecio2 = 0;
     nPrecio3 = 0;
     nNomina = 0;
     nMaximo = 0;
     nRecibo = 0;
     nTotal = 0;
     nVariac = 0;
     nBruto = 0;
     nImporte = 0;

     #labtraba#0 = #aovi0008#0;         || empresa
     #labtraba#1 = #aovi0008#3;         || trabajador
     |LEE 000#labtraba.=;

     swVacac = 0;

     #aovi0001#0 = #labtraba#87;        || convenio
     #aovi0001#1 = #labtraba#17;        || categoria
     |LEE 000#aovi0001.=;
     |SI FSalida = 0;
          nPrecio1 = #aovi0001#93;
          nPrecio2 = #aovi0001#101;
          nPrecio3 = #aovi0001#123;
          |SI #aovi0001#38 = 1;
               swVacac = 1;
          |FINSI;
     |FINSI;

     #aovi0011#0 = #labtraba#0;        || empresa
     #aovi0011#1 = #labtraba#1;        || trabajador
     |LEE 000#aovi0011.=;
     |SI FSalida = 0;
          |SI #aovi0011#125 ! "S";
               nPrecio1 = #aovi0011#93;
               nPrecio2 = #aovi0011#101;
          |FINSI;
          nPrecio3 = #aovi0011#123;
          |SI #aovi0011#38 = 1;
               swVacac = 1;
          |FINSI;
     |FINSI;

     nPrecioEnt1 = 0;
     nPrecioEnt2 = 0;
     nPrecioMed1 = 0;
     nPrecioMed2 = 0;
     nDietasEnt = #aovi0008#16;
     nDietasMed = #aovi0008#20;
     nConceptoEnt = 0;
     nConceptoMed = 0;

     |HAZ PreciosDietas;

     #aovi0008#6 = nPrecio1;                     || precio 1
     #aovi0008#7 = nPrecio2;                     || precio 2
     #aovi0008#8 = nPrecio1 * #aovi0008#5;       || total 1 = precio1 * horas
     #aovi0008#12 = nPrecio3;                    || precio 3

     || Dieta 1 es el precio A PAGAR (al trabajador)
     || Dieta 2 es el precio DIETA

     #aovi0008#14 = nPrecioEnt1;              || precio dieta entera 1
     #aovi0008#15 = nPrecioEnt2;              || precio dieta entera 2
     #aovi0008#17 = nDietasEnt * nPrecioEnt1;

     #aovi0008#18 = nPrecioMed1;              || precio dieta media 1
     #aovi0008#19 = nPrecioMed2;              || precio dieta media 2
     #aovi0008#21 = nDietasMed * nPrecioMed1;

     || el total es el total horas + dietas enteras + dietas medias

     #aovi0008#22 = #aovi0008#8 + #aovi0008#17 + #aovi0008#21;

     || busco nomina del mes en el mantenimiento de calculos

     #nomcalca#0 = #aovi0008#0;         || empresa
     #nomcalca#1 = #aovi0008#3;         || trabajador
     #nomcalca#2 = #aovi0008#2;         || mes
     #nomcalca#3 = 0;                   || tipo
     |LEE 000#nomcalca.=;
     |SI FSalida = 0;
          nBruto = #nomcalca#46;
          nBruto = nBruto - #nomcalca#57 - #nomcalca#58 - #nomcalca#59;
          nBruto = nBruto - #nomcalca#60 - #nomcalca#73;
     |FINSI;
     || nTotal = #aovi0008#8;
     nTotal = #aovi0008#22;

     nImporteVac = 0;
     |SI #nomcalca#66 ! 0;    || he tenido vacaciones
          |SI swVacac = 0;    || tengo que ponerlas como que NO las trabajo
                              || si no se generara menos importe de la cuenta
                              || y generara un anticipo que no procede
               |SI #labtraba#234 ! 0;
                    nImporteVac = #nomcalca#66 * (#labtraba#234 / 5) * nPrecio1;
               |SINO;
                    nImporteVac = #nomcalca#66 * 8 * nPrecio1;
               |FINSI;
          |FINSI;
     |FINSI;
     nBruto = nBruto - nImporteVac; || quito esto del bruto de la nomina si
                                    || existe porque si no al restarlo al
                                    || importe bruto de la nomina se genera
                                    || un anticipo que no es real como pasaba
                                    || con las ITs

     nVariac = 0;
     nAntici = 0;
     nNomina = 0;
     nRestante = 0;

     |SI nDietasEnt = 0; |Y nDietasMed = 0;
          |SI nBruto ] nTotal;
               nNomina = nBruto;
               nVariac = 0;
               nAntici = nBruto - nTotal;
               |HAZ GrabaAntici;
          |SINO;
               nNomina = nTotal;
               nVariac = nTotal - nBruto;
          |FINSI;

          nRestante = nVariac;

          #aovi0012#0 = #labtraba#0;
          #aovi0012#1 = #labtraba#1;
          |LEE 000#aovi0012.=;
          |SI FSalida = 0;
               |BUCLE GrabaVar1;
          |SINO;
               #aovi0002#0 = #labtraba#87;
               #aovi0002#1 = #labtraba#17;
               |LEE 000#aovi0002.=;
               |SI FSalida = 0;
                    |BUCLE GrabaVar2;
               |FINSI;
          |FINSI;
     |SINO;
          |HAZ CalculaDietas;

          |SI nAntici ! 0;
               |HAZ GrabaAntici;
          |FINSI;
     |FINSI;

     |SI nRestante > 0;
          nRecibo = nRecibo + nRestante;
     |FINSI;

     |SI #aovi0014#2 = "N";
          || en nomina
          nConcepto = #aovi0014#3;
          nUnidades = 1;
          nImporte = nRecibo;
          |HAZ GrabaVar;
          nRecibo = 0;
          nImporte = 0;
     |FINSI;

     nNomina = nTotal - nRecibo;

     #aovi0008#9 = nNomina + nAntici;       || total 1 nomina
     #aovi0008#10 = nRecibo;      || total 1 recibo
     #aovi0008#11 = nPrecio2 * #aovi0008#5;      || total 2 = precio2 * horas
     #aovi0008#13 = nPrecio3 * #aovi0008#5;      || total 3 = precio3 * horas

     |GRABA 020#aovi0008.a;
     |LIBERA #aovi0008;
|FPRC;

|DEFBUCLE aovi0008;
     #aovi0008#1;
     ;
     #0#0, #0#4, #0#5, #0#2;
     #0#1, #0#4, #0#5, #0#3;
     be;
     NULL, aovi0008;
|FIN;

|PROCESO aovi0008_limpia;
     |BORRA 020#aovi0008.a;
|FPRC;

|DEFBUCLE aovi0008_limpia;
     #aovi0008#1;
     ;
     #0#0, #0#4, #0#5, #0#2;
     #0#1, #0#4, #0#5, #0#3;
     be;
     NULL, aovi0008_limpia;
|FIN;

|PROCESO horas_aomentra;
     |DDEFECTO #aovi0007;
     |DDEFECTO #aovi0008;

     #aovi0007#0 = #100#2;         || empresa
     #aovi0007#1 = #0#4;                || a¤o
     #aovi0007#2 = #0#5;                || mes
     |LEE 101#aovi0007.=;
     |SI FSalida ! 0;
          #aovi0007#0 = #100#2;         || empresa
          #aovi0007#1 = #0#4;                || a¤o
          #aovi0007#2 = #0#5;                || mes
          |GRABA 020#aovi0007.n;
          |LIBERA #aovi0007;
     |FINSI;

     #aovi0008#0 = #100#2;         || empresa
     #aovi0008#1 = #0#4;                || a¤o
     #aovi0008#2 = #0#5;                || mes
     #aovi0008#3 = #100#3;         || codigo trabajador
     |LEE 101#aovi0008.=;
     FEstado = FSalida;

     #labtraba#0 = #aovi0008#0;
     #labtraba#1 = #aovi0008#3;
     |LEE 000#labtraba.=;

     #aovi0008#4 = #labtraba#2;                   || nombre trabajador
     #aovi0008#5 = #aovi0008#5 + #100#5;     || horas realizadas

     |SI FEstado = 0;
          |GRABA 020#aovi0008.a;
     |SINO;
          #aovi0008#0 = #100#2;         || empresa
          #aovi0008#1 = #0#4;                || a¤o
          #aovi0008#2 = #0#5;                || mes
          #aovi0008#3 = #100#3;         || codigo trabajador
          |GRABA 020#aovi0008.n;
     |FINSI;
|FPRC;

|DEFBUCLE aomentr4;
     #100#1006;
     ;
     fFechaDesde, #0#0, #0#2, 00001, 000000, 00;
     fFechaHasta, #0#1, #0#3, 99999, 999999, 99;
     ;
     NULL, horas_aomentra;
|FIN;

|PROCESO graba_horas_j;
     |DDEFECTO #aovi0007;
     |DDEFECTO #aovi0008;

     #aovi0007#0 = #100#2;         || empresa
     #aovi0007#1 = #0#4;                || a¤o
     #aovi0007#2 = #0#5;                || mes
     |LEE 101#aovi0007.=;
     |SI FSalida ! 0;
          #aovi0007#0 = #100#2;         || empresa
          #aovi0007#1 = #0#4;                || a¤o
          #aovi0007#2 = #0#5;                || mes
          |GRABA 020#aovi0007.n;
          |LIBERA #aovi0007;
     |FINSI;

     #aovi0008#0 = #100#2;         || empresa
     #aovi0008#1 = #0#4;                || a¤o
     #aovi0008#2 = #0#5;                || mes
     #aovi0008#3 = #100#4;         || codigo trabajador
     |LEE 101#aovi0008.=;
     FEstado = FSalida;

     #labtraba#0 = #aovi0008#0;
     #labtraba#1 = #aovi0008#3;
     |LEE 000#labtraba.=;

     #aovi0008#4 = #labtraba#2;                || nombre trabajador
     #aovi0008#5 = #aovi0008#5 + nHoras;       || horas realizadas
     #aovi0008#16 = #aovi0008#16 + nDietasEnt; || dietas enteras
     #aovi0008#20 = #aovi0008#20 + nDietasMed; || medias dietas

     |SI FEstado = 0;
          |GRABA 020#aovi0008.a;
     |SINO;
          #aovi0008#0 = #100#2;         || empresa
          #aovi0008#1 = #0#4;                || a¤o
          #aovi0008#2 = #0#5;                || mes
          #aovi0008#3 = #100#4;         || codigo trabajador
          |GRABA 020#aovi0008.n;
     |FINSI;
|FPRC;

|PROCESO TipoDieta;
     || busca importe de dieta para el modulo de MARCOS JIMENEZ
     swEntera = 0;       || es dieta entera
     swMedia = 0;        || es dieta media

     #aomj0002#0 = #labtraba#87;
     #aomj0002#1 = #labtraba#17;
     |LEE 000#aomj0002.=;
     |SI FSalida = 0;
          |SI nTipoHora = #aomj0002#10;
               swEntera = 1;       || es dieta entera
          |FINSI;
          |SI nTipoHora = #aomj0002#11;
               swMedia = 1;       || es dieta media
          |FINSI;
     |FINSI;

     #aomj0003#0 = #labtraba#0;
     #aomj0003#1 = #labtraba#1;
     |LEE 000#aomj0003.=;
     |SI FSalida = 0;
          |SI nTipoHora = #aomj0003#10;
               swEntera = 1;       || es dieta entera
          |FINSI;
          |SI nTipoHora = #aomj0003#11;
               swMedia = 1;       || es dieta media
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO horas_aojentrh;
     || inicializo variables
     nHoras = 0;
     nDietasEnt = 0;
     nDietasMed = 0;

     #labtraba#0 = #100#2;
     #labtraba#1 = #100#4;
     |LEE 000#labtraba.=;

     nTipoHora = #100#19;
     |SI nTipoHora ! 00;
          |HAZ TipoDieta;
          |SI swEntera = 1;
               nDietasEnt = #100#17;
          |FINSI;
          |SI swMedia = 1;
               nDietasMed = #100#17;
          |FINSI;
     |SINO;
          nHoras = #100#17;
     |FINSI;

     || ya se si lo que tengo es hora normal, dieta entera o dieta media
     || ahora lo grabo

     |HAZ graba_horas_j;
|FPRC;

|DEFBUCLE aojentr7;
     #100#1007;
     ;
     #0#0, "     ", 000000, 000000, fFechaDesde, #0#2, 001;
     #0#1, "zzzzz", 999999, 999999, fFechaHasta, #0#3, 999;
     ;
     NULL, horas_aojentrh;
|FIN;

|DEFBUCLE aojentrh;
     #100#1006;
     ;
     "     ", 000000, #0#0, #0#2, fFechaDesde, 001;
     "zzzzz", 999999, #0#1, #0#3, fFechaHasta, 999;
     ;
     NULL, horas_aojentrh;
|FIN;

|PROCESO ulti_dia;  || halla el ultimo dia del mes
     numer = anynum $ 4;
     |SI numer = 0;
             bisiesto = 1;
     |SINO;
             bisiesto = 0;
     |FINSI;
     |SI mesnum =  1;  topemes = 31;            |FINSI;
     |SI mesnum =  2;  topemes = 28 + bisiesto; |FINSI;
     |SI mesnum =  3;  topemes = 31;            |FINSI;
     |SI mesnum =  4;  topemes = 30;            |FINSI;
     |SI mesnum =  5;  topemes = 31;            |FINSI;
     |SI mesnum =  6;  topemes = 30;            |FINSI;
     |SI mesnum =  7;  topemes = 31;            |FINSI;
     |SI mesnum =  8;  topemes = 31;            |FINSI;
     |SI mesnum =  9;  topemes = 30;            |FINSI;
     |SI mesnum = 10;  topemes = 31;            |FINSI;
     |SI mesnum = 11;  topemes = 30;            |FINSI;
     |SI mesnum = 12;  topemes = 31;            |FINSI;
|FPRC;

|PROCESO CargaDef;
     |DBASS aRutaDef;
     |DBASS aRutaDatos;
     |QBF aRutaDef;
     |QBF aRutaDatos;
     aRutaDef = aRutaDef + "laboral/def/" + aDef + ".mas";
     |CARGA_DEF aRutaDef, enthoras@;

     |SI FSalida ! 0;
         aAlfa = "    Error en CARGADEF: " + aDefCopia + " !!!";
         |MENAV aAlfa;
         |ACABA;
     |FINSI;

     aRutaDatos = aRutaDatos + "laboral/fich/";
     |PATH_DAT #100 aRutaDatos;
     |ABRE #100;
|FPRC;

|PROCESO DescargaDef;
     |CIERRA #100;
     |DESCARGA_DEF #100;
|FPRC;

|PROCESO CargaHorasSub;
     || para cargar las horas de subcontratas del modulo de Marcos Jimenez

     |DBASS aAlfa; |QBF aAlfa;
     aAlfa = aAlfa + "laboral/def/aojentr7.mas";
     |XABRE "E", aAlfa, nHandle;

     |SI FSalida = 0;         || Marcos Jimenez
          aDef = "aojentr7";
          |HAZ CargaDef;
          |BUCLE aojentr7;
          |HAZ DescargaDef;
     |FINSI;

     |XCIERRA nHandle;
|FPRC;

|PROCESO CargaHoras;
     || estoy en el modulo de Marcos Jimenez o estoy en el de Vipei??

     |DBASS aAlfa; |QBF aAlfa;
     aAlfa = aAlfa + "laboral/def/aojentrh.mas";
     |XABRE "E", aAlfa, nHandle;

     |SI FSalida = 0;         || Marcos Jimenez
          aDef = "aojentrh";
     |SINO;                   || Vipei y resto de construccion
          aDef = "aomentr4";
     |FINSI;

     |HAZ CargaDef;

     |SI aDef = "aojentrh";
          |BUCLE aojentrh;
     |SINO;
          |BUCLE aomentr4;
     |FINSI;

     |HAZ DescargaDef;

     |XCIERRA nHandle;
|FPRC;

|PROCESO Calculos;
     aDia = "01";
     nMes = #0#5;
     aMes = nMes;
     aMes = ("00" + aMes) % -102;
     nAny = #0#4;
     aAny = nAny;
     aFecha = aDia + "." + aMes + "." + aAny;
     fFechaDesde = aFecha;

     mesnum = nMes;
     anynum = nAny;
     |HAZ ulti_dia;
     aDia = topemes;
     aDia = ("00" + aDia) % -102;
     aFecha = aDia + "." + aMes + "." + aAny;
     fFechaHasta = aFecha;

     |ABRE #aovi0001;
     |ABRE #aovi0011;
     |ABRE #aovi0002;
     |ABRE #aovi0003;
     |ABRE #aovi0005;
     |ABRE #aovi0007;
     |ABRE #aovi0008;
     |ABRE #labtraba;
     |ABRE #nomcalca;
     |ABRE #aovi0012;
     |ABRE #aovi0013;
     |ABRE #aomj0002;
     |ABRE #aomj0003;
     |ABRE #aovi0014; |LEE 000#aovi0014.p; |CIERRA #aovi0014;

     |BUCLE aovi0008_limpia;

     |HAZ CargaHoras;

     |HAZ CargaHorasSub;

     |BUCLE aovi0008;

     |CIERRA #aomj0002;
     |CIERRA #aomj0003;
     |CIERRA #aovi0013;
     |CIERRA #aovi0012;
     |CIERRA #nomcalca;
     |CIERRA #labtraba;
     |CIERRA #aovi0008;
     |CIERRA #aovi0007;
     |CIERRA #aovi0005;
     |CIERRA #aovi0003;
     |CIERRA #aovi0002;
     |CIERRA #aovi0011;
     |CIERRA #aovi0001;
|FPRC;

|PROCESO BorraVar2;
     #nomvarli#0 = #labtraba#0;
     #nomvarli#1 = #labtraba#1;
     #nomvarli#2 = #0#5;
     #nomvarli#3 = 0;
     #nomvarli#4 = nConcepto;
     |LEE 101#nomvarli.=;
     |SI FSalida = 0;
          |BORRA 020#nomvarli.a;
     |FINSI;
|FPRC;

|PROCESO BorraVar;
     #labtraba#0 = #nomvarli#0;
     #labtraba#1 = #nomvarli#1;
     |LEE 000#labtraba.=;

     swBorra = 0;

     #aovi0003#0 = #labtraba#87;
     #aovi0003#1 = #labtraba#17;
     #aovi0003#2 = #nomvarli#4;
     |LEE 000#aovi0003.=;
     |SI FSalida = 0;
          swBorra = 1;
     |FINSI;

     #aovi0013#0 = #labtraba#0;
     #aovi0013#1 = #labtraba#1;
     #aovi0013#2 = #nomvarli#4;
     |LEE 000#aovi0003.=;
     |SI FSalida = 0;
          swBorra = 1;
     |FINSI;

     #aomj0002#0 = #labtraba#87;
     #aomj0002#1 = #labtraba#17;
     |LEE 000#aomj0002.=;
     |SI FSalida = 0;
          |SI #aomj0002#2= #nomvarli#4; |O #aomj0002#6 = #nomvarli#4;
               swBorra = 1;
          |FINSI;
     |FINSI;

     #aomj0003#0 = #labtraba#0;
     #aomj0003#1 = #labtraba#1;
     |LEE 000#aomj0003.=;
     |SI FSalida = 0;
          |SI #aomj0003#2= #nomvarli#4; |O #aomj0003#6 = #nomvarli#4;
               swBorra = 1;
          |FINSI;
     |FINSI;

     |SI #nomvarli#4 = #aovi0014#3;
          swBorra = 1;
     |FINSI;

     |SI swBorra = 1;
          |BORRA 020#nomvarli.a;
     |FINSI;
|FPRC;

|DEFBUCLE BorraVar;
     #nomvarli#1;
     ;
     #0#0, #0#2, #0#5, 0, 101;
     #0#1, #0#3, #0#5, 0, 499;
     be;
     NULL, BorraVar;
|FIN;

|PROGRAMA;
     |CLS;
     |PINPA #0#0;
     |PINDA #0#0;
     |ENDATOS #1#0;
     |SI FSalida = 0;
          |ABRE #aomparam;
          |LEE 000#aomparam.p;
          |CIERRA #aomparam;

          nEmpAUTO_D = #0#0;
          nEmpAUTO_H = #0#1;
          nTraAUTO_D = #0#2;
          nTraAUTO_H = #0#3;
          nAnyAUTO = #0#4;
          nMesAUTO = #0#5;

          |ABRE #labtraba;
          |ABRE #aovi0003;
          |ABRE #aomj0002;
          |ABRE #aomj0003;
          |ABRE #aovi0014;
          |LEE 000#aovi0014.p;
          |BUCLE BorraVar;
          |CIERRA #aovi0014;
          |CIERRA #aomj0003;
          |CIERRA #aomj0002;
          |CIERRA #aovi0003;
          |CIERRA #labtraba;

          |SUB_EJECUTA "nomcalcu;AUTO";

          |HAZ Calculos;

          |SUB_EJECUTA "nomcalcu;AUTOCONREG";
     |FINSI;
|FPRO;
