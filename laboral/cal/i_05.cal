|INCLUYE i_fiche;
|INCLUYE i_varia;

|PROCESO trabajub; |TIPO 0;       || EN CONTRATOS 05 Y 25 VARIABLES DEL
|SI #0#27 ! #0#2;                 || TRABAJADOR JUBILADO
    |ABRE #2;
        #2#0 = #0#1;
        #2#1 = #0#27;
    |LEE 040#2=;
    |SI FSalida = 0
        |PINTA 1125,#2#2;
        trabanom = #2#2;
        trabafec = #2#11;
        trabapro = #2#19;
        trabacat = #2#18;
        trabacen = #2#77; #0#28 = trabacen; |PINTA #0#28;
        #2#0 = #0#1;
        #2#1 = #0#2;
        |LEE 040#2=;              || vuelve a cargar el trabajador a contratar
    |SINO;
        |MENAV mensa2;
        #0#27 = 0;
        |ERROR;
    |FINSI;
    |CIERRA #2;
|SINO;
    |MENAV mensaL;
    |ERROR;
|FINSI;
|FPRC;

|PROCESO centrjub; |TIPO 0;     || EN CONTRATOS 05 Y 25 VARIABLES DEL
|ABRE #3;                       || CENTRO DE TRABAJO DEL TRABAJO
    #3#0 = #0#1;                || JUBILADO
    #3#1 = #0#28;
|LEE 040#3=;
|SI FSalida = 0;
    |PINTA 1227,#3#2;
        direcc = #3#3;
        numero = #3#4;
        piso = #3#5;
        cod1 = #3#7;
        cod2 = #3#8;
        locali = #3#6;
    |HAZ compac_05;
        centrnom = compactado;
|SINO;
    |MENAV mensa3;
        #0#28 = 0;
    |ERROR;
|FINSI;
|CIERRA #3;
|SI #0#28 ! trabacen;
    |MENAV mensaK;
    |ERROR;
|FINSI;
|FPRC;

|PROCESO equiporc; |TIPO 0;     || CONTRATOS 05 Y 25
    trabanum = #0#32 * 100;     || CALCULA PORCENTAJE JORNADAS
    equi = trabanum / #0#50;
|FPRC;

|PROCESO compac_05;                      || SUBRUTINA DE RUTINA domicili
    setenta = espa * 70;
    compactado = "";                  || DE COMPACTACION DE CAMPOS
|QBF direcc;
|QBF numero;
|QBF piso;
|QBF cod1; cod1 = "00" + cod1;   cod1 = cod1 % -102;
|QBF cod2; cod2 = "000" + cod2;  cod2 = cod2 % -103;
|QBF locali;
    compactado = direcc + coma;
    compactado = compactado + numero;
    compactado = compactado + "  ";
    compactado = compactado + piso;
    compactado = compactado + " (";
    compactado = compactado + cod1;
    compactado = compactado + cod2;
    compactado = compactado + ") ";
    compactado = compactado + locali;
    compactado = compactado + setenta;
    compactado = compactado % 170;
|FPRC;

|PROCESO jornada; |TIPO 0;
     |SI #0#56 = "C";    |ACABA;   |FINSI;

     jorna = ((#0#32*100)/#0#34) ! 0;
     |HAZ JornadaMax;
|FPRC;

|PROCESO JornadaMax; |TIPO 0;
     |SI Campo ! 53;
          #0#53 = jorna;  |PINTA #0#53;
     |SINO;
          jorna = #0#53;
     |FINSI;
     |SI jorna > 77;
          |MENAV "    Jornada maxima excedida (77%) ...";
     |FINSI;
|FPRC;

|PROCESO TipoJornada; |TIPO 0;
     |SI #0#56 = "C";
          |C_M #0#31, 1, "N";
          |C_M #0#32, 1, "N";
          |C_M #0#33, 1, "N";
          |C_M #0#34, 1, "N";
          |C_M #0#53, 1, "N";
     |SINO;
          |C_M #0#31, 1, "S";
          |C_M #0#32, 1, "S";
          |C_M #0#33, 1, "S";
          |C_M #0#34, 1, "S";
          |C_M #0#53, 1, "S";
     |FINSI;
|FPRC;
