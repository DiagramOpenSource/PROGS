|FICHEROS;
     nomw0006 #0,MANTE;
     labtraba #1;

     temp0001@ #500;
     nomconli;
     nom900de;
|FIN;

|VARIABLES;
     valor = 4;
     concep = 2;
     nModo = 0;
     nPos1 = 0;
     nPos2 = 0;
     &enConcepto = 0;
     &eaDescripcion = "";
|FIN;

|INCLUYE i_auxil;

|PROCESO carga_con;
     convenio = #1#87;
     concepto = #0Campo;
|FPRC;

|PROCESO si_modif;
     |CAMPO_MODIFICA #0#4, 1, "S";
|FPRC;

|PROCESO no_modif;
     |CAMPO_MODIFICA #0#4, 1, "N";
|FPRC;

|PROCESO Comprueba; |TIPO 0;
     nModo = (FEntrada / 100) ! 0;

     |ABRE #nomconli;
     #nomconli#0 = #labtraba#87;
     #nomconli#2 = #0#2;
     |LEE 000#nomconli.=;
     |SI FSalida = 0; |O #nomconli#2 ] 900;
          |SI FSalida = 0;
               #0#5 = #nomconli#6;
               |PINTA #0#5;
          |FINSI;
          |SI #nomconli#2 > 900; |Y #nomconli#2 < 999;
               |ABRE #nom900de;
               |SELECT #2#nom900de;
               #nom900de#1 = #0#2;
               |LEE 000#nom900de.=;
               |SI FSalida = 0;
                    #0#5 = #nom900de#3;
                    |PINTA #0#5;
               |SINO;
                    aAlfa = "    ATENCION: El concepto 900 especificado no existe. Pulse ";
                    aAlfa = aAlfa + "F2 para consultar la lista de funciones";
                    |MENAV aAlfa;
                    |ERROR;
               |FINSI;
               |SELECT #1#nom900de;
               |CIERRA #nom900de;
          |FINSI;
          |SI nModo = 2;
               |GRABA 020#0a;
               |LIBERA #0;
          |FINSI;
     |SINO;
          aAlfa = "    ATENCION: Concepto no Definido en convenio. Pulse F11 ";
          aAlfa = aAlfa + "para consultar.";
          |MENAV aAlfa;
          |ERROR;
     |FINSI;
     |CIERRA #nomconli;
|FPRC;

|PROCESO BuscaCon; |TIPO 11;
     |SI FSalida = 13;
          aAlfa = "SELECT nomconli.2,nomconli.6 FROM nomconli INTO tm" + Usuario;
          aAlfa = aAlfa + ".def WHERE 0 == " + #labtraba#87;
          |SQL aAlfa;
          |SI FSalida ! 0;
               |MENAV "    No hay definidos conceptos en el convenio";
               |ERROR;
               |ACABA;
          |FINSI;
          |DBASE aAlfa; |QBF aAlfa; aAlfa = aAlfa + "def/tm" + Usuario + ".mas";
          |CARGA_DEF aAlfa , temp0001@;
          |SI FSalida = 0;
               aAlfa = "tm" + Usuario; |NOME_DAT #500 aAlfa;
               |ABRE #temp0001@;
               nModo = 2 + 4 + 8 + 16;
               nPos1 = 1250; nPos2 = 2380;
               |C_V #temp0001@#0, 1, "S";
               |C_V #temp0001@#1, 1, "S";
               |LINEAL_SIMPLE #1#temp0001@,nModo, nPos1, nPos2, NULL, NULL, NULL;
               |SI FSalida = 0;
                    #nomw0006#2 = #temp0001@#0;
                    |PINTA #nomw0006#2;
                    #nomw0006#5 = #temp0001@#1;
                    |PINTA #nomw0006#5;
               |SINO;
                    |ERROR;
               |FINSI;
               |CIERRA #temp0001@;
               |DELINDEX #temp0001@;
               |DESCARGA_DEF #temp0001@;
          |FINSI;
          |DBASE aAlfa; |QBF aAlfa; aAlfa = aAlfa + "def/tm" + Usuario + ".mas";
          |FBORRA aAlfa;
          |ACABA;
     |FINSI;
|FPRC;

|PROCESO ConsultaLineal900;
     |ABRE #nom900de;
     |CONSULTA_CLAVE #1#nom900de;
     |SI FSalida = 0;
          #0#2 = #nom900de#1;
          |PINTA #0#2;
     |SINO;
          |ERROR;
     |FINSI;
     |CIERRA #nom900de;
|FPRC;
