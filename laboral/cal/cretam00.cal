|FICHEROS;
     cretam00 #0;
     nomcontr;
     labempre;
     labclean;
     silcon06;
|FIN;

|VARIABLES;
     aRuta = "";
     aLocal = "";
     aDirLocal = "";
     aUnidad = "";
     aAux = "";
     aPrograma = "";
     aPathDSSelect = "";
     aBorra = "";
     aParaIni = "";
     aCarpetaCaptura = "";
     aTextoIni = "";
     nHandle = 0;
     aEjecuta = "";
     aAbre = "";
     aPath = "";
     nError = 0;
     aAlfa = "";
     aAlfa1 = "";
     aAlfa2 = "";
     aFichero = "";
     aLong = "";
     nLong = 0;
     nCarac = 0;
     nPos = 0;
     aCarac = "";
     aProg = "";
     swSigue = 0;

     aLon = "";
     nLon = 0;
     swSal = 0;
     &eaCadena = "";
     &eaCadenaSal = "";
     &swBasico = 0;
     &swMsDos = 0;

     aOrigen = "";
     aServidor = "";
     aDestino = "";
     aIPRemoto = "";
     nCanal = 0;

     aBase = "";
     aBaseLocal = "";
     FLinea = 0;
     aLinea = "";
     nLinea = 0;
     aRutaFonts = "";
     aWindows = "";
     {1}aContiene = "";
     swPaseAgired = 0;
     swAginom = 0;
     swIplabor2 = 0;
     &enEmpresa = 0;
     &enSwActiva = 0;
|FIN;

/****************************************************************************/
/**********   PROCESO PARA PASAR LAS EMPRESAS ACTIVAS EN AGIRED  ************/
/****************************************************************************/

|PROCESO silcon06;
     #labempre#0 = #silcon06#0;
     |LEE 101#labempre.=;
     |SI FSalida = 0; |Y #labempre#50 ! "S";
          enEmpresa = #labempre#0;
          |HAZ ActivaSN_plb;

          |SI enSwActiva = 1;
               #labempre#67 = "S";
               |GRABA 020#labempre.a;
          |FINSI;

          |LIBERA #labempre;
     |FINSI;
|FPRC;

|DEFBUCLE silcon06;
     #silcon06#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, silcon06;
|FIN;

|PROCESO Iniciando_emp;
     #labempre#67 = "N";
     |GRABA 020#labempre.a;
     |LIBERA #labempre;
|FPRC;

|DEFBUCLE Iniciando_emp;
     #labempre#1;
     ;
     INICIO;
     FINAL;
     be;
     NULL, Iniciando_emp;
|FIN;

|PROCESO PaseAgired;
     |PINTA 1327, "ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿"
     |PINTA 1427, "³   Inicializando nuevos campos.   ³";
     |PINTA 1527, "³ Por favor, espere unos segundos. ³";
     |PINTA 1627, "³                                  ³"
     |PINTA 1727, "ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ"

     |ABRE #labempre;
     |ABRE #labclean;

     #labclean#0 = "labempre";
     #labclean#1 = 67
     |LEE 101#labclean.=;
     |SI FSalida ! 0;
          |BUCLE Iniciando_emp;

          #labclean#0 = "labempre";
          #labclean#1 = 67;
          #labclean#2 = "S";
          |GRABA 020#labclean.n;
          |LIBERA #labclean;
     |FINSI;

     |BUCLE silcon06;

     swPaseAgired = 1;

     |CIERRA #labclean;
     |CIERRA #labempre;

     |PINTA 1327, "                                    ";
     |PINTA 1427, "                                    ";
     |PINTA 1527, "                                    ";
     |PINTA 1627, "                                    ";
     |PINTA 1727, "                                    ";
|FPRC;

/****************************************************************************/
/**********   PROCESO DESDE LA PANTALLA PARA BUSCAR EL FICHERO   ************/
/****************************************************************************/

|PROCESO Browse;
     swSigue = 0;
     aAlfa = #0#0;
     |QBF aAlfa;
     |SI FSalida = 10;
          swSigue = 1;
     |FINSI;
     |SI FSalida = 0; |Y aAlfa = "";
          swSigue = 1;
     |FINSI;
     |SI swSigue = 0;
          |ACABA;
     |FINSI;

     aRuta    = "";

     |HAZ Traete_dsselect;

     |ESPECIFICA "RBASS", aContiene;
     aDirLocal = aContiene1;
     aUnidad = aDirLocal % 102;

     aAux = aUnidad + "\DOCUMENTOS\";
     aPrograma = aAux + "dsselect.exe";

     aPathDSSelect = aAux;

     aBorra = aPathDSSelect + "dsselect.txt";
     |RFBORRA aBorra;

     aParaIni = aAux + "dsselect.ini";
     |RFBORRA aParaIni;

     aCarpetaCaptura = aUnidad;
     |SI Campo = 2;
          aTextoIni = &34 + aCarpetaCaptura + &34 + " " + &34 + aBorra + &34 + " c";
     |FINSI;
     |SI Campo = 3;
          aTextoIni = &34 + aCarpetaCaptura + &34 + " " + &34 + aBorra + &34 + " C";
     |FINSI;
     |XABRE "CW", aParaIni, nHandle;
     |SI FSalida ] 0;
          |XGRABA nHandle, aTextoIni;
     |FINSI;
     |XCIERRA nHandle;

     aEjecuta = aPrograma;
     |RSYSTEM aEjecuta;

     aAbre = aPathDSSelect + "dsselect.txt";
     |XABRE "CU", aAbre, nHandle;
     |SI FSalida < 0;
          aPath = "";
          nError = 1;
          |ACABA;
     |SINO;
          |XLEE nHandle, 250, aPath;
          |SI FSalida < 0
               nError = 1;
          |SINO;
               |QBF aPath;
               |SI aPath ! "";
                    aAlfa    = aPath;
                    aFichero = "";
                    aLong    = aAlfa % 0;
                    nLong    = aLong;
                    aRuta    = "";
                    |PARA nCarac = 1;  |SI nCarac [ nLong;  |HACIENDO nCarac = nCarac + 1;
                          nPos   = (nCarac * 100) + 1;
                          aCarac =  aAlfa % nPos;
                          |SI aCarac = "/";  |O aCarac = "\";
                              aProg = "";
                          |SINO;
                              aProg = aProg + aCarac;
                          |FINSI;

                          aRuta = aRuta + aCarac;
                    |SIGUE;
               |FINSI;
          |FINSI;
     |FINSI;
     |XCIERRA nHandle;
     aBorra = aPathDSSelect + "dsselect.txt";
     |RFBORRA aBorra;

     swMsDos = 1;
     swBasico = 0;
     eaCadena = aRuta;
     |HAZ CambiaTildes_plb;
     aRuta = eaCadenaSal;
     #0Campo = aRuta;
     |PINTA #0Campo;
|FPRC;

|PROCESO CompruebaRuta;
     aRuta = #0Campo;
     |QBF aRuta;
     aAlfa = aRuta % 101;
     aAlfa1 = aRuta % -101;

     |SI aAlfa = "/"; |Y aAlfa1 ! "/";
          aRuta = aRuta + "/";
     |SINO;
          |SI aAlfa1 ! "\";
               aRuta = aRuta + "\";
          |FINSI;
     |FINSI;

     #0Campo = aRuta;
     |PINTA #0Campo;
|FPRC;

|PROCESO Restaura;
     |POPV;
|FPRC;

|PROCESO ActivaReal;
     |SI #0#8 = "P";
          |CAMPO_MODIFICA #0#9, 1, "N";
          |CAMPO_MODIFICA #0#10, 1, "N";
          |CAMPO_VISUAL #0#9, 1, "N";
          |CAMPO_VISUAL #0#10, 1, "N";
          |PINTA 0914, "³                                                      ³";
     |SINO;
          |CAMPO_MODIFICA #0#9, 1, "S";
          |CAMPO_MODIFICA #0#10, 1, "S";
          |CAMPO_VISUAL #0#9, 1, "S";
          |CAMPO_VISUAL #0#10, 1, "S";
          |PINTA 0914, "³ Periodo liquidativo de inicio en REAL ...:    /      ³";
          |PINTA #0#9;
          |PINTA #0#10;
     |FINSI;
|FPRC;

|PROCESO Rutas;
     |DBASE aBase;  |QBF aBase;
     aOrigen = aBase + "ejecutables/dspath.exe";

     aContiene = "";
     |ESPECIFICA "RBASS", aContiene;
     aBaseLocal = aContiene1;

     aDestino = aBaseLocal + "bin/dspath.exe";
     |XABRE "CE", aDestino, nCanal;
     |SI FSalida ] 0;
          || existe ya
          |XCIERRA nCanal;
     |SINO;
          || me lo traigo
          |IP_REMOTO aIPRemoto;
          |SI aIPRemoto ! "";
               |ENVIA_FICHERO aOrigen, aDestino;
          |SINO;
               |COPIA_FICHERO aOrigen, aDestino;
          |FINSI;
     |FINSI;

     |RSYSTEM aDestino;
     aDestino = aBaseLocal + "bin/dspath.txt";
     |XABRE "CU", aDestino, nCanal;
     |SI FSalida ] 0;
          |PARA; |SI FLinea ] 0; |HACIENDO;
               |XLEE nCanal, 250, aLinea;
               FLinea = FSalida;
               nLinea = nLinea + 1;
               |SI nLinea = 1;
                    aAlfa = aLinea % 105;
                    |QBF aAlfa;
                    |SI aAlfa = "WinXP";
                         aWindows = "XP";
                    |FINSI;
                    |SI aAlfa = "WinVi";
                         aWindows = "Vista";
                    |FINSI;
                    |SI aAlfa = "Win7";
                         aWindows = "7";
                    |FINSI;
                    |SI aAlfa = "Win8"; |O aAlfa = "Win8.";
                         aWindows = "8";
                    |FINSI;
                |FINSI;
                |SI nLinea = 3;
                    aRutaFonts = aLinea;
                    |QBF aRutaFonts;
                |FINSI;
          |SIGUE;
     |FINSI;

     |XCIERRA nCanal;
|FPRC;

|PROCESO WindowsXP;
     aIPRemoto = "";
     |IP_REMOTO aIPRemoto;  |QBT aIPRemoto;

     |DBASS aOrigen; |QBF aOrigen;      || busco patrones
     aServidor = aOrigen;

     |SI swAginom = 1;
          aOrigen = aServidor + "aginom/ejecutables/code128.ttf";
     |SINO;
          aOrigen = aServidor + "laboral/ejecutables/code128.ttf";
     |FINSI;
     aDestino = aRutaFonts + "\code128.ttf";

     |SI aIPRemoto ! "";
          |ENVIA_FICHERO aOrigen, aDestino;
     |SINO;
          |COPIA_FICHERO aOrigen, aDestino;
     |FINSI;
|FPRC;

|PROCESO Windows7;
     aAlfa = "    ATENCION. Para la correcta visualización de los informes "
     aAlfa = aAlfa + "se debe instalar un tipo de letra adicional";
     |MENAV aAlfa;
     aAlfa = "    A cotinuación se abrirá una ventana de Windows para realizar ";
     aAlfa = aAlfa + "la instalación."
     |MENAV aAlfa;
     aAlfa = "    Pulse el botón Instalar en la parte superior de la ventana "
     aAlfa = aAlfa + "y ciérrela una vez instalada.";
     |MENAV aAlfa;

     aIPRemoto = "";
     |IP_REMOTO aIPRemoto;  |QBT aIPRemoto;

     |DBASS aOrigen; |QBF aOrigen;      || busco patrones
     aServidor = aOrigen;

     |SI swAginom = 1;
          aOrigen = aServidor + "aginom/ejecutables/code128.ttf";
     |SINO;
          aOrigen = aServidor + "laboral/ejecutables/code128.ttf";
     |FINSI;
     aDestino = "C:\DOCUMENTOS\code128.ttf";

     |SI aIPRemoto ! "";
          |ENVIA_FICHERO aOrigen, aDestino;
     |SINO;
          |COPIA_FICHERO aOrigen, aDestino;
     |FINSI;

     |RSYSTEM aDestino;
     |RFBORRA aDestino;

     aDestino = "C:\Windows\Fonts\";
     aDestino = aDestino + "code128.ttf";
     |XABRE "CE", aDestino, nCanal;
     |SI FSalida ] 0;
          |XCIERRA nCanal;
          |MENAV "    El nuevo tipo de letra ha sido instalado correctamente";
     |FINSI;
|FPRC;

|PROCESO InstalaFuente;
     |HAZ Rutas;

     aDestino = aRutaFonts + "\code128.ttf";
     |XABRE "CE", aDestino, nCanal;
     |SI FSalida ] 0;
          |XCIERRA nCanal;
          |ACABA;
     |FINSI;

     |SI aWindows = "XP"; |O aWindows = "Vista";
          |HAZ WindowsXP;
     |FINSI;
     |SI aWindows = "7"; |O aWindows = "8";
          |HAZ Windows7;
     |FINSI;
|FPRC;

|PROCESO Version;
     |DBASE aAlfa;
     aAlfa1 = aAlfa - "aginom";
     |SI aAlfa ! aAlfa1;
          swAginom = 1;
     |SINO;
          swIplabor2 = 1;
     |FINSI;
|FPRC;

|PROGRAMA;
     |HAZ Version;
     |CLS;

     |ABRE #0;
     |DDEFECTO #0;
     |LEE 001#0p;
     |SI FSalida ! 0;
          |DDEFECTO #0;
          |GRABA 020#0n;
     |FINSI;
     |LEE 101#0=;
     |SI #0#7 = "N";
          |HAZ PaseAgired;
     |FINSI;

     aAlfa1 = "";
     aAlfa2 = "";

     |PINPA #0#0
     |PINDA #0#0;
     |HAZ ActivaReal;
     |ENDATOS #1#0;
     |SI FSalida = 0;
          |GRABA 020#0a;
     |FINSI;
     |SI swPaseAgired = 1;
          #0#7 = "S";
     |FINSI;

     |SI #0#1 = "S";
          |ABRE #nomcontr;
          |LEE 101#nomcontr.p;
          |SI FSalida = 0;
               #nomcontr#17 = 3;
               #nomcontr#18 = 3;
               #nomcontr#61 = 1;
               |GRABA 020#nomcontr.a;
          |FINSI;
          |CIERRA #nomcontr;
          |HAZ InstalaFuente;
     |FINSI;
     |LIBERA #0;
     |CIERRA #0;
|FPRO;
