|FICHEROS;
     semtrans #0;
     labtraba #2;        || trabajadores
     semequiv #3;        || equiv. codigos
     semtrati #4;        || titulos transf.
     nomrevca #12;       || transfer.cabs.
     nomrevli #13;       || transfer.lins.
|FIN;

|VARIABLES;
     nSeleccio = 0;
     aAlfa1 = "";
     aAlfa2 = "";
     nNume1 = 0;
     nNume2 = 0;
     fFech1 = @;
     fFech2 = @;
     nPara1 = 0;
     nPara2 = 0;

     nDreg = 0;
     nHreg = 0;
     nDemp = 0;
     nHemp = 0;

     nLectura = 0;
     &nTotBanco = 0;
     &nTotSucur = 0;
     &nTotFinal = 0;
     &nBanco = 0;
     &nSucur = 0;
     &nSwTotBanco = 0;
|FIN;
____________________________________/ CALCULOS DE PANTALLA
|PROCESO mayus; |TIPO 0;
     #0Campo = #0Campo > #0Campo;
     |PINTA #0Campo;
|FPRC;

|PROCESO seleccion; |TIPO 0;
     |SI #0#2 = "D";
          aAlfa1 = "S";
          aAlfa2 = "N";
     |SINO;
          aAlfa1 = "N";
          aAlfa2 = "S";
     |FINSI;
     |C_M #0#0, 1, aAlfa1;
     |C_M #0#1, 1, aAlfa1;
     |C_M #0#12, 1, aAlfa1;
     |C_M #0#16, 1, aAlfa1;

     |C_M #0#18, 1, aAlfa2;
     |C_M #0#19, 1, aAlfa2;
     |C_M #0#20, 1, aAlfa2;
     |C_M #0#21, 1, aAlfa2;
     |C_M #0#22, 1, aAlfa2;
     |C_M #0#23, 1, aAlfa2;
     |C_M #0#24, 1, aAlfa2;
     |C_M #0#25, 1, aAlfa2;
     |C_M #0#26, 1, aAlfa2;
     |C_M #0#27, 1, aAlfa2;
     |C_M #0#28, 1, aAlfa2;
     |C_M #0#29, 1, aAlfa2;
     |C_M #0#30, 1, aAlfa2;
     |C_M #0#31, 1, aAlfa2;
     |C_M #0#32, 1, aAlfa2;
     |C_M #0#33, 1, aAlfa2;
     |C_M #0#34, 1, aAlfa2;
     |C_M #0#35, 1, aAlfa2;
     |C_M #0#36, 1, aAlfa2;
     |C_M #0#37, 1, aAlfa2;
|FPRC;
____________________________________/ LECTURA Y CALCULO
|PROCESO Proceso;
     #12#0 = #13#0;
     #12#1 = #13#1;
     |LEE 000#12=;
     |SI FSalida ! 0;    |ACABA;   |FINSI;   || lee revisiones cabs.

     |SI #13#6 > 0;                          || si tiene importe positivo
          |DDEFECTO #2;
          #2#0 = #13#0;
          #2#1 = #13#3;
          |LEE 000#2=;                       || lee trabajador
          |SI FSalida = 0;

               |DDEFECTO #3;
               #3#1 = #2#0;
               #3#2 = #2#1;
               |LEE 000#3=;                  || lee equiv. codigos

               |HAZ Ordena;

               |SI nBanco = 0;     nBanco = #0#8; |FINSI;
               |SI nSucur = 0;     nSucur = #0#9; |FINSI;

               |SI nBanco ! #0#8;
                    |SI nLectura = 1;   |PRINT;   |FINSI;
                    |SI nLectura = 2;
                         |PRINT;
                         |PIEINF;
                         |FININF;
                         |INFOR "semtran2";
                    |FINSI;
                    |SI nLectura = 3;
                         nSwTotBanco = 1;
                         |PIEINF;
                         |FININF;
                         |INFOR "semtran3";
                    |FINSI;
                    nBanco = #0#8;
                    nSucur = #0#9;
                    nTotSucur = 0;
                    nTotBanco = 0;
               |SINO;
                    |SI nSucur ! #0#9;
                         |SI nLectura = 2;   |PRINT;   |FINSI;
                         |SI nLectura = 3;
                              nSwTotBanco = 0;
                              |PIEINF;
                              |FININF;
                              |INFOR "semtran3";
                         |FINSI;
                         nSucur = #0#9;
                         nTotSucur = 0;
                    |FINSI;
               |FINSI;
               |SI nLectura = 3;   |PRINT;   |FINSI;

               nTotSucur = nTotSucur + #13#6;
               nTotBanco = nTotBanco + #13#6;
               nTotFinal = nTotFinal + #13#6;

               nSeleccio = 1;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Ordena;
     aAlfa1 = #13#5 % 104;    #0#8 = aAlfa1;      || banco
     aAlfa2 = #13#5 % 504;    #0#9 = aAlfa2;      || sucursal
|FPRC;

|DEFBUCLE Leyendo;
     #13#1;
     ;
     nDemp, nDreg,      1;
     nHemp, nHreg, 999999;
     ;
     NULL, Ordena, NULL, NULL, NULL, Proceso;
     #0#8, #0#9, #13#0, #13#3;
|FIN;

|PROCESO Inicializa;
     nBanco = 0;
     nSucur = 0;
     nTotBanco = 0;
     nTotSucur = 0;
     nTotFinal = 0;
     nSeleccio = 0;
|FPRC;

|PROCESO Impresion;
     |SI #0#2 = "S";
          |DDEFECTO #semtrati;
          |ABRE #semtrati;
          #semtrati#0 = nDemp;
          #semtrati#1 = nDreg;
          |LEE 000#semtrati.=;
          |CIERRA #semtrati;
     |SINO;
          |DDEFECTO #semtrati;
     |FINSI;

     |SI #0#4 = "S";
          |MENSA "0000Imprimiendo Resumen Bancos ...";
          |HAZ Inicializa;
          |INFOR "semtran1";
          nLectura = 1;
          |BUCLE Leyendo;
          |PRINT;
          |PIEINF;
          |FININF;
     |FINSI;
     |SI #0#5 = "S";
          |MENSA "0000Imprimiendo Resumen Sucursales ...";
          |HAZ Inicializa;
          |INFOR "semtran2";
          nLectura = 2;
          |BUCLE Leyendo;
          |PRINT;
          |PIEINF;
          |FININF;
     |FINSI;
     |SI #0#6 = "S";
          |MENSA "0000Imprimiendo Resumen Nominas ...";
          |HAZ Inicializa;
          |INFOR "semtran3";
          nLectura = 3;
          |BUCLE Leyendo;
          nSwTotBanco = 1;
          |PIEINF;
          |FININF;
     |FINSI;
|FPRC;

|PROCESO Tipo3;
     |IMPRE 0;
     |SI FSalida = 0;
          |ABRE #2;
          |ABRE #3;
          |SELECT #2#3;
          |ABRE #12;
          |SI #0#2 = "D";
               nDreg = #0#0;       nHreg = #0#1;
               nDemp = #0#12;      nHemp = #0#16;
               |HAZ Impresion;
          |FINSI;
          |SI #0#2 = "S";
               |PARA nPara1 = 18; |SI nPara1 [ 27; |HACIENDO nPara1 = nPara1 + 1;
                    nPara2 = nPara1 + 10;
                    |SI #0nPara1 ! 0;|Y #0nPara2 ! 0;
                         nDreg = #0nPara2;    nHreg = nDreg;
                         nDemp = #0nPara1;    nHemp = nDemp;
                         |HAZ Impresion;
                    |FINSI;
               |SIGUE;
          |FINSI;
          |CIERRA #2;
          |CIERRA #3;
          |CIERRA #12;

          |SI nSeleccio = 0;
               |MENAV "    Seleccion vacia ...";
          |FINSI;
     |SINO;
          |MENAV "0000Proceso abortado.";
     |FINSI;
     |FINIMP;
|FPRC;

|PROGRAMA;
     |CLS;
     |CABEZA "Transferencias";
     |ABRE #0;
     |PINPA #0#0;
     |LEE 101#0p;
     |SI FSalida ! 0;    |GRABA 020#0b; |FINSI;
     #0#2 = "S";
     |PINDA #0#0;
     |ENDATOS #1#0;
     |SI FSalida = 0;
          |HAZ Tipo3;
          |GRABA 020#0a;
     |FINSI;
     |LIBERA #0;
     |CIERRA #0;
|FPRO;
