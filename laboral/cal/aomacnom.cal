|FICHEROS;
    aomacnom #0;
    aomentra #1;         || entrada de horas I
    labtraba #2;         || trabajadores
    aomconce #3;         || conceptos
    nomvarca #4;         || variaciones cabs.
    nomvarli #5;         || variaciones lins.
|FIN;

|VARIABLES;
    nume1 = 0;
    nume2 = 0;
    nume3 = 0;
    nume4 = 0;
    alfa1 = "";
    alfa2 = "";
    alfa3 = "";
    alfa4 = "";
    fech1 = @;
    fech2 = @;
    fech3 = @;
    fech4 = @;
    FEstado = 0;
    seleccio = 0;
    sw_rup = 0;

    mes = 0;
    any = 0;
    bisiesto = 0;
    topemes = 0;
    desfec = @;
    hasfec = @;
    fecsys = @;
    p_mes = "";
    ant_emp = 0;
    ant_tra = 0;
    ant_tip = 0;
    unida = 0;
    valor = 0;
    total = 0;
|FIN;
____________________________________/ CALCULOS DE PANTALLA
|PROCESO mirames; |TIPO 7;
    fech1 = ~;
    alfa1 = fech1;
    alfa1 = alfa1 % 402;
    #0Campo = alfa1;
|PINTA #0Campo;
|FPRC;

|PROCESO pintames; |TIPO 0;
|SI #0Campo =  1;  p_mes = "ENERO     ";  |FINSI;
|SI #0Campo =  2;  p_mes = "FEBRERO   ";  |FINSI;
|SI #0Campo =  3;  p_mes = "MARZO     ";  |FINSI;
|SI #0Campo =  4;  p_mes = "ABRIL     ";  |FINSI;
|SI #0Campo =  5;  p_mes = "MAYO      ";  |FINSI;
|SI #0Campo =  6;  p_mes = "JUNIO     ";  |FINSI;
|SI #0Campo =  7;  p_mes = "JULIO     ";  |FINSI;
|SI #0Campo =  8;  p_mes = "AGOSTO    ";  |FINSI;
|SI #0Campo =  9;  p_mes = "SEPTIEMBRE";  |FINSI;
|SI #0Campo = 10;  p_mes = "OCTUBRE   ";  |FINSI;
|SI #0Campo = 11;  p_mes = "NOVIEMBRE ";  |FINSI;
|SI #0Campo = 12;  p_mes = "DICIEMBRE ";  |FINSI;
|PINTA 1433, p_mes;
|FPRC;

|PROCESO defecto;   || CARGA EL A¥O DE LA FECHA DEL SISTEMA
    fecsys = ~;
    alfa1 = fecsys;
    alfa1 = alfa1 % 402;
    mes = alfa1;

    fecsys = ~;
    alfa1 = fecsys;
    alfa1 = alfa1 % -104;
    any = alfa1;

    nume1 = any $ 4;
|SI nume1 = 0;
        bisiesto = 1;
|SINO;
        bisiesto = 0;
|FINSI;

|SI mes =  1;  topemes = 31;             |FINSI;
|SI mes =  2;  topemes = 28 + bisiesto;  |FINSI;
|SI mes =  3;  topemes = 31;             |FINSI;
|SI mes =  4;  topemes = 30;             |FINSI;
|SI mes =  5;  topemes = 31;             |FINSI;
|SI mes =  6;  topemes = 30;             |FINSI;
|SI mes =  7;  topemes = 31;             |FINSI;
|SI mes =  8;  topemes = 31;             |FINSI;
|SI mes =  9;  topemes = 30;             |FINSI;
|SI mes = 10;  topemes = 31;             |FINSI;
|SI mes = 11;  topemes = 30;             |FINSI;
|SI mes = 12;  topemes = 31;             |FINSI;
|FPRC;

|PROCESO fechas; |TIPO 7;
|HAZ defecto;

    alfa1 = topemes; alfa1 = "00" + alfa1;   alfa1 = alfa1 % -102;
    alfa2 = mes;     alfa2 = "00" + alfa2;   alfa2 = alfa2 % -102;
    alfa3 = any;

    alfa4 = "01."       + alfa2 + "." + alfa3;  desfec = alfa4; || desde fecha
    alfa4 = alfa1 + "." + alfa2 + "." + alfa3;  hasfec = alfa4; || hasta fecha

    #0#0 = desfec;  |PINTA #0#0;
    #0#1 = hasfec;  |PINTA #0#1;
|FPRC;
____________________________________/
|PROCESO graba_cab;
|DDEFECTO #4;
    #4#0 = ant_emp;
    #4#1 = ant_tra;
    #4#2 = #0#12;
    #4#3 = 0;
|LEE 000#4=;
|SI FSalida ! 0;    |GRABA 020#4n; |FINSI;
|FPRC;

|PROCESO graba_tra;
    #2#0 = ant_emp;
    #2#1 = ant_tra;
|LEE 121#2=;
|SI FSalida = 0;
        #2#98 = total;
    |GRABA 020#2a;
    |SI #2#97 = 0;
            alfa1 = "    Trabajador [" + #2#0 + "-" + #2#1 + "] con tipo de ajuste CERO ...";
        |MENAV alfa1;
    |FINSI;
|FINSI;
|LIBERA #2;
    total = 0;
    ant_emp = #1#2;
    ant_tra = #1#3;
|FPRC;

|PROCESO graba_var;
    unida = unida ! 2;
    valor = valor ! 2;
|SI unida ! 0;|Y valor ! 0;
    |HAZ graba_cab;
        #3#0 = ant_tip;
    |LEE 000#3=;
    |SI FSalida = 0;
        |SI #3#5 < unida;
                unida = #3#5;      || limite de unidades
        |FINSI;
        |DDEFECTO #5;
            #5#0 = #4#0;      || empresa
            #5#1 = #4#1;      || trabajador
            #5#2 = #4#2;      || mes
            #5#3 = #4#3;      || tipo
            #5#4 = #3#4;      || concepto convenio
        |LEE 101#5=;
            FEstado = FSalida;

            alfa1 = unida; alfa1 = ("          " + alfa1) % -110;
            #5#5 = alfa1;     || unidades
            alfa1 = valor; alfa1 = ("            " + alfa1) % -112;
            #5#6 = alfa1;     || valor

        |SI FEstado ! 0; |GRABA 020#5n; |FINSI;
        |SI FEstado = 0; |GRABA 020#5a; |FINSI;
        |LIBERA #5;
    |SINO;
            alfa1 = "    Concepto [" + #3#0 + "] inaccesible o inexistente ...";
        |MENAV alfa1;
    |FINSI;
|FINSI;
    ant_tip = #1#4;
    unida = 0;
    valor = 0;
|FPRC;

|PROCESO pintando;
    #0#06 = #1#0;   |PINTA #0#06;
    #0#07 = #1#2;   |PINTA #0#07;
    #0#08 = #1#3;   |PINTA #0#08;
    #0#09 = #1#1;   |PINTA #0#09;
    #0#11 = #1#7;   |PINTA #0#11;
    #0#10 = #1#4;   |PINTA #0#10;
|FPRC;

|PROCESO preparando1;
    #2#0 = #1#2;
    #2#1 = #1#3;
|LEE 000#2=;
|SI FSalida = 0;
    |HAZ pintando;
    |SI sw_rup = 0;
            sw_rup = 1;
            ant_emp = #1#2;
            ant_tra = #1#3;
            ant_tip = #1#4;
    |FINSI;

    |SI ant_emp ! #1#2;|O ant_tra ! #1#3;
        |HAZ graba_var;
        |HAZ graba_tra;
    |SINO;
        |SI ant_tip ! #1#4;
            |HAZ graba_var;
        |FINSI;
    |FINSI;

    |SI unida = 0;
            unida = #1#5;
            valor = #1#6 / unida;
    |SINO;
            nume1 = (unida * valor) + #1#6;
            unida = unida + #1#5;
            valor = nume1 / unida;
    |FINSI;
        total = total + #1#6;
|SINO;
        alfa1 = "    Trabajador [" + #2#0 + "-" + #2#1 + "] inaccesible o inexistente ...";
    |MENAV alfa1;
|FINSI;
|FPRC;

|DEFBUCLE preparando;
 #1#6;
 ;
 #0#2, #0#4,  1, #0#0,      1,  0;
 #0#3, #0#5, 99, #0#1, 999999, 99;
 ;
 NULL, preparando1;
|FIN;

|PROCESO tipo3; |TIPO 3;
|ABRE #2;      || trabajadores
|ABRE #3;      || conceptos
|ABRE #4;      || variaciones cabs.
|ABRE #5;      || variaciones lins.
|BUCLE preparando;
|SI sw_rup = 0;
    |MENAV "    Seleccion vacia ...";
|SINO;
    |HAZ graba_var;
    |HAZ graba_tra;
|FINSI;
|CIERRA #2;
|CIERRA #3;
|CIERRA #4;
|CIERRA #5;
|FPRC;
