|FICHEROS;
     nomcal01 #0;
     nomcal02 #1,MANTE;
     nomfical #3;
     nomvarca;
     nomvarli;
     labtraba;
     nomcatli;
     nomvart3;
     labmunic;

     nomcalca;
     nomhicca;
|FIN;

|VARIABLES;
     &enCalen = 0;
     &eSwNoCrea = 0;

     nInkey   = 0;
     nSwSal   = 0;
     aFichero = "";
     aAlfUlt  = "";
     aAlfa    = "";
     aAlfa1   = "";
     aAlfa2   = "";
     aAlfa3   = "";
     aAlfa4   = "";
     nNume1   = 0;
     nNume2   = 0;
     nNume3   = 0;
     nNume4   = 0;
     nNume5   = 0;
     nNPeso   = 0;
     nVPeso   = 0;
     ndMes    = 0;
     nhMes    = 0;
     y        = 0;
     nT1      = 0;
     nT2      = 0;
     nDia     = 0;
     aDia     = "";
     aRV      = "";
     aRH      = "";
     nModoEntrada = 0;
     nLnEstado    = 0;

     nPara1   = 0;
     nPara2   = 0;
     nPara3   = 0;
     fFecha1  = @;
     fFecha   = @;

     nTotal   = 0;
     nMensual = 0;
     nPDSem   = 0;
     nTDMes   = 0;
     nSaldo   = 0;
     &DVentas = "";

     NumMes = 0;
     dig1 = 0;
     enEjercicio = 0;
     NombreMes = "";
     nEmp = 0;
     nTra = 0;
     nAny = 0;
     nCal = 0;
     nModo = 0;
     nPeso = 0;
     nCampo = 0;
     nCampoD = 0;
     nCampoH = 0;
     aDesde = "";
     aHasta = "";
     nDesde = 0;
     nHasta = 0;
     aDiaQueBusco = "";
     nDiaQueBusco = 0;
     aAlfa0 = "";

     nUnidades = 0;
     aUnidades = "";
     aTexto = "";
     nConcepto = 0;
     nHoras = 0;
     nDias = 0;
     nConvenio = 0;
     nMes = 0;
     FEstado = 0;
     nNume = 0;
     nInd = 0;

     aCodPos = "";

     &nCodCal = 0;
     &aAnyCal = "";
     &aCPCal = "";
     &aMunPos = "";

     fFechaAlta = @;
     fFechaBaja = @;
|FIN;

|PROCESO Defectos_nomcal01;
     fFecha = ~;
     aAlfa = fFecha;
     aAlfa = aAlfa % -104;
     #0#2 = aAlfa;
     |PINTA #0#2;
|FPRC;

|PROCESO Contador; |TIPO 0;
     nDias = 0;
     nHoras = 0;
     |PARA nCampo = 4; |SI nCampo [ 45; |HACIENDO nCampo = nCampo + 1;
          |SI #nomcal02#nCampo# ! 0;
               nHoras = nHoras + #nomcal02#nCampo#;
               nDias = nDias + 1;
          |FINSI;
     |SIGUE;
     #nomcal02#82 = nDias;
     #nomcal02#83 = nHoras;

     |PINTA #nomcal02#82;
     |PINTA #nomcal02#83;
|FPRC;

|PROCESO Contador1; |TIPO 7;
     |HAZ Contador;
|FPRC;

|PROCESO CompTra;
     |SI #labtraba#42 ! "P"; |Y #labtraba#42 ! "X"; |Y #labtraba#42 ! "T";
          aAlfa = "    ATENCION. El trabajador no tiene clave de cotización ";
          aAlfa = aAlfa + "a tiempo parcial: X, T ó P";
          |MENAV aAlfa;
          |ERROR;
     |FINSI;
|FPRC;

|PROCESO CompCal;


/*
     |ABRE #nomfical;
     #nomfical#0 = #nomcal01#3;
     #nomfical#1 = #nomcal01#2;
     |LEE 000#nomfical.=;
     |SI FSalida = 0;
     |CIERRA #nomfical;
*/
|FPRC;

|PROCESO GrabaConcepto;
     #nomvarli#0 = nEmp;
     #nomvarli#1 = nTra;
     #nomvarli#2 = nMes;
     #nomvarli#3 = 0;
     #nomvarli#4 = nConcepto;
     |LEE 101#nomvarli.=;
     FEstado = FSalida;

     aUnidades = nUnidades;
     aUnidades = ("            " + aUnidades) % -112;
     nUnidades = aUnidades;
     #nomvarli#5 = nUnidades;
     #nomvarli#6 = aTexto;
     |SI FEstado = 0;
          |GRABA 020#nomvarli.a;
     |SINO;
          |GRABA 020#nomvarli.n;
     |FINSI;

     |LIBERA #nomvarli;
|FPRC;

|PROCESO Los900;
     nConcepto = 905;
     nUnidades = nHoras;
     aTexto = "H.Mes Cot.";
     |HAZ GrabaConcepto;

     nConcepto = 911;
     nUnidades = nHoras;
     aTexto = "H.Max.Mes";
     |HAZ GrabaConcepto;

     nConcepto = 912;
     nUnidades = nHoras;
     aTexto = "H.Me.Tra.";
     |HAZ GrabaConcepto;

     nConcepto = 914;
     nUnidades = nDias;
     aTexto = "Dias Alta";
     |HAZ GrabaConcepto;

     nConcepto = 918;
     nUnidades = nHoras;
     aTexto = "Horas TC2";
     |HAZ GrabaConcepto;

     nNume = 40;

     |ABRE #nomcatli;
     #nomcatli#0 = #labtraba#87;
     #nomcatli#1 = #labtraba#17;
     #nomcatli#2 = 909;
     |LEE 000#nomcatli.=;
     |SI FSalida = 0;
          nNume = #nomcatli#3;
     |FINSI;
     |CIERRA #nomcatli;

     nConcepto = 917;
     nUnidades = nHoras * (nNume / 40) / 7;
     aTexto = "H.Me.Tra.";
     |HAZ GrabaConcepto;
|FPRC;

|PROCESO nomvart3;
     nConcepto = #nomvart3#1;
     nUnidades = nHoras;
     aTexto = "";
     |HAZ GrabaConcepto;
|FPRC;

|DEFBUCLE nomvart3;
     #nomvart3#1;
     3;
     nConvenio, 001, "S";
     nConvenio, 499, "S"
     ;
     NULL, nomvart3;
|FIN;

|PROCESO Cuenta;
     #nomcal02#0 = nEmp;
     #nomcal02#1 = nTra;
     #nomcal02#2 = nAny;
     #nomcal02#3 = nMes;
     |LEE 000#nomcal02.=;
     |PARA nCampo = 4; |SI nCampo [ 45; |HACIENDO nCampo = nCampo + 1;
          |SI #nomcal02#nCampo# ! 0;
               nHoras = nHoras + #nomcal02#nCampo#;
               nDias = nDias + 1;
          |FINSI;
     |SIGUE;
     #nomcal02#82 = nDias;
     #nomcal02#83 = nHoras;

     |PINTA #nomcal02#82;
     |PINTA #nomcal02#83;
|FPRC;

|PROCESO LeeTrab;
     |ABRE #labtraba;
     #labtraba#0 = #nomcal02#0;
     #labtraba#1 = #nomcal02#1;
     |LEE 000#labtraba.=;
     |CIERRA #labtraba;
     nConvenio = #labtraba#87;
|FPRC;

|PROCESO Variaciones;
     |HAZ LeeTrab;

     |ABRE #nomvarca;
     |DDEFECTO #nomvarca;
     #nomvarca#0 = nEmp;
     #nomvarca#1 = nTra;
     #nomvarca#2 = nMes;
     #nomvarca#3 = 0;
     |LEE 101#nomvarca.=;
     |SI FSalida ! 0;
          #nomvarca#0 = nEmp;
          #nomvarca#1 = nTra;
          #nomvarca#2 = nMes;
          #nomvarca#3 = 0;
          |GRABA 020#nomvarca.n;
     |FINSI;
     |LIBERA #nomvarca;
     |CIERRA #nomvarca;

     nDias = 0;
     nHoras = 0;
     |HAZ Cuenta;

     |ABRE #nomvarli;

     |BUCLE nomvart3;
     |HAZ Los900;

     |CIERRA #nomvarli;
|FPRC;

|PROCESO ActualizaVar; |TIPO 2;
     nInd = 0 +.1;

     |SI nInd = -1;
          |ACABA;
     |FINSI;

     |GRABA 000#nomcal02.a;

     nEmp = #nomcal02#0;
     nTra = #nomcal02#1;
     nAny = #nomcal02#2;
     nMes = #nomcal02#3;

     nDias = 0;
     nHoras = 0;
     |HAZ Cuenta;
     |SI nHoras ! 0;
          |SI #labtraba#42 = "P";
               |HAZ Variaciones;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO BorraNomvarli;
     #nomvarli#0 = #nomcal02#0;
     #nomvarli#1 = #nomcal02#1;
     #nomvarli#2 = #nomcal02#3;
     #nomvarli#3 = 0;
     #nomvarli#4 = nConcepto;
     |LEE 101#nomvarli.=;
     |SI FSalida = 0;
          |BORRA 020#nomvarli.a;
     |FINSI;
     |LIBERA #nomvarli;
|FPRC;

|PROCESO BorraVar; |TIPO 2;
     nModo = (FEntrada / 100) ! 0;
/*
     || la verdad, esto no entiendo para que

     |SI nModo = 3;
          |ABRE #nomvarli;
          nConcepto = 101;
          |HAZ BorraNomvarli;

          |CIERRA #nomvarli;
     |FINSI;
*/
|FPRC;

|PROCESO Error13; |TIPO 11;
     |SI #nomcal02#3 = 12; |Y FSalida = 3;
          |ERROR;
     |FINSI;
|FPRC;

|PROCESO PintaMes;
     |SI #1#3 = 1;  aAlfa = "ENERO     "; |FINSI;
     |SI #1#3 = 2;  aAlfa = "FEBRERO   "; |FINSI;
     |SI #1#3 = 3;  aAlfa = "MARZO     "; |FINSI;
     |SI #1#3 = 4;  aAlfa = "ABRIL     "; |FINSI;
     |SI #1#3 = 5;  aAlfa = "MAYO      "; |FINSI;
     |SI #1#3 = 6;  aAlfa = "JUNIO     "; |FINSI;
     |SI #1#3 = 7;  aAlfa = "JULIO     "; |FINSI;
     |SI #1#3 = 8;  aAlfa = "AGOSTO    "; |FINSI;
     |SI #1#3 = 9;  aAlfa = "SEPTIEMBRE"; |FINSI;
     |SI #1#3 = 10; aAlfa = "OCTUBRE   "; |FINSI;
     |SI #1#3 = 11; aAlfa = "NOVIEMBRE "; |FINSI;
     |SI #1#3 = 12; aAlfa = "DICIEMBRE "; |FINSI;

     |ATRI I;
     |PINTA 0911, aAlfa;
     |ATRI 0;
|FPRC;

|PROCESO PresentaMes; |TIPO 7;
     |HAZ PintaMes;

     aRV = &179;  aRH = &205;  nDia   = 1;

     nNume1 = 3 + #1#47;
     nNume2 = nNume1 + #1#48 - 1;
     |PARA nPara3 = 4; |SI nPara3 [ 45; |HACIENDO nPara3 = nPara3 + 1;
          aAlfa1 = "N";
          |SI nPara3 ] nNume1; |Y nPara3 [ nNume2;
               aAlfa1 = "S";
          |FINSI;

          |CAMPO_ATRIBUTO #1nPara3, D, 0, 0;
          |C_V #1nPara3, 1, aAlfa1;
          |C_M #1nPara3, 1, aAlfa1;
          |C_P #1nPara3,0,nNume4;

          nNume4 = nNume4 - 1;
          nNume5 = nNume4 - 96;
          |SI aAlfa1 = "N";
               aAlfa2 = aRV + "         "+ aRV;
               |PINTA nNume4, aAlfa2;
               aAlfa3 = aRH + aRH;
               |PINTA nNume5, aAlfa3;
          |SINO;
               aDia = nDia; |QBF aDia; aDia = ("   " + aDia) % -102;
               nNPeso = nDia + 50;
               nVPeso = #1nNPeso;

               |ATRI I; |PINTA nNume5, aDia; |ATRI 0;

               |SI nVPeso = 1;     || festivo
                    |ATRI P; |PINTA nNume5, aDia; |ATRI 0;
                    |C_M #1nPara3, 1, "N";
                    |CAMPO_ATRIBUTO #1nPara3, S, 0, 0;
               |FINSI;
               |SI nVPeso = 2;     || no es alta
                    |ATRI P; |PINTA nNume5, aDia; |ATRI 0;
                    |C_M #1nPara3, 1, "N";
                    |CAMPO_ATRIBUTO #1nPara3, S, 2, 0;
               |FINSI;
               |SI nVPeso = 3;     || IT
                    |ATRI P; |PINTA nNume5, aDia; |ATRI 0;
                    |C_M #1nPara3, 1, "N";
                    |CAMPO_ATRIBUTO #1nPara3, S, 1, 0;
               |FINSI;

               |PINTA #1nPara3;
               nDia = nDia + 1;
          |FINSI;
     |SIGUE;
     || nTotal   = #0#7;
     ||nMensual = #1#46;
|FPRC;

|PROCESO borra_nomcal02;
     |BORRA 020#nomcal02.a;
|FPRC;

|DEFBUCLE borra_nomcal02;
     #nomcal02#1;
     ;
     #nomcal01#0, #nomcal01#1, #nomcal01#2, 01;
     #nomcal01#0, #nomcal01#1, #nomcal01#2, 12;
     be;
     NULL, borra_nomcal02;
|FIN;

|PROCESO Tipo2; |TIPO 2;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 3;
          |BUCLE borra_nomcal02;
     |FINSI;
|FPRC;

|PROCESO NombreMes;
     |SI NumMes = 1;  NombreMes = "Enero     "; |FINSI;
     |SI NumMes = 2;  NombreMes = "Febrero   "; |FINSI;
     |SI NumMes = 3;  NombreMes = "Marzo     "; |FINSI;
     |SI NumMes = 4;  NombreMes = "Abril     "; |FINSI;
     |SI NumMes = 5;  NombreMes = "Mayo      "; |FINSI;
     |SI NumMes = 6;  NombreMes = "Junio     "; |FINSI;
     |SI NumMes = 7;  NombreMes = "Julio     "; |FINSI;
     |SI NumMes = 8;  NombreMes = "Agosto    "; |FINSI;
     |SI NumMes = 9;  NombreMes = "Septiembre"; |FINSI;
     |SI NumMes = 10; NombreMes = "Octubre   "; |FINSI;
     |SI NumMes = 11; NombreMes = "Noviembre "; |FINSI;
     |SI NumMes = 12; NombreMes = "Diciembre "; |FINSI;
|FPRC;

|PROCESO ITS;
     |ABRE #nomcalca;
     |ABRE #nomhicca;
     |ABRE #nomvarca;

     || PRIMERO MIRO MENSUAL

     #nomcalca#0 = #labtraba#0;
     #nomcalca#1 = #labtraba#1;
     #nomcalca#2 = #1#3;
     #nomcalca#3 = 0;
     |LEE 000#nomcalca.=;
     |SI FSalida = 0;
          nNume = #nomcalca#31 + #nomcalca#32 + #nomcalca#33 + #nomcalca#34;
          nNume = nNume +  #nomcalca#65 + #nomcalca#66 + #nomcalca#78;

          |SI nNume ! 0;
               |PARA nCampo = 27; |SI nCampo [ 30; |HACIENDO nCampo = nCampo + 1;
                    nDesde = 0;
                    nHasta = 0;
                    nCampoD = nCampo - 8;
                    nCampoH = nCampo - 4;
                    |SI #nomcalca#nCampo# ! " ";
                         |SI #nomcalca#nCampoD# = 0;
                              nDesde = 1;
                         |SINO;
                              nDesde = #nomcalca#nCampoD#;
                         |FINSI;
                         |SI #nomcalca#nCampoH# = 0;
                              nHasta = nTDMes;
                         |SINO;
                              nHasta = #nomcalca#nCampoH#;
                         |FINSI;
                         |SI nDesde [ nDia; |Y nDia [ nHasta;
                              nPeso = 3;
                         |FINSI;
                    |FINSI;
               |SIGUE;
          |FINSI;
     |SINO;
/*
          || SI NO, MIRO VARIACIONES

          || DE MOMENTO NO, METERME EN ESTO ES UN LIO

          #nomvarca#0 = #labtraba#0;
          #nomvarca#1 = #labtraba#1;
          #nomvarca#2 = #1#3;
          #nomvarca#3 = 0;
          |LEE 000#nomvarca.=;
          |SI FSalida = 0;
               |PARA nCampo = 12; |SI nCampo [ 15; |HACIENDO nCampo = nCampo + 1;
                    nDesde = 0;
                    nHasta = 0;
                    nCampoD = nCampo - 8;
                    nCampoH = nCampo - 4;
                    |SI #nomvarca#nCampo# ! " ";
                         |SI #nomvarca#nCampoD# = 0;
                              nDesde = 1;
                         |SINO;
                              nDesde = #nomvarca#nCampoD#;
                         |FINSI;
                         |SI #nomvarca#nCampoH# = 0;
                              nHasta = nTDMes;
                         |SINO;
                              nHasta = #nomvarca#nCampoH#;
                         |FINSI;
                         |SI nDesde [ nDia; |Y nDia [ nHasta;
                              nPeso = 3;
                         |FINSI;
                    |SIGUE;
                    nNume = #labtraba#89 + #labtraba#92;
                    |SI nNume ! 0;
                         |SI #nomvarca#12 = " "; |Y #nomvarca#13 = " ";
                         |Y #nomvarca#14 = " "; |Y #nomvarca#15 = " ";
                              nPeso = 3;
                         |FINSI;
                    |FINSI;
               |FINSI;
          |FINSI;
*/
     |FINSI;

     #nomhicca#0 = #labtraba#0;
     #nomhicca#1 = #labtraba#1;
     #nomhicca#66 = #1#2 - 2000;
     #nomhicca#2 = #1#3;
     #nomhicca#3 = 0;
     |LEE 000#nomhicca.=;
     |SI FSalida = 0;
          nNume = #nomhicca#31 + #nomhicca#32 + #nomhicca#33 + #nomhicca#34;
          nNume = nNume +  #nomhicca#65 + #nomhicca#105 + #nomhicca#119;

          |SI nNume ! 0;
               |PARA nCampo = 27; |SI nCampo [ 30; |HACIENDO nCampo = nCampo + 1;
                    nDesde = 0;
                    nHasta = 0;
                    nCampoD = nCampo - 8;
                    nCampoH = nCampo - 4;
                    |SI #nomhicca#nCampo# ! " ";
                         |SI #nomhicca#nCampoD# = 0;
                              nDesde = 1;
                         |SINO;
                              nDesde = #nomhicca#nCampoD#;
                         |FINSI;
                         |SI #nomhicca#nCampoH# = 0;
                              nHasta = nTDMes;
                         |SINO;
                              nHasta = #nomhicca#nCampoH#;
                         |FINSI;
                         |SI nDesde [ nDia; |Y nDia [ nHasta;
                              nPeso = 3;
                         |FINSI;
                    |FINSI;
               |SIGUE;
          |FINSI;
     |FINSI;

     |CIERRA #nomvarca;
     |CIERRA #nomhicca;
     |CIERRA #nomcalca;
|FPRC;

|PROCESO AsignaPesos;
               |PARA nDia = 1; |SI nDia [ nTDMes; |HACIENDO nDia = nDia + 1;

                    || 0: laborable
                    || 1: festivo

                    nPeso = 0;

                    || primero el semanal
                    aAlfa0 = nDia; |QBF aAlfa0;
                    aAlfa0 = ("00" + aAlfa0) % -102;
                    aAlfa1 = NumMes; |QBF aAlfa1;
                    aAlfa1 = ("00" + aAlfa1) % -102;
                    aAlfa2 = nAny;
                    aAlfa3 = aAlfa0 + "."  + aAlfa1 + "." + aAlfa2;
                    fFecha = aAlfa3;
                    fFecha1 = fFecha % 1;
                    aAlfa3 = fFecha1 % 1102;
                    |SI aAlfa3 = "Lu"; nPeso = #nomfical#2; |FINSI;
                    |SI aAlfa3 = "Ma"; nPeso = #nomfical#3; |FINSI;
                    |SI aAlfa3 = "Mi"; nPeso = #nomfical#4; |FINSI;
                    |SI aAlfa3 = "Ju"; nPeso = #nomfical#5; |FINSI;
                    |SI aAlfa3 = "Vi"; nPeso = #nomfical#6; |FINSI;
                    |SI aAlfa3 = "Sa"; nPeso = #nomfical#7; |FINSI;
                    |SI aAlfa3 = "Do"; nPeso = #nomfical#8; |FINSI;

                    || despues el diario

                    |PARA nCampo = 11; |SI nCampo [ 98; |HACIENDO nCampo = nCampo + 3;
                         |SI #nomfical#nCampo# = 1;
                              nCampoD = nCampo - 2;
                              nCampoH = nCampo - 1;
                              aDesde = (#nomfical#nCampoD# % 302) + (#nomfical#nCampoD# % 102);
                              aHasta = (#nomfical#nCampoH# % 302) + (#nomfical#nCampoH# % 102);
                              nDesde = aDesde;

                              nHasta = aHasta;
                              aAlfa1 = NumMes; |QBF aAlfa1;
                              aAlfa1 = ("00" + aAlfa1) % -102;
                              aAlfa2 = nDia; |QBF aAlfa2;
                              aAlfa2 = ("00" + aAlfa2) % -102;
                              aDiaQueBusco = aAlfa1 + aAlfa2; || (mes+dia)
                              nDiaQueBusco = aDiaQueBusco;
                              |SI nDesde [ nDiaQueBusco; |Y nDiaQueBusco [ nHasta;
                                   nPeso = #nomfical#nCampo#;
                              |FINSI;
                         |FINSI;
                    |SIGUE;

                    |PARA nCampo = 100; |SI nCampo [ 158; |HACIENDO nCampo = nCampo + 2;
                         |SI #nomfical#nCampo# = 1;
                              nCampoD = nCampo - 1;
                              nCampoH = nCampo - 1;
                              aDesde = (#nomfical#nCampoD# % 302) + (#nomfical#nCampoD# % 102);
                              aHasta = (#nomfical#nCampoH# % 302) + (#nomfical#nCampoH# % 102);
                              nDesde = aDesde;

                              nHasta = aHasta;
                              aAlfa1 = NumMes; |QBF aAlfa1;
                              aAlfa1 = ("00" + aAlfa1) % -102;
                              aAlfa2 = nDia; |QBF aAlfa2;
                              aAlfa2 = ("00" + aAlfa2) % -102;
                              aDiaQueBusco = aAlfa1 + aAlfa2; || (mes+dia)
                              nDiaQueBusco = aDiaQueBusco;
                              |SI nDesde [ nDiaQueBusco; |Y nDiaQueBusco [ nHasta;
                                   nPeso = #nomfical#nCampo#;
                              |FINSI;
                         |FINSI;
                    |SIGUE;

                    nNPeso = nDia + 50;

                    || PESOS
                    || 0: laborable
                    || 1: festivo
                    || 2: no esta en alta
                    || 3: esta en IT

                    || comprobacion de fechas de alta y baja

                    |HAZ LeeTrab;
                    fFechaAlta = #labtraba#36;
                    aAlfa = #labtraba#37;
                    |QBF aAlfa;
                    |SI aAlfa = ""; |O aAlfa = "..........";
                         fFechaBaja = "31.12.2999";
                    |SINO;
                         fFechaBaja = #labtraba#37;
                    |FINSI;

                    aAlfa1 = nDia; |QBF aAlfa1; aAlfa1 = ("00" + aAlfa1) % -102;
                    aAlfa2 = #1#3; |QBF aAlfa2; aAlfa2 = ("00" + aAlfa2) % -102;
                    aAlfa3 = nAny;
                    aAlfa = aAlfa1 + "." + aAlfa2 + "." + aAlfa3;
                    fFecha = aAlfa;
                    |SI fFechaAlta [ fFecha; |Y fFecha [ fFechaBaja;
                         || correcto, el trabajador esta de alta
                    |SINO;
                         nPeso = 2;     || TRABAJADOR EN BAJA
                    |FINSI;

                    || comprobacion de periodos en IT

                    |SI nPeso ! 2;
                         |HAZ ITS;
                    |FINSI;

                    #1nNPeso = nPeso;
               |SIGUE;
|FPRC;

|PROCESO DiasMes;
     nTDMes = 31;
     |SI NumMes = 4; |O NumMes = 6; |O NumMes = 9; |O NumMes = 11;
          nTDMes = 30;
     |SINO;
          |SI NumMes = 2;
               nTDMes = 28;
               |SI nAny = 1992; |O nAny = 1996; |O nAny = 2000;
               |O nAny = 2004; |O nAny = 2008; |O nAny = 2012;
               |O nAny = 2016; |O nAny = 2020; |O nAny = 2024;
                    nTDMes = 29;
               |FINSI;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO CreoMeses;
     |ABRE #1;

     |PARA nPara1 = ndMes; |SI nPara1 [ nhMes; |HACIENDO nPara1 = nPara1 + 1;

          |DDEFECTO #1;
          #1#0 = #0#0;         || Empresa
          #1#1 = #0#1;         || Trabajador
          #1#2 = #0#2;         || A¤o
          #1#3 = nPara1;       || Mes
          |LEE 000#1=;
          |SI FSalida ! 0;     || si existe no lo creo, claro

               || NumMes = dig1 + (nPara1 - 1);
               NumMes = nPara1
               |SI NumMes > 12; NumMes = NumMes - 12; |FINSI;
               |HAZ NombreMes;

               aAlfa1 = NumMes; |QBF aAlfa1;
               aAlfa1 = ("00" + aAlfa1) % -102;
               aAlfa2 = nAny;
               aAlfa3 = "01." + aAlfa1 + "." + aAlfa2;
               fFecha = aAlfa3;
               fFecha1 = fFecha % 1;
               aAlfa3 = fFecha1 % 1102;
               |SI aAlfa3 = "Lu";  nPDSem = 1; |FINSI;
               |SI aAlfa3 = "Ma";  nPDSem = 2; |FINSI;
               |SI aAlfa3 = "Mi";  nPDSem = 3; |FINSI;
               |SI aAlfa3 = "Ju";  nPDSem = 4; |FINSI;
               |SI aAlfa3 = "Vi";  nPDSem = 5; |FINSI;
               |SI aAlfa3 = "Sa";  nPDSem = 6; |FINSI;
               |SI aAlfa3 = "Do";  nPDSem = 7; |FINSI;

               |HAZ DiasMes;

               |DDEFECTO #1;
               #1#0 = #0#0;         || Empresa
               #1#1 = #0#1;         || Trabajador
               #1#2 = #0#2;         || A¤o
               #1#3 = nPara1;       || Mes
               #1#47 = nPDSem;
               #1#48 = nTDMes;
               #1#49 = NombreMes;
               #1#50 = NumMes;

               |GRABA 020#1n;

               |HAZ AsignaPesos;

               |GRABA 020#1a;
          |SINO;
               nPara1 = #1#3;       || Mes
               nPDSem = #1#47;
               nTDMes = #1#48;
               NombreMes = #1#49;
               NumMes = #1#50;

               |HAZ DiasMes;

               |HAZ AsignaPesos;

               |GRABA 020#1a;
          |FINSI;
     |SIGUE;

     |CIERRA #1;
|FPRC;

|PROCESO CreaAuxi;
     nEmp = #nomcal01#0;
     nTra = #nomcal01#1;
     nAny = #nomcal01#2;
     nCal = #nomcal01#3;

     ndMes  = 1;
     nhMes  = 12;

     |HAZ LeeTrab;
     aAlfa1 = #labtraba#8; aAlfa1 = "00" + aAlfa1;  aAlfa1 = aAlfa1 % -102;
     aAlfa2 = #labtraba#9; aAlfa2 = "000" + aAlfa2; aAlfa2 = aAlfa2 % -103;

     aCodPos = aAlfa1 + aAlfa2;    || Codigo Postal
/*
     nCodCal = #nomcal01#3;
     aAnyCal = #nomcal01#2
     aCPCal = aCodPos;
     aMunPos = #labtraba#6;

     |HAZ LeeCalendario;
*/
     |ABRE #nomfical;
     #nomfical#0 = #nomcal01#3;
     #nomfical#1 = #nomcal01#2;
     |LEE 000#nomfical.=;
     |CIERRA #nomfical;

     |HAZ CreoMeses;
|FPRC;

|PROCESO inferior_cal;
     #nomcal02#0 = #nomcal01#0;
     #nomcal02#1 = #nomcal01#1;
     #nomcal02#2 = #nomcal01#2;
     #nomcal02#3 = 01;
|FPRC;

|PROCESO superior_cal;
     #nomcal02#0 = #nomcal01#0;
     #nomcal02#1 = #nomcal01#1;
     #nomcal02#2 = #nomcal01#2;
     #nomcal02#3 = 12;
|FPRC;

|PROCESO Tipo3; |TIPO 3;
     |SI FSalida = 0;
          |MODULO aAlfa; |QBF aAlfa;
          |SI aAlfa = "labtraba";
               nModo = (FEntrada / 100) ! 0;
               |SI nModo = 1;
                    |GRABA 020#nomcal01.n;
               |FINSI;
          |FINSI;
          |HAZ CreaAuxi;
          |ENTLINEAL #1#nomcal02, -4, 2, 22, 0, inferior_cal, superior_cal;
     |FINSI;
|FPRC;
