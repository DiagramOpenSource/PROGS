|FICHEROS;
    nomemilt #0;    || informe empresas ilt
    labempre #1;    || empresas
    labtraba #2;    || trabajadores
    nomcalca #3;    || calculos cabs.
    nomhicca #4;    || historico calculos cabs.
    nomemitm #10;   || temporal empresas ilt
|FIN;

|VARIABLES;
    alfa1 = "";
    alfa2 = "";
    alfa3 = "";
    alfa4 = "";
    nume1 = 0;
    nume2 = 0;
    nume3 = 0;
    nume4 = 0;
    para1 = 0;
    para2 = 0;
    para3 = 0;
    para4 = 0;
    fech1 = @;
    fech2 = @;
    fech3 = @;
    fech4 = @;
    elsw1 = 0;
    elsw2 = 0;
    elsw3 = 0;
    elsw4 = 0;
    FEstado = 0;
    seleccio = 0;
    modo = 0;
    sw_rup = 0;

    &p_mes = "";
    &p_mes1 = "";
    &p_mes2 = "";
    &sw_lin = 0;
    ant_mutua = -1;
    ant_empre = -1;
    &da = 0;
    &bcc = 0;
    &bat = 0;
    &bit = 0;
    &di = 0;
    &t1 = 0;
    &t2 = 0;
    &t3 = 0;
    &t4 = 0;
    &t5 = 0;
    &tt1 = 0;
    &tt2 = 0;
    &tt3 = 0;
    &tt4 = 0;
    &tt5 = 0;
|FIN;
____________________________________/
|PROCESO mayus; |TIPO 0;
    #0Campo = #0Campo > #0Campo;
|PINTA #0Campo;
|FPRC;

|PROCESO defecto; |TIPO 7; ||calcula el a¤o por defecto segun fecha sistema
    fech1 = ~;
    alfa1 = fech1;
    alfa1 = alfa1 % -102;
    #0Campo = alfa1;
|PINTA #0Campo;
|FPRC;

|PROCESO mirames; |TIPO 7; || CALCULA EL MES POR DEFECTO SEGUN FECHA SISTEMA
     |SI Campo = 4;
          fech1 = ~;
          alfa1 = fech1;
          alfa1 = alfa1 % 402;
          #0Campo = alfa1;
          |PINTA #0Campo;
     |SINO;
          #0Campo = #0#4;
          |PINTA #0Campo;
     |FINSI;
|FPRC;

|PROCESO pintames; |TIPO 0;      || PINTA EL MES LITERAL
|SI #0Campo =  1;  p_mes = "ENERO     ";  |FINSI;
|SI #0Campo =  2;  p_mes = "FEBRERO   ";  |FINSI;
|SI #0Campo =  3;  p_mes = "MARZO     ";  |FINSI;
|SI #0Campo =  4;  p_mes = "ABRIL     ";  |FINSI;
|SI #0Campo =  5;  p_mes = "MAYO      ";  |FINSI;
|SI #0Campo =  6;  p_mes = "JUNIO     ";  |FINSI;
|SI #0Campo =  7;  p_mes = "JULIO     ";  |FINSI;
|SI #0Campo =  8;  p_mes = "AGOSTO    ";  |FINSI;
|SI #0Campo =  9;  p_mes = "SEPTIEMBRE";  |FINSI;
|SI #0Campo = 10;  p_mes = "OCTUBRE   ";  |FINSI;
|SI #0Campo = 11;  p_mes = "NOVIEMBRE ";  |FINSI;
|SI #0Campo = 12;  p_mes = "DICIEMBRE ";  |FINSI;
|SI Campo = 4;
     |PINTA 1342, p_mes;
     p_mes1 = p_mes;
|SINO;
     |PINTA 1442, p_mes;
     p_mes2 = p_mes;
|FINSI;
|FPRC;
____________________________________/ LECTURA DEL TEMPORAL E IMPRESION
|PROCESO imprime;
|SI ant_empre ! #1#0;
    |SI ant_empre ! -1;
            sw_lin = 2; |PRINT;    || imprime totales empresa
    |FINSI;
        t1 = 0;     t2 = 0;   t3 = 0;   t4 = 0;   t5 = 0;
        ant_empre = #1#0;
        sw_lin = 1; |PRINT;        || imprime nombre empresa
|FINSI;

    t1 = t1 + da;        tt1 = tt1 + da;
    t2 = t2 + bcc;       tt2 = tt2 + bcc;
    t3 = t3 + bat;       tt3 = tt3 + bat;
    t4 = t4 + bit;       tt4 = tt4 + bit;
    t5 = t5 + di;        tt5 = tt5 + di;

    sw_lin = 0;     |PRINT;        || linea de detalle
|FPRC;

|PROCESO lee_empresa_h1;
|SI #0#8 = "R";
        nume1 = 0;
    |SI #0#5 = "S"; nume1 = nume1 + #4#31;   |FINSI;
    |SI #0#6 = "S"; nume1 = nume1 + #4#32;   |FINSI;
    |SI #0#7 = "S"; nume1 = nume1 + #4#65;   |FINSI;
                                             || cuando es resumido, si el
    |SI nume1 = 0;                           ||/trabajador no tiene ILT no
        |ACABA;                              ||/sale
    |FINSI;
|FINSI;

|DDEFECTO #1;  #1#0 = #4#0;                  |LEE 000#1=;   || lee empresa
|DDEFECTO #2;  #2#0 = #4#0;   #2#1 = #4#1;   |LEE 000#2=;   || lee trabajador
    da = #4#8;      || dias alta
    bcc = #4#39;    || base CC
    bat = #4#40;    || base AT
    bit = #4#62;    || base ILT
    bat = bat - bit;
    di = 0;         || dias ILT
|SI #0#5 = "S";     di = di + #4#31;    |FINSI;
|SI #0#6 = "S";     di = di + #4#32;    |FINSI;
|SI #0#7 = "S";     di = di + #4#65;    |FINSI;
|HAZ imprime;
|FPRC;

|PROCESO ordena_h;
|DDEFECTO #2;  #2#0 = #4#0;   #2#1 = #4#1;   |LEE 000#2=;   || lee trabajador
|FPRC;

|DEFBUCLE lee_empresa_h;
 #4#1;
 ;
 #10#1,     1, #0#3, #0#4, 0;
 #10#1, 99999, #0#3, #0#9, 0;
 ;
 NULL, ordena_h, NULL, NULL, NULL, lee_empresa_h1;
 #2#0, #2#32, #2#75, #2#1;
|FIN;

|PROCESO lee_empresa_m1;
|SI #0#8 = "R";
        nume1 = 0;
    |SI #0#5 = "S"; nume1 = nume1 + #3#31;   |FINSI;
    |SI #0#6 = "S"; nume1 = nume1 + #3#32;   |FINSI;
    |SI #0#7 = "S"; nume1 = nume1 + #3#65;   |FINSI;
                                             || cuando es resumido, si el
    |SI nume1 = 0;                           ||/trabajador no tiene ILT no
        |ACABA;                              ||/sale
    |FINSI;
|FINSI;

|DDEFECTO #1;  #1#0 = #3#0;                  |LEE 000#1=;   || lee empresa
|DDEFECTO #2;  #2#0 = #3#0;   #2#1 = #3#1;   |LEE 000#2=;   || lee trabajador
    da = #3#8;      || dias alta
    bcc = #3#39;    || base CC
    bat = #3#40;    || base AT
    bit = #3#62;    || base ILT
    bat = bat - bit;
    di = 0;         || dias ILT
|SI #0#5 = "S";     di = di + #3#31;    |FINSI;
|SI #0#6 = "S";     di = di + #3#32;    |FINSI;
|SI #0#7 = "S";     di = di + #3#65;    |FINSI;
|HAZ imprime;
|FPRC;

|PROCESO ordena_m;
|DDEFECTO #2;  #2#0 = #3#0;   #2#1 = #3#1;   |LEE 000#2=;   || lee trabajador
|FPRC;

|DEFBUCLE lee_empresa_m;
 #3#1;
 ;
 #10#1,     1, #0#4, 0;
 #10#1, 99999, #0#9, 0;
 ;
 NULL, ordena_m, NULL, NULL, NULL, lee_empresa_m1;
 #2#0, #2#32, #2#75, #2#1;
|FIN;

|PROCESO lee_tempo1;
|SI ant_mutua ! #10#0;
    |SI sw_rup ! 0;
        |PIEINF;
        |FININF;
    |FINSI;
        tt1 = 0;    tt2 = 0;  tt3 = 0;  tt4 = 0;  tt5 = 0;
        sw_rup = 1;
        ant_mutua = #10#0;
    |INFOR "nomemilt";
|FINSI;

    ant_empre = -1;
|SI #0#2 = "M";     |BUCLE lee_empresa_m;    |FINSI;
|SI #0#2 = "H";     |BUCLE lee_empresa_h;    |FINSI;

    sw_lin = 2;
|PRINT;   || totales empresa
|FPRC;

|DEFBUCLE lee_tempo;
 #10#1;
 ;
     0,     1;
 99999, 99999;
 ;
 NULL, lee_tempo1;
|FIN;
____________________________________/ GRABACION DEL TEMPORAL
|PROCESO gra_tempo_h1;
    #1#0 = #4#0;
|LEE 000#1=;
|SI FSalida = 0;
        #10#0 = #1#19;   || clave entidad
        #10#1 = #4#0;    || empresa
    |LEE 000#10=;
    |SI FSalida ! 0;
            elsw1 = 0;
        |SI #0#5 = "S";|Y #4#31 ! 0;    elsw1 = 1;     |FINSI;
        |SI #0#6 = "S";|Y #4#32 ! 0;    elsw1 = 1;     |FINSI;
        |SI #0#7 = "S";|Y #4#65 ! 0;    elsw1 = 1;     |FINSI;
        |SI elsw1 ! 0;
            |GRABA 020#10n;
                seleccio = 1;
        |FINSI;
    |FINSI;
|FINSI;
|FPRC;

|DEFBUCLE gra_tempo_h;
 #4#1;
 ;
 #0#0,     1, #0#3, #0#4, 0;
 #0#1, 99999, #0#3, #0#9, 0;
 ;
 NULL, gra_tempo_h1;
|FIN;

|PROCESO gra_tempo_m1;
    #1#0 = #3#0;
|LEE 000#1=;
|SI FSalida = 0;
        #10#0 = #1#19;   || clave entidad
        #10#1 = #3#0;    || empresa
    |LEE 000#10=;
    |SI FSalida ! 0;
            elsw1 = 0;
        |SI #0#5 = "S";|Y #3#31 ! 0;    elsw1 = 1;     |FINSI;
        |SI #0#6 = "S";|Y #3#32 ! 0;    elsw1 = 1;     |FINSI;
        |SI #0#7 = "S";|Y #3#65 ! 0;    elsw1 = 1;     |FINSI;
        |SI elsw1 ! 0;
            |GRABA 020#10n;
                seleccio = 1;
        |FINSI;
    |FINSI;
|FINSI;
|FPRC;

|DEFBUCLE gra_tempo_m;
 #3#1;
 ;
 #0#0,     1, #0#4, 0;
 #0#1, 99999, #0#9, 0;
 ;
 NULL, gra_tempo_m1;
|FIN;
____________________________________/ PROCESO PRINCIPAL
|PROCESO tipo3; |TIPO 3;
|SI #0#5 ! "N";|O #0#6 ! "N";|O #0#7 ! "N";
    |IMPRE 0;
    |SI FSalida = 0;

        |MENSA "    Preparando Temporal ...";
            alfa1 = Usuario;     |QBF alfa1;
        |NOME_DAT #10 alfa1;
        |DELINDEX #10;
        |ABRE #1;
        |ABRE #10;
        |SI #0#2 = "M"; |BUCLE gra_tempo_m; |FINSI;
        |SI #0#2 = "H"; |BUCLE gra_tempo_h; |FINSI;
        |CIERRA #1;
        |CIERRA #10;

        |SI seleccio ! 0;
            |MENSA "    Imprimiendo ...";
            |ABRE #1;
            |ABRE #2;
            |INFOR "nomemilt";
            |BUCLE lee_tempo;
            |SI sw_rup ! 0;
                |PIEINF;
                |FININF;
            |FINSI;
            |CIERRA #1;
            |CIERRA #2;
        |SINO;
            |MENAV "    Seleccion vacia ...";
        |FINSI;
        |DELINDEX #10;

    |SINO;
        |MENAV "    Proceso abortado ...";
    |FINSI;
    |FINIMP;
|SINO;
    |MENAV "    Sin seleccion ...";
|FINSI;
|FPRC;
