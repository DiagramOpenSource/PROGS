|FICHEROS;
     cretap23 #0;
     cretam36;
     cretam37;

     cretam18;

     cretam02;
     cretae02;

     labempre;
     labcentr;
     nomcenes;
     labtraba;
|FIN;

|VARIABLES;
     &eaFichero = "";
     &aSoloFichero = "";

     nNume = 0;
     nNume1 = 0;
     nNume2 = 0;
     aLon = "";
     nLon = 0;
     nEspacio = 0;
     aEspacio = "";
     nBlancos = 0;
     nBlancosIzd = 0;
     nBlancosDer = 0;
     aBlancosIzd = "";
     aBlancosDer = "";

     &aCadena = "";
     aCadenaSal = "";
     &swQuitaTildes = 0;

     FLinea = 0;
     nNroLinea = 0;
     aNroLinea = "";
     aAlfa = "";

     nHandle = 0;
     aLinea = "";
     &aTotalLineas = "";

     aCCC = "";
     nAnyDesde = 0;
     nMesDesde = 0;
     aAnyDesde = "";
     aMesDesde = "";
     nTipo = 0;
     aDia = "";
     aMes = "";
     aAny = "";
     aEtiqueta = "";
     nLoc = 0;
     aValor = "";
     nValor = "";
     aCampoFecha = "";
     aDiaRec = "";
     aMesRec = "";
     aAnyRec = "";
     aHoraRec = "";
     aEtiquetaIni = "";
     aEtiquetaFin = "";
     aMesHasta = "";
     aAnyHasta = "";
     aTipo = "";

     nEmp = 0;
     nTra = 0;
     swEncontrada = 0;
     aAlfa1 = "";
     aAlfa2 = "";
     &enMes = 0;
     &enAny = 0;
     &enSwAlta = 0;

     aNaf = "";
     aEstado = "";
     fFechaRec = @;

     aTipoIPF = "";
     aNumeroIPF = "";
     aSiglas = "";

     aDiaDesdeTramo = "";
     aMesDesdeTramo = "";
     aAnyDesdeTramo = "";
     aFechaDesdeTramo = "";
     aDiaHastaTramo = "";
     aMesHastaTramo = "";
     aAnyHastaTramo = "";
     aFechaHastaTramo = "";

     aDias = "";
     aHoras = "";
     aDescripcion = "";
     nConcepto = 0;
     nImporte = 0;
     aCentro = "";

     nCampo = 0;
     nCampo1 = 0;
     nCampo2 = 0;
     nCampo3 = 0;
     aTipoSuma = "";
     nBase = 0;
     aTipoRLC = "";
     fFecha = @;
     aHora = "";
     &eaCadena = "";
     &eaCadenaSal = "";
     &swMsDos = 0;
     &swBasico = 0;

     nMesDatos = 0;
     nMesClave = 0;
     nAnyClave = 0;
     nMesHasta = 0;
     nAnyHasta = 0;
     nMesControl = 0;
     nAnyControl = 0;

     nPos = 0;
     swBlancoAnt = 0;
     nCaracter = 0;
     swAtrasos = 0;
     swNoActiva = 0;
     nMesTemp = 0;
     swActualiza = 0;

     nAnyAtrasos = 0;
     nDesdeAnyAtr = 0;
     nHastaAnyAtr = 0;

     nSumaTipo = 0;
     nBuscaTipo = 0;
|FIN;

|PROCESO Centra;
     aLon = aCadena % 0;
     nLon = aLon;
     nBlancos = nEspacio - nLon;
     nNume = nBlancos / 2;
     nBlancosIzd = nNume ! 0;
     nBlancosDer = nBlancos - nBlancosIzd;
     aBlancosIzd = " " * nBlancosIzd;
     aBlancosDer = " " * nBlancosDer;
     aCadenaSal = aBlancosIzd + aCadena + aBlancosDer;

|FPRC;

|PROCESO cretae02_atrasos;
     |SI nMesDesde = #cretae02#17;
     |Y nMesHasta = #cretae02#18;
     |Y nAnyDesde = #cretae02#19;
          || este es el tpo de atrasos que buscaba
          nTipo = #cretae02#3;
     |FINSI;
|FPRC;

|DEFBUCLE cretae02_atrasos;
     #cretae02#1;
     ;
     00001, nAnyControl, nMesControl, 05, aCCC, "        ";
     99999, nAnyControl, nMesControl, 20, aCCC, "zzzzzzzz";
     ;
     NULL, cretae02_atrasos;
|FIN;

|PROCESO BuscaTipoAtrasos;
     nTipo = 0;
     |BUCLE cretae02_atrasos;
|FPRC;

/****************************************************************************/
/***************************   BORRADO PREVIO *******************************/
/****************************************************************************/

|PROCESO Borra_cretam37;
     |BORRA 020#cretam37.a;
     |LIBERA #cretam37;
|FPRC;

|DEFBUCLE Borra_cretam37;
     #cretam37#2;
     ;
     aCCC, nAnyClave, nMesClave, nTipo, nDesdeAnyAtr;
     aCCC, nAnyClave, nMesClave, nTipo, nHastaAnyAtr;
     ;
     NULL, Borra_cretam37;
|FIN;

|PROCESO Borra_cretam36;
     |BORRA 020#cretam36.a;
     |LIBERA #cretam36;
|FPRC;

|DEFBUCLE Borra_cretam36;
     #cretam36#2;
     ;
     aCCC, nAnyDesde, nMesClave, nTipo, nDesdeAnyAtr;
     aCCC, nAnyDesde, nMesClave, nTipo, nHastaAnyAtr;
     ;
     NULL, Borra_cretam36;
|FIN;

/****************************************************************************/
/*********** PROCESOS PARA BUSCAR CODIGOS DE EMP, TRA Y CEN *****************/
/****************************************************************************/

|PROCESO BuscaCCC;
     nEmp = #cretam02#0;
     swEncontrada = 1;
|FPRC;

|DEFBUCLE BuscaCCC_At;
     #cretam02#1;
     19;
     00001, 2000, nMesDesde, nTipo, aCCC, nAnyDesde;
     99999, 2999, nMesHasta, nTipo, aCCC, nAnyDesde;
     ;
     NULL, BuscaCCC;
|FIN;

|DEFBUCLE BuscaCCC;
     #cretam02#1;
     ;
     00001, nAnyDesde, nMesDesde, nBuscaTipo, aCCC;
     99999, nAnyDesde, nMesDesde, nBuscaTipo, aCCC;
     ;
     NULL, BuscaCCC;
|FIN;

|PROCESO BuscaPorCCC;
     swEncontrada = 0;
     |SI swAtrasos = 0;
          nBuscaTipo = nTipo;
          |SI nTipo = 60;
               nBuscaTipo = 00;
          |FINSI;
          |BUCLE BuscaCCC;
     |SINO;
          |BUCLE BuscaCCC_At;
     |FINSI;
|FPRC;

/****************************************************************************/
/********************   TIPOS DE DATOS COMUNES  *****************************/
/****************************************************************************/

|PROCESO TipoFecha;
     aDia = "";
     aMes = "";
     aAny = "";

     |PARA; |SI; |HACIENDO;
          |HAZ LeeLinea;

          aEtiqueta = "Dia";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               aDia = aValor;
          |FINSI;

          aEtiqueta = "Mes";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               aMes = aValor;
          |FINSI;

          aEtiqueta = "Anho";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               aAny = aValor;
          |FINSI;

          aEtiqueta = aCampoFecha;
          |HAZ BuscaEtiFin;
          |SI nLoc = 1;
               |SAL;
          |FINSI;
     |SIGUE;
|FPRC;

|PROCESO FechaHoraRec;
     |PARA; |SI; |HACIENDO;
          |HAZ LeeLinea;

          aEtiqueta = "FechaRecaudacion";
          |HAZ BuscaEtiIni;
          |SI nLoc = 1;
               aCampoFecha = "FechaRecaudacion";
               |HAZ TipoFecha;
               aDiaRec = aDia;
               aMesRec = aMes;
               aAnyRec = aAny;
               aAlfa = aDiaRec + "." + aMesRec + "." + aAnyRec;
               fFechaRec = aAlfa;
          |FINSI;

          aEtiqueta = "HoraRecaudacion";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               aHoraRec = (aValor % 102) + ":" + (aValor % 302) + ":" + (aValor % 502);
          |FINSI;

          aEtiqueta = "FechaHoraRecaudacion";
          |HAZ BuscaEtiFin;
          |SI nLoc = 1;
               |SAL;
          |FINSI;
     |SIGUE;
|FPRC;

|PROCESO CCC;
     |PARA; |SI; |HACIENDO;
          |HAZ LeeLinea;

          aEtiqueta = "Regimen";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               aCCC = aValor;
          |FINSI;

          aEtiqueta = "Provincia";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               aCCC = aCCC + aValor;
          |FINSI;

          aEtiqueta = "Numero";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               aCCC = aCCC + aValor;
          |FINSI;

          aEtiqueta = "Ccc";
          |HAZ BuscaEtiFin;
          |SI nLoc = 1;
               |SAL;
          |FINSI;
     |SIGUE;
|FPRC;

/****************************************************************************/
/***********     PROCESOS PARA BUSCAR LAS ETIQUETAS       *******************/
/****************************************************************************/

|PROCESO BuscaEtiFin;
     nLoc = 0;
     aEtiqueta = "</" + aEtiqueta + ">";
     aAlfa = aLinea - aEtiqueta;
     |SI aAlfa ! aLinea;
          nLoc = 1;
     |FINSI;
|FPRC;

|PROCESO BuscaEtiIni;
     nLoc = 0;
     aEtiqueta = "<" + aEtiqueta;
     aAlfa = aLinea - aEtiqueta;
     |SI aAlfa ! aLinea;
          nLoc = 1;
     |FINSI;
|FPRC;

|PROCESO QuitaBlancos;
     aAlfa = aValor;
     aValor = "";
     aLon = aAlfa % 0;
     nLon = aLon;
     |PARA nPos = 1; |SI nPos [ nLon; |HACIENDO nPos = nPos + 1;
          nCaracter = (nPos * 100) + 1;
          aAlfa1 = aAlfa % nCaracter;
          |SI aAlfa1 = " "; |Y swBlancoAnt = 1;
               || quito el blanco este que me sobra
          |SINO;
               aValor = aValor + aAlfa1;
          |FINSI;
          |SI aAlfa1 = " ";
               swBlancoAnt = 1;
          |SINO;
               swBlancoAnt = 0;
          |FINSI;
     |SIGUE;
|FPRC;

|PROCESO BuscaEtiIniFin;
     aValor = "";
     nLoc = 0;

     aEtiquetaIni = "<" + aEtiqueta + ">";
     aEtiquetaFin = "</" + aEtiqueta + ">";
     aAlfa = aLinea - aEtiquetaIni;
     aAlfa = aAlfa  - aEtiquetaFin;
     |SI aAlfa ! aLinea;
          aValor = aAlfa;
          |SI aEtiqueta = "RazonSocial"; |O aEtiqueta = "EntidadAtEp";
          |O aEtiqueta = "DescripcionLRLC"; |O aEtiqueta = "ModalidadPago";
          |O aEtiqueta = "TextoPieRLC";
          |O aEtiqueta = "CuotaALiquidar";
               |QBF aValor;
          |SINO;
               |QBT aValor;
          |FINSI;
          nLoc = 1;
          |SI aEtiqueta = "ModalidadPago";
               swMsDos = 1;
               swBasico = 0;
               eaCadena = aValor;
               |HAZ CambiaTildes_plb;
               aValor = eaCadenaSal;
          |FINSI;
          |SI aEtiqueta = "RazonSocial";
               |HAZ QuitaBlancos;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO LeeLinea;
     |XLEE nHandle, 300, aLinea;
     FLinea = FSalida;
     nNroLinea = nNroLinea + 1;
     aNroLinea = nNroLinea;
     aAlfa = " Linea " + aNroLinea + " de " + aTotalLineas;

     |ATRI R;
     aCadena = aAlfa; nEspacio = 35; |HAZ Centra;
     |PINTA 2223, aCadenaSal;
     |ATRI 0;
|FPRC;

|PROCESO TextoPie;
     aAlfa = #cretam36#31 % 101;
     |SI aAlfa = "P";
          aAlfa = "Este recibo no implica el pago de las cuotas si no va ";
          aAlfa = aAlfa + "acompa¤ado del correspondiente ";
          #cretam36#44 = aAlfa;

          aAlfa = "comprobante de ingreso, sello o validaci¢n mec nica de la ";
          aAlfa = aAlfa + "Entidad Financiera. Este documento ";
          #cretam36#45 = aAlfa;

          aAlfa = " recoge los c lculos realizados a la fecha ";
          aAlfa = aAlfa + "de la  confirmaci¢n/cierre de la liquidaci¢n.";
          #cretam36#46 = aAlfa;
     |FINSI;
     |SI aAlfa = "D";
          aAlfa = "Este recibo no implica el pago de las cuotas si no va ";
          aAlfa = aAlfa + "acompa¤ado del correspondiente ";
          #cretam36#44 = aAlfa;

          aAlfa = "comprobante de ingreso de la Entidad Financiera. ";
          aAlfa = aAlfa + "Este documento recoge los c lculos ";
          #cretam36#45 = aAlfa;

          aAlfa = "realizados a la fecha ";
          aAlfa = aAlfa + "de la confirmaci¢n/cierre de la liquidaci¢n.";
          #cretam36#46 = aAlfa;
     |FINSI;
|FPRC;

|PROCESO DatosPiePaginaRLC;
     #cretam36#0 = nEmp;
     #cretam36#1 = nAnyClave
     #cretam36#2 = nMesClave;
     #cretam36#3 = nTipo;
     #cretam36#4 = aCCC;
     #cretam36#58 = nAnyAtrasos;
     |LEE 000#cretam36.=;
     || ha de existir, la he grabado en la cabecera

     || lo primero grabar los campos del pie, resulta que cuando es pago elec-
     || tronico, la linea del texto es de mas de 255 caracteres, con lo que
     || el basico no sabe tratar la variable, lo hago a pelo y ya esta

     || la primera es la del pie, no tengo que mirarla que si no esto peta
     |XLEE nHandle, 300, aLinea;

     |HAZ TextoPie;

     FLinea = 0;

     |PARA;
     |SI FLinea ] 0;
     |HACIENDO;
          FLinea = 0;
          |HAZ LeeLinea;

          || aEtiqueta = "TextoPieRLC";
          ||HAZ BuscaEtiIni;
          ||SI nLoc = 1;
               ||HAZ TextoPie;
          ||FINSI;

          aEtiqueta = "IdentificacionAplicacionCB";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               #cretam36#48 = aValor;
          |FINSI;

          aEtiqueta = "TipoFormatoCB";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               #cretam36#49 = aValor;
          |FINSI;

          aEtiqueta = "EmisoraCB";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               #cretam36#50 = aValor;
          |FINSI;

          aEtiqueta = "SufijoCB";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               #cretam36#51 = aValor;
          |FINSI;

          aEtiqueta = "ReferenciaCB";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               #cretam36#52 = aValor;
          |FINSI;

          aEtiqueta = "IdentificadorCB";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               #cretam36#53 = aValor;
          |FINSI;

          aEtiqueta = "ImporteCB";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               #cretam36#54 = aValor;
          |FINSI;

          aEtiqueta = "DigitoParidadCB";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               #cretam36#55 = aValor;
          |FINSI;

          aEtiqueta = "DatosPiePaginaRLC";
          |HAZ BuscaEtiFin;
          |SI nLoc = 1;
               |GRABA 020#cretam36.a;
               |LIBERA #cretam36;
               |SAL;
          |FINSI;
     |SIGUE;
|FPRC;

|PROCESO LineaRLC;
     FLinea = 0;

     nConcepto = 0;
     aDescripcion = "";
     nBase = 0;
     nImporte = 0;

     |PARA;
     |SI FLinea ] 0;
     |HACIENDO;
          FLinea = 0;
          |HAZ LeeLinea;

          aEtiqueta = "DescripcionLRLC";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               aDescripcion = aValor;

               aCadena = aDescripcion;
               swQuitaTildes = 1;
               |HAZ QuitaRaros;
               aDescripcion = aCadena;

               aAlfa = " " * 100;
               aDescripcion = aDescripcion + aAlfa;
               aDescripcion = (aDescripcion % 199) + " ";
               |SELECT #2#cretam18;
               #cretam18#1 = aDescripcion;
               |LEE 000#cretam18.=;
               |SI FSalida = 0;
                    nConcepto = #cretam18#0;
               |FINSI;
          |FINSI;

          aEtiqueta = "BaseLRLC";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               nBase = aValor;
               nBase = nBase / 100;
          |FINSI;

          aEtiqueta = "ImporteLRLC";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               nImporte = aValor;
               nImporte = nImporte / 100;
          |FINSI;

          aEtiqueta = "LineaRLC";
          |HAZ BuscaEtiFin;
          |SI nLoc = 1;
               || ya tengo los datos para guardar la linea (el cretam37)

               |DDEFECTO #cretam37;
               #cretam37#0 = nEmp;
               #cretam37#1 = nAnyClave;
               #cretam37#2 = nMesClave;
               #cretam37#3 = nTipo;
               #cretam37#4 = aCCC;
               #cretam37#5 = nConcepto;
               #cretam37#10 = nAnyAtrasos;
               |LEE 000#cretam37.=;
               |SI FSalida ! 0;
                    || tiene que entrar siempre, no puede existir
                    #cretam37#6 = aDescripcion;
                    #cretam37#7 = nBase;
                    #cretam37#8 = nImporte;
                    #cretam37#9 = aSoloFichero;

                    |GRABA 020#cretam37.n;
                    |LIBERA #cretam37;

                    |SI nConcepto = 998;
                         |SI nTipo ! 02;
                              nMesTemp = nMesHasta;
                         |SINO;
                              nMesTemp = nMesDesde;
                              || si no, no entraria en diciembre finiquito
                              || dic - ene
                         |FINSI;

                         |PARA nMesDatos = nMesDesde;
                         |SI nMesDatos [ nMesTemp;
                         |HACIENDO nMesDatos = nMesDatos + 1;
                              || para que me tenga en cuenta la parte exonerada
                              |SI nSumaTipo = 60
                                   nTipo = 00;
                              |FINSI;

                              swActualiza = 0;
                              #cretam02#0 = nEmp;
                              #cretam02#1 = nAnyDesde;
                              #cretam02#2 = nMesDatos;
                              #cretam02#3 = nTipo;
                              #cretam02#4 = aCCC;
                              |LEE 101#cretam02.=;
                              |SI FSalida = 0;
                              |Y #cretam02#20 = nMesControl;
                              |Y #cretam02#21 = nAnyControl;
                                   swActualiza = 1;
                              |SINO;
                                   || en atrasos puede ser la clave el a¤o
                                   || desde o el a¤o clave, hago esto para
                                   || buscar uno de los dos
                                   || probablemente serviria solo el AnyClave
                                   || pero con atrasos no me fio
                                   #cretam02#0 = nEmp;
                                   #cretam02#1 = nAnyClave;
                                   #cretam02#2 = nMesDatos;
                                   #cretam02#3 = nTipo;
                                   #cretam02#4 = aCCC;
                                   |LEE 101#cretam02.=;
                                   |SI FSalida = 0;
                                   |Y #cretam02#20 = nMesControl;
                                   |Y #cretam02#21 = nAnyControl;
                                        swActualiza = 1;
                                   |FINSI;
                              |FINSI;

                              || posibilidades

                              || Si no hay exoneracion:
                              || solo viene una T - Total
                              || en este caso NO viene E1 ni E4

                              || Si hay exoneracion y hay parte exonerada y
                              || parte NO exonerada:
                              || E1: No exonerada (lo que hay que pagar)
                              || E4: parte exonerada

                              || Si hay exoneracion y TODA la cuota esta
                              || exonerada solo viene
                              || E4: parte exonerada

                              |SI nSumaTipo = 60; || es E4
                                   |SI #cretam02#22 = "C"; |Y #cretam02#23 = "E1";
                                        swActualiza = 0;
                                        || ya se habia recogido confirmacion

                                        || no actualizo, ya que es el caso
                                        || de una liquidacion con E1 y E4
                                        || es decir, que tiene una parte
                                        || exonerada (E4) y una parte que
                                        || se paga (E1) que es la que ha de
                                        || quedarse y ya tenia grabada la E1
                                   |SINO;
                                        || no se habia recogido ninguna
                                        || E1, grabo de momento mi E4 porque
                                        || es posible que se exonere TODA la
                                        || cuota, con lo que se graba a CERO

                                        || si la E1 viene despues, la machacar 
                                        || si no viene E1, es que est  toda
                                        || la cuota exonerada

                                        nImporte = 0;
                                   |FINSI;

                                   || esto lo dejo como estaba
                                   nTipo = 60;
                              |FINSI;

                              |SI swActualiza = 1;
                                   #cretam02#16 = nImporte;
                                   #cretam02#22 = "C";
                                   #cretam02#23 = #cretam36#14;
                                   |GRABA 020#cretam02.a;
                              |FINSI;
                              |LIBERA #cretam02;
                         |SIGUE;
                    |FINSI;
               |FINSI;
               |SAL;
          |FINSI;
     |SIGUE;
|FPRC;

|PROCESO DatosCuerpoRLC;
     FLinea = 0;

     |PARA;
     |SI FLinea ] 0;
     |HACIENDO;
          FLinea = 0;
          |HAZ LeeLinea;

          aEtiqueta = "LineaRLC";
          |HAZ BuscaEtiIni;
          |SI nLoc = 1;
               |HAZ LineaRLC;
          |FINSI;

          aEtiqueta = "DatosCuerpoRLC";
          |HAZ BuscaEtiFin;
          |SI nLoc = 1;
               |SAL;
          |FINSI;
     |SIGUE;
|FPRC;

|PROCESO DatosCabeceraRLC;
     || primero grabo ya mi cabecera (el cretam36)

     nEmp = 0;
     |HAZ BuscaPorCCC;

     swNoActiva = 0;
     #labempre#0 = nEmp;
     |LEE 000#labempre.=;
     |SI FSalida = 0; |Y #labempre#67 = "S";
          || esta activa
     |SINO;
          swNoActiva = 1;
          |ACABA;
     |FINSI;

     |DDEFECTO #cretam36;
     #cretam36#0 = nEmp;
     #cretam36#1 = nAnyClave
     #cretam36#2 = nMesClave;
     #cretam36#3 = nTipo;
     #cretam36#4 = aCCC;
     #cretam36#58 = nAnyAtrasos;
     |LEE 000#cretam36.=;
     |SI FSalida ! 0;
          || tiene que entrar siempre
          #cretam36#6 = #labempre#2;
          #cretam36#11 = aTipo;
          #cretam36#14 = aTipoRLC;
          #cretam36#12 = fFechaRec;
          #cretam36#13 = aHoraRec;
          |SI nTipo = 90;
               nNume = aMesRec;
               |SI nNume = 01;
                    nNume1 = 12;
                    nNume2 = aAnyRec;
                    nNume2 = nNume2 - 1;
               |SINO;
                    nNume1 = nNume - 1;
                    nNume2 = aAnyRec;
               |FINSI;
               #cretam36#56 = nNume2;
               #cretam36#57 = nNume1;
          |FINSI;

          #cretam36#17 = aMesDesde;
          #cretam36#18 = aAnyDesde;
          #cretam36#19 = aMesHasta;
          #cretam36#20 = aAnyHasta;
          #cretam36#28 = aSoloFichero;
          |GRABA 020#cretam36.n;

          || la leo otra vez para dejarla leida y despues grabarla de nuevo

          #cretam36#0 = nEmp;
          #cretam36#1 = nAnyClave;
          #cretam36#2 = nMesClave;
          #cretam36#3 = nTipo;
          #cretam36#4 = aCCC;
          #cretam36#58 = nAnyAtrasos;
          |LEE 000#cretam36.=;
     |FINSI;

     FLinea = 0;

     |PARA;
     |SI FLinea ] 0;
     |HACIENDO;
          FLinea = 0;
          |HAZ LeeLinea;

          aEtiqueta = "AlcanceCalculo";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               #cretam36#29 = aValor;
          |FINSI;

          aEtiqueta = "CuotaALiquidar";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               #cretam36#30 = aValor;
          |FINSI;

          aEtiqueta = "Autorizado";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               #cretam36#15 = aValor;
          |FINSI;

          aEtiqueta = "Envio";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               #cretam36#16 = aValor;
          |FINSI;

          aEtiqueta = "DescripcionTipoLiquidacion";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               #cretam36#21 = aValor;
          |FINSI;

          aEtiqueta = "RazonSocial";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               #cretam36#22 = aValor;
          |FINSI;

          aEtiqueta = "TipoIpf";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               #cretam36#23 = aValor;
          |FINSI;

          aEtiqueta = "NumeroIpf";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               #cretam36#24 = (aValor % -109);
          |FINSI;

          aEtiqueta = "EntidadAtEp";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               #cretam36#25 = aValor;
          |FINSI;

          aEtiqueta = "NumeroTrabajadores";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               #cretam36#26 = aValor;
          |FINSI;

          aEtiqueta = "ModalidadPago";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               #cretam36#31 = aValor;
          |FINSI;

          aEtiqueta = "NumeroLiquidacion";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               #cretam36#27 = aValor;
          |FINSI;

          aEtiqueta = "ReferenciaCI";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               #cretam36#32 = aValor;
          |FINSI;

          aEtiqueta = "FechaCI";
          |HAZ BuscaEtiIni;
          |SI nLoc = 1;
               aCampoFecha = "FechaCI";
               |HAZ TipoFecha;
               aAlfa = aDia + "." + aMes + "." + aAny;
               fFecha = aAlfa;
               #cretam36#33 = fFecha;
          |FINSI;

          aEtiqueta = "HoraCI";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               aAlfa = (aValor % 102) + ":" + (aValor % 302) + ":" + (aValor % 502);
               #cretam36#34 = aAlfa;
          |FINSI;

          aEtiqueta = "HuellaCI";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               #cretam36#35 = aValor;
          |FINSI;

          aEtiqueta = "PeriodoPagoPE";
          |HAZ BuscaEtiIni;
          |SI nLoc = 1;
               |HAZ LeeLinea;
               aEtiqueta = "PeriodoDesde";
               |HAZ BuscaEtiIni;
               |SI nLoc = 1;
                    aCampoFecha = "PeriodoDesde";
                    |HAZ TipoFecha;
                    #cretam36#36 = aMes;
                    #cretam36#37 = aAny;
               |FINSI;

               |HAZ LeeLinea;
               aEtiqueta = "PeriodoHasta";
               |HAZ BuscaEtiIni;
               |SI nLoc = 1;
                    aCampoFecha = "PeriodoHasta";
                    |HAZ TipoFecha;
                    #cretam36#38 = aMes;
                    #cretam36#39 = aAny;
               |FINSI;
          |FINSI;

          aEtiqueta = "EmisoraPE";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               #cretam36#40 = aValor;
          |FINSI;

          aEtiqueta = "NumeroReferenciaPE";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               #cretam36#41 = aValor;
          |FINSI;

          aEtiqueta = "IdentificacionPE";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               #cretam36#42 = aValor;
          |FINSI;

          aEtiqueta = "ImportePE";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               nValor = aValor;
               #cretam36#43 = nValor / 100;
          |FINSI;

          aEtiqueta = "DatosCabeceraRLC";
          |HAZ BuscaEtiFin;
          |SI nLoc = 1;
               |GRABA 020#cretam36.a;
               |LIBERA #cretam36;
               |SAL;
          |FINSI;
     |SIGUE;
|FPRC;

|PROCESO LecturaXML;
     FLinea = 0;

     aAlfa = eaFichero;
     |XABRE "", aAlfa, nHandle; nHandle = FSalida;
     |SI FSalida ] 0;
          |PARA;
          |SI FLinea ] 0;
          |HACIENDO;
               FLinea = 0;
               |HAZ LeeLinea;

               aEtiqueta = "Ccc";
               |HAZ BuscaEtiIni;
               |SI nLoc = 1;
                    |HAZ CCC;
               |FINSI;

               aEtiqueta = "PeriodoDesde";
               |HAZ BuscaEtiIni;
               |SI nLoc = 1;
                    aCampoFecha = "PeriodoDesde";
                    |HAZ TipoFecha;
                    aMesDesde = aMes;
                    nMesDesde = aMes;
                    aAnyDesde = aAny;
                    nAnyDesde = aAny;
               |FINSI;

               aEtiqueta = "PeriodoHasta";
               |HAZ BuscaEtiIni;
               |SI nLoc = 1;
                    aCampoFecha = "PeriodoHasta";
                    |HAZ TipoFecha;
                    aMesHasta = aMes;
                    nMesHasta = aMes;
                    aAnyHasta = aAny;
                    nAnyHasta = aAny;
               |FINSI;

               aEtiqueta = "Tipo";
               |HAZ BuscaEtiIniFin;
               |SI nLoc = 1;
                    swAtrasos = 0;
                    aTipo = aValor;
                    |SI aAlfa = "L00";
                         nTipo = 00;
                         nMesClave = nMesDesde;
                         nAnyClave = nAnyDesde;
                    |FINSI;
                    |SI aAlfa = "L13";
                         nTipo = 02;
                         nMesClave = nMesDesde;
                         nAnyClave = nAnyDesde;
                    |FINSI;
                    |SI aAlfa = "L90";
                         nTipo = 90;
                         nMesClave = nMesDesde;
                         nAnyClave = nAnyDesde;
                    |FINSI;
                    |SI aTipo = "L03";
                         swAtrasos = 1;
                         nTipo = 999;
                         || son atrasos, hay que buscar el nTipo
                         || pero esto lo hare despues en la fecha de control
                    |FINSI;

                    |SI nTipo = 00;
                         nTipo = nTipo + nSumaTipo;
                    |FINSI;

                    || con el tipo a 999, en atrasos no borrara nada, lo
                    || hara despues

                    nDesdeAnyAtr = 0;
                    nHastaAnyAtr = 0;
                    |BUCLE Borra_cretam36;
                    |BUCLE Borra_cretam37;
                    |SELECT #1#cretam36;
                    |SELECT #1#cretam37;
               |FINSI;

               aEtiqueta = "FechaControl";
               |HAZ BuscaEtiIni;
               |SI nLoc = 1;
                    aCampoFecha = "FechaControl";
                    |HAZ TipoFecha;

                    nMesControl = aMes;
                    nAnyControl = aAny;
                    nAnyAtrasos = nAnyDesde;

                    nMesClave = nMesControl;
                    nAnyClave = nAnyControl;

                    || si encuentro este campo es que estoy en atrasos
                    || y ya tengo leido que es tipo L03, ahora ire a buscar que
                    || tipo de atrasos eran

                    |HAZ BuscaTipoAtrasos;

                    |SI nTipo = 00;
                         aAlfa = "    ATENCION. No se ha encontrado registro de Atrasos ";
                         aAlfa = aAlfa + " del mes " + aMesDesde + " al " + aMesHasta;
                         aAlfa = aAlfa + " de " + aAnyDesde + " en CCC: " + aCCC;
                         |MENAV aAlfa;
                    |FINSI;

                    nDesdeAnyAtr = 0; nHastaAnyAtr = 0;
                    |BUCLE Borra_cretam36;
                    nDesdeAnyAtr = nAnyAtrasos; nHastaAnyAtr = nAnyAtrasos;
                    |BUCLE Borra_cretam36;
                    nDesdeAnyAtr = 0; nHastaAnyAtr = 0;
                    |BUCLE Borra_cretam37;
                    nDesdeAnyAtr = nAnyAtrasos; nHastaAnyAtr = nAnyAtrasos;
                    |BUCLE Borra_cretam37;
                    |SELECT #1#cretam36;
                    |SELECT #1#cretam37;
               |FINSI;

               aEtiqueta = "FechaHoraRecaudacion";
               |HAZ BuscaEtiIni;
               |SI nLoc = 1;
                    |HAZ FechaHoraRec;
               |FINSI;

               aEtiqueta = "TipoRLC";
               |HAZ BuscaEtiIniFin;
               |SI nLoc = 1;
                    aTipoRLC = aValor;
               |FINSI;

               aEtiqueta = "DatosCabeceraRLC";
               |HAZ BuscaEtiIni;
               |SI nLoc = 1;
                    |HAZ DatosCabeceraRLC;
                    |SI swNoActiva = 1;
                         |ACABA;
                    |FINSI;
               |FINSI;

               aEtiqueta = "DatosCuerpoRLC";
               |HAZ BuscaEtiIni;
               |SI nLoc = 1;
                    |HAZ DatosCuerpoRLC;
               |FINSI;

               aEtiqueta = "DatosPiePaginaRLC";
               |HAZ BuscaEtiIni;
               |SI nLoc = 1;
                    |HAZ DatosPiePaginaRLC;
               |FINSI;

               aEtiqueta = "ReciboLiquidacionCotizaciones";
               |HAZ BuscaEtiFin;
               |SI nLoc = 1;
                    |SAL;
               |FINSI;
          |SIGUE;
     |FINSI;

     |XCIERRA nHandle;
|FPRC;

|PROCESO LecturaPreviaXML;
     FLinea = 0;

     aAlfa = eaFichero;
     |XABRE "", aAlfa, nHandle; nHandle = FSalida;
     |SI FSalida ] 0;
          |PARA;
          |SI FLinea ] 0;
          |HACIENDO;
               FLinea = 0;
               nNume = nNume + 1;
               |XLEE nHandle, 250, aLinea;

               aEtiqueta = "CuotaALiquidar";
               |HAZ BuscaEtiIniFin;
               |SI nLoc = 1;
                    |SI aValor = "Total";
                         || Tipo T: No hay exoneracion
                         nSumaTipo = 0;
                    |FINSI;
                    |SI aValor = "No exonerada";  || el recibo lo grabo en el 00
                         || Tipo E1: No exonerada
                         nSumaTipo = 0;
                    |FINSI;
                    |SI aValor = "Exonerada";
                         || Tipo E4: Exonerada
                         nSumaTipo = 60;
                    |FINSI;
                    |SAL;
               |FINSI;
               |SI nNume = 50;     || asumo que no la he encontrado por lo que sea
                    |SAL;
               |FINSI;
          |SIGUE;
     |FINSI;

     |XCIERRA nHandle;
|FPRC;

|PROGRAMA;
     |ABRE #cretam36;
     |ABRE #cretam37;

     |ABRE #cretam02;
     |ABRE #cretam18;

     |ABRE #labempre;
     |ABRE #labcentr;
     |ABRE #nomcenes;
     |ABRE #labtraba;

     nSumaTipo = 0;
     |HAZ LecturaPreviaXML;   || para determinar el tipo de liquidacion
                              || Total, Exonerada o No Exonerada
     |HAZ LecturaXML;

     |CIERRA #cretam37;
     |CIERRA #cretam36;

     |CIERRA #cretam18;
     |CIERRA #cretam02;

     |CIERRA #labempre;
     |CIERRA #labcentr;
     |CIERRA #nomcenes;
     |CIERRA #labtraba;
|FPRO;
