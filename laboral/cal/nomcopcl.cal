|FICHEROS;
     nomcopcl #0;
     nomfical #1;
     nomtrcal #100;
|FIN;

|VARIABLES;
     dianum = 0;
     mesnum = 0;
     swbien = 0;
     topemes = 0;
     nume1 = 0;
     diades = 0;
     mesdes = 0;
     diahas = 0;
     meshas = 0;
     resto = 0;
     bisiesto = 0;
     swentra = 0;
     semana1 = 0;
     campo = 0;
     desde = 0;
     hasta = 0;
     pesos = 0;
     mesact = 0;
     topemes1 = 0;
     topemes2 = 0;
     mid = 0;
     diafij = 0;
     mesfij = 0;
     mid_1 = 0;
     mid_2 = 0;
     errorfec1 = "     ATENCION !!! Fecha erronea / Formato (DDMM) ...";
     errorfec2 = "     ATENCION !!! Error de entrada ...";
     fecha  = @;
     alfa1  = "";
     alfa2  = "";
     alfa3  = "";
     mesalf = "";   || calculo lib_0
     clave  = "";
|FIN;

|PROCESO copia; |TIPO 3;
     |ABRE #1;
     #1#00 = #0#0;
     #1#01 = #0#1;
     |LEE 040#1=;
     |SI FSalida = 0;
          #1#00 = #0#2;
          #1#01 = #0#3;
          |GRABA 040#1n;
          |SI FSalida ! 0; |Y #0#4 = "S";
               |BORRA 020#1c;
               |GRABA 020#1n;
          |FINSI;
          |HAZ graba;
     |SINO;
          |MENAV "    ATENCION !!! Codigo origen inexistente ...";
     |FINSI;
     |CIERRA #1;
|FPRC;

/* ************************************************************************ */
/*  PROCESOS COPIADOS DEL NOMCALFI PARA CREAR EL nomtrcal AUTOMATICAMENTE   */
/* ************************************************************************ */

|PROCESO graba;   || construye calendario en fichero de trabajo
|SI FSalida ! 7;  || CTRL-C
    |ABRE #100;
        #100#00 = #0#2;   || codigo calendario
        #100#13 = #0#3;   || anyo calendario
    |LEE 140#100=;
    |SI FSalida = 0;   |BORRA 020#100c;   |FINSI;
    |DDEFECTO #100;
        #100#00 = #0#2;   || codigo calendario
        #100#13 = #0#3;   || anyo calendario
|DEBUG;
    |HAZ firstdia;
     |HAZ anyo;
    |ATRI P;
    |PINTA 2415, "Confeccionando calendario, espere ...";
    |ATRI 0;
|DEBUG;
    |HAZ semana;
    |HAZ fechas;
    |HAZ diasfi;
    |GRABA 020#100n;
    |CIERRA #100;
    |BLIN 24;
|FINSI;
|FPRC;

||****************************************************************************
||******* rutinas ************************************************************
||****************************************************************************

|PROCESO lib_0;        || convierte a literal mes en numero y halla ultimo dia
     |SI mesnum =  1;  topemes = 31;             |FINSI;
     |SI mesnum =  2;  topemes = 28 + bisiesto;  |FINSI;
     |SI mesnum =  3;  topemes = 31;             |FINSI;
     |SI mesnum =  4;  topemes = 30;             |FINSI;
     |SI mesnum =  5;  topemes = 31;             |FINSI;
     |SI mesnum =  6;  topemes = 30;             |FINSI;
     |SI mesnum =  7;  topemes = 31;             |FINSI;
     |SI mesnum =  8;  topemes = 31;             |FINSI;
     |SI mesnum =  9;  topemes = 30;             |FINSI;
     |SI mesnum = 10;  topemes = 31;             |FINSI;
     |SI mesnum = 11;  topemes = 30;             |FINSI;
     |SI mesnum = 12;  topemes = 31;             |FINSI;
|FPRC;

|PROCESO firstdia;           || calcula el dia de la semana del 01.01.XXXX
     alfa1 = "01.01." + #0#3;
     fecha = alfa1;
     fecha = fecha % 1;
     alfa1 = fecha % 11;
     |QBF alfa1;
     |SI alfa1 = "Lunes";     semana1 = 2;   |FINSI;
     |SI alfa1 = "Martes";    semana1 = 3;   |FINSI;
     |SI alfa1 = "Miercoles"; semana1 = 4;   |FINSI;
     |SI alfa1 = "Jueves";    semana1 = 5;   |FINSI;
     |SI alfa1 = "Viernes";   semana1 = 6;   |FINSI;
     |SI alfa1 = "Sabado";    semana1 = 7;   |FINSI;
     |SI alfa1 = "Domingo";   semana1 = 8;   |FINSI;
|FPRC;

|PROCESO anyo; |TIPO 0;       || chequea si el a¤o es bisiesto
    nume1 = #0#3;
        resto = nume1 $ 4;
    |SI resto = 0;
            bisiesto = 1;
    |SINO;
            bisiesto = 0;
    |FINSI;
|FPRC;


||****************************************************************************
||******* crean el calendario en el fichero de trabajo ***********************
||****************************************************************************

|PROCESO semana;      || calcula los pesos segun dia semana
     |PARA mesnum = 1; |SI mesnum [ 12; |HACIENDO mesnum = mesnum + 1;
          |HAZ lib_0;
          alfa1 = "";
          |PARA dianum = 1; |SI dianum [ topemes; |HACIENDO dianum = dianum + 1;
               clave = #1semana1;     || carga en 'clave' el peso correspondiente
               alfa1 = alfa1 + clave; || recarga 'alfa1' con el peso
               semana1 = semana1 + 1; || pasa el dia de la semana
               |SI semana1 = 9;
                    semana1 = 2;
               |FINSI;
          |SIGUE;
          #100mesnum = alfa1;
     |SIGUE;
|FPRC;

|PROCESO fechas;      || calcula los pesos segun margenes de fechas
     |PARA campo = 9; |SI campo [ 96; |HACIENDO campo = campo + 3;
          |SI #1campo ! "    ";
               desde = campo;
               hasta = campo + 1;
               pesos = campo + 2;

               alfa1 = #1desde % 102;    || separa el desde
               alfa2 = #1desde % 302;
               diades = alfa1;
               mesdes = alfa2;

               alfa1 = #1hasta % 102;    || separa el hasta
               alfa2 = #1hasta % 302;
               diahas = alfa1;
               meshas = alfa2;

               |PARA mesact = mesdes; |SI mesact [ meshas; |HACIENDO mesact=mesact+1;
                    alfa1 = #100mesact;         || carga la variable mes del fichero
                    alfa2 = "";               || limpia la variable
                    mesnum = mesact;
                    |HAZ lib_0;                   || calcula el tope del mes

                    |SI mesact=mesdes; topemes1=diades; |SINO; topemes1=1;       |FINSI;
                    |SI mesact=meshas; topemes2=diahas; |SINO; topemes2=topemes; |FINSI;

                    |PARA mid = 1; |SI mid [ topemes; |HACIENDO mid = mid + 1;
                         nume1 = (mid * 100) + 1;
                         clave = alfa1 % nume1;
                         |SI mid ] topemes1; |Y mid [ topemes2;
                              clave = #1pesos;
                         |FINSI;
                         alfa2 = alfa2 + clave;
                    |SIGUE;
                    #100mesact = alfa2;          || descarga la variable mes al fichero
               |SIGUE;
          |FINSI;
     |SIGUE;
|FPRC;

|PROCESO diasfi;      || calcula los pesos segun dias fijos
     |PARA campo = 99; |SI campo [ 157; |HACIENDO campo = campo + 2;
          |SI #1campo ! "    ";
               pesos = campo + 1;

               alfa1 = #1campo % 102;    || separa el dia fijo
               alfa2 = #1campo % 302;
               diafij = alfa1;
               mesfij = alfa2;

               mesnum = mesfij;
               |HAZ lib_0;

               alfa1 = #100mesfij;
               alfa2 = "";
               alfa3 = "";
               mid_1 = 0;
               mid_2 = 0;
               |SI diafij ! 1;
                    mid_1 = 99 + diafij;
                    alfa2 = alfa1 % mid_1;
               |FINSI;
               |SI diafij ! topemes;
                    mid_2 = (diafij + 1);
                    alfa3 = alfa1 % mid_2;
               |FINSI;
               alfa1 = alfa2 + #1pesos + alfa3;
               #100mesfij = alfa1;
          |FINSI;
     |SIGUE;
|FPRC;
