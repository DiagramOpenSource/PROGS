|FICHEROS;
    aominfhr #0;   || def de impresion (graba)
    aomentra #1;   || fichero de entrada de horas
    aomhoras #2;   || fichero de tipos de hora
    labtraba #3;   || fichero de trabajadores
|FIN;

|VARIABLES;
     anttraba = 0;
     antthora = 0;
     seleccio = 0;
     alfa1 = "";
     alfa2 = "";
     alfa3 = "";
     alfa4 = "";

    &empre = "";
    &traba = "";
    &nombr = "";
    &codhh = 0;

    &horas = 0;
    &ptash = 0;
    &ptasd = 0;
    &ptast = 0;

    &thoras = 0;
    &tptash = 0;
    &tptasd = 0;
    &tptast = 0;

    &tthh = 0;
    &ttph = 0;
    &ttpd = 0;
    &tttt = 0;

    &swtotal = 0;
|FIN;

|PROGRAMA;
|CLS;
|CABEZA "INF.HORAS REALIZ.";
|PINPA #0#0;
|DDEFECTO #0;
|ABRE #0;
|LEE 101#0p;
|SI FSalida ! 0;    |GRABA 020#0b; |FINSI;
|PINDA #0#0;
|ENDATOS #1#0;
|SI FSalida = 0;
    |GRABA 020#0a;
|FINSI;
|LIBERA #0;
|CIERRA #0;
|FPRO;

************************************************************************
   RUTINAS DE LECTURA E IMPRESION
************************************************************************

|PROCESO lee1;
|DDEFECTO #3;
    #3#0 = #1#2;
    #3#1 = #1#3;
||LEE 020#3=;
|LEE 000#3=;
|SI FSalida ! 0;    #3#2 = "<INEXISTENTE>";  |FINSI;
    empre = #1#2;  empre = "00000" + empre;  empre = empre % -105;
    traba = #1#3;  traba = "00000" + traba;  traba = traba % -105;
    nombr = #3#2;
|FPRC;

|PROCESO lee2;
|DDEFECTO #2;
    #2#0 = #1#4;
|LEE 020#2=;
    codhh = #1#4;
|FPRC;

|PROCESO detalle1;    || acumulados por trabajador
    thoras = thoras + #1#5;
    tptash = tptash + (#1#5 * #2#1);
    tptasd = tptasd + #1#6;
    tptast = tptash + tptasd;
|FPRC;

|PROCESO detalle2;    || acumulados por horas
    horas = horas + #1#5;
    ptash = ptash + (#1#5 * #2#1);
    ptasd = ptasd + #1#6;
    ptast = ptash + ptasd;
|FPRC;

|PROCESO limpia1;   || totales por trabajador
    tthh = tthh + thoras;
    ttph = ttph + tptash;
    ttpd = ttpd + tptasd;
    tttt = tttt + tptast;
    thoras = 0;
    tptash = 0;
    tptasd = 0;
    tptast = 0;
|FPRC;

|PROCESO limpia2;   || totales por horas
    horas = 0;
    ptash = 0;
    ptasd = 0;
    ptast = 0;
|FPRC;

|PROCESO abucle0;
|SI anttraba = -1;   anttraba = #1#3;  |HAZ lee1;  |FINSI;
|SI antthora = -1;   antthora = #1#4;  |HAZ lee2;  |FINSI;
    seleccio = 1;
|SI anttraba ! #1#3;
        swtotal = 0;
    |PRINT;
        swtotal = 1;
    |PRINT;
    |HAZ limpia1;
    |HAZ limpia2;
    |HAZ lee1;
    |HAZ lee2;
        anttraba = #1#3;
        antthora = #1#4;
|FINSI;
|SI antthora ! #1#4;
        swtotal = 0;
    |PRINT;
    |HAZ limpia2;
    |HAZ lee2;
        antthora = #1#4;
|FINSI;
|HAZ detalle2;
|HAZ detalle1;
|FPRC;

|DEFBUCLE aominfhr0;
 #1#1;
 ;
 #0#1, #0#3, #0#5, #0#7, #0#11, #0#09;
 #0#2, #0#4, #0#6, #0#8, #0#12, #0#10;
 ;
 NULL, NULL, NULL, NULL, NULL, abucle0;
 #1#2, #1#3, #1#4, #1#0, #1#1;
|FIN;

************************************************************************
   RUTINAS DE INICIO DE PROCESO
************************************************************************

|PROCESO proceso; |TIPO 3;
|IMPRE 0;
|SI FSalida = 0;
    |INFOR "aominfhr";
    |ABRE #2;
    |ABRE #3;
        anttraba = -1;
        antthora = -1;
    |BUCLE aominfhr0;
    |CIERRA #2;
    |CIERRA #3;
    |SI seleccio = 0;
        |MENAV "    Seleccion vacia ...";
    |SINO;
            swtotal = 0;
        |PRINT;
            swtotal = 1;
        |PRINT;
        |HAZ limpia1;
        |PIEINF;
    |FINSI;
    |FININF;
|SINO;
    |MENAV "    Proceso abortado ...";
|FINSI;
|FINIMP;
|FPRC;
