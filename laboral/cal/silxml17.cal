|FICHEROS;
     silcon17 #0;

     labempre;
     labtraba;
     labcentr;
     labcontr;
     nomcenes;

     silcon06;
     silcon15;
     labnct10;
     silcon18;
|FIN;

|VARIABLES;
     aAlfa = "";
     aAlfa1 = "";
     aAlfa2 = "";
     aCadena = "";
     nNivel = 0;
     nHandle = 0;
     swConsultaEmp = 0;
     nOpcionSS = 0;
     nNumTra = 0;
     aRuta = "";
     &eaCadena = "";
     &eaCadenaSal = "";
     &eaCaracter1 = "";
     &eaCaracter2 = "";
     aFichero = "";
     fFecha = @;
     aFecha = "";
     aHora = "";
     aFich = "";
     &eaCodNif = "";
     aCodPos = "";
     nContMuni = 0;
     aContMuni = "";
     aRutaBAT = "";

     aRutaSal = "";
     aLon = "";
     nLon = 0;
     nPunt = 0;
     nPos = 0;
     aCaracter = "";
     aCodMuni = "";
     aMuni = "";
     nMensajeF5 = 0;
     aCodigoMunicipio = "";
     aUsuario = "";
     &eaVengoDe = "";
     &swSigue = 0;
     swConectaSS = 0;
     {1}aContiene = "";
     aRutaErrores = "";
     aOrigen = "";
     aDestino = "";
     nHandleBAT = 0;
     aUnidad = "";
     aResto = "";
     nSwEtiq = 0;
     aEtiqu = "";
     aError = "";
     nNumChars = 0;
     aCCC = "";
     aValor = "";
     nValor = 0;
     &nContador = 0;
     nCampo = 0;
     nNume = 0;
     &swParteOnlineOK = 0;
|FIN;

|PROCESO Registro;
     |ABRE #labnct10;

     #labnct10#0 = #labtraba#0;
     #labnct10#1 = #labtraba#1;
     |SI swConsultaEmp = 1;
          #labnct10#11 = #0#7;
          #labnct10#1 = 00000;
     |SINO;
          #labnct10#11 = #labtraba#77;
     |FINSI;

     #labnct10#2 = nOpcionSS;
     |SI nOpcionSS = 75;
          #labnct10#3 = "Envio de partes de  baja, confirmacion y alta";
     |FINSI;
     |SI nOpcionSS = 76;
          #labnct10#3 = "Consulta partes";
     |FINSI;
     |SI nOpcionSS = 77;
          #labnct10#3 = "Anulacion procesos de IT";
     |FINSI;
     |SI nOpcionSS = 78;
          #labnct10#3 = "Emision informes";
     |FINSI;

     fFecha = ~;
     aFecha = fFecha;
     #labnct10#4 = fFecha;         || fecha

     |HORA aHora;
     |QBF aHora;
     #labnct10#5 = aHora;          || hora

     aUsuario = Usuario % 810;
     |QBF aUsuario;
     #labnct10#6 = aUsuario;       || usuario
     #labnct10#7 = eaVengoDe;

     |SI swConsultaEmp = 0;
          #labnct10#8 = #labtraba#45;             || tipo de contrato
          #labnct10#9 = #labtraba#36;             || fecha de alta
          #labnct10#10 = #labtraba#37;            || fecha de baja
     |FINSI;

     |GRABA 020#labnct10.n;
     |LIBERA #labnct10;
|FPRC;

|PROCESO ActualizaPartes;
     |ABRE #silcon18;

     |SELECT #1#silcon18;

     #silcon18#20 = "-";  || es el contador, me quedo lo que tenia
     #silcon18#2 = nContador;  || es el contador, me quedo lo que tenia
     |LEE 101#silcon18.=;
     |SI FSalida = 0;
          |BORRA 020#silcon18.a;
          |LIBERA #silcon18;
     |FINSI;

     #silcon18#0 = #silcon17#0;
     #silcon18#1 = #silcon17#1;
     #silcon18#2 = nContador;  || es el contador, me quedo lo que tenia
     |PARA nCampo = 3; |SI nCampo [ 17; |HACIENDO nCampo = nCampo + 1;
          |SI nCampo ! 6;
               || el #6 es la fecha de creacion del parte
               #silcon18#nCampo# = #silcon17#nCampo#;
          |FINSI;
     |SIGUE;

     |PARA nCampo = 22; |SI nCampo [ 31; |HACIENDO nCampo = nCampo + 1;
          #silcon18#nCampo# = #silcon17#nCampo#;
     |SIGUE;
     #silcon18#6 = fFecha;
     #silcon18#20 = "*";
     #silcon18#31 = 1;
     |GRABA 020#silcon18.n;
     |LIBERA #silcon18;

     |CIERRA #silcon18;
|FPRC;

|PROCESO Envio;
     swSigue = 0;

     |SUB_EJECUTA "labnct00";
     |SI swSigue = 0;
          |ACABA;
     |SINO;
          |SI swSigue = 2;
               swConectaSS = 1;
          |FINSI;
     |FINSI;

     |SI swConectaSS = 1;
          aContiene = "";
          |ESPECIFICA "RBASS", aContiene;
          aAlfa = aContiene1;
          |QBF aAlfa;
          aAlfa = aAlfa + "conectass";
          #labcontr#63 = aAlfa;
     |FINSI;

     aRuta = #labcontr#51;
     |QBF aRuta;
     aAlfa = aRuta % -101;
     |SI aAlfa ! "\"; |Y aAlfa ! "/";        || ruta de los XML
          aRuta = aRuta + "/";
     |FINSI;

     || ruta para errores
     aAlfa1 = #labcontr#63;
     |QBF aAlfa1;
     aAlfa = aAlfa1 % -101;
     |SI aAlfa ! "\"; |Y aAlfa ! "/";        || ruta de los XML
          aAlfa1 = aAlfa1 + "\";
     |FINSI;

     aAlfa = aAlfa1 + "ERRORES";
     aRutaErrores = aAlfa;

     aAlfa = aRutaErrores + "\ERRORESNET.XML";
     |RFBORRA aAlfa;

     |DBASE aOrigen; |QBF aOrigen;
     aOrigen = aOrigen + "ejecutables/rdn.exe";
     aDestino = #labcontr#63; |QBF aDestino; aDestino = aDestino + "rdn.exe";

     || bien, esto es por el tema del VDesk, que en una maquina virtual pare-
     || cia que no funcionaba bien el ejecutar el rdn en el RSYSTEM directamente
     || asi que lo que hice fue hacer un bat y ejecutar el BAT

     |XABRE "CBW", aRutaBAT, nHandleBAT;

     aAlfa = #labcontr#63; |QBF aAlfa;
     aRuta = aAlfa; |HAZ CompRuta; aAlfa = aRuta;
     aAlfa2 = aAlfa + "ERRORES\";
     aRuta = aAlfa2; |HAZ CompRuta; aAlfa2 = aRuta;

     aUnidad = aAlfa % 102;
     aResto = aAlfa - aUnidad;

     aAlfa = "@echo off" + &13 + &10;
     |XGRABA nHandleBAT, aAlfa;

     aUnidad = aUnidad + &13 + &10;
     |XGRABA nHandleBAT, aUnidad;

     aAlfa = "CD " + aResto + &13 + &10;
     |XGRABA nHandleBAT, aAlfa;

     aAlfa1 = #labcontr#62; |QBF aAlfa1;
     aAlfa = "rdn.exe " + aAlfa1 + " " + aFich + " " + aAlfa2;

     aAlfa = "start " + aAlfa + &13 + &10;
     |XGRABA nHandleBAT, aAlfa;

     |XCIERRA nHandleBAT;
     |RSYSTEM aRutaBAT;
     |PULSATECLA;
     |PULSATECLA;
     |PULSATECLA;
     |PULSATECLA;
     |PULSATECLA;

     || examinamos la respuesta que nos da

     nSwEtiq = 0; aEtiqu = ""; aError = ""; nNumChars = 0;
     aAlfa = #labcontr#63; |QBF aAlfa;
     aAlfa = aRutaErrores + "\ERRORESNET.XML";

     |HAZ Registro;
     swParteOnlineOK = 0;
     |CONFI "    S¿Se ha enviado correctamente el parte?";
     |SI FSalida = 0;
          |HAZ ActualizaPartes;
          swParteOnlineOK = 1;
     |FINSI;
|FPRC;

|PROCESO CompRuta;
     aRutaSal = "";
     |QBF aRuta;
     aLon = aRuta % 0;
     nLon = aLon;
     |PARA nPunt = 1; |SI nPunt [ nLon; |HACIENDO nPunt = nPunt + 1;
          nPos = (nPunt * 100) + 1;
          aCaracter = aRuta % nPos;
          |SI aCaracter = "/";
               aRutaSal = aRutaSal + "\";
          |SINO;
               aRutaSal = aRutaSal + aCaracter;
          |FINSI;
     |SIGUE;
     aCaracter = aRutaSal % -101;
     |SI aCaracter ! "\";
          aRutaSal = aRutaSal + "\";
     |FINSI;
     aRuta = aRutaSal;

     eaCadena = aRuta;
     eaCaracter1 = "/";
     eaCaracter2 = "\";
     |HAZ sustituye_plb;
     aRuta = eaCadenaSal;
|FPRC;

|PROCESO Linea;
     |QBF aAlfa;
     aCadena = aAlfa;
     |HAZ QuitaRaros;
     |SI nNivel = 1;
          aCadena = aCadena;
     |FINSI;
     |SI nNivel = 2;
          aCadena = "  " + aCadena;
     |FINSI;
     |SI nNivel = 3;
          aCadena = "  " + "  " + aCadena;
     |FINSI;
     |SI nNivel = 4;
          aCadena = "  " + "  " + "  " + aCadena;
     |FINSI;
     |SI nNivel = 5;
          aCadena = "  " + "  " + "  " + "  " + aCadena;
     |FINSI;
     |SI nNivel = 6;
          aCadena = "  " + "  " + "  " + "  " + "  " + aCadena;
     |FINSI;
     aCadena = aCadena + &13 + &10;
     aAlfa = aCadena;
     |XGRABA nHandle, aAlfa;
|FPRC;

|PROCESO CCC;
     |ABRE #labcentr;
     |ABRE #nomcenes;
     |SI swConsultaEmp = 0;
          |SI #labtraba#45 ! "421"; |Y #labtraba#45 ! "422";
               #labcentr#0 = #labtraba#0;
               #labcentr#1 = #labtraba#77;
               |LEE 000#labcentr.=;
               |SI FSalida = 0;
                    aCCC = #labcentr#10;
               |SINO;
                    |MENAV "    ATENCION: No se encuentra el centro del trabajador";
               |FINSI;
          |SINO;
               #nomcenes#0 = #labtraba#0;
               #nomcenes#13 = #labtraba#77;
               #nomcenes#1 = "87";
               |LEE 000#nomcenes.=;
               |SI FSalida = 0;
                    aCCC = #nomcenes#10;
               |SINO;
                    |MENAV "    ATENCION: No se encuentra el centro de formación";
               |FINSI;
          |FINSI;
     |SINO;
          |SI #labtraba#45 ! "421"; |Y #labtraba#45 ! "422";
               #labcentr#0 = #0#0;
               #labcentr#1 = #0#7;
               |LEE 000#labcentr.=;
               |SI FSalida = 0;
                    aCCC = #labcentr#10;
               |SINO;
                    |MENAV "    ATENCION: No se encuentra el centro del trabajador";
               |FINSI;
          |SINO;
               #nomcenes#0 = #labtraba#0;
               #nomcenes#13 = #labtraba#77;
               #nomcenes#1 = "87";
               |LEE 000#nomcenes.=;
               |SI FSalida = 0;
                    aCCC = #nomcenes#10;
               |SINO;
                    |MENAV "    ATENCION: No se encuentra el centro de formación";
               |FINSI;
          |FINSI;
     |FINSI;
     |CIERRA #nomcenes;
     |CIERRA #labcentr;

     aAlfa1 = "";
     |ABRE #silcon06;
     |ABRE #silcon15;

     #silcon15#0 = #labempre#0;
     #silcon15#1 = #labcentr#1;
     |LEE 000#silcon15.=;
     |SI FSalida = 0;
          aAlfa1 = #silcon15#2;
          |QBF aAlfa1;
     |SINO;
          #silcon06#0 = #labempre#0;
          |LEE 000#silcon06.=;
          |SI FSalida = 0;
               aAlfa1 = #silcon06#2;
               |QBF aAlfa1;
          |FINSI;
     |FINSI;

     |SI aAlfa1 = "";
          |MENAV "    ATENCION: no se ha encontrado el régimen de la empresa";
     |FINSI;

     |CIERRA #silcon15;
     |CIERRA #silcon06;

     aAlfa1 = ("0000" + aAlfa1) % -104;
     |SI aAlfa1 = "0613"; aAlfa1 = "0163"; |FINSI;
     aAlfa2 = aCCC; |QBF aAlfa2;
     aAlfa = "<CCC>" + aAlfa1 + aAlfa2 + "</CCC>";
     |HAZ Linea;
|FPRC;

|PROCESO Trabajadores;
     #labtraba#0 = #0#0;
     #labtraba#1 = #0#1;
     |LEE 000#labtraba.=;

     #labempre#0 = #labtraba#0;
     |LEE 000#labempre.=;

     nNivel = 2;
     aAlfa = "<TRABAJADOR>";
     |HAZ Linea;

     nNivel = 3;
     aAlfa = nOpcionSS;
     aAlfa = "<OPCIONSS>" + aAlfa + "</OPCIONSS>";
     |HAZ Linea;

     |HAZ CCC;

     aAlfa = #labtraba#75; |QBF aAlfa;
     aAlfa = "<NAF>" + aAlfa + "</NAF>";
     |HAZ Linea;

     aAlfa = #silcon17#4; |QBF aAlfa;
     aAlfa = "<CONTINGENCIA>" + aAlfa + "</CONTINGENCIA>";
     |HAZ Linea;

     aAlfa = #silcon17#3; |QBF aAlfa;
     aAlfa = "P" + aAlfa;
     aAlfa = "<PARTE>" + aAlfa + "</PARTE>";
     |HAZ Linea;

     |SI #silcon17#3 = "B";
          aValor = #silcon17#7;
          aValor = (aValor % 102) + "/" + (aValor % 402) + "/" + (aValor % -104);
          |QBF aValor;
          aAlfa = "<FECHA_BAJA>" + aValor + "</FECHA_BAJA>";
          |HAZ Linea;
     |FINSI;

     |SI #silcon17#3 = "C";
          aValor = #silcon17#26;
          aValor = (aValor % 102) + "/" + (aValor % 402) + "/" + (aValor % -104);
          aAlfa = "<FECHA_PARTE_CONF>" + aValor + "</FECHA_PARTE_CONF>";
          |HAZ Linea;

          nValor = #silcon17#27;
          aValor = nValor;
          aAlfa = "<NUMERO_PARTE_CONF>" + aValor + "</NUMERO_PARTE_CONF>";
          |HAZ Linea;
     |FINSI;

     |SI #silcon17#3 = "A";
          aValor = #silcon17#23;
          aValor = (aValor % 102) + "/" + (aValor % 402) + "/" + (aValor % -104);
          aAlfa = "<FECHA_ALTA>" + aValor + "</FECHA_ALTA>";
          |HAZ Linea;

          aValor = #silcon17#22;
          aAlfa = "<CAUSA_ALTA>" + aValor + "</CAUSA_ALTA>";
          |HAZ Linea;
     |FINSI;

     aAlfa = #silcon17#11;
     |QBF aAlfa; aAlfa = ("00" + aAlfa) % -102; aValor = aAlfa;
     aAlfa = #silcon17#30;
     |QBF aAlfa; aAlfa = ("00" + aAlfa) % -102; aValor = aValor + aAlfa;
     aAlfa = #silcon17#12;
     |QBF aAlfa; aAlfa = ("000000" + aAlfa) % -106; aValor = aValor + aAlfa;
     aAlfa = "<NUM_COLEGIADO>" + aValor + "</NUM_COLEGIADO>";
     |HAZ Linea;

     aValor = #silcon17#13;
     |QBF aValor;
     aAlfa = "<CIAS>" + aValor + "</CIAS>";
     |HAZ Linea;

     aValor = #silcon17#5;
     |QBF aValor;
     aAlfa = "<RECAIDA>" + aValor + "</RECAIDA>";
     |HAZ Linea;

     aValor = #silcon17#9;
     |QBT aValor;
     aAlfa = "<DIAS_POSIBLE_BAJA>" + aValor + "</DIAS_POSIBLE_BAJA>";
     |HAZ Linea;

     aValor = "";
     nNume = #labtraba#45;
     |SI 100 [ nNume; |Y nNume [ 199;        || fijos TC
          aValor = "2";
     |FINSI;
     |SI 400 [ nNume; |Y nNume [ 499;        || no fijos TC
          aValor = "2";
     |FINSI;
     |SI nNume = 999;
          aValor = "2";                      || autonomos
     |FINSI;
     |SI aValor = "";                        || resto
          aValor = "1";
     |FINSI;
     aAlfa = "<TIPO_CTTO>" + aValor + "</TIPO_CTTO>";
     |HAZ Linea;

     |SI #silcon17#14 ! 0;
          nValor = #silcon17#14;
          aValor = nValor;
          aAlfa = "<DIAS_COTIZ_MES>" + aValor + "</DIAS_COTIZ_MES>";
          |HAZ Linea;
     |FINSI;

     |SI #silcon17#15 ! 0;
          nValor = #silcon17#15;
          aValor = nValor;
          aAlfa = "<SUM_DIAS_COTIZ>" + aValor + "</SUM_DIAS_COTIZ>";
          |HAZ Linea;
     |FINSI;

     |SI #silcon17#16 ! 0;
          nValor = #silcon17#16;
          aValor = nValor;
          aAlfa = "<BASE_COTIZ>" + aValor + "</BASE_COTIZ>";
          |HAZ Linea;
     |FINSI;

     |SI #silcon17#17 ! 0;
          nValor = #silcon17#17;
          aValor = nValor;
          aAlfa = "<SUM_BASE_COTIZ>" + aValor + "</SUM_BASE_COTIZ>";
          |HAZ Linea;
     |FINSI;

     |SI #silcon17#24 ! 0;
          nValor = #silcon17#24;
          aValor = nValor;
          aAlfa = "<COTIZ_AANT_HORAS_EXTRA>" + aValor + "</COTIZ_AANT_HORAS_EXTRA>";
          |HAZ Linea;
     |FINSI;

     |SI #silcon17#25 ! 0;
          nValor = #silcon17#25;
          aValor = nValor;
          aAlfa = "<COTIZ_AANT_OTROS_CONCE>" + aValor + "</COTIZ_AANT_OTROS_CONCE>";
          |HAZ Linea;
     |FINSI;

     nNivel = 2;
     aAlfa = "</TRABAJADOR>";
     |HAZ Linea;

     nNumTra = nNumTra + 1;
|FPRC;

|PROCESO GeneraXML;
     |ABRE #labcontr;
     |LEE 000#labcontr.p;
     |CIERRA #labcontr;

     |ABRE #labempre;
     |ABRE #labtraba;

     aRuta = #labcontr#51;
     |QBF aRuta;
     |HAZ CompRuta;
     aRuta = aRuta + "NETCONTRATA\"
     |RMKDIR aRuta;
     aRuta = aRuta + "SSONLINE\"
     |RMKDIR aRuta;

     eaCadena = aRuta;
     eaCaracter1 = "/";
     eaCaracter2 = "\";
     |HAZ sustituye_plb;
     aRuta = eaCadenaSal;

     nOpcionSS = 75;
     aAlfa = nOpcionSS;
     |QBF aAlfa;
     aFichero = "OPCION" + aAlfa;

     aAlfa1 = #0#0; |QBF aAlfa1; aAlfa1 = ("00000" + aAlfa1) % -105;
     aAlfa2 = #0#1; |QBF aAlfa2; aAlfa2 = ("00000" + aAlfa2) % -105;

     aFichero = aFichero + "_" + aAlfa1 + "_" + aAlfa2;

     fFecha = ~;
     aFecha = fFecha;
     aFecha = (aFecha % 102) + (aFecha % 402) + (aFecha % 904);
     aFichero = aFichero + "_" + aFecha;
     |HORA aHora;
     |QBF aHora;
     aHora = aHora - ":" - ":";
     aFichero = aFichero + "_" + aHora + ".xml";

     aRutaBAT = aRuta + "ejecuta.bat";
     aRuta = aRuta + aFichero;
     |XABRE "CBW", aRuta, nHandle;
     |SI FSalida ] 0;
          aFich = aRuta;

          nNivel = 1;
          aAlfa = "<?xml version=" + &34 + "1.0" + &34 + " encoding=" + &34 + "ISO-8859-1" + &34 + " ?>";
          |HAZ Linea;

          aAlfa = "<TRABAJADORES>";
          |HAZ Linea;

          |HAZ Trabajadores;

          nNivel = 1;
          aAlfa = "</TRABAJADORES>";
          |HAZ Linea;

          |CIERRA #labtraba;
          |CIERRA #labempre;
     |SINO;
          aAlfa1 = #labcontr#51;
          |QBF aAlfa1;
          aAlfa1 = aAlfa1 + "\NETCONTRATA\SSONLINE\"
          aAlfa = "    ATENCION. No se ha podido grabar el fichero 'ejecuta.bat' ";
          aAlfa = aAlfa + "en la carpeta " + aAlfa1;
          |MENAV aAlfa;

          aAlfa = #labcontr#51;
          |QBF aAlfa;
          aAlfa = "    Compruebe que la ruta especificada en el Control de la Aplicación";
          aAlfa = aAlfa + " para depositar los ficheros XML, es válida.";
          |MENAV aAlfa;
     |FINSI;
     |XCIERRA nHandle;
|FPRC;

|PROCESO ConexionConectaSS;
     |HAZ GeneraXML;
     |HAZ Envio;
|FPRC;
