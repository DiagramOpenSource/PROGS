|FICHEROS;
    labcomhc #0;         || def de introduccion
    labcomhl #1;         || temporal de introduccion
    labtraba #2;         || trabajadores
    labempre #3;         || empresas
    labcon00 #4;         || definicion decretos
    nomequiv;            || equivalencia contratos
    labcontr #100;
|FIN;

|VARIABLES;
    alfa1 = "";
    alfa2 = "";
    alfa3 = "";
    alfa4 = "";
    nume1 = 0;
    nume2 = 0;
    nume3 = 0;
    nume4 = 0;
    para1 = 0;
    para2 = 0;
    para3 = 0;
    para4 = 0;
    fech1 = @;
    fech2 = @;
    fech3 = @;
    fech4 = @;
    FEstado = 0;
    men1 = "0000ATENCION !!! Fecha erronea ...";
    dia1 = "";
    mes1 = "";
    any1 = "";
    fec = "";
    blanco = "          ";
    puntos = "..........";
    sw = 0;
    dia = 0;
    mes = 0;
    any = 0;
    tope = 0;
    mod = 0;
    &domemp = "";
    &tipo = "";
    &empr = 0;
    &baja = "";
    &alta = "";
    linea = 0;
    &codpostal = "";
    &localidad = "";
    aContrato = "";
|FIN;

|INCLUYE i_comun;
____________________________________/ CALCULOS DE PANTALLA
|PROCESO fechacon; |TIPO 0;
    fec = #0Campo;  sw = 0;
|SI fec ! blanco; |Y fec ! puntos;
        dia1 = fec % A102;
        mes1 = fec % A402;
        any1 = fec % A704;
        dia = dia1;
        mes = mes1;
        any = any1;
    |SI dia ! 0; |Y mes ! 0; |Y any ! 0;
        |SI mes [ 12; |Y mes ] 1;
            |SI mes=1;|O mes=3;|O mes=5;|O mes=7;|O mes=8;|O mes=10;|O mes=12;
                    tope = 31;
            |SINO;
                |SI mes = 2;
                         mod = any $ 4;
                    |SI mod = 0;
                            tope = 29;
                    |SINO;
                            tope = 28;
                    |FINSI;
                |SINO;
                        tope = 30;
                |FINSI;
            |FINSI;
            |SI dia [ tope;
                    sw = 1;
            |FINSI;
        |FINSI;
    |FINSI;
|SINO;
        sw = 1;
|FINSI;
|SI sw = 0;
    |MENAV men1;
    |ERROR;
|FINSI;
|FPRC;
____________________________________/ EDICION DE LOS TRABAJADORES
|PROCESO CodigosNuevos;
     |QBF aContrato;
     aContrato = ("000" + aContrato) % -103;
     |ABRE #nomequiv;
     #nomequiv#0 = aContrato;
     |LEE 000#nomequiv.=;
     |SI FSalida = 0;
          |SI #labtraba#42 = "X";|O #labtraba#42 = "T";|O #labtraba#42 = "P";
               aContrato = #nomequiv#2;      || tiempo parcial
          |SINO;
               aContrato = #nomequiv#1;      || tiempo completo
          |FINSI;
     |FINSI;
     |CIERRA #nomequiv;
|FPRC;

|PROCESO lee_con;
|DDEFECTO #4;
|ABRE #4;
    #4#0 = #2#45;
|LEE 000#4=;
|CIERRA #4;
|FPRC;

|PROCESO lee_traba1;
|SI #0#4 = "S";          || solo tiempo parcial
     |SI #2#42 ! "P";|Y #2#42 ! "T";|Y #2#42 ! "X";
          |ACABA;
     |FINSI;
|FINSI;
|SI #0#4 = "N";          || sin tiempo parcial
     |SI #2#42 = "P";|O #2#42 = "T";|O #2#42 = "X";
          |ACABA;
     |FINSI;
|FINSI;

|HAZ lee_con;
|DDEFECTO #1;
    #1#0 = #2#1;         || codigo
    #1#1 = #2#75;        || afiliacion
    #1#2 = #2#2;         || nombre
    #1#3 = #2#33;        || grupo tarifa
    #1#4 = #0#3;         || baja
    #1#5 = #0#2;         || alta
    #1#6 = #2#7;         || dni/nif
    aContrato = #2#45;   |HAZ CodigosNuevos; #2#45 = aContrato;
    #1#7 = #2#45 + "/" + #4#1; || contrato
    ||#1#7 = #2#45; || contrato
    #1#8 = #2#32;        || epigrafe
|GRABA 020#1n;
|FPRC;

|DEFBUCLE lee_traba;
 #2#1;
 44;
 #0#0,     1, "A";
 #0#0, 99999, "A";
 ;
 NULL, lee_traba1;
|FIN;

|PROCESO recoge; |TIPO 3;
    empr = #0#0;
    tipo = #0#1;
    alta = #0#2;
    baja = #0#3;
|ABRE #1;  |CIERRA #1;  |DELINDEX #1;
|SI #0#1 = "T";
    |ABRE #1;
    |BUCLE lee_traba;
    |CIERRA #1;
|FINSI;
|ENTLINEAL #1#1, 1, 3, 21, 0, min, max;
|HAZ imprimir;
|DELINDEX #1;
|FPRC;

|PROCESO min;
    #1#0 = 1;
|FPRC;

|PROCESO max;
    #1#0 = 99999;
|FPRC;
____________________________________/ IMPRESION
|PROCESO lee_tempo1;
    linea = linea + 1;
|PRINT;
|FPRC;

|DEFBUCLE lee_tempo;
 #1#1;
 ;
     1;
 99999;
 e;
 NULL, lee_tempo1;
|FIN;

|PROCESO imprimir;
|ABRE #100; |LEE 000#100p; |CIERRA #100;
#100#60 = "N";
|SI #100#60 = "S";
     Impresora = "CrystalReport";
     |IMPRE -1;
|SINO;
     |IMPRE 0;
|FINSI;
|SI Impresora = "CrystalReport";
     |MENAV "    ATENCION: Informe no disponible en formato Crystal Reports";
     |ACABA;
|FINSI;
|SI FSalida = 0;
    |HAZ r_regis0;
        domemp = #3#3;
    |QBF domemp;
        domemp = domemp + " " + #3#4 + " " + #3#5;
||        domemp = domemp + " " + #3#4 + " " + #3#5 + " (";
        alfa1 = #3#7;  alfa1 = "00"  + alfa1;  alfa1 = alfa1 % -102;
        alfa2 = #3#8;  alfa2 = "000" + alfa2;  alfa2 = alfa2 % -103;
||        domemp = domemp + alfa1 + alfa2 + ") " + #3#6;
    |QBF domemp;
        codpostal = alfa1 + alfa2;
        localidad = #3#6;
    |PARA para1 = 1; |SI para1 [ #0#6; |HACIENDO para1 = para1 + 1;
            linea = 0;
        |INFOR "labcomhl";
        |BUCLE lee_tempo;
        |DDEFECTO #1;
        |PARA; |SI linea > 25; |HACIENDO;
                linea = linea - 25;
        |SIGUE;
        |PARA para2 = linea; |SI para2 [ 25; |HACIENDO para2 = para2 + 1;
            |PRINT;
        |SIGUE;
        |PIEINF;
        |FININF;
            r_emp = #0#0;
        |HAZ r_regis1;
    |SIGUE;
|SINO;
    |MENAV "    Proceso Abortado ...";
|FINSI;
|FINIMP;
|FPRC;
