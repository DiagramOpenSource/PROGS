|FICHEROS;
     labcerre #0;
     labcerrt #100;
     labcerrl;           || codigos de respuesta

     labempre;
     labtraba;
     labhisce;
     labcontr;
|FIN;

|VARIABLES;
     aAlfa = "";
     aAlfa4 = "";
     aFichero = "";

     nEmp = 0;
     nTra = 0;
     aCCC = "";
     aNIF = "";
     swEstadoLin = 0;
     aError = "";
     aResultado = "";
     nNumTraTot = 0;
     nNumTraPro = 0;

     nSwEtiq = 0;
     aEtiqu = "";
     nNumChars = 0;
     nHandle = 0;
     swAcaba = 0;
     aErrorGen = "";
     aErrorTra = "";
     nHandle1 = 0;
|FIN;

|PROCESO imprime;
     |PRINT;
|FPRC;

|DEFBUCLE imprime;
     #100#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, imprime;
|FIN;

|PROCESO informe;
     |BUCLE imprime;
|FPRC;

|PROCESO BuscaFichero;
     |SI FSalida = 10;
          aFichero = "F";
          |BROWSE aFichero;
          |QBF aFichero;
          aFichero = aFichero - "F";
          #0#0 = aFichero;
          |PINTA #0#0;
     |FINSI;
|FPRC;

|PROCESO labhisce2;
     nEmp = #labhisce#1;
     nTra = #labhisce#2;
|FPRC;

|DEFBUCLE labhisce2;
     #labhisce#3;
     ;
     aCCC, aNIF;
     aCCC, aNIF;
     ;
     NULL, labhisce2;
|FIN;

|PROCESO labhisce1;
     nEmp = #labhisce#1;
|FPRC;

|DEFBUCLE labhisce1;
     #labhisce#3;
     ;
     aCCC, "            ";
     aCCC, "zzzzzzzzzzzz";
     ;
     NULL, labhisce1;
|FIN;

|PROCESO GrabaLinea;
     |DDEFECTO #100;

     |SI swEstadoLin = 2;
          #100#0 = 0;           || mensaje general

          |BUCLE labhisce1;
          #100#1 = nEmp;
          #labempre#0 = nEmp;
          |LEE 000#labempre.=;
          #100#5 = #labempre#2;

          #100#2 = 00000;
          #100#4 = "";

          #100#3 = aError;
          |SI aError = "A000001";
               #100#7 = aResultado;
          |FINSI;
          |SI aError = "A000002";
               aAlfa4 = nNumTraTot; |QBF aAlfa4;
               #100#7 = "Numero trabajadores total: " + aAlfa4;
          |FINSI;
          |SI aError = "A000003";
               aAlfa4 = nNumTraPro; |QBF aAlfa4;
               #100#7 = "Numero trabajadores procesados: " + aAlfa4;
          |FINSI;
     |FINSI;

     |SI swEstadoLin = 21;
          #100#0 = 1;           || error empresa

          |BUCLE labhisce1;

          #100#1 = nEmp;
          #labempre#0 = nEmp;
          |LEE 000#labempre.=;
          #100#5 = #labempre#2;

          #100#2 = 00000;
          #100#6 = "";

          #100#3 = aErrorGen;
          #labcerrl#0 = aErrorGen;
          |LEE 000#labcerrl.=;
          |SI FSalida = 0;
               #100#7 = #labcerrl#1;
          |SINO;
               #100#7 = "Codigo de error no encontrado";
          |FINSI;
     |FINSI;

     |SI swEstadoLin = 22;
          #100#0 = 2;           || error trabajador

          |BUCLE labhisce2;

          #100#1 = nEmp;
          #100#2 = nTra;
          #100#4 = aNIF;
          #labempre#0 = nEmp;
          |LEE 000#labempre.=;

          #labtraba#0 = nEmp;
          #labtraba#1 = nTra;
          |LEE 000#labtraba.=;

          #100#5 = #labempre#2;
          #100#6 = #labtraba#2;

          #100#3 = aErrorTra;
          #labcerrl#0 = aErrorTra;
          |LEE 000#labcerrl.=;
          |SI FSalida = 0;
               #100#7 = #labcerrl#1;
          |SINO;
               #100#7 = "Codigo de error no encontrado";
          |FINSI;
     |FINSI;

     |GRABA 020#100n;
     |LIBERA #100;
|FPRC;

|PROCESO TrataFichero;
     nSwEtiq = 0; aEtiqu = ""; aError = ""; nNumChars = 0;
     aAlfa = #labcontr#63; |QBF aAlfa;
     aAlfa = aFichero;

     swEstadoLin = 0;
     |XABRE "C", aAlfa, nHandle; nHandle = FSalida;
     |SI FSalida ] 0;
          swAcaba = 0;
          |PARA; |SI FSalida ] 0; |Y swAcaba = 0; |HACIENDO;
               |XLEE nHandle, 250, aAlfa;

               aAlfa = aAlfa - "<Resultado>";
               |SI FSalida ! 0;
                    swEstadoLin = 1;    || AQUI EMPIEZA LO QUE ME INTERESA
               |FINSI;

               |SI swEstadoLin = 1;
                    aAlfa = aAlfa - "<Cuenta_cotizacion>";
                    |SI FSalida ! 0;
                         swEstadoLin = 2;
                    |FINSI;
               |FINSI;

               |SI swEstadoLin = 2;
                    aAlfa = aAlfa - "<CCC>";
                    |SI FSalida ! 0;
                         aAlfa = aAlfa - "</CCC>";
                         aCCC = aAlfa % 115;
                         |QBF aCCC;
                         aAlfa = aAlfa - aCCC;
                    |FINSI;

                    aAlfa = aAlfa - "<Errores_General>";
                    |SI FSalida ! 0;
                         swEstadoLin = 21;
                    |FINSI;
               |FINSI;

               |SI swEstadoLin = 21;
                    |PARA; |SI; |HACIENDO;
                         aAlfa = aAlfa - "<Error>";
                         |SI FSalida ! 0;
                              aAlfa = aAlfa - "</Error>";
                              |QBF aAlfa;
                              aErrorGen = aAlfa % 107;
                              aAlfa = aAlfa - aErrorGen;
                              |HAZ GrabaLinea;
                         |SINO;
                              |SAL;
                         |FINSI;
                    |SIGUE;
               |FINSI;

               aAlfa = aAlfa - "</Errores_General>";
               |SI FSalida ! 0;
                    swEstadoLin = 2;
               |FINSI;

               |SI swEstadoLin = 2;
                    aAlfa = aAlfa - "<Datos_Trabajador>";
                    |SI FSalida ! 0;
                         swEstadoLin = 22;
                    |FINSI;
               |FINSI;

               |SI swEstadoLin = 22;
                    aAlfa = aAlfa - "<DNI_NIE>";
                    |SI FSalida ! 0;
                         aAlfa = aAlfa - "</DNI_NIE>";
                         |QBF aAlfa;
                         || aNIF = aAlfa;
                         aNIF = aAlfa % 109;
                         aAlfa = aAlfa - aNIF;
                    |FINSI;

                    |PARA; |SI; |HACIENDO;
                         aAlfa = aAlfa - "<Error>";
                         |SI FSalida ! 0;
                              aAlfa = aAlfa - "</Error>";
                              ||QBF aAlfa;
                              aErrorTra = aAlfa % 107;
                              aAlfa = aAlfa - aErrorTra;
                              |HAZ GrabaLinea;
                         |SINO;
                              |SAL;
                         |FINSI;
                    |SIGUE;
               |FINSI;

               aAlfa = aAlfa - "</Datos_Trabajador>";
               |SI FSalida ! 0;
                    swEstadoLin = 2;
               |FINSI;

               aAlfa = aAlfa - "<Descripcion_Resultado>";
               |SI FSalida ! 0;
                    aAlfa = aAlfa - "</Descripcion_Resultado>";
                    |QBF aAlfa;
                    aError = "A000001";
                    aResultado = aAlfa;
                    |HAZ GrabaLinea;
               |FINSI;

               aAlfa = aAlfa - "<Num_Trabajadores_Total>";
               |SI FSalida ! 0;
                   aAlfa = aAlfa - "</Num_Trabajadores_Total>";
                    |QBF aAlfa;
                    aError = "A000002";
                    nNumTraTot = aAlfa;
                    |HAZ GrabaLinea;
               |FINSI;

               aAlfa = aAlfa - "<Num_Trabajadores_Procesados>";
               |SI FSalida ! 0;
                    aAlfa = aAlfa - "</Num_Trabajadores_Procesados>";
                    |QBF aAlfa;
                    aError = "A000003";
                    nNumTraPro = aAlfa;
                    |HAZ GrabaLinea;
               |FINSI;

               |SI swEstadoLin = 2;
                    aAlfa = aAlfa - "</Cuenta_cotizacion>";
                    |SI FSalida ! 0;
                         aCCC = "";
                         swEstadoLin = 1;
                    |FINSI;
               |FINSI;

               |SI swEstadoLin = 1;
                    aAlfa = aAlfa - "</Resultado>";
                    |SI FSalida ! 0;
                         swEstadoLin = 0;
                    |FINSI;
               |FINSI;

               |SI swEstadoLin = 0;
                    aAlfa = aAlfa - "<Huella_Digital>";
                    |SI FSalida ! 0;
                         swAcaba = 1;
                    |FINSI;
               |FINSI;
          |SIGUE;

          |XLEE nHandle, 250, aAlfa;
     |FINSI;
     |XCIERRA nHandle1;
|FPRC;

|PROGRAMA;
     |CLS;
     |PINPA #0#0;
     |PINDA #0#0;
     |ENDATOS #1#0;
     |SI FSalida = 0;
          aFichero = #0#0;
          || aFichero = "C:\DOCUME~1\MIGUEL~1\ESCRIT~1\Trabajo\CERTIF~1\PRUEBA~1.XML";

          aAlfa = Usuario; |QBT aAlfa; aAlfa = ("t" + aAlfa) % 108;
          |NOME_DAT #100 aAlfa;
          |DELINDEX #100;
          |ABRE #100;

          |ABRE #labempre;
          |ABRE #labtraba;
          |ABRE #labhisce;
          |ABRE #labcerrl;

          |HAZ TrataFichero;

          ||CONSULTA_CLAVE #1#100;

          Impresora = "CrystalReport";
          |IMPRE -1;
          |INFOR "labcerre";
          |HAZ informe;
          |PIEINF;
          |FININF;
          |FINIMP;

          |CIERRA #labcerrl;
          |CIERRA #labhisce;
          |CIERRA #labtraba;
          |CIERRA #labempre;

          |CIERRA #100;
          |DELINDEX #100;
     |FINSI;
|FPRO;
