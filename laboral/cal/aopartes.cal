|FICHEROS;
     aopartes #0;
     aoparte1 #1,MANTE; ||Tmp
     aomentra #2;
     labtraba #3;
     aomobras #4;
     aomsubob #5;
     aomovimc #6;
     aomoviml #7;
     aomparam #10;
|FIN;

|VARIABLES;
     &enForma = 0;
     &efFecha = @;
     &enUni = 0;
     &enHora = 0;
     &enObra = 0;
     &enSubobra = 0;
     &enTraba = 0;
     &eaSerPre = "";
     &enNumPre = 0;
     &eaNivel = "";
     &eaCapitulo = "";
     &eaPartida = "";
     &eaDesParti = "";

     nLin = 0;
     nSwCheq = 0;
     nHay = 0;
     aAlfa = "";
 {-1}aMenu = "";
     aMenu1 = "2400";
     aMenu2 = "1";
     aMenu3 = "Opciones:  ";
     aMenu4 = "RAC";
     aMenu5 = " Repasar ";
     aMenu6 = " Aceptar ";
     aMenu7 = " Cancelar ";
|FIN;

|PROCESO UltLin7;
     |SELECT #1#7;
     #7#0 = #6#0;
     #7#1 = #6#1;
     #7#2 = 999999;
     |LEE 000#7];
     |SI FSalida = 0;
          |LEE 000#7a;
     |SINO;
          |LEE 000#7u;
     |FINSI;
     |SI FSalida = 0; |Y #7#0 = #6#0;  |Y #7#1 = #6#1;
          nLin = #7#2 + 1;
     |SINO;
          nLin = 1;
     |FINSI;
|FPRC;

|PROCESO Actualiza;
     |SI nSwCheq = 0;
          |SI #1#10 = 0;
               aMenu2 = 1;
               |ERROR10;
          |FINSI;
          |ACABA;
     |FINSI;

     |DDEFECTO #6;
     #6#0 = #0#8;
     #6#1 = #0#9;
     |LEE 000#6=;
     |SI FSalida ! 0; |GRABA 020#6b; |FINSI;
     |SI aMenu2 = 2; #6#2 = "S"; |FINSI;
     |GRABA 020#6a;
     |LIBERA #6;

     |DDEFECTO #7;
     |SELECT #2#7;
     #7#0 = #6#0;
     #7#1 = #6#1;
     #7#3 = #1#0;
     #7#6 = #1#3;
     #7#4 = #1#1;
     #7#7 = #1#4;
     #7#9 = #1#6;
     #7#10 = #1#7;
     |LEE 101#7=;
     |SI FSalida ! 0;
          |HAZ UltLin7;
          #7#0 = #6#0;
          #7#1 = #6#1;
          #7#2 = nLin;
          |GRABA 020#7b;
     |FINSI;
     #7#3 = #1#0;
     #7#6 = #1#3;
     #7#4 = #1#1;
     #7#7 = #1#4;
     #7#9 = #1#6;
     #7#10 = #1#7;

     #7#5 = #1#2;
     #7#8 = #1#5;
     #7#11 = #1#8;
     #7#12 = #1#9;
     #7#13 = #1#10;
     |GRABA 020#7a;
     |LIBERA #7;
     |SI aMenu2 = 2;
          enForma = 1;
          efFecha = #1#0;
          enUni = #1#8;
          enHora = #1#7;
          enObra = #1#4;
          enSubobra = #1#10;
          enTraba = #1#1;
          eaSerPre = #1#11;
          enNumPre = #1#12;
          eaNivel = #1#14;
          eaCapitulo = #1#15;
          eaPartida = #1#17;
          eaDesParti = #1#18;
          |HAZ ActGestion;
     |FINSI;
|FPRC;

|DEFBUCLE Actualiza;
     #1#3;
     ;
     INICIO;
     FINAL;
     ;
     NULL, Actualiza;
|FIN;

|PROCESO aomentra;
     |DDEFECTO #3;
     #3#0 = #2#2;
     #3#1 = #2#3;
     |LEE 000#3=;

     |DDEFECTO #4;
     #4#0 = #2#1;
     |LEE 000#4=;

     #1#0 = #2#0;
     #1#1 = #2#3;
     #1#2 = #3#2;
     #1#3 = #2#2;
     #1#4 = #2#1;
     #1#5 = #4#1;
     #1#6 = #2#7;
     #1#7 = #2#4;
     #1#8 = #2#5;
     #1#9 = #2#6;
     #1#10 = 0;
     ||
     |SELECT #2#7;
     #7#0 = #0#8;
     #7#1 = #0#9;
     #7#3 = #1#0;
     #7#6 = #1#3;
     #7#4 = #1#1;
     #7#7 = #1#4;
     #7#9 = #1#6;
     #7#10 = #1#7;
     |LEE 000#7=;
     |SI FSalida = 0;
          #1#10 = #7#13;
     |FINSI;

     #1#11 = #2#8;
     #1#12 = #2#9;
     #1#13 = #2#10;
     #1#14 = #10#4;
     #1#15 = #2#11;
     #1#16 = #2#12;
     #1#17 = #2#13;
     #1#18 = #2#14;
     |GRABA 020#1n;
     nHay = 1;
|FPRC;

|DEFBUCLE aomentra;
     #2#1;
     ;
     #0#0, #0#2, #0#4, #0#6, #0#10, 01;
     #0#1, #0#3, #0#5, #0#7, #0#11, 99;
     ;
     NULL, aomentra;
|FIN;

|PROCESO Min5;
     #5#0 = #1#4;
     #5#1 = 000000;
|FPRC;

|PROCESO Max5;
     #5#0 = #1#4;
     #5#1 = 999999;
|FPRC;

|PROCESO Tipo66; |TIPO 66;
     |ABRE #5;
     #5#0 = #1#4;
     #5#1 = 0;
     |LEE 000#5];
     |CONSULTA_F_CLAVE #1#5, Min5, Max5;
     |SI FSalida = 0;
          #1#10 = #5#1; |PINTA #1#10;
     |SINO;
          |ERROR;
     |FINSI;
     |CIERRA #5;
|FPRC;

|PROCESO Tecla; |TIPO 0;
     |SI FSalida = 0; |Y #1Campo = Contenido;
          |ENCAMPO #10#1;
          |GRABA 020#1a;
     |FINSI;
|FPRC;

|PROCESO Min;
     #1#0 = "01.01.0000";
     #1#3 = 00001;
     #1#1 = 00001;
     #1#4 = 00001;
     #1#6 = 000000;
     #1#7 = 01;
|FPRC;

|PROCESO Max;
     #1#0 = "31.12.9999";
     #1#3 = 99999;
     #1#1 = 99999;
     #1#4 = 99999;
     #1#6 = 999999;
     #1#7 = 99;
|FPRC;

|PROCESO Usu; |TIPO 7;
     #0#8 = Usuario % 810;
     |PINTA #0#8;
|FPRC;

|PROCESO Tipo3; |TIPO 3;
     |ABRE #10; |LEE 000#10p; |CIERRA #10;
     |ABRE #6;
     #6#0 = #0#8;
     #6#1 = #0#9;
     |LEE 000#6=;
     |CIERRA #6;
     |SI #6#2 = "S";
          |MENAV "0000Movimiento  Exportado...";
          |ACABA;
     |FINSI;

     aAlfa = Usuario;
     |NOME_DAT #1 aAlfa;
     |DELINDEX #1;

     |ABRE #1;
     |ABRE #3;
     |ABRE #4;
     |ABRE #7;
     |BUCLE aomentra;
     |CIERRA #7;
     |CIERRA #1;
     |CIERRA #3;
     |CIERRA #4;

     |SI nHay = 0;
          |MENAV "0000No hay registros...";
     |SINO;
          |PINPA #0#1;
          |C_P #0#8, 1, 725;
          |C_P #0#9, 1, 755;
          |PINTA #0#8; |PINTA #0#9;
          |PARA; |SI aMenu2 = 1; |HACIENDO;
               |ENTLINEAL #2#1, 1, 4, 19, 0, Min, Max;
               |MENU aMenu;
               aMenu2 = FSalida;
               |SI aMenu2 = 2;
                    nSwCheq = 0;
                    |BUCLE Actualiza;
                    |SI aMenu2 ! 2;
                         |MENAV "0000Hay registros sin Subobra...";
                    |FINSI;
               |FINSI;
          |SIGUE;
     |FINSI;

     |SI aMenu2 = 2;
          |MENSA "0000Actualizado gestion...";
     |SINO;
          |MENSA "0000Actualizando mantenimiento...";
     |FINSI;
     |ABRE #6;
     |ABRE #7;
     nSwCheq = 1;
     |BUCLE Actualiza;
     |CIERRA #6;
     |CIERRA #7;

     |DELINDEX #1;
|FPRC;
