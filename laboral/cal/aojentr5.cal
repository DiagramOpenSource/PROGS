|FICHEROS;
     aojentr5 #0;
     aojentr6 #1,MANTE;   || Este es el temporal
     aojentr7 #2,MANTE;   || ''
     aomasitc #3;
     aomasitl #4;
     labempre #5;
     labtraba #6;
     aomparam #10;

     aovi0001;
     aovi0011;
     aomj0002;
     aomj0003;
     aomj0007;
     aomj0009;

     aojentr6@;     || Este es el real
     aojentr7@;     || ''

     agifa041@;
     gomantho@;
     gomantl1@;
     gopre001@;
     gosubcoc@;
     agifa112@;
     gotrabaj@;
     gotrabal@;
     gopartob@;
     referen@;
|FIN;

|VARIABLES;
     &eaEsMJ = "";       || Modulo 003774
     &eaCerti = "";
     &enSwCerti = 0;
     aSerPreSub = "";
     nNumPreSub = 0;
     nBuffer = 0;
     nPuntero = 0;
     nLineas = 0;
     nCalc = 0;
     nCol = 0;
     fFecha = @;
     aSubProg = "";
     nLin = 0;
     aAlfa = "";
     aPath = "";
     aFich = "";
     aDef = "";
     aDef1 = "";
     nModo = 0;
 {-1}aMenu = "";
     aMenu1 = "2400";
     aMenu2 = "1";
     aMenu3 = "";
     aMenu4 = "RAC";
     aMenu5 = " Repasar ";
     aMenu6 = " Actualizar ";
     aMenu7 = " Cancelar ";
     nTraba = 0;
     f = @;

     &nUnidades = 0;
     &nPendientes = 0;

     &enForma = 0;
     &efFecha = @;
     &enUni = 0;
     &enHora = 0;
     &enObra = 0;
     &eaObra = "";
     &enSubobra = 0;
     &enTraba = 0;
     &eaSerPre = "";
     &enNumPre = 0;
     &eaNivel = "";
     &eaCapitulo = "";
     &eaPartida = "";
     &eaDesParti = "";
     &enPrecioHora = 0;
     &enEmpresa = 0;
     &enPrecioVenta = 0;
     &enSubcontrata = 0;
     &enPrecioAux = 0;
     &enImpoSS = 0;
     &enTipoHora = 0;
     &aTrabajador = "";
|FIN;

|PROCESO CogeLineas;
     aAlfa = #gopartob@#4;
     |AL_BUF nBuffer, nPuntero, aAlfa;
     nPuntero = nPuntero + 1;
     |BORRA 020#gopartob@.a;
|FPRC;

|DEFBUCLE CogeLineas;
     #gopartob@#1004;
     ;
     aSerPreSub, nNumPreSub, #0#2, 1;
     aSerPreSub, nNumPreSub, #0#2, 999;
     be;
     NULL, CogeLineas;
|FIN;

|PROCESO GrabaLineas;
     |PARA nCalc = 100; |SI nCalc ] 0; |HACIENDO nCalc = nCalc - 1;
          |DEL_BUF nBuffer, nCalc, aAlfa;
          |SI aAlfa ! ""; nLineas = nCalc; |SAL; |FINSI;
     |SIGUE;

     |ABRE #gopartob@;
     |PARA nCalc = 1; |SI nCalc [ nLineas; |HACIENDO nCalc = nCalc + 1;
          |DEL_BUF nBuffer, nCalc, aAlfa;
          |DDEFECTO #gopartob@;
          #gopartob@#0 = aSerPreSub;
          #gopartob@#1 = nNumPreSub;
          #gopartob@#2 = #0#2;
          #gopartob@#3 = nCalc;
          #gopartob@#4 = aAlfa;
          |GRABA 020#gopartob@.n;
     |SIGUE;
     |CIERRA #gopartob@;
|FPRC;

|PROCESO Observaciones;
     aAlfa = aDef + "gopartob";
     |CARGA_DEF aAlfa, gopartob@;
     |SI FSalida ! 0; |ACABA; |FINSI;
     aAlfa = aPath + "fich/";
     aAlfa = aAlfa + (("00000" + #0#6) % -105) + "/";
     |PATH_DAT #gopartob@#aAlfa#;

     |PUSHV 0501 2380;
     |BLANCO 1401 2380;
     |BLANCO 1403 2374;
     |ATRI R; |PINTA 1404, " OBSERVACIONES "; |ATRI 0;
     |DIMENSIONA 200, nBuffer, 0;
     nPuntero = 1;
     |BUCLE CogeLineas;
     nModo = 100; nLin = 99; nCol = 90;
     |EDITA nBuffer, 1, 100, 1505, 2372, nModo, "", 0, nLin, nCol;
     |HAZ GrabaLineas;
     |FINDIM nBuffer;
     |POPV;

     |DESCARGA_DEF #gopartob@;
|FPRC;

|PROCESO GrabaTraba;
     #gotrabaj@#1 = #6#2;
     #gotrabaj@#3 = #6#17;
     #gotrabaj@#25 = #6#18;
     #gotrabaj@#2 = #6#87;
     #gotrabaj@#4 = #6#3;
     #gotrabaj@#5 = #6#4;
     #gotrabaj@#6 = #6#5;
     #gotrabaj@#7 = #6#6;
     #gotrabaj@#8 = #6#8;
     #gotrabaj@#9 = #6#9;
     #gotrabaj@#10 = #6#76;
     #gotrabaj@#11 = #6#11;
     #gotrabaj@#12 = #6#7;
     #gotrabaj@#13 = #6#75;
     #gotrabaj@#14 = #6#19;
     #gotrabaj@#15 = #6#238;
     #gotrabaj@#16 = #6#244;
     #gotrabaj@#28 = "S";

     #gotrabal@#0 = #gotrabaj@#0;
     #gotrabal@#1 = 1;
     |LEE 101#gotrabal@.=;
     |SI FSalida ! 0;
          |GRABA 020#gotrabal@.b=;
          #gotrabal@#2 = #6#36;
          #gotrabal@#3 = #6#37;
          |GRABA 020#gotrabal@.a;
     |FINSI;
     |GRABA 020#gotrabaj@.a;

     |LIBERA #gotrabaj@;
     |LIBERA #gotrabal@;
|FPRC;

|PROCESO CreaTraba;
     |ABRE #6;
     #6#0 = #1#0;
     #6#1 = #1#5;
     |LEE 020#6=;
     |SI FSalida = 0;
          aAlfa = aDef + "gotrabaj";
          |CARGA_DEF aAlfa, gotrabaj@;
          |SI FSalida = 0;
               aAlfa = aDef + "gotrabal";
               |CARGA_DEF aAlfa, gotrabal@;
               |SI FSalida = 0;
                    aAlfa = aPath + "fich/";
                    aAlfa = aAlfa + (("00000" + #0#6) % -105) + "/";
                    |PATH_DAT #gotrabaj@#aAlfa#;
                    |ABRE #gotrabaj@;
                    |ABRE #gotrabal@;
                    #gotrabaj@#0 = #1#5;
                    |LEE 000#gotrabaj@.=;
                    |SI FSalida ! 0;
                         |GRABA 020#gotrabaj@.b;
                         |HAZ GrabaTraba;
                    |FINSI;
                    |CIERRA #gotrabal@;
                    |CIERRA #gotrabaj@;
                    |DESCARGA_DEF #gotrabal@;
               |FINSI;
               |DESCARGA_DEF #gotrabaj@;
          |FINSI;
     |FINSI;
     |CIERRA #6;
|FPRC;

|PROCESO Borra2;
     |BORRA 020#2a;
|FPRC;

|DEFBUCLE Borra2;
     #2#1;
     ;
     #1#0, #1#1, #1#2, #1#3, #1#4, #1#5, 001;
     #1#0, #1#1, #1#2, #1#3, #1#4, #1#5, 999;
     ;
     NULL, Borra2;
|FIN;

|PROCESO Tipo6_2; |TIPO 2;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 3;
          |BUCLE Borra2;
     |SINO;
          |HAZ CreaTraba;
     |FINSI;
|FPRC;

|PROCESO Min2;
     #2#2 = #0#6;
     #2#0 = #0#0;
     #2#1 = #0#1;
     #2#20 = #0#9;
     #2#6 = #0#2;
     #2#4 = #1#5;
     #2#8 = 1;
|FPRC;

|PROCESO Max2;
     #2#2 = #0#6;
     #2#0 = #0#0;
     #2#1 = #0#1;
     #2#20 = #0#9;
     #2#6 = #0#2;
     #2#4 = #1#5;
     #2#8 = 999;
|FPRC;

|PROCESO UniDefecto;
     |ABRE #2;
     #2#2 = #1#0;
     #2#0 = #1#1;
     #2#1 = #1#2;
     #2#20 = #1#3;
     #2#6 = #1#4;
     #2#4 = #1#5;
     #2#8 = 1;
     |LEE 000#2];
     |SI FSalida ! 0; |O #2#2 ! #1#0; |O #2#0 ! #1#1; |O #2#1 ! #1#2;
               |O #2#20 ! #1#3; |O #2#6 ! #1#4; |O #2#4 ! #1#5;
          |DDEFECTO #2;
          #2#2 = #1#0;
          #2#0 = #1#1;
          #2#1 = #1#2;
          #2#20 = #1#3;
          #2#6 = #1#4;
          #2#4 = #1#5;
          #2#5 = #6#2;
          #2#8 = 1;
          #2#17 = #1#7;
          #2#9 = aSerPreSub;
          #2#10 = nNumPreSub;
          |GRABA 020#2n;
     |FINSI;
     |CIERRA #2;
|FPRC;

|PROCESO Tipo6_3; |TIPO 3;
     |ABRE #6;
     #6#0 = #1#0;
     #6#1 = #1#5;
     |LEE 020#6=;
     |CIERRA #6;
     aTrabajador = #6#2;
||     |SI #1#7 ! 0; |Y #10#4 = "E";
      |SI #10#4 = "E";
          |HAZ UniDefecto;
     |FINSI;

     nUnidades = #1#7;
     |PUSHV 0501 2380;
     |PARA; |SI; |HACIENDO;
          |ENTLINEAL #1#2, 7, 2, 22, 1, Min2, Max2;
          |SI nPendientes > 0;
               |MENAV "    ATENCION: Quedan unidades pendientes de asignar";
          |SINO;
               |SI nPendientes < 0;
                    |MENAV "    ATENCION: Se han asignado mas unidades de las disponibles";
               |SINO;
                    |SAL;
               |FINSI;
          |FINSI;
     |SIGUE;
     |POPV;
|FPRC;

|PROCESO AsignaMate;
     aSubProg = ":dscomer9/guerz087 agif0041 " + (("00000" + #0#6) % -105);
     aSubProg = aSubProg + ";" +  #0#0 + (("000000" + #0#1)%-106) + (("000000" + #0#9)%-106);
     |SUB_EJECUTA_NP aSubProg;
|FPRC;

|PROCESO ConsumoMate;
     aSubProg = ":dscomer9/guerz079 agif0041 " + (("00000" + #0#6) % -105);
     aSubProg = aSubProg + ";" +  #0#0 + (("000000" + #0#1)%-106) + #0#2;
     |SUB_EJECUTA_NP aSubProg;
|FPRC;

|PROCESO GeneraAlbaran;
     aSubProg = ":dscomer9/guerz083 agif0041 " + (("00000" + #0#6) % -105);
     aSubProg = aSubProg + ";" +  #0#0 + (("000000" + #0#1)%-106) + #0#2;
     aSubProg = aSubProg + (("000000" + #0#9)%-106);
     |SUB_EJECUTA_NP aSubProg;
|FPRC;

|PROCESO GeneraFactura;
     aSubProg = ":dscomer9/guerz084 agif0041 " + (("00000" + #0#6) % -105);
     aSubProg = aSubProg + ";" +  #0#0 + (("000000" + #0#1)%-106) + #0#2;
     aSubProg = aSubProg + (("000000" + #0#9)%-106);
     |SUB_EJECUTA_NP aSubProg;
|FPRC;

|PROCESO ParteMaquina;
     aSubProg = ":dscomer9/guerz086 agif0041 " + (("00000" + #0#6) % -105);
     aSubProg = aSubProg + ";" +  #0#0 + (("000000" + #0#1)%-106) + #0#2;
     aSubProg = aSubProg + (("000000" + #0#9)%-106);
     |SUB_EJECUTA_NP aSubProg;
|FPRC;

|PROCESO TeclasFuncion;
     |VARIABLES; {20} aPopup = ""; |FINSI;
     aPopup1 = "-1";  || Si fuera -1 en la posicion
     aPopup2 = 6;
     aPopup3 = "<F4> Asignación de Materiales";
     aPopup4 = "<F5> Consumo de Materiales";
     aPopup5 = "<F6> Entrada Albaranes";
     aPopup6 = "<F7> Entrada de Facturas";
     aPopup7 = "<F8> Observaciones Partes Trabajo";
     aPopup8 = "<F9> Entrada Partes Maquinaria";
     IaPopup = aPopup1  <-;
     |ESPECIFICA "TRACKPOPUP", aPopup;
     |SI FSalida = 1; FSalida = 12; |FINSI;
     |SI FSalida = 2; FSalida = 13; |FINSI;
     |SI FSalida = 3; FSalida = 14; |FINSI;
     |SI FSalida = 4; FSalida = 15; |FINSI;
     |SI FSalida = 5; FSalida = 16; |FINSI;
     |SI FSalida = 6; FSalida = 17; |FINSI;
|FPRC;

|PROCESO CheqTra; |TIPO 0;
     |HAZ MiraCerti;
     |SI eaCerti = "S";
          |MENAV "0000Registro certificado. No se puede modificar...";
          |ERROR; |ACABA;
     |FINSI;

     |ABRE #6;
     #6#0 = #1#0;
     #6#1 = #1#5;
     |LEE 000#6=;
     |SI FSalida = 0;
          |SI #6#44 = "B";
               fFecha = #6#37;
               |SI fFecha < #0#2;
                    |MENAV "    ATENCION: Trabajador en situación de BAJA";
                    |ERROR;
               |FINSI;
          |FINSI;
     |FINSI;
     |CIERRA #6;
|FPRC;

|PROCESO Tipo6_11; |TIPO 11;
     |SI FSalida = 10;
          |HAZ TeclasFuncion;
          |ERROR;
     |FINSI;
     |SI FSalida = 12;
          |HAZ AsignaMate;
          |ERROR;
     |FINSI;
     |SI FSalida = 13;
          |HAZ ConsumoMate;
          |ERROR;
     |FINSI;
     |SI FSalida = 14;
          |HAZ GeneraAlbaran;
          |ERROR;
     |FINSI;
     |SI FSalida = 15;
          |HAZ GeneraFactura;
          |ERROR;
     |FINSI;
     |SI FSalida = 16;
          |HAZ Observaciones;
          |ERROR;
     |FINSI;
     |SI FSalida = 17;
          |HAZ ParteMaquina;
          |ERROR;
     |FINSI;
|FPRC;

|PROCESO Tipo6_7; |TIPO 7;
     |ABRE #5;
     #5#0 = #1#0;
     |LEE 000#5=;
     |CIERRA #5;
     |PINTA 1804, #5#2;

     |ABRE #6;
     #6#0 = #1#0;
     #6#1 = #1#5;
     |LEE 000#6=;
     |CIERRA #6;
     |PINTA 2004, #6#2;

     f = #0#2 % A1;
     aAlfa = f % A11;
     |PINTA 1604, aAlfa;
|FPRC;

|PROCESO BuscaPreSS;
     || busca importe de hora de trabajo para el modulo de MARCOS JIMENEZ
     enImpoSS = 0;
     |ABRE #aomj0007;
     #aomj0007#0 = #labtraba#87;
     #aomj0007#1 = #labtraba#17;
     |LEE 000#aomj0007.=;
     |SI FSalida = 0;
          enImpoSS = #aomj0007#3;
     |FINSI;
     |CIERRA #aomj0007;

     |ABRE #aomj0009;
     #aomj0009#0 = #labtraba#0;
     #aomj0009#1 = #labtraba#1;
     |LEE 000#aomj0009.=;
     |SI FSalida = 0;
          enImpoSS = #aomj0009#3;
     |FINSI;
     |CIERRA #aomj0009;
|FPRC;

|PROCESO BuscaImporteMJ;
     || busca importe de dieta para el modulo de MARCOS JIMENEZ
     enPrecioHora = 0;
     enPrecioVenta = 0;

     |ABRE #aomj0002;
     #aomj0002#0 = #labtraba#87;
     #aomj0002#1 = #labtraba#17;
     |LEE 000#aomj0002.=;
     |SI FSalida = 0;
          |SI enHora = #aomj0002#10;
               enPrecioHora = #aomj0002#5;
               enPrecioVenta = #aomj0002#4;
               enPrecioAux = #aomj0002#5;
          |FINSI;
          |SI enHora = #aomj0002#11;
               enPrecioHora = #aomj0002#9;
               enPrecioVenta = #aomj0002#8;
               enPrecioAux = #aomj0002#9;
          |FINSI;
     |FINSI;
     |CIERRA #aomj0002;

     |ABRE #aomj0003;
     #aomj0003#0 = #labtraba#0;
     #aomj0003#1 = #labtraba#1;
     |LEE 000#aomj0003.=;
     |SI FSalida = 0;
          |SI enHora = #aomj0003#10;
               enPrecioHora = #aomj0003#5;
               enPrecioVenta = #aomj0003#4;
               enPrecioAux = #aomj0003#5;
          |FINSI;
          |SI enHora = #aomj0003#11;
               enPrecioHora = #aomj0003#9;
               enPrecioVenta = #aomj0003#8;
               enPrecioAux = #aomj0003#9;
          |FINSI;
     |FINSI;
     |CIERRA #aomj0003;
|FPRC;

|PROCESO BuscaImporte;
     || busca importe de hora de trabajo para el modulo de VIPEI
     enPrecioHora = 0;
     enPrecioVenta = 0;
     |ABRE #aovi0001;
     #aovi0001#0 = #labtraba#87;
     #aovi0001#1 = #labtraba#17;
     |LEE 000#aovi0001.=;
     |SI FSalida = 0;
          enPrecioHora = #aovi0001#101;
          enPrecioVenta = #aovi0001#122;
          enPrecioAux = #aovi0001#93;
     |FINSI;
     |CIERRA #aovi0001;

     |ABRE #aovi0011;
     #aovi0011#0 = #labtraba#0;
     #aovi0011#1 = #labtraba#1;
     |LEE 000#aovi0011.=;
     |SI FSalida = 0;
          enPrecioHora = #aovi0011#101;
          enPrecioVenta = #aovi0011#122;
          enPrecioAux = #aovi0011#93;
     |FINSI;
     |CIERRA #aovi0011;
|FPRC;

|PROCESO ActuaGestion;
     |ABRE #6;
     #6#0 = #2#2;
     #6#1 = #2#4;
     |LEE 020#6=;
     |CIERRA #6;

     efFecha = #aojentr7@#6;
     enUni = #aojentr7@#17;
     enHora = #aojentr7@#19;        || YA se usa tipo de hora

     || para el modulo 3774 de nomina de Marcos Jimenez, si hay algo en tipo
     || de hora se indica si es media dieta o dieta entera, para todos
     || los demas se quedara el tipo 00

     enObra = #aojentr7@#0;
     eaObra = #aojentr7@#0;
     enSubobra = #aojentr7@#1;
     enTraba = #aojentr7@#4;
     eaSerPre = #aojentr7@#9;
     enNumPre = #aojentr7@#10;
     eaNivel = #10#4;
     eaCapitulo = #aojentr7@#12;
     eaPartida = #aojentr7@#15;
     eaDesParti = #aojentr7@#16;
     enEmpresa = #aojentr7@#2;
     |SI enHora ! 00;
          |HAZ BuscaImporteMJ;
     |SINO;
          |HAZ BuscaImporte;
     |FINSI;
     |HAZ BuscaPreSS;
     enSubcontrata = #aojentr7@#20;
     |HAZ ActGestion;
     #aojentr7@#18 = nLin;

     |SI eaCerti = "S"; |Y enSwCerti = 0;
          |ERROR10;
     |FINSI;
|FPRC;

|PROCESO ActualizaL;
     |SALVA_DATOS #2;
     |REPON_DATOS #aojentr7@;
     enForma = 1;
     |HAZ ActuaGestion;

     |SI enSwCerti = 0;
          |GRABA 020#aojentr7@.n;
     |FINSI;
|FPRC;

|DEFBUCLE ActualizaL;
     #2#1;
     ;
     #1#0, #1#1, #1#2, #1#3, #1#4, #1#5, 001;
     #1#0, #1#1, #1#2, #1#3, #1#4, #1#5, 999;
     ;
     NULL, ActualizaL;
|FIN;

|PROCESO ActualizaC;
     |SALVA_DATOS #1;
     |REPON_DATOS #aojentr6@;
     |GRABA 020#aojentr6@.n;
     |BUCLE ActualizaL;
|FPRC;

|DEFBUCLE Actualiza;
     #1#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, ActualizaC;
|FIN;

|PROCESO DesactualizaL;
     enForma = -1;
     |HAZ ActuaGestion;

     |BORRA 020#aojentr7@.a;
     |LIBERA #aojentr7@;
|FPRC;

|DEFBUCLE DesactualizaL;
     #aojentr7@#1007;
     ;
     #0#6, #0#0, #0#1, #0#9, #0#2, nTraba, 001;
     #0#6, #0#0, #0#1, #0#9, #0#2, nTraba, 999;
     be;
     NULL, DesactualizaL;
|FIN;

|PROCESO DesactualizaC;
     nTraba = #aojentr6@#5;
     |BUCLE DesactualizaL;
     |BORRA 020#aojentr6@.a;
     |LIBERA #aojentr6@;
|FPRC;

|DEFBUCLE Desactualiza;
     #aojentr6@#1006;
     ;
     #0#6, #0#0, #0#1, #0#9, #0#2, 00001;
     #0#6, #0#0, #0#1, #0#9, #0#2, 99999;
     be;
     NULL, DesactualizaC;
|FIN;

|PROCESO Actualizar;
     |BUCLE Desactualiza;

     |ABRE #aojentr7@;
     |ABRE #aojentr6@;
     |BUCLE Actualiza;
     |CIERRA #aojentr6@;
     |CIERRA #aojentr7@;
|FPRC;

|PROCESO CreaUnidades;
     |DDEFECTO #6;
     #6#0 = #1#0;
     #6#1 = #1#5;
     |LEE 000#6=;
     |SI #6#44 = "B";
          fFecha = #6#37;
          |SI fFecha < #0#2;
               |||||MENAV "    ATENCION: Trabajador en situación de BAJA";
               |ACABA;
          |FINSI;
     |FINSI;
     |DDEFECTO #2;
     #2#2 = #1#0;
     #2#0 = #1#1;
     #2#1 = #1#2;
     #2#20 = #1#3;
     #2#6 = #1#4;
     #2#4 = #1#5;
     #2#5 = #6#2;
     #2#8 = 1;
     #2#17 = #1#7;
     #2#9 = aSerPreSub;
     #2#10 = nNumPreSub;
     |GRABA 020#2n;
|FPRC;

|PROCESO LlenaTraba;
     |SI #0#2 < #4#10; |O #0#2 > #4#11;
          |ACABA;
     |FINSI;

     #6#0 = #0#6;
     #6#1 = #4#8;
     |LEE 000#6=;
     |SI FSalida ! 0; |ACABA; |FINSI;
     |SI #6#44 = "B";
          fFecha = #6#37;
          |SI fFecha < #0#2;
               |ACABA;             ||Trabajador Baja
          |FINSI;
     |FINSI;

     #1#0 = #4#0;
     #1#1 = #4#1;
     #1#2 = #4#2;
     #1#3 = #4#6;
     #1#4 = #0#2;
     #1#5 = #4#8;
     |LEE 000#1=;
     |SI FSalida ! 0;
          #1#6 = #0#8;
          #1#7 = #0#3;
          |GRABA 020#1n;
          |HAZ CreaTraba;
          |SI #10#4 = "E";
               |HAZ CreaUnidades;
          |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE LlenaTraba;
     #3#1;
     ;
     #0#6, #0#0, #0#1, "     ", 000001, #0#8, #0#9;
     #0#6, #0#0, #0#1, "zzzzz", 999999, #0#8, #0#9;
     ;
     NULL, NULL, LlenaTraba;
|FIN;

|PROCESO LlenaTmp2;
     |SALVA_DATOS #aojentr7@;
     |REPON_DATOS #2;
     |GRABA 020#2n;
|FPRC;

|DEFBUCLE LlenaTmp2;
     #aojentr7@#1007;
     ;
     #0#6, #0#0, #0#1, #0#9, #0#2, 00001, 001;
     #0#6, #0#0, #0#1, #0#9, #0#2, 99999, 999;
     ;
     NULL, LlenaTmp2;
|FIN;

|PROCESO LlenaTmp1;
     |SALVA_DATOS #aojentr6@;
     |REPON_DATOS #1;
     |GRABA 020#1n;
|FPRC;

|DEFBUCLE LlenaTmp1;
     #aojentr6@#1006;
     ;
     #0#6, #0#0, #0#1, #0#9, #0#2, 00001;
     #0#6, #0#0, #0#1, #0#9, #0#2, 99999;
     ;
     NULL, LlenaTmp1;
|FIN;

|PROCESO Tipo3; |TIPO 3;
     |HAZ Observaciones;
     |DDEF aDef1
     aAlfa = aDef1 + "aojentr6";
     |CARGA_DEF aAlfa, aojentr6@;
     |SI FSalida = 0;
          aAlfa = aDef1 + "aojentr7";
          |CARGA_DEF aAlfa, aojentr7@;
          |SI FSalida = 0;
               aAlfa = "0" + Usuario;
               |NOME_DAT #1 aAlfa; |DELINDEX #1;
               aAlfa = "1" + Usuario;
               |NOME_DAT #2 aAlfa; |DELINDEX #2;
               |ABRE #1;
               |ABRE #2;
               |ABRE #6;
               |BUCLE LlenaTmp1;
               |BUCLE LlenaTmp2;
               |BUCLE LlenaTraba;
               |CIERRA #6;
               |CIERRA #2;
               |CIERRA #1;
               |PARA; |SI; |HACIENDO;
                    |ENTLINEAL #1#1, 6, 2, 22, 0, Min1, Max1;
                    |MENU aMenu;
                    |SI FSalida = 2; |HAZ Actualizar; |SAL; |FINSI;
                    |SI FSalida = 3; |SAL; |FINSI;
               |SIGUE;
               |DELINDEX #1;
               |DELINDEX #2;
               |DESCARGA_DEF #aojentr7@;
          |FINSI;
          |DESCARGA_DEF #aojentr6@;
     |FINSI;
|FPRC;

|PROCESO Empresa66; |TIPO 66;
     aAlfa = aDef + "agifa041";
     |CARGA_DEF aAlfa, agifa041@;
     |SI FSalida = 0;
          aAlfa = aPath + "fich/";
          |PATH_DAT #agifa041@#aAlfa#;
          |ABRE #agifa041@;
          #agifa041@#0 = #0#6;
          |LEE 000#agifa041@.];
          |CONSULTA_CLAVE #1#agifa041@;
          |SI FSalida = 0;
               #0#6 = #agifa041@#0; |PINTA #0#6;
          |SINO;
               |ERROR;
          |FINSI;
          |CIERRA #agifa041@;
          |DESCARGA_DEF #agifa041@;
     |FINSI;
|FPRC;

|PROCESO Empresa0; |TIPO 0;
     aAlfa = aDef + "agifa041";
     |CARGA_DEF aAlfa, agifa041@;
     |SI FSalida = 0;
          aAlfa = aPath + "fich/";
          |PATH_DAT #agifa041@#aAlfa#;
          |DDEFECTO #agifa041@;
          |ABRE #agifa041@;
          #agifa041@#0 = #0#6;
          |LEE 000#agifa041@.=;
          |SI FSalida = 0;
               #0#7 = #agifa041@#1; |PINTA #0#7;
          |SINO;
               |MENAV "0000No existe empresa...";
               |ERROR;
          |FINSI;
          |CIERRA #agifa041@;
          |DESCARGA_DEF #agifa041@;
     |FINSI;
|FPRC;

|PROCESO Expedientes;
     |SI #gomantho@#9 ! "T";
          |SALVA_DATOS #gomantho@;
          |REPON_DATOS #referen@;
          |GRABA 020#referen@.n;
     |FINSI;
|FPRC;

|DEFBUCLE Expedientes;
     #gomantho@#1002;
     ;
     "     ", 000001;
     "zzzzz", 999999;
     ;
     NULL, Expedientes;
|FIN;

|PROCESO Expediente66; |TIPO 66;
     aAlfa = aDef + "gomantho";
     |CARGA_DEF aAlfa, gomantho@;
     |SI FSalida = 0;
          |CARGA_DEF aAlfa, referen@;
          |SI FSalida = 0;
               aAlfa = aPath + "fich/";
               aAlfa = aAlfa + (("00000" + #0#6) % -105) + "/";
               |PATH_DAT #gomantho@#aAlfa#;
               aAlfa = Usuario;
               |NOME_DAT #referen@#aAlfa#;
               |DELINDEX #referen@;
               |ABRE #referen@;
               |BUCLE Expedientes;
               #referen@#0 = #0#0;
               #referen@#1 = #0#1;
               |LEE 000#referen@.];
               |CONSULTA_CLAVE #1#referen@;
               |SI FSalida = 0;
                    #0#0 = #referen@#0; |PINTA #0#0;
                    #0#1 = #referen@#1; |PINTA #0#1;
               |SINO;
                    |ERROR;
               |FINSI;
               |CIERRA #referen@;
               |DELINDEX #referen@;
               |DESCARGA_DEF #referen@;
          |FINSI;
          |DESCARGA_DEF #gomantho@;
     |FINSI;
|FPRC;

|PROCESO Expediente0; |TIPO 0;
     aAlfa = aDef + "gomantho";
     |CARGA_DEF aAlfa, gomantho@;
     |SI FSalida = 0;
          aAlfa = aPath + "fich/";
          aAlfa = aAlfa + (("00000" + #0#6) % -105) + "/";
          |PATH_DAT #gomantho@#aAlfa#;
          |DDEFECTO #gomantho@;
          |ABRE #gomantho@;
          #gomantho@#0 = #0#0;
          #gomantho@#1 = #0#1;
          |LEE 000#gomantho@.=;
          |SI FSalida = 0;
               |SI #gomantho@#9 = "T";
                    |MENAV "0000Obra terminada...";
               |SINO;
                    #0#4 = #gomantho@#29; |PINTA #0#4;
                    #0#5 = #gomantho@#3; |PINTA #0#5;
               |FINSI;
          |SINO;
               |MENAV "0000No existe obra...";
               |ERROR;
          |FINSI;
          |CIERRA #gomantho@;
          |DESCARGA_DEF #gomantho@;
     |FINSI;
|FPRC;

|PROCESO SubConPrv;
     #gomantl1@#3 = #gosubcoc@#4;
     #gomantl1@#4 = #gosubcoc@#5;
     |LEE 000#gomantl1@.=;
     |SI FSalida = 0; |Y #gomantl1@#10 ! "T";
          |SI #gomantl1@#0 = #0#0; |Y #gomantl1@#1 = #0#1;
               #agifa112@#0 = #gosubcoc@#2;
               |LEE 000#agifa112@.=;
               |SI FSalida = 0;
                    |SALVA_DATOS #agifa112@;
                    |REPON_DATOS #referen@;
                    |GRABA 000#referen@.n;
               |FINSI;
          |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE SubConPrv;
     #gosubcoc@#3002;
     20;
     #gomantl1@#3, #gomantl1@#4, "N";
     #gomantl1@#3, #gomantl1@#4, "N";
     ;
     NULL, SubConPrv;
|FIN;

|PROCESO LinExpe;
     |BUCLE SubConPrv;
|FPRC;

|DEFBUCLE LinExpe;
     #gomantl1@#1003;
     ;
     #0#0, #0#1, 001;
     #0#0, #0#1, 999;
     ;
     NULL, LinExpe;
|FIN;

|PROCESO Proveedor66; |TIPO 66;
     aAlfa = aDef + "gosubcoc";
     |CARGA_DEF aAlfa, gosubcoc@;
     |SI FSalida = 0;
          aAlfa = aDef + "agifa112";
          |CARGA_DEF aAlfa, agifa112@;
          |SI FSalida = 0;
               aAlfa = aDef + "agifa112";
               |CARGA_DEF aAlfa, referen@;
               |SI FSalida = 0;
                    aAlfa = aDef + "gomantl1";
                    |CARGA_DEF aAlfa, gomantl1@;
                    |SI FSalida = 0;
                         aAlfa = aPath + "fich/";
                         aAlfa = aAlfa + (("00000" + #0#6) % -105) + "/";
                         |PATH_DAT #gosubcoc@#aAlfa#;
                         |PATH_DAT #agifa112@#aAlfa#;
                         |PATH_DAT #gomantl1@#aAlfa#;
                         aAlfa = Usuario;
                         |NOME_DAT #referen@#aAlfa#;
                         |DELINDEX #referen@;
                         |ABRE #referen@;
                         |ABRE #agifa112@;
                         |BUCLE LinExpe;
                         |CIERRA #agifa112@;
                         #referen@#0 = #0#8;
                         |LEE 000#referen@.];
                         |CONSULTA_CLAVE #1#referen@;
                         |SI FSalida = 0;
                              #0#8 = #referen@#0; |PINTA #0#8;
                         |SINO;
                              |ERROR;
                         |FINSI;
                         |CIERRA #referen@;
                         |DELINDEX #referen@;
                         |DESCARGA_DEF #gomantl1@;
                    |FINSI;
                    |DESCARGA_DEF #referen@;
               |FINSI;
               |DESCARGA_DEF #agifa112@;
          |FINSI;
          |DESCARGA_DEF #gosubcoc@;
     |FINSI;
|FPRC;

|PROCESO Proveedor0; |TIPO 0;
     aAlfa = aDef + "agifa112";
     |CARGA_DEF aAlfa, agifa112@;
     |SI FSalida = 0;
          aAlfa = aPath + "fich/";
          aAlfa = aAlfa + (("00000" + #0#6) % -105) + "/";
          |PATH_DAT #agifa112@#aAlfa#;
          |DDEFECTO #agifa112@;
          |ABRE #agifa112@;
          #agifa112@#0 = #0#8;
          |LEE 000#agifa112@.=;
          |SI FSalida = 0;
               #0#10 = #agifa112@#1; |PINTA #0#10;
          |SINO;
               |MENAV "0000No existe proveedor...";
               |ERROR;
          |FINSI;
          |CIERRA #agifa112@;
          |DESCARGA_DEF #agifa112@;
     |FINSI;
|FPRC;

|PROCESO Subcontra;
     #gomantl1@#3 = #gosubcoc@#4;
     #gomantl1@#4 = #gosubcoc@#5;
     |LEE 000#gomantl1@.=;
     |SI FSalida = 0; |Y #gomantl1@#10 ! "T";
          |SI #gomantl1@#0 = #0#0; |Y #gomantl1@#1 = #0#1;
               |SALVA_DATOS #gosubcoc@;
               |REPON_DATOS #referen@;
               |GRABA 020#referen@.n;
          |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE Subcontra;
     #gosubcoc@#3002;
     20, 2;
     #gomantl1@#3, #gomantl1@#4, "N", #0#8;
     #gomantl1@#3, #gomantl1@#4, "N", #0#8;
     ;
     NULL, Subcontra;
|FIN;

|PROCESO LinExpe1;
     |BUCLE Subcontra;
|FPRC;

|DEFBUCLE LinExpe1;
     #gomantl1@#1003;
     ;
     #0#0, #0#1, 001;
     #0#0, #0#1, 999;
     ;
     NULL, LinExpe1;
|FIN;

|PROCESO Subcontrata66; |TIPO 66;
     aAlfa = aDef + "gosubcoc";
     |CARGA_DEF aAlfa, gosubcoc@;
     |SI FSalida = 0;
          aAlfa = aDef + "gosubcoc";
          |CARGA_DEF aAlfa, referen@;
          |SI FSalida = 0;
               aAlfa = aDef + "gomantl1";
               |CARGA_DEF aAlfa, gomantl1@;
               |SI FSalida = 0;
                    aAlfa = aPath + "fich/";
                    aAlfa = aAlfa + (("00000" + #0#6) % -105) + "/";
                    |PATH_DAT #gosubcoc@#aAlfa#;
                    |PATH_DAT #gomantl1@#aAlfa#;
                    aAlfa = Usuario;
                    |NOME_DAT #referen@#aAlfa#;
                    |DELINDEX #referen@;
                    |ABRE #referen@;
                    |BUCLE LinExpe1;
                    #referen@#0 = #0#9;
                    |CONSULTA_CLAVE #1#referen@;
                    |SI FSalida = 0;
                         #0#9 = #referen@#0; |PINTA #0#9;
                    |SINO;
                         |ERROR;
                    |FINSI;
                    |CIERRA #referen@;
                    |DELINDEX #referen@;
                    |DESCARGA_DEF #gomantl1@;
               |FINSI;
               |DESCARGA_DEF #referen@;
          |FINSI;
          |DESCARGA_DEF #gosubcoc@;
     |FINSI;
|FPRC;

|PROCESO Subcontrata0;
     aAlfa = aDef + "gosubcoc";
     |CARGA_DEF aAlfa, gosubcoc@;
     |SI FSalida = 0;
          aAlfa = aDef + "gomantl1";
          |CARGA_DEF aAlfa, gomantl1@;
          |SI FSalida = 0;
               aAlfa = aPath + "fich/";
               aAlfa = aAlfa + (("00000" + #0#6) % -105) + "/";
               |PATH_DAT #gosubcoc@#aAlfa#;
               |PATH_DAT #gomantl1@#aAlfa#;
               |DDEFECTO #gosubcoc@;
               |ABRE #gosubcoc@;
               #gosubcoc@#0 = #0#9;
               |LEE 000#gosubcoc@.=;
               |SI FSalida ! 0;
                    |MENAV "0000No existe Subcontrata...";
                    |ERROR;
               |SINO;
                    aSerPreSub = #gosubcoc@#4;
                    nNumPreSub = #gosubcoc@#5;
                    |ABRE #gomantl1@; |SELECT #3#gomantl1@;
                    #gomantl1@#3 = #gosubcoc@#4;
                    #gomantl1@#4 = #gosubcoc@#5;
                    |LEE 000#gomantl1@.=;
                    |SI #gomantl1@#0 ! #0#0; |O #gomantl1@#1 ! #0#1;
                         |MENAV "0000La subcontrata no pertenece a este expediente...";
                         |ERROR;
                    |SINO;
                         |SI #gomantl1@#10 = "T";
                              |MENAV "0000Expediente terminado...";
                              |ERROR;
                         |SINO;
                              |SI #gosubcoc@#2 ! #0#8;
                                   aAlfa = "0000Subcontrata con proveedor:" + #gosubcoc@#2;
                                   |MENAV aAlfa;
                                   |ERROR;
                              |SINO;
                                   |SI #gosubcoc@#20 = "S";
                                        |MENAV "0000Subcontrata finalizada...";
                                        |ERROR;
                                   |FINSI;
                              |FINSI;
                         |FINSI;
                    |FINSI;
               |FINSI;
               |CIERRA #gosubcoc@;
               |DESCARGA_DEF #gomantl1@;
          |FINSI;
          |DESCARGA_DEF #gosubcoc@;
     |FINSI;
|FPRC;

|PROCESO Min1;
     #1#0 = #0#6;
     #1#1 = #0#0;
     #1#2 = #0#1;
     #1#3 = #0#9;
     #1#4 = #0#2;
     #1#5 = 1;
|FPRC;

|PROCESO Max1;
     #1#0 = #0#6;
     #1#1 = #0#0;
     #1#2 = #0#1;
     #1#3 = #0#9;
     #1#4 = #0#2;
     #1#5 = 99999;
|FPRC;

|PROGRAMA;
     eaEsMJ = "S";
     |ABRE #10; |LEE 000#10p; |CIERRA #10;
     |DBASS aPath;
     aPath = aPath + #10#1; |QBF aPath;
     aDef = aPath + "def/";
     aFich = aPath + "fich/";
     |CLS;
     |PINPA #0#0;
     |PINDA #0#0;
     |ENTLINEAL #1#1, 6, 7, 22, 0, Min1, Max1;
     |PARA FSalida = 0; |SI FSalida = 0; |HACIENDO;
          |ENDATOS #1#0;
     |SIGUE;
|FPRO;


|PROCESO MiraCerti;
     enSwCerti = 1;
     |BUCLE ActualizaL;
     enSwCerti = 0;
|FPRC;
