|FICHEROS;
     aojinfho #0;

     labempre;
     labtraba;
     aojentrh;
     aojentr7;
     aovi0001;
     aovi0011;
     aomj0002;
     aomj0003;

     enthoras@ #100;
|FIN;

|VARIABLES;
     &nPrecioEnt1 = 0;
     &nPrecioEnt2 = 0;
     &nPrecioMed1 = 0;
     &nPrecioMed2 = 0;

     &nPrecio1 = 0;
     &nPrecio2 = 0;
     &nPrecio3 = 0;

     &nTipoHora = 0;
     &nHoras = 0;
     &nDietasEnt = 0;
     &nDietasMed = 0;
     &aSubCont = "";
     &fFecha = @;
     &aSerExp = "";
     &nNumExp = 0;

     aDef = "";
     aDefCopia = "";
     aRutaDef = "";
     aAlfa = "";
     aRutaDatos = "";
     nHandle = 0;
     swEntera = 0;
     swMedia = 0;
     fFechaDesde = @;
     fFechaHasta = @;
     numer = 0;
     anynum = 0;
     mesnum = 0;
     bisiesto = 0;
     topemes = 0;

     aDia = "";
     aMes = "";
     aAny = "";
     nDia = 0;
     &nMes = 0;
     &nAny = 0;
     aFecha = "";
|FIN;

|PROCESO TipoDieta;
     || busca importe de dieta para el modulo de MARCOS JIMENEZ
     swEntera = 0;       || es dieta entera
     swMedia = 0;        || es dieta media

     #aomj0002#0 = #labtraba#87;
     #aomj0002#1 = #labtraba#17;
     |LEE 000#aomj0002.=;
     |SI FSalida = 0;
          |SI nTipoHora = #aomj0002#10;
               swEntera = 1;       || es dieta entera
          |FINSI;
          |SI nTipoHora = #aomj0002#11;
               swMedia = 1;       || es dieta media
          |FINSI;
     |FINSI;

     #aomj0003#0 = #labtraba#0;
     #aomj0003#1 = #labtraba#1;
     |LEE 000#aomj0003.=;
     |SI FSalida = 0;
          |SI nTipoHora = #aomj0003#10;
               swEntera = 1;       || es dieta entera
          |FINSI;
          |SI nTipoHora = #aomj0003#11;
               swMedia = 1;       || es dieta media
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO PreciosDietas;
     nPrecioEnt1 = 0;
     nPrecioEnt2 = 0;
     nPrecioMed1 = 0;
     nPrecioMed2 = 0;

     || busca importe de dieta para el modulo de MARCOS JIMENEZ

     #aomj0002#0 = #labtraba#87;
     #aomj0002#1 = #labtraba#17;
     |LEE 000#aomj0002.=;
     |SI FSalida = 0;
          nPrecioEnt1 = #aomj0002#5;
          nPrecioEnt2 = #aomj0002#4;
          nPrecioMed1 = #aomj0002#9;
          nPrecioMed2 = #aomj0002#8;
     |FINSI;

     #aomj0003#0 = #labtraba#0;
     #aomj0003#1 = #labtraba#1;
     |LEE 000#aomj0003.=;
     |SI FSalida = 0;
          nPrecioEnt1 = #aomj0003#5;
          nPrecioEnt2 = #aomj0003#4;
          nPrecioMed1 = #aomj0003#9;
          nPrecioMed2 = #aomj0003#8;
     |FINSI;
|FPRC;

|PROCESO PreciosHoras;
     nPrecio1 = 0;
     nPrecio2 = 0;
     nPrecio3 = 0;

     #aovi0001#0 = #labtraba#87;        || convenio
     #aovi0001#1 = #labtraba#17;        || categoria
     |LEE 000#aovi0001.=;
     |SI FSalida = 0;
          nPrecio1 = #aovi0001#93;
          nPrecio2 = #aovi0001#101;
          nPrecio3 = #aovi0001#123;
     |FINSI;

     #aovi0011#0 = #labtraba#0;        || empresa
     #aovi0011#1 = #labtraba#1;        || trabajador
     |LEE 000#aovi0011.=;
     |SI FSalida = 0;
          nPrecio1 = #aovi0011#93;
          nPrecio2 = #aovi0011#101;
          nPrecio3 = #aovi0011#123;
     |FINSI;
|FPRC;

|PROCESO horas_aojentrh;
     || inicializo variables
     nHoras = 0;
     nDietasEnt = 0;
     nDietasMed = 0;

     #labtraba#0 = #100#2;
     #labtraba#1 = #100#4;
     |LEE 000#labtraba.=;

     nTipoHora = #100#19;
     |SI nTipoHora ! 00;
          |HAZ TipoDieta;
          |SI swEntera = 1;
               nDietasEnt = #100#17;
          |FINSI;
          |SI swMedia = 1;
               nDietasMed = #100#17;
          |FINSI;
     |SINO;
          nHoras = #100#17;
     |FINSI;

     || ya se si lo que tengo es hora normal, dieta entera o dieta media
     || ahora acumulo

     fFecha = #100#6;

     |SI aDef = "aojentr7";
          aSubCont = "S";
     |SINO;
          aSubCont = "N";
     |FINSI;
     aSerExp = #100#0;
     nNumExp = #100#1;

     aAlfa = fFecha;
     aAlfa = aAlfa % 402;
     nMes = aAlfa;

     nAny = #0#6;

     |PRINT;
|FPRC;

|DEFBUCLE aojentr7;
     #100#1007;
     ;
     #labtraba#0, #0#7, #0#8,  000000, fFechaDesde, #labtraba#1, 001;
     #labtraba#0, #0#9, #0#10, 999999, fFechaHasta, #labtraba#1, 999;
     ;
     NULL, horas_aojentrh;
|FIN;

|DEFBUCLE aojentrh;
     #100#1006;
     ;
     #0#7, #0#8,  #labtraba#0, #labtraba#1, fFechaDesde, 001;
     #0#9, #0#10, #labtraba#0, #labtraba#1, fFechaHasta, 999;
     ;
     NULL, horas_aojentrh;
|FIN;

|PROCESO ulti_dia;  || halla el ultimo dia del mes
     numer = anynum $ 4;
     |SI numer = 0;
             bisiesto = 1;
     |SINO;
             bisiesto = 0;
     |FINSI;
     |SI mesnum =  1;  topemes = 31;            |FINSI;
     |SI mesnum =  2;  topemes = 28 + bisiesto; |FINSI;
     |SI mesnum =  3;  topemes = 31;            |FINSI;
     |SI mesnum =  4;  topemes = 30;            |FINSI;
     |SI mesnum =  5;  topemes = 31;            |FINSI;
     |SI mesnum =  6;  topemes = 30;            |FINSI;
     |SI mesnum =  7;  topemes = 31;            |FINSI;
     |SI mesnum =  8;  topemes = 31;            |FINSI;
     |SI mesnum =  9;  topemes = 30;            |FINSI;
     |SI mesnum = 10;  topemes = 31;            |FINSI;
     |SI mesnum = 11;  topemes = 30;            |FINSI;
     |SI mesnum = 12;  topemes = 31;            |FINSI;
|FPRC;

|PROCESO CargaDef;
     |DBASS aRutaDef;
     |DBASS aRutaDatos;
     |QBF aRutaDef;
     |QBF aRutaDatos;
     aRutaDef = aRutaDef + "laboral/def/" + aDef + ".mas";
     |CARGA_DEF aRutaDef, enthoras@;

     |SI FSalida ! 0;
         aAlfa = "    Error en CARGADEF: " + aDefCopia + " !!!";
         |MENAV aAlfa;
         |ACABA;
     |FINSI;

     aRutaDatos = aRutaDatos + "laboral/fich/";
     |PATH_DAT #100 aRutaDatos;
     |ABRE #100;
|FPRC;

|PROCESO DescargaDef;
     |CIERRA #100;
     |DESCARGA_DEF #100;
|FPRC;

|PROCESO CargaHorasSub;
     || para cargar las horas de subcontratas del modulo de Marcos Jimenez

     |DBASS aAlfa; |QBF aAlfa;
     aAlfa = aAlfa + "laboral/def/aojentr7.mas";
     |XABRE "E", aAlfa, nHandle;

     |SI FSalida = 0;         || Marcos Jimenez
          aDef = "aojentr7";
          |HAZ CargaDef;
          |BUCLE aojentr7;
          |HAZ DescargaDef;
     |FINSI;

     |XCIERRA nHandle;
|FPRC;

|PROCESO CargaHoras;
     || estoy en el modulo de Marcos Jimenez o estoy en el de Vipei??

     |DBASS aAlfa; |QBF aAlfa;
     aAlfa = aAlfa + "laboral/def/aojentrh.mas";
     |XABRE "E", aAlfa, nHandle;

     |SI FSalida = 0;         || Marcos Jimenez
          aDef = "aojentrh";
/*
     || bueno si pide el informe el resto se hara, eso es rapido,
     || copiando los procesos del aovi0006
     |SINO;                   || Vipei y resto de construccion
          aDef = "aomentr4";
*/
     |FINSI;

     |HAZ CargaDef;

     |SI aDef = "aojentrh";
          |BUCLE aojentrh;
/*
     || bueno si pide el informe el resto se hara, eso es rapido,
     || copiando los procesos del aovi0006
     |SINO;
          |BUCLE aomentr4;
*/
     |FINSI;

     |HAZ DescargaDef;

     |XCIERRA nHandle;
|FPRC;


|PROCESO labtraba;
     nPrecioEnt1 = 0;
     nPrecioEnt2 = 0;
     nPrecioMed1 = 0;
     nPrecioMed2 = 0;

     nPrecio1 = 0;
     nPrecio2 = 0;
     nPrecio3 = 0;

     |HAZ PreciosHoras;
     |HAZ PreciosDietas;

     |HAZ CargaHoras;
     |HAZ CargaHorasSub;
|FPRC;

|DEFBUCLE labtraba;
     #labtraba#1;
     ;
     #0#0, #0#2;
     #0#1, #0#3;
     ;
     NULL, labtraba;
|FIN;

|PROCESO Fechas;
     aDia = "01";
     nMes = #0#4;
     aMes = nMes;
     aMes = ("00" + aMes) % -102;
     nAny = #0#6;
     aAny = nAny;
     aFecha = aDia + "." + aMes + "." + aAny;
     fFechaDesde = aFecha;

     nMes = #0#5;
     aMes = nMes;
     aMes = ("00" + aMes) % -102;
     mesnum = nMes;
     anynum = nAny;
     |HAZ ulti_dia;
     aDia = topemes;
     aDia = ("00" + aDia) % -102;
     aFecha = aDia + "." + aMes + "." + aAny;
     fFechaHasta = aFecha;
|FPRC;

|PROCESO informe;
     |ABRE #labempre;
     |ABRE #labtraba;
     |ABRE #aojentrh;
     |ABRE #aojentr7;
     |ABRE #aovi0001;
     |ABRE #aovi0011;
     |ABRE #aomj0002;
     |ABRE #aomj0003;

     |HAZ Fechas;
     |BUCLE labtraba;

     |CIERRA #aomj0003;
     |CIERRA #aomj0002;
     |CIERRA #aovi0011;
     |CIERRA #aovi0001;
     |CIERRA #aojentr7;
     |CIERRA #aojentrh;
     |CIERRA #labtraba;
     |CIERRA #labempre;
|FPRC;

|PROGRAMA;
     |CLS;
     |PINPA #0#0;
     |PINDA #0#0;
     |ENDATOS #1#0;
     |SI FSalida = 0;
          Impresora = "CrystalReport";
          |IMPRE -1;
          |INFOR "aojinfho";
          |HAZ informe;

          |PIEINF;
          |FININF;
          |FINIMP;
     |FINSI;
|FPRO;
