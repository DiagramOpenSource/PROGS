|FICHEROS;
     nomir001 #0;
     labempre #1;   || empresas
     labtraba #2;   || trabajadores
     nomhicca #3;   || historico cabs.
     nomcalca #4;   || calculos cabs.
     labregis #5;   || registro contratos
     nomhicli #6;   || historico lins.
     nomcalli #7;   || calculos lins.
     nomconli #8;   || convenios lins.
     nomvarca;
     nomauto2,MANTE;

     nomir002 #101,MANTE; || asignar codigo
     nomir003 #102,MANTE; || parametros de calculo
     nomir004 #103,MANTE; || ayuda para pintar campos en formato estandar
     nomir005 #104,MANTE; || presentacion de resultados
     nomir007 #106,MANTE; || fechas de alta/baja modificadas

     nomir999 #999;      || detector de cambio de version

     nomir008 #2000;     || resultados calculo masivo
     nomir009 #2002;     || regularizacion automatica

     :/modelos/def/dsm190li  #137; || mantenimiento 190 lineas
     :/modelos/def/dsm19001;       || mantenimiento 190 lineas 2016
     :/modelos/def/dsmom026  #138; || grupos de empresas para 110/190
|FIN;

|VARIABLES;
     aAlfa1 = "";
     aAlfa2 = "";
     aAlfa3 = "";
     aAlfa4 = "";
     nNume1 = 0;
     nNume2 = 0;
     nNume3 = 0;
     nNume4 = 0;
     nPara1 = 0;
     nPara2 = 0;
     nPara3 = 0;
     nPara4 = 0;
     nPara5 = 0;
     fFech1 = @;
     fFech2 = @;
     fFech3 = @;
     fFech4 = @;

     x = "";
     final = "";
     letra = "";
     letrasnif = "TRWAGMYFPDXBNJZSQVHLCKE";
     lugar = 0;
     resto = 0;
     nifnum = 0;

     &nCalculo99 = 0;          || comunicacion con 'nomcalir'
     &nEmpr99 = 0;
     &nTrab99 = 0;
     &nAnyo99 = 0;
     &nEstima = 0;
     &nSeguri = 0;
     &nMes99 = 0;
     &nSwMes99 = 0;
     &aFeAlta = "";
     &aFeBaja = "";

     &nDiferencias = 0;
     &nVEmp = 0;
     &aVDNI = "";
     &nVAny = 0;
     &nCuotNueva = 0;
     &nFam00    =  0;
     &nFam01    = 0;
     &nFam02    = 0;
     &nFam03    = 0;
     &nFam04    = 0;
     &nFam05    = 0;
     &enEdad    = 0;
     &eaTContr  = "";
     &eaDiscap  = "";
     &eaSitFam  = "";
     &eaCeMel   = "";
     &eaGrado   = "";
     &enEmpresa = 0;
     &eaNif     = "";
     &enModoNif = 0;
     &enAnyo    = 0;

     nModo = 0;
     nModoExt = 0;
     nInkey = 0;
     nSwRepinta = 0;
 {-1}pMenu  = "";
     pMenu1 = "1625";
     pMenu2 = "1";
     pMenu3 = "6";
     pMenu4 = "Datos Familiares ";
     pMenu5 = "Asignar Codigo de Trabajador.";
     pMenu6 = "Emitir Comunicacion de  Datos.";
     pMenu7 = "Configurar Parametros del Calculo.";
     pMenu8 = "Presentar Resultados del Calculo.";
     pMenu9 = "Actualizar Porcentaje en Trabajador.";
     nCodEmpr = 0;
     nCodTrab = 0;
     &aEjemplar = "";
     NifLetra = "";
     nEstado = 0;
     nEstado2 = 0;
     nLinea = 0;
     nDosDigitos = 0;
     nEdad = 0;
     nEsBases = 0;
     nEsCuota = 0;
     nTipo = 0;
     nGuardaTipo = 0;
     nAcumSS = 0;
     nMes = 0;
     nSwMes = 0;
     &nElTop = 12;
     &nElMes = 0;
     &nElAny = 0;
     nAcumBases = 0;
     nAcumCuota = 0;
     aPMensual = "";
     nOtrosIngresos = 0;
     nOtrosSocial = 0;
     nAsignado = 0;
     &nEsteMes = 0;
     nBaseEste = 0;
     nSeguEste = 0;

     nB0 = 0;
     nB1 = 0;
     nB2 = 0;
     nB3 = 0;
     nB4 = 0;
     nB5 = 0;
     nR0 = 0;
     nR1 = 0;
     nR2 = 0;
     nR3 = 0;
     nR4 = 0;
     nR5 = 0;
     nP0 = 0;
     nP1 = 0;
     nP2 = 0;
     nP3 = 0;
     nP4 = 0;
     nP5 = 0;
     nEspecial = 0;
     nInferior = 0;
     nMaximo = 0;
     nEntero = 0;
     nEl20 = 0;
     nEl21 = 0;
     nEl22 = 0;
     nEl23 = 0;

     n12 = 0;      n25 = 0;      n100 = 0;
     n150 = 0;     n200 = 0;     n300 = 0;
     n375 = 0;     n500 = 0;     n550 = 0;
     n600 = 0;     n650 = 0;     n850 = 0;
     n1150 = 0;    n1250 = 0;    n1350 = 0;
     n1351 = 0;    n1450 = 0;    n1675 = 0;
     n1850 = 0;    n2000 = 0;    n2025 = 0;
     n3500 = 0;

     &EURODB = 0;

     nSwPagas = 0;
     nPaga12 = 0;

     nRNT = 0;
     nMINPER = 0;
     nRED46 = 0;
     nDISTRA = 0;
     nPROLONLAB = 0;
     nMOVILGEO = 0;
     n65PER = 0;
     n75PER = 0;
     nDISPER = 0;
     nASISPER = 0;
     nPENSION = 0;
     nDESEM = 0;
     nCUIDADO = 0;
     nNUMDES = 0;
     nHIJOS = 0;
     nDISDES = 0;
     nASISDES = 0;
     n65AS = 0;
     nNUMAS = 0
     n75AS = 0;
     nDISAS = 0;
     nREDU = 0;
     nBASE = 0;
     aBASE = "";
     nRED65 = 0;
     nRED75 = 0;
     nREDIS = 0;
     nRETRIB = 0;
     aEXENTOS = "";
     nCUOTA = 0;
     nTIPO = 0;
     nLIMITE = 0;
     nANUALIDADES = 0;
     nCONTRATO = 0;
     nIMPORTE = 0;
     nEDADAS = 0;
     nASISAS = 0;
     nCONYUGE = 0;
     nMINDES = 0;
     aSITUFAM = "";
     nBASE1 = 0;
     nBASE2 = 0;
     nCUOTA1 = 0;
     nCUOTA2 = 0;
     nBASEA = 0;
     aBASEA = "";
     nTIPOA = 0;
     nRETENIDO = 0;
     nPERCIBIDO = 0;
     nAdopcion = 0;
     nREDDESC = 0;
     nREDASC = 0;
     nRETRIBa   = 0;
     nIMPORTEA  = 0;
     aREVISAR   = "N";
     nINCREIMPORTE = 0;
     nINCREBASE = 0;

     &aFecha = "";
     &p_mes = "";
     &aDia = "";
     &aAnyo = "";

     &nRete1_PA = 0;
     &nRete2_PA = 0;
     &nRete3_PA = 0;

     nCuot = 0;
     nBase = 0;
     nRete = 0;

     aClave = "";
     aDSubClave = "";
     aHSubClave = "";
     aEspe = "";
     nElAnyAnt = 0;
     aCeuMelTemp = "";
     &swTipoMedio = 0;
     &swAvisaMedio = 0;
     swEl190 = 0;
     &nTIPOEst = 0;
     nElCodigo = 0;

     aFechaBaja = "";
     aMesBaja = "";
     aAnyBaja = "";
     nMesBaja = 0;
     nAnyBaja = 0;
     nElTopAnt = 0;
     nBaseFiniq = 0;
     nRetenFiniq = 0;
     &swFiniq = 0;
     &swMesBaja = 0;
     &nBaseEste2 = 0;
     &nRetenEste2 = 0;
     &eaVengoDe = "";
     &nJorAgr = 0;
     &enMenav = 0;
     &nBaseHist = 0;
     &nSSHist= 0;
     nREDCOPA = 0;
     nAutGastos = 0;
|FIN;

|| **********************************************************************
|PROCESO dsm190li_1_06;
     swEl190 = 1;
     |SI aEspe = "N";
          nBase = nBase + #dsm190li#16;      || bases
          nCuot = nCuot + #dsm190li#17;      || cuota
     |SINO;
          nBase = nBase + #dsm190li#18;      || bases
          nCuot = nCuot + #dsm190li#19;     || cuota
     |FINSI;
|FPRC;

|DEFBUCLE dsm190li_06;
     #dsm190li#1;
     ;
     nElCodigo, nElAnyAnt, 99, 190, 0, 0, #102#0, 0000, aClave, aDSubClave, 0;
     nElCodigo, nElAnyAnt, 99, 190, 0, 0, #102#0, 0000, aClave, aHSubClave, 4;
     ;
     NULL, dsm190li_1_06;
|FIN;

|PROCESO dsm19001_1_06;
     swEl190 = 1;
     |SI aEspe = "N";
          nBase = nBase + #dsm19001#16;      || bases
          nBase = nBase + #dsm19001#65;      || bases
          nCuot = nCuot + #dsm19001#17;      || cuota
          nCuot = nCuot + #dsm19001#66;      || cuota
     |SINO;
          nBase = nBase + #dsm19001#18;      || bases
          nBase = nBase + #dsm19001#67;      || bases
          nCuot = nCuot + #dsm19001#19;      || cuota
          nCuot = nCuot + #dsm19001#68;      || cuota
     |FINSI;
|FPRC;

|DEFBUCLE dsm19001_06;
     #dsm19001#1;
     ;
     nElCodigo, nElAnyAnt, 99, 190, 0, 0, #102#0, 0000, aClave, aDSubClave, 0;
     nElCodigo, nElAnyAnt, 99, 190, 0, 0, #102#0, 0000, aClave, aHSubClave, 4;
     ;
     NULL, dsm19001_1_06;
|FIN;

|PROCESO dsmom026_06;
     nElCodigo = #dsmom026#0;
|FPRC;

|DEFBUCLE dsmom026_06;
     #dsmom026#2;
     ;
     #102#41, 00001;
     #102#41, 99999;
     ;
     NULL, dsmom026_06;
|FIN;

|PROCESO acumula_06;
     |ABRE #labtraba;
     #labtraba#0 = #101#1;
     #labtraba#1 = #101#2;
     |LEE 000#labtraba.=;
     |CIERRA #labtraba;

     |SI #labtraba#99 ! "L";
          nBase = 0;
          nCuot = 0;
          aClave = #labtraba#99;
          aDSubClave = #labtraba#102;
          aHSubClave = #labtraba#102;
          aEspe = #labtraba#105;
          nElCodigo = #102#41;

          |BUCLE dsmom026_06;

          |SI nElAnyAnt [ 2015;
              |BUCLE dsm190li_06;
          |FINSI;

          |SI nElAnyAnt ] 2016;
              |BUCLE dsm19001_06;
          |FINSI;

          nRete = ((nCuot / nBase) * 100);
          nRete = nRete - 0.005;
          nRete = nRete ! 2;       || trunco a dos decimales
          nRete = nRete ! 0;       || redondeo a cero decimales

          nRete1_PA = nRete;
     |FINSI;

     |SI #labtraba#100 ! "L";
          nBase = 0;
          nCuot = 0;
          aClave = #labtraba#100;
          aDSubClave = #labtraba#103;
          aHSubClave = #labtraba#103;
          aEspe = #labtraba#106;

          |SI nElAnyAnt [ 2015;
              |BUCLE dsm190li_06;
          |FINSI;

          |SI nElAnyAnt ] 2016;
              |BUCLE dsm19001_06;
          |FINSI;

          nRete = ((nCuot / nBase) * 100);
          nRete = nRete - 0.005;
          nRete = nRete ! 2;       || trunco a dos decimales
          nRete = nRete ! 0;       || redondeo a cero decimales

          nRete2_PA = nRete;
          |SI nRete2_PA < 0;
               nRete2_PA = 0;
          |FINSI;
     |FINSI;

     |SI #labtraba#101 ! "L";
          nBase = 0;
          nCuot = 0;
          aClave = #labtraba#101;
          aDSubClave = #labtraba#104;
          aHSubClave = #labtraba#104;
          aEspe = #labtraba#107;

          |SI nElAnyAnt [ 2015;
              |BUCLE dsm190li_06;
          |FINSI;

          |SI nElAnyAnt ] 2016;
              |BUCLE dsm19001_06;
          |FINSI;

          nRete = ((nCuot / nBase) * 100);
          nRete = nRete - 0.005;
          nRete = nRete ! 2;       || trunco a dos decimales
          nRete = nRete ! 0;       || redondeo a cero decimales

          nRete3_PA = nRete;
          |SI nRete3_PA < 0;
               nRete3_PA = 0;
          |FINSI;
     |FINSI;
|FPRC;
|| **********************************************************************

|PROCESO Ordenados001_06;
     nNume1 = 0;
     |PARA nPara1 = 20; |SI nPara1 [ 29; |HACIENDO nPara1 = nPara1 + 1;
          |SI #102nPara1 ! 0;
               |SI nNume1 = 0;
                    nNume1 = #102nPara1;
               |FINSI;
               |SI nNume1 > #102nPara1;
                    |MENAV "0000Asegurese de que los descendientes esten ordenados por fecha de nacimiento.";
                    |SAL;
               |SINO;
                    nNume1 = #102nPara1;
               |FINSI;
          |FINSI;
     |SIGUE;
|FPRC;

|PROCESO ConstantesEuro_06;      || Pasa constantes de cuotas retencion a EURO
                              || o a Pesetas segun la moneda elegida
     |SI EURODB = 2;
          n12 = 75.13;        n25 = 150.25;       n100 = 601.01;
          n150 = 901.52;      n200 = 1202.02;     n300 = 1803.04;
          n375 = 2253.80;     n500 = 3005.06;     n550 = 3305.57;
          n600 = 3606.07;     n650 = 3906.58;     n850 = 5108.60;
          n1150 = 6911.64;    n1250 = 7512.65;    n1350 = 8113.66;
          n1351 = 8113.67;    n1450 = 8714.68;    n1675 = 10066.95;
          n1850 = 11118.72;   n2000 = 12020.24;   n2025 = 12170.50;
          n3500 = 21035.42;
     |SINO;
          n12 = 12500;        n25 = 25000;        n100 = 100000;
          n150 = 150000;      n200 = 200000;      n300 = 300000;
          n375 = 375000;      n500 = 500000;      n550 = 550000;
          n600 = 600000;      n650 = 650000;      n850 = 850000;
          n1150 = 1150000;    n1250 = 1250000;    n1350 = 1350000;
          n1351 = 1350001;    n1450 = 1450000;    n1675 = 1675000;
          n1850 = 1850000;    n2000 = 2000000;    n2025 = 2025000;
          n3500 = 3500000;
      |FINSI;
|FPRC;

|PROCESO Inicializa_06;
     nRNT = 0;
     nMINPER = 0;
     nRED46 = 0;
     nDISTRA = 0;
     nPROLONLAB = 0;
     nMOVILGEO = 0;
     n65PER = 0;
     n75PER = 0;
     nDISPER = 0;
     nASISPER = 0;
     nPENSION = 0;
     nDESEM = 0;
     nCUIDADO = 0;
     nNUMDES = 0;
     nMINDES = 0;
     nHIJOS = 0;
     nDISDES = 0;
     nASISDES = 0;
     n65AS = 0;
     nNUMAS = 0
     n75AS = 0;
     nDISAS = 0;
     nASISAS = 0;
     nREDU = 0;
     nBASE = 0;
     nRED65 = 0;
     nRED75 = 0;
     nREDIS = 0;
     nRETRIB = 0;
     aEXENTOS = "";
     nCUOTA = 0;
     nTIPO = 0;
     nLIMITE = 0;
     nANUALIDADES = #102#12;       || anualidades a favor de los hijos
     nTIPO = 0;
     nCONTRATO = #102#5;
     nIMPORTE = 0;
     nRETRIBa   = 0;
     nBASEA     = 0;
     nTIPOA     = 0;
     nIMPORTEA  = 0;
     nPERCIBIDO = 0;
     nRETENIDO  = 0;
     aREVISAR   = "N";
     nINCREIMPORTE = 0;
     nINCREBASE = 0;
     nREDCOPA = 0;
|FPRC;

|PROCESO SinCalculo_06;
     |SI enMenav = 1;
          #102#9 = nBaseHist;
          #102#11 = nSSHist;
     |FINSI;

     #104#2  = #102#1;        || edad
     #104#3  = #102#6;        || discapacitado S/N
     #104#4  = #102#7;        || grado 1-2
     #104#5  = #102#8;        || necesita ayuda S/N
     #104#6  = #102#2;        || situacion laboral
     #104#7  = #102#5;        || tipo de contrato
     #104#8  = #102#3;        || situacion familiar
     #104#9  = #102#9;        || ingresos anuales
     #104#10 = #102#10;       || reduccion 17.2
     #104#11 = #102#11;       || gastos anuales 17.3
     #104#15 = #102#13;       || anualidades conyuge
     #104#16 = #102#12;       || anualidades hijos
     #104#17 = #102#15;       || regularizando S/N
     #104#18 = #102#14;       || Residencia Ceuta y Melilla S/N
     #104#56 = #102#14;       || Rendimientos Ceuta y Melilla S/N
     #104#54 = #102#73;       || Movilidad Geografica S/N
     #104#53 = #102#72;       || Prolongacion Actividad Laboral S/N
|FPRC;

|PROCESO Descendientes_06;
     |PDEFECTO #104, 19, 25;
     |PDEFECTO #104, 29, 30;
     |PDEFECTO #104, 57, 70;
     nEl20 = 0;     nEl21 = 0;     nEl22 = 0;     nEl23 = 0;
     |PARA nPara1 = 20; |SI nPara1 [ 29; |HACIENDO nPara1 = nPara1 + 1;
          nEdad = #0#2 - #102nPara1;
          |SI #102nPara1 ! 0;
               nPara2 = nPara1 + 10;    || grado minusvalia
               nPara3 = nPara1 + 22;    || por entero S/N
               nPara4 = nPara1 + 65;    || movilidad reducida S/N
               nPara5 = nPara1 + 55;    || A¤o Adopcion

               || solo tienen reduccion menores de 25 a¤os o mayores c/minusv.
               |SI nEdad < 25; |O #102nPara2 = "1"; |O #102nPara2 = "2";
                    nNUMDES = nNUMDES + 1;
                    nAdopcion = #0#2 - #102nPara5;
                    nEntero = 0.5;
                    |SI #102nPara3 = "S";    || si computa o no por entero
                         nEntero = 1;
                         #104#24 = #104#24 + 1;   || por entero
                    |FINSI;

                    #104#19 = #104#19 + 1;   || numero de descendientes

                                             || calculamos edad
                    |SI nEdad < 3;
                         #104#21 = #104#21 + 1;
                    |FINSI;
                    |SI nEdad ]3; |Y nEdad < 25;
                         #104#20 = #104#20 + 1;
                    |FINSI;
                                             || calculamos a¤os adopcion
                    |SI nAdopcion < 3;
                         #104#66 = #104#66 + 1;
                    |FINSI;

                                             || minimo por descendientes
                    |SI #104#19 = 1;
                         #104#29 = #104#29 + (1400 * nEntero);
                    |FINSI;

                    |SI #104#19 = 2;
                         #104#29 = #104#29 + (1500 * nEntero);
                    |FINSI;

                    |SI #104#19 = 3;
                         #104#29 = #104#29 + (2200 * nEntero);
                    |FINSI;

                    |SI #104#19 > 3;
                         #104#29 = #104#29 + (2300 * nEntero);
                    |FINSI;
                    nMINDES = #104#29;

                                             || reduccion por cuidado de descend.
                    ||SI #102nPara1 > 2000;
                    |SI nEdad < 3;
                         nCUIDADO = nCUIDADO + 1200 * nEntero;
                    |SINO;
                         nNume1 = #102nPara5 - #102nPara1;
                         |SI #102nPara5 ] #102nPara1; |Y #102nPara5 > 2000; |Y nNume1 < 19;
                              nCUIDADO = nCUIDADO + 1200 * nEntero;
                         |FINSI;
                    |FINSI;

                    |SI #102nPara2 = "1";    || reduccion discapacidad descend.
                         nDISDES = nDISDES + (2000 * nEntero);    || 33-65%
                         #104#22 = #104#22 + 1;
                    |FINSI;

                    |SI #102nPara2 = "2";
                         nDISDES = nDISDES + (5000 * nEntero);    || > 65%
                         #104#23 = #104#23 + 1;
                    |FINSI;

                                             || reduccion asistencia descend.disc.
                    |SI #102nPara2 = "1"; |Y #102nPara4 = "S";
                         nASISDES = nASISDES + (2000 * nEntero);
                         #104#67 = #104#67 + 1;
                    |FINSI;

                    |SI #102nPara2 = "2";
                         nASISDES = nASISDES + (2000 * nEntero);
                    |FINSI;
               |FINSI;
          |FINSI;
     |SIGUE;
     |SI nNUMDES > 2;
          nHIJOS = 600;
     |FINSI;
|FPRC;

|PROCESO SinCalculo190_06;
     #104#2  = enEdad;        || edad
     #104#3  = eaDiscap;      || discapacitado S/N
     #104#4  = eaGrado;       || grado 1-2
     #104#5  = #102#8;        || necesita ayuda S/N
     #104#6  = #102#2;        || situacion laboral
     #104#7  = eaTContr;      || tipo de contrato
     #104#8  = eaSitFam;      || situacion familiar
     #104#9  = #102#9;        || ingresos anuales
     #104#10 = #102#10;       || reduccion 17.2
     #104#11 = #102#11;       || gastos anuales 17.3
     #104#15 = #102#13;       || anualidades conyuge
     #104#16 = #102#12;       || anualidades hijos
     #104#17 = "N";           || regularizando S/N
     #104#18 = eaCeMel;       || Ceuta y Melilla S/N
|FPRC;

|PROCESO Descendientes190_06;
     |PDEFECTO #104, 19, 25;
     #104#19 = nFam01;   || menores de 25 (26)
     #104#20 = nFam02;   || de 3 a 16 (25)
     #104#21 = nFam03;   || de 0 a 3 (24)
     #104#22 = nFam04;   || de 33 a 65 (27)
     #104#23 = nFam05;   || mas de 65 (28)

     nEl20 = #104#20;    || minimo familiar
     nEl21 = #104#21;    || minimo familiar
     nEl22 = #104#22;    || minimo familiar
     nEl23 = #104#23;    || minimo familiar
|FPRC;

|PROCESO Rendimiento_06;
     #104#12 = 0;
     #104#27 = #102#9 - (#102#10 + #102#11);      || rend.neto trabajo
     nRNT = #104#27;
|FPRC;

|PROCESO MinimoPersonal_06;
     nMINPER = 3400;
|FPRC;

|PROCESO Reduccion46_06;
     |SI #104#27 [ 8200;
          #104#12 = 3500;
     |SINO;
          |SI #104#27 > 13000;
               #104#12 = 2400;
          |SINO;
               #104#12 = 3500 - (0.2291 * (#104#27 - 8200.01));
          |FINSI;
     |FINSI;
     nRED46 = #104#12;
|FPRC;

|PROCESO ReduccTraAct_06;             || reducciones trabajadores activos
     |SI #104#3 = "S";                       || discapacitado
          |SI #104#4 = "2";                  || >65
               ||#104#12 = #104#12 * 2.75;
               nDISTRA = 6200;
          |SINO;
               |SI #104#4 = "1";
                    |SI #102#8 = "S";
                         ||#104#12 = #104#12 * 2.25;     || >33 y <65 con ayuda
                         nDISTRA = 6200;
                    |SINO;
                         ||#104#12 = #104#12 * 1.75;     || >33 y <65 sin ayudaç
                         nDISTRA = 2800;
                    |FINSI;
               |FINSI;
          |FINSI;
     |FINSI;

     |SI #102#72 = "S"; |Y #102#1 > 64;  || Prolongacion de la actividad laboral
          nPROLONLAB = nRED46;
     |FINSI;

     |SI #102#73 = "S";            || Reduccion por movilidad goegrafica
          nMOVILGEO = nRED46;
     |FINSI;
|FPRC;

|PROCESO ReduCopaAmerica_06;
     |SI #102#2 = "A";
          |SI #102#122 = "S";
               nREDCOPA = #104#27 * 0.65;
          |SINO;
               nREDCOPA = 0;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO ReduccionPerceptor_06;
     |SI #102#1 > 64;         || reduccion por edad del perceptor
          n65PER = 800;
     |FINSI;
     |SI #102#1 > 74;         || reduccion asistencia a perceptores
          n75PER = 1000;      || mayores de 75 a¤os
     |FINSI;
     |SI #102#6 = "S";        || reduccion por discapacidad del perceptor
          |SI #102#7 = "1";
               nDISPER = 2000;          || 33-65%
          |SINO;
               nDISPER = 5000;          || >65%
          |FINSI;
     |FINSI;
     |SI #102#6 = "S";        || reduccion asistencia a perceptores discapac.
          |SI #102#7 = "2";             || >65%
               nASISPER = 2000;
          |SINO;
               |SI #102#8 = "S";        || 33-65% y necesita asistencia
                    nASISPER = 2000;
               |FINSI;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO ReduccPenDes_06;
     |SI #102#3 = "P";
          nPENSION = 600;
     |FINSI;
     |SI #102#3 = "D";
          nDESEM = 1200;
     |FINSI;
|FPRC;

|PROCESO Ascendientes_06;
     |PDEFECTO #104, 46, 50;
     |PARA nPara1 = 95; |SI nPara1 [ 104; |HACIENDO nPara1 = nPara1 + 1;
          |SI #102nPara1 ! 0;
               nPara2 = nPara1 - 33;    || % Discapacidad
               nPara3 = nPara1 - 43;    || Convivencia
               nPara4 = nPara1 + 10;    || Movilidad Reducida

               nEDADAS = #0#2 - #102nPara1;
               |SI nEDADAS < 65; |Y #102nPara2 ! "1"; |Y #102nPara2 ! "2";
                    |ACABA;        || solo mayores de 64 a¤os o menos de 64
               |FINSI;             || a¤os con disc. tienen reduccion

               |SI #102nPara3 = 0; #102nPara3 = 1; |FINSI;
               |SI #102nPara3 = 1;
                    #104#48 = #104#48 + 1;   || por entero
               |FINSI;

                                        || reduccion de edad por ascendientes
               n65AS = n65AS + (800 / #102nPara3);

                                        || reduc.asistencia mayores de 75 a¤os
               |SI nEDADAS > 74;
                    n75AS = n75AS + (1000 / #102nPara3);
                    #104#68 = #104#68 + 1;
               |FINSI;

                                        || reduccion discapacidad ascendientes
               |SI #102nPara2 = "1";
                    #104#46 = #104#46 + 1;   || de 33 a 65
                    nDISAS = nDISAS + (2000 / #102nPara3);
               |FINSI;
               |SI #102nPara2 = "2";
                    #104#47 = #104#47 + 1;   || mas de 65
                    nDISAS = nDISAS + (5000 / #102nPara3);
               |FINSI;

                                       || reduccion asistencia ascend.discap.
               |SI #102nPara2 = "1"; |Y #102nPara4 = "S";
                    nASISAS = nASISAS + (2000 / #102nPara3);
                    #104#69 = #104#69 + 1;
               |FINSI;
               |SI #102nPara2 = "2";
                    nASISAS = nASISAS + (2000 / #102nPara3);
               |FINSI;
               nREDASC = #104#46 + #104#47 + #104#68 + #104#69;
               |SI nREDASC ! 0;
                    #104#49 = #104#49 + 1;        || totales
               |FINSI;
          |FINSI;
     |SIGUE;
|FPRC;

|PROCESO Resultados_06;
     nRED65 = n65PER + n65AS;
     nRED75 = n75PER + n75AS;
     nREDIS = nDISPER + nDISDES + nDISAS + nASISPER + nASISDES + nASISAS;
|FPRC;

|PROCESO Base_06;
     nCONYUGE = #102#13;
     nREDU = nRED46 + nPENSION + nDESEM + nMOVILGEO + nPROLONLAB + nDISTRA;
     nREDU = nREDU + nMINPER + nMINDES + nCUIDADO + nHIJOS + nRED65 + nRED75;
     nREDU = nREDU + nREDIS + nCONYUGE + nREDCOPA;

     |SI nRNT > nREDU;
          nBASE = nRNT - nREDU;
     |SINO;
          nBASE = 0;
     |FINSI;
|FPRC;

|PROCESO CuotaRetencion_06;
                              || rendimientos exentos de retencion

     #104#41 = 0;             || limite de ingresos
     #104#42 = "NO";          || cuota limitada
     #104#43 = 0;             || cuota limite

     nRETRIB = #102#9;     || ingresos anuales perceptor
     aSITUFAM = #102#3;    || situacion familiar
     || nNUMDES = 104#19;     || nro. descendientes

     |SI #102#9 [ 13135;
          |SI #104#8 = "1";
               nNume1 = 10750 + nPENSION + nDESEM;
               |SI #104#19 = 1; |Y #102#9 [ nNume1; aEXENTOS = "S";  |FINSI;
               nNume1 = 12030 + nPENSION + nDESEM;
               |SI #104#19 > 1; |Y #102#9 [ nNume1; aEXENTOS = "S";  |FINSI;
          |FINSI;
          |SI #104#8 = "2";
               nNume1 = 10600 + nPENSION + nDESEM;
               |SI #104#19 = 0; |Y #102#9 [ nNume1; aEXENTOS = "S";  |FINSI;
               nNume1 = 11825 + nPENSION + nDESEM;
               |SI #104#19 = 1; |Y #102#9 [ nNume1; aEXENTOS = "S";  |FINSI;
               nNume1 = 13135 + nPENSION + nDESEM;
               |SI #104#19 > 1; |Y #102#9 [ nNume1; aEXENTOS = "S";  |FINSI;
          |FINSI;
          |SI #104#8 = "3";
               nNume1 = 7515 + nPENSION + nDESEM;
               |SI #104#19 = 0; |Y #102#9 [ nNume1; aEXENTOS = "S";  |FINSI;
               nNume1 = 8215 + nPENSION + nDESEM;
               |SI #104#19 = 1; |Y #102#9 [ nNume1; aEXENTOS = "S";  |FINSI;
               nNume1 = 8965 + nPENSION + nDESEM;
               |SI #104#19 > 1; |Y #102#9 [ nNume1; aEXENTOS = "S";  |FINSI;
          |FINSI;
     |FINSI;

     |SI aEXENTOS = "S";
          nCUOTA = 0;
          nTIPO = 0;
          |ACABA;
     |FINSI;

                         || rendimientos sujetos a retencion


     |SI nANUALIDADES = 0; |O nANUALIDADES ] nBASE;
          nEsBases = nBASE;              || calculo de la cuota de retencion
          |HAZ Escala_06;
          nCUOTA = nEsCuota;
     |SINO;
          nBASE1 = nBASE - nANUALIDADES;
          nEsBases = nBASE1;
          |HAZ Escala_06;
          nCUOTA1 = nEsCuota;

          nBASE2 = nANUALIDADES;
          nEsBases = nBASE2;
          |HAZ Escala_06;
          nCUOTA2 = nEsCuota;

          nCUOTA = nCUOTA1 + nCUOTA2;
     |FINSI;

     |SI nRETRIB [ 22000;               || reduccion del 35%
          |SI aSITUFAM = "1";
               |SI nNUMDES = 1;
                    nLIMITE = (nRETRIB - (10750 + nPENSION + nDESEM)) * 0.35;
               |FINSI;
               |SI nNUMDES > 1;
                    nLIMITE = (nRETRIB - (12030 + nPENSION + nDESEM)) * 0.35;
               |FINSI;
          |FINSI;
          |SI aSITUFAM = "2";
               |SI nNUMDES = 0;
                    nLIMITE = (nRETRIB - (10600 + nPENSION + nDESEM)) * 0.35;
               |FINSI;
               |SI nNUMDES = 1;
                    nLIMITE = (nRETRIB - (11825 + nPENSION + nDESEM)) * 0.35;
               |FINSI;
               |SI nNUMDES > 1;
                    nLIMITE = (nRETRIB - (13135 + nPENSION + nDESEM)) * 0.35;
               |FINSI;
          |FINSI;
          |SI aSITUFAM = "3";
               |SI nNUMDES = 0;
                    nLIMITE = (nRETRIB - (7515 + nPENSION + nDESEM)) * 0.35;
               |FINSI;
               |SI nNUMDES = 1;
                    nLIMITE = (nRETRIB - (8215 + nPENSION + nDESEM)) * 0.35;
               |FINSI;
               |SI nNUMDES > 1;
                    nLIMITE = (nRETRIB - (8965 + nPENSION + nDESEM)) * 0.35;
               |FINSI;
          |FINSI;
          |SI nCUOTA > nLIMITE;
               nCUOTA = nLIMITE;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Escala_06;
     |SI #0#2 ] 2000; |Y #0#2 [ 2004;
          nB0 = 0;       nR0 = 0;        nP0 = 15;
          nB1 = 4000;    nR1 = 600;      nP1 = 24;
          nB2 = 13800;   nR2 = 2952;     nP2 = 28;
          nB3 = 25800;   nR3 = 6312;     nP3 = 37;
          nB4 = 45000;   nR4 = 13416;    nP4 = 45;
     |FINSI;

     |SI #0#2 = 2005;
          nB0 = 0;       nR0 = 0;           nP0 = 15;
          nB1 = 4080;    nR1 = 612;         nP1 = 24;
          nB2 = 14076;   nR2 = 3011.04;     nP2 = 28;
          nB3 = 26316;   nR3 = 6438.24;     nP3 = 37;
          nB4 = 45900;   nR4 = 13684.32;    nP4 = 45;
     |FINSI;

     |SI #0#2 ] 2006;
          nB0 = 0;          nR0 = 0;           nP0 = 15;
          nB1 = 4161.60;    nR1 = 624.24;      nP1 = 24;
          nB2 = 14357.52;   nR2 = 3071.26;     nP2 = 28;
          nB3 = 26842.32;   nR3 = 6567.00;     nP3 = 37;
          nB4 = 46818.00;   nR4 = 13958.00;    nP4 = 45;
     |FINSI;

     nEsCuota = 0;
     |SI nEsBases < nB1;
          nEsCuota = nEsBases % nP0;
     |SINO;
          |SI nEsBases < nB2;
               nEsCuota = nR1 + ((nEsBases - nB1) % nP1);
          |SINO;
               |SI nEsBases < nB3;
                    nEsCuota = nR2 + ((nEsBases - nB2) % nP2);
               |SINO;
                    |SI nEsBases < nB4;
                         nEsCuota = nR3 + ((nEsBases - nB3) % nP3);
                    |SINO;
                         nEsCuota = nR4 + ((nEsBases - nB4) % nP4);
                    |FINSI;
               |FINSI;
          |FINSI;
     |FINSI;
     nEsCuota = nEsCuota ! EURODB;
|FPRC;

|PROCESO TipoRetencion_06;

     nTIPO = (nCUOTA / nRETRIB) * 100;
     nTIPO = nTIPO - .005;              || trunca a dos decimales
     nTIPO = nTIPO ! 2;
     |SI nTIPO < 0; nTIPO = 0;     |FINSI;

                                   || limites generales (2%, 15% y 45%)
     |SI nTIPO > 45;
          nTIPO = 45;
     |SINO;
          |SI nCONTRATO = "3"; |Y nTIPO < 15;
               nTIPO = 15;
          |SINO;
               |SI nCONTRATO = "2"; |Y nTIPO < 2;
                    nTIPO = 2;
               |FINSI;
          |FINSI;
     |FINSI;

     |SI #102#14 = "S"; |Y #102#15 = "N";    || residencia en Ceuta/Melilla y
          nTIPO = nTIPO / 2;                 || no regularizacion
     |FINSI;

     nTIPO = nTIPO ! 0;

     nIMPORTE = nRETRIB % nTIPO;
     nIMPORTE = nIMPORTE ! 2;

     |SI #102#15 = "S";
          |HAZ Regulariza_06;
     |FINSI;
|FPRC;

|PROCESO Regulariza_06;
     |SI enMenav = 1;    || no me tienes que regularizar si vienes del
          |ACABA;        || calculo de diferencias de modelos
     |FINSI;
     nRETRIBa   = #102#18;         || ingresos iniciales
     nBASEA     = #102#117;        || base inicial
     nTIPOA     = #102#118;        || tipo inicial
     nIMPORTEA  = #102#19;         || cuota inicial
     nPERCIBIDO = #102#16;         || ingresos ya satifechos
     nRETENIDO  = #102#17;         || retenciones ya practicadas

                         || tratamiento especial Ceuta y Melilla
     |SI #102#14 = "S"; |Y #102#115 = "S"; |Y #102#116 ! 13;
          nRETENIDO = #102#17 * 2;
     |SINO;
          |SI #102#14 = "S"; |Y #102#115 = "N"; |Y #102#116 = 13;
               || nRETENIDO = #104#17 * 2;xxx
               nIMPORTE = nPERCIBIDO * (nTIPOA/100);
               nNume1 = (nRETRIBa - nPERCIBIDO) * ((nTIPOA / 2) / 100);
               nIMPORTE = nIMPORTE + nNume1;
          |SINO;
               |SI #102#14 = "N"; |Y #102#115 = "S"; |Y #102#116 = 14;
                    || nRETENIDO = #104#17 * 2;
                    nIMPORTE = nPERCIBIDO * ((nTIPOA / 2) / 100);
                    nNume1 = ((nRETRIBa - nPERCIBIDO) * (nTIPOA / 100));
                    nIMPORTE = nIMPORTE + nNume1;
               |FINSI;
          |FINSI;
     |FINSI;
                         || calculo del tipo de retencion
     nTIPO = ((nIMPORTE - nRETENIDO) / (nRETRIB - nPERCIBIDO)) * 100;

     |SI nTIPO < 0; nTIPO = 0;     |FINSI;
     nTIPO = nTIPO - .005;              || trunca a dos decimales
     nTIPO = nTIPO ! 2;

     |SI nTIPO < 0; nTIPO = 0;     |FINSI;

                         || limites generales (max. 45% y min 15%, 2%)
     |SI nTIPO > 45;
          nTIPO = 45;
     |SINO;
          |SI #102#14 = "S"; |Y #102#115 = "N"; |Y #102#116 = 13;
               |SI nCONTRATO = "3"; |Y nTIPO < 8;
                    nTIPO = 8;
               |SINO;
                    |SI nCONTRATO = "1"; |Y nTIPO < 1;
                         nTIPO = 1;
                    |FINSI;
               |FINSI;
          |SINO;
               |SI nCONTRATO = "3"; |Y nTIPO < 15;
                    nTIPO = 15;
               |SINO;
                    |SI nCONTRATO = "2"; |Y nTIPO < 2;
                         nTIPO = 2;
                    |FINSI;
               |FINSI;
          |FINSI;
     |FINSI;

                         || tratamiento especial Ceuta y Melilla
     |SI #102#14 = "S"; |Y #102#115 = "S"; |Y #102#116 ! 13;
          nTIPO = nTIPO / 2;
          nRETENIDO = nRETENIDO / 2;
     |SINO;
          |SI #102#14 = "S"; |Y #102#115 = "S"; |Y #102#116 = 13;
               nTIPO = nTIPO / 2;
          |FINSI;
          |SI #102#14 = "S"; |Y #102#115 = "N"; |Y #102#116 ! 13; |Y #102#116 ! 0;
               nTIPO = nTIPO / 2;
          |FINSI;   || lo ultimo se lo a¤ado por problemas con el 2004 en
                    || Ceuta/Melilla cuando regulariza automaticamente
     |FINSI;
                         || tratamiento especial Ceuta y Melilla

                         || redondeo del tipo;
     nTIPO = nTIPO ! 0;
                         || calculo del importe anual de la retencion
     nIMPORTE = (((nRETRIB - nPERCIBIDO) * nTIPO) / 100) + nRETENIDO;
     nIMPORTE = nIMPORTE ! EURODB;

     |SI #102#116 = 11; |O #102#116 = 12; |O #102#116 = 14; |O #102#116 = 15;
       |O #102#116 = 0;
          |ACABA;
     |FINSI;

                         || limite de art. 81.5
          aBASEA = nBASEA;
          nBASEA = aBASEA;
          aBASE  = nBASE;
          nBASE  = aBASE;

     |SI nBASEA ] nBASE; |Y nTIPO > nTIPOA;
          nTIPO = nTIPOA;
          aREVISAR = "S";
     |SINO;
          |SI nBASEA < nBASE; |Y nIMPORTEA < nIMPORTE;
               nINCREIMPORTE = nIMPORTE - nIMPORTEA;
               nINCREBASE = nBASE - nBASEA;
               |SI nINCREIMPORTE > nINCREBASE;
                    aREVISAR = "S";
                    nIMPORTE = nIMPORTEA + nINCREBASE;
                    nTIPO = ((nIMPORTE - nRETENIDO) / (nRETRIB - nPERCIBIDO));
                    nTIPO = nTIPO * 100;
                    |SI nTIPO < 0;
                         nTIPO = 0;
                    |SINO;
                         nTIPO = nTIPO - .005;   || trunca a dos decimales
                         nTIPO = nTIPO ! 2;
                         nTIPO = nTIPO ! 0;
                         |SI nTIPO < 0; nTIPO = 0;     |FINSI;
                         nIMPORTE = ((nRETRIB - nPERCIBIDO) * nTIPO) / 100;
                         nIMPORTE = nIMPORTE + nRETENIDO;
                         nINCREIMPORTE = nIMPORTE - nIMPORTEA;
                         |SI nINCREIMPORTE > nINCREBASE; |Y nTIPO > 0;
                              nTIPO = nTIPO - 1;
                         |FINSI;
                    |FINSI;
               |FINSI;
          |FINSI;
     |FINSI;
     |SI aREVISAR = "S";
          |SI #102#14 = "S";
               |SI nCONTRATO = "3"; |Y nTIPO < 8;
                    nTIPO = 8;
               |SINO;
                    |SI nCONTRATO = "2"; |Y nTIPO < 1;
                         nTIPO = 1;
                    |FINSI;
               |FINSI;
          |SINO;
               |SI nCONTRATO = "3"; |Y nTIPO < 15;
                    nTIPO = 15;
               |SINO;
                    |SI nCONTRATO = "2"; |Y nTIPO < 2;
                         nTIPO = 2;
                    |FINSI;
               |FINSI;
          |FINSI;
          nIMPORTE = (((nRETRIB - nPERCIBIDO) * nTIPO) / 100) + nRETENIDO;
     |FINSI;
|FPRC;

|PROCESO Regulariza_Enero_06;
     |SI #nomir009#19 = 2; |Y nElMes = 1;
          swTipoMedio = 0;
          nRete1_PA = 0;
          nRete2_PA = 0;
          nRete3_PA = 0;
          swEl190 = 0;

          nElAnyAnt = nElAny - 1;

          |HAZ acumula_06;

          |SI swEl190 = 0;
               fFech1 = #labtraba#36;
               aAlfa1 = nElAny;
               aAlfa1 = "01.01." + aAlfa1;
               fFech2 = aAlfa1;
               aAlfa2 = #labtraba#0; aAlfa2 = "00000" + aAlfa2; aAlfa2 = aAlfa2 % -105;
               aAlfa3 = #labtraba#1; aAlfa3 = "00000" + aAlfa3; aAlfa3 = aAlfa3 % -105;
               |SI fFech1 < fFech2;     || alta antes de este a¤o y sin 190
                    aAlfa1 = "    ATENCION: Trabajador " + aAlfa2 + "." + aAlfa3;
                    aAlfa1 = aAlfa1 + " no tiene calculado Mod. 190. Media IRPF del a¤o ";
                    aAlfa1 = aAlfa1 + "anterior inexacta";
                    |MENAV aAlfa1;
               |FINSI;
          |FINSI;

          |SI nTIPO < nRete1_PA;
               nTIPOEst = nTIPO;   || me lo guardo para luego
               nTIPO = nRete1_PA;
               |SI nTIPO < 0;
                    nTIPO = 0;
               |FINSI;
               swTipoMedio = 1;
          |FINSI;
          nIMPORTE = nRETRIB * (nTIPO / 100);
     |FINSI;
|FPRC;

|PROCESO GuardaDatos_06;
     #104#13 = nMINPER;       || Minimo Personal
     #104#12 = nRED46;        || Reducc. Rendimientos del trabajo art.46
     #104#57 = nPROLONLAB;    || Reducc. Prolongacion de la Activ. Laboral
     #104#58 = nMOVILGEO;     || Reducc. Movilidad Geografica
     #104#59 = nDISTRA;       || Reducc. Discapacidad Trabajadores Activos
     #104#60 = nCUIDADO;       || Reducc. Cuidado de Hijos
     #104#61 = n65PER + n65AS;        || Reducc. Edad
     #104#62 = ASISPER + n75AS;   || Reducc. Asistencia
     #104#63 = nDISPER + nDISDES + nDISAS + nASISPER + nASISDES + nASISAS;
               || Reducc. Discapacidad Trabajadores no activos
     #104#64 = nPENSION + nDESEM;  || Reducc. Pensionista-Desempleado
     #104#65 = nHIJOS;        || Reducc. por tener mas de dos descend.
     #104#70 = nREDCOPA;      || Reducc. Copa America

     #104#35 = nBASE;
     #104#25 = nTIPO;
     #104#26 = nIMPORTE;
|FPRC;


|PROCESO Punto05_06;
     |SI nDiferencias = 1;
          #0#0 = aVDNI;
          #0#2 = nVAny;
          #0#3 = nVEmp;
     |FINSI;

     |ABRE #102;
     |ABRE #104;
     #102#0 = #0#0;
     #102#40= #0#2;
     #102#41= #0#3;
     |LEE 000#102=;
     |SI FSalida = 0;
          |SI nDiferencias = 0;
               |PUSHV 0501 2380;
               |PINPA #0#104;
               |HAZ Ordenados001_06;
          |FINSI;
          |DDEFECTO #104;
          #104#0 = #0#0;
          #104#1 = #0#2;
          #104#44= #0#3;
          |LEE 101#104=;
          nEstado = FSalida;
          nGuardaTipo = #104#25;

          |HAZ ConstantesEuro_06;
          |HAZ Inicializa_06;

          |SI nDiferencias = 0; |O enMenav = 1;
               |HAZ SinCalculo_06;
          |SINO;
               |SI nFam00 = 0;
                    |HAZ SinCalculo_06;
                    ||HAZ Descendientes_06;
               |SINO;
                    |HAZ SinCalculo190_06;
                    ||HAZ Descendientes190_06;
               |FINSI;
          |FINSI;
          |HAZ Rendimiento_06;
          |HAZ MinimoPersonal_06;
          |HAZ Reduccion46_06;
          |HAZ ReduccTraAct_06;
          |HAZ ReduCopaAmerica_06;
          |HAZ ReduccionPerceptor_06;
          |HAZ ReduccPenDes_06;
          |HAZ Descendientes_06;
          |HAZ Ascendientes_06;
          |HAZ Resultados_06;
          |HAZ Base_06;
          |HAZ CuotaRetencion_06;
          |HAZ TipoRetencion_06;
          |HAZ Regulariza_06;
          |HAZ Regulariza_Enero_06;
          |HAZ GuardaDatos_06;

          |SI nEstado = 0;    |GRABA 020#104a;    |FINSI;
          |SI nEstado ! 0;    |GRABA 020#104n;    |FINSI;

          |SI nDiferencias = 0;
               |PINDA #0#104;
               |PAUSA;
               |LIBERA #104;
               |POPV;

               |SI nGuardaTipo ! #104#25;|O #0#5 = "N";
                    #0#5 = "N";
                    |GRABA 020#0a;
                    |SI FSalida = 0;
                         |PINTA #0#5;
                    |FINSI;

                    |SI #102#18 = 0;|Y #102#19 = 0;
                         #102#18 = #104#9;        || guarda ingresos iniciales
                         #102#19 = #104#26;       || guarda cuota inicial
                         #102#117 = #104#35;      || guarda base inicial
                         #102#118 = #104#25;      || guarda tipo inicial
                         |GRABA 020#102a;
                    |FINSI;
               |FINSI;
          |SINO;
               |LIBERA #104;
               nCuotNueva = #104#26;
          |FINSI;
     |SINO;
          |SI enMenav = 0;
              |MENAV "    Parametros de calculo inaccesibles ...";
          |FINSI;
     |FINSI;
     |LIBERA #102;
     |CIERRA #102;
     |CIERRA #104;
|FPRC;

|PROCESO Punto05Multi_06;
     |ABRE #102;
     |ABRE #104;
     #102#0 = #0#0;
     #102#40= #0#2;
     #102#41= #0#3;
     |LEE 000#102=;
     |SI FSalida = 0;
          |DDEFECTO #104;
          #104#0 = #0#0;
          #104#1 = #0#2;
          #104#44= #0#3;
          |LEE 101#104=;
          nEstado = FSalida;
          nGuardaTipo = #104#25;

          |HAZ ConstantesEuro_06;
          |HAZ Inicializa_06;

          |HAZ SinCalculo_06;
          |HAZ Rendimiento_06;
          |HAZ MinimoPersonal_06;
          |HAZ Reduccion46_06;
          |HAZ ReduccTraAct_06;
          |HAZ ReduCopaAmerica_06;
          |HAZ ReduccionPerceptor_06;
          |HAZ ReduccPenDes_06;
          |HAZ Descendientes_06;
          |HAZ Ascendientes_06;
          |HAZ Resultados_06;
          |HAZ Base_06;
          |HAZ CuotaRetencion_06;
          |HAZ TipoRetencion_06;
          |HAZ Regulariza_06;
          |HAZ Regulariza_Enero_06;
          |HAZ GuardaDatos_06;

          |SI nEstado = 0;    |GRABA 020#104a;    |FINSI;
          |SI nEstado ! 0;    |GRABA 020#104n;    |FINSI;
          |LIBERA #104;

          |SI nGuardaTipo ! #104#25;|O #0#5 = "N";
               |SI #102#18 = 0;|Y #102#19 = 0;
                    #102#18 = #104#9;        || guarda ingresos iniciales
                    #102#19 = #104#26;       || guarda cuota inicial
                    #102#117 = #104#35;      || guarda base inicial
                    #102#118 = #104#25;      || guarda tipo inicial
                    |GRABA 020#102a;
               |FINSI;
          |FINSI;
     |SINO;
          |MENAV "    Parametros de calculo inaccesibles ...";
     |FINSI;
     |LIBERA #102;
     |CIERRA #102;
     |CIERRA #104;
|FPRC;
