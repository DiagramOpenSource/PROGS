|FICHEROS;
     nominf01 #0;
     labempre #1;
     labtraba #2;
     nomtrtmp #200;
     labregis;
     nomequiv;
     copiadef@ #500;
|FIN;

|VARIABLES;
     &aNif = "";
     aDef = "";
     aAlfa = "";

     &aNom = "";
     &aEmp = "";
     &aTra = "";
     &aFal = "";
     &aFba = "";
     &aFvt = "";
     &aCen = "";
     &aCon = "";
     &aCat = "";
     &aAre = "";
     &aSec = "";
     &aTip = "";
     &aEpi = "";
     &aNomEmp = "";
     &aGru = "";
     &aMot = "";

     aCenAnt = "";
     aConAnt = "";
     aCatAnt = "";
     aAreAnt = "";
     aSecAnt = "";
     aTipAnt = "";
     aEpiAnt = "";
     aGruAnt = "";
     aNifAnt = "";
     &aTotAnt = "";
     &swlin = 0;

     alfa1 = "";
     alfa2 = "";
     alfa3 = "";
     x = "";
     final = "";
     nume1 = 0;
     nume2 = 0;
     nume3 = 0;
     lugar = 0;
     nifnum = 0;
     resto = 0;
     corta = 0;
     letrasnif = "TRWAGMYFPDXBNJZSQVHLCKE";
     letra = "";

     nContrato = 0;
     swSigue = 0;
     nAntEmpresa = 0;

     fAltaTrab = @;
     aAltaTrab = "";
     nAltaTrab = 0;
     fBajaTrab = @;
     aBajaTrab = "";
     nBajaTrab = 0;
     nTotalDias = 0;
     nTotalMeses = 0;
     nDias = 0;

     fDesde = @;
     fHasta = @;
     swHayAlguno = 0;
     fInicio30 = @;

     aFechaAlta = "";
     nFechaAlta = "";
     fFechaBaja = @;
     aFechaBaja = "";
     nFechaBaja = "";
     &aInicio30 = "";

     fFecha = @;
     aFecha = "";
     nDia = 0;
     nMes = 0;
     nAny = 0;
     aDia = "";
     aMes = "";
     aAny = "";
     swGraba = 0;
     fFechaDif = @;
     nMeses = 0;
     nAnys = 0;
     nAntEmp = 0;
     nAntTra = 0;
     aFic = "";
     aPath = "";
|FIN;

/*--------------------------------------------------------------------------*/
/*                         CALCULOS DESDE EL MANTENIMIENTO                  */
/*--------------------------------------------------------------------------*/

|PROCESO Defectos; |TIPO 7;
     fFecha = ~;
     aFecha = fFecha;
     aMes = aFecha % 402;
     aAny = aFecha % -104;
     nMes = aMes;
     nMes = nMes + 1;
     nAny = aAny;
     |SI nMes = 13;
          nMes = 1;
          nAny = nAny + 1;
     |FINSI;
     |SI nMes = 2;
          aDia = "28";
     |FINSI;
     |SI nMes = 1; |O nMes = 3; |O nMes = 5; |O nMes = 7; |O nMes = 8;
     |O nMes = 10; |O nMes = 12;
          aDia = "31";
     |FINSI;
     |SI nMes = 4; |O nMes = 6; |O nMes = 9; |O nMes = 11;
          aDia = "30";
     |FINSI;
     aMes = nMes;
     aAny = nAny;
     /*
     aFecha = "01" + "." + aMes + "." + aAny;
     fFecha = aFecha;
     #0#4 = aFecha;
     |PINTA #0#4;
     */
     aFecha = aDia + "." + aMes + "." + aAny;
     fFecha = aFecha;
     #0#5 = aFecha;
     |PINTA #0#5;
|FPRC;

/*--------------------------------------------------------------------------*/
/*                         CALCULOS DEL PROCESO                             */
/*--------------------------------------------------------------------------*/

|PROCESO imptraba;
     |SI aNifAnt ! ""; |Y aNifAnt ! #200#3;
          swlin = 1;
          |PRINT;
          swlin = 0;
     |SINO;
          swlin = 0;
     |FINSI;

     aEmp = #200#0;  aEmp = "00000" + aEmp; aEmp = aEmp % -105;
     aTra = #200#1;  aTra = "00000" + aTra; aTra = aTra % -105;
     aFal = #200#4;
     aFba = #200#5;
     aFvt = #200#16;
     aCen = #200#6; aCen = "00" + aCen;    aCen = aCen % -102;
     aCon = #200#7; aCon = "000" + aCon;   aCon = aCon % -103;
     aCat = #200#8; aCat = "000" + aCat;    aCat = aCat % -103;
     aAre = #200#9;
     aSec = #200#10;
     aTip = #200#11; aTip = "000" + aTip;   aTip = aTip % -103;
     aEpi = #200#12; aEpi = "000" + aEpi;   aEpi = aEpi % -103;
     aNom = #200#2;
     aNif = #200#3;
     aNomEmp = #200#13;
     aGru = #200#14; aGru = "00" + aGru;    aGru = aGru % -102;
     aMot = #200#15 % 116;
     |SI aMot = "Motivo de baja: ";
          aMot = #200#15 % 1783;
     |SINO;
          aMot = #200#15;
     |FINSI;
     aAlfa = aCenAnt + aConAnt + aCatAnt + aAreAnt + aSecAnt + aTipAnt + aEpiAnt + aGruAnt;
     |QBF aAlfa;
     |SI aAlfa ! "";
          |SI aCenAnt ! aCen; aCen = aCen + "(*)"; |FINSI;
          |SI aConAnt ! aCon; aCon = aCon + "(*)"; |FINSI;
          |SI aCatAnt ! aCat; aCat = aCat + "(*)"; |FINSI;
          |SI aAreAnt ! aAre; aSec = aSec + "(*)"; |FINSI;
          |SI aSecAnt ! aSec; aSec = aSec + "(*)"; |FINSI;
          |SI aTipAnt ! aTip; aTip = aTip + "(*)"; |FINSI;
          |SI aEpiAnt ! aEpi; aEpi = aEpi + "(*)"; |FINSI;
          |SI aGruAnt ! aGru; aGru = aGru + "(*)"; |FINSI;
     |FINSI;
     |PRINT;
     aCenAnt = aCen % 102;
     aConAnt = aCon % 103;
     aCatAnt = aCat % 103;
     aAreAnt = aAre % 104;
     aSecAnt = aSec % 104;
     aTipAnt = aTip % 103;
     aEpiAnt = aEpi % 103;
     aGruAnt = aGru % 102;

     aNifAnt = #200#3;
     aTotAnt = #200#18;
     aInicio30 = #200#19;
|FPRC;

|DEFBUCLE imptraba;
     #nomtrtmp#2;
     ;
     INICIO;
     FINAL;
     ;
     NULL, imptraba;
|FIN;

|PROCESO GrabaTraba;
     #labempre#0 = #labtraba#0;
     |LEE 000#labempre.=;

     aAlfa = #labtraba#36;
     |QBF aAlfa;
     |SI aAlfa ! ""; |Y aAlfa ! "..........";
          #200#4 = #labtraba#36;   || fecha alta
     |SINO;
          #200#4 = "00.00.0000";
     |FINSI;
     #200#0 = #labtraba#0;    || empresa
     #200#1 = #labtraba#1;    || trabajador
     #200#2 = #labtraba#2;    || nombre
     #200#3 = #labtraba#7;    || nif
     #200#5 = #labtraba#37;   || fecha baja
     #200#16 = #labtraba#177;   || fecha vto. contrato
     #200#6 = #labtraba#77;   || centro
     #200#7 = #labtraba#87;   || convenio
     #200#8 = #labtraba#17;   || categoria
     #200#9 = #labtraba#62;   || area
     #200#10 = #labtraba#63;  || seccion
     #200#11 = #labtraba#45;  || contrato
     #200#12 = #labtraba#32;  || epigrafe

     #200#13 = #labempre#2;   || empresa
     #200#14 = #labtraba#33;  || grupo de tarifa
     #200#15 = #labtraba#184; || motivo de la baja

     alfa1 = nDias;
     alfa2 = nMeses;
     #200#17 = alfa2 + " meses, " + alfa1 + " dias";
     alfa1 = nTotalDias;
     alfa2 = nTotalMeses;
     #200#18 = alfa2 + " meses, " + alfa1 + " dias";
     #200#19 = fInicio30;
     |GRABA 020#200n;
|FPRC;

|DEFBUCLE GrabaTraba;
     #labtraba#2;
     ;
     aNif;
     aNif;
     ;
     NULL, GrabaTraba;
|FIN;

|PROCESO CompTraba;
     #nomequiv#0 = #labtraba#45;
     |LEE 000#nomequiv.=;
     |SI FSalida = 0;
          nContrato = #nomequiv#1;
     |SINO;
          nContrato = #labtraba#45;
     |FINSI;
     |SI nContrato < 400;          || es un contrato temporal: adelante
          |ACABA;
     |FINSI;

     || si cambio de empresa ya no vale
     ||SI nAntEmpresa ! 0; |Y nAntEmpresa ! #labtraba#0;
     |SI #labtraba#0 ! #500#0;
          |ACABA;
     |FINSI;

     || empezamos a contar dias trabajados en contato temporal en los
     || ultimos 30 meses

     aAltaTrab = #labtraba#36;
     aBajaTrab = #labtraba#37;

     fAltaTrab = aAltaTrab;
     |QBF aBajaTrab;
     |SI aBajaTrab ! ""; |Y aBajaTrab ! "..........";
          fBajaTrab = aBajaTrab;
     |SINO;
          fBajaTrab = fFechaBaja;
     |FINSI;

     nDias = 0;

     |SI fBajaTrab < fInicio30;
          |ACABA;
     |FINSI;
     |SI fAltaTrab < fInicio30;
          fAltaTrab = fInicio30;
     |FINSI;

     nAltaTrab = fAltaTrab;
     nBajaTrab = fBajaTrab;

     || nDias = nBajaTrab - nAltaTrab;

     fFechaDif = fBajaTrab - fAltaTrab;
     aFecha = fFechaDif;
     alfa1 = aFecha % 102;
     alfa2 = aFecha % 402;
     alfa3 = aFecha % -104;
     nDias = alfa1;
     nMeses = alfa2;
     nAnys = alfa3;
     |SI nDias ] 30;
          nDias = nDias - 30;
          nMeses = nMeses + 1;
     |FINSI;
     nMeses = nMeses + (nAnys * 12);

     nAntEmpresa = #labtraba#0;

     |SI swGraba = 1;
          |HAZ GrabaTraba;
     |SINO;
          nTotalDias = nTotalDias + nDias;
          nTotalMeses = nTotalMeses + nMeses;
          |SI nTotalDias ] 30;
               nTotalMeses = nTotalMeses + 1;
               nTotalDias = nTotalDias - 30;
          |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE CompTraba;
     #labtraba#2;
     ;
     aNif;
     aNif;
     ;
     NULL, CompTraba;
|FIN;

|PROCESO labtraba;
     alfa1 = #500#0; alfa1 = ("00000" + alfa1) % -105;
     alfa2 = #500#1; alfa2 = ("00000" + alfa2) % -105;
     alfa3 = alfa1 + "." + alfa2;
     |PINTA 2134, alfa3;

     #nomtrtmp#4 = #500#36;
     #nomtrtmp#0 = #500#0;
     #nomtrtmp#1 = #500#1;
     |LEE 000#nomtrtmp.=;
     |SI FSalida = 0;    || ya me he ocupado de este trabajador
          |ACABA;
     |FINSI;

     |SI #500#1 < nAntTra; |Y #500#0 = nAntEmp;
          |ERROR10;
     |FINSI;

     swSigue = 0;

     aFechaBaja = #500#37;
     |QBF aFechaBaja;
     |SI aFechaBaja ! ""; |Y aFechaBaja ! "..........";
          fFechaBaja = aFechaBaja;
     |SINO;
          |SELECT #2#labregis;
          #labregis#1 = #500#0;
          #labregis#2 = #500#1;
          |LEE 000#labregis.=;
          |SI FSalida = 0;
               aFechaBaja = #labregis#20;
               |QBF aFechaBaja;
               |SI aFechaBaja ! ""; |Y aFechaBaja ! "..........";
                    fFechaBaja = aFechaBaja;
               |SINO;
                    aFechaBaja = #labregis#8;
                    |QBF aFechaBaja;
                    |SI aFechaBaja ! ""; |Y aFechaBaja ! "..........";
                         fFechaBaja = aFechaBaja;
                    |FINSI;
               |FINSI;
          |FINSI;
     |FINSI;

     |SI aFechaBaja ! ""; |Y aFechaBaja ! "..........";
          ||SI #0#4 [ fFechaBaja; |Y fFechaBaja [ #0#5;
          |SI fFechaBaja [ #0#5;
               swSigue = 1
          |FINSI;
     |FINSI;

     || Si swSigue = 1 es porque el trabajador finaliza contrato entre las
     || fechas especificadas

     |SI swSigue = 1;
          aNif = #500#7;

          aFecha = fFechaBaja;
          aDia = aFecha % 102;
          aMes = aFecha % 402;
          aAny = aFecha % -104;

          nMes = aMes;
          nMes = nMes - 6;
          nAny = aAny;
          nAny = nAny - 2;
          |SI nMes < 0;
               nMes = 12 + nMes;
               nAny = nAny - 1;
          |FINSI;
          aDia = ("00" + aDia) % -102;
          aMes = nMes;
          aMes = ("00" + aMes) % -102;
          aAny = nAny;
          aFecha = aDia + "." + aMes + "." + aAny;

          fInicio30 = aFecha;

          ||fDesde = #0#4;
          fHasta = #0#5;

          nTotalDias = 0;
          nTotalMeses = 0;
          swGraba = 0;
          nAntEmpresa = 0;
          |BUCLE CompTraba;

          |SI nTotalMeses ] 24;
               swHayAlguno = 1;
               swGraba = 1;
               nAntEmpresa = 0;
               |BUCLE CompTraba;
          |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE labtraba;
     #500#1002;
     44;
     #0#0, #0#2, "A";
     #0#1, #0#3, "A";
     ;
     NULL, labtraba;
|FINSI;

|PROGRAMA;
     |CLS;
     |PINPA #0#0;
     |PINDA #0#0;
     |ENDATOS #1#0;
     |SI FSalida = 0;
          aAlfa = Usuario;  |QBF aAlfa;    aAlfa = "nh" + aAlfa;
          |NOME_DAT #200 aAlfa;         || tmp contador de trabajadores
          |DELINDEX #200;   || Se carga el temporal que pudiera existir
          |ABRE #labtraba;
          |ABRE #labempre;
          |ABRE #labregis;
          |ABRE #nomequiv;
          |ABRE #200;

          |DBASE aPath;
          aDef = aPath + "/def/labtraba";
          aFic = aPath + "/fich/";
          |CARGA_DEF aDef, copiadef@;

          |BUCLE labtraba;

          |DESCARGA_DEF #copiadef@;

          |CIERRA #200;
          |CIERRA #nomequiv;
          |CIERRA #labregis;
          |CIERRA #labtraba;
          |CIERRA #labempre;

          |SI swHayAlguno = 0;
               |MENAV "    No se han encontrado trabajadores afectados";
          |SINO;
               |IMPRE 0;
               |SI FSalida = 0;
                    |INFOR "nominf01";

                    |BUCLE imptraba;
                    swlin = 1;
                    |PRINT;

                    |PIEINF;
                    |FININF;
               |FINSI;
               |FINIMP;
          |FINSI;
     |FINSI;
|FPRO;
