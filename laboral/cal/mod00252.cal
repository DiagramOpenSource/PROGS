|FICHEROS;
    mod00252 #0;
    labempre #1;
    labtraba #2;         || trabajadores
    eosm0030 #30;
    eosm0031 #31;
    eosm0032 #32;
    eosm0035;
    nomfusli #140;
|FIN;

|VARIABLES;
    alfa1 = "";
    alfa2 = "";
    alfa3 = "";
    alfa4 = "";
    nume1 = 0;
    nume2 = 0;
    nume3 = 0;
    nume4 = 0;
    para1 = 0;
    para2 = 0;
    para3 = 0;
    para4 = 0;
    fech1 = @;
    fech2 = @;
    fech3 = @;
    fech4 = @;
    elsw1 = 0;
    elsw2 = 0;
    elsw3 = 0;
    elsw4 = 0;
    FEstado = 0;
    seleccio = 0;
    modo = 0;
    sw_rup = 0;

    p_mes = "";
    fabre = "";
    field = "";
    field2 = "";
    fcanl = "";
    longi = 0;
    puntero = 0;
    campo0 = "";
    campo1 = "";
    campo2 = "";
    campo3 = "";
    campo4 = "";
    campo5 = "";
    campo6 = "";
    campo7 = "";
    campo8 = "";
    campo9 = "";
    campo10= "";
    campo11= "";
    campo12= "";
    campo13= "";
    cuantos = 0;
    pumes = 0;

     fFech1 = @;
     aAlfa1 = "";
     nEmpresa = 0;
     aClave = "";
     aSubClave = "";
     nTipo = 0;
     nMes = 0;
     nBase = 0;
     nPorc = 0;
     nCuot = 0;
     nEspe = 0;
     nPorE = 0;
     nCuoE = 0;
     nGtos = 0;
     nRedu = 0;
     nPens = 0;
     nAnua = 0;
     aEspe = "";
     aRepe = "";
     nExento = 0;

     aAlfa = "";
     nHandle = 0;
     aOrigen = "";
     aLon = "";
     nLon = 0;
     nPos = 0;
     nPos1 = 0;
     nNume = 0;
     aSoloFichero = "";
     aCaracter = "";
|FIN;
____________________________________/ CALCULOS DESDE CAMPOS
|CALCULO pintames; |TIPO 0;      || PINTA EL MES LITERAL
|SI #0Campo =  1;  p_mes = "ENERO     ";  |FINSI;
|SI #0Campo =  2;  p_mes = "FEBRERO   ";  |FINSI;
|SI #0Campo =  3;  p_mes = "MARZO     ";  |FINSI;
|SI #0Campo =  4;  p_mes = "ABRIL     ";  |FINSI;
|SI #0Campo =  5;  p_mes = "MAYO      ";  |FINSI;
|SI #0Campo =  6;  p_mes = "JUNIO     ";  |FINSI;
|SI #0Campo =  7;  p_mes = "JULIO     ";  |FINSI;
|SI #0Campo =  8;  p_mes = "AGOSTO    ";  |FINSI;
|SI #0Campo =  9;  p_mes = "SEPTIEMBRE";  |FINSI;
|SI #0Campo = 10;  p_mes = "OCTUBRE   ";  |FINSI;
|SI #0Campo = 11;  p_mes = "NOVIEMBRE ";  |FINSI;
|SI #0Campo = 12;  p_mes = "DICIEMBRE ";  |FINSI;
|ATRI I;
|PINTA 1231, p_mes;
|ATRI 0;
|FCAL;

|CALCULO mirames; |TIPO 7; ||CALCULA EL MES POR DEFECTO SEGUN FECHA SISTEMA
    fech1 = ~;
    alfa1 = fech1;
    alfa1 = alfa1 % 402;
    #0Campo = alfa1;
|PINTA #0Campo;
|FCAL;

|PROCESO DefectoMenos1; |TIPO 7;
     fFech1 = ~;
     aAlfa1 = fFech1;
     aAlfa1 = aAlfa1 % -104;
     #0Campo = aAlfa1;
     #0Campo = #0Campo - 1;
     |PINTA #0Campo;
|FPRC;

|PROCESO QuitaK; |TIPO 7;
     |SI #0Campo = "K";
          #0Campo = "H";
          #0#6 = "01";
          |PINTA #0Campo;
          |PINTA #0#6;
     |FINSI;
|FPRC;

|PROCESO Browse;
     |SI FSalida = 10;
          aAlfa = "F"; |BROWSE aAlfa;
          aAlfa = aAlfa % 0299; |QBF aAlfa;
          |SI aAlfa ! "";
               #0#1 = aAlfa; |PINTA #0#1;
          |FINSI;
          |ERROR;
          |ACABA;
     |FINSI;

     aAlfa = #0#1; |QBF aAlfa;
     |XABRE "EC", aAlfa, nHandle;
     |SI FSalida < 0;
          |MENAV "    No encuentro el fichero"; |ERROR;
     |FINSI;
     |XCIERRA nHandle;
|FPRC;

____________________________________/

|PROCESO Graba_eosm0032;
     |SI aClave = " ";             |ACABA;   |FINSI;
     |SI nBase = 0;|Y nCuot = 0;
          |ACABA;
     |FINSI;

     nEspe = 0;
     nCuoE = 0;
     |SI aEspe = "S";
          nEspe = nBase; nBase = 0;
          nCuoE = nCuot; nCuot = 0;
     |FINSI;

     nTipo = 0;
     |SI aClave = "A";
          |SI #labtraba#111 = 0;   #labtraba#111 = 1;  |FINSI;
          nTipo = #labtraba#111;
     |FINSI;

     |DDEFECTO #eosm0032;
     #eosm0032#0 = nEmpresa;       || empresa
     #eosm0032#1 = #0#5;           || a¤o
     #eosm0032#2 = #labtraba#7;    || nif
     #eosm0032#3 = aClave;         || clave
     #eosm0032#4 = aSubClave;      || subclave
     #eosm0032#5 = nTipo;          || tipo relacion
     #eosm0032#6 = nMes;           || mes
     |LEE 101#eosm0032.=;
     |SI FSalida ! 0;
          |GRABA 020#eosm0032.b;
     |FINSI;

     |SI #0#7 = "S";                              || INICIALIZA...
          #eosm0032#7 = nBase;                    || base
          #eosm0032#9 = nCuot;                    || cuota
          #eosm0032#10 = nEspe;                   || en especie
          #eosm0032#12 = nCuoE;                   || cuota en especie
          #eosm0032#15 = nGtos;                   || gastos 17.3 (seg.soc.)
     |SINO;                                       || ACUMULA...
          #eosm0032#7 = #eosm0032#7 + nBase;      || base
          #eosm0032#9 = #eosm0032#9 + nCuot;      || cuota
          #eosm0032#10 = #eosm0032#10 + nEspe;    || en especie
          #eosm0032#12 = #eosm0032#12 + nCuoE;    || cuota en especie
          #eosm0032#15 = #eosm0032#15 + nGtos;    || gastos 17.3 (seg.soc.)
     |FINSI;

     #eosm0032#8 = (#eosm0032#9 * 100) / #eosm0032#7;       || (%) dinerarias
     #eosm0032#11 = (#eosm0032#12 * 100) / #eosm0032#10;    || (%) en especie
     #eosm0032#13 = aRepe;                   || repercute

     nGtos = 0;     || la seguridad social la actualiza con la primera clave
                    || que llega

     |GRABA 020#eosm0032.a;
     |LIBERA #eosm0032;
|FPRC;

|PROCESO Graba_eosm0035;
     |SI aClave = " ";             |ACABA;   |FINSI;
     |SI nBase = 0;|Y nCuot = 0;
          |ACABA;
     |FINSI;

     nEspe = 0;
     nCuoE = 0;
     |SI aEspe = "S";
          nEspe = nBase; nBase = 0;
          nCuoE = nCuot; nCuot = 0;
     |FINSI;

     nTipo = 0;
     |SI aClave = "A";
          |SI #labtraba#111 = 0;   #labtraba#111 = 1;  |FINSI;
          nTipo      = #labtraba#111;
          aSubClave = "";
     |FINSI;

     |DDEFECTO #eosm0035;
     #eosm0035#0 = nEmpresa;       || empresa
     #eosm0035#1 = #0#5;           || a¤o
     #eosm0035#2 = #labtraba#7;    || nif
     #eosm0035#3 = aClave;         || clave
     #eosm0035#4 = aSubClave;      || subclave
     #eosm0035#5 = nTipo;          || tipo relacion
     #eosm0035#6 = nMes;           || mes
     |LEE 101#eosm0035.=;
     |SI FSalida ! 0;
          |GRABA 020#eosm0035.b;
     |FINSI;

     |SI #0#7 = "S";                              || INICIALIZA...
          #eosm0035#7 = nBase;                    || base
          #eosm0035#9 = nCuot;                    || cuota
          #eosm0035#10 = nEspe;                   || en especie
          #eosm0035#12 = nCuoE;                   || cuota en especie
          #eosm0035#15 = nGtos;                   || gastos 17.3 (seg.soc.)
     |SINO;                                       || ACUMULA...
          #eosm0035#7 = #eosm0035#7 + nBase;      || base
          #eosm0035#9 = #eosm0035#9 + nCuot;      || cuota
          #eosm0035#10 = #eosm0035#10 + nEspe;    || en especie
          #eosm0035#12 = #eosm0035#12 + nCuoE;    || cuota en especie
          #eosm0035#15 = #eosm0035#15 + nGtos;    || gastos 17.3 (seg.soc.)
     |FINSI;

     #eosm0035#8 = (#eosm0035#9 * 100) / #eosm0035#7;       || (%) dinerarias
     #eosm0035#11 = (#eosm0035#12 * 100) / #eosm0035#10;    || (%) en especie
     #eosm0035#13 = aRepe;                   || repercute

     nGtos = 0;     || la seguridad social la actualiza con la primera clave
                    || que llega

     |GRABA 020#eosm0035.a;
     |LIBERA #eosm0035;
|FPRC;

|PROCESO Graba_eosm0031;
     |DDEFECTO #eosm0031;
     #eosm0031#0 = nEmpresa;
     #eosm0031#1 = #0#5;
     #eosm0031#2 = #labtraba#7;
     |LEE 101#eosm0031.=;
     |SI FSalida ! 0;
          #eosm0031#3 = #labtraba#2;
          |GRABA 020#eosm0031.n;
     |SINO;
          #eosm0031#3 = #labtraba#2;
          |GRABA 020#eosm0031.a;
     |FINSI;
     |LIBERA #eosm0031;
|FPRC;

|PROCESO Graba_eosm0030;
     |DDEFECTO #eosm0030;
     #eosm0030#0 = nEmpresa;
     #eosm0030#1 = #0#5;
     |LEE 101#eosm0030.=;
     |SI FSalida ! 0;
          #eosm0030#2 = #labempre#2;         || nombre
          #eosm0030#15 = #labempre#15;       || persona de contacto
          #eosm0030#16 = #labempre#111;      || telefono
          #eosm0030#17 = #labempre#15;       || representante
          #eosm0030#18 = #labempre#18;       || cargo
          |GRABA 020#eosm0030.n;
     |SINO;
          #eosm0030#2 = #labempre#2;         || nombre
          #eosm0030#17 = #labempre#15;       || representante
          #eosm0030#18 = #labempre#18;       || cargo
          |GRABA 020#eosm0030.a;
     |FINSI;
     |LIBERA #eosm0030;
|FPRC;

|PROCESO LeeTodo;
     |DDEFECTO #labempre;
     #labempre#0 = #0#2;
     |LEE 000#labempre.=;

     nEmpresa = #labempre#0;
     #nomfusli#1 = nEmpresa;
     #nomfusli#0 = 0;
     |LEE 000#nomfusli.];
     |SI FSalida = 0;|Y #nomfusli#1 = nEmpresa;
          nEmpresa = #nomfusli#0;
     |FINSI;
|FPRC;

|PROCESO grabalo;
|DDEFECTO #2;
    #2#0 = #0#2;
    #2#1 = campo1;
|LEE 101#2=;
    FEstado = FSalida;

|DEFECTO #2#1;
    #2#2 = campo3 + ", " + campo4;      || apellidos y nombre
    #2#7 = campo2;                      || dni
    #2#3 = campo5 + " " + campo6;       || SG y domicilio
    #2#6 = campo8;                      || localidad
    alfa1= campo7 % 102;
    #2#8 = alfa1;                       || cod.postal 1
    alfa1= campo7 % -103;
    #2#9 = alfa1;                       || cod.postal 2
    #2#10= campo9;                      || provincia
    #2#30= campo11;                     || (%) IRPF

    #2#99 = #0#3;   || clave perceptora
    #2#102 = #0#6;  || subclave perceptora

|SI FEstado = 0;    |GRABA 020#2a; |FINSI;
|SI FEstado ! 0;    |GRABA 020#2n; |FINSI;
|SI FSalida = 0;
         cuantos = cuantos + 1;
    |ATRI I;
    |PINTA 2033, #2#0;
    |PINTA 2039, #2#1;
    |PINTA 2134, cuantos;
    |ATRI 0;

        |HAZ LeeTodo;
        nBase = campo10;
        nCuot = campo12;
        aEspe = "N";
        aRepe = "N";
        aClave    = #0#3;
        aSubClave = #0#6;
        nMes = #0#4;
        |HAZ Graba_eosm0030;
        |HAZ Graba_eosm0031;

        |SI #0#5 [ 2015;
            |HAZ Graba_eosm0032;
        |FINSI;

        |SI #0#5 ] 2016;
            |HAZ Graba_eosm0035;
        |FINSI;
|FINSI;
|LIBERA #2;
|FPRC;

|PROCESO separa2;
|SI puntero = 1; campo1 = campo0;    |FINSI;   || trabajador
|SI puntero = 2; campo2 = campo0;    |FINSI;   || dni
|SI puntero = 3; campo3 = campo0;    |FINSI;   || apellidos
|SI puntero = 4; campo4 = campo0;    |FINSI;   || nombre
|SI puntero = 5; campo5 = campo0;    |FINSI;   || SG
|SI puntero = 6; campo6 = campo0;    |FINSI;   || domicilio
|SI puntero = 7; campo7 = campo0;    |FINSI;   || cod.postal
|SI puntero = 8; campo8 = campo0;    |FINSI;   || localidad
|SI puntero = 9; campo9 = campo0;    |FINSI;   || provincia
|SI puntero =10; campo10= campo0;    |FINSI;   || base
|SI puntero =11; campo11= campo0;    |FINSI;   || (%)
|SI puntero =12; campo12= campo0;    |FINSI;   || retencion
|SI puntero =13; campo13= campo0;    |FINSI;   || ejercicio devengo
|SI puntero > 13;
    |MENAV "    Campos del secuencial no configurados; desechados ...";
|FINSI;
    campo0 = "";
    puntero = puntero + 1;
|FPRC;

|PROCESO separa;
    alfa1 = field % 0;
    longi = alfa1;

    puntero = 1;
    campo0 = "";    campo1 = "";   campo2 = "";   campo3 = "";
    campo4 = "";    campo5 = "";   campo6 = "";   campo7 = "";
    campo8 = "";    campo9 = "";   campo10= "";   campo11= "";
    campo12= "";    campo13= "";

|PARA para1 = 1; |SI para1 [ longi; |HACIENDO para1 = para1 + 1;
        nume1 = (para1 * 100) + 1;
        alfa1 = field % nume1;
    |SI alfa1 = ",";
        |HAZ separa2;
    |SINO;
            campo0 = campo0 + alfa1;
    |FINSI;
|SIGUE;
|HAZ separa2;      || como NO termina con una ',' hay que buscar mas
|FPRC;

|PROCESO tipo3;
     |ABRET #labempre;
     |ABRET #labtraba;
     |ABRET #eosm0030;
     |ABRET #eosm0031;
     |ABRET #eosm0032;
     |ABRET #eosm0035;
     |ABRET #nomfusli;
     |SELECT #2#nomfusli;
|DEBUG;
     alfa1 = #0#1;
     |QBF alfa1;
     aOrigen = alfa1;
     aLon = alfa1 % 0;
     nLon = aLon;

     |DBASE aAlfa;
     |QBF aAlfa;
     aAlfa = aAlfa + "tmp/";

     aLon = alfa1 % 0;
     nLon = aLon;

     aSoloFichero = "";
     |PARA nPos = nLon; |SI nPos ] 1; |HACIENDO nPos = nPos - 1;
          nPos1 = (nPos * 100) + 1;
          aCaracter = alfa1 % nPos1;
          |SI aCaracter = "\"; |O aCaracter = "/";
               |SAL;
          |SINO;
               aSoloFichero = aCaracter + aSoloFichero;
          |FINSI;
     |SIGUE;

     alfa1 = aAlfa + aSoloFichero;

     |RECIBE_FICHERO aOrigen, alfa1;

     fabre = "rb  " + alfa1;
     |FABRE fabre;
     |SI FSalida ] 0;
          fcanl = FSalida;
          field2 = "";
          field = fcanl + " 1"; |FLEE field;
          |PARA; |SI FSalida > 0; |HACIENDO;
               nume1 = &field;
               |SI nume1 ! 10;
                    field2 = field2 + field;
               |SINO;
                    field = field2;
                    field2 = "";
                    |HAZ separa;
                    |HAZ grabalo;
               |FINSI;
               field = fcanl + " 1";  |FLEE field;
          |SIGUE;
          |FCIERRA fcanl;
     |SINO;
          alfa1 = "    Fichero [" + (fabre % 5) + "] inaccesible para lectura ...";
          |MENAV alfa1;
     |FINSI;

        |CIERRAT #labtraba;
        |CIERRAT #labempre;
        |CIERRAT #eosm0030;
        |CIERRAT #eosm0031;
        |CIERRAT #eosm0032;
        |CIERRAT #eosm0035;
        |CIERRAT #nomfusli;
|FPRC;

|PROGRAMA;
|CLS;
|CABEZA "Importacion Socios";
|DDEFECTO #0;
|PINPA #0#0;
|ABRE #0;
|LEE 101#0p;
|SI FSalida ! 0;    |GRABA 020#0b; |FINSI;
|PINDA #0#0;
|ENDATOS #1#0;
|SI FSalida = 0;
    |HAZ tipo3;
        alfa1 = "    Importados " + cuantos + " registros ...";
    |MENAV alfa1;
    |GRABA 020#0a;
|FINSI;
|LIBERA #0;
|CIERRA #0;
|FPRO;
