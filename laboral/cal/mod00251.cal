|FICHEROS;
    mod00251 #0;
    labempre #1;         || empresas
    labtraba #2;         || trabajadores
    nomconli #3;         || convenios lins.
    nomvarca #4;         || variaciones cabs.
    nomvarli #5;         || variaciones lins.
    mod00255 #6;         || planillas lins.
|FIN;

|VARIABLES;
    alfa1 = "";
    alfa2 = "";
    alfa3 = "";
    alfa4 = "";
    nume1 = 0;
    nume2 = 0;
    nume3 = 0;
    nume4 = 0;
    para1 = 0;
    para2 = 0;
    para3 = 0;
    para4 = 0;
    fech1 = @;
    fech2 = @;
    fech3 = @;
    fech4 = @;
    elsw1 = 0;
    elsw2 = 0;
    elsw3 = 0;
    elsw4 = 0;
    FEstado = 0;
    seleccio = 0;
    modo = 0;
    sw_rup = 0;

    p_mes = "";
    fabre = "";
    field = "";
    field2 = "";
    fcanl = "";
    longi = 0;
    puntero = 0;
    campo0 = "";
    campo1 = "";
    campo2 = "";
    campo3 = "";
    campo4 = "";
    campo5 = "";
    cuantos = 0;
    cod_tra = 0;
    cto_imp = 0;
    impor = 0;

     aAlfa = "";
     nHandle = 0;
     aOrigen = "";
     aLon = "";
     nLon = 0;
     nPos = 0;
     nPos1 = 0;
     nNume = 0;
     aSoloFichero = "";
     aCaracter = "";
|FIN;
____________________________________/ CALCULOS DESDE CAMPOS
|CALCULO pintames; |TIPO 0;      || PINTA EL MES LITERAL
|SI #0Campo =  1;  p_mes = "ENERO     ";  |FINSI;
|SI #0Campo =  2;  p_mes = "FEBRERO   ";  |FINSI;
|SI #0Campo =  3;  p_mes = "MARZO     ";  |FINSI;
|SI #0Campo =  4;  p_mes = "ABRIL     ";  |FINSI;
|SI #0Campo =  5;  p_mes = "MAYO      ";  |FINSI;
|SI #0Campo =  6;  p_mes = "JUNIO     ";  |FINSI;
|SI #0Campo =  7;  p_mes = "JULIO     ";  |FINSI;
|SI #0Campo =  8;  p_mes = "AGOSTO    ";  |FINSI;
|SI #0Campo =  9;  p_mes = "SEPTIEMBRE";  |FINSI;
|SI #0Campo = 10;  p_mes = "OCTUBRE   ";  |FINSI;
|SI #0Campo = 11;  p_mes = "NOVIEMBRE ";  |FINSI;
|SI #0Campo = 12;  p_mes = "DICIEMBRE ";  |FINSI;
|ATRI I;
|PINTA 1531, p_mes;
|ATRI 0;
|FCAL;

|CALCULO mirames; |TIPO 7; ||CALCULA EL MES POR DEFECTO SEGUN FECHA SISTEMA
    fech1 = ~;
    alfa1 = fech1;
    alfa1 = alfa1 % 402;
    #0Campo = alfa1;
|PINTA #0Campo;
|FCAL;

|PROCESO Browse;
     |SI FSalida = 10;
          aAlfa = "F"; |BROWSE aAlfa;
          aAlfa = aAlfa % 0299; |QBF aAlfa;
          |SI aAlfa ! "";
               #0#1 = aAlfa; |PINTA #0#1;
          |FINSI;
          |ERROR;
          |ACABA;
     |FINSI;

     aAlfa = #0#1; |QBF aAlfa;
     |XABRE "EC", aAlfa, nHandle;
     |SI FSalida < 0;
          |MENAV "    No encuentro el fichero"; |ERROR;
     |FINSI;
     |XCIERRA nHandle;
|FPRC;

|PROCESO lee_concepto; |TIPO 0;
|DDEFECTO #1;
|ABRE #1;
    #1#0 = #0#2;
|LEE 000#1=;
|CIERRA #1;

|DDEFECTO #3;
|ABRE #3;
    #3#0 = #1#1;
    #3#2 = #0Campo;
|LEE 000#3=;
|SI FSalida = 0;
    |ATRI I;
    |SI Campo = 3;  |PINTA 1231, #3#6;  |FINSI;
    |SI Campo = 5;  |PINTA 1331, #3#6;  |FINSI;
    |SI Campo = 6;  |PINTA 1431, #3#6;  |FINSI;
    |ATRI 0;
|SINO;
        alfa1 = ":CONCEPTO SIN DEFINIR EN EL CONVENIO " + #1#1;
    |ATRI I;
    |SI Campo = 3;  |PINTA 1231, alfa1; |FINSI;
    |SI Campo = 5;  |PINTA 1331, alfa1; |FINSI;
    |SI Campo = 6;  |PINTA 1431, alfa1; |FINSI;
    |ATRI 0;
|FINSI;
|CIERRA #3;
|FPRC;
____________________________________/
|PROCESO gra_impor;
|DDEFECTO #5;
    #5#0 = #4#0;
    #5#1 = #4#1;
    #5#2 = #4#2;
    #5#3 = #4#3;
    #5#4 = cto_imp;
|LEE 101#5=;
    FEstado = FSalida;
    #5#5 = "         1";
    alfa1 = impor;  alfa1 = ("          " + alfa1) % -110;
    #5#6 = alfa1;
|SI FEstado = 0; |GRABA 020#5a; |FINSI;
|SI FEstado ! 0; |GRABA 020#5n; |FINSI;
|LIBERA #5;
|FPRC;

|PROCESO gra_lin;
|DDEFECTO #5;
    #5#0 = #4#0;
    #5#1 = #4#1;
    #5#2 = #4#2;
    #5#3 = #4#3;
    #5#4 = 903;
|LEE 101#5=;
    FEstado = FSalida;
    alfa1 = campo2; alfa1 = ("          " + alfa1) % -110;
    #5#5 = alfa1;
    #5#6 = "Jornadas";
|SI FEstado = 0; |GRABA 020#5a; |FINSI;
|SI FEstado ! 0; |GRABA 020#5n; |FINSI;
|LIBERA #5;

    cto_imp = #0#3; impor = campo3;     |HAZ gra_impor;     || importe 1
    cto_imp = #0#5; impor = campo4;     |HAZ gra_impor;     || importe 2
    cto_imp = #0#6; impor = campo5;     |HAZ gra_impor;     || importe 3

|ATRI I;
|PINTA 2034, #5#0;
|PINTA 2039, #5#1;
|ATRI 0;

    cuantos = cuantos + 1;
|FPRC;

|PROCESO gra_cab;
|DDEFECTO #4;
    #4#0 = #0#2;    || empresa
    #4#1 = cod_tra; || trabajador
    #4#2 = #0#4;    || mes
    #4#3 = 0;       || tipo
|LEE 000#4=;
|SI FSalida ! 0;
    |GRABA 020#4n;
|FINSI;
|FPRC;

|PROCESO planilla;
    #6#0 = #0#2;    || empresa
    #6#2 = campo1;  || codigo GESTION
|LEE 000#6=;
|SI FSalida = 0;
        cod_tra = #6#1;       || codigo AGINOM equivalente
|SINO;
        cod_tra = campo1;     || codigo AGINOM=codigo GESTION
|FINSI;
|FPRC;

|PROCESO separa2;
|SI puntero = 1; campo1 = campo0;    |FINSI;   || trabajador
|SI puntero = 2; campo2 = campo0;    |FINSI;   || jornadas
|SI puntero = 3; campo3 = campo0;    |FINSI;   || importe 1
|SI puntero = 4; campo4 = campo0;    |FINSI;   || importe 2
|SI puntero = 5; campo5 = campo0;    |FINSI;   || importe 3
|SI puntero > 5;
    |MENAV "    Campos del secuencial no configurados; desechados ...";
|FINSI;
    campo0 = "";
    puntero = puntero + 1;
|FPRC;

|PROCESO separa;
    alfa1 = field % 0;
    longi = alfa1;

    puntero = 1;
    campo0 = "";    campo1 = "";   campo2 = "";
    campo3 = "";    campo4 = "";   campo5 = "";

|PARA para1 = 1; |SI para1 [ longi; |HACIENDO para1 = para1 + 1;
        nume1 = (para1 * 100) + 1;
        alfa1 = field % nume1;
    |SI alfa1 = "|";
        |HAZ separa2;
    |SINO;
            campo0 = campo0 + alfa1;
    |FINSI;
|SIGUE;
||HAZ separa2;      || como termina con un '|' no hay que buscar mas
|FPRC;

|PROCESO tipo3;
     |ABRE #4;
     |ABRE #5;
     |ABRE #6;
     |SELECT #2#6;
|DEBUG;
     alfa1 = #0#1;
     |QBF alfa1;
     aOrigen = alfa1;
     aLon = alfa1 % 0;
     nLon = aLon;

     |DBASE aAlfa;
     |QBF aAlfa;
     aAlfa = aAlfa + "tmp/";

     aLon = alfa1 % 0;
     nLon = aLon;

     aSoloFichero = "";
     |PARA nPos = nLon; |SI nPos ] 1; |HACIENDO nPos = nPos - 1;
          nPos1 = (nPos * 100) + 1;
          aCaracter = alfa1 % nPos1;
          |SI aCaracter = "\"; |O aCaracter = "/";
               |SAL;
          |SINO;
               aSoloFichero = aCaracter + aSoloFichero;
          |FINSI;
     |SIGUE;

     alfa1 = aAlfa + aSoloFichero;

     |RECIBE_FICHERO aOrigen, alfa1;

     fabre = "rb  " + alfa1;
     |FABRE fabre;
     |SI FSalida ] 0;
          fcanl = FSalida;
          field2 = "";
          field = fcanl + " 1"; |FLEE field;
          |PARA; |SI FSalida > 0; |HACIENDO;
               nume1 = &field;
               |SI nume1 ! 10;
                    field2 = field2 + field;
               |SINO;
                    field = field2;
                    field2 = "";
                    |HAZ separa;
                    |HAZ planilla;
                    |HAZ gra_cab;
                    |HAZ gra_lin;
               |FINSI;
               field = fcanl + " 1";  |FLEE field;
          |SIGUE;
          |FCIERRA fcanl;
     |SINO;
          alfa1 = "    Fichero [" + (fabre % 5) + "] inaccesible para lectura ...";
          |MENAV alfa1;
     |FINSI;
     |CIERRA #4;
     |CIERRA #5;
     |CIERRA #6;
|FPRC;

|PROGRAMA;
|CLS;
|CABEZA "Importacion Jornadas";
|DDEFECTO #0;
|PINPA #0#0;
|ABRE #0;
|LEE 101#0p;
|SI FSalida ! 0;    |GRABA 020#0b; |FINSI;
|PINDA #0#0;
|ENDATOS #1#0;
|SI FSalida = 0;
    |HAZ tipo3;
        alfa1 = "    Importados " + cuantos + " registros ...";
    |MENAV alfa1;
    |GRABA 020#0a;
|FINSI;
|LIBERA #0;
|CIERRA #0;
|FPRO;
