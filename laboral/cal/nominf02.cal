|FICHEROS;
     nominf02 #0;
     nomir012 #1;

     labempre;
     labtraba;
     nomir003 #102;
     nomir013;
     nomir015;
|FIN;

|VARIABLES;
     fFecha = @;
     aAlfa = "";
     nNume = 0;
     &nAnyNac = 0;
     &aTipCon = "";
     &aDisca = "";
     &aProlon = "";
     &aMovil = "";
     &aCeuMel = "";
     &aSituacion = "";
     &aViv = "";

     &nAMe75 = 0;
     &nAMa75 = 0;
     &nAMe75Ent = 0;
     &nAMa75Ent = 0;
     &nA33Ent = 0;
     &nA66Ent = 0;
     &nAMREnt = 0;

     &nDMe3Ent = 0;
     &nDMa3Ent = 0;
     &aD1Com = "";
     &aD2Com = "";
     &aD3Com = "";
     &aD4Com = "";
     &nD4Com = 0;
     &nD4Tot = 0;
     &nD4Ent = 0;
     &nD33Ent = 0;
     &nD66Ent = 0;
     &nDMREnt = 0;

     swSigue = 0;
     nPara1 = 0;
     nPara2 = 0;
     nPara3 = 0;
     nPara4 = 0;
     nPara5 = 0;
     nEdad = 0;
     nHijo = 0;

     nMes = 0;
     &aCausaReg = "";
     &nRetriAnt = 0;
     &nRetenAnt = 0;
     &aCeuMelAnt = "";
     &nBaseAnt = 0;
     &nMinAnt = 0;
     &nTipoAnt = 0;
     &aMinorAnt = "";
     &nImpMinorAnt = 0;

     aParam = "";
     &nEmp = 0;
     &nTra = 0;
     &nElAnyIRPF = 0;
     &nElMesIRPF = 0;
     swHayAlgo = 0;
     nCampo = 0;
|FIN;

|PROCESO Defectos;
     fFecha = ~;
     aAlfa = fFecha;
     aAlfa = aAlfa % 402;
     #0#5 = aAlfa;
     |PINTA #0#5;

     aAlfa = fFecha;
     aAlfa = aAlfa % -104;
     #0#4 = aAlfa;
     |PINTA #0#4;
|FPRC;

|PROCESO nomir013;
     swSigue = 0;
     #nomir013#0 = #labtraba#7;
     #nomir013#41 = #labtraba#0;
     #nomir013#40 = #0#4;
     #nomir013#120 = #0#5;
     |LEE 000#nomir013.];
     |SI FSalida = 0;
          |SI #nomir013#0 = #labtraba#7; |Y #nomir013#41 = #labtraba#0;
          |Y #nomir013#40 = #0#4; |Y #nomir013#120 = #0#5;
               swSigue = 1;
          |SINO;
               |LEE 000#nomir013.a;
               |SI #nomir013#0 = #labtraba#7; |Y #nomir013#41 = #labtraba#0;
               |Y #nomir013#40 = #0#4;
                    swSigue = 1;
               |FINSI;
          |FINSI;
     |FINSI;

     |SI swSigue = 0;
          || no se han encontrado datos de IRPF para el periodo solicitado
     |SINO;
          || cojo los datos que he encontrado, ya sean del mismo mes que
          || pido en el informe porque ese mes cambiaron y si no del mes
          || anterior que tuviera
          |PARA nCampo = 0; |SI nCampo [ 127; |HACIENDO nCampo = nCampo + 1;
               #nomir003#nCampo# = #nomir013#nCampo#;
          |SIGUE;
     |FINSI;
|FPRC;

|PROCESO Ascendientes;
     nAMe75 = 0;
     nAMa75 = 0;
     nAMe75Ent = 0;
     nAMa75Ent = 0;
     nA33Ent = 0;
     nA66Ent = 0;
     nAMREnt = 0;

     |PARA nPara1 = 95; |SI nPara1 [ 104; |HACIENDO nPara1 = nPara1 + 1;
          nEdad = #0#4 - #102nPara1;
          |SI #102nPara1 ! 0;
               nPara2 = nPara1 - 33;    || % Discapacidad
               nPara3 = nPara1 - 43;    || Convivencia
               nPara4 = nPara1 + 10;    || Movilidad Reducida

               |SI nEdad ] 65; |O #102nPara2 = "1"; |O #102nPara2 = "2";
                    |SI nEdad ] 75;
                         nAMa75 = nAMa75 + 1;
                         |SI #102nPara3 = 1;
                              nAMa75Ent = nAMa75Ent + 1;
                         |FINSI;
                    |SINO;
                         nAMe75 = nAMe75 + 1;
                         |SI #102nPara3 = 1;
                              nAMe75Ent = nAMe75Ent + 1;
                         |FINSI;
                    |FINSI;
                    |SI #102nPara2 = "1"; |Y #102nPara3 = 1;
                        nA33Ent = nA33Ent + 1;
                    |FINSI;
                    |SI #102nPara4 = "S"; |Y #102nPara3 = 1;
                        nAMREnt = nAMREnt + 1;
                    |FINSI;
                    |SI #102nPara2 = "2"; |Y #102nPara3 = 1;
                        nA33Ent = nA33Ent + 1;
                    |FINSI;
               |FINSI;
          |FINSI;
     |SIGUE;
|FPRC;

|PROCESO Descendientes;
     swSigue = 0;

     || de momento con lo que haya en el nomir003
     || HAZ nomir013;

     nDMe3Ent = 0;
     nDMa3Ent = 0;
     aD1Com = "";
     aD2Com = "";
     aD3Com = "";
     nD4Tot = 0;
     nD4Ent = 0;
     nD33Ent = 0;
     nD66Ent = 0;
     nDMREnt = 0;

     |PARA nHijo = 1; |SI nHijo [ 10; |HACIENDO nHijo = nHijo + 1;
          nPara1 = nHijo + 19;

          |SI #102nPara1 ! 0;
               nPara2 = nPara1 + 10;    || grado minusvalia
               nPara3 = nPara1 + 22;    || por entero S/N
               nPara4 = nPara1 + 65;    || movilidad reducida S/N
               nPara5 = nPara1 + 55;    || A¤o Adopcion

               nEdad = #0#4 - #102nPara1;

               |SI nEdad < 25; |O #102nPara2 = "1"; |O #102nPara2 = "2";
                    |SI nHijo ] 4;
                         nD4Tot = nD4Tot + 1;
                    |FINSI;

                    |SI #102nPara3 = "S";    || si computa o no por entero
                         |SI nEdad < 3;
                              nDMe3Ent = nDMe3Ent + 1;
                         |FINSI;
                         |SI nEdad ] 3; |Y nEdad < 25;
                              nDMa3Ent = nDMa3Ent + 1;
                         |FINSI;
                         |SI nHijo = 1;
                              aD1Com = "Entero";
                         |FINSI;
                         |SI nHijo = 2;
                              aD2Com = "Entero";
                         |FINSI;
                         |SI nHijo = 3;
                              aD3Com = "Entero";
                         |FINSI;
                         |SI nHijo ] 4;
                              nD4Ent = nD4Ent + 1;
                         |FINSI;
                         |SI #102nPara2 = "1";
                              nD33Ent = nD33Ent + 1;
                         |FINSI;
                         |SI #102nPara2 = "2";
                              nD66Ent = nD66Ent + 1;
                         |FINSI;
                         |SI #102nPara4 = "S";
                              nDMREnt = nDMREnt + 1;
                         |FINSI;
                    |SINO;
                         |SI nHijo = 1;
                              aD1Com = "Por mitad";
                         |FINSI;
                         |SI nHijo = 2;
                              aD2Com = "Por mitad";
                         |FINSI;
                         |SI nHijo = 3;
                              aD3Com = "Por mitad";
                         |FINSI;
                    |FINSI;
               |FINSI;
          |FINSI;
     |SIGUE;
|FPRC;

|PROCESO Inicia;
     fFecha = @;
     aAlfa = "";
     nNume = 0;
     nAnyNac = 0;
     aTipCon = "";
     aDisca = "";
     aProlon = "";
     aMovil = "";
     aCeuMel = "";
     aSituacion = "";
     aViv = "";

     nAMe75 = 0;
     nAMa75 = 0;
     nAMe75Ent = 0;
     nAMa75Ent = 0;
     nA33Ent = 0;
     nA66Ent = 0;
     nAMREnt = 0;

     nDMe3Ent = 0;
     nDMa3Ent = 0;
     aD1Com = "";
     aD2Com = "";
     aD3Com = "";
     aD4Com = "";
     nD4Com = 0;
     nD4Tot = 0;
     nD4Ent = 0;
     nD33Ent = 0;
     nD66Ent = 0;
     nDMREnt = 0;

     swSigue = 0;
     nPara1 = 0;
     nPara2 = 0;
     nPara3 = 0;
     nPara4 = 0;
     nPara5 = 0;
     nEdad = 0;
     nHijo = 0;

     nRetriAnt = 0;
     nRetenAnt = 0;
     aCeuMelAnt = "";
     nBaseAnt = 0;
     nMinAnt = 0;
     nTipoAnt = 0;
     aMinorAnt = "";
     nImpMinorAnt = 0;
|FPRC;

|PROCESO nomir012;
     |HAZ Inicia;

     #labempre#0 = #nomir012#1;
     |LEE 000#labempre.=;

     #labtraba#0 = #nomir012#1;
     #labtraba#1 = #nomir012#2;
     |LEE 000#labtraba.=;

     #nomir003#0 = #labtraba#7;
     #nomir003#40 = #0#4;
     #nomir003#41 = #labtraba#0;
     |LEE 000#nomir003.=;
     |HAZ nomir013;

     #nomir015#44 = #labtraba#0;
     #nomir015#0 = #labtraba#7;
     #nomir015#1 = #0#4;
     #nomir015#75 = #0#5;
     |LEE 000#nomir015.=;
     |SI FSalida ! 0;
          |ACABA;
     |FINSI;

     aAlfa = #labtraba#11;
     aAlfa = aAlfa % -104;
     nAnyNac = aAlfa;
     aCausaReg = #nomir012#103;

     aAlfa = #nomir003#5;
     |SI aAlfa = "1";
          aTipCon = "GENERAL";
     |FINSI;
     |SI aAlfa = "2";
          aTipCon = "INFERIOR AL A¥O";
     |FINSI;
     |SI aAlfa = "3";
          aTipCon = "RELACION ESPECIAL";
     |FINSI;
     |SI aAlfa = "4";
          aTipCon = "MANUALES";
     |FINSI;

     |SI #nomir003#6 = "S";
          |SI #nomir003#7 = "1";
               aDisca = "Desde el 33% al 64%";
               |SI #nomir003#8 = "S";
                    aDisca = aDisca + " con ayuda";
               |FINSI;
          |SINO;
               |SI #nomir003#7 = "2";
                    aDisca = "El 65% o m s";
               |SINO;
                    aDisca = "NO";
               |FINSI;
          |FINSI;
     |SINO;
          aDisca = "NO";
     |FINSI;

     |SI #nomir003#72 = "S";
          aProlon = "SI";
     |SINO;
          aProlon = "NO";
     |FINSI;

     |SI #nomir003#73 = "S";
          aMovil = "SI";
     |SINO;
          aMovil = "NO";
     |FINSI;

     |SI #nomir003#14 = "S";
          aCeuMel = "SI";
     |SINO;
          aCeuMel = "NO";
     |FINSI;

     |SI #nomir003#2 = "A";
          aSituacion = "ACTIVO";
     |FINSI;
     |SI #nomir003#2 = "P";
          aSituacion = "PENSIONISTA";
     |FINSI;
     |SI #nomir003#2 = "D";
          aSituacion = "DESEMPLEADO";
     |FINSI;
     |SI #nomir003#2 = "O";
          aSituacion = "OTROS";
     |FINSI;

     |SI #nomir003#124 = "S";
          aViv = "R‚gimen Transitorio";
     |SINO;
          |SI #nomir003#126 = "S";
               aViv = "R‚gimen General";
          |SINO;
               aViv = "NO";
          |FINSI;
     |FINSI;

     |HAZ Descendientes;
     |HAZ Ascendientes;

     nMes = #0#5;
     |SALVA_FICHA #nomir012;

     |SI nMes ! 1;
          #nomir012#1 = #labtraba#0;
          #nomir012#2 = #labtraba#1;
          #nomir012#3 = #0#4;
          #nomir012#4 = nMes - 1;
          |LEE 000#nomir012.=;

          |SI FSalida = 0;
               nRetriAnt = #nomir012#93;
               nRetenAnt = #nomir012#94;
               nBaseAnt = #nomir012#96;
               nMinAnt = #nomir012#97;
               nTipoAnt = #nomir012#98;
               nImpMinorAnt = #nomir012#100;
          |FINSI;
     |FINSI;

     |SI #nomir012#95 = "S";
          aCeuMelAnt = "SI";
     |SINO;
          aCeuMelAnt = "NO";
     |FINSI;

     |SI #nomir012#99 = "S";
           aMinorAnt = "R‚gimen Transitorio";
     |SINO;
          |SI #nomir012#110 = "S";
               aMinorAnt = "R‚gimen General";
          |SINO;
               aMinorAnt = "NO";
          |FINSI;
     |FINSI;

     |REPON_FICHA #nomir012;

     |PRINT;
     swHayAlgo = 1;
|FPRC;

|DEFBUCLE nomir012;
     #nomir012#1;
     ;
     #0#0, #0#2, #0#4, #0#5;
     #0#1, #0#3, #0#4, #0#5;
     ;
     NULL, nomir012;
|FIN;

|PROGRAMA;
     aParam = PARAMETRO;
     |QBF aParam;
     |SI aParam = "";
          |CLS;
          |PINPA #0#0;
          |PINDA #0#0;
          |HAZ Defectos;
          |ENDATOS #1#0;
     |SINO;
          #0#0 = nEmp;
          #0#1 = nEmp;
          #0#2 = nTra;
          #0#3 = nTra;
          #0#4 = nElAnyIRPF;
          #0#5 = nElMesIRPF;
     |FINSI;

     |SI FSalida = 0;
          Impresora = "CrystalReport";
          |IMPRE -1;
          |INFOR "nominf02";

          |ABRE #nomir015;
          |ABRE #nomir003;
          |ABRE #nomir013;
          |ABRE #labempre;
          |ABRE #labtraba;
          |ABRE #nominf02;
          |ABRE #nomir012;

          swHayAlgo = 0;
          |BUCLE nomir012;
          |SI swHayAlgo = 1;
               |PIEINF;
               |FININF;
               |FINIMP;
          |SINO;
               aAlfa = "    ATENCION: no se han encontrado datos ";
               aAlfa = aAlfa + "en la selección solicitada.";
               |MENAV aAlfa;
          |FINSI;

          |CIERRA #nomir012;
          |CIERRA #nominf02;
          |CIERRA #labtraba;
          |CIERRA #labempre;
          |CIERRA #nomir013;
          |CIERRA #nomir003;
          |CIERRA #nomir015;
     |FINSI;
|FPRO;
