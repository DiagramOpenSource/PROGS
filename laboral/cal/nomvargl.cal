|FICHEROS;
     nomvargl #0;
     labtraba #1;
     nomvarca #2;
     nomvarli #3;
     labempre #4;

     nomabsca;
     nomabsli;
|FIN;

|VARIABLES;
    mod = 0;
    baja = 0;
    mesbaja = 0;
    desde = 0;
    hasta = 0;
    swvalido = 0;
    ceros = 0;
    tipoa_1 = 0;
    varnum1 = 0;
    desde_1 = 0;
    varnum2 = 0;
    hasta_1 = 0;
    swerror = 0;
    swporsi = 0;
    c = 0;
    FEstado = 0;
    campo = 0;
    elotro = 0;
    desde_2 = 0;
    hasta_2 = 0;
    tipoa_2 = 0;
    swexiste = 0;
    unida = 0;
    valor = 0;
    mensaje0 = "0000ATENCION !!! Error de entrada ...";
    mensaje1 = "0000ATENCION !!! Primer dia erroneo ...";
    mensaje2 = "0000ATENCION !!! Tipo de enfermedad erroneo en ficha de trabajadores ...";
    mensaje3 = "0000ATENCION !!! Fecha fuera de periodo de alta ...";
    mensaje4 = "0000ATENCION !!! Concepto fuera de rango ...";
    mensaje5 = "0000ATENCION !!! Fecha fuera del mes actual ...";
    mensaje6 = "0000ATENCION !!! Variaciones sin conceptos ...";
    men1 = "0000ATENCION !!! Fecha erronea ...";
    dia1 = "";
    mes1 = "";
    any1 = "";
    dia = 0;
    mes = 0;
    any = 0;
    tope = 0;
    sw = 0;
    fec = "";
    blanco = "          ";
    puntos = "..........";
    varnum = 0;
    desnum = 0;
    hasnum = 0;
    fecsys = @;
    alfa1 = "";
    alfa2 = "";
    swcheck = 0;
    victima = "";
    posinum = 0;
    posialf = "";
    positra = 0;
    posipun = 0;
    swident = 0;
    espac = "";
    longi = 0;
    corta = 0;
    corta2 = 0;
    swActiva = 0;

     aDia = "";
     aMes = "";
     aAny = "";
     fFecha = @;
     fFechaDesde = @;
     fFechaHasta = @;
     aAlfa = "";
     FEstado1 = 0;
     nCampo1 = 0;
     nCampo = 0;
     anynum = 0;
     mesnum = 0;
     bisiesto = 0;
     numer = 0;
     topemes = 0;
|FIN;

||************************************************************************
||                CALCULOS DESDE EL MANTENIMIENTO
||************************************************************************

|PROCESO ayuda74; |TIPO 7;
     |PUSHV 05010580;
     aAlfa = "            Opciones: (S) Activar, (N) No activar, (X) Sin cambios              ";
     |ATRI S;
     |PINTA 0501, aAlfa;
     |ATRI 0;
|FPRC;

|PROCESO Restaura;
     |POPV;
|FPRC;

|PROCESO ActivaExo; |TIPO 7;
     |CAMPO_MODIFICA #0#75, 1, "N";
     |SI #0#74 = "S";
          |CAMPO_MODIFICA #0#75, 1, "S";
     |FINSI;
     |SI #0#74 = "N";
          #0#75 = 0;
          |PINTA #0#75;
     |FINSI;
|FPRC;

|PROCESO ulti_dia;  || halla el ultimo dia del mes
     bisiesto = 0;
     numer = anynum $ 4;
     |SI numer = 0;
          bisiesto = 1;
     |FINSI;
     topemes = 31;
     |SI mesnum= 2;
          topemes = 28 + bisiesto;
     |FINSI;
     |SI mesnum= 4;|O mesnum= 6;|O mesnum= 9;|O mesnum=11;
          topemes = 30;
     |FINSI;
|FPRC;

|PROCESO CeroNo;
     nCampo = Campo - 4;
     |SI #0Campo = "D"; |Y FSalida ! 2;
          |SI #0nCampo = 0;
               aAlfa = "    Es necesario indicar el día que acaba la situación ";
               aAlfa = aAlfa + "de ERE. No se permite el valor 0 en 'hasta día'";
               |MENAV aAlfa;
               |ERROR;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO PorDes; |TIPO 7;
     nCampo1 = Campo - 68;
     |SI #0nCampo1 = "D";
          |CAMPO_MODIFICA #0Campo, 1, "S";
     |SINO;
          |CAMPO_MODIFICA #0Campo, 1, "N";
          #0Campo = 100;
          |PINTA #0Campo;
     |FINSI;
|FPRC;

|PROCESO ActivaSN;
     aAlfa = #0#12;
     |QBF aAlfa;
     |SI aAlfa = ""; |O aAlfa = "..........";
          |CAMPO_MODIFICA #0#73, 1, "N";
          #0#73 = 00;
          |PINTA #0#73;
     |SINO;
          |CAMPO_MODIFICA #0#73, 1, "S";
     |FINSI;
|FPRC;

|PROCESO verifec1; |TIPO 0;
        fec = #0Campo; sw = 0;
|SI fec ! blanco; |Y fec ! puntos;
  dia1 = fec % A102;
  mes1 = fec % A402;
  any1 = fec % A704;
  dia = dia1; mes = mes1; any = any1;
  |SI dia ! 0; |Y mes ! 0; |Y any ! 0;
     |SI mes [ 12; |Y mes ] 1;
       |SI mes = 1; |O mes = 3; |O mes = 5; |O mes = 7; |O mes = 8; |O mes = 10; |O mes = 12;
           tope = 31;
       |SINO;
           |SI mes = 2;
               mod = any $ 4;
               |SI mod = 0;
                   tope = 29;
               |SINO;
                   tope = 28;
               |FINSI;
            |SINO;
               tope = 30;
            |FINSI;
        |FINSI;
        |SI dia [ tope;
            sw = 1;
        |FINSI;
     |FINSI;
  |FINSI;
|SINO;
  sw = 1;
|FINSI;
|SI sw = 0;
    |MENAV men1;
    |ERROR;
|FINSI;
|FPRC;

|PROCESO checfech; |TIPO 0;  || chequea que la fecha sea mayor o nula
     |SI #0Campo ! 0;
          hasnum = #0Campo;
          varnum = Campo - 4;
          desnum = #0varnum;
          |SI desnum > hasnum;
               |MENAV mensaje0;
               |ERROR;
          |FINSI;

          fecsys = ~;
          alfa1 = fecsys;
          alfa1 = alfa1 % -104;
          anynum = alfa1;
          mesnum = #0#72;
          |HAZ ulti_dia;
          |SI #0Campo > topemes;
               alfa1 = topemes;
               alfa1 = "    ATENCION: el mes tiene " + alfa1 + " días.";
               |MENAV alfa1;
               #0Campo = topemes;
               |PINTA #0Campo;
               |ERROR;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO desglosa; |TIPO 0;  || saca el dia de baja
    alfa1 = #0Campo % 102;
    alfa2 = #0Campo % 402;
    baja = alfa1;
    mesbaja = alfa2;
|SI mesbaja ! #0#72; |Y mesbaja ! 0;
    |MENAV mensaje5;
    |ERROR;
|FINSI;
|FPRC;

|PROCESO antbaja1; |TIPO 0;  || comprueba que dias no pasen de baja (DESDE)
|SI baja ! 0; |Y #0Campo ! 0;
    |SI #0Campo ] baja;
        |MENAV mensaje3;
        |ERROR;
    |FINSI;
|FINSI;
|FPRC;

|PROCESO antbaja2; |TIPO 0;  || comprueba que dias no pasen de baja (HASTA)
|SI baja ! 0; |Y #0Campo ! 0;
    |SI #0Campo ] baja;
        |MENAV mensaje3;
        |ERROR;
    |FINSI;
|FINSI;
|FPRC;

|PROCESO checnume1; |TIPO 0;
|SI #0Campo ! "          ";
        posipun = 0;
        victima = #0Campo;  posinum = victima;
    |QBT victima;
        victima = "          " + victima;
        victima = victima % -110;                     || ajusta a la derecha
    |SI posinum ] -999999.99; |Y posinum [ 999999.99;
        |PARA posinum = 1; |SI posinum [ 10; |Y posinum ! 0; |HACIENDO posinum = posinum + 1;
                positra = 100 * posinum;
                positra = -1 - positra;
                posialf = victima % positra;
            |HAZ identida;
            |SI swident = 4;                || caracter igual al espacio
                posinum = 10;               || sale bien
            |FINSI;
            |SI swident = 3;                || caracter igual al signo (+/-)
                positra = 100 + posinum;
                positra = 0 - positra;
                victima = victima % positra;
                victima = "          " + victima;
                victima = victima % -110;
                posinum = 10;               || sale bien
            |FINSI;
            |SI swident = 2;                || caracter igual al punto decimal
                |SI posipun = 0;
                    posipun = posinum;
                |SINO;
                    positra = posinum - posipun;
                    |SI positra = 2;
                        swident = 0;
                    |SINO;
                        posipun = posinum;
                    |FINSI;
                |FINSI;
            |FINSI;
            |SI swident = 0;                || caracter malo, acabar !
                posinum = -1;               || sale mal
            |FINSI;
        |SIGUE;
        |SI posipun ! 0; |Y swident ! 0;        || si hay decimales y es bueno
            posipun = posipun - 1;
            |SI posipun ! 0;
                positra = 100 + posipun;
                positra = 0 - positra;
                posialf = victima % positra;       || separa decimales
                posipun = posipun + 1;
                positra = (posipun + 1) * 100;
                positra = positra + (10 - posipun);
                positra = 0 - positra;
                victima = victima % positra;       || coge la parte entera
            |QBF posialf;
                posialf = posialf + "00";
                posialf = posialf % 102;           || redondea decimales
                victima = victima + "." + posialf; || construye numero
            |SINO;
                victima = victima % 109;
            |FINSI;
         |FINSI;
    |SINO;
        posinum = 0;
    |FINSI;
    |SI posinum = 0;
        |ERROR;
    |SINO;
        |QBT victima;
            victima = "          " + victima;
            #0Campo = victima % -110;
        |PINTA #0Campo;
    |FINSI;
|FINSI;
|FPRC;

|PROCESO checnume2; |TIPO 0;             || chequea si es traducible a numero
    alfa1 = #0Campo % 0;
    longi = alfa1;
    espac = " " * longi;
    corta = (longi + 100) * -1;
|SI longi = 10;
        desde = -9999999.9999;
        hasta =  9999999.9999;
|SINO;
        desde = -9999999.9999;
        hasta =  9999999.9999;
|FINSI;
|SI #0Campo ! espac;
        posipun = 0;
        victima = #0Campo;  posinum = victima;
    |QBT victima;
        victima = espac + victima;
        victima = victima % corta;           || ajusta a la derecha
    |SI posinum ] desde; |Y posinum [ hasta;
        |PARA posinum = 1; |SI posinum [ longi; |Y posinum ! 0; |HACIENDO posinum = posinum + 1;
                positra = 100 * posinum;
                positra = -1 - positra;
                posialf = victima % positra;
            |HAZ identida;
            |SI swident = 4;                || caracter igual al espacio
                posinum = longi;            || sale bien
            |FINSI;
            |SI swident = 3;                || caracter igual al signo (+/-)
                positra = 100 + posinum;
                positra = 0 - positra;
                victima = victima % positra;
                victima = espac + victima;
                victima = victima % corta;
                posinum = longi;            || sale bien
            |FINSI;
            |SI swident = 2;                || caracter igual al punto decimal
                |SI posipun = 0;
                    posipun = posinum;
                |SINO;
                    positra = posinum - posipun;
                    |SI positra = 2;
                        swident = 0;
                    |SINO;
                        posipun = posinum;
                    |FINSI;
                |FINSI;
            |FINSI;
            |SI swident = 0;                || caracter malo, acabar !
                posinum = -1;               || sale mal
            |FINSI;
        |SIGUE;
        |SI posipun ! 0; |Y swident ! 0;        || si hay decimales y es bueno
            posipun = posipun - 1;
            |SI posipun ! 0;
                positra = 100 + posipun;
                positra = 0 - positra;
                posialf = victima % positra;       || separa decimales
                posipun = posipun + 1;
                positra = (posipun + 1) * 100;
                positra = positra + (longi - posipun);
                positra = 0 - positra;
                victima = victima % positra;       || coge la parte entera
                |QBF posialf;
                posialf = posialf + "0000";
                posialf = posialf % 104;           || redondea decimales
                |SI victima < 1; victima = "0"; |FINSI;
                victima = victima + "." + posialf; || construye numero
            |SINO;
                corta2 = (corta * -1) - 1;
                victima = victima % corta2;
            |FINSI;
         |FINSI;
    |SINO;
        posinum = 0;
    |FINSI;
    |SI posinum = 0;
        |ERROR;
    |SINO;
        |QBT victima;
            victima = espac + victima;
            #0Campo = victima % corta;
        |PINTA #0Campo;
    |FINSI;
|FINSI;
|FPRC;

|PROCESO identida;
    swident = 0;
|SI posialf = "-"; |O posialf = "+";    swident = 3; |FINSI; || signos
|SI posialf = ".";                      swident = 2; |FINSI; || punto
|SI posialf = "0"; |O posialf = "1";
                   |O posialf = "2";
                   |O posialf = "3";
                   |O posialf = "4";
                   |O posialf = "5";
                   |O posialf = "6";
                   |O posialf = "7";
                   |O posialf = "8";
                   |O posialf = "9";    swident = 1; |FINSI; || numeros
|SI posialf = " ";                      swident = 4; |FINSI; || espacios
|FPRC;

|PROCESO checcon5; |TIPO 0;    || chequea rangos de conceptos
|SI #0Campo ! 0; |Y #0Campo ! 999;
        swcheck = 0;
    |SI #0Campo ] 101; |Y #0Campo [ 199; swcheck = 1; |FINSI;
    |SI #0Campo ] 201; |Y #0Campo [ 208; swcheck = 1; |FINSI;
    |SI #0Campo ] 301; |Y #0Campo [ 310; swcheck = 1; |FINSI;
    |SI #0Campo ] 401; |Y #0Campo [ 420; swcheck = 1; |FINSI;
    |SI #0Campo ] 501; |Y #0Campo [ 506; swcheck = 1; |FINSI;
    |SI #0Campo ] 901; |Y #0Campo [ 999; swcheck = 1; |FINSI;
    |SI swcheck = 0;
        |MENAV mensaje4;
        |ERROR;
    |FINSI;
|FINSI;
|FPRC;

|PROCESO borra; |TIPO 0;   || borra desde y hasta cuando el tipo es nulo
|SI #0Campo = " ";
        desde = Campo - 8;
        hasta = Campo - 4;
        #0desde = 0;
        #0hasta = 0;
    |PINTA #0desde;
    |PINTA #0hasta;
|FINSI;
|FPRC;

|PROCESO mirames; |TIPO 7; ||CALCULA EL MES POR DEFECTO SEGUN FECHA SISTEMA
    fecsys = ~;
    alfa1 = fecsys;
    alfa1 = alfa1 % 402;
    #0Campo = alfa1;
|PINTA #0Campo;
|FPRC;

|PROCESO comprueba; || PARA QUE LOS CONCEPTOS NO SEAN CERO TODOS
    swvalido = 0;
|PARA ceros = 27; |SI ceros [ 41; |HACIENDO ceros = ceros + 1;
    |SI #0ceros ! 0;
            swvalido = 1;
    |FINSI;
|SIGUE;
|FPRC;

||************************************************************************
||                CALCULOS DE LECTURA Y PROCESO
||************************************************************************

|PROCESO chec1;    || chequea que dias actual sea ! 0, cuando lo deba de ser
|SI #0tipoa_1 ! "F"; |Y #0tipoa_1 ! "H";
        varnum1 = desde_1;
        varnum2 = hasta_1;
    |SI #0varnum1 = 0;
        |SI #0tipoa_1 ! "A";                || si el tipo no es correcto
            |SI #1#60 ! #0tipoa_1;
                    swerror = 1;
                    #0tipoa_1 = "";   || (asi no entra en los siguientes)
            |FINSI;
        |FINSI;
        |SI #0tipoa_1="E"; |O #0tipoa_1="M";  || si dias actual enfer. = 0
            |SI #1#89 = 0;
                    swerror = 1;
            |FINSI;
        |FINSI;
        |SI #0tipoa_1 = "A";                || si dias actual accid. = 0
            |SI #1#92 = 0;
                    swerror = 1;
            |FINSI;
        |FINSI;
    |SINO;
        |SI #0tipoa_1 = "E"; |O #0tipoa_1 = "M"; || si dias actual ! 0
            |SI #1#89 ! 0;                       || y desde dia ! 0
                    swporsi = 0;                 || (enfermedad)
                |PARA c = tipoa_1 - 1; |SI c ] 8; |HACIENDO c = c - 1;
                        hasta = c - 4;
                        desde = tipoa_1 - 8;
                    |SI #0tipoa_1 = #0c; |Y #0desde > #0hasta; |Y #0hasta ! 0;
                            swporsi = 1;
                    |FINSI;
                |SIGUE;
                |SI swporsi = 0;
                        swerror = 1;
                |FINSI;
            |FINSI;
        |FINSI;
        |SI #0tipoa_1 = "A";                   || si dias actual ! 0
            |SI #1#92 ! 0;                     || y desde dia ! 0
                    swporsi = 0;               || (accidente)
                |PARA c = tipoa_1 - 1; |SI c ] 8; |HACIENDO c =c - 1;
                        hasta = c - 4;
                        desde = tipoa_1 - 8;
                    |SI #0tipoa_1 = #0c; |Y #0desde > #0hasta; |Y #0hasta ! 0;
                            swporsi = 1;
                    |FINSI;
                |SIGUE;
                |SI swporsi = 0;
                        swerror = 1;
                |FINSI;
            |FINSI;
        |FINSI;
    |FINSI;
|FINSI;
|FPRC;


|PROCESO activasino;
     swActiva = 1;
     |ABRE #4;
     #4#0 = #1#0;
     |LEE 000#4=;
     |SI #4#50 = "S";
          swActiva = 0;  || empresa no activa
     |FINSI;
     |CIERRA #4;
|FPRC;

|PROCESO lineas;
     |PARA campo = 27; |SI campo [ 41; |HACIENDO campo = campo + 1;
          |SI #0campo ! 0;
               unida = campo + 15;
               valor = campo + 30;
               |DDEFECTO #3;
               #3#0 = #1#0;     || empresa
               #3#1 = #1#1;     || trabajador
               #3#2 = #0#72;    || mes
               #3#3 = 0;        || tipo
               #3#4 = #0campo;  || concepto
               |LEE 101#3=;
               FEstado = FSalida;
               #3#5 = #0unida;  || unidades
               #3#6 = #0valor;  || valor
               |SI FEstado ! 0;
                    |HAZ activasino;
                    |SI swActiva = 1;
                         |GRABA 020#3n;
                    |FINSI;
               |FINSI;
               |SI FEstado = 0;
                    |HAZ activasino;
                    |SI swActiva = 1;
                         |GRABA 020#3a;
                    |FINSI;
               |FINSI;
               |LIBERA #3;
          |FINSI;
     |SIGUE;
|FPRC;

|PROCESO Lineal;
     |ABRE #nomabsca;
     |ABRE #nomabsli;

     #nomabsca#0 = #1#0;
     #nomabsca#1 = #1#1;
     |LEE 101#nomabsca.=;
     FEstado1 = FSalida;

     |SI #labtraba#47 ! 0;
          #nomabsca#2 = #labtraba#47;
     |FINSI;

     |SI FEstado1 = 0;
          |GRABA 020#nomabsca.a;
     |SINO;
          |GRABA 020#nomabsca.n;
     |FINSI;
     |LIBERA #nomabsca;

     aDia = #0desde_1; |QBF aDia; aDia = ("00" + aDia) % -102;
     aMes = #0#72; |QBF aMes; aMes = ("00" + aMes) % -102;
     fFecha = ~; aAlfa = fFecha; aAny = aAlfa % -104;
     aAlfa = aDia + "." + aMes + "." + aAny;
     fFechaDesde = aAlfa;

     aDia = #0hasta_1; |QBF aDia; aDia = ("00" + aDia) % -102;
     aMes = #0#72; |QBF aMes; aMes = ("00" + aMes) % -102;
     fFecha = ~; aAlfa = fFecha; aAny = aAlfa % -104;
     aAlfa = aDia + "." + aMes + "." + aAny;
     fFechaHasta = aAlfa;

     #nomabsli#0 = #1#0;
     #nomabsli#1 = #1#1;
     #nomabsli#2 = fFechaDesde;
     |LEE 101#nomabsli.=;
     |SI FSalida = 0;
          |BORRA 020#nomabsli.a;
     |FINSI;

     #nomabsli#0 = #1#0;
     #nomabsli#1 = #1#1;
     #nomabsli#2 = fFechaDesde;
     #nomabsli#3 = fFechaHasta;
     |SI #0tipoa_1 = "V"; |O #0tipoa_1 = "H";
          #nomabsli#4 = #0tipoa_1;
     |FINSI;
     |SI #0tipoa_1 = "F"; |O #0tipoa_1 = "f";
          #nomabsli#4 = "A";
     |FINSI;
     |SI #nomabsli#4 = "A"; |O #nomabsli#4 = "H";
          #nomabsli#5 = "S";
     |SINO;
          #nomabsli#5 = "N";
     |FINSI;
     #nomabsli#6 = 0;

     |GRABA 020#nomabsli.n;
     |LIBERA #nomabsli;

     |CIERRA #nomabsli;
     |CIERRA #nomabsca;
|FPRC;

|PROCESO cabecera;
     |DDEFECTO #2;
     #2#0 = #1#0;  || empresa
     #2#1 = #1#1;  || trabajador
     #2#2 = #0#72; || mes
     #2#3 = 0;     || tipo
     |LEE 101#2=;
     FEstado = FSalida;
     |SI #2#16 = "..........";|O #0#12 ! ".............";
          #2#16 = #0#12;
          #2#46 = #0#73;
     |FINSI;
     |PARA campo = 13; |SI campo [ 17; |HACIENDO campo = campo + 1;
          elotro = campo + 4;        || promedios, anticipos,especie
          |SI #2elotro = 0;|O #0campo ! 0;
               #2elotro = #0campo;        || y fecha de baja
          |FINSI;
     |SIGUE;

     |PARA campo = 0; |SI campo [ 3; |HACIENDO campo = campo + 1;
          desde_1 = campo;
          hasta_1 = campo +  4;    || origen
          tipoa_1 = campo +  8;
          desde_2 = campo +  4;
          hasta_2 = campo +  8;    || destino
          tipoa_2 = campo + 12;
          swerror = 0;
          |SI #0tipoa_1 ! " ";
               |SI #0tipoa_1 = "V"; |O #0tipoa_1 = "H"; |O #0tipoa_1 = "F";
               |O #0tipoa_1 = "f";
                    |HAZ Lineal;
                    swerror = 1;   || para que no grabe
               |SINO;
                    |HAZ chec1;
               |FINSI;
          |FINSI;
          |SI swerror = 0;
               |SI #2desde_2 = 0;|O #0desde_1 ! 0;  #2desde_2 = #0desde_1;  |FINSI;
               |SI #2hasta_2 = 0;|O #0hasta_1 ! 0;  #2hasta_2 = #0hasta_1;  |FINSI;
               alfa1 = #2tipoa_2;  |QBF alfa1;
               alfa2 = #0tipoa_1;  |QBF alfa2;
               |SI alfa1 = "";   |O alfa2 ! "";     #2tipoa_2 = #0tipoa_1;  |FINSI;
          |SINO;
               #2desde_2 = 0;
               #2hasta_2 = 0;
               #2tipoa_2 = " ";
          |FINSI;
     |SIGUE;

     #2#47 = #0#76;
     #2#48 = #0#77;
     #2#49 = #0#78;
     #2#50 = #0#79;

     ||SI FEstado = 0;  |GRABA 020#2a;  |FINSI;
     ||SI FEstado ! 0;  |GRABA 020#2n;  |FINSI;

     |SI FEstado ! 0;
          |HAZ activasino;
          |SI swActiva = 1;
               |GRABA 020#2n;
          |FINSI;
     |FINSI;

     |SI FEstado = 0;
          |HAZ activasino;
          |SI swActiva = 1;
               |GRABA 020#2a;
          |FINSI;
     |FINSI;
     |LIBERA #2;

     |SI #0#74 = "S";
          #1#188 = "S";
          #1#219 = #0#75;
          |GRABA 020#1a;
          |LIBERA #1;
     |FINSI;
     |SI #0#74 = "N";
          #1#188 = "N";
          #1#219 = 0;
          |GRABA 020#1a;
          |LIBERA #1;
     |FINSI;
|FPRC;

|PROCESO varia;
     |SI #labtraba#44 = "A";
          |PINTA 667, #1#0;
          |PINTA 767, #1#1;
          |HAZ cabecera;
          |HAZ lineas;
     |FINSI;
|FPRC;

|DEFBUCLE nomvargl0;
     #1#1;
     87,17;
     #0#19,#0#21,#0#23,#0#25;
     #0#20,#0#22,#0#24,#0#26;
     e;
     NULL,varia;
|FIN;

|PROCESO proceso; |TIPO 3;
     |HAZ comprueba;
     |SI swvalido = 1;
          |ABRET #2;
          |ABRET #3;
          |ATRI P;
          |CUADRO 05 73 08 53;
          |ATRI 0;
          |BUCLE nomvargl0;
          |CIERRAT #2;
          |CIERRAT #3;
     |SINO;
          |MENAV mensaje6;
     |FINSI;
|FPRC;
