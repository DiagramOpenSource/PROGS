|FICHEROS;
     nomesfin #0;
     nomow084 #984,MANTE;     || empresa
     nomow085 #985,MANTE;     || por nif de trabajador
     nomow086 #986;           || por codigo de trabajador
     nomow134 #134;

     labtraba  #200;
     nomhicca  #201;
     nomhicli  #202;
     labempre  #203;
     nomconli  #204;
     nomhiatr  #205;
     nomconca  #206;
     labcentr  #207;
     nomcenes  #208;
     nomnotay  #215;
     nomir003  #101;
     nomcalca;
     nomcalac;
     nomir012;
|FIN;

|VARIABLES;
     nCampo            = 0;
     nHandle           = 0;
     &nTipoRel         = 0;
     nDevengo          = 0;
     nImporteBaseI     = 0;
     nImporteRetencion = 0;
     nImporteSSTrabaja = 0;
     ded_not           = 0;
     nEmpresa          = 0;
     nCentro           = 0;
     notaria           = 0;
     nAtrasos          = 0;
     nEjer             = 0;
     nEjerAt           = 0;
     nHay              = 0;
     nGraba            = 0;
     nBoton5           = 0;
     nEl20             = 0;

     aBase             = "";
     aFichero          = "";
     aPathLaboral      = "";
     aClave            = "";
     aDni              = "";
     aSubClave         = "";
     aNombreTrabajador = "";
     aEspecie          = "";
     aRepercute        = "";
     aAlfa             = "";
     aClaveDia         = "";

     &nDiferencias     = 0;
     &nVEmp            = 0;
     &aVDNI            = "";
     &nVAny            = 0;
     &nCuotNueva       = 0;
     &nBaseHist        = 0;
     &nSSHist          = 0;

     &nSwMasivo = 0;
     &swCabecera = 0;
     nEmpAnt = 0;
     nComAnt = 0;
     nDCompl = 0;
     nHCompl = 0;
     nDActiv = 0;
     nHActiv = 0;

     nIRPFMayo = 0;
     nBaseJunio = 0;
     nRetJunio = 0;
     nIRPFJunio = 0;
     nIRPFJulio = 0;
     nDifJunio = 0;
     nAntEmp = 0;
     nAntTra = 0;
     swSigue = 0;
     &nCuotaMin = 0;
     &nCuotaMax = 0;
     &nVivienda = 0;
     &swVivienda = 0;
     &nMes = 0;
     swDeDonde = 0;
     &nElAnyIRPF = 0;
     aAlfa1 = "";
     aAlfa2 = "";
     aAlfa3 = "";
     aMensa0 = "";
     aMensa1 = "";
     aMensa2 = "";
     nTrabajador = 0;
     nTotalTrab = 0;
     aNif = "";
     &eaVengoDe = "";
     &eaVivienda1 = "";
     &eaVivienda2 = "";
     &nAny = 0;
     &nTopemes = 0;
     aFechaRef = "";
     fFechaRef = @;
     aFechaAlta = "";
     fFechaAlta = @;
     aFechaBaja = "";
     fFechaBaja = @;
|FIN;

|PROCESO Vivienda; |TIPO 7;
     |SI #985#22 ! 0;
          || he tenido deduccion vivienda durante el a¤o
          |COLOR 0,7; |ATRI R;
          |PINTA 1839, " DED.";
          |PINTA 1939, " VIV.";
          |ATRI 0;

          |PINTA 1844, "                                    ³";
          |PINTA 1944, "                                    ³";
          |ATRI R;
          |PINTA 1845, "Min           ";
          |PINTA 1864, "M x           ";
          |ATRI 0;

          |CAMPO_VISUAL #985#18, 1, "N";
          |CAMPO_VISUAL #985#23, 1, "S";
          |CAMPO_VISUAL #985#24, 1, "S";
          |CAMPO_POSICION #985#23, 1, 1850;
          |CAMPO_POSICION #985#24, 1, 1869;
          |PINTA #985#23;
          |PINTA #985#24;

          |SI #985#19 ! 0;    || o no llegamos al minimo o nos pasamos del maximo
               |SI #985#19 < 0;
                    |COLOR 0,7; |ATRI R;
                    |PINTA 1945, "Ret. inferior al m¡nimo:";
                    |ATRI 0;
               |FINSI;
               |SI #985#19 > 0;
                    |COLOR 0,7; |ATRI R;
                    |PINTA 1945, "Ret. superior al m ximo:";
                    |ATRI 0;
               |FINSI;
               |CAMPO_VISUAL #985#19, 1, "S";
               |SI #985#19 < 0;
                    |CAMPO_ATRIBUTO #985#19, N, 1, 7;
               |FINSI;
               |SI #985#19 > 0;
                    |CAMPO_ATRIBUTO #985#19, N, 4, 7;
               |FINSI;
               |PINTA #985#19;
               |CAMPO_ATRIBUTO #985#19, N, 0, 7;
          |SINO;
               |CAMPO_VISUAL #985#19, 1, "N";
               |PINTA 1947, "Retenci¢n IRPF entre los l¡mites";
          |FINSI;
     |SINO;
          |PINTA 1839, "Retenci¢n AEAT .............:            ";
          |PINTA 2039, "                                         ";
          |CAMPO_VISUAL #985#18, 1, "S";
          |CAMPO_VISUAL #985#19, 1, "S";
          |CAMPO_VISUAL #985#23, 1, "N";
          |CAMPO_VISUAL #985#24, 1, "N";
          |CAMPO_POSICION #985#23, 1, 2035;
          |CAMPO_POSICION #985#24, 1, 2047;
          |PINTA #985#18;
          |PINTA 1939, "Diferencia Datos Ejerc.-AEAT:            ";
          |SI #985#19 < 0;
               |CAMPO_ATRIBUTO #985#19, N, 1, 7;
          |FINSI;
          |SI #985#19 > 0;
               |CAMPO_ATRIBUTO #985#19, N, 4, 7;
          |FINSI;
          |PINTA #985#19;
          |CAMPO_ATRIBUTO #985#19, N, 0, 7;
     |FINSI;
|FPRC;

|| Carga del Historico

|PROCESO GrabaTemporal2;
     #986#0 = #labtraba#0;          || empresa
     #986#1 = #labtraba#1;          || Codigo Trabajador
     #986#2 = #0#4;
     #986#3 = nMes;

     |LEE 101#986=;
     |SI FSalida ! 0;
          |DDEFECTO #986;
          #986#0 = #labtraba#0;     || empresa
          #986#1 = #labtraba#1;     || Codigo Trabajador
          #986#2 = #0#4;
          #986#3 = nMes;
          |GRABA 020#986b;
     |FINSI;

     #986#4 = #labtraba#7;    || NIF
     #986#5 = nTipoRel;       || tipo de relacion laboral
     #986#6 = #labtraba#131;  || transformacion a indefinido S/N

     |SI swDeDonde = 1;       || desde historico, mes
          #986#7 = #986#7 + nImporteBaseI;
          #986#8 = #986#8 + nImporteRetencion;
          #986#9 = #986#9 + nImporteSSTrabaja;
     |FINSI;
     |SI swDeDonde = 2;       || desde historico, atrasos
          #986#10 = #986#10 + nImporteBaseI;
          #986#11 = #986#11 + nImporteRetencion;
          #986#12 = #986#12 + nImporteSSTrabaja;
     |FINSI;
     |SI swDeDonde = 3;       || desde historico, atrasos
          #986#13 = #986#13 + nImporteBaseI;
          #986#14 = #986#14 + nImporteRetencion;
          #986#15 = #986#15 + nImporteSSTrabaja;
     |FINSI;
     |SI swDeDonde = 4;       || desde atrasos mensual
          #986#16 = #986#16 + nImporteBaseI;
          #986#17 = #986#17 + nImporteRetencion;
          #986#18 = #986#18 + nImporteSSTrabaja;
     |FINSI;

     |GRABA 020#986a;
     |LIBERA #986;
|FPRC;

|PROCESO GrabaTemporal1;
     #985#0     = #labtraba#0;          || empresa
     #985#6     = #labtraba#7;          || NIF
     |LEE 101#985=;
     |SI FSalida ! 0;
          |DDEFECTO #985;
          #985#0     = #labtraba#0;     || empresa
          #985#6     = #labtraba#7;     || NIF (a¤o)
          |GRABA 020#985b;
     |FINSI;

     #985#1     = #0#4;            || periodo (a¤o)
     #985#2     = 00;              || periodo (¨mes?)
     #985#3     = 000;             || modelos
     #985#4     = 00;              || complemento
     #985#5     = 00;              || actividad
     #985#6     = #labtraba#7;
     #985#7     = #0#4;            || A¤o Devengo
     #985#8     = "";              || clave
     #985#9     = "";              || subclave
     #985#10    = nTipoRel;
     #985#11    = aNombreTrabajador;

     #985#12 = #985#12 + nImporteBaseI;           || campos totales
     #985#13 = #985#13 + nImporteRetencion;
     #985#14 = #985#14 + nImporteSSTrabaja;

     |SI swDeDonde = 1;                   || del historico
          #985#25 = #985#25 + nImporteBaseI;
          #985#26 = #985#26 + nImporteRetencion;
          #985#27 = #985#27 + nImporteSSTrabaja;
     |FINSI;
     |SI swDeDonde = 2;                   || del historico atrasos
          #985#34 = #985#34 + nImporteBaseI;
          #985#35 = #985#35 + nImporteRetencion;
          #985#36 = #985#36 + nImporteSSTrabaja;
     |FINSI;
     |SI swDeDonde = 3;                   || del mensual
          #985#28 = #985#28 + nImporteBaseI;
          #985#29 = #985#29 + nImporteRetencion;
          #985#30 = #985#30 + nImporteSSTrabaja;
     |FINSI;
     |SI swDeDonde = 4;                   || de atrasos
          #985#37 = #985#37 + nImporteBaseI;
          #985#38 = #985#38 + nImporteRetencion;
          #985#39 = #985#39 + nImporteSSTrabaja;
     |FINSI;

     |SI swDeDonde ! 2;
          |SI #labtraba#42 = "H";
               #985#14 = #nomir003#11;
          |FINSI;
     |FINSI;

     #985#20 = nEl20;
     #985#21 = aClaveDia;
     #985#22 = nVivienda;
     #985#40 = eaVivienda1;
     #985#41 = eaVivienda2;

     |GRABA 020#985a;
     |LIBERA #985;

     nImporteBaseI     = 0;
     nImporteRetencion = 0;
     nImporteSSTrabaja = 0;
     ded_not           = 0;
|FPRC;

|PROCESO GrabaTemporal0;
     #984#0 = #labempre#0;
     |LEE 101#984=;
     |SI FSalida ! 0;
          #984#0 = #labempre#0;
          #984#1 = #labempre#2;
          |GRABA 020#984n;
     |FINSI;
     |LIBERA #984;
|FPRC;

|PROCESO nomir012;
     |SI #nomir012#100 ! 0; |Y #nomir012#85 = #nomir012#86;
          nVivienda = 1;
          eaVivienda1 = #nomir012#99;
          eaVivienda2 = #nomir012#110;
          |ERROR10;
     |FINSI;
|FPRC;
/*
|DEFBUCLE nomir012;
     #nomir012#1;
     ;
     #labtraba#0, #labtraba#1, #0#4, 01;
     #labtraba#0, #labtraba#1, #0#4, 12;
     ;
     NULL, nomir012;
|FIN;
*/

|DEFBUCLE nomir012;
     #nomir012#3;
     ;
     aDni, #labtraba#0, #0#4, 01;
     aDni, #labtraba#0, #0#4, 12;
     ;
     NULL, nomir012;
|FIN;

|PROCESO DatosTrab;
     nEmpresa = #labtraba#0;
     nCentro = #labtraba#77;

     nEl20     = 0;
     aClaveDia = "";
     nVivienda = 0;

     aNombreTrabajador = #labtraba#2;
     aDni              = #labtraba#7;
     nTipoRel          = #labtraba#111;

     aClave            = #labtraba#99;
     aSubClave         = #labtraba#102;
     aEspecie          = #labtraba#105;
     aRepercute        = #labtraba#108;

     #215#0 = #labtraba#0;
     #215#1 = #labtraba#77;
     |LEE 000#215=;
     |SI FSalida ! 0;
         #215#2 = 0;       || (%) a cero
     |FINSI;
     notaria = #215#2;

     |DDEFECTO #nomir003;
     #nomir003#0 = #labtraba#7;
     #nomir003#40 = #0#4;
     #nomir003#41 = #labtraba#0;
     |LEE 000#nomir003.=;
     |SI FSalida = 0;
          |SI #nomir003#124 = "S"; |O #nomir003#126 = "S";
               nVivienda = 1;
          |FINSI;
     |FINSI;

     || esto porque puede ser que tuviera vivienda en algun momento del a¤o
     || y luego se la quitaran
     || se cargan las variables eaVivienda1 y eaVivienda2, que despues se
     || usaran en la estimacion en el nomir001

     eaVivienda1 = "";
     eaVivienda2 = "";

     |BUCLE nomir012;
|FPRC;

|PROCESO nomcalac;
|ACABA;
     aMensa0 = "                             ";
     aMensa1 = "  Leyendo datos de atrasos   ";
     aAlfa1 = #nomcalac#0; |QBF aAlfa1; aAlfa1 = ("00000" + aAlfa1) % -105;
     aAlfa2 = #nomcalac#1; |QBF aAlfa2; aAlfa2 = ("00000" + aAlfa2) % -105;
     aMensa2 = "   Trabajador " + aAlfa1 + "." + aAlfa2 + "    ";
     |ATRI R;
     |PINTA 1425, aMensa0;
     |PINTA 1525, aMensa1;
     |PINTA 1625, aMensa2;
     |PINTA 1725, aMensa0;
     |ATRI 0;

     #labempre#0 = #nomcalac#0;    || leo empresa
     |LEE 000#labempre.=;
     |SI FSalida ! 0;  |DDEFECTO #labempre; |FINSI;

     #labtraba#0 = #nomcalac#0;    || leo trabajador
     #labtraba#1 = #nomcalac#1;
     |LEE 000#labtraba.=;
     |SI FSalida ! 0; |DDEFECTO #labtraba; |FINSI;

     |SI #labtraba#99 = "E";
          |ACABA;
     |FINSI;

     |HAZ DatosTrab;

     nImporteBaseI = #nomcalac#14 + #nomcalac#16 + #nomcalac#69 + #nomcalac#70;
     nImporteRetencion = #nomcalac#15 + #nomcalac#17 + #nomcalac#71 + #nomcalac#72;

     ded_not = (#nomcalac#48 < notaria) ! 2;
     nImporteSSTrabaja = (ded_not + #nomcalac#49 + #nomcalac#50 + #nomcalac#51);

     nMes = #nomcalac#2;
     swDeDonde = 4;           || de atrasos mensual

     |HAZ GrabaTemporal0;

     |HAZ GrabaTemporal2;

     |HAZ GrabaTemporal1;
|FPRC;

|DEFBUCLE nomcalac;
     #nomcalac#1;
     ;
     #0#0, #0#2, 01, 5;
     #0#1, #0#3, 12, 5;
     ;
     NULL, nomcalac;
|FIN;

|PROCESO nomcalca;
     aMensa0 = "                             ";
     aMensa1 = "  Leyendo datos del mensual  ";
     aAlfa1 = #nomcalca#0; |QBF aAlfa1; aAlfa1 = ("00000" + aAlfa1) % -105;
     aAlfa2 = #nomcalca#1; |QBF aAlfa2; aAlfa2 = ("00000" + aAlfa2) % -105;
     aMensa2 = "   Trabajador " + aAlfa1 + "." + aAlfa2 + "    ";
     |ATRI R;
     |PINTA 1425, aMensa0;
     |PINTA 1525, aMensa1;
     |PINTA 1625, aMensa2;
     |PINTA 1725, aMensa0;
     |ATRI 0;

     #labempre#0 = #nomcalca#0;    || leo empresa
     |LEE 000#labempre.=;
     |SI FSalida ! 0;  |DDEFECTO #labempre; |FINSI;

     #labtraba#0 = #nomcalca#0;    || leo trabajador
     #labtraba#1 = #nomcalca#1;
     |LEE 000#labtraba.=;
     |SI FSalida ! 0; |DDEFECTO #labtraba; |FINSI;

     |SI #labtraba#99 = "E";
          |ACABA;
     |FINSI;

     #986#0 = #nomcalca#0;          || empresa
     #986#1 = #nomcalca#1;          || Codigo Trabajador
     #986#2 = #0#4;
     #986#3 = #nomcalca#2;

     |LEE 000#986=;
     |SI FSalida = 0;
          |SI #986#7 ! 0;
               || ya hay datos en el historico, no puede haber datos a la vez
               || en mensual y en historico, debe ser una nomina que no
               || corresponda al periodo que calculamos

               |ACABA;

          |FINSI;
     |SINO;
          nMes = #nomcalca#2;
          nAny = #0#4;
          |HAZ topemes_plb;

          aAlfa1 = nTopemes;
          aAlfa2 = #nomcalca#2; |QBF aAlfa2; aAlfa2 = ("00" + aAlfa2) % -102;
          aAlfa3 = #0#4;
          aFechaRef = aAlfa1 + "." + aAlfa2 + "." + aAlfa3;
          fFechaRef = aFechaRef;
          aFechaAlta = #nomcalca#4; |QBF aFechaAlta;
          |SI aFechaAlta ! ""; |Y aFechaAlta ! "..........";
               fFechaAlta = aFechaAlta;
               |SI fFechaAlta > fFechaRef;
                    |ACABA;
               |FINSI;
          |FINSI;

          aFechaRef = "01" + (aFechaRef % 308);
          fFechaRef = aFechaRef;
          aFechaBaja = #nomcalca#5; |QBF aFechaBaja;
          |SI aFechaBaja ! ""; |Y aFechaBaja ! "..........";
               fFechaBaja = aFechaBaja;
               |SI fFechaBaja < fFechaRef;
                    |ACABA;
               |FINSI;
          |FINSI;
     |FINSI;

     |HAZ DatosTrab;

     nImporteBaseI = #nomcalca#14 + #nomcalca#16 + #nomcalca#69 + #nomcalca#70;
     nImporteRetencion = #nomcalca#15 + #nomcalca#17 + #nomcalca#71 + #nomcalca#72;

     ded_not = (#nomcalca#48 < notaria) ! 2;
     nImporteSSTrabaja = (ded_not + #nomcalca#49 + #nomcalca#50 + #nomcalca#51);

     nMes = #nomcalca#2;
     |SI #nomcalca#3 ] 0; |Y #nomcalca#3 [ 2;
          swDeDonde = 3;           || del mensual
     |FINSI;

     |HAZ GrabaTemporal0;

     |HAZ GrabaTemporal2;

     |HAZ GrabaTemporal1;
|FPRC;

|DEFBUCLE nomcalca;
     #nomcalca#1;
     184;
     #0#0, #0#2, 12, 0, #0#4;
     #0#1, #0#3, 12, 9, #0#4;
     ;
     NULL, nomcalca;
|FIN;

|PROCESO nomhicca;
     aMensa0 = "                             ";
     aMensa1 = " Leyendo datos del hist¢rico ";
     aAlfa1 = #nomhicca#0; |QBF aAlfa1; aAlfa1 = ("00000" + aAlfa1) % -105;
     aAlfa2 = #nomhicca#1; |QBF aAlfa2; aAlfa2 = ("00000" + aAlfa2) % -105;
     aMensa2 = "   Trabajador " + aAlfa1 + "." + aAlfa2 + "    ";
     |ATRI R;
     |PINTA 1425, aMensa0;
     |PINTA 1525, aMensa1;
     |PINTA 1625, aMensa2;
     |PINTA 1725, aMensa0;
     |ATRI 0;

     #labempre#0 = #nomhicca#0;    || leo empresa
     |LEE 000#labempre.=;
     |SI FSalida ! 0;  |DDEFECTO #labempre; |FINSI;

     #labtraba#0 = #nomhicca#0;    || leo trabajador
     #labtraba#1 = #nomhicca#1;
     |LEE 000#labtraba.=;
     |SI FSalida ! 0; |DDEFECTO #labtraba; |FINSI;

     |SI #labtraba#99 = "E";
          |ACABA;
     |FINSI;

     |HAZ DatosTrab;

     nImporteBaseI = #nomhicca#14 + #nomhicca#16 + #nomhicca#108 + #nomhicca#109;
     nImporteRetencion = #nomhicca#15 + #nomhicca#17 + #nomhicca#110 + #nomhicca#111;

     ded_not = (#nomhicca#48 < notaria) ! 2;
     nImporteSSTrabaja = (ded_not + #nomhicca#49 + #nomhicca#50 + #nomhicca#51);

     nMes = #nomhicca#2;
     |SI #nomhicca#3 ] 0; |Y #nomhicca#3 [ 2;
          swDeDonde = 1;           || del historico, normal
     |FINSI;
     |SI #nomhicca#3 ] 5; |Y #nomhicca#3 [ 20;
          swDeDonde = 2;           || del historico, atrasos
     |FINSI;

     |HAZ GrabaTemporal0;

     |HAZ GrabaTemporal2;

     |HAZ GrabaTemporal1;
|FPRC;

|DEFBUCLE nomhicca;
     #201#1;
     ;
     #0#0, #0#2, nEjer, 01,  0;
     #0#1, #0#3, nEjer, 12, 99;
     ;
     NULL, nomhicca;
|FIN;

|PROCESO Historico;
     aAlfa = #0#4;
     aAlfa = aAlfa % -102;
     nEjer = aAlfa;

     |BUCLE nomhicca;
     |BUCLE nomcalca;
     |BUCLE nomcalac;
     ||HAZ Estimado;
|FPRC;

|PROCESO dsmow085;
     nTrabajador = nTrabajador + 1;

     aMensa0 = "                             ";
     aMensa1 = " Calculando retenciones AEAT ";
     aAlfa1 = nTrabajador; |QBF aAlfa1;
     aAlfa2 = nTotalTrab; |QBF aAlfa2;
     aMensa2 = "    Trabajador " + aAlfa1 + " de " + aAlfa2 + "             ";
     aMensa2 = aMensa2 % 129;
     |ATRI R;
     |PINTA 1425, aMensa0;
     |PINTA 1525, aMensa1;
     |PINTA 1625, aMensa2;
     |PINTA 1725, aMensa0;
     |ATRI 0;

     nDiferencias     = 1;
     nVEmp            = #985#0;
     aVDNI            = #985#6;
     nVAny            = #985#1;
     nCuotNueva       = 0;
     nBaseHist        = #985#12;
     nSSHist          = #985#14;
     nCuotaMax        = 0;
     nCuotaMin        = 0;
     nVivienda        = 0;
     swVivienda       = #985#22;   || aqui me viene cargado si tengo vivienda
                                   || o no del #dsm190li#64
     eaVivienda1 = #985#40;
     eaVivienda2 = #985#41;
     nElAnyIRPF = #0#4;

     |SUB_EJECUTA_NP ":laboral/nomir011";

     nGraba  = 0;
     || #985#18 = nCuotNueva;
     #985#18 = #985#18 + nCuotNueva;
     #985#19 = #985#13 - #985#18;

     |SI nVivienda ! 0;
          #985#19 = 0;
          #985#22 = nVivienda;
          #985#23 = nCuotaMin;
          #985#24 = nCuotaMax;
          |SI #985#13 < nCuotaMin;
               #985#19 = #985#13 - nCuotaMin;
          |FINSI;
          |SI #985#13 > nCuotaMax;
               #985#19 = #985#13 - nCuotaMax;
          |FINSI;
     |SINO;
          #985#22 = 0;
     |FINSI;

     |SI #985#14 = 0;  |Y #985#21 = "H";
         #985#14 = #985#20;
     |FINSI;

     |SI #985#12 ! #985#15;  nGraba = 1;  nHay = 1;  |FINSI;
     |SI #985#13 ! #985#16;  nGraba = 1;  nHay = 1;  |FINSI;
     |SI #985#14 ! #985#17;  nGraba = 1;  nHay = 1;  |FINSI;
     |SI #985#19 ! 0;        nGraba = 1;  nHay = 1;  |FINSI;
     |SI #985#22 ! 0;        nGraba = 1;  nHay = 1;  |FINSI;

     |SI nGraba = 1;
         |GRABA 020#985a;
     |SINO;
         |BORRA 020#985a;
     |FINSI;
     |LIBERA #985;
|FPRC;

|DEFBUCLE dsmow085;
     #985#1;
     ;
     INICIO;
     FINAL;
     be;
     NULL, dsmow085;
|FIN;

|PROCESO dsmow085cuenta;
     nTotalTrab = nTotalTrab + 1;
|FPRC;

|DEFBUCLE dsmow085cuenta;
     #985#1;
     ;
     INICIO;
     FINAL;
     be;
     NULL, dsmow085cuenta;
|FIN;

|PROCESO Impresion;
     |SI #985#19 [ #nomow134#2; |O #985#19 ] #nomow134#3;
          |PRINT;
     |FINSI;
|FPRC;

|DEFBUCLE Impresion;
     #985#1;
     ;
     #nomow084#0, "            ";
     #nomow084#0, "zzzzzzzzzzzz";
     ;
     NULL, Impresion;
|FIN;

|PROCESO nomow084;
     |BUCLE Impresion;
|FPRC;

|DEFBUCLE nomow084;
     #984#1;
     ;
     #nomow134#0;
     #nomow134#1;
     ;
     NULL, nomow084;
|FIN;

|PROCESO Boton5;
     |ESTADO_CONTROL nBoton5, "HIDE";

     nEmpresa = #984#0;
     aNif = #985#6;

     |PUSHV 09102170;
     |PINPA #0#nomow134;
     |PINDA #0#nomow134;

     #nomow134#0 = #984#0;
     #nomow134#1 = #984#0;

     |PINTA #nomow134#0;
     |PINTA #nomow134#1;

     |ENDATOS #1#nomow134;
     |SI FSalida ! 0;
          |POPV;
          |ESTADO_CONTROL nBoton5, "SHOW";
          |ACABA;
     |FINSI;
     |POPV;

     Impresora = "CrystalReport";
     |IMPRE -1;

     |INFOR "nomow085";
     |BUCLE nomow084;

     |PIEINF;
     |FININF;
     |FINIMP;

     #984#0 = nEmpresa;
     |LEE 000#984.=;
     |PONCONTROL 23, "si";

     #985#0 = nEmpresa;
     #985#6 = aNif;
     |LEE 000#985.=;
     |PONCONTROL 23, "si";

     |ESTADO_CONTROL nBoton5, "SHOW";
|FPRC;

|PROCESO Baja986;
     #986#4 = #985#6;
     #986#2 = #0#4;
     #986#3 = 01;
|FPRC;

|PROCESO Alta986;
     #986#4 = #985#6;
     #986#2 = #0#4;
     #986#3 = 12;
|FPRC;

|PROCESO Consulta; |TIPO 11;
     |SI FSalida = 13;
          |FIN_CONTROL_WINDOWS nBoton5;
          |PUSHV 05012380;
          |ENTLINEAL #2#986, -3, 4, 22, 1, Baja986, Alta986;
          |POPV;
          |CONTROL_SIMPLE 0, "BOTON,  Imprimir Diferencias ", 2149, 2169, Boton5;
          nBoton5 = FSalida;
          |PTEC 808;
     |FINSI;
|FPRC;

|PROCESO Baja985;
     #985#0 = #984#0;
     #985#6 = "            ";
|FPRC;

|PROCESO Alta985;
     #985#0 = #984#0;
     #985#6 = "zzzzzzzzzzzz";
|FPRC;

|PROCESO LineasMod;
     |ENTLINEAL #1#985, -2, 4, 22, 1, Baja985, Alta985;
|FPRC;

|PROCESO LineasCon; |TIPO 14;
     |ENTLINEAL #1#985, -2, 7, 22, 1, Baja985, Alta985;
     #985#0 = #985#0;
     #985#6 = "            ";
     |LEE 000#985];
     |PINDA #0#985;
|FPRC;

|PROCESO Baja984;
     #984#0 = 00001;
|FPRC;

|PROCESO Alta984;
     #984#0 = 99999;
|FPRC;

|PROCESO Diferencias;
     aFichero = ("84" + Usuario + "zzzzzzzz") % 108;
     |NOME_DAT #984 aFichero;
     |ABRE #984;  |CIERRA #984;  |DELINDEX #984;

     aFichero = ("85" + Usuario + "zzzzzzzz") % 108;
     |NOME_DAT #985 aFichero;
     |ABRE #985;  |CIERRA #985;  |DELINDEX #985;

     aFichero = ("86" + Usuario + "zzzzzzzz") % 108;
     |NOME_DAT #986 aFichero;
     |ABRE #986;  |CIERRA #986;  |DELINDEX #986;

     nSwMasivo = 1;

     |ABRE #984;
     |ABRE #985;
     |ABRE #986;
     |ABRE #labempre;
     |ABRE #labcentr;
     |ABRE #labtraba;
     |ABRE #nomir012;
     |ABRE #nomir003;
     |ABRE #215;

     |PUSHV 13201860;

     |HAZ Historico;

     |BUCLE dsmow085cuenta;
     |BUCLE dsmow085;

     |POPV;

     |PUSHV 0501 2380;

     |PINPA #0#985;

     |CONTROL_SIMPLE 0, "BOTON,  Imprimir Diferencias ", 2149, 2169, Boton5;
     nBoton5 = FSalida;

     |ENTLINEAL #1#984, -1, 4, 22, 1, Baja984, Alta984;

     |FIN_CONTROL_WINDOWS nBoton5;

     |POPV;

     |CIERRA #215;
     |CIERRA #nomir003;
     |CIERRA #nomir012;
     |CIERRA #labtraba;
     |CIERRA #labcentr;
     |CIERRA #labempre;
     |CIERRA #986;
     |CIERRA #985;
     |CIERRA #984;

     |DELINDEX #984;
     |DELINDEX #985;
     |DELINDEX #986;
|FPRC;

|PROGRAMA;
     |CLS;
     |PINPA #0#0;
     |PINDA #0#0;
     |ENDATOS #1#0;
     |SI FSalida = 0;
          eaVengoDe = "nomesfin";
          |HAZ Diferencias;
     |FINSI;
|FPRO;
