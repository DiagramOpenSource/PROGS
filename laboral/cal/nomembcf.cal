|FICHEROS;
     nomembcf #0;
     temp0001@ #500;
     labtraba;
     nomconli;
     nomembca;
|FIN;

|VARIABLES;
     aAlfa = "";
     nModo = 0;
     nPos1 = 0;
     nPos2 = 0;
     nEmp = 0;
     nTra = 0;
     nEmpCop = 0;
     nTraCop = 0;
|FIN;

|PROCESO CopiaCon;
     #nomembcf#0 = nEmp;
     #nomembcf#1 = nTra;
     #nomembcf#2 = #nomembcf#2;
     |GRABA 000#nomembcf.n;
|FPRC;

|DEFBUCLE CopiaCon;
     #nomembcf#1;
     ;
     nEmpCop, nTraCop, 101;
     nEmpCop, nTraCop, 999;
     ;
     NULL, CopiaCon;
|FIN;

|PROCESO BorraCon;
     |BORRA 000#nomembcf.a;
|FPRC;

|DEFBUCLE BorraCon;
     #nomembcf#1;
     ;
     nEmp, nTra, 101;
     nEmp, nTra, 999;
     ;
     NULL, BorraCon;
|FIN;

|PROCESO Copia;
     |PUSHV 18512169;
     |BLANCO 18512169;
     |CUADRO 17492271;
     |PINTA 1951, "                    ";
     |PINTA 2051, "                    ";
     |PINTA 2151, "                    ";
     |ATRI R;
                || 5        6
                || 1234567890123456789
     |PINTA 1851, "COPIA DE CONCEPTOS ";
     |ATRI 0;
     |PINTA 1951, "Empresa ....:      ";
     E_sup = 99999; E_inf = "";
     nEmpCop = 1965 ? 0;
     |PINTA 2051, "Trabajador .:      ";
     E_sup = 99999; E_inf = "";
     nTraCop = 2065 ? 0;
     |PINTA 2151, "Correcto S/N:      ";
     E_sup = 1; E_inf = "SN";
     aAlfa = 2165 ? 0;

     |SI aAlfa = "S";
          nEmp = #nomembca#0;
          nTra = #nomembca#1;
          |BUCLE BorraCon;
          |BUCLE CopiaCon;
          #nomembcf#0 = nEmp;
          #nomembcf#1 = nTra;
          #nomembcf#2 = 101;
          |LEE 000#nomembcf.];
          |PONCONTROL 23, "si";
     |FINSI;

     |POPV;
|FPRC;

|PROCESO Comprueba; |TIPO 0;
     |ABRE #nomconli;
     #nomconli#0 = #labtraba#87;
     #nomconli#2 = #0#2;
     |LEE 000#nomconli.=;
     |SI FSalida = 0; |O #0#2 > 900;
          |SI #0#2 < 900;
               #0#3 = #nomconli#6;
          |SINO;
               |SI #0#2 = 906; #0#3 = "Anticip1"; |FINSI;
               |SI #0#2 = 907; #0#3 = "Anticip2"; |FINSI;
               |SI #0#2 = 908; #0#3 = "Especie1"; |FINSI;
               |SI #0#2 = 923; #0#3 = "Especie2"; |FINSI;
               |SI #0#2 = 924; #0#3 = "Especie3"; |FINSI;
               |SI #0#2 = 926; #0#3 = "Anticip3"; |FINSI;
          |FINSI;
          |PINTA #0#3;
     |SINO;
          aAlfa = "    ATENCION: Concepto no Definido en convenio. Pulse F5 ";
          aAlfa = aAlfa + "para consultar.";
          |MENAV aAlfa;
          |ERROR;
     |FINSI;
     |CIERRA #nomconli;
|FPRC;

|PROCESO BuscaCon; |TIPO 11;
     |SI FSalida = 13;
          aAlfa = "SELECT nomconli.2,nomconli.6 FROM nomconli INTO tm" + Usuario;
          aAlfa = aAlfa + ".def WHERE 0 == " + #labtraba#87;
          |SQL aAlfa;
          |SI FSalida ! 0;
               |MENAV "    No hay definidos conceptos en el convenio";
               |ERROR;
               |ACABA;
          |FINSI;
          |DBASE aAlfa; |QBF aAlfa; aAlfa = aAlfa + "def/tm" + Usuario + ".mas";
          |CARGA_DEF aAlfa , temp0001@;
          |SI FSalida = 0;
               aAlfa = "tm" + Usuario; |NOME_DAT #500 aAlfa;
               |ABRE #temp0001@;
               nModo = 2 + 4 + 8 + 16;
               nPos1 = 1250; nPos2 = 2380;
               |C_V #temp0001@#0, 1, "S";
               |C_V #temp0001@#1, 1, "S";
               |LINEAL_SIMPLE #1#temp0001@,nModo, nPos1, nPos2, NULL, NULL, NULL;
               |SI FSalida = 0;
                    #0#2 = #temp0001@#0;
                    |PINTA #0#2;
                    #0#3 = #temp0001@#1;
                    |PINTA #0#3;
               |SINO;
                    |ERROR;
               |FINSI;
               |CIERRA #temp0001@;
               |DELINDEX #temp0001@;
               |DESCARGA_DEF #temp0001@;
          |FINSI;
          |DBASE aAlfa; |QBF aAlfa; aAlfa = aAlfa + "def/tm" + Usuario + ".mas";
          |FBORRA aAlfa;
          |ACABA;
     |FINSI;
     |SI FSalida = 14;
          |HAZ Copia;
     |FINSI;
|FPRC;
