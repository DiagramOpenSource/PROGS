|FICHEROS;
     semrapid #0;
     labempre #1;
     labtraba #2;
     semtraba #3;
     semtrcon #4;
     semconce #5;
     semraptm #6,MANTE;
     semequiv #7;
     semcntcu;
|FIN;

|VARIABLES;
     aAlfa1 = "";
     aAlfa2 = "";
     aAlfa3 = "";
     aAlfa4 = "";
     nNume1 = 0;
     nNume2 = 0;
     nNume3 = 0;
     nNume4 = 0;
     nPara1 = 0;
     nPara2 = 0;
     nPara3 = 0;
     nPara4 = 0;
     fFech1 = @;
     fFech2 = @;
     fFech3 = @;
     fFech4 = @;
     nFEstado = 0;
     nSeleccio = 0;
     nModo = 0;

     alfa1 = "";
     alfa2 = "";
     alfa3 = "";
     alfa4 = "";
     nume1 = 0;
     nume2 = 0;
     nume3 = 0;
     nume4 = 0;
     fech1 = @;
     fech2 = @;
     fech3 = @;
     fech4 = @;
     sdia = 0;
     smas = 0;
     sdife = 0;
     srest = 0;

     nCampo = 0;
     nCuantos = 0;
     nOpcion = 0;
 {-1}pMenu = "";
     pMenu1 = "2400";
     pMenu2 = "2";
     pMenu3 = " Elija opcion: ";
     pMenu4 = "ARC";
     pMenu5 = " Actualizar ";
     pMenu6 = " Revisar ";
     pMenu7 = " Cancelar ";
|FIN;
____________________________________/ DESDE PANTALLA
|PROCESO Aviso; |TIPO 0;
     |C_M #0#9, 0, aAlfa1;
     |SI aAlfa1 = "S";|Y #0#9 = "T";
          |MENAV "0000IMPORTANTE: Con Tipo T se perdera el desglose manual existente.";
          |AVISO;
          |CONFI "0000NContinuar?";
          |SI FSalida ! 0;
               |ERROR;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Partida; |TIPO 7;
     aAlfa1 = #0#6; aAlfa1 = aAlfa1 % 402;
     aAlfa2 = #0#7; aAlfa2 = aAlfa2 % 402;
     |SI aAlfa1 ! aAlfa2;
          |C_M #0#9, 1, "S";
     |SINO;
          #0#9 = "T";    |PINTA #0#9;
          |C_M #0#9, 1, "N";
     |FINSI;
|FPRC;

|PROCESO Defecto; |TIPO 7;    || calcula el a¤o por defecto segun sistema
     fFech1 = ~;
     aAlfa1 = fFech1;
     aAlfa1 = aAlfa1 % -104;
     #0Campo = aAlfa1;
     |PINTA #0Campo;
|FPRC;

|PROCESO mirasem; |TIPO 7;
    alfa1 = #0#4;
    alfa1 = "01.01." + alfa1;
    fech1 = alfa1;            || primero saca el dia de la semana
    fech2 = fech1 % 1;        ||/que fue el uno de enero
    alfa1 = fech2 % 11;

|SI alfa1 = "Lunes";     sdia=1; smas=1; |FINSI;
|SI alfa1 = "Martes";    sdia=7; smas=1; |FINSI;
|SI alfa1 = "Miercoles"; sdia=6; smas=1; |FINSI;
|SI alfa1 = "Jueves";    sdia=5; smas=1; |FINSI; ||saca el dia del mes que fue
|SI alfa1 = "Viernes";   sdia=4; smas=1; |FINSI; ||el primer lunes del a¤o
|SI alfa1 = "Sabado";    sdia=3; smas=1; |FINSI;
|SI alfa1 = "Domingo";   sdia=2; smas=1; |FINSI;

    alfa1 = ("00" + sdia) % -102;  || construye el primer lunes del a¤o
    fech1 = ~;                     ||/como fecha inicial de calculo
    alfa2 = fech1 % -104;
    alfa1 = alfa1 + ".01." + alfa2;
    fech1 = alfa1;
    nume1 = fech1;                 || calcula dias hasta primer lunes a¤o

    fech1 = ~;                     || calcula dias hasta fecha actual
    nume2 = fech1;

    sdife = nume2 - nume1;         || calcula la diferencia desde el primer
    |SI sdife ] 0;
        srest = sdife $ 7;           || lunes hasta la fecha actual,la redondea,
        sdife = sdife + (7 - srest); || la divide por 7 y le suma 1 por la
        #0Campo = (sdife / 7) + smas;|| primera semana (en su caso !!!)
    |SINO;
        #0Campo = 1;
    |FINSI;

    |ABRE #semcntcu;     |LEE 000#semcntcu.p;     |CIERRA #semcntcu;

    #0Campo = #0Campo - #semcntcu#3;
    |SI #0Campo = 0; #0Campo = 52; |FINSI;
    |PINTA #0Campo;
|HAZ deshas;
|FPRC;

|PROCESO deshas; |TIPO 0;   || calcula segun semana, desde dia hasta dia
    |ABRE #semcntcu;     |LEE 000#semcntcu.p;     |CIERRA #semcntcu;

    nume2 = (#0#5 + #semcntcu#3) - smas;
    nume2 = (nume2 * 7) + (sdia - 1);   || dias hasta
    nume1 = nume2 - 6;                  || dias desde

    nume3 = #0#4;
    nume3 = nume3 - 1;
    alfa1 = "31.12." + nume3;    || saca los dias que hay hasta el 31.12
    fech1 = alfa1;
    nume3 = fech1;

    nume1 = nume1 + nume3;
    fech1 = nume1;         || fecha desde
    #0#6 = fech1;

    nume2 = nume2 + nume3;
    fech1 = nume2;         || fecha hasta
    #0#7 = fech1;

|PINTA #0#6;
|PINTA #0#7;
|FPRC;
____________________________________/ GESTION PANTALLA TEMPORAL
|PROCESO Busqueda; |TIPO 11;
     |SI FSalida = 12;
          |CONSULTA_CLAVE #2#semraptm;
          |PTEC 802;
     |FINSI;
|FPRC;

|PROCESO Concepto; |TIPO 0;
     |SI Campo = 2; nCampo = 112;  |FINSI;
     |SI Campo = 3; nCampo = 107;  |FINSI;
     |SI Campo = 4; nCampo = 110;  |FINSI;
     |SI Campo = 5; nCampo = 109;  |FINSI;
     |SI Campo = 6; nCampo = 306;  |FINSI;
     |SI Campo = 7; nCampo = 307;  |FINSI;

     |ABRE #semconce;
     #semconce#0 = nCampo;
     |LEE 000#semconce.=;
     |SI FSalida ! 0;
          aAlfa1 = "0000Concepto [" + nCampo + "] inaccesible.";
          |MENAV aAlfa1;
     |FINSI;
     |CIERRA #semconce;
|FPRC;

|PROCESO SiExiste; |TIPO 7;
     |ABRE #semequiv;
     |SELECT #2#semequiv;
     #semequiv#1 = #semraptm#0;
     #semequiv#2 = #semraptm#1;
     |LEE 000#semequiv.=;
     |SI FSalida ! 0;
          |MENAV "0000Trabajador inexistente en equivalencias.";
          |PTEC 807;
     |FINSI;
     |SELECT #1#semequiv;
     |CIERRA #semequiv;
|FPRC;

|PROCESO Tipo13; |TIPO 13;
     |ABRE #labtraba;
     #labtraba#0 = #semraptm#0;
     #labtraba#1 = #semraptm#1;
     |LEE 000#labtraba.=;
     |CIERRA #labtraba;

     |ABRE #semequiv;
     |SELECT #2#semequiv;
     #semequiv#1 = #labtraba#0;
     #semequiv#2 = #labtraba#1;
     |LEE 000#semequiv.=;
     |CIERRA #semequiv;

     |PINTA 2208, #semequiv#4;
     |PINTA 2214, #labtraba#2;
     |PINTA 2257, #labtraba#7;
|FPRC;
____________________________________/ CARGA Y GRABACION TEMPORAL
|PROCESO semraptm_1;
     |DDEFECTO #labtraba;
     #labtraba#0 = #semraptm#0;
     #labtraba#1 = #semraptm#1;
     |LEE 000#labtraba.=;
     |SI FSalida ! 0;    |ACABA;   |FINSI;

     |DDEFECTO #semtraba;
     #semtraba#3 = #0#4;           || a¤o
     #semtraba#4 = #0#5;           || semana
     #semtraba#0 = #semraptm#0;    || empresa
     #semtraba#1 = #semraptm#1;    || trabajador
     |LEE 000#semtraba.=;
     |SI FSalida ! 0;
          #semtraba#2 = #labtraba#2;    || nombre trabajador
          |GRABA 020#semtraba.n;
     |FINSI;

     |PARA nPara1 = 2; |SI nPara1 [ 7; |HACIENDO nPara1 = nPara1 + 1;
          |SI nPara1 = 2; nCampo = 112;  |FINSI;
          |SI nPara1 = 3; nCampo = 107;  |FINSI;
          |SI nPara1 = 4; nCampo = 110;  |FINSI;
          |SI nPara1 = 5; nCampo = 109;  |FINSI;
          |SI nPara1 = 6; nCampo = 306;  |FINSI;
          |SI nPara1 = 7; nCampo = 307;  |FINSI;

          #semconce#0 = nCampo;
          |LEE 000#semconce.=;

          |DDEFECTO #semtrcon;
          #semtrcon#5 = #0#4;           || a¤o
          #semtrcon#6 = #0#5;           || semana
          #semtrcon#0 = #semraptm#0;    || empresa
          #semtrcon#1 = #semraptm#1;    || trabajador
          #semtrcon#2 = nCampo;         || concepto
          |LEE 101#semtrcon.=;
          |SI FSalida ! 0;
               |GRABA 020#semtrcon.b;
          |FINSI;
          #semtrcon#3 = #semconce#1;         || descripcion
          |SI #0#9 = "T";
               #semtrcon#4 = #semraptm#nPara1#;   || unidades TOTAL
               #semtrcon#8 = 0;
               #semtrcon#9 = 0;
          |FINSI;
          |SI #0#9 = "1";
               #semtrcon#8 = #semraptm#nPara1#;   || unidades mes1
               #semtrcon#4 = #semtrcon#8 + #semtrcon#9;     || total
          |FINSI;
          |SI #0#9 = "2";
               #semtrcon#9 = #semraptm#nPara1#;   || unidades mes2
               #semtrcon#4 = #semtrcon#8 + #semtrcon#9;     || total
          |FINSI;
          #semtrcon#7 = #semconce#2;         || tipo
          |GRABA 020#semtrcon.a;
          |SI FSalida = 0;
               nCuantos = nCuantos + 1;
          |FINSI;
          |LIBERA #semtrcon;
     |SIGUE;
|FPRC;

|DEFBUCLE semraptm;
     #semraptm#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, semraptm_1;
|FIN;

|PROCESO Actualizar;
     |ABRE #semtraba;
     |ABRE #semtrcon;
     |ABRE #semconce;
     |ABRE #labtraba;
     |BUCLE semraptm;
     |CIERRA #semtraba;
     |CIERRA #semtrcon;
     |CIERRA #semconce;
     |CIERRA #labtraba;
     |SI nCuantos = 0;
          |MENAV "0000Carga nula.";
     |SINO;
          aAlfa1 = "0000Actualizados [" + nCuantos + "] conceptos.";
          |MENAV aAlfa1;
     |FINSI;
|FPRC;

|PROCESO inf6;
     #semraptm#8 = 0;
     #semraptm#0 = 1;
     #semraptm#1 = 1;
|FPRC;

|PROCESO sup6;
     #semraptm#8 = 999999;
     #semraptm#0 = 99999;
     #semraptm#1 = 99999;
|FPRC;

|PROCESO semtrcon_1;
     |SI #semrapid#8 = "S";
          |DDEFECTO #semequiv;
          #semequiv#1 = #semtrcon#0;
          #semequiv#2 = #semtrcon#1;
          |LEE 000#semequiv.=;
     |SINO;
          #semequiv#5 = 0;
     |FINSI;

     |DDEFECTO #semraptm;
     #semraptm#8 = #semequiv#5;    || nro.orden
     #semraptm#0 = #semtrcon#0;    || empresa
     #semraptm#1 = #semtrcon#1;    || trabajador
     |LEE 101#semraptm.=;
     |SI FSalida ! 0;
          |GRABA 020#semraptm.b;
     |FINSI;

     nCampo = 0;
     |SI #semtrcon#2 = 112;   nCampo = 2;    |FINSI;
     |SI #semtrcon#2 = 107;   nCampo = 3;    |FINSI;
     |SI #semtrcon#2 = 110;   nCampo = 4;    |FINSI;
     |SI #semtrcon#2 = 109;   nCampo = 5;    |FINSI;
     |SI #semtrcon#2 = 306;   nCampo = 6;    |FINSI;
     |SI #semtrcon#2 = 307;   nCampo = 7;    |FINSI;

     |SI nCampo ! 0;
          |SI #0#9 = "T";
               #semraptm#nCampo# = #semtrcon#4;   || unidades TOTAL
          |FINSI;
          |SI #0#9 = "1";
               #semraptm#nCampo# = #semtrcon#8;   || unidades mes1
          |FINSI;
          |SI #0#9 = "2";
               #semraptm#nCampo# = #semtrcon#9;   || unidades mes2
          |FINSI;
     |FINSI;

     |GRABA 020#semraptm.a;
     |LIBERA #semraptm;
|FPRC;

|DEFBUCLE semtrcon;
     #semtrcon#1;
     ;
     #0#4, #0#5, #0#0, #0#2,   1;
     #0#4, #0#5, #0#1, #0#3, 999;
     ;
     NULL, semtrcon_1;
|FIN;

|PROCESO Tipo3; |TIPO 3;
     |MENSA "0000Cargando datos ya introducidos ...";
     aAlfa1 = Usuario;   |QBF aAlfa1;
     |NOME_DAT #6 aAlfa1;
     |DELINDEX #semraptm;
     |ABRE #semraptm;
     |ABRE #semequiv;
     |SELECT #2#semequiv;
     |BUCLE semtrcon;
     |CIERRA #semraptm;
     |CIERRA #semequiv;
     |BLIN 4;

     |PARA; |SI; |HACIENDO;
          |PINPA #0#semraptm;
          aAlfa1 = "Tipo " + #0#9;
          |ATRI P;  |PINTA 2320, aAlfa1;     |ATRI 0;
          |ENTLINEAL #1#semraptm, 3, 3, 20, 0, inf6, sup6;
          |AVISO;
          |MENU pMenu;
          nOpcion = FSalida;
          |SI nOpcion = 1;
               |HAZ Actualizar;
               |SAL;
          |FINSI;
          |SI nOpcion = 3;
               |MENAV "0000Carga anulada.";
               |SAL;
          |FINSI;
     |SIGUE;
|FPRC;
