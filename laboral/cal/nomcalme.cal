|FICHEROS;
     nomcalme #0;
     labtraba;
     nomcalm1 #101,MANTE;
     nomcalm2 #102,MANTE;
     nomequiv;
     nomvarca;
|FIN;

|VARIABLES;
     nTotalTrab = 0;
     nCampo = 0;
     nCampo1 = 0;
     &nMedia = 0;
     &nAltaFechaRef = 0;
     aAlfa = "";
     aAlfa1 = "";
     aAlfa2 = "";
     aAlfa3 = "";
     aAlfa4 = "";
     aAlfa5 = "";
     FEstado = 0;
     nDia = 0;
     nTrab = 0;
     fFecha = @;
     fFechaAlta = @;
     fFechaBaja = @;
     fFechaRef = @;
     fFechaIni = @;
     nFecha = 0;
     nFechaIni = 0;
     nFechaRef = 0;
     aParam = "";
     fFechaCon = @;
     &enEmp = 0;
     &enTra = 0;
     &eaNom = "";
     &eaNIF = "";
     &eaAlta = "";
     &eaBaja = "";
     &eaContrato = "";
     &enOpcion = 0;
     nContrato = 0;
     aContrato = "";
     &enParada = 0;
     nMesRef = 0;
     aMesRef = "";
     nAnyRef = 0;
     aAnyRef = "";
     nMesBusca = 0;
     aFecha = "";
     aFechaBaja = "";
     aMes = "";
     nMes = 0;
     nDias = 0;
|FIN;

|PROCESO Ayuda; |TIPO 7;
     |MENSA "     Pulse F5 para consultar los trabajadores en alta este dia";
|FPRC;

|PROCESO AyudaBlanco; |TIPO 7;
     |MENSA "                                                               ";
|FPRC;


|PROCESO inferior;
     #102#0 = fFechaCon;
     #102#1 = "          ";
|FPRC;

|PROCESO superior;
     #102#0 = fFechaCon;
     #102#1 = "zzzzzzzzzz";
|FPRC;

|PROCESO Trabajadores;
     |SI #101Campo ! Contenido;
          #101Campo = Contenido;
          |PINTA #101Campo;
     |FINSI;
     |SI FSalida = 13;
          aAlfa = #101Campo;

          aAlfa1 = aAlfa; aAlfa1 = (aAlfa1 % 402)
          nMesBusca = aAlfa1;

          aAlfa1 = #0#3; aAlfa1 = (aAlfa1 % 402)
          nMesRef = aAlfa1;

          aAlfa1 = #0#3; aAlfa1 = (aAlfa1 % -104)
          nAnyRef = aAlfa1;

          |SI nMesBusca > nMesRef;
               nAnyRef = nAnyRef - 1;
          |FINSI;
          aAnyRef = nAnyRef;

          aAlfa = (aAlfa % 102) + "." + (aAlfa % -102) + "." + aAnyRef;
          fFechaCon = aAlfa;

          |PUSHV 05012380;
          |ENTLINEAL #1#102, 2, 4, 22, 1, inferior, superior;
          |POPV;
     |FINSI;
|FPRC;

|PROCESO Tipo12; |TIPO 12;

|FPRC;

|PROCESO Media;
     nTotalTrab = 0;

     |ABRE #nomcalm1;
     |LEE 000#nomcalm1.p;

     |SI FSalida = 0;
          |PARA nCampo = 1; |SI nCampo [ 179; |HACIENDO nCampo = nCampo + 2;
               aAlfa = #nomcalm1#nCampo#;
               |QBF aAlfa;
               |SI aAlfa = "";
                    |CAMPO_MODIFICA #nomcalm1#nCampo#, 1, "N";
               |FINSI;
               nCampo1 = nCampo + 1;
               nTotalTrab = nTotalTrab + #nomcalm1#nCampo1#;
          |SIGUE;

          nMedia = nTotalTrab / #0#5;
          nMedia = nMedia ! 4;
     |FINSI;

     nAltaFechaRef = #nomcalm1#182;

     |PINPA #0#nomcalm1;
     |PINDA #0#nomcalm1;

     aAlfa1 = nTotalTrab;
     aAlfa1 = aAlfa1 + " trab."
     || aAlfa2 = "90";
     nMedia = nMedia ! 2;
     aAlfa3 = nMedia;
     aAlfa3 = aAlfa3 + " trab.";
     aAlfa4 = nAltaFechaRef;
     aAlfa5 = fFechaRef;
     aAlfa4 = aAlfa4 + " (" + (aAlfa5 % 102) + "/" + (aAlfa5 % 402) + ")";
     |PINTA 2070, aAlfa1;
     ||PINTA 2170, aAlfa2;
     |ATRI S;
     |PINTA 2170, aAlfa3;
     |ATRI 0;
     ||PINTA 2370, aAlfa4;

     ||PAUSA;

     |SI enParada = 1;
          |ENDATOS #2#101;
     |FINSI;

     |CIERRA #nomcalm1;
|FPRC;

|PROCESO GrabaDia;
     #nomcalm1#0 = 1;
     |LEE 101#nomcalm1.=;
     FEstado = FSalida;

     nDias = #0#5;
     nCampo = 179 - (2 * nDias) + (2 * (nDia + 1));
     nCampo1 = nCampo + 1;

     || nCampo = (nDia * 2) + 1
     || nCampo1 = nCampo + 1;
     aAlfa1 = fFecha;
     aAlfa2 = (aAlfa1 % 102) + "/" + (aAlfa1 % 402);
     #nomcalm1#nCampo# = aAlfa2;
     #nomcalm1#nCampo1# = nTrab;
     |SI FSalida ! 0;
          |GRABA 020#nomcalm1.n;
     |SINO;
          |GRABA 020#nomcalm1.a;
     |FINSI;
     |LIBERA #nomcalm1;
|FPRC;

|PROCESO GrabaTrab;
     #nomcalm2#0 = fFecha;
     #nomcalm2#1 = #labtraba#7;
     |LEE 101#nomcalm2.=;
     |SI FSalida ! 0;
          #nomcalm2#0 = fFecha;
          #nomcalm2#1 = #labtraba#7;
          #nomcalm2#2 = #labtraba#1;
          #nomcalm2#3 = #labtraba#2;
          #nomcalm2#4 = #labtraba#36;
          #nomcalm2#5 = #labtraba#37;
          #nomcalm2#6 = #labtraba#45;
          |GRABA 020#nomcalm2.n;
          |LIBERA #nomcalm2;
          nTrab = nTrab + 1;
     |FINSI;
|FPRC;

|PROCESO CodigosAntiguos;
     |QBF aContrato;
     aContrato = ("000" + aContrato) % -103;
     |ABRE #nomequiv;
     #nomequiv#0 = aContrato;
     |LEE 000#nomequiv.=;
     |SI FSalida = 0;
          |SI #labtraba#42 = "X";|O #labtraba#42 = "T";|O #labtraba#42 = "P";
               aContrato = #nomequiv#2;      || tiempo parcial
          |SINO;
               aContrato = #nomequiv#1;      || tiempo completo
          |FINSI;
     |FINSI;
     |CIERRA #nomequiv;
|FPRC;

|PROCESO CuentaTrab;
     |SI #labtraba#1 = enTra; || estoy en el trabajador que estoy dando de alta
          #labtraba#2 = eaNom;
          #labtraba#7 = eaNIF;
          #labtraba#36 = eaAlta;
          #labtraba#37 = eaBaja;
          #labtraba#45 = eaContrato;
     |FINSI;

     aFecha = fFecha;
     aMes = aFecha % 402;
     nMes = aMes;

     fFechaAlta = #labtraba#36;
     aFechaBaja = #labtraba#37;
     |QBF aFechaBaja;
     |SI aFechaBaja = ""; |O aFechaBaja = "..........";
          #nomvarca#0 = #labtraba#0;
          #nomvarca#1 = #labtraba#1;
          #nomvarca#2 = nMes;
          #nomvarca#3 = 0;
          |LEE 000#nomvarca.=;
          |SI FSalida = 0;
               aFechaBaja = #nomvarca#16;
               |QBF aFechaBaja;
               |SI aFechaBaja = ""; |O aFechaBaja = "..........";
                    aFechaBaja = "31.12.2999";
               |FINSI;
          |SINO;
               aFechaBaja = "31.12.2999";
          |FINSI;
     |FINSI;

     fFechaBaja = aFechaBaja;

     |SI fFechaAlta [ fFecha; |Y fFecha [ fFechaBaja; |Y #labtraba#42 ! "H";
          nContrato = #labtraba#45;
          aContrato = nContrato;
          |HAZ CodigosAntiguos;
          nContrato = aContrato;
          |SI #0#4 = 1;
               |SI nContrato < 400;
                    |HAZ GrabaTrab;
               |FINSI;
          |SINO;
               |SI nContrato ! 422;
                    |HAZ GrabaTrab;
               |FINSI;
          |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE labtraba;
     #labtraba#1;
     ;
     #0#0, 00001;
     #0#0, 99999;
     ;
     NULL, CuentaTrab;
|FIN;

|PROCESO Calculo;
     aAlfa1 = Usuario;     aAlfa1 = (aAlfa1 + "xxxxxxxx") % 108;
     |NOME_DAT #101 aAlfa1;
     |DELINDEX #101;

     aAlfa1 = Usuario;     aAlfa1 = (aAlfa1 + "zzzzzzzz") % 108;
     |NOME_DAT #102 aAlfa1;
     |DELINDEX #102;

     |ABRE #labtraba;
     |ABRE #101;
     |ABRE #102;
     |ABRE #nomvarca;

     fFechaRef = #0#3;
     nFechaRef = fFechaRef;
     nFechaIni = nFechaRef - #0#5;
     fFechaIni = nFechaIni;

     nDia = 0;
     |PARA nFecha = nFechaIni; |SI nFecha [ nFechaRef; |HACIENDO nFecha = nFecha + 1;
          fFecha = nFecha;
          nTrab = 0;
          |BUCLE labtraba;
          |HAZ GrabaDia;
          nDia = nDia + 1;
     |SIGUE;

     |CIERRA #nomvarca;
     |CIERRA #102;
     |CIERRA #101;
     |CIERRA #labtraba;
|FPRC;

|PROGRAMA;
     aParam = PARAMETRO;
     |QBF aParam;
     |SI aParam = "";
          |CLS;
          |PINPA #0#0;
          |PINDA #0#0;

          |ENDATOS #1#0;
          enParada = 1;
     |SINO;
          aAlfa1 = aParam % 105;
          #0#0 = aAlfa1;
          aAlfa2 = aParam % -110;
          #0#3 = aAlfa2;
          #0#4 = enOpcion;
     |FINSI;

     |SI FSalida = 0;
          |HAZ Calculo;
          |HAZ Media;
     |FINSI;
|FPRO;
