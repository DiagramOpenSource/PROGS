        *** SOLO CALCULA LAS PAGAS 99 ***

|FICHEROS;
    nomcaltt #00;      || pantalla calculo
    nomconca #09;      || convenios cabeceras
    nomconli #10;      || convenios lineas
    nomcalca #11;      || registro calculos cabeceras
    nomcalli #12;      || registro calculos lineas
    nomconst #15;      || constantes
    nombonif #16;      || bonificaciones
    nomepigr #17;      || epigrafes
    nomtrcal #20;      || calendario laboral
     labempre;
     labtraba;
|FIN;

|VARIABLES;
    FEstado = 0;
    act = 0;
    actc = 0;
    acum1_200 = 0;
    acum2_200 = 0;
    acum_10 = 0;
    acum_11 = 0;
    acum_12 = 0;
    acum_13 = 0;
    acum_30 = 0;
    acum_301 = 0;
    acum_302 = 0;
    acum_303 = 0;
    acum_31 = 0;
    acum_311 = 0;
    acum_312 = 0;
    acum_313 = 0;
    acum_40 = 0;
    acum_401 = 0;
    acum_402 = 0;
    acum_403 = 0;
    acum_41 = 0;
    acum_50 = 0;
    acum_51 = 0;
    acum_52 = 0;
    acum_53 = 0;
    acum_54 = 0;
    acum_90 = 0;
    acum_99 = 0;
    acum_991 = 0;
    acum_992 = 0;
    acum_993 = 0;
    acumb = 0;
    ajus_1 = 0;
    ajus_2 = 0;
    alfa1 = "";        || variable de trabajo
    alfa2 = "";        || variable de trabajo
    alfa3 = "";        || variable de trabajo
    alfa4 = "";        || variable de trabajo
    aAlfa = "";
    anti_41 = 0;
    any = "";          || calculo 'desglosa'
    any1 = "";
    anyalt = 0;
    anynum = 0;
    anyos = 0;
    anyven = 0;
    base_1 = 0;
    base_2 = 0;
    base_3 = 0;
    base_4 = 0;
    base_5 = 0;
    base_6 = 0;
    base_7 = 0;
    base_8 = 0;
    base_9 = 0;
    base_A = 0;
    base_B = 0;
    base_C = 0;
    base_D = 0;
    base_E = 0;
    base_F = 0;
    bases = 0;
    bisiesto = 0;
    blanco = "          ";
    bon_0 = 0;
    bon_1 = 0;
    bon_2 = 0;
    bon_3 = 0;
    bruto = 0;
    bruto_pag = 0;
    bruto_rec = 0;
    cabecera = 0;      || switch para grabar o no cabeceras de calculos
    campo = 0;
    campomax = 0;
    campomin = 0;
    coba_1 = 0;
    coba_2 = 0;
    coba_3 = 0;
    concep0 = 0;
    concep1 = 0;
    concep2 = 0;
    concepto = 0;      || variable de trabajo del campo 'concepto'
    conser = 0;
    convenio = 0;      || switch de error de existencia o no de linea convenio
    coti_0 = 0;
    coti_1 = 0;
    coti_2 = 0;
    dabse_2 = 0;
    dabse_k = 0;
    dacci_2 = 0;
    dacci_3 = 0;
    dacci_k = 0;
    dacum_30 = 0;
    dacum_31 = 0;
    dacum_vac = 0;
    dalta_1 = 0;
    dalta_2 = 0;
    dalta_3 = 0;
    dalta_k = 0;
    danti_0 = 0;
    danti_1 = 0;
    dbaja_1 = 0;
    dbaja_2 = 0;
    dbaja_3 = 0;
    dbaja_k = 0;
    dboni_1 = 0;
    dboni_2 = 0;
    dcobr_0 = 0;
    dcofi_1 = 0;
    dcofi_2 = 0;
    dcofi_3 = 0;
    dcofi_4 = 0;
    dcofi_5 = 0;
    dcofi_6 = 0;
    dcofi_999 = 0;
    dcoti_0 = 0;
    denfe_2 = 0;
    denfe_3 = 0;
    denfe_4 = 0;
    denfe_k = 0;
    des = 0;
    des1 = 0;
    des2 = 0;
    desc = 0;
    desde = 0;
    dhuel_2 = 0;
    dhuel_k = 0;
    dia = 0;
    dia1 = "";
    dia_1 = 0;
    dia_2 = 0;
    dia_3 = 0;
    dia_4 = 0;
    dia_k = 0;
    diaalt = 0;
    diafest = 0;
    dialabo = 0;
    dias_dto = 0;
    diasp = 0;
    diast = 0;
    diaven = 0;
    difer1 = 0;
    difere = 0;
    divide = 0;
    dlabo_0 = 0;
    dlabo_k = 0;
    dmate_2 = 0;
    dmate_k = 0;
    doce = 0;
    dto_con = 0;
    dto_dfp = 0;
    dto_ho1 = 0;
    dto_ho2 = 0;
    dto_tot = 0;
    duni1_101 = 0;
    duni1_102 = 0;
    duni1_103 = 0;
    duni1_200 = 0;
    duni1_902 = 0;
    duni1_903 = 0;
    duni1_999 = 0;
    duni2_101 = 0;
    duni2_102 = 0;
    duni2_103 = 0;
    duni2_200 = 0;
    duni2_902 = 0;
    duni2_903 = 0;
    duni2_999 = 0;
    dvaca_1 = 0;
    dvaca_2 = 0;
    dvaca_k = 0;
    dvalo_101 = 0;
    dvalo_102 = 0;
    dvalo_103 = 0;
    dvalo_200 = 0;
    dvalo_902 = 0;
    dvalo_903 = 0;
    dvalo_999 = 0;
    dvalo_pro = 0;
    empre = 0;
    fec = "";
    fecalf = "";       || calculo 'desglosa'
    fecbaj = "";       || calculo 'baja'
    fecsis = @;        || variable fecha de trabajo
    fecsys = @;        || variable fecha de trabajo
    fvenci = "";       || fecha vencimiento antiguedad
    graba04 = 0;
    graba05 = "";
    graba06 = 0;
    graba07 = 0;
    graba08 = "";
    graba09 = 0;
    graba10 = 0;
    graba11 = 0;
    guarda = 0;
    has = 0;
    has1 = 0;
    has2 = 0;
    hasc = 0;
    hasta = 0;
    horasex = 0;
    horastp = 0;
    indemni = 0;
    lin_1 = 0;
    lin_2 = 0;
    liquido = 0;
    mas = 0;
    men1 = "0000ATENCION !!! Fecha erronea ...";
    mensaje0 = "    * SELECCION VACIA * ";
    mensaje1 = "ATENCION, PROCESO DETENIDO !!! Convenio ";
    mensaje2 = ", INEXISTENTE en fichero ";
    mes = 0;
    mes1 = "";
    mes_a = 0;
    mesalt = 0;
    mesco = 0;
    mesdes = 0;
    meshas = 0;
    meslabo = "";      || mes del calendario laboral
    mesnum = 0;
    mespinta = "";     || variable de trabajo para pintar el mes
    mesven = 0;
    mid = 0;
    mod = 0;
    || nomtmpca = "nomtmpca";
    || nomtmpli = "nomtmpli";
    &nomtmpca = "";
    &nomtmpli = "";
    nume0 = 0;
    nume1 = 0;
    nume2 = 0;
    numer = 0;
    p_mes = "";        || variable de trabajo para pintar mes literal
    paga = 0;
    perio = 0;
    perio1 = 0;
    perio2 = 0;
    perio3 = 0;
    perio4 = 0;
    perio5 = 0;
    perio6 = 0;
    perio7 = 0;
    perio8 = 0;
    period = 0;
    por1_cont = 0;
    por1_dfpg = 0;
    por1_hor1 = 0;
    por1_hor2 = 0;
    por2_cont = 0;
    por2_dfpg = 0;
    por2_hor1 = 0;
    por2_hor2 = 0;
    porcp = 0;
    pp = 0;
    product = 0;
    prom_acc = 0;
    prom_enf = 0;
    puntero = 0;
    puntos = "..........";
    reten_1 = 0;
    reten_11 = 0;
    reten_12 = 0;
    reten_13 = 0;
    reten_2 = 0;
    seleccio = 0;      || switch para mensaje 'Seleccion vacia' o no
    stop = 0;
    sw = 0;
    swaju_vac = 0;
    swajuac = 0;
    swajuen = 0;
    swalta = 0;
    swbaja = 0;
    swcongra = 0;
    swgracon = 0;
    swpaga = 0;
    swpro = 0;
    tempo1 = 0;
    tempo2 = 0;
    tempo3 = 0;
    tempo4 = 0;
    tempo5 = 0;
    tempor = 0;
    tipo1 = "";        || mes que cumple a¤o (actual)
    tipo2 = "";        || mes que cumple a¤o (siguiente)
    tipo3 = "";        || trimestre que cumple a¤o (actual)
    tipo4 = "";        || trimestre que cumple a¤o (siguiente)
    tipo5 = "";        || semestre que cumple a¤o (actual)
    tipo6 = "";        || semestre que cumple a¤o (siguiente)
    tipo7 = "";        || a¤o que cumple a¤o (actual)
    tipo8 = "";        || a¤o que cumple a¤o (siguiente)
    tipos = 0;
    tope = 0;
    topemes = 0;
    topemes1 = 0;
    topemes2 = 0;
    totact = 0;
    total = 0;
    totalt = 0;
    totven = 0;
    trabamen = "";     || variable de trabajo para construir mensaje
    unida = 0;
    vacacio = 0;
    valor = 0;
    vcobr_0 = 0;
    vlabo_0 = 0;
    vlabo_k = 0;
    &EURODB = 0;

     &nCConst = 0;
     &fCFecha = @;
     &aCError = "";
     &nEpigConv = 0;
     aAlfa1 = "";
     aAlfa2 = "";
     inkey = 0;
|FIN;

|PROGRAMA;
|HAZ LeeEURODB;
|CLS;
|CABEZA "CALCULO ABIERTO";
|PINPA #0#0;
|ABRE #0;
|LEE 140#0p;
|SI FSalida ! 0;
    |DDEFECTO #0;
    |GRABA 020#0n;
    |LEE 140#0p;
|FINSI;
|PINDA #0#0;
|ENDATOS #1#0;
|SI FSalida = 0;
    |PARA campo = 45; |SI campo [ 83; |HACIENDO campo = campo + 1;
            #0campo = 0;
    |SIGUE;
    |GRABA 020#0a;
|FINSI;
|LIBERA #0;
|CIERRA #0;
|FPRO;

||****************************************************************************
||                    LECTURA PRINCIPAL DE TRABAJADORES
||****************************************************************************

|PROCESO ayuda1; |TIPO 7;
|PUSHV 12321873;
|ATRI I;
|CUADRO 12 32 18 73;
|ATRI R;
||            1234567890123456789012345678901234567890
|PINTA 1333, " ----- CLAVES DE SALARIO POSIBLES ----- ";
|PINTA 1433, " (M) cotizacion mensual                 ";
|PINTA 1533, " (D) cotizacion diaria                  ";
|PINTA 1633, " (T) tiempo parcial cotizacion diaria   ";
|PINTA 1733, " (X) tiempo parcial cotizacion mensual  ";
|ATRI 0;
|FPRC;

|PROCESO ayuda2; |TIPO 0;
|POPV;
|FPRC;

|PROCESO fechacon; |TIPO 0;      || chequea una fecha alfanumerica de 10 pos.
    fec = #0Campo; sw = 0;
|SI fec ! blanco; |Y fec ! puntos;
        dia1 = fec % A102;
        mes1 = fec % A402;
        any1 = fec % A704;
        dia = dia1;
        mes = mes1;
        any = any1;
    |SI dia ! 0; |Y mes ! 0; |Y any ! 0;
        |SI mes [ 12; |Y mes ] 1;
            |SI mes=1;|O mes=3;|O mes=5;|O mes=7;|O mes=8;|O mes=10;|O mes=12;
                    tope = 31;
            |SINO;
                |SI mes = 2;
                         mod = any $ 4;
                    |SI mod = 0;
                            tope = 29;
                    |SINO;
                            tope = 28;
                    |FINSI;
                |SINO;
                        tope = 30;
                |FINSI;
            |FINSI;
            |SI dia [ tope;
                    sw = 1;
            |FINSI;
        |FINSI;
    |FINSI;
|SINO;
        sw = 1;
|FINSI;
|SI sw = 0;
    |MENAV men1;
    |ERROR;
|FINSI;
|FPRC;

|PROCESO noentres; |TIPO 7;
|SI #0#1 = 0;
        #0#2 = "..........";
        #0#3 = "..........";
    |CAMPO_MODIFICA #0#2, 1, "N";
    |CAMPO_MODIFICA #0#3, 1, "N";
|SINO;
    |SI #0#2 = "..........";  #0#2 = #0#5;  |FINSI;
    |SI #0#3 = "..........";  #0#3 = #0#6;  |FINSI;
    |CAMPO_MODIFICA #0#2, 1, "S";
    |CAMPO_MODIFICA #0#3, 1, "S";
|FINSI;
|PINTA #0#2;
|PINTA #0#3;
|FPRC;

|PROCESO nopases; |TIPO 7;
|SI #0#42 = 0;
    |CAMPO_MODIFICA #0#43, 1, "N";
    |CAMPO_MODIFICA #0#44, 1, "N";
    |CAMPO_MODIFICA #0#84, 1, "N";
    |CAMPO_MODIFICA #0#85, 1, "N";
|SINO;
    |CAMPO_MODIFICA #0#43, 1, "S";
    |CAMPO_MODIFICA #0#44, 1, "S";
    |CAMPO_MODIFICA #0#84, 1, "S";
    |CAMPO_MODIFICA #0#85, 1, "S";
|FINSI;
|FPRC;

|PROCESO lectura; |TIPO 3;
|ABRE #9;
|ABRE #10;
    #9#0 = #0#4;
|LEE 000#9=;
|SI FSalida = 0;
    |HAZ leeregis;
    |ABRET #11;
    |ABRET #12;
    |HAZ meses;
    |HAZ calleer1;
    |PARA mes_a = mesdes; |SI mes_a [ meshas; |HACIENDO mes_a = mes_a + 1;
            mesnum = mes_a;
            anynum = period;
        |HAZ ulti_dia;
            guarda = topemes;
        |HAZ pintalo;
        |HAZ concepto;
        |HAZ cabecera;
    |SIGUE;
    |SI cabecera = 0;
        |MENAV "    ATENCION!!! Seleccion vacia ...";
    |SINO;
        |HAZ presenta;
        |PAUSA;
    |FINSI;
    |CIERRAT #11;
    |CIERRAT #12;
    |DELINDEX #11;
    |DELINDEX #12;
|SINO;
    |ATRI P;
        trabamen = #0#4;
        trabamen = "    " + mensaje1 + trabamen + mensaje2;
    |MENAV trabamen;
    |ATRI 0;
|FINSI;
|CIERRA #9;
|CIERRA #10;
|FPRC;

||***************************************************************************
||                GRABACION DE LINEAS DE REGISTRO
||***************************************************************************

|PROCESO meses;
    fecalf = #0#5;  |HAZ desglosa;  mesdes = mes;
    period = any;
    fecalf = #0#6;  |HAZ desglosa;  meshas = mes;
|SI mesdes > meshas;
        meshas = 12;
|FINSI;
|FPRC;

|PROCESO concepto;
|PARA campo = 8; |SI campo [ 17; |HACIENDO campo = campo + 1;
    |SI #0campo ! 999;
            concepto = #0campo;
        |HAZ convenio;
        |SI convenio = 0;
                unida = campo + 10;
                valor = campo + 20;
            |DDEFECTO #12;
                #12#00 =    999;   || codigo empresa
                #12#01 =  99999;   || codigo trabajador
                #12#02 =  mes_a;   || mes
                #12#03 =      0;   || tipo
                #12#04 = #0campo;  || concepto
                #12#05 = #10#03;   || calculo alfa
            |SI #10#03 = "Z";
                |SI #0#7 = "M";|O #0#7 = "X";  #12#05 = "F"; |FINSI; ||mensual
                |SI #0#7 = "D";|O #0#7 = "T";  #12#05 = "M"; |FINSI; ||diario
            |FINSI;
                #12#06 = #10#04;   || calculo nume
                #12#07 = #10#05;   || situacion
                #12#08 = #10#06;   || descripcion
                #12#09 = #0unida;
                #12#10 = #0valor;
                #12#11 =      0;   || unidades calculo (aqui no se tocan)
            |GRABA 020#12n;
            |LIBERA #12;
                cabecera = 1;
        |FINSI;
    |FINSI;
|SIGUE;
|FPRC;

|PROCESO pintalo;
    alfa1 = mes_a;
|QBF alfa1;
    alfa1 = "00" + alfa1;
    alfa1 = alfa1 % -102;
|PINTA 0673, alfa1;
|FPRC;

||***************************************************************************
||                 GRABACION DE CABECERA DE REGISTRO
||***************************************************************************

|PROCESO cabecera;
|HAZ calleer2;         || lee constantes y calendario
|HAZ caldias;          || calcula los dias de enfermedad, etc.
|HAZ calanti;          || calcula el salario base, antiguedad y plus
|HAZ calunac;          || calcula las unidades y los acumulados
|HAZ calenfe;          || calcula los complementos de enfermedad y accidente
|HAZ calpror;          || calcula la prorrata
|HAZ calpaga;          || calcula las pagas
|HAZ calfin1;          || calculo final 1
|HAZ calfin2;          || calculo final 2
|DDEFECTO #11;
    #11#00 =    999;   || empresa
    #11#01 =  99999;   || trabajador
    #11#02 =  mes_a;   || mes
    #11#03 =      0;   || tipo
    #11#04 = #00#05;   || fecha de alta
    #11#05 = #00#06;   || fecha de baja
    #11#06 = #00#05;   || fecha de antiguedad
    #11#07 = dia_2;    || dias periodo
    #11#08 = dcoti_0;  || dias alta
    #11#09 =   "99";   || tipo contrato
    #11#10 = dcoti_0;  || dias contrato
    #11#11 =      0;   || horas contrato
    #11#12 = #00#01;   || codigo bonificacion
    #11#13 = #00#44;   || (%) IRPF
    #11#14 = acum_991; || base IRPF 1
    #11#15 = reten_11; || retencion IRPF 1
    #11#16 = acum2_200;|| base IRPF pagas
    #11#17 = reten_2;  || retencion IRPF pagas
    #11#18 = acum_41;  || base IRPF exenta
|PARA campo = 12; |SI campo [ 15; |HACIENDO campo = campo + 1;
        desde = campo - 8;
        hasta = campo - 4;
        puntero = desde + 15;  #11puntero = 0;   || tabla de
        puntero = hasta + 15;  #11puntero = 0;   || /variaciones
        puntero = campo + 15;  #11puntero = 0;
|SIGUE;
    #11#31 = denfe_2;  || total dias enfermedad
    #11#32 = dacci_2;  || total dias accidente
    #11#33 = dhuel_2;  || total dias huelga
    #11#34 = dabse_2;  || total dias absentismo
    #11#35 = prom_enf; || promedio enfermedad
    #11#36 = prom_acc; || promedio accidente
    #11#37 = acum_10 + coba_3;  || base cotizacion
    #11#38 = dvalo_pro;|| base prorratas
    #11#39 = base_6;   || base cotizacion cont. comunes
    #11#40 = base_7;   || base cotizacion D+F+P
    #11#41 = dacum_30; || base H.E. 1
    #11#42 = dacum_31; || base H.E. 2
    #11#43 = bon_0;    || base 1 bonifi.
    #11#44 = bon_3;    || base 2 bonifi.
    #11#45 = base_E;   || S.S. cgo. empresa
    #11#46 = bruto_rec;|| bruto recibo
    #11#47 = bruto_pag;|| bruto paga
    #11#48 = dto_con;  || dto. cont. comunes
    #11#49 = dto_dfp;  || dto. D+F+P
    #11#50 = dto_ho1;  || dto. horas 1
    #11#51 = dto_ho2;  || dto. horas 2
    #11#52 =     0;    || anticipos -1-
    #11#53 =     0;    || anticipos -2-
    #11#54 =     0;    || en especie
    #11#55 = acum_51;  || importe ayuda familiar      (501)
    #11#56 = acum_52;  || importe ayuda subnormalidad (502)
    #11#57 = acum_53;  || importe enfermedad      (503)
    #11#58 = acum_54;  || importe accidentes      (504)
    #11#59 = denfe_3+denfe_4;  || complemento enfermedad (511+515)
    #11#60 = dacci_3;  || complemento accidente       (512)
    #11#61 = bon_1;    || (%) con. comunes
    #11#62 = base_5;   || base cotiz. enfe/acci
    #11#63 = base_F;   || cuota bonif. 1
    #11#64 = base_D;   || cuota bonif. 2
    #11#65 = dmate_2;  || total dias maternidad
    #11#66 = dvaca_2;  || total dias vacaciones
    #11#67 = #0#84;    || (%) irpf 2
    #11#68 = #0#85;    || (%) irpf 3
    #11#69 = acum_992; || base irpf 2
    #11#70 = acum_993; || base irpf 3
    #11#71 = reten_12; || reten. irpf 2
    #11#72 = reten_13; || reten. irpf 3
    #11#73 = denfe_4;  || prestacion enf. empresa     (515)
    #11#74 = acum_99;  || total irpf
    #11#75 = reten_1;  || total retenciones
|GRABA 020#11a;             || ya habia grabado antes para |BUCLELIN
|LIBERA #11;
|FPRC;

||****************************************************************************
||            RUTINAS GENERALES ESPECIFICAS O COMUNES A CALCULOS
||****************************************************************************

|PROCESO leeregis;     || si existe registro anterior se borra
     alfa1 = Usuario;
     |QBF alfa1;
     nomtmpca = "c3" + alfa1;
     nomtmpli = "c4" + alfa1;
|NOME_DAT #11 nomtmpca;
|NOME_DAT #12 nomtmpli;
|DELINDEX #11;
|DELINDEX #12;
|FPRC;

|PROCESO convenio;     || comprueba si existe linea de convenio
    convenio = 0;
    #10#00 = #0#4;
    #10#02 = concepto;
|LEE 000#10=;
|SI FSalida = 0; |O concepto ] 901;  || los conceptos 9xx pasan aunque no esten
        convenio = 0;
    |SI concepto ] 901;
        |HAZ novecien;
    |FINSI;
|SINO;
        convenio = 1;
|FINSI;
|FPRC;

|PROCESO novecien;    || chequea los conceptos 9xx
|SI concepto = 901;
        #10#3 = "V";
        #10#4 = 0;               || el concepto 901 esta reservado
        #10#5 = 0;               || /para que guarde las horas extras
        #10#6 = "N§ HORAS";      || subvencionadas
|FINSI;
|SI concepto = 902;
        #10#3 = "V";
        #10#4 = 0;               || el concepto 902 esta reservado
        #10#5 = 0;               || /para que guarde los dias de indemniz.
        #10#6 = "INDEMNI.";      || al a¤o
|FINSI;
|SI concepto = 904;
        #10#3 = "V";
        #10#4 = 0;               || el concepto 903 esta reservado
        #10#5 = 0;               || /para que guarde los dias vacacio./a¤o
        #10#6 = "VACACION";
|FINSI;
|SI concepto ] 905; |Y concepto [ 998;
        #10#3 = "V";
        #10#4 = 0;               || los conceptos 903-998 se reservan para
        #10#5 = 0;               || /calculos especiales, los acepta aunque
        #10#6 = "XXXXXXXX";      || /no existan en el convenio
|FINSI;
|SI concepto = 999;              || si es 999 no lo graba !!!
        convenio = 1;
|FINSI;
|FPRC;

|PROCESO ulti_dia;  || halla el ultimo dia del mes
    numer = anynum $ 4;
|SI numer = 0;
        bisiesto = 1;
|SINO;
        bisiesto = 0;
|FINSI;
|SI mesnum =  1;  topemes = 31;            |FINSI;
|SI mesnum =  2;  topemes = 28 + bisiesto; |FINSI;
|SI mesnum =  3;  topemes = 31;            |FINSI;
|SI mesnum =  4;  topemes = 30;            |FINSI;
|SI mesnum =  5;  topemes = 31;            |FINSI;
|SI mesnum =  6;  topemes = 30;            |FINSI;
|SI mesnum =  7;  topemes = 31;            |FINSI;
|SI mesnum =  8;  topemes = 31;            |FINSI;
|SI mesnum =  9;  topemes = 30;            |FINSI;
|SI mesnum = 10;  topemes = 31;            |FINSI;
|SI mesnum = 11;  topemes = 30;            |FINSI;
|SI mesnum = 12;  topemes = 31;            |FINSI;
|FPRC;

|PROCESO grabacon;        || GRABA O REGRABA LINEAS DE CALCULO SEGUN VARIABLES
    doce = graba04;
|HAZ leedoce;
    FEstado = FSalida;
|SI FEstado ! 0; |O swgracon = 1;    || switch para regrabar campos convenio
        #12#05 = graba05; || calculo alfa
        #12#06 = graba06; || calculo nume
        #12#07 = graba07; || situacion
        #12#08 = graba08; || descripcion
|FINSI;
|SI FEstado = 0; |Y swcongra = 1;    || switch para regrabar acumulando
        tempo1 = #12#10;  || valor anterior
        tempo2 = #12#11;  || unidades calculo anterior
        tempo3 = graba10; || valor actual
        tempo4 = graba11; || unidades calculo actual
        tempo5 = (tempo1 * tempo2) + (tempo3 * tempo4); || importe total
        graba10 = tempo5; || valor concepto nuevo
        graba11 = 1;      || unidades calculo concepto nuevo
|FINSI;
    #12#09 = graba09;     || unidades
    #12#10 = graba10;     || valor
    #12#11 = graba11;     || unidades calculo
|SI FEstado ! 0;  |GRABA 020#12n;  |FINSI;
|SI FEstado = 0;  |GRABA 020#12a;  |FINSI;
|LIBERA #12;
|FPRC;

|PROCESO coefi;      || CALCULA EN 'dcofi_999' COEFICIENTE SEGUN LINEA CALCULO
    concepto = #12#4;
|HAZ convenio;       || lee el concepto del convenio para recoger vacaciones
|SI #12#5 ="M"; #12#9 = dia_4;  |FINSI; || si es 'M' le pone dias cotizables
|SI #12#5 ="D"; #12#9 = dia_2;  |FINSI; || si es 'D' le pone dias del mes
|SI #12#6 = 0;  dcofi_999 = 1;        |FINSI;
|SI #12#6 = 1;  dcofi_999 = dcofi_1;  |FINSI;  || dias cobro
|SI #12#6 = 2;  dcofi_999 = dcofi_2;  |FINSI;  || dias cotizables
|SI #10#7 = "N";  dcofi_999 = dcofi_5;  |FINSI;  || no entra en vacaciones
|SI #10#7 = "E";  dcofi_999 = dcofi_6;  |FINSI;  || solo entra en vacaciones
|SI #12#5 ="L";         || dias laborables
    |SI #10#7 = "S";  #12#9 = dlabo_0;  |FINSI;
    |SI #10#7 = "N";  #12#9 = vlabo_0;  |FINSI;
    |SI #10#7 = "E";  #12#9 = dvaca_1;  |FINSI;
|FINSI;
|SI #12#5 ="K";         || dias festivos
    |SI #10#7 = "S";  #12#9 = dlabo_k;  |FINSI;
    |SI #10#7 = "N";  #12#9 = vlabo_k;  |FINSI;
    |SI #10#7 = "E";  #12#9 = dvaca_k;  |FINSI;
|FINSI;
|SI #12#5 ="L";|O #12#5 ="K";
        dcofi_999 = 1;
|FINSI;
|FPRC;

|PROCESO cal_vaca;
|SI #10#7 = "S";|O #10#7 = "E";   || si entra en vacaciones
    |SI #12#05="D";                divide=dia_2;     |FINSI; || dias mes
    |SI #12#05="M";                divide=dia_4;     |FINSI; || dias cotiz.
    |SI #12#05="L";|O #12#05="K";  divide=#12#09;    |FINSI; || dias lab./fes.
    |SI #12#05="V";|O #12#05="F";  divide=   30;     |FINSI; || dias fijos
        product = #12#09 * #12#10;
        product = product ! EURODB;
        dacum_vac = dacum_vac + (product / divide);
|FINSI;
|FPRC;

|PROCESO leedoce;
|LIBERA #12;
    #12#0 =  999;
    #12#1 = 99999;
    #12#2 = mes_a;
    #12#3 = 0;
    #12#4 = doce;
|LEE 100#12=;
|FPRC;

|PROCESO cogegra;
    graba05 = #10#3;
    graba06 = #10#4;
    graba07 = #10#5;
    graba08 = #10#6;
|FPRC;

|PROCESO constante;
     fecsys = ~;
     alfa1 = fecsys;
     alfa1 = alfa1 % 308;
     alfa1 = "01" + alfa1;

     fCFecha = alfa1;         || primer dia de mes de calculo
     aCError = "";            || inicializo errores
     |HAZ LeeCons;
     |SI aCError ! "";
          alfa1 = #0#0; alfa1 = "000" + #0#0; alfa1 = alfa1 % -103;
          aAlfa = "0000ATENCION: Constante: " + alfa1;
          aAlfa = aAlfa + " La constante no esta definida.";
          |MENAV aAlfa;
     |FINSI;
|FPRC;

||****************************************************************************
||   CALCULO DE LECTURA DE CALENDARIO Y CONSTANTES Y BONIFICACIONES
||****************************************************************************

|PROCESO calleer1;
|SI #0#0 ! 0;
    nCConst = #0#0;
    |HAZ constante;

    ||ABRE #15;
        ||#15#0  = #0#0;
    ||LEE 020#15=;
    ||SI FSalida ! 0;               || (halla tambien porcentajes dtos.)
        ||DDEFECTO #15;
    ||FINSI;
    ||CIERRA #15;

        por1_cont = #15#07;                             || (%) contigen. (trab.)
        por1_hor1 = #15#09;                             || (%) horas 1   (trab.)
        por1_hor2 = #15#11;                             || (%) horas 2   (trab.)
        por1_dfpg = #15#13 + #15#15 + #15#17 + #15#19;  || (%) d+f+p+g   (trab.)
        por2_cont = #15#06;                             || (%) contigen. (emp.)
        por2_hor1 = #15#08;                             || (%) horas 1   (emp.)
        por2_hor2 = #15#10;                             || (%) horas 2   (emp.)
        por2_dfpg = #15#12 + #15#14 + #15#16 + #15#18;  || (%) d+f+p+g   (emp.)
|FINSI;
|SI #0#1 ! 0;
    |ABRE #16;
        #16#0  = #0#1;
    |LEE 000#16=;
    |SI FSalida ! 0;
        |DDEFECTO #16;
    |FINSI;
    |CIERRA #16;
|FINSI;
||SI #0#39 ! 0;
    |ABRE #17;
    || alfa1 = #0#5;
    || alfa1 = alfa1 % -104;
    || nume1 = alfa1;
    || SI nume1 [ 2006;
    ||    nEpigConv  = #labtraba#32;
    || SINO;
    #labtraba#204 = #0#86;
    #labempre#128 = #0#87;
    |HAZ VerifCNAE;
    ||FINSI;
          #17#0 = nEpigConv;
    |LEE 000#17=;                  || solo lee el epigrafe si ha cambiado
    |SI FSalida ! 0;
        |DDEFECTO #17;
    |FINSI;
    |CIERRA #17;
||FINSI;
|FPRC;

|PROCESO calleer2;
|SI #0#40 ! 0;
    |ABRE #20;
        #20#0  = #0#40;
        #20#13 = period;
    |LEE 000#20=;
    |SI FSalida ! 0;
        |DDEFECTO #20;
    |FINSI;
        topemes1 = 0;
        topemes2 = 0;   || calcula los dias laborables del mes
    |HAZ calendar;
        conser = dialabo;   || guarda los dias laborables del mes
    |CIERRA #20;
|SINO;
        dialabo = topemes;
        conser = dialabo;   || guarda los dias laborables del mes
|FINSI;
|FPRC;

||****************************************************************************
||                       CALCULO DE DIAS
||****************************************************************************

|PROCESO caldias;        || CALCULA DIAS VARIOS
|HAZ cerodias;           || inicializa variables
    dia_k = diafest;     || dias festivos del mes
    dia_1 = dialabo;     || dias laborables del mes
    dia_2 = topemes;     || dias que tiene el mes
|SI #0#7 = "M"; |O #0#7 = "X";
        dia_3 = 30;      || dias cotizacion 'mensual'
|SINO;
        dia_3 = topemes; || dias cotizacion 'diario'
|FINSI;
|HAZ altas;              || calcula dias de alta (en su caso)
|HAZ bajas;              || calcula dias de baja (en su caso)
|HAZ diatodos;
|SI #0#1 ! 0;
    |HAZ bonif;              || calcula dias bonificacion
|FINSI;
|FPRC;

||****************************************************************************
||                        RUTINAS DEL CALCULO DE DIAS
||****************************************************************************

|PROCESO cerodias;     || INICIALIZA VARIABLES DEL CALCULO DE DIAS
    dalta_1 = 0;   dalta_2 = 0;
    dbaja_1 = 0;   dbaja_2 = 0;
    dboni_1 = 0;   dboni_2 = 0;
    topemes1 = 0;  topemes2 = 0;
    topemes = guarda;             || restaura ultimo dia natural del mes
    dialabo = conser;             || restaura dias laborables del mes
|FPRC;

|PROCESO calendar;                 || CALCULA LOS DIAS LABORABLES
    nume1 = mes_a;
    meslabo = #20nume1;
    diafest = 0;
    dialabo = 0;
|SI topemes1 = 0; topemes1 = 1;      |FINSI;
|SI topemes2 = 0; topemes2 = guarda; |FINSI;
|PARA mid = topemes1; |SI mid [ topemes2; |HACIENDO mid = mid + 1;
        nume1 = (mid * 100) + 1;
        alfa1 = meslabo % nume1;
    |SI alfa1 = "0";
            dialabo = dialabo + 1;     || cuenta dias laborables
    |SINO;
            diafest = diafest + 1;     || cuenta dias festivos
    |FINSI;
|SIGUE;
|FPRC;

|PROCESO altas;
    swalta = 0;
    fecalf = #0#5;
    dalta_3 = 1;
|HAZ desglosa;
|SI mes = mes_a; |Y any = period;     || si es alta este mes
        swalta = 1;
        topemes1 = 0;
        topemes2 = dia - 1;
        dialabo = 0;
        diafest = 0;
    |SI topemes2 ! 0;  |HAZ calendar;  |FINSI;  || por si es alta el dia 1
        dalta_k = diafest;         || dias festivos antes del alta
        dalta_1 = dialabo;         || dias laborables antes del alta
        dalta_2 = dia - 1;         || dias cotizables antes del alta
        dalta_3 = dia;             || dia que es alta
|FINSI;
|FPRC;

|PROCESO bajas;
    swbaja = 0;
    dbaja_3 = dia_2;
    fecbaj = #0#6;
    fecalf = #0#6;
|HAZ desglosa;
|SI mes = mes_a; |Y any = period;     || si es baja este mes
        swbaja = 1;
        topemes1 = dia + 1;
        topemes2 = 0;
        dialabo = 0;
        diafest = 0;
    |SI topemes1 [ guarda;  |HAZ calendar;  |FINSI;  || por si es final de mes
        dbaja_k = diafest;         || dias festivos despues de la baja
        dbaja_1 = dialabo;         || dias laborables despues de la baja
        dbaja_2 = dia_2 - dia;     || dias cotizables despues de la baja
        dbaja_3 = dia;             || dia hasta el que trabaja
|FINSI;
|FPRC;

|PROCESO diatodos;          || CALCULA TOTALES FINALES DE DIAS

    dia_4 = dia_3;   || dias cotizables

|SI swbaja!0; |Y dbaja_2!0;
        dbaja_2=dbaja_2+( (dia_4-(dalta_2+dbaja_2)) - ((dbaja_3-dalta_3)+1) );
|FINSI;

    dcobr_0=dia_4-(dalta_2+dbaja_2);
    dcoti_0=dia_4-(dalta_2+dbaja_2);
    dlabo_0=dia_1-(dalta_1+dbaja_1);
    dlabo_k=dia_k-(dalta_k+dbaja_k);

    vcobr_0=dcobr_0 - dvaca_2;  || resta los dias de vacaciones
    vlabo_0=dlabo_0 - dvaca_1;
    vlabo_k=dlabo_k - dvaca_k;

|SI dcobr_0 < 0;  dcobr_0 = 0;  |FINSI;  || chequea negativos dias cobro
|SI dcoti_0 < 0;  dcoti_0 = 0;  |FINSI;  || chequea negativos dias cotizables
|SI dlabo_0 < 0;  dlabo_0 = 0;  |FINSI;  || chequea negativos dias laborables
|SI dlabo_k < 0;  dlabo_k = 0;  |FINSI;  || chequea negativos dias festivos
|SI vcobr_0 < 0;  vcobr_0 = 0;  |FINSI;  || chequea neg. dias cobro sin vaca.
|SI vlabo_0 < 0;  vlabo_0 = 0;  |FINSI;  || chequea neg. dias labor. sin vaca.
|SI vlabo_k < 0;  vlabo_k = 0;  |FINSI;  || chequea neg. dias fest. sin vaca.

    dcofi_1 = dcobr_0 /dia_4;        || coeficiente para dias cobro
    dcofi_2 = dcoti_0 /dia_4;        || coeficiente para dias cotizables
    dcofi_3 = 1;
    dcofi_4 = 1;
    dcofi_5 = 1;              || coef. para vacac.=N
    dcofi_6 = 0;              || coef. para vacac.=E
|FPRC;

|PROCESO bonif;
    fecalf = #0#2;   || desglosa fecha alta bonificacion
|HAZ desglosa;
    diaalt = dia;     mesalt = mes;          anyalt = any;
    totalt = (anyalt * 100) + mesalt;

    fecalf = #0#3;   || desglosa fecha vto. bonificacion
|HAZ desglosa;
    diaven = dia;     mesven = mes;          anyven = any;
    totven = (anyven * 100) + mesven;
|SI totven = 0;
        totven = 999912;
|FINSI;

    alfa1 = period;     alfa1 = alfa1 % -104;  nume1 = alfa1;
    totact = (nume1 * 100) + mes_a;

    dboni_1 = 0;  || dias de bonificacion
    dboni_2 = 0;  || dias de no bonificacion

|SI totact = totalt;          || si la bonificacion es alta este mes
        dboni_1 = (dcoti_0 - diaalt) + 1;
        dboni_2 = dcoti_0 - dboni_1;
    |SI dboni_1 < 0;  dboni_1 = 0;  |FINSI;
    |SI dboni_2 < 0;  dboni_2 = 0;  |FINSI;
|FINSI;

|SI totalt [ totact; |Y totven ] totact;    || si esta entre el a¤o o es igual
        dboni_1 = dcoti_0;
        dboni_2 = 0;
|FINSI;

|SI totven = totact;         || si vence bonificacion este mes
        dboni_1 = diaven;
        dboni_2 = dcoti_0 - dboni_1;
    |SI dboni_2 < 0;  dboni_2 = 0;  |FINSI;
|FINSI;
|FPRC;

||****************************************************************************
||                      SUBRUTINAS DEL CALCULO DE DIAS
||****************************************************************************

|PROCESO desglosa;     || DESGLOSA EN DIA, MES Y ANYO UNA FECHA DD.MM.AAAA
    alfa1 = fecalf %  102;   dia = alfa1;
    alfa1 = fecalf %  402;   mes = alfa1;
    alfa1 = fecalf % -104;   any = alfa1;
|SI alfa1 = "....";
        dia = 0;
        mes = 0;
        any = 0;
|FINSI;
|FPRC;

||****************************************************************************
||                CALCULO DEL SALARIO BASE, ANTIGUEDAD Y PLUS
||****************************************************************************

|PROCESO calanti;     || LANZA LOS CALCULOS DE ANTIGUEDAD Y PLUS
|HAZ cal_101;                    || calcula el salario base
|SI #9#2 = 3;                    || (si antig=3, calcula antes el plus !!!)
    |HAZ cal_103;
|FINSI;
|HAZ cal_102;                    || calcula la antiguedad
|SI #9#2 ! 3;                    || calcula los pluses
    |HAZ cal_103;                || (si antig=3, ya habra entrado antes !!!)
|FINSI;
|HAZ gratodo;                    || regraba o graba conceptos 101,102 y 103
|FPRC;

||****************************************************************************
||            RUTINAS DEL CALCULO DEL SALARIO BASE, ANTIGUEDAD Y PLUS
||****************************************************************************

|PROCESO cal_101;         || CALCULA EL SALARIO BASE
    doce = 101;
|HAZ leedoce;
|SI FSalida = 0;
    |HAZ coefi;
        duni1_101 = #12#9;
        duni2_101 = dcofi_999 * duni1_101;  || halla unid.calculo
        dvalo_101 = #12#10;
        duni2_101 = duni2_101 ! 6;          || redondea
|FINSI;
|FPRC;

|PROCESO cal_102;     || CALCULA LA ANTIGUEDAD
    danti_0 = 0;  || limpia variables para el calculo de la antiguedad
    danti_1 = 0;
    doce = 102;
|HAZ leedoce;
|SI FSalida = 0;
    |HAZ coefi;
        duni1_102 = #12#9;
        dvalo_102 = #12#10;
    |SI #9#2 < 2;
            duni2_102 = duni1_102 *dcofi_999;      || por coefic. segun calculo
    |SINO;
        |HAZ anyanti;
        |HAZ poranti;            || si es tipo 2 ¢ 3 va a calcular (%) ¢ ptas.
    |FINSI;
    |SI #9#2 = 2;|O #9#2 = 3;
            dvalo_102 = danti_0;                  || ptas. antig./categ.
            duni1_102 = danti_1;                  || coge cantidad entera
            duni2_102 = danti_1 * dcofi_999;      || reduce cantidad
    |FINSI;
        duni2_102 = duni2_102 ! 6;   || redondea unid. calculo
        ||dvalo_102 = dvalo_102 ! 2   || redondea valor, comentado por redon. euro
|SINO;
        duni1_102 = 0;
        duni2_102 = 0;
        dvalo_102 = 0;  || para no grabar despues
|FINSI;
|FPRC;

|PROCESO cal_103;       || CALCULA EL PLUS
    doce = 103;
|HAZ leedoce;
|SI FSalida = 0;
    |HAZ coefi;
        duni1_103 = #12#9;
        dvalo_103 = #12#10;
    |SI #9#3 < 2;
            duni2_103 = duni1_103 *dcofi_999;         || por coefic. segun calculo
    |FINSI;
    |SI #9#3 = 2;
            dvalo_103 = duni1_101 *dvalo_101 /100;    || 1% sal.base
            duni2_103 = duni1_103 *dcofi_999;         || las unidades, las que vengan
    |FINSI;
    |SI #9#3 = 3;
            dvalo_103 = (duni1_101*dvalo_101 /100) +(duni1_102 *dvalo_102 /100);
            duni2_103 = duni1_103 *dcofi_999;         || las unidades, las que vengan
    |FINSI;
        duni2_103 = duni2_103 ! 6;   || redondea unid. calculo
        ||dvalo_103 = dvalo_103 ! 2   || redondea valor, comentado por redon. euro
|SINO;
        duni1_103 = 0;
        duni2_103 = 0;
        dvalo_103 = 0;  || para no grabar despues
|FINSI;
|FPRC;

|PROCESO gratodo;     || GRABA O REGRABA CONCEPTOS 101,102 Y 103
    swgracon = 0;     || para que no machaque campos si existe
    swcongra = 0;     || para que no acumule si existe
    graba04 = 101;
    graba09 = duni1_101;
    graba10 = dvalo_101;
    graba11 = duni2_101;
|SI duni1_101 ! 0; |O dvalo_101 ! 0; |O duni2_101 ! 0;
    |HAZ grabacon;
|FINSI;
    graba04 = 102;
    graba09 = duni1_102;
    graba10 = dvalo_102;
    graba11 = duni2_102;
|SI duni1_102 ! 0; |O dvalo_102 ! 0; |O duni2_102 ! 0;
    |HAZ grabacon;
|FINSI;
    graba04 = 103;
    graba09 = duni1_103;
    graba10 = dvalo_103;
    graba11 = duni2_103;
|SI duni1_103 ! 0; |O dvalo_103 ! 0; |O duni2_103 ! 0;
    |HAZ grabacon;
|FINSI;
|FPRC;

||****************************************************************************
||          SUBRUTINAS DEL CALCULO DEL SALARIO BASE, ANTIGUEDAD Y PLUS
||****************************************************************************

|PROCESO poranti;  || halla el porcentaje o, en su caso, la cantidad, a aplicar
    danti_0 = 0;   || carga cantidad fija de antig./categ.
    danti_1 = 0;   || carga las unidades segun calculo
|FPRC;

|PROCESO anyanti;              || CALCULA LOS A¥OS DE ANTIGUEDAD
    anyos = 0;
|FPRC;

||****************************************************************************
||            CALCULO DE LAS UNIDADES Y DE LOS ACUMULADOS
||****************************************************************************

|PROCESO calunac;      || LECTURA DE CONCEPTOS
|CIERRAT #12;           || cierra aqui y abre abajo para dejarlo como viene
    #11#00 =    999;   || empresa
    #11#01 =  99999;   || trabajador
    #11#02 =  mes_a;   || mes
    #11#03 =      0;   || tipo
|GRABA 020#11n;        || graba la cabecera vacia para poder leer con |BUCLELIN
|LIBERA #11;
|LEE 120#11=;

    acum_10 = 0;
    acum_11 = 0;
    acum_12 = 0;
    acum_13 = 0;
    acum_30 = 0;
    acum_301 = 0;
    acum_302 = 0;
    acum_303 = 0;
    acum_31 = 0;
    acum_311 = 0;
    acum_312 = 0;
    acum_313 = 0;
    acum_40 = 0;
    acum_401 = 0;
    acum_402 = 0;
    acum_403 = 0;
    acum_41 = 0;    anti_41 = 0;
    acum_50 = 0;
    acum_90 = 0;
    horasex = 0;  || horas subvencionadas
    indemni = -1; || dias indemnizacion al a¤o
    vacacio = 30; || dias vacaciones a¤o
    horastp = 0;  || horas mes tiempo parcial

|BUCLELIN 0cal_999#11;

    acum_10 = acum_10 ! EURODB;
    acum_11 = acum_11 ! EURODB;
    acum_12 = acum_12 ! EURODB;
    acum_13 = acum_13 ! EURODB;
    acum_30 = acum_30 ! EURODB;
    acum_301 = acum_301 ! EURODB;
    acum_302 = acum_302 ! EURODB;
    acum_303 = acum_303 ! EURODB;
    acum_31 = acum_31 ! EURODB;
    acum_311 = acum_311 ! EURODB;
    acum_312 = acum_312 ! EURODB;
    acum_313 = acum_313 ! EURODB;
    acum_40 = acum_40 ! EURODB;
    acum_401 = acum_401 ! EURODB;
    acum_402 = acum_402 ! EURODB;
    acum_403 = acum_403 ! EURODB;
    acum_41 = acum_41 ! EURODB;    anti_41 = anti_41 ! EURODB;
    acum_50 = acum_50 ! EURODB;
    acum_90 = acum_90 ! EURODB;
|ABRET #12;
|FPRC;

||****************************************************************************
||             RUTINAS DEL CALCULO DE LAS UNIDADES Y ACUMULADOS
||****************************************************************************

|PROCESO cal_999;     || CALCULA LAS UNIDADES CALCULO DE LOS DEMAS CONCEPTOS
|SI #12#04 < 200; |O #12#04 > 208;    || los de pagas extras no entran
    |SI #12#04 > 103;                 || el 101,102 y 103 ya estan calculados
        |HAZ coefi;                   || saca el coeficiente y unidades
            duni1_999 = #12#9;                    || unidades de 'coefi'
            duni2_999 = duni1_999 * dcofi_999;    || halla unidades calculo
            duni2_999 = duni2_999 ! 6;            || redondea
            graba04 = #12#4;                      || carga concepto
            graba09 = duni1_999;                  || carga unidades
            graba10 = #12#10;
            graba11 = duni2_999;                  || carga unidades calculo
            swgracon = 0;                         || para que no machaque campos
            swcongra = 0;                         || para que no acumule
        |HAZ grabacon;                            || regraba linea calculo
    |SINO;
            concepto = #12#04;
        |HAZ convenio;
    |FINSI;
        duni2_999 = #12#11;
        dvalo_999 = #12#10;
        product = duni2_999 * dvalo_999;
        product = product ! EURODB;
    |SI #12#4]101;|Y #12#4[199;
                        acum_10 =acum_10 +product;
        |SI #10#8 = 1;  acum_11 =acum_11 +product;  |FINSI;
        |SI #10#8 = 2;  acum_12 =acum_12 +product;  |FINSI;
        |SI #10#8 = 3;  acum_13 =acum_13 +product;  |FINSI;
    |FINSI;
    |SI #12#4]301;|Y #12#4[304;
                        acum_30 =acum_30 +product;
        |SI #10#8 = 1;  acum_301 =acum_301 +product;  |FINSI;
        |SI #10#8 = 2;  acum_302 =acum_302 +product;  |FINSI;
        |SI #10#8 = 3;  acum_303 =acum_303 +product;  |FINSI;
    |FINSI;
    |SI #12#4]305;|Y #12#4[399;
                        acum_31 =acum_31 +product;
        |SI #10#8 = 1;  acum_311 =acum_311 +product;  |FINSI;
        |SI #10#8 = 2;  acum_312 =acum_312 +product;  |FINSI;
        |SI #10#8 = 3;  acum_313 =acum_313 +product;  |FINSI;
    |FINSI;
    |SI #12#4]400;|Y #12#4[410;
                        acum_40 =acum_40 +product;
        |SI #10#8 = 1;  acum_401 =acum_401 +product;  |FINSI;
        |SI #10#8 = 2;  acum_402 =acum_402 +product;  |FINSI;
        |SI #10#8 = 3;  acum_403 =acum_403 +product;  |FINSI;
    |FINSI;
    |SI #12#4]411;|Y #12#4[499; acum_41 =acum_41 +product; |FINSI;||IRPF exent.
    |SI #12#4]501;|Y #12#4[501; acum_51 =product;          |FINSI;
    |SI #12#4]502;|Y #12#4[502; acum_52 =product;          |FINSI;
    |SI #12#4]505;|Y #12#4[599; acum_50 =acum_50 +product; |FINSI;
    |SI #12#4]901;|Y #12#4[901; horasex =duni1_999;        |FINSI;||ho.extras
    |SI #12#4]902;|Y #12#4[902; indemni =duni1_999;        |FINSI;||di.indemn.
    |SI #12#4]904;|Y #12#4[904; vacacio =duni1_999;        |FINSI;||di.vacaci.
    |SI #12#4]905;|Y #12#4[905; horastp =duni1_999;        |FINSI;||ho.t.p.
    |SI #12#4]906;|Y #12#4[999; acum_90 =acum_90 +product; |FINSI;
    |HAZ cal_vaca;
|FINSI;
|FPRC;

||****************************************************************************
||        CALCULO DE LA ENFERMEDAD (503), ACCID. (504) Y COMPLEMENTOS
||****************************************************************************

|PROCESO calenfe;
    prom_enf = 0;   || promedio enfermedad (trabajador)
    prom_acc = 0;   || promedio accidente (trabajador)
    base_1 = 0;   || base enfermedad al 75% (503)
    base_2 = 0;   || base enfermedad al 60% (503)
    base_3 = 0;   || base accidente  al 75% (504)
    base_4 = 0;   || base maternidad al 75% (503)
    denfe_4= 0;   || complm. empresa al 60% (515)
    coba_1 = 0;   || base cotizacion enfermedad
    coba_2 = 0;               || base cotizacion accidente
    coba_3 = 0;               || base cotizacion absentismo
    acum_53 = 0;  || acumula (503)
    acum_54 = 0;  || acumula (504)
|FPRC;

||****************************************************************************
||                        CALCULO DE LA PRORRATA
||****************************************************************************

|PROCESO calpror;
    dvalo_pro = 0;   || acumulador total prorratas
    perio1 = 0;        perio5 = 0;
    perio2 = 0;        perio6 = 0;   || acumuladores de dias paga
    perio3 = 0;        perio7 = 0;
    perio4 = 0;        perio8 = 0;
|PARA paga = 1; |SI paga [ 8; |HACIENDO paga = paga + 1;
    |HAZ empieza;
    |HAZ busca;
    |SI swpro = 1;
            concep1 = 51 + paga;      || calcula el 1er. campo de conceptos
            concep2 = concep1 + 32;   || calcula el ultimo campo de conceptos
        |PARA campo = concep1; |SI campo [ concep2; |HACIENDO campo = campo + 8;
            |SI #9campo ! 0;
                                    desc = #9campo;  hasc = #9campo;
                |SI #9campo = 100;  desc = 101;      hasc = 199;     |FINSI;
                |SI #9campo = 400;  desc = 401;      hasc = 420;     |FINSI;
                |PARA actc = desc; |SI actc [ hasc; |HACIENDO actc = actc + 1;
                        concepto = actc;    || bucle para los 100 y 400
                    |HAZ leeconcep;         || lee y acumula conceptos paga
                |SIGUE;
            |FINSI;                  || si el concepto no es cero
        |SIGUE;                      || pasa los conceptos
        |HAZ leecon_200;             || lee el concepto de la paga de convenios
        |HAZ cal_unid;               || calcula las unidades
        |HAZ gracal_200;             || graba el concepto 2xx
    |FINSI;                          || si la prorrata corresponde a este mes
    |HAZ guarda_per;                 || guarda acumulado dias corresp. a paga
|SIGUE;                              || pasa las pagas de 1 a 8
|FPRC;

||****************************************************************************
||                   RUTINAS PARA EL CALCULO DE LA PRORRATA
||****************************************************************************

|PROCESO empieza;         || SACA LAS VARIABLES DEL CONVENIO SEGUN PAGA
    swpro = 0;
    perio = 0;
    dvalo_200 = 0;
    duni1_200 = 0;
    duni2_200 = 0;
    desde = paga + 3;      || desde prorrata
    hasta = paga + 11;     || hasta prorrata
    tipos = paga + 27;     || tipo de calculo
    diasp = paga + 35;     || dias
    porcp = paga + 43;     || porcentaje
    concep0 = paga + 200;  || concepto de paga
|FPRC;

|PROCESO busca;  || BUSCA SI HAY PRORRATA EN LA PAGA ACTUAL Y VA CONTANDO DIAS
    mas = 0;
|SI #9hasta < #9desde;
        nume1 = period - 1;
        des1 = #9desde;   des2 =       1;
        has1 =      12;   has2 = #9hasta;
    |SI #9hasta < mes_a; |Y #9desde [ mes_a;
            mas = 1;         || ejemplo: desde=4, hasta=3, actual=4
    |FINSI;
|SINO;
        nume1 = period;
        des1 = #9desde;   des2 =  0;
        has1 = #9hasta;   has2 = -1;
|FINSI;
    nume1 = nume1 + mas;
    nume2 = period + mas;

    des = (nume1 * 100) + #9desde;  || mes desde
    has = (nume2 * 100) + #9hasta;  || mes hasta
    act = (period  * 100) + mes_a;     || mes actual

|SI act ] des; |Y act [ has;
        swpro = 1;
|FINSI;

|PARA pp = des1; |SI pp [ has1; |HACIENDO pp = pp + 1;
    |SI #0#7 ! "M";|Y #0#7 ! "X";  || si es mensual cuenta por 30 los meses
            mesnum = pp;
            anynum = nume1;
        |HAZ ulti_dia;
    |SINO;
            topemes = 30;
    |FINSI;
        perio = perio + topemes;
|SIGUE;
|PARA pp = des2; |SI pp [ has2; |HACIENDO pp = pp + 1;
    |SI #0#7 ! "M";|Y #0#7 ! "X";  || si es mensual cuenta por 30 los meses
            mesnum = pp;
            anynum = nume2;
        |HAZ ulti_dia;
    |SINO;
            topemes = 30;
    |FINSI;
        perio = perio + topemes;
|SIGUE;
|FPRC;

|PROCESO leeconcep;  || lee y acumula concepto para paga
    doce = concepto;
|HAZ leedoce;
|SI FSalida = 0;
        duni1_999 = #12#09;
        duni2_999 = #12#11;
        dvalo_999 = #12#10;
    |SI #12#05 = "F"; |Y duni1_999 = 0; || si es calculo 'F' carga las unidades
            duni1_999 = 1;              || con la unidad !!!
    |FINSI;
    |HAZ cal_valo;   || va acumulando el valor de los 5
|FINSI;              || si el concepto existe en calculos
|FPRC;

|PROCESO leecon_200;      || LEE CONCEPTO 2xx EN CONVENIOS
    #10#0 = #9#0;
    #10#2 = concep0;
|LEE 000#10=;
|SI FSalida ! 0;
    |DDEFECTO #10;
|FINSI;
|SI #10#4 = 0;  dcofi_999 =       1;  |FINSI;
|SI #10#4 = 1;  dcofi_999 = dcofi_1;  |FINSI;
|SI #10#4 = 2;  dcofi_999 = dcofi_2;  |FINSI;
|FPRC;

|PROCESO cal_valo;    || CALCULA EL VALOR ACUMULADO DE LOS 5 CONCEPTOS POR PAGA
    product = duni1_999 * dvalo_999;
    product = product ! EURODB;
|SI #9tipos = "N";   || si es tipo 'N' o 'P'
    |SI #12#05="D";               divide=dia_2;     |FINSI;  || dias mes
    |SI #12#05="M";               divide=dia_4;     |FINSI;  || dias cotiz.
    |SI #12#05="L";|O #12#05="K"; divide=duni1_999; |FINSI;  || dias labo./fest.
    |SI #12#05="V";|O #12#05="F"; divide=   30;     |FINSI;  || dias ???
|SINO;
        divide = 100;
|FINSI;
|SI concepto ] 401;|Y concepto [ 420;
    |SI #9#140 = "S";
            dvalo_200 = dvalo_200 + (product / divide);
    |FINSI;
|SINO;
        dvalo_200 = dvalo_200 + (product / divide);
|FINSI;
|FPRC;

|PROCESO cal_unid;   || CALCULA LAS UNIDADES DEL CONCEPTO 2xx
|SI #9tipos = "N";   || si es tipo 'N' o 'P'
        duni1_200 = (dia_4 * #9diasp * dcofi_999) / perio;
|SINO;
        duni1_200 = #9porcp * dcofi_999;
|FINSI;
    duni1_200 = duni1_200 ! 4;  || redondea
    ||dvalo_200 = dvalo_200 ! 2  || comentado por redon. euro
|FPRC;

|PROCESO gracal_200;    || GRABA EL CONCEPTO 2xx;
    swgracon = 1;       || para que grabe todos los campos
    swcongra = 0;       || para que no acumule si existe
    graba04 = concep0;
|HAZ cogegra;
    graba09 = duni1_200;
    graba10 = dvalo_200;
    graba11 = duni2_200;
|SI duni1_200 ! 0; |O duni2_200 ! 0; |O dvalo_200 ! 0;
    |HAZ grabacon;
        product = dvalo_200 * duni1_200;
        product = product ! EURODB;
        dvalo_pro = dvalo_pro + product;    || acumula total prorratas
|FINSI;
|FPRC;

|PROCESO guarda_per;       || GUARDA EL ACUMULADO DIAS CORRESP. A LA PAGA
|SI paga = 1;  perio1 = perio;  |FINSI;
|SI paga = 2;  perio2 = perio;  |FINSI;
|SI paga = 3;  perio3 = perio;  |FINSI;
|SI paga = 4;  perio4 = perio;  |FINSI;
|SI paga = 5;  perio5 = perio;  |FINSI;
|SI paga = 6;  perio6 = perio;  |FINSI;
|SI paga = 7;  perio7 = perio;  |FINSI;
|SI paga = 8;  perio8 = perio;  |FINSI;
|FPRC;

||****************************************************************************
||                        CALCULO DE LAS PAGAS
||****************************************************************************

|PROCESO calpaga;
    acum1_200 = 0;
    acum2_200 = 0;
    swpaga = 0;
    dcofi_999 = dcofi_1;
|PARA paga = 1; |SI paga [ 8; |HACIENDO paga = paga + 1;
    |HAZ confec_0;                || inicializa
    |HAZ confec_1;                || calcula pagas
|SIGUE;
|FPRC;

||****************************************************************************
||                 RUTINAS PARA EL CALCULO DE LAS PAGAS
||****************************************************************************

|PROCESO confec_0;   || INICIALIZA VALORES
    duni1_999 = 0;
    duni2_999 = 0;
    duni2_200 = 0;
    dvalo_999 = 0;
    dias_dto = 0;
    mesco = paga + 19;        || mes confeccion
|FPRC;

|PROCESO confec_1;     || LANZA CALCULO DE PAGAS
|SI #9mesco = 99;
    |HAZ cargavar;                 || saca variables de convenio y trabajador
    |HAZ leecal_200;               || lee el concepto paga
    |SI #9mesco = 99;              || si el mes de confeccion es el 99
        |HAZ confec_2;             || /calcula solo lo de este mes
        |HAZ grapaga;
    |FINSI;
|FINSI;
|FPRC;

||****************************************************************************
||                 SUBRUTINAS DEL CALCULO DE LAS PAGAS
||****************************************************************************

|PROCESO cargavar;   || SACA LAS VARIABLES DE CONVENIO Y TRABAJADOR SEGUN PAGA
    desde = paga + 3;         || desde prorrata
    hasta = paga + 11;        || hasta prorrata
    tipos = paga + 27;        || tipo de calculo
    diasp = paga + 35;        || dias
    porcp = paga + 43;        || porcentaje
    concep0 = paga + 200;     || concepto de paga
|SI paga [ 5;                                        || desorden de campos !!!
        diast = paga + 49;    || dias dto. enfe/acci
        acumb = paga + 54;    || acumulado bases o dias
        bases = paga + 64;    || bases diferidas
|SINO;
        diast = paga + 72;    || dias dto. enfe/acci
        acumb = paga + 75;    || acumulado bases o dias
        bases = paga + 78;    || bases diferidas
|FINSI;
|SI paga = 1; perio = perio1; |FINSI;
|SI paga = 2; perio = perio2; |FINSI;
|SI paga = 3; perio = perio3; |FINSI;
|SI paga = 4; perio = perio4; |FINSI;   || acumuladores de dias pagas
|SI paga = 5; perio = perio5; |FINSI;
|SI paga = 6; perio = perio6; |FINSI;
|SI paga = 7; perio = perio7; |FINSI;
|SI paga = 8; perio = perio8; |FINSI;
|FPRC;

|PROCESO leecal_200;    || LEE CONCEPTO DE LINEAS DE CALCULOS CORREP. A PAGA
    doce = concep0;
|HAZ leedoce;
|SI FSalida ! 0;         || si no esta en calculos, lee convenio
        #10#0 = #9#0;
        #10#2 = concep0;
    |LEE 000#10=;
    |SI FSalida ! 0;
        |DDEFECTO #10;
    |FINSI;
        #12#05 = #10#3;   || carga calculo alfa de convenios
        #12#06 = #10#4;   || carga calculo nume de convenios
        #12#07 = #10#5;   || carga situacion de convenios
        #12#08 = #10#6;   || carga descripcion de convenios
        #12#09 = 0;
        #12#10 = 0;
        #12#11 = 0;
|FINSI;
    duni1_999 = #12#09;
    duni2_999 = #12#11;
    dvalo_999 = #12#10;
|FPRC;

|PROCESO confec_2;    || CALCULA PAGA CUANDO ES 99 (propor. a los dias en alta)
|SI #9tipos = "N";    || si es tipo 'N' o 'P'
        duni2_999 = (dia_4 * dcofi_999) * (#9diasp / perio);
|SINO;
        duni2_999 = #9porcp * dcofi_999;
|FINSI;
    duni2_999 = duni2_999 ! 4;  || redondea
    product = duni2_999 * dvalo_999;  product = product ! EURODB;
    acum1_200 = acum1_200 + product;
|FPRC;

|PROCESO grapaga;       || REGRABA LA PAGA  (SIEMPRE 99)
    graba04 = concep0;
    swcongra = 0;       || para que no acumule si existe
    swgracon = 1;       || para que regrabe los campos si existe
    graba05 = "P";
    graba06 = 0;
    graba07 = #12#7;
    graba08 = #12#8;
    duni1_999 = duni1_999 ! 4;
    duni2_999 = duni2_999 ! 4;
    || dvalo_999 = dvalo_999 ! 2 ||comentado por redon. euro
    graba09 = duni1_999;
    graba10 = dvalo_999;
    graba11 = duni2_999;
|SI duni1_999 ! 0; |O duni2_999 ! 0; |O dvalo_999 ! 0;
    |HAZ grabacon;
|FINSI;
|FPRC;

||****************************************************************************
||                      CALCULO FINAL 1 (ajusta a brutos)
||****************************************************************************

|PROCESO calfin1;    || CALCULOS ANTERIORES AL AJUSTE A BRUTOS
|HAZ calirpfr;  || calcula base IRPF recibos
|HAZ calbruto;  || calculo bruto recibo
|HAZ calajus1;  || ajusta a brutos, concepto 407 (no cotiza) y 120 (cotiza)
|HAZ calirpfp;  || calcula base IRPF pagas
|FPRC;

||****************************************************************************
||                       RUTINAS CALCULO FINAL 1
||****************************************************************************

|PROCESO calirpfr;   || CALCULA BASES IRPF
    acum_99  = acum_10 + acum_30  + acum_31  + acum_40;   || total
    acum_99  = acum_99 +acum_50+acum_51+acum_52+acum_53+acum_54+acum_90+acum1_200;
    acum_991 = acum_11 + acum_301 + acum_311 + acum_401;  || irpf 1
    acum_991 = acum_991+acum_50+acum_51+acum_52+acum_53+acum_54+acum_90+acum1_200;
    acum_992 = acum_12 + acum_302 + acum_312 + acum_402;  || irpf 2
    acum_993 = acum_13 + acum_303 + acum_313 + acum_403;  || irpf 3

    acum_99 = acum_99  ! EURODB;
    acum_991= acum_991 ! EURODB;
    acum_992= acum_992 ! EURODB;
    acum_993= acum_993 ! EURODB;
|FPRC;

|PROCESO calbruto;   || CALCULA BRUTO RECIBOS Y PAGAS
    bruto_rec = acum_99 + acum_41 + anti_41;   || bruto recibo
    bruto_pag = acum2_200;                     || bruto pagas
    bruto_rec = bruto_rec ! EURODB;
    bruto_pag = bruto_pag ! EURODB;
|FPRC;

|PROCESO calajus1;   || AJUSTA A BRUTOS (cotizando -120-, o sin cotizar -407-)
|SI #0#42 [ 2; |Y #0#42 ! 0;
    |SI #0#42 = 1;
            concepto = 407;
    |SINO;
            concepto = 120;
    |FINSI;
    |HAZ ajustes;
    |SI convenio = 0;
            stop = #0#43 * dcofi_999;   stop = stop ! EURODB;
            ajus_1 = stop - bruto_rec;
            graba04 = concepto;
        |HAZ cogegra;
            graba09 = 1;
            graba10 = ajus_1;
            graba11 = 1;
            swgracon = 1;     || para que regrabe campos
            swcongra = 1;     || para que acumule si existe
        |HAZ grabacon;
        |SI #0#42 = 1;
                            acum_40  =acum_40 + ajus_1;
            |SI #10#8 = 1;  acum_401 =acum_401 +ajus_1;  |FINSI;
            |SI #10#8 = 2;  acum_402 =acum_402 +ajus_1;  |FINSI;
            |SI #10#8 = 3;  acum_403 =acum_403 +ajus_1;  |FINSI;
        |SINO;
                            acum_10 =acum_10 +ajus_1;
            |SI #10#8 = 1;  acum_11 =acum_11 +ajus_1;  |FINSI;
            |SI #10#8 = 2;  acum_12 =acum_12 +ajus_1;  |FINSI;
            |SI #10#8 = 3;  acum_13 =acum_13 +ajus_1;  |FINSI;
        |FINSI;
            bruto_rec = bruto_rec + ajus_1; || actualiza bruto recibo
        |SI #10#8 = 1;  acum_991 =acum_991 +ajus_1;  |FINSI;
        |SI #10#8 = 2;  acum_992 =acum_992 +ajus_1;  |FINSI;
        |SI #10#8 = 3;  acum_993 =acum_993 +ajus_1;  |FINSI;
            acum_99 = acum_99 + ajus_1;     || actualiza IRPF total
    |FINSI;
|FINSI;
|FPRC;

|PROCESO calirpfp;   || CALCULA RETENCIONES IRPF
    reten_11 = acum_991 % #0#44;   || retencion IRPF 1
    reten_12 = acum_992 % #0#84;   || retencion IRPF 2
    reten_13 = acum_993 % #0#85;   || retencion IRPF 3
    reten_11 = reten_11 ! EURODB;
    reten_12 = reten_12 ! EURODB;
    reten_13 = reten_13 ! EURODB;
    reten_1 = reten_11 + reten_12 + reten_13;  || total retenciones

    reten_2 = acum2_200 % #0#44;  || retencion IRPF pagas
    reten_2 = reten_2 ! EURODB;
|FPRC;

||****************************************************************************
||                            CALCULO FINAL 2 (ajuste a liquidos)
||****************************************************************************

|PROCESO calfin2;   || CALCULOS POSTERIORES AL AJUSTE A BRUTOS
|HAZ calextra;      || calcula bases h. extras redondeadas
|HAZ calenacc;      || calcula base cotizacion
|HAZ calcotiz;      || calcula bases cotizacion redondeadas de con.com. y D+F+P
|HAZ calboni1;      || calcula bases bonificacion y dtos. por bonificacion
|HAZ calajus3;      || ajusta a liquidos, concepto 407 (no cotiza)
|HAZ calajus4;      || ajusta a liquidos, concepto 120 (cotiza)
|HAZ calajus5;      || ajusta a liquidos y brutos, concepto 411 (no cotiza y no IRPF)
|HAZ calssemp;      || calcula S.S. cgo. empresa
|FPRC;

||****************************************************************************
||                       RUTINAS CALCULO FINAL 2
||****************************************************************************

|PROCESO calextra;      || CALCULA BASE H.EXTRA REDONDEADAS A 300
|SI #0#7 = "M"; |O #0#7 = "X";
    dacum_30 = acum_30 / 300;
    dacum_30 = dacum_30 ! EURODB;
    dacum_30 = dacum_30 * 300;     || horas extras 1

    dacum_31 = acum_31 / 300;
    dacum_31 = dacum_31 ! EURODB;
    dacum_31 = dacum_31 * 300;     || horas extras 2
|SINO;
    tempor = acum_30 / dcoti_0;
    tempor = tempor / 10;
    tempor = tempor ! EURODB;
    tempor = tempor * 10;
    dacum_30 = tempor * dcoti_0;

    tempor = acum_31 / dcoti_0;
    tempor = tempor / 10;
    tempor = tempor ! EURODB;
    tempor = tempor * 10;
    dacum_31 = tempor * dcoti_0;
|FINSI;
|FPRC;

|PROCESO calenacc;      || CALCULA BASE COTIZACION ENFER/ACCI.
    base_5 = coba_1 + coba_2;
|FPRC;

|PROCESO calcotiz;      || CALCULA BASE CON.COMUNES Y BASE D+F+P
    swajuen = 0;    || para saber si tiene que redondear o no C.C.
    swajuac = 0;    || idem de A.T.

    base_6 = base_5 + acum_10 + dvalo_pro + coba_3; || base cotizacion C.C.
    base_7 = base_6;                                || base cotizacion A.T.

    campomax = #0#38 + 35;  coti_1 = #15campomax * dcoti_0; || maximo C.C.
    campomin = #0#38 + 23;  coti_0 = #15campomin * dcoti_0; || minimo C.C.
|SI horastp ! 0;
    |SI #0#7 = "X";|O #0#7 = "T";
        |SI #0#38 =  1;  coti_0 = 510 * horastp;  |FINSI;
        |SI #0#38 =  2;  coti_0 = 423 * horastp;  |FINSI;
        |SI #0#38 =  3;  coti_0 = 368 * horastp;  |FINSI;   || minimo C.C.
        |SI #0#38 =  4;  coti_0 = 342 * horastp;  |FINSI;   || /de tiempos
        |SI #0#38 =  5;  coti_0 = 342 * horastp;  |FINSI;   || /parciales
        |SI #0#38 =  6;  coti_0 = 342 * horastp;  |FINSI;
        |SI #0#38 =  7;  coti_0 = 342 * horastp;  |FINSI;
        |SI #0#38 =  8;  coti_0 = 342 * horastp;  |FINSI;
        |SI #0#38 =  9;  coti_0 = 342 * horastp;  |FINSI;
        |SI #0#38 = 10;  coti_0 = 342 * horastp;  |FINSI;
        |SI #0#38 = 11;  coti_0 = 225 * horastp;  |FINSI;
    |FINSI;
|FINSI;
    coti_0 = coti_0 ! EURODB; || minimo C.C.
    coti_1 = coti_1 ! EURODB; || maximo C.C.
|SI coti_0 ] base_6;  base_6 = coti_0;  swajuen = 1;  |FINSI;
|SI coti_1 [ base_6;  base_6 = coti_1;  swajuen = 1;  |FINSI;

|SI swajuen = 0;
    |SI #0#7 ! "D";
            base_6 = base_6 / 300;
            base_6 = base_6 ! EURODB;
            base_6 = base_6 * 300;
    |SINO;
            coti_2 = base_6 / dcoti_0;
            coti_2 = coti_2 / 10;
            coti_2 = coti_2 ! EURODB;
            coti_2 = coti_2 * 10;
            base_6 = coti_2 * dcoti_0;
    |FINSI;
|FINSI;

    coti_0 = #15#20 * dcoti_0;   || maximo A.T.
|SI #0#38 = 12; coti_1 = #15#22 * dcoti_0;  |FINSI;
|SI #0#38 = 11; coti_1 = #15#21 * dcoti_0;  |FINSI;  || minimo A.T.
|SI #0#38 < 11; coti_1 = #15#23 * dcoti_0;  |FINSI;
|SI horastp ! 0;
    |SI #0#7 = "X";|O #0#7 = "T";
        |SI #0#38 = 12;  coti_1 = 225 * horastp;  |FINSI; || minimo A.T.
        |SI #0#38 = 11;  coti_1 = 225 * horastp;  |FINSI; || /de tiempos
        |SI #0#38 < 11;  coti_1 = 342 * horastp;  |FINSI; || /parciales
    |FINSI;
|FINSI;
    coti_0 = coti_0 ! EURODB; || maximo A.T.
    coti_1 = coti_1 ! EURODB; || minimo C.C.
|SI base_7 ] coti_0;   base_7 = coti_0;  swajuac = 1;  |FINSI;
|SI base_7 [ coti_1;   base_7 = coti_1;  swajuac = 1;  |FINSI;

|SI swajuac = 0;
    |SI #0#7 ! "D";
            base_7 = base_7 / 300;
            base_7 = base_7 ! EURODB;
            base_7 = base_7 * 300;
    |SINO;
            coti_2 = base_7 / dcoti_0;
            coti_2 = coti_2 / 10;
            coti_2 = coti_2 ! EURODB;
            coti_2 = coti_2 * 10;
            base_7 = coti_2 * dcoti_0;
    |FINSI;
|FINSI;

    base_7 = base_7 + dacum_30 + dacum_31;  || base A.T. + bases h. extras
|FPRC;

|PROCESO calboni1;       || CALCULA BASES BONIFICACION
    bon_0 = 0;           || base bonificacion 1
    bon_1 = por1_cont;   || (%) cont. comunes
    bon_2 = por1_dfpg;   || (%) D+F+P
    bon_3 = 0;           || base bonificacion 2
|SI #0#1 ! 0; |Y dboni_1 ! 0;
        lin_1 = #16#6 % #16#3;
        lin_2 = #16#7 % #16#3;
        bon_0 = base_6;
        bon_3 = base_7;
        bon_1 = por1_cont - lin_1;
        bon_2 = por1_dfpg - lin_2;
    |SI #16#9 = "S";
            bon_1 = bon_1 - #15#5;
    |FINSI;
    |SI dboni_2 ! 0;
            bon_0 = bon_0 * dboni_1 / (dboni_1 + dboni_2);
            bon_3 = bon_3 * dboni_1 / (dboni_1 + dboni_2);
        |SI #0#7 = "M"; |O #0#7 = "X";
                bon_0 = bon_0 / 300;
                bon_0 = bon_0 ! EURODB;
                bon_0 = bon_0 * 300;
                bon_3 = bon_3 / 300;
                bon_3 = bon_3 ! EURODB;
                bon_3 = bon_3 * 300;
        |SINO;
                coti_2 = bon_0  / dboni_1;
                coti_2 = coti_2 / 10;
                coti_2 = coti_2 ! EURODB;
                coti_2 = coti_2 * 10;
                bon_0  = coti_2 * dboni_1;
                coti_2 = bon_3  / dboni_1;
                coti_2 = coti_2 / 10;
                coti_2 = coti_2 ! EURODB;
                coti_2 = coti_2 * 10;
                bon_3  = coti_2 * dboni_1;
        |FINSI;
    |FINSI;
|FINSI;
    dto_con = base_6 % bon_1;       dto_con = dto_con ! EURODB; || dto. cont. comunes
    dto_dfp = base_7 % bon_2;       dto_dfp = dto_dfp ! EURODB; || dto. D+F+P
    dto_ho1 = dacum_30 % por1_hor1; dto_ho1 = dto_ho1 ! EURODB; || dto. horas 1
    dto_ho2 = dacum_31 % por1_hor2; dto_ho2 = dto_ho2 ! EURODB; || dto. horas 2
    dto_tot = dto_con + dto_dfp + dto_ho1 + dto_ho2;
    dto_tot = dto_tot + reten_1;  || total dtos. (excluidos anticipos/especie !)
    liquido = bruto_rec - dto_tot;     || liquido
|FPRC;

|PROCESO calajus3;   || AJUSTA A LIQUIDOS (no cotizar -407-)
|SI #0#42 = 3;
        concepto = 407;
    |HAZ ajustes;
    |SI convenio = 0;
            stop = #0#43 * dcofi_999;   stop = stop ! EURODB;
            difere = stop - liquido;           || cantidad a ajustar
            ajus_2 = (difere*100)/(100-#0#44); || cantidad a ajustar sin (%)ret.
            ajus_2 = ajus_2 ! EURODB;
            difere = (bruto_rec + ajus_2) - (dto_tot - reten_1);  || recalculo
            difer1 = (acum_99 + ajus_2) % #0#44;                  || /del IRPF
            difer1 = difer1 ! EURODB;
            difere = difere - difer1;
            ajus_2 = ajus_2 + (stop - difere);  || reajuste por diferencia
                        acum_40 = acum_40 + ajus_2;
        |SI #10#8 = 1;  acum_401 =acum_401 +ajus_2;  |FINSI;
        |SI #10#8 = 2;  acum_402 =acum_402 +ajus_2;  |FINSI;
        |SI #10#8 = 3;  acum_403 =acum_403 +ajus_2;  |FINSI;
        |HAZ calirpfr;
        |HAZ calbruto;                           || recalcula
        |HAZ calirpfp;
            graba04 = concepto;
        |HAZ cogegra;
            graba09 = 1;
            graba10 = ajus_2;
            graba11 = 1;
            swgracon = 1;  || para que regrabe los campos si existe
            swcongra = 1;  || para que acumule si existe
        |HAZ grabacon;
    |FINSI;
|FINSI;
|FPRC;

|PROCESO calajus4;   || AJUSTA A LIQUIDOS (cotiza -120-)
|SI #0#42 = 4;
        difere = 0;
        concepto = 120;
    |HAZ ajustes;
    |SI convenio = 0;
            stop = #0#43 * dcofi_999;   stop = stop ! EURODB;
        |PARA ; |SI liquido ! stop; |HACIENDO;
                difer1 = stop - liquido;
                difere = difere + difer1;   || acumula diferencia en var.trab.
                acum_10 = acum_10 + difer1; || acumula diferencia en
                                            || /acumulados conceptos 1??
            |SI #10#8 = 1;  acum_11 =acum_11 +difer1;  |FINSI;
            |SI #10#8 = 2;  acum_12 =acum_12 +difer1;  |FINSI;
            |SI #10#8 = 3;  acum_13 =acum_13 +difer1;  |FINSI;
            |HAZ calirpfr; || recalcula base IRPF recibo
            |HAZ calbruto; || recalculo bruto recibo
            |HAZ calirpfp; || recalcula retenciones IRPF recibo
            |HAZ calcotiz; || recalcula b.cotiz. redondeadas de cn.cm. y D+F+P
            |HAZ calboni1; || recalcula b.bonif., dtos. por bonif. y LIQUIDO!!
            liquido = liquido ! EURODB;
        |SIGUE;
            graba04 = concepto;
        |HAZ cogegra;
            graba09 = 1;
            graba10 = difere;   || diferencia acumulada en el bucle
            graba11 = 1;
            swgracon = 1;   || para que regrabe si existe
            swcongra = 1;   || para que acumule si existe
        |HAZ grabacon;
    |FINSI;
|FINSI;
|FPRC;

|PROCESO calajus5;  || AJUSTA A BRUTOS Y LIQUIDOS (sin cotizar y sin IRPF -411-)
|SI #0#42 ] 5;
         concepto = 411;
    |HAZ ajustes;
    |SI convenio = 0;
            stop = #0#43 * dcofi_999;   stop = stop ! EURODB;
        |SI #0#42 = 5;
                 ajus_1 = stop - bruto_rec;
        |SINO;
                 ajus_1 = stop - liquido;
        |FINSI;
            graba04 = concepto;
        |HAZ cogegra;
            graba09 = 1;
            graba10 = ajus_1;
            graba11 = 1;
            swgracon = 1;  || para que regrabe si existe
            swcongra = 1;  || para que acumule si existe
        |HAZ grabacon;
            acum_41 = acum_41 + ajus_1;     || actualiza acumulado
            bruto_rec = bruto_rec + ajus_1; || actualiza bruto recibo
        |SI #0#42 = 6;
                liquido = liquido + ajus_1; || actualiza liquido recibo
        |FINSI;
    |FINSI;
|FINSI;
|FPRC;

|PROCESO calssemp;    || CALCULA S.S. A CGO. EMPRESA
    base_A = 0; base_B = 0;
    base_C = 0; base_D = 0;
    base_8 = (base_6 % por2_cont) + (base_7 % por2_dfpg);  || base S.S.
    base_9 = (base_7 - base_5) % (#17#1 + #17#2);          || epigrafe normal
    base_9 = base_9 + (base_5 % (#15#3 + #15#4));          || epigrafe enferm.
|SI #0#1 ! 0;
        lin_1 = #16#4 % #16#3;
        lin_2 = #16#5 % #16#3;
        base_A = bon_0 % lin_1; || cuota bonif. S.S. linea 1
        base_B = bon_3 % lin_2; || cuota bonif. S.S. linea 2
    |SI #16#08 = "S";
            base_C = base_9 % #16#3;      || cuota bonificada accidentes
    |FINSI;
    |SI #16#11 = "S";
            base_D = horasex * #16#12;    || cuota (2) bonif. horas formacion
    |FINSI;
|FINSI;
    base_E = base_8 +base_9 - (base_A +base_B +base_C +base_D); || S.S. empresa
    base_F = base_A + base_B + base_C;    || cuota (1) bonif. resto
|FPRC;

||****************************************************************************
||                   SUBRUTINAS CALCULOS FINALES
||****************************************************************************

|PROCESO ajustes;
|HAZ convenio;
|SI convenio = 0;
    |SI #10#4 = 0;  dcofi_999 =       1;  |FINSI;
    |SI #10#4 = 1;  dcofi_999 = dcofi_1;  |FINSI;
    |SI #10#4 = 2;  dcofi_999 = dcofi_2;  |FINSI;
|FINSI;
|FPRC;

||****************************************************************************
||                 CALCULOS DE PRESENTACION EN PANTALLA
||****************************************************************************

|PROCESO presenta;
|PINPA #1#0;
|PARA mes_a = mesdes; |SI mes_a [ meshas; |HACIENDO mes_a = mes_a + 1;
        #11#0 =   999;
        #11#1 = 99999;
        #11#2 = mes_a;
        #11#3 =     0;
    |LEE 020#11=;
    |HAZ displaya;
|SIGUE;
|CONFI "2400SDesea informe impreso : ";
|SI FSalida = 0;
    |IMPRE 0;
    |SI FSalida = 0;
        |INFOR "nomcaltt";
        |PRINT;
        |PIEINF;
        |FININF;
    |FINSI;
    |FINIMP;
|FINSI;
|FPRC;

|PROCESO displaya;
    bruto = 44 + mes_a;
    empre = 56 + mes_a;
    total = 68 + mes_a;
    #0bruto = #11#46;
    #0empre = #11#45;
    #0total = #11#45 + #11#46;
|PINTA #0bruto;
|PINTA #0empre;
|PINTA #0total;
    #0#81 = #0#81 + #0bruto;
    #0#82 = #0#82 + #0empre;
    #0#83 = #0#83 + #0total;
|PINTA #0#81;
|PINTA #0#82;
|PINTA #0#83;
|FPRC;

|PROCESO MinConst;
     #nomconst#88 = #labtraba#247;
     #nomconst#79 = aAlfa1;
     #nomconst#0 = 001;
|FPRC;

|PROCESO MaxConst;
     #nomconst#88 = #labtraba#247;
     #nomconst#79 = aAlfa2;
     #nomconst#0 = 999;
|FPRC;

|PROCESO ConsultaConst; |TIPO 0;
     inkey = FSalida;
     |ABRE #nomconst;
     |SI inkey = 13;
          fecsys = ~;
          aAlfa = fecsys;
          aAlfa1 = "01.01." + (aAlfa % -104);
          aAlfa2 = "31.12." + (aAlfa % -104);
          |CONSULTA_F_CLAVE #4#nomconst, MinConst, MaxConst;
          |SI FSalida = 0;
               #0Campo = #nomconst#0;
          |FINSI;
          |PINTA #0Campo;
     |SINO;
          |SELECT #2#nomconst;
          #nomconst#0 = #0Campo;
          |LEE 000#nomconst.=;
          |SI FSalida ! 0;
               aAlfa = "    ERROR: Constante no encontrada en el Fichero de Constantes. ";
               aAlfa = aAlfa + "Pulse F5 para consultar disponibles."
               |MENAV aAlfa;
               |ERROR;
          |FINSI;
     |FINSI;
     |PINTA 0734, #nomconst#1;
     |CIERRA #nomconst;
|FPRC;
