|FICHEROS;
     nomcalim #0;
     nomtmp02 #100;
     :integral/dsam0000;
     :integral/dsam0101 #100;      || Poblaciones (codigos postales)
     labcentr;
     labempre;
     nomfical;
     nomfiest;
     nomfilin;
     labmunic;
     nomconca;
     labcalen;
     labmunic;
     nomcontr;
     labcontr;
     labauton;

     nomtmpf1 #1000;
     nomtmpf2 #1001;

     nomconfi;
|FIN;

|VARIABLES;
     nCalc   = 0;
     nCalc1  = 0;
     nCalc2  = 0;
     nCalc3  = 0;
     nCont   = 0;
     nEmpresa = 0;
     nCentro  = 0;

     aObserv1 = "";
     aObserv2 = "";
     aObserv3 = "";

     fFecha  = @;
     fFechaIni = @;
     fFechaFin = @;
     aFecha = "";
     aAny = "";
     nAny = 0;
     nTipo = 0;
     aTipo = "";
     aAlfa = "";
     aAlfa1 = "";
     &aNomLogo = "";

     aAlfaART  = "";
     aAlfaART1 = "";
     aAlfaART2 = "";
     aIP       = "";

     nActividad = 0;
     nAutonomia = 0;
     nPoblacion = 0;
     aFiesta = "";
     aDia = "";
     aMes = "";
     nCodProv = 0;
     nCodPob = 0;
     aCodPostal = "";
     nCalendario = 0;
     nCampo = 0;
     nCampo1 = 0;
     nCampo2 = 0;
     nCampo3 = 0;
     nCampo4 = 0;
     nPeso = 0;
     nMes = 0;
     nDia = 0;
     aConvenio = "";
     swComentario = 0;
     aCadena = "";
     aLen = "";
     nLen = 0;
     aCaracRaro = "";
     nCampos = 0;
     nPos = 0;
     nAscii = 0;
     aAlfa2 = "";
     nNume = 0;
     swCalen = 0;

   {1}aContiene = "";

     &nCodCal = 0;
     &aAnyCal = "";
     &aCPCal = "";
     &aMunPos = "";

     nFecha = 0;
     aDiaMes = "";
     &nDiasAny = 0;
     &nSabados = 0;
     &nDomingos = 0;
     &nLocal = 0;
     &nAutonomica = 0;
     &nNacional = 0;
     &aAutonomia = "";
     &nDiasVac = 0;
     &swSabadosLab = 0;
     &swDomingosLab = 0;
     &swHayFestConv = 0;
     swEncontrada = 0;
     swAsignado = 0;
|FIN;

|PROCESO Activa6; |TIPO 7;
     |SI #0#5 = 1;
          |CAMPO_MODIFICA #0#6, 1, "S";
          |CAMPO_VISUAL #0#6, 1, "S";
          |PINTA 1625, " Tipo ......:              ";
          |PINTA #0#6;
     |SINO;
          |CAMPO_MODIFICA #0#6, 1, "N";
          |CAMPO_VISUAL #0#6, 1, "N";
          |PINTA 1625, "                           ";
     |FINSI;
|FPRC;

|PROCESO DefectoAny; |TIPO 7;
     fFecha = ~;
     aAlfa = fFecha;
     aAlfa = aAlfa % 402;
     nNume = aAlfa;
     |SI nNume ] 11;
          aAlfa = fFecha;
          aAlfa = aAlfa % -104;
          nNume = aAlfa;
          #0#4 = nNume + 1;
     |SINO;
          aAlfa = fFecha;
          aAlfa = aAlfa % -104;
          nNume = aAlfa;
          #0#4 = nNume;
     |FINSI;
     |PINTA #0#4;
|FPRC;

|PROCESO CrystalRaros;
       |QBF aAlfa;

       aCadena = "";
       aLen    = aAlfa % 0;
       nLen    = aLen;
       |PARA nCampos = 1;  |SI nCampos [ nLen;  |HACIENDO nCampos = nCampos + 1;
             nPos       = (100 * nCampos) + 1;
             aCaracRaro = aAlfa % nPos;

             nAscii = &aCaracRaro;

             |SI aCaracRaro = "Á";  aCaracRaro = "A";  |FINSI;
             |SI aCaracRaro = "É";  aCaracRaro =  &144;  |FINSI;
             |SI aCaracRaro = "È";  aCaracRaro = "E";  |FINSI;
             |SI aCaracRaro = "Í";  aCaracRaro = "I";  |FINSI;
             |SI aCaracRaro = "Ó";  aCaracRaro = "O";  |FINSI;
             |SI aCaracRaro = "Ò";  aCaracRaro = "O";  |FINSI;
             |SI aCaracRaro = "Ú";  aCaracRaro = "U";  |FINSI;
             |SI aCaracRaro = "Ü";  aCaracRaro = "U";  |FINSI;

             |SI aCaracRaro = "á";  aCaracRaro = " ";  |FINSI;
             |SI aCaracRaro = "é";  aCaracRaro = "‚";  |FINSI;

             |SI nAscii = 223;      aCaracRaro = " ";  |FINSI;
             |SI nAscii = 218;      aCaracRaro = "‚";  |FINSI;

             |SI aCaracRaro = "í";  aCaracRaro = "¡";  |FINSI;
             |SI aCaracRaro = "ó";  aCaracRaro = "¢";  |FINSI;
             |SI aCaracRaro = "ú";  aCaracRaro = "£";  |FINSI;
             |SI aCaracRaro = "º";  aCaracRaro = "§";  |FINSI;
             |SI aCaracRaro = "ª";  aCaracRaro = "¦";  |FINSI;
             |SI aCaracRaro = "ñ";  aCaracRaro = "¤";  |FINSI;
             |SI aCaracRaro = "Ñ";  aCaracRaro = "¥";  |FINSI;
             |SI aCaracRaro = "€";  aCaracRaro = "ç";  |FINSI;
             |SI aCaracRaro = "‡";  aCaracRaro = "Ç";  |FINSI;

             aCadena = aCadena + aCaracRaro;
       |SIGUE;

       aAlfa = aCadena;
|FPRC;

|PROCESO Tipo;
     nTipo = -1;
     aTipo = #nomfiest#9;
     aAlfa = aTipo % -101;
     |SI aAlfa = "D"; |Y nActividad = #nomfiest#6;     || de Actividad
          aAlfa = aTipo % 101;
          |SI aAlfa = "N";
               nTipo = 3;     || de Actividad
               |ERROR10;
          |FINSI;
          |SI aAlfa = "A"; |Y #nomfiest#0 = nAutonomia;
               nTipo = 3;     || de Actividad
               |ERROR10;
          |FINSI;
          |SI aAlfa = "L"; |Y #nomfiest#4 = nPoblacion;
               nTipo = 3;     || de Actividad
               |ERROR10;
          |FINSI;
     |SINO;
          aAlfa = aTipo % 101;
          |SI aAlfa = "N";
               nTipo = 0;     || Nacional
               |ERROR10;
          |FINSI;
          |SI aAlfa = "A"; |Y #nomfiest#0 = nAutonomia;
               nTipo = 0;     || Autonomica
               |ERROR10;
          |FINSI;
          |SI aAlfa = "L"; |Y #nomfiest#4 = nPoblacion;
               nTipo = 2;     || Local
               |ERROR10;
          |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE Tipo;
     #nomfiest#1;
     ;
     00, 00, 00000, 000, aFiesta;
     99, 99, 99999, 999, aFiesta;
     ;
     NULL, Tipo;
|FIN;

|PROCESO BuscaTipo;
     aFiesta = aDia + "." + aMes;
     |BUCLE Tipo;
|FPRC;

|PROCESO Actividad;
     #labempre#0 = #labcentr#0;
     |LEE 000#labempre.=;
     |SI FSalida = 0;
          nActividad = #labempre#12;
     |FINSI;
|FPRC;

|PROCESO Autonomia;
     aAutonomia = "";
     #dsam0101#0 = nCodProv;
     #dsam0101#1 = nCodPob;
     |LEE 000#dsam0101.=;
     |SI FSalida = 0;
          nAutonomia = #dsam0101#4;
          |ABRE #labauton;
          #labauton#0 = nAutonomia;
          |LEE 000#labauton.=;
          |SI FSalida = 0;
               aAutonomia = #labauton#1;
          |FINSI;
          |CIERRA #labauton;
     |FINSI;
|FPRC;

|PROCESO labmunic;
     nPoblacion = #labmunic#1;
|FPRC;

|DEFBUCLE labmunic;
     #labmunic#4;
     ;
     aCodPostal;
     aCodPostal;
     ;
     NULL, labmunic;
|FIN;

|PROCESO Convenio;
     aAlfa = #labcentr#19;
     aConvenio = "";
     |QBF aAlfa;
     |SI #labcentr#16 ! 1; |O #labcentr#19 ! "";
          aConvenio = #labcentr#19;
          #nomconca#0 = #labcentr#16;
          |LEE 000#nomconca.=;
     |SINO;
          #nomconca#0 = #labempre#1;
          |LEE 000#nomconca.=;
          |SI FSalida = 0;
               aConvenio = #nomconca#1;
          |FINSI;
     |FINSI;

     nDiasVac = #nomconca#144;
     |SI #nomconca#160 = "L";
          nDiasVac = #nomconca#161;
     |FINSI;
|FPRC;

|PROCESO labcalen;
     |DDEFECTO #labcalen;
     #labcalen#0 = #labcentr#0;
     #labcalen#49 = #labcentr#1;
     |LEE 000#labcalen.=;
|FPRC;

|PROCESO DatosCentro;
     aAlfa1 = #labcentr#7;
     |QBF aAlfa1; aAlfa1 = "00" + aAlfa1; aAlfa1 = aAlfa1 % -102;
     nCodProv = aAlfa1;
     aAlfa2 = #labcentr#8;
     |QBF aAlfa2; aAlfa2 = "000" + aAlfa2; aAlfa2 = aAlfa2 % -103;
     nCodPob = aAlfa2;
     aCodPostal = aAlfa1 + aAlfa2;
     |BUCLE labmunic;
     |HAZ Autonomia;
     |HAZ Actividad;
     |HAZ Convenio;
     |HAZ labcalen;
|FPRC;

|PROCESO Horario;
     |DDEFECTO #labcalen;
     #labcalen#0 = #labcentr#0;
     #labcalen#49 = #labcentr#1;
     |LEE 000#labcalen.=;

     |PARA nCampo = 21; |SI nCampo [ 27; |HACIENDO nCampo = nCampo + 1;
          #nomtmp02#nCampo# = "";
     |SIGUE;
     |PARA nCampo = 30; |SI nCampo [ 32; |HACIENDO nCampo = nCampo + 1;
          #nomtmp02#nCampo# = "";
     |SIGUE;
     |PARA nCampo = 34; |SI nCampo [ 47; |HACIENDO nCampo = nCampo + 1;
          #nomtmp02#nCampo# = "";
     |SIGUE;

     swCalen = 0;
     aAlfa = #labcalen#7;
     |QBF aAlfa;
     nNume = aAlfa;
     |SI nNume ! 0;
          #nomtmp02#21 = #labcalen#7;   || HORAS SEMANALES
          #nomtmp02#22 = #labcalen#1;   || MA¥ANA DE
          #nomtmp02#23 = #labcalen#2;   || MA¥ANA A
          #nomtmp02#24 = #labcalen#3;   || TARDE DE
          #nomtmp02#25 = #labcalen#4;   || TARDE A
          #nomtmp02#26 = #labcalen#5;   || SABADO DE
          #nomtmp02#27 = #labcalen#6;   || SABADO A
          swCalen = swCalen + 1;
     |FINSI;

     aAlfa = #labcalen#14;
     |QBF aAlfa;
     |SI aAlfa ! "";
          #nomtmp02#33 = #labcalen#14;      || HORAS SEMANALES
          aAlfa1 = #labcalen#8; |QBF aAlfa1; aAlfa2 = #labcalen#9; |QBF aAlfa2;
          |SI aAlfa1 ! "";
               aAlfa = "1er. Turno: de " + aAlfa1 + " a " + aAlfa2;
               #nomtmp02#30 = aAlfa;      || TURNO 1
          |FINSI;
          aAlfa1 = #labcalen#10; |QBF aAlfa1; aAlfa2 = #labcalen#11; |QBF aAlfa2;
          |SI aAlfa1 ! "";
               aAlfa = "2do. Turno: de " + aAlfa1 + " a " + aAlfa2;
               #nomtmp02#31 = aAlfa;      || TURNO 2
          |FINSI;
          aAlfa1 = #labcalen#12; |QBF aAlfa1; aAlfa2 = #labcalen#13; |QBF aAlfa2;
          |SI aAlfa1 ! "";
               aAlfa = "3er. Turno: de " + aAlfa1 + " a " + aAlfa2;
               #nomtmp02#32 = aAlfa;      || TURNO 3
          |FINSI;
          swCalen = swCalen + 1;
     |FINSI;

     aAlfa = #labcalen#43;
     |QBF aAlfa;
     |SI aAlfa ! "";
          #nomtmp02#48 = #labcalen#43;      || HORAS SEMANALES
          aAlfa = #labcalen#15; |QBF aAlfa;
          |SI aAlfa ! "";
               aAlfa = "De " + #labcalen#15 + " a " + #labcalen#16;
               #nomtmp02#34 = aAlfa;    || lunes MA¥ANA
               aAlfa = "De " + #labcalen#17 + " a " + #labcalen#18;
               #nomtmp02#35 = aAlfa;      || lunes TARDE
          |FINSI;
          aAlfa = #labcalen#19; |QBF aAlfa;
          |SI aAlfa ! "";
               aAlfa = "De " + #labcalen#19 + " a " + #labcalen#20;
               #nomtmp02#36 = aAlfa;      || martes MA¥ANA
               aAlfa = "De " + #labcalen#21 + " a " + #labcalen#22;
               #nomtmp02#37 = aAlfa;      || martes TARDE
          |FINSI;
          aAlfa = #labcalen#23; |QBF aAlfa;
          |SI aAlfa ! "";
               aAlfa = "De " + #labcalen#23 + " a " + #labcalen#24;
               #nomtmp02#38 = aAlfa;      || miercoles MA¥ANA
               aAlfa = "De " + #labcalen#25 + " a " + #labcalen#26;
               #nomtmp02#39 = aAlfa;      || miercoles TARDE
          |FINSI;
          aAlfa = #labcalen#27; |QBF aAlfa;
          |SI aAlfa ! "";
               aAlfa = "De " + #labcalen#27 + " a " + #labcalen#28;
               #nomtmp02#40 = aAlfa;      || jueves MA¥ANA
               aAlfa = "De " + #labcalen#29 + " a " + #labcalen#30;
               #nomtmp02#41 = aAlfa;      || jueves TARDE
          |FINSI;
          aAlfa = #labcalen#31; |QBF aAlfa;
          |SI aAlfa ! "";
               aAlfa = "De " + #labcalen#31 + " a " + #labcalen#32;
               #nomtmp02#42 = aAlfa;      || viernes MA¥ANA
               aAlfa = "De " + #labcalen#33 + " a " + #labcalen#34;
               #nomtmp02#43 = aAlfa;      || viernes TARDE
          |FINSI;
          aAlfa = #labcalen#35; |QBF aAlfa;
          |SI aAlfa ! "";
               aAlfa = "De " + #labcalen#35 + " a " + #labcalen#36;
               #nomtmp02#44 = aAlfa;      || sabado MA¥ANA
               aAlfa = "De " + #labcalen#37 + " a " + #labcalen#38;
               #nomtmp02#45 = aAlfa;      || sabado TARDE
          |FINSI;
          aAlfa = #labcalen#39; |QBF aAlfa;
          |SI aAlfa ! "";
               aAlfa = "De " + #labcalen#39 + " a " + #labcalen#40;
               #nomtmp02#46 = aAlfa;      || domingo MA¥ANA
               aAlfa = "De " + #labcalen#41 + " a " + #labcalen#42;
               #nomtmp02#47 = aAlfa;      || domingo TARDE
          |FINSI;
          swCalen = swCalen + 1;
     |FINSI;

     |SI swCalen = 0;
          #nomtmp02#21 = -1;   || HORAS SEMANALES
     |FINSI;
|FPRC;

|PROCESO PreCarga;
     |PARA nCampo = 99; |SI nCampo [ 157; |HACIENDO nCampo = nCampo + 2;
          aAlfa = #nomfical#nCampo#;
          |QBF aAlfa;
          |SI aAlfa = "";
               |SAL;
          |FINSI;
     |SIGUE;
     |PARA nCampo1 = 9; |SI nCampo1 [ 96; |HACIENDO nCampo1 = nCampo1 + 3;
          nCampo2 = nCampo1 + 1;
          nCampo3 = nCampo1 + 2;
          |SI #nomfical#nCampo1# ! "    "; |Y #nomfical#nCampo3# = 1;
               nCampo4 = nCampo + 1;
               #nomfical#nCampo# = #nomfical#nCampo1#;
               #nomfical#nCampo4# = 1;
               nCampo = nCampo + 2;
          |FINSI;
     |SIGUE;
|FPRC;

|PROCESO CuentaDias;
     #nomfical#0 = #labcentr#15;
     #nomfical#1 = #nomcalim#4;
     |LEE 000#nomfical.=;
     aAlfa = "01.01." + ("0000" + #nomcalim#4) % -104;
     fFecha = aAlfa;
     nFecha = fFecha;

     nDiasAny = 0;
     nSabados = 0;
     nDomingos = 0;
     swSabadosLab = 0;
     swDomingosLab = 0;
     |SI #nomfical#7 = 1;
          swSabadosLab = 1;
     |FINSI;
     |SI #nomfical#8 = 1;
          swDomingosLab = 1;
     |FINSI;
     |PARA nFecha = nFecha; |SI; |HACIENDO nFecha = nFecha + 1;
          fFecha = nFecha;
          aFecha = fFecha;
          aAny = aFecha % -104;
          nAny = aAny;
          |SI nAny ! #nomcalim#4;
               |SAL;
          |FINSI;

          nDiasAny = nDiasAny + 1;
          fFecha = nFecha;
          fFechaIni = fFecha % 1; aAlfaART = fFechaIni % 1199; aAlfaART = aAlfaART < aAlfaART; |QBF aAlfaART;

          |SI aAlfaART = "sabado";  nSabados = nSabados + 1; |FINSI;
          |SI aAlfaART = "domingo"; nDomingos = nDomingos + 1; |FINSI;
     |SIGUE;
|FPRC;

|PROCESO nomconfi;
     aAlfaART = fFecha; aAlfaART = (aAlfaART % 0102) + "." + (aAlfaART % 0402);
     |SI #nomconfi#2 = aAlfaART;
          #nomtmpf1#4 = "S";
          #nomtmpf1#10 = "S";
          swHayFestConv = 1;
     |FINSI;
|FPRC;

|DEFBUCLE nomconfi;
     #nomconfi#1;
     ;
     #labcentr#16, #0#4, "     ";
     #labcentr#16, #0#4, "zzzzz";
     ;
     NULL, nomconfi;
|FIN;

|PROCESO FestivosConvenio;
     |BUCLE nomconfi;
|FPRC;

|PROCESO BuscaFiesta;
     |SI #nomfiest#0 = 0;
          |SI #nomfiest#2 = 0; |Y #nomfiest#4 = 0;
               #nomtmpf1#6 = "S";
               swEncontrada = 1;
          |SINO;
               |SI #nomfiest#4 = nPoblacion;
                    #nomtmpf1#7 = "S";
                    swEncontrada = 1;
               |FINSI;
          |FINSI;
     |SINO;
          |SI #nomfiest#0 = nAutonomia;
               #nomtmpf1#6 = "S";
               #nomtmpf1#9 = "S";
               swEncontrada = 1;
          |FINSI;
     |FINSI;
     |SI #nomtmpf1#6 = "S"; |Y #nomtmpf1#7 = "S";
          |ERROR10;
     |FINSI;
|FPRC;

|DEFBUCLE BuscaFiesta;
     #nomfiest#1;
     ;
     00, 00, 00000, 000, aAlfaART;
     19, 52, 99999, 999, aAlfaART;
     ;
     NULL, BuscaFiesta;
|FIN;

|PROCESO CreaTmpFechas;
   #nomfical#0 = #labcentr#15;
   #nomfical#1 = #nomcalim#4;
   |LEE 000#nomfical.=;
   |SI FSalida ! 0; |DDEFECTO #nomfical; |FINSI;

   |ABRE #nomfiest;

   nLocal = 0;
   nAutonomica = 0;
   nNacional = 0;
   swHayFestConv = 0;

   |HAZ CuentaDias;

   |PARA nMes = 1; |SI nMes [ 12; |HACIENDO nMes = nMes + 1;
      aAlfaART = "01." + (("00" + nMes) % -102) + "." + ("0000" + #nomcalim#4) % -104;
      fFecha = aAlfaART;
      fFechaIni = fFecha % 1; aAlfaART = fFechaIni % 1199; aAlfaART = aAlfaART < aAlfaART; |QBF aAlfaART;
      |SI aAlfaART = "lunes";     nCalc = 0; |FINSI;
      |SI aAlfaART = "martes";    nCalc = 1; |FINSI;
      |SI aAlfaART = "miercoles"; nCalc = 2; |FINSI;
      |SI aAlfaART = "jueves";    nCalc = 3; |FINSI;
      |SI aAlfaART = "viernes";   nCalc = 4; |FINSI;
      |SI aAlfaART = "sabado";    nCalc = 5; |FINSI;
      |SI aAlfaART = "domingo";   nCalc = 6; |FINSI;

      nCalc1 = fFecha; nCalc1 = nCalc1 - nCalc; fFechaIni = nCalc1;

      nCalc1 = nMes + 1;
      aAlfaART = "01." + (("00" + nCalc1) % -102) + ".";
      |SI nMes < 12;
         aAlfaART = aAlfaART + ("0000" + #nomcalim#4) % -104;
      |SINO;
         aAlfaART = "01.01.";
         aAlfaART = aAlfaART + ("0000" + (#nomcalim#4 + 1)) % -104;
      |FINSI;
      fFecha = aAlfaART; nCalc1 = fFecha; nCalc1 = nCalc1 - 1; fFecha = nCalc1;
      fFechaFin = fFecha % 1; aAlfaART = fFechaFin % 1199; aAlfaART = aAlfaART < aAlfaART; |QBF aAlfaART;

      |SI aAlfaART = "lunes";     nCalc = 6; |FINSI;
      |SI aAlfaART = "martes";    nCalc = 5; |FINSI;
      |SI aAlfaART = "miercoles"; nCalc = 4; |FINSI;
      |SI aAlfaART = "jueves";    nCalc = 3; |FINSI;
      |SI aAlfaART = "viernes";   nCalc = 2; |FINSI;
      |SI aAlfaART = "sabado";    nCalc = 1; |FINSI;
      |SI aAlfaART = "domingo";   nCalc = 7; |FINSI;

      nCalc1 = fFecha; nCalc1 = nCalc1 + nCalc; fFechaFin = nCalc1;

      nCalc = fFechaIni; nCalc1 = fFechaFin;
      nCalc2 = nCalc1 - nCalc;
      |SI nCalc2 < 41; nCalc1 = nCalc + 41; fFechaFin = nCalc1; |FINSI;
      |PARA; |SI nCalc [ nCalc1; |HACIENDO nCalc = nCalc + 1;
         |DDEFECTO #nomtmpf1;
         #nomtmpf1#0 = #labcentr#0;
         #nomtmpf1#1 = #labcentr#1;
         #nomtmpf1#2 = nMes;
         #nomtmpf1#8 = #nomcalim#4;
         fFecha = nCalc;
         #nomtmpf1#3 = fFecha;
         aAlfaART = fFecha; aAlfaART = aAlfaART % 0402; nCalc2 = aAlfaART;
         |SI nCalc2 = nMes;
            #nomtmpf1#4 = "S";
         |SINO;
            #nomtmpf1#4 = "N";
         |FINSI;
         fFecha = fFecha % 1; aAlfaART = fFecha % 1199; aAlfaART = aAlfaART < aAlfaART; |QBF aAlfaART;
         |SI aAlfaART = "lunes";     nCalc2 = 1; |FINSI;
         |SI aAlfaART = "martes";    nCalc2 = 2; |FINSI;
         |SI aAlfaART = "miercoles"; nCalc2 = 3; |FINSI;
         |SI aAlfaART = "jueves";    nCalc2 = 4; |FINSI;
         |SI aAlfaART = "viernes";   nCalc2 = 5; |FINSI;
         |SI aAlfaART = "sabado";    nCalc2 = 6; |FINSI;
         |SI aAlfaART = "domingo";   nCalc2 = 7; |FINSI;
         nCalc2 = nCalc2 + 1;
         |SI #nomfical#nCalc2# = 1;
            |SI #nomfical#nCalc2# = "1";
               #nomtmpf1#5 = "S";
            |SINO;
               #nomtmpf1#5 = "N";
            |FINSI;
         |FINSI;
         || migue
         |SI #0#6 = "V";
               |SI nCalc2 = 7;
                      #nomtmpf1#5 = "N";
               |FINSI;
         |FINSI;
         |SI #nomtmpf1#4 = "S";   || Para que solo marque los que se imprimen
            fFecha = nCalc;
            aAlfaART = fFecha; aAlfaART = (aAlfaART % 0102) + (aAlfaART % 0402);
            |PARA nCalc2 = 9; |SI nCalc2 [ 97; |HACIENDO nCalc2 = nCalc2 + 3;
               nCalc3 = nCalc2 + 1;
               |SI #nomfical#nCalc2# [ aAlfaART; |Y aAlfaART [ #nomfical#nCalc3#;
                  nCalc3 = nCalc3 + 1;
                  |SI #nomfical#nCalc3# = 1;
                     aAlfaART = fFecha; aAlfaART = (aAlfaART % 0102) + "." + (aAlfaART % 0402);
                     #nomfiest#8 = aAlfaART; aAlfaART1 = #nomfiest#8;

                     swEncontrada = 0;
                     |BUCLE BuscaFiesta;
                     |SI swEncontrada = 0;
                        #nomtmpf1#6 = "s";
                        #nomtmpf1#7 = "N";
                     |FINSI;
                  |FINSI;
               |FINSI;
            |SIGUE;

            aAlfaART = fFecha; aAlfaART = (aAlfaART % 0102) + (aAlfaART % 0402);
            |PARA nCalc2 = 99; |SI nCalc2 [ 157; |HACIENDO nCalc2 = nCalc2 + 2;
               |SI #nomfical#nCalc2# = aAlfaART;
                  nCalc3 = nCalc2 + 1;
                  |SI #nomfical#nCalc3# = 1;
                     aAlfaART = fFecha; aAlfaART = (aAlfaART % 0102) + "." + (aAlfaART % 0402);
                     #nomfiest#8 = aAlfaART; aAlfaART1 = #nomfiest#8;

                     swEncontrada = 0;
                     |BUCLE BuscaFiesta;
                     |SI swEncontrada = 0;
                        #nomtmpf1#6 = "s";
                        #nomtmpf1#7 = "N";
                     |FINSI;

                  |FINSI;
               |FINSI;
            |SIGUE;

            |HAZ FestivosConvenio;

         |FINSI;
         |GRABA 020#nomtmpf1.c;

         |DDEFECTO #nomtmpf2;
         |SI #nomtmpf1#6 = "S";
            #nomtmpf2#0 = #nomtmpf1#0;
            #nomtmpf2#1 = #nomtmpf1#1;
            |LEE 000#nomtmpf2.=;
            |SI FSalida ! 0;
               #nomtmpf2#0 = #nomtmpf1#0;
               #nomtmpf2#1 = #nomtmpf1#1;
               |GRABA 020#nomtmpf2.c;
            |FINSI;

            aAlfaART1 = #nomtmpf1#3; |QBF aAlfaART1;
            aAlfaART2 = aAlfaART1 % 0402; aAlfaART1 = aAlfaART1 % 0102;
            |SI aAlfaART2 = "01"; aAlfaART1 = aAlfaART1 + " de Enero"; |FINSI;
            |SI aAlfaART2 = "02"; aAlfaART1 = aAlfaART1 + " de Febrero"; |FINSI;
            |SI aAlfaART2 = "03"; aAlfaART1 = aAlfaART1 + " de Marzo"; |FINSI;
            |SI aAlfaART2 = "04"; aAlfaART1 = aAlfaART1 + " de Abril"; |FINSI;
            |SI aAlfaART2 = "05"; aAlfaART1 = aAlfaART1 + " de Mayo"; |FINSI;
            |SI aAlfaART2 = "06"; aAlfaART1 = aAlfaART1 + " de Junio"; |FINSI;
            |SI aAlfaART2 = "07"; aAlfaART1 = aAlfaART1 + " de Julio"; |FINSI;
            |SI aAlfaART2 = "08"; aAlfaART1 = aAlfaART1 + " de Agosto"; |FINSI;
            |SI aAlfaART2 = "09"; aAlfaART1 = aAlfaART1 + " de Septiembre"; |FINSI;
            |SI aAlfaART2 = "10"; aAlfaART1 = aAlfaART1 + " de Octubre"; |FINSI;
            |SI aAlfaART2 = "11"; aAlfaART1 = aAlfaART1 + " de Noviembre"; |FINSI;
            |SI aAlfaART2 = "12"; aAlfaART1 = aAlfaART1 + " de Diciembre"; |FINSI;
            |SI #0#5 = 1; |Y #0#6 = "H";
                 |PARA nCont = 2; |SI nCont [ 16; |HACIENDO nCont = nCont + 1;
                    aAlfaART = #nomtmpf2#nCont#; |QBF aAlfaART;
                    |SI aAlfaART = "";
                       #nomtmpf2#nCont# = aAlfaART1;
                       |SAL;
                    |FINSI;
                 |SIGUE;
                 |GRABA 020#nomtmpf2.a;
            |FINSI;
            |SI #0#5 = 1; |Y #0#6 = "V";
                 |SI #nomtmpf1#9 = "N";
                      nNacional = nNacional + 1;
                      swAsignado = 0;
                      |PARA nCont = 2; |SI nCont [ 11; |HACIENDO nCont = nCont + 1;
                          aAlfaART = #nomtmpf2#nCont#; |QBF aAlfaART;
                          |SI aAlfaART = "";
                             #nomtmpf2#nCont# = aAlfaART1;
                             swAsignado = 1;
                             |SAL;
                          |FINSI;
                      |SIGUE;
                      |SI swAsignado = 0;
                           |PARA nCont = 20; |SI nCont [ 21; |HACIENDO nCont = nCont + 1;
                              aAlfaART = #nomtmpf2#nCont#; |QBF aAlfaART;
                              |SI aAlfaART = "";
                                 #nomtmpf2#nCont# = aAlfaART1;
                                 |SAL;
                              |FINSI;
                           |SIGUE;
                      |FINSI;
                      |GRABA 020#nomtmpf2.a;
                 |SINO;
                      nAutonomica = nAutonomica + 1;
                      |PARA nCont = 12; |SI nCont [ 15; |HACIENDO nCont = nCont + 1;
                         aAlfaART = #nomtmpf2#nCont#; |QBF aAlfaART;
                         |SI aAlfaART = "";
                            #nomtmpf2#nCont# = aAlfaART1;
                            |SAL;
                         |FINSI;
                      |SIGUE;
                      |GRABA 020#nomtmpf2.a;
                 |FINSI;
            |FINSI;
         |FINSI;
         |SI #nomtmpf1#7 = "S";
            nLocal = nLocal + 1;
            #nomtmpf2#0 = #nomtmpf1#0;
            #nomtmpf2#1 = #nomtmpf1#1;
            |LEE 000#nomtmpf2.=;
            |SI FSalida ! 0;
               |DDEFECTO #nomtmpf2;
               #nomtmpf2#0 = #nomtmpf1#0;
               #nomtmpf2#1 = #nomtmpf1#1;
               |GRABA 020#nomtmpf2.c;
            |FINSI;

            aAlfaART1 = #nomtmpf1#3; |QBF aAlfaART1;
            aAlfaART2 = aAlfaART1 % 0402; aAlfaART1 = aAlfaART1 % 0102;
            |SI aAlfaART2 = "01"; aAlfaART1 = aAlfaART1 + " de Enero"; |FINSI;
            |SI aAlfaART2 = "02"; aAlfaART1 = aAlfaART1 + " de Febrero"; |FINSI;
            |SI aAlfaART2 = "03"; aAlfaART1 = aAlfaART1 + " de Marzo"; |FINSI;
            |SI aAlfaART2 = "04"; aAlfaART1 = aAlfaART1 + " de Abril"; |FINSI;
            |SI aAlfaART2 = "05"; aAlfaART1 = aAlfaART1 + " de Mayo"; |FINSI;
            |SI aAlfaART2 = "06"; aAlfaART1 = aAlfaART1 + " de Junio"; |FINSI;
            |SI aAlfaART2 = "07"; aAlfaART1 = aAlfaART1 + " de Julio"; |FINSI;
            |SI aAlfaART2 = "08"; aAlfaART1 = aAlfaART1 + " de Agosto"; |FINSI;
            |SI aAlfaART2 = "09"; aAlfaART1 = aAlfaART1 + " de Septiembre"; |FINSI;
            |SI aAlfaART2 = "10"; aAlfaART1 = aAlfaART1 + " de Octubre"; |FINSI;
            |SI aAlfaART2 = "11"; aAlfaART1 = aAlfaART1 + " de Noviembre"; |FINSI;
            |SI aAlfaART2 = "12"; aAlfaART1 = aAlfaART1 + " de Diciembre"; |FINSI;
            |PARA nCont = 17; |SI nCont [ 21; |HACIENDO nCont = nCont + 1;
               aAlfaART = #nomtmpf2#nCont#; |QBF aAlfaART;
               |SI aAlfaART = "";
                  #nomtmpf2#nCont# = aAlfaART1;
                  |SAL;
               |FINSI;
            |SIGUE;
            |GRABA 020#nomtmpf2.a;
         |FINSI;
         |SI #nomtmpf1#6 = "s";
            #nomtmpf1#6 = "S";
            |GRABA 020#nomtmpf1.a;
         |FINSI;
         |SI swHayFestConv = 1;
               #nomtmpf2#0 = #nomtmpf1#0;
               #nomtmpf2#1 = #nomtmpf1#1;
               |LEE 000#nomtmpf2.=;
               |SI FSalida = 0;
                    #nomtmpf2#25 = "S";
                    |GRABA 020#nomtmpf2.a;
               |FINSI;
         |FINSI;
      |SIGUE;
   |SIGUE;

   |CIERRA #nomfiest;
|FPRC;

|PROCESO labcentr;
     #dsam0000#0 = #labcentr#0;
     |LEE 000#dsam0000.=;
     |SI FSalida = 0;
          |SI #dsam0000#69 = "N";
               |ACABA;
          |FINSI;
     |FINSI;

     |DDEFECTO #nomtmp02;
     |HAZ DatosCentro;

     nCalendario = #labcentr#15;
     |LEE 000#nomfilin.=;
     swComentario = 1;

     |DDEFECTO #nomfical;
     |ABRE #nomfical;
     #nomfical#0 = nCalendario;
     #nomfical#1 = #0#4;
     |LEE 000#nomfical.=;

     ||SI FSalida = 0;
          nPoblacion = #nomfical#159;
          |HAZ PreCarga;
          |PARA nCampo = 99; |SI nCampo [ 157; |HACIENDO nCampo = nCampo + 2;
               aDia = #nomfical#nCampo#;
               |QBF aDia;
               |SI aDia = ""; |Y nCalendario = 000; |Y nCampo = 99;
                    || parche para que si no tiene calendario asignado (el 000)
                    || salga un calendario en blanco, vamos que entre
                    aDia = "0";
                    #nomfical#100 = 1;
               |FINSI;
               |SI aDia ! "";
                    aMes = aDia % -102;
                    nMes = aMes;
                    aDia = aDia % 102;
                    nDia = aDia;
                    nCampo2 = nCampo + 1;
                    nPeso = #nomfical#nCampo2#;
                    |SI nPeso = 1;
                         #nomtmp02#0 = #labcentr#0;
                         #nomtmp02#1 = #labcentr#1;
                         #nomtmp02#2 = #0#4;
                         #nomtmp02#3 = nMes;
                         #nomtmp02#4 = nDia;
                         |HAZ BuscaTipo;
                         |SI nTipo ] 0;
                            #nomtmp02#5 = nTipo;
                            |LEE 000#nomtmp02.=;
                            |SI FSalida ! 0;
                               #nomtmp02#6 = #labempre#2;    || NOMBRE EMPRESA
                               #nomtmp02#7 = #labcentr#2;    || NOMBRE CENTRO
                               #nomtmp02#8 = #labcentr#3;    || DOMICILIO
                               #nomtmp02#9 = #labcentr#4;    || NUMERO
                               #nomtmp02#10 = #labcentr#5;   || PISO
                               #nomtmp02#11 = #labcentr#6;   || LOCALIDAD

                               aAlfa = #labcentr#7;
                               aAlfa = "00" + aAlfa; aAlfa = aAlfa % -102;
                               #nomtmp02#12 = aAlfa;         || CP 1

                               aAlfa = #labcentr#8;
                               aAlfa = "000" + aAlfa; aAlfa = aAlfa % -103;
                               #nomtmp02#13 = aAlfa;         || CP 2

                               #nomtmp02#14 = #labcentr#9;   || PROVINCIA
                               #nomtmp02#15 = #labcentr#10;  || NRO SEG SOCIAL
                               #nomtmp02#16 = #labcentr#13;  || CLAVE ENT ACC
                               #nomtmp02#17 = #labcentr#14;  || ENTIDAD ACCIDENTES

                               #nomtmp02#18 = aConvenio;  || CONVENIO
                               #nomtmp02#19 = #labcentr#11;  || PLANTILLA
                               #nomtmp02#20 = #nomconca#141;  || HORAS ANUALES

                               |HAZ Horario;

                               |HAZ BuscaTipo;
                               |SI nTipo ] 0;
                                    aAlfa = "";
                                    #nomfilin#0 = nCalendario;
                                    aAlfa2 = #0#4;
                                    #nomfilin#1 = aAlfa2;
                                    #nomfilin#2 = swComentario;
                                    |LEE 000#nomfilin.=;
                                    |SI FSalida = 0;
                                       aAlfa = #nomfilin#3;
                                       |QBF aAlfa;
                                       aCadena = aAlfa;
                                       |HAZ CrystalRaros;
                                       aAlfa = aCadena;
                                       #nomtmp02#28 = aAlfa;
                                    |SINO;
                                       #nomtmp02#28 = "";
                                    |FINSI;
                                    #nomtmp02#29 = swComentario;
                                    swComentario = swComentario + 1;

                                    #nomtmp02#5 = nTipo;
                                    |GRABA 020#nomtmp02.n;
                               |FINSI;
                            |FINSI;
                        |FINSI;
                    |FINSI;
               |FINSI;
          |SIGUE;
     ||FINSI;

     |SI #0#5 = "1";
        |HAZ CreaTmpFechas;
        aObserv1 = ""; aObserv2 = ""; aObserv3 = "";
        aAlfaART  = ""; aAlfaART1 = "";

        |DDEFECTO #labcalen;
        #labcalen#0 = #labcentr#0;
        #labcalen#49 = #labcentr#1;
        |LEE 000#labcalen.=;

        aObserv1 = #labcalen#45; |QBF aObserv1;
        aAlfa = aObserv1 % -101;
        |SI aAlfa = "-";
           aObserv1 = aObserv1 - "-";
        |SINO;
           aObserv1 = aObserv1 + " ";
        |FINSI;

        aObserv1 = aObserv1 + #labcalen#46; |QBF aObserv1;
        aAlfa = aObserv1 % -101;
        |SI aAlfa = "-";
           aObserv1 = aObserv1 - "-";
        |SINO;
           aObserv1 = aObserv1 + " ";
        |FINSI;

        aObserv1 = aObserv1 + #labcalen#47; |QBF aObserv1;
        aAlfa = aObserv1 % -101;
        |SI aAlfa = "-";
           aObserv1 = aObserv1 - "-";
        |SINO;
           aObserv1 = aObserv1 + " ";
        |FINSI;

        aObserv1 = aObserv1 + #labcalen#48; |QBF aObserv1;
        aAlfa = aObserv1 % -101;
        |SI aAlfa = "-";
           aObserv1 = aObserv1 - "-";
        |SINO;
           aObserv1 = aObserv1 + " ";
        |FINSI;

        |SI #0#6 ! "V";
             |ABRE #nomfilin;
             #nomfilin#0 = #labcentr#15;
             #nomfilin#1 = #nomcalim#4;
             #nomfilin#2 = 1;
             |LEE 000#nomfilin.];
             |PARA; |SI FSalida = 0; |Y #nomfilin#0 = #labcentr#15; |Y #nomfilin#1 = #nomcalim#4; |HACIENDO;
                aAlfaART1 = #nomfilin#3; |QBF aAlfaART1;
                aAlfaART1 = aAlfaART1 % 0; nCalc = aAlfaART1;
                aAlfaART1 = aAlfaART % 0; nCalc1 = aAlfaART1;
                nCalc2 = nCalc + nCalc1;
                |SI nCalc2 > 240;
                   |SI aObserv1 = "";
                      aObserv1 = aAlfaART;
                      aAlfaART = #nomfilin#3;
                      |QBF aAlfaART;
                      aAlfaART = aAlfaART + " ";
                   |SINO;
                      |SI aObserv2 = "";
                         aObserv2 = aAlfaART;
                         aAlfaART = #nomfilin#3;
                         |QBF aAlfaART;
                         aAlfaART = aAlfaART + " ";
                      |SINO;
                         |SI aObserv3 = "";
                            aObserv3 = aAlfaART; aAlfaART = ""; |SAL;
                         |FINSI;
                      |FINSI;
                   |FINSI;
                |SINO;
                   aAlfaART1 = #nomfilin#3; |QBF aAlfaART1;
                   aAlfaART = aAlfaART + " " + aAlfaART1;
                |FINSI;

                |LEE 000#nomfilin.s;
             |SIGUE;

             |QBF aAlfaART;
             |SI aAlfaART ! "";
                |SI aObserv1 = "";
                   aObserv1 = aAlfaART; aAlfaART = aAlfaART1;
                |SINO;
                   |SI aObserv2 = "";
                      aObserv2 = aAlfaART; aAlfaART = aAlfaART1;
                   |SINO;
                      |SI aObserv3 = "";
                         aObserv3 = aAlfaART; aAlfaART = "";
                      |FINSI;
                   |FINSI;
                |FINSI;
             |FINSI;
             |CIERRA #nomfilin;
        |FINSI;

        #nomtmpf2#0 = #labcentr#0;
        #nomtmpf2#1 = #labcentr#1;
        |LEE 000#nomtmpf2.=;
        |SI FSalida ! 0;
           |DDEFECTO #nomtmpf2;
           #nomtmpf2#0 = #labcentr#0;
           #nomtmpf2#1 = #labcentr#1;
           |GRABA 020#nomtmpf2.c;
        |FINSI;
        #nomtmpf2#22 = aObserv1;
        #nomtmpf2#23 = aObserv2;
        #nomtmpf2#24 = aObserv3;

        |GRABA 020#nomtmpf2.a;
     |FINSI;
|FPRC;

|DEFBUCLE labcentr;
     #labcentr#1;
     ;
     #0#0, #0#2;
     #0#1, #0#3;
     ;
     NULL, labcentr;
|FIN;

|PROCESO Calendario;
     aAlfa = "tm" + Usuario; |NOME_DAT #nomtmpf1#aAlfa#;
     |DELINDEX #nomtmpf1; |ABRE #nomtmpf1;
     |BUCLE labcentr;
     |CIERRA #nomtmpf1;
|FPRC;

|PROCESO nomtmp02;
     |PRINT;
|FPRC;

|DEFBUCLE nomtmp02;
     #nomtmp02#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, nomtmp02;
|FIN;

|PROCESO CargaDefectos;
     #labempre#0 = #nomtmpf1#0;
     |LEE 000#labempre.=;

     #labcentr#0 = #nomtmpf1#0;
     #labcentr#1 = #nomtmpf1#1;
     |LEE 000#labcentr.=;

     nCodProv = #labcentr#7;
     nCodPob = #labcentr#8;
     |HAZ Autonomia;

     |DDEFECTO #labcalen;
     #labcalen#0 = #nomtmpf1#0;
     #labcalen#49 = #nomtmpf1#1;
     |LEE 000#labcalen.=;

     |DDEFECTO #nomconca;
     aAlfa = #labcentr#19;
     |QBF aAlfa;
     |SI aAlfa = "";
          nNume = #labempre#1;
     |SINO;
          nNume = #labcentr#16
     |FINSI;
     #nomconca#0 = nNume;
     |LEE 000#nomconca.=;

     aAlfa = #labcentr#14;
     |QBF aAlfa;
     |SI aAlfa = "Sin Entidad";
          #labcentr#14 = "";
     |FINSI;
|FPRC;

|PROCESO nomtmpf1;
   |SI nEmpresa ! #nomtmpf1#0; |O nCentro ! #nomtmpf1#1;
      #nomtmpf2#0 = #nomtmpf1#0;
      #nomtmpf2#1 = #nomtmpf1#1;
      |LEE 000#nomtmpf2.=;
      |SI FSalida ! 0; |DDEFECTO #nomtmpf2; |FINSI;

      |HAZ CargaDefectos;

      |PRINT;
      nEmpresa = #nomtmpf1#0; nCentro  = #nomtmpf1#1;
   |FINSI;
|FPRC;

|DEFBUCLE nomtmpf1;
#nomtmpf1#1;
;
INICIO;
FINAL;
;
NULL, nomtmpf1;
|FIN;

|PROCESO imprime;
     |LEE 000#nomtmp02.p;
     |SI FSalida = 0;
          Impresora = "CrystalReport";
          |IMPRE -1;
          |SI FSalida = 0;
             |SI #nomcalim#5 = "1";

                |ABRE #nomcontr;
                |LEE 000#nomcontr.p;
                |SI FSalida ! 0; |DDEFECTO #nomcontr; |FINSI;
                |CIERRA #nomcontr;

                |ABRE #labcontr;
                |LEE 000#labcontr.p;
                |SI FSalida ! 0; |DDEFECTO #labcontr; |FINSI;
                |CIERRA #labcontr;

                || Copio el logo ....
                aNomLogo = #nomcontr#57; |QBF aNomLogo;
                |SI aNomLogo = ""; aNomLogo = "blanco.bmp"; |FINSI;

                aContiene = "";
                |ESPECIFICA "RBASS", aContiene;
                aAlfaART1 = aContiene1; |QBF aAlfaART1;
                aAlfaART2 = aAlfaART1 % 0101;
                |PARA nCont = 101; |SI aAlfaART2 ! "/"; |Y aAlfaART2 ! "\"; |HACIENDO nCont = nCont + 100;
                   aAlfaART2 = aAlfaART1 % nCont;
                |SIGUE;
                nCont = ((nCont / 100) ! 0) + 99;
                aAlfaART1 = aAlfaART1 % nCont;
                aAlfaART1 = aAlfaART1 + "documentos/"; |RMKDIR aAlfaART;
                aAlfaART1 = aAlfaART1 + "logos/"; |RMKDIR aAlfaART;
                aAlfaART1 = aAlfaART1 + "logo.bmp";

                |DBASE aAlfaART; |QBF aAlfaART;
                aAlfaART = aAlfaART + "documentos/logos/" + aNomLogo;
                aIP = ""; |IP_REMOTO aIP;
                |SI aIP = "";
                   |COPIA_FICHERO aAlfaART, aAlfaART1;
                |SINO;
                   |ENVIA_FICHERO aAlfaART, aAlfaART1;
                |FINSI;

                || ... y copio el fichero de detalle 'extra'
                aContiene = "";
                |ESPECIFICA "RBASS", aContiene;
                aAlfaART1 = aContiene1; |QBF aAlfaART1;
                aAlfaART1 = aAlfaART1 + "crystal/detalle1.dbf";

                |DFICB aAlfaART; |QBF aAlfaART;
                aAlfaART = aAlfaART + "tm" + Usuario + ".dbf";
                aIP = ""; |IP_REMOTO aIP;
                |SI aIP = "";
                   |COPIA_FICHERO aAlfaART, aAlfaART1;
                |SINO;
                   |ENVIA_FICHERO aAlfaART, aAlfaART1;
                |FINSI;

                |SI #0#6 = "H";
                     |INFOR "nomcali1";
                |SINO;
                     || si es serna su cuadre, si no, el estandar vertical
                     aAlfa = "";
                     nNume = #labempre#0;
                     #labempre#0 = 14;
                     |LEE 000#labempre.=;
                     |SI FSalida = 0;
                           aAlfa = #labempre#10;
                           |QBF aAlfa;
                     |FINSI;
                     |SI aAlfa = "B03337086"; |O aAlfa = "B03337085";
                          |INFOR "nomcali3";
                     |SINO;
                          |INFOR "nomcali2";
                     |FINSI;
                     #labempre#0 = nNume;
                     |LEE 000#labempre.=;
                |FINSI;
                |SI FSalida = 0;
                   |BUCLE nomtmpf1;
                   |PIEINF; |FININF;
                |FINSI;
             |SINO;
                |INFOR "nomcalim";
                |SI FSalida = 0;
                   |BUCLE nomtmp02;
                   |PIEINF; |FININF;
                |FINSI;
             |FINSI;
             |FINIMP;
             |SI #nomcalim#5 = "1";
                aContiene = "";
                |ESPECIFICA "RBASS", aContiene;
                aAlfaART1 = aContiene1; |QBF aAlfaART1;
                aAlfaART1 = aAlfaART1 + "crystal/detalle1.dbf";
                |RFBORRA aAlfaART1;
             |FINSI;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO PintaFormato;
   |ATRI I;
   |SI #0#5 = "1"; |PINTA 1541, "Gr fico"; |FINSI;
   |SI #0#5 = "2"; |PINTA 1541, "Listado"; |FINSI;
   |ATRI N;
|FPRC;

|PROGRAMA;
     |CLS;

     |PINPA #0#0;

     |ABRE #0;
     |DDEFECTO #0;
     |LEE 000#0p;
     |SI FSalida = 0;
          |BORRA 020#0a;
     |FINSI;

     |PINDA #0#0;

     |ENDATOS #1#0;
     |SI FSalida = 0;
          aAlfa = Usuario;    |QBF aAlfa;    aAlfa = ("c" + aAlfa) % 108;
          |NOME_DAT #100 aAlfa;
          |DELINDEX #100;
          |ABRE #100;
          |ABRE #labcentr;
          |ABRE #labcontr;
          |ABRE #labempre;
          |ABRE #nomfical;
          |ABRE #nomconca;
          |ABRE #labcalen;
          |ABRE #dsam0101;      || Poblaciones (Codigos Postales)
          |ABRE #dsam0000;
          |ABRE #nomfilin;

          aAlfa = "t1" + Usuario; |NOME_DAT #nomtmpf2#aAlfa#;
          |DELINDEX #nomtmpf2;
          |ABRE #nomtmpf2;
          |HAZ Calendario;
          |HAZ imprime;
          |CIERRA #nomtmpf2;

          |CIERRA #nomfilin;
          |CIERRA #dsam0000;
          |CIERRA #dsam0101;      || Poblaciones (Codigos Postales)
          |CIERRA #nomconca;
          |CIERRA #nomfical;
          |CIERRA #labcontr;
          |CIERRA #labcentr;
          |CIERRA #labcalen;
          |CIERRA #labempre;
          |CIERRA #100;
     |FINSI;

     |GRABA 000#0n;
     |CIERRA #0;
|FPRO;
