|FICHEROS;
     cretai04 #0;

     cretae02;
     cretae03;
     cretat13;

     cretam27;        || respuesta por CCC
     cretam25;        || respuesta por NAF
     cretam26;        || respuesta por Tramo

     cretat12;
     cretat13;

     cretat22;

     labempre;
     labcentr;
     labtraba;

     cretam02;
|FIN;

|VARIABLES;
     &nEmp = 0;
     &aCCC = "";
     &aReferencia = "";
     &aEstado = "";
     &aCentros = "";
     aDesdeRef = "";
     aHastaRef = "";
     aAlfa = "";
     nNume = "";

     fech1 = @;
     fech2 = @;
     alfa1 = "";
     alfa2 = "";
     nume1 = 0;
     nume2 = 0;
     aAlfa1 = "";
     aAlfa2 = "";

     nAscii = 0;
     aCaracter = "";
     aCaracterSal = "";
     aCadenaSal = "";
     aCadena = "";
     aLongitud = "";
     nLongitud = 0;
     nPos = 0;
     nBusca = 0;
     nCampo = 0;
     swErrorTramo = 0;
     FEstado = 0;

     swAlgunError = 0;
     nDesdeAny = 0;
     nHastaAny = 0;
     nDesdeMes = 0;
     nHastaMes = 0;
     nMesBuscaErr = 0;
     nAnyBuscaErr = 0;

     swErrorCCC = 0;
     swErroresTrab = 0;
     nErroresTrab = 0;

     &enAny = 0;
     &enMes = 0;
     &eaCCC = "";
     &eaNafBusca = "";
     &enTraPorNaf = 0;
     &enEmpNaf = 0;
     &enSwAlta = 0;

     {1}aContiene = "";
     aIP = "";
     nCampo1 = 0;
     aDestino = "";
     aOrigen = "";
     aFichero = "";
     aFicTmp = "";
     aRutaLocal = "";
     swL90 = 0;
     nTipo = 0;
     swSigue = 0;
|FIN;

|PROCESO AvisoTipos;
     |SI #0Campo = 01;
          aAlfa = "    ATENCION: con la selección 'Desde tipo 01'";
          aAlfa = aAlfa + " no se incluyen las liquidaciones L00";
          |MENAV aAlfa;
     |FINSI;
|FPRC;

|PROCESO AyudaTipos; |TIPO 7;
     |PUSHV 08622380;
     |ATRI R;
     ||            6         7
     ||            4567890123456789
     |PINTA 0864, "  - Opciones -  ";
     |PINTA 0964, " 00 :Normal     ";
     |PINTA 1064, " 02 :Finiquitos ";
     |PINTA 1164, "5-20:Atrasos    ";
     |PINTA 1264, " 90 :Complement.";
     |PINTA 1364, "                ";
     |PINTA 1464, "                ";
     |ATRI 0;
|FPRC;

|PROCESO Restaura;
     |POPV;
|FPRC;

|PROCESO Defectos;
     fech1 = ~;
     alfa1 = fech1;
     alfa1 = alfa1 % 402;          || MES
     nume1 = alfa1;

     alfa2 = fech1;
     alfa2 = alfa2 % -104;
     nume2 = alfa2;                || A¥O

     |SI nume1 = 1;
         #0#2 = nume2 - 1;
         #0#3 = 12;
     |SINO;
         #0#2 = nume2;
         #0#3 = nume1 - 1;
     |FINSI;

     |PINTA #0#2;
     |PINTA #0#3;
|FPRC;

|PROCESO sustituye2;
     nAscii = & aCaracter;
     aCaracterSal = aCaracter;
     |SI nAscii = 160; |O nAscii = 225;    || el caracter  
          aCaracterSal = "a";
     |FINSI;
     |SI nAscii = 130;             || el caracter ‚
          aCaracterSal = "e";
     |FINSI;
     |SI nAscii = 161; |O nAscii = 237; || el caracter ¡
          aCaracterSal = "i";
     |FINSI;
     |SI nAscii = 162; |O nAscii = 243;            || el caracter ¢
          aCaracterSal = "o";
     |FINSI;
     |SI nAscii = 163;             || el caracter £
          aCaracterSal = "u";
     |FINSI;
     /*
     |SI nAscii = 164;             || el caracter ¤
          aCaracterSal = "\361";
     |FINSI;
     |SI nAscii = 241;             || el caracter ¤ (otro codigo ¨?)
          aCaracterSal = "\361";
     |FINSI;
     |SI nAscii = 165;             || el caracter ¥
          aCaracterSal = "\321";
     |FINSI;
     |SI nAscii = 129;
          aCaracterSal = "\374";   || el caracter u con dieresis
     |FINSI;
     |SI nAscii = 154;
          aCaracterSal = "\334";   || el caracter U (mayuscula) con dieresis
     |FINSI;
     |SI nAscii = 167;
          aCaracterSal = "\272";   || el caracter §
     |FINSI;
     |SI nAscii = 166;
          aCaracterSal = "\252";   || el caracter ¦
     |FINSI;
     |SI aCaracter = "(";
          aCaracterSal = "\(";
     |FINSI;
     |SI aCaracter = ")";
          aCaracterSal = "\)";
     |FINSI;
     */
|FPRC;

|PROCESO sustituye;
     aCadenaSal = "";
     aLongitud = aCadena % 0;
     nLongitud = aLongitud;
     |PARA nPos = 1; |SI nPos [ nLongitud; |HACIENDO nPos = nPos + 1;
          nBusca = (nPos * 100) + 1;
          aCaracter = aCadena % nBusca;
          |HAZ sustituye2;
          aCadenaSal = aCadenaSal + aCaracterSal;
     |SIGUE;
|FPRC;

|PROCESO Tildes;
     |PARA nCampo = 15; |SI nCampo [ 23; |HACIENDO nCampo = nCampo + 2;
          aCadena = #cretam27#nCampo#;
          |QBF aCadena
          |SI aCadena ! "";
               |HAZ sustituye;
               #cretam27#nCampo# = aCadenaSal;
          |FINSI;
     |SIGUE;

     |PARA nCampo = 15; |SI nCampo [ 23; |HACIENDO nCampo = nCampo + 2;
          aCadena = #cretam25#nCampo#;
          |QBF aCadena
          |SI aCadena ! "";
               |HAZ sustituye;
               #cretam25#nCampo# = aCadenaSal;
          |FINSI;
     |SIGUE;

     |PARA nCampo = 11; |SI nCampo [ 19; |HACIENDO nCampo = nCampo + 2;
          aCadena = #cretam26#nCampo#;
          |QBF aCadena
          |SI aCadena ! "";
               |HAZ sustituye;
               #cretam26#nCampo# = aCadenaSal;
          |FINSI;
     |SIGUE;
|FPRC;

|PROCESO BuscaCodTrab;
     enEmpNaf = #cretam25#0;
     enAny = #cretam25#1;
     enMes = #cretam25#2;
     eaCCC = #cretam25#4;
     eaNafBusca = #cretam25#5;

     |SALVA_FICHA #labempre;
     |SALVA_FICHA #labcentr;

     enTraPorNaf = 0;
     |HAZ BuscaNaf_plb;

     |REPON_FICHA #labcentr;
     |REPON_FICHA #labempre;

     #labtraba#0 = #cretam25#0;
     #labtraba#1 = enTraPorNaf;
     |LEE 000#labtraba.=;
     |SI FSalida ! 0;
          || si no existe, el nombre en blanco
          #labtraba#2 = "";
     |SINO;
          || si existe pero no esta de alta en el periodo
          || el nombre sale, pero el codigo en blanco
          |SI enSwAlta = 0;
               enTraPorNaf = 0;
          |FINSI;
     |FINSI;

     aAlfa = enTraPorNaf;
     |QBF aAlfa;
     aAlfa = ("00000" + aAlfa) % -105;
     #cretae03#10 = aAlfa;
|FPRC;

|PROCESO RegistrosCCC;
     |PARA nCampo = 0; |SI nCampo [ 19; |HACIENDO nCampo = nCampo + 1;
          #cretat22#nCampo# = #cretae02#nCampo#;
          |SI nCampo = 13; |O nCampo = 16;
               aAlfa = #cretat22#nCampo#; |QBF aAlfa;
               aAlfa1 = aAlfa - "::";
               |SI aAlfa ! aAlfa1;
                    aAlfa = (aAlfa % 103) + (aAlfa % -101) + "0";
                    #cretat22#nCampo# = aAlfa;
               |FINSI;
          |FINSI;
     |SIGUE;
     |GRABA 020#cretat22.n;
     |LIBERA #cretat22;
|FPRC;

|DEFBUCLE RegistrosCCC;
     #cretae02#1;
     ;
     #0#0, #0#2, #0#3, #0#7, "               ", aDesdeRef;
     #0#1, #0#2, #0#3, #0#8, "zzzzzzzzzzzzzzz", aHastaRef;
     ;
     NULL, RegistrosCCC;
|FIN;

|PROCESO cretam26;
     swErrorTramo = 1;
     |HAZ Tildes;

     |SI swAlgunError = 0;
          |HAZ IniciaInforme;
     |FINSI;
     swAlgunError = 1;
     |PRINT;
|FPRC;

|DEFBUCLE cretam26;
     #cretam26#1;
     ;
     nEmp, nDesdeAny, nDesdeMes, #cretam25#3, #cretam25#4, #cretam25#5, 01, #cretam25#13, 1;
     nEmp, nHastaAny, nHastaMes, #cretam25#3, #cretam25#4, #cretam25#5, 99, #cretam25#13, 2;
     ;
     NULL, cretam26;
|FIN;

|PROCESO BuscaErrores;
     |SI #cretae03#3 = 0; |O #cretae03#3 = 90;
          nDesdeMes = #cretae03#2;
          nHastaMes = #cretae03#2;
          nDesdeAny = #cretae03#1;
          nHastaAny = #cretae03#1;
     |FINSI;
     |SI #cretae03#3 = 2;
          nDesdeMes = #cretae03#2;
          nHastaMes = #cretae03#2 + 1;
          nDesdeAny = #cretae03#1;
          nHastaAny = #cretae03#1;
     |FINSI;
     |SI #cretae03#3 ] 5; |Y #cretae03#3 [ 20;
          nDesdeMes = #cretae03#17;
          nHastaMes = #cretae03#18;
          nDesdeAny = #cretae03#19;
          nHastaAny = #cretae03#19;
     |FINSI;

     |PARA nMesBuscaErr = nDesdeMes;
     |SI nMesBuscaErr [ nHastaMes;
     |HACIENDO nMesBuscaErr = nMesBuscaErr + 1;
          nAnyBuscaErr = nDesdeAny;
          #cretam25#0 = #cretae03#0;
          #cretam25#1 = nAnyBuscaErr;
          #cretam25#2 = nMesBuscaErr;
          #cretam25#3 = #cretae03#3;
          #cretam25#4 = #cretae03#4;
          #cretam25#5 = #cretae03#5;
          #cretam25#13 = #cretae03#6;
          |LEE 000#cretam25.=;
          |SI FSalida = 0;
               || encontrado error, ahora busco cuales son y en que tramos

               |SI #cretae03#10 = 00000; |O #cretam25#14 = "A9701";
                    |HAZ BuscaCodTrab;
               |FINSI;

               swErrorTramo = 0;
               |BUCLE cretam26;
               |SI swErrorTramo = 0;
                    || trabajador del que no me dan errores en los tramos
                    || pero que me lo rechazan por otro tipo de errores que
                    || vienen por NAF, hay que imprimirlo claro
                    |DDEFECTO #cretam26;

                    |HAZ Tildes;

                    |SI swAlgunError = 0;
                         |HAZ IniciaInforme;
                    |FINSI;

                    |HAZ DefectoMesLiq;

                    swAlgunError = 1;
                    |PRINT;
               |FINSI;
          |FINSI;
     |SIGUE;
|FPRC;

|PROCESO DefectoMesLiq;
     || explico esto
     || la primera agrupacion del informe es CCC - Tipo Liq. - Mes Periodo
     || el Mes Periodo, en atrasos es el mes liquidativo (mes real atrasos)
     || que lo saco del #cretam26#8
     || este campo viene a cero en liquidaciones normales al hacer
     || este PRINT, asi que lo pongo por defecto como la fecha
     || del mes de la liquidacion, si no, duplica cabecera

     aAlfa1 = #cretam27#2; |QBF aAlfa1; aAlfa1 = ("00" + aAlfa1) % -102;
     aAlfa2 = #cretam27#1;
     aAlfa = "01" + "." + aAlfa1 + "." + aAlfa2;
     #cretam26#8 = aAlfa;
|FPRC;

|PROCESO impresion;
     |DDEFECTO #cretae03;               || leo el registro del cretae03
     #cretae03#0 = #cretat13#0;         || que me quiero llevar al informe
     #cretae03#1 = #cretat13#1;         || desde el temporal
     #cretae03#2 = #cretat13#2;
     #cretae03#3 = #cretat13#3;
     #cretae03#4 = #cretat13#4;
     #cretae03#5 = #cretat13#5;
     #cretae03#6 = #cretat13#6;
     |LEE 000#cretae03.=;

     |SI swErroresTrab = 1;
          |SI #cretae03#8 = "E";
          ||migue
          |Y #cretae03#6 = aReferencia; || solo si el error esta en los datos
                                        || del trabajador de la ultima ref.
          || probablemente la mejor solucion seria grabar solo los errores que me
          || vengan del ULTIMO fichero de bases en el cretat13, pero de momento
          || lo dejo que se controle aqui

               nErroresTrab = nErroresTrab + 1;
          |FINSI;
          |ACABA;
     |FINSI;

     |DDEFECTO #cretam25;
     |DDEFECTO #cretam26;

     #labempre#0 = #cretae03#0;
     |LEE 000#labempre.=;

     aAlfa = #cretae03#10;
     aAlfa = aAlfa % 105;
     nNume = aAlfa;
     #labtraba#0 = #labempre#0;
     #labtraba#1 = nNume;
     |LEE 000#labtraba.=;

     || Busqueda de mensajes por CCC

     swErrorCCC = 0;
     |DDEFECTO #cretam27;
     #cretam27#0 = #cretat12#0;
     #cretam27#1 = #cretat12#1;
     #cretam27#2 = #cretat12#2;
     #cretam27#3 = #cretat12#3;
     #cretam27#4 = #cretat12#4;
     #cretam27#13 = #cretat12#6;
     |LEE 000#cretam27.=;
     |SI FSalida = 0;
          |SI #cretat12#8 = "E";        || quiere decir que la liquidacion
               swErrorCCC = 1;          || esta rechazada
          |FINSI;
          || ES QUE HAY MENSAJES GRABADOS POR CCC

          || otra posibilidad, hay error en algun trabajador, pero es un error
          || que no impide que la liquidacion este aceptada
          |SI #cretam27#14 = "R9529"; |O #cretam27#16 = "R9529"; |O #cretam27#18 = "R9529";
          |O #cretam27#20 = "R9529"; |O #cretam27#22 = "R9529";
               |SI nErroresTrab ! 0;
                    aEstado = "CE";
               |FINSI;
          |FINSI;
     |FINSI;

     |SI #cretae03#8 = "E";
     || migue
     |Y #cretae03#6 = aReferencia; || solo si el error esta en los datos
                                   || del trabajador de la ultima ref.
     || probablemente la mejor solucion seria grabar solo los errores que me
     || vengan del ULTIMO fichero de bases en el cretat13, pero de momento
     || lo dejo que se controle aqui

          |HAZ BuscaErrores;
     |SINO;
          |SI #0#5 = "T"; |O swErrorCCC = 1;
               |SI swAlgunError = 0;
                    |HAZ IniciaInforme;
               |FINSI;
               |HAZ Tildes;
               |SI swErrorCCC = 1; |Y nErroresTrab = 0;
                    || tengo error en el CCC, y no tengo ningun trabajador
                    || con error, los marco todos como erroneos
                    #cretae03#8 = "NP";
               |FINSI;
               swAlgunError = 1;

               |HAZ DefectoMesLiq;

               |SI #cretae03#8 = "E";
               || migue
               || si es error solo si pertenece a la ultima referencia

               || probablemente la mejor solucion seria grabar solo los errores que me
               || vengan del ULTIMO fichero de bases en el cretat13, pero de momento
               || lo dejo que se controle aqui

               |SINO;
                    |PRINT;
               |FINSI;
          |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE cretat13;
     #cretat13#1;
     ;
     #cretat12#0, #cretat12#1, #cretat12#2, #cretat12#3, #cretat12#4, "            ";
     #cretat12#0, #cretat12#1, #cretat12#2, #cretat12#3, #cretat12#4, "zzzzzzzzzzzz";
     ;
     NULL, impresion;
|FIN;

|PROCESO impresionCCC;
     || leo otra vez por CCC, para ver los datos por si hay mas de un
     || tipo de liquidacion

     swErroresTrab = 1;
     nErroresTrab = 0;
     |BUCLE cretat13;

     swErroresTrab = 0;
     aEstado = #cretat12#8;
     |BUCLE cretat13;
|FPRC;

|DEFBUCLE cretat12_L90;       || por la clave 2, campos de la fecha de control
     #cretat12#2;
     ;
     nEmp, #0#3, #0#2, #0#7, aCCC;
     nEmp, #0#3, #0#2, #0#8, aCCC;
     ;
     NULL, impresionCCC;
|FIN;

/*
|DEFBUCLE cretat12_L90;       || por la clave 2, campos de la fecha de control
     #cretat12#2;
     1, 2;
     nEmp, #0#3, #0#2, #0#7, aCCC, #cretam02#1, #cretam02#2;
     nEmp, #0#3, #0#2, #0#8, aCCC, #cretam02#1, #cretam02#2;
     ;
     NULL, impresionCCC;
|FIN;
*/
|DEFBUCLE cretat12;
     #cretat12#1;
     ;
     nEmp, #0#2, #0#3, #0#7, aCCC;
     nEmp, #0#2, #0#3, #0#8, aCCC;
     ;
     NULL, impresionCCC;
|FIN;

|PROCESO GrabaTemporal;
     |SI #cretae02#3 ] 5; |Y #cretae02#3 [ 20;
          || lo que viene ahora lo hare solo en atrasos en principio,
          || se ha dado por el 1112.247
          || el problema es que p.ej. un caso de Atrasos del 01 al 06,
          || mes de la fecha de control 07. Si hay un CCC con un
          || trab. con errores en mes 03, en el informe sale el mes 03 y el
          || resto de trabajadores (los correctos) ¨en que mes tienen que salir
          || si son atrasos del 01 al 06? Pues salen en el mes de la fecha de
          || Control. No pasa nada, eso puede ser hasta correcto si digo que
          || salgan TODOS los trabajadores
          || Pero si solo quiero errores, en el Crystal, la cabecera del mes
          || 07 SIGUE SALIENDO, aunque se oculten los trabajadores por ser
          || Correctos y haber pedido SOLO ERRORES

          || lo que hago es que en atrasos, no me lleve al informe los trab.
          || que son "C" en una liquidacion que tambien es "C"
          || probablemente esto lo podria hacer en L00, pero no me fio por
          || si se fastidia algun caso de liquidaciones en que el error esta
          || por CCC y no por trabajador, que eso esta por probar y no se
          || muy bien como ira

          |SI #cretae02#8 = "E"; |Y #cretae03#8 = "C";
               || trabajador correcto dentro de un CCC con errores
               |SI #0#5 = "E";
                    || solo sacame los que tengan errores
                    |ACABA;
               |FINSI;
          |FINSI;
     |FINSI;

     || primero guardo el temporal de los CCC
     |DDEFECTO #cretat12;
     #cretat12#0 = #cretae02#0;
     #cretat12#1 = #cretae02#1;
     #cretat12#2 = #cretae02#2;
     #cretat12#3 = #cretae02#3;
     #cretat12#4 = #cretae02#4;
     |LEE 101#cretat12.=;
     FEstado = FSalida;

     |PARA nCampo = 5; |SI nCampo [ 16; |HACIENDO nCampo = nCampo + 1;
          #cretat12#nCampo# = #cretae02#nCampo#;
          |SI nCampo = 13; |O nCampo = 16;
               aAlfa = #cretat12#nCampo#; |QBF aAlfa;
               aAlfa1 = aAlfa - "::";
               |SI aAlfa ! aAlfa1;
                    aAlfa = (aAlfa % 103) + (aAlfa % -101) + "0";
                    #cretat12#nCampo# = aAlfa;
               |FINSI;
          |FINSI;
     |SIGUE;

     |SI #cretae02#3 = 90; |O #cretae02#3 = 91;
          #cretat12#17 = #cretam02#20;
          #cretat12#18 = #cretam02#21;
     |FINSI;

     |SI FEstado = 0;
          |GRABA 020#cretat12.a;
     |SINO;
          |GRABA 020#cretat12.n;
     |FINSI;

     |LIBERA #cretat12;

     || ahora de los trabajadores por NAF

     |DDEFECTO #cretat13;

     #cretat13#0 = #cretae03#0;
     #cretat13#1 = #cretae03#1;
     #cretat13#2 = #cretae03#2;
     #cretat13#3 = #cretae03#3;
     #cretat13#4 = #cretae03#4;
     #cretat13#5 = #cretae03#5;
     |LEE 101#cretat13.=;
     FEstado = FSalida;

     |PARA nCampo = 6; |SI nCampo [ 16; |HACIENDO nCampo = nCampo + 1;
          #cretat13#nCampo# = #cretae03#nCampo#;
     |SIGUE;

     |SI FEstado = 0;
          |GRABA 020#cretat13.a;
     |SINO;
          |GRABA 020#cretat13.n;
     |FINSI;

     |LIBERA #cretat13;
|FPRC;

|DEFBUCLE cretae03;
     #cretae03#1;
     ;
     #cretae02#0, #cretae02#1, #cretae02#2, #cretae02#3, #cretae02#4, "            ", #cretae02#6;
     #cretae02#0, #cretae02#1, #cretae02#2, #cretae02#3, #cretae02#4, "zzzzzzzzzzzz", #cretae02#6;
     ;
     NULL, GrabaTemporal;
|FIN;

|PROCESO BuscaReferencias;
     || las L90 no las busco en el primer bucle
     |SI swL90 = 0;
          |SI #cretae02#3 = 90; |O #cretae02#3 = 91;
               |ACABA;
          |FINSI;
     |FINSI;

     || a ver que trabajadores se incluian en esta referencia

     swSigue = 0;
     |SI aCCC ! #cretae02#4; |Y aCCC ! "";
          || cuando cambio de CCC imprimo, este bucle es para
          || imprimir del temporal
          swSigue = 1;
     |FINSI;
     |SI #cretae02#3 ! nTipo;
          || tambien si cambio de tipo claro, es otra liquidacion
          swSigue = 1;
     |FINSI;
     |SI swSigue = 1;
          |SI swL90 = 0;
               |BUCLE cretat12;
          |FINSI;
          |SI swL90 = 1;
               |BUCLE cretat12_L90;
          |FINSI;
          aReferencia = "";
          nTipo = 0;
     |FINSI;

     |SI #cretae02#8 ! " "; |Y #cretae02#8 ! "V";
          || solo los que tengan respuesta registrada
          || esto es para ir guardando en el temporal los trabajadores
          || que despues se van a imprimir

          || ademas guardare tambien los CCC que se van a imprimir
          || para asi quedarme con los mensjaes solo de la ultima referencia
          || por cada tipo

          |BUCLE cretae03;
          || aEstado = #cretae02#8;
     |FINSI;

      nEmp = #cretae02#0;
      aCCC = #cretae02#4;
      aCentros = #cretae02#9;
      aReferencia = #cretae02#6;
      nTipo = #cretae02#3;
|FPRC;


|DEFBUCLE BuscaRef_L90;
     #cretae02#1;
     7, 8;
     #cretam02#0, #cretam02#1, #cretam02#2, #cretam02#3, "               ", aDesdeRef, "B", "C";
     #cretam02#0, #cretam02#1, #cretam02#2, #cretam02#3, "zzzzzzzzzzzzzzz", aHastaRef, "B", "E";
     ;
     NULL, BuscaReferencias;
|FIN;

|PROCESO cretam02;
     || aReferencia = "";
     nTipo = 0;
     |BUCLE BuscaRef_L90;
|FPRC;

|DEFBUCLE cretam02;
     #cretam02#3;
     ;
     #0#0, #0#2, #0#3, 90, "               ";
     #0#1, #0#2, #0#3, 91, "zzzzzzzzzzzzzzz";
     ;
     NULL, cretam02;
|FIN;

|DEFBUCLE BuscaReferencias;
     #cretae02#1;
     7, 8;
     #0#0, #0#2, #0#3, #0#7, "               ", aDesdeRef, "B", "C";
     #0#1, #0#2, #0#3, #0#8, "zzzzzzzzzzzzzzz", aHastaRef, "B", "E";
     ;
     NULL, BuscaReferencias;
|FIN;

|PROCESO Proceso;
     aDesdeRef = "        ";
     aHastaRef = "zzzzzzzz";

     aReferencia = "";
     nTipo = 0;
     swL90 = 0;
     |BUCLE BuscaReferencias;

     || para que la ultima salga siempre:
     |BUCLE cretat12;

     || para las L90
     || busco primero las liquidaciones que tengan como fecha de control
     || la fecha de la liquidacion pedida
     swL90 = 1;
     |SI #0#7 [ 90; |Y 90 [ #0#8;
          |BUCLE cretam02;

          || para que la ultima salga siempre:
          |BUCLE cretat12_L90;
     |FINSI;

     swL90 = 0;
|FPRC;

|PROCESO IniciaInforme;
     Impresora = "CrystalReport";
     |IMPRE -1;
     |SI #0#4 = "R";
          |INFOR "cretai4b";
     |FINSI;
     |SI #0#4 = "D";
          |INFOR "cretai4c";
     |FINSI;
|FPRC;

|PROCESO FinInforme;
     |PIEINF;
     |FININF;
     |FINIMP;
|FPRC;

|PROCESO BorraDBF;
     aContiene = "";
     |ESPECIFICA "RBASS", aContiene;
     aDestino = aContiene1; |QBF aDestino;
     aRutaLocal = aDestino;
     aDestino = aDestino + "crystal/" + aFichero + ".dbf";
     |RFBORRA aDestino;
|FPRC;

|PROCESO CopiaDBF;
     aContiene = "";
     |ESPECIFICA "RBASS", aContiene;
     aDestino = aContiene1; |QBF aDestino;
     aRutaLocal = aDestino;
     aDestino = aDestino + "crystal/" + aFichero + ".dbf";

     |DFICE aOrigen; |QBF aOrigen;
     aOrigen = aOrigen + aFicTmp + ".dbf";
     aIP = ""; |IP_REMOTO aIP;

     |SI aIP = "";
          |COPIA_FICHERO aOrigen, aDestino;
     |SINO;
          |ENVIA_FICHERO aOrigen, aDestino;
     |FINSI;
|FPRC;

|PROGRAMA;
     |CLS;
     |PINPA #0#0;
     |ABRE #0;
     |DDEFECTO #0;
     |LEE 000#0p;
     |SI FSalida = 0;
          |BORRA 020#0a;
     |SINO;
          |HAZ Defectos;
     |FINSI;

     |PINDA #0#0;

     |ENDATOS #1#0;

     |SI FSalida = 0;
          |ABRE #cretae02;
          |ABRE #cretae03;

          |ABRE #cretam27;
          |ABRE #cretam25;
          |ABRE #cretam26;

          |ABRE #labempre;
          |ABRE #labcentr;
          |ABRE #labtraba;

          aAlfa = Usuario; |QBT aAlfa;    aAlfa = ("c4" + aAlfa) % 108;
          |NOME_DAT #cretat12#aAlfa#;
          |DELINDEX #cretat12;
          |ABRE #cretat12;

          aAlfa = Usuario; |QBT aAlfa;    aAlfa = ("c5" + aAlfa) % 108;
          |NOME_DAT #cretat13#aAlfa#;
          |DELINDEX #cretat13;
          |ABRE #cretat13;

          aAlfa = Usuario; |QBT aAlfa;
          aFicTmp = "tm2" + aAlfa; |QBF aFicTmp;
          |NOME_DAT #cretat22#aFicTmp#;
          |ABRE #cretat22; |CIERRA #cretat22; |DELINDEX #cretat22;
          |ABRE #cretat22;

          |HAZ Proceso;

          aFichero = "cretat22"; |HAZ BorraDBF;
          |BUCLE RegistrosCCC;

          |SI swAlgunError = 1;
               |HAZ FinInforme;
          |SINO;
               |SI #0#5 = "E";
                    aAlfa = "    No se han encontrado errores en la respuesta de la liquidación ";
                    aAlfa = aAlfa + "para la selección realizada.";
                    |MENAV aAlfa;
               |FINSI;
          |FINSI;

          |CIERRA #cretat22;

          |CIERRA #labtraba;
          |CIERRA #labcentr;
          |CIERRA #labempre;

          |CIERRA #cretam26;
          |CIERRA #cretam25;
          |CIERRA #cretam27;

          |CIERRA #cretae03;
          |CIERRA #cretae02;

          |CIERRA #cretat12;
          |CIERRA #cretat13;
          |DELINDEX #cretat12;
          |DELINDEX #cretat13;
     |FINSI;

     |GRABA 000#0n;
     |CIERRA #0;
|FPRO;
