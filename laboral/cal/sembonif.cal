|FICHEROS;
     sembonif;           || el mismo
     labtraba;           || fichero de trabajadores
     labempre;           || fichero de empresas
     nombonif;           || tipos de bonificaciones
     nombotra;
     nomboni3;
|FIN;

|VARIABLES;
     nNume1 = 0;
     nNume2 = 0;
     nSumale = 0;
     nPara1 = 0;
     nPara2 = 0;
     sw1 = 0;
     aAlfa1 = "";
     aAlfa2 = "";
     aAlfa3 = "";
     aApell1 = "";
     aApell2 = "";
     aNombre = "";
     &nPag = 0;
     &aDescripcion = "";
     &aDescripcion2 = "";
     aFecha_Alta = "";
     fFecha_Alta = @;
     fFecha_Baja = @;
     fFecha = @;
     fFecha_Ant = @;
     fFecha_60 = @;
     &aFecha = "";
     &aFecha1 = "";
     &aFecha2 = "";
     &aFecha3 = "";
     &aFecha4 = "";
     &aFecha_Baja = "";
     aMes = "";
     aAnyo = "";
     &aAcronimo = "";
     nSeleccion = 0;
     nPerbonant = 0;
     nBonif = 0;
|FIN;

|PROCESO sep_nombre;
    aAlfa1 = #labtraba#2;   |QBF aAlfa1;
    aAlfa2 = aAlfa1 % 0;   nPara2 = aAlfa2;
    sw1 = 0;
    aApell1 = "";
    aApell2 = "";
    aNombre = "";
|PARA nPara1 = 1; |SI nPara1 [ nPara2; |HACIENDO nPara1 = nPara1 + 1;
        nNume2 = (nPara1 * 100) + 1;
        aAlfa3 = aAlfa1 % nNume2;
    |SI aAlfa3 = " ";|O aAlfa3 = ",";
        |SI aApell1 ! "";
                sw1 = sw1 + 1;
        |FINSI;
        |SI sw1 ] 2;|Y aNombre ! "";
                aNombre = aNombre + aAlfa3;
        |FINSI;
    |SINO;
        |SI sw1 = 0;  aApell1 = aApell1 + aAlfa3; |FINSI;
        |SI sw1 = 1;  aApell2 = aApell2 + aAlfa3; |FINSI;
        |SI sw1 ] 2;  aNombre = aNombre + aAlfa3; |FINSI;
    |FINSI;
|SIGUE;
    aAcronimo = (aApell1 % 102) + (aApell2 % 102) + (aNombre % 101);
    aAcronimo = aAcronimo > aAcronimo;
|FPRC;

|PROCESO mes;
     aFecha_Alta = #labtraba#40;   || ahora cojo la fecha alta bonificacion
     nPerbonant = 1;          || para que la primera vez entre
     |PARA nNume1 = 14; |SI nNume1 < 20; |Y nPerbonant ! 0; |HACIENDO nNume1 = nNume1 + 1;
          |SI #nombonif#nNume1# = 0;
               |SI nPerbonant = 999;
                    nSumale = 0;        || bonificacion de por vida (1 solo tramo)
                    nSeleccion = 1;     || no pinta ningun mes
                    aFecha_Baja = "---";
                    |PRINT;             || y lo enga¤o para que pinte el resto
               |SINO;
                    fFecha_Alta = aFecha_Alta;
                    aFecha_Alta = aFecha_Alta % 102;
                    nSumale = (nPerbonant) / 1000;
                    |SI aFecha_Alta = "01";
                         nSumale = nPerbonant / 1000;
                    |FINSI;
                    fFecha_Baja = fFecha_Alta + nSumale;
                    aFecha_Baja = fFecha_Baja;
               |FINSI;
          |FINSI;
          nPerbonant = #nombonif#nNume1#;    || periodo bonificacion anterior
     |SIGUE;

     |SI aFecha_Alta ! "01";                 || si no empieza el dia 1 tiene
          fFecha_Baja = fFecha_Baja + 0.001; || q coger parte del mes siguien-
     |FINSI;                                 || te

     |PARA nNume1 = 1; |SI fFecha_Alta < fFecha_Baja; |HACIENDO nNume1 = nNume1 + 1;
          |PARA nNume2 = 1; |SI fFecha_Alta < fFecha_Baja; |Y nNume2 [ 4; |HACIENDO nNume2 = nNume2 + 1;
               fFecha = fFecha_Alta % 0;
               aFecha = fFecha;

               aMes = aFecha % 1110;
               aAnyo = aFecha % 704;
               |QBF aMes;
               aFecha = aMes + " " + aAnyo;
               |SI nNume2 = 1; aFecha1 = aFecha; |FINSI;
               |SI nNume2 = 2; aFecha2 = aFecha; |FINSI;
               |SI nNume2 = 3; aFecha3 = aFecha; |FINSI;
               |SI nNume2 = 4; aFecha4 = aFecha; |FINSI;
               fFecha_Alta = fFecha_Alta + 0.001;
          |SIGUE;
          nSeleccion = 1;
          |PRINT;
          aFecha1 = ""; aFecha2 = ""; aFecha3 = ""; aFecha4 = "";
     |SIGUE;
|FPRC;

|PROCESO nombotra;
     nBonif = #nombotra#2;
     |ERROR10;
|FPRC;

|DEFBUCLE nombotra;
     #nombotra#1;
     ;
     #labtraba#0, #labtraba#1, 001;
     #labtraba#0, #labtraba#1, 999;
     ;
     NULL, nombotra;
|FIN;

|PROCESO bonifi;
     nSeleccion = 0;
     aDescripcion = "";
     aDescripcion2 = "";
     |INFOR "sembonif";

     #labempre#0 = #labtraba#0;
     |LEE 000#labempre.=;

     fFecha_Alta = #labtraba#36;
     |SI fFecha_Alta ] #sembonif#6; |Y fFecha_Alta [ #sembonif#7;
          #nombonif#0 = #labtraba#39;
          |LEE 000#nombonif.=;
          |SI #nombonif#14 ! 0;
               |SI #nombonif#4 ! 0;
                    aDescripcion = #nombonif#3 + "% sobre C.C. (" + #nombonif#4 + "%). ";
               |FINSI;
               |SI #nombonif#5 ! 0;
                    aDescripcion = #nombonif#3 + "% sobre C.P. (" + #nombonif#5 + "%). ";
               |FINSI;
               aDescripcion = aDescripcion + "Durante " + #nombonif#14 + " meses."
          |FINSI;
          |SI #nombonif#15 ! 0;
               |SI #nombonif#4 ! 0;
                    aDescripcion2 = #nombonif#20 + "% sobre C.C. (" + #nombonif#4 + "%). ";
               |FINSI;
               |SI #nombonif#5 ! 0;
                    aDescripcion2 = #nombonif#20 + "% sobre C.P. (" + #nombonif#5 + "%). ";
               |FINSI;
               aDescripcion2 = aDescripcion2 + "Durante " + #nombonif#15 + " meses."
          |FINSI;
          nPag = nPag + 1;
          |HAZ sep_nombre;
          |HAZ mes;
     |FINSI;

     |SI nSeleccion ! 0;
          |PIEINF;
     |FINSI;
     |FININF;
|FPRC;

|DEFBUCLE bonifi;
     #labtraba#1;
     39, 44;
     #sembonif#0, #sembonif#2, #sembonif#4, "A";
     #sembonif#1, #sembonif#3, #sembonif#5, "A";
     ;
     NULL, bonifi;
|FIN;

|PROCESO bonifi_60;
     aFecha_Alta = "";
     aFecha_Baja = "";
     aDescripcion = "";
     #nombonif#2 = "R.D.L. 16/2001";
     |INFOR "sembonif";

     #labempre#0 = #labtraba#0;
     |LEE 000#labempre.=;
     fFecha_60 = #labtraba#11;
     fFecha_60 = fFecha_60 + 0.000060;
     fFecha_Ant = #labtraba#34;
     fFecha_Ant = fFecha_Ant + 0.000005
     |SI fFecha_Ant > fFecha_60;             ||  Fecha de alta
          #labtraba#40 = fFecha_Ant;
     |SINO;
          #labtraba#40 = fFecha_60;
     |FINSI;
     aFecha_Baja = "---"

     aDescripcion = "Reducci¢n del " + #labtraba#211 + "% sobre C.C. (22,30%) segun RDL 16/2001 ";
     aDescripcion2 = "para mayores de 60 a¤os";

     nPag = nPag + 1;
     |HAZ sep_nombre;
     |PRINT;
     |PIEINF;
     |FININF;
|FPRC;

|DEFBUCLE bonifi_60;
     #labtraba#1;
     211, 44;
     #sembonif#0, #sembonif#2,   1, "A";
     #sembonif#1, #sembonif#3, 100, "A";
     ;
     NULL, bonifi_60;
|FIN;

|PROCESO bonifi_fom;
     nSeleccion = 0;
     aDescripcion = "";
     aDescripcion2 = "";
     |INFOR "sembonif";

     #labempre#0 = #labtraba#0;
     |LEE 000#labempre.=;

     nBonif = 0;
     |BUCLE nombotra;

     |SI nBonif ! 0;
          fFecha_Alta = #nombotra#4;

          |SI fFecha_Alta ] #sembonif#6; |Y fFecha_Alta [ #sembonif#7;
               #nombonif#0 = #nombotra#2;

               #nomboni3#0 = #nombotra#2;
               |LEE 000#nomboni3.=;
               |SI FSalida = 0;
                    #nombonif#2 = "R.D.L. 05/2006";

                    aDescripcion = #nombotra#7 + " euros mensuales.";
                    |SI #nomboni3#3 ! 999;
                         aDescripcion = aDescripcion + " Durante " + #nomboni3#3 + " meses."
                    |SINO;
                         aDescripcion = aDescripcion + " Hasta fin de contrato."
                    |FINSI;

                    aDescripcion2 = "Descripcion: " + #nombotra#3;

                    #labtraba#40 = #nombotra#4;
                    aFecha_Baja = #nombotra#5;
                    |SI aFecha_Baja = "31.12.9999";
                         aFecha_Baja = "---";
                    |FINSI;

                    nPag = nPag + 1;
                    |HAZ sep_nombre;
                    nSeleccion = 1;
               |FINSI;
          |FINSI;

          |SI nSeleccion ! 0;
               |PRINT;
               |PIEINF;
          |FINSI;
          |FININF;
     |FINSI;
|FPRC;

|DEFBUCLE bonifi_fom;
     #labtraba#1;
     44;
     #sembonif#0, #sembonif#2, "A";
     #sembonif#1, #sembonif#3, "A";
     ;
     NULL, bonifi_fom;
|FIN;

|PROCESO Tipo3; |TIPO 3;
     |IMPRE 0;
     |SI FSalida = 0;
          |ABRE #labempre;
          |ABRE #nombonif;
          |ABRE #nombotra;
          |ABRE #nomboni3;
          |BUCLE bonifi;
          |BUCLE bonifi_60;
          |SI #sembonif#16 = "S"
              |BUCLE bonifi_fom;
          |FINSI;
          |CIERRA #nomboni3;
          |CIERRA #nombotra;
          |CIERRA #nombonif;
          |CIERRA #labempre;
     |SINO;
          |MENAV "0000Proceso abortado.";
     |FINSI;
     |FINIMP;
|FPRC;
