|FICHEROS;
     cretai07;
     labempre;

     cretam36;
     cretam37;
|FIN;

|VARIABLES;
     nCampo              = 0;
     nCampo1             = 0;
     nCampo2             = 0;
     FEstado             = 0;
     nMes                = 0;
     nAny                = 0;
     nNume               = 0;
     nTramos             = 0;
     nImpresion          = 0;
     nMesDesde           = 0;
     nMesHasta           = 0;
     nAnyDesde           = 0;
     nAnyHasta           = 0;
     nPasada             = 0;
     nOk                 = 0;
     nTp                 = 0;
     nDTp                = 0;
     nHTp                = 0;
     nOpcion             = 0;

     aReferencia         = "";
     aFecha              = "";
     aAlfa               = "";
     aAntCCC             = "";
     aEmp                = "";
     aNomEmp             = "";
     aFic                = "";

     fFecha              = @;

 {-1}aMenu               = "";
     aMenu1              = "2400";
     aMenu2              = "1";
     aMenu3              = "Archivo documental activado. Elija opci¢n: ";
     aMenu4              = "IAB";
     aMenu5              = "Imprimir";
     aMenu6              = "Archivar";
     aMenu7              = "Ambos";

     &nError             = 0;
     &eaVengoDe          = "";
     &nArchi             = 0;
     &swErrorDSArchi     = 0;
     &enEmp              = 0;
     &enTipo             = 0;
     &eaTitulo           = "";
     &enTra              = 0;
     &enAny              = 0;
     &enDesdeMes         = 0;
|FIN;

|PROCESO Defectos;
     fFecha = ~;
     aFecha = fFecha;
     aAlfa = aFecha % -104;
     nAny = aAlfa;

     aAlfa = aFecha % 402;
     nMes = aAlfa;
     nMes = nMes - 1;
     |SI nMes = 0;
          nMes = 12;
          nAny = nAny - 1;
     |FINSI;

     #cretai07#2 = nAny;
     #cretai07#3 = nMes;
     |PINTA #cretai07#2;
     |PINTA #cretai07#3;
|FPRC;

|PROCESO AvisoTipos;
     |SI #cretai07#Campo# = 01;
          aAlfa = "    ATENCION: con la selección 'Desde tipo 01'";
          aAlfa = aAlfa + " no se incluyen las liquidaciones L00";
          |MENAV aAlfa;
     |FINSI;
|FPRC;

|PROCESO AyudaTipos; |TIPO 7;
     |PUSHV 08622380;
     |ATRI R;
     ||            6         7
     ||            34567890123456789
     |PINTA 0863, "  - Opciones -   ";
     |PINTA 0963, " 00 :Normal      ";
     |PINTA 1063, " 02 :Finiquitos  ";
     |PINTA 1163, "5-20:Atrasos     ";
     |PINTA 1263, " 90 :Complement. ";
     |PINTA 1363, "                 ";
     |PINTA 1463, "                 ";
     |ATRI 0;
|FPRC;

|PROCESO Restaura;
     |POPV;
|FPRC;

|PROCESO cretam37;
     |SI nPasada = 0;
         nOk = 1;
         |ERROR10;
         |ACABA;
     |FINSI;

     |PRINT;

     nImpresion = nImpresion + 1;
|FPRC;

|DEFBUCLE cretam37;
     #cretam37#1;
     ;
     #cretam36#0, #cretam36#1, #cretam36#2, #cretam36#3, #cretam36#4, 000, #cretam36#58;
     #cretam36#0, #cretam36#1, #cretam36#2, #cretam36#3, #cretam36#4, 999, #cretam36#58;
     ;
     NULL, cretam37;
|FIN;

|PROCESO cretam36;
     |SI nPasada = 0;
         |SI nOk = 1;
             |ERROR10;
             |ACABA;
         |FINSI;
     |FINSI;

     |BUCLE cretam37;
     |PIEINF;
|FPRC;

|DEFBUCLE cretam36_L90;
     #cretam36#3;
     ;
     #cretai07#0, #cretai07#2, #cretai07#3, #cretai07#6, "               ";
     #cretai07#1, #cretai07#2, #cretai07#3, #cretai07#7, "zzzzzzzzzzzzzzz";
     ;
     NULL, cretam36;
|FIN;

|DEFBUCLE cretam36;
     #cretam36#1;
     ;
     #cretai07#0, #cretai07#2, #cretai07#3, #cretai07#6, "               ", 0000;
     #cretai07#1, #cretai07#2, #cretai07#3, #cretai07#7, "zzzzzzzzzzzzzzz", 2999;
     ;
     NULL, cretam36;
|FIN;

|PROCESO Papel;
     nOk  = 0;

     nPasada = 0;
     |BUCLE cretam36;

     |SI nOk = 0;
         |BUCLE cretam36_L90;
     |FINSI;

     |SI nOk = 0;
         aAlfa = "    No se han encontrado datos de RLC para imprimir ";
         aAlfa = aAlfa + "con la selección realizada.";
         |MENAV aAlfa;
         |ACABA;
     |FINSI;

     Impresora = "CrystalReport";
     |IMPRE -1;
     |SI FSalida ! 0;
         |MENAV "0000Impresora CrystalReport no definida, emision cancelada";
         |ACABA;
     |FINSI;

     |INFOR "cretai07";

     nPasada = 1;
     |BUCLE cretam36;
     |BUCLE cretam36_L90;

     |FININF;
     |FINIMP;
|FPRC;

|DEFBUCLE cretam36A_L90;
     #cretam36#3;
     ;
     enEmp, #cretai07#2, #cretai07#3, nDTp, "               ";
     enEmp, #cretai07#2, #cretai07#3, nHTp, "zzzzzzzzzzzzzzz";
     ;
     NULL, cretam36;
|FIN;

|DEFBUCLE cretam36A;
     #cretam36#1;
     ;
     enEmp, #cretai07#2, #cretai07#3, nDTp, "               ", 0000;
     enEmp, #cretai07#2, #cretai07#3, nHTp, "zzzzzzzzzzzzzzz", 2999;
     ;
     NULL, cretam36;
|FIN;

|PROCESO Mensaje;
     aEmp = "                                             ";

     aAlfa   = ("00000" + #labempre#0) % -105;
     aNomEmp = #labempre#2;
     aNomEmp = aNomEmp % 126;
     aEmp = "Empresa: " + aAlfa + " - " + aNomEmp;
     aEmp = " " + aEmp + " ";

     |CUADRO 1217 1619;
     |ATRI R;
     |PINTA 1318, "      - Procesando archivo de RLC -                 ";
     |PINTA 1418, "                                                    ";
     |PINTA 1518, "                                                    ";
     |PINTA 1518, aEmp;
     |ATRI 0;
|FPRC;

|PROCESO EmiteTipo;
     nDTp = nTp;
     nHTp = nTp;
     nOk  = 0;

     nPasada = 0;
     |BUCLE cretam36A;

     |SI nOk = 0;
         |BUCLE cretam36A_L90;
     |FINSI;

     |SI nOk = 0;
         |ACABA;
     |FINSI;

     |HAZ Mensaje;

     |SI enTipo = 0;  eaTitulo = "RLC - MES NORMAL";       |FINSI;
     |SI enTipo = 2;  eaTitulo = "RLC - FINIQUITOS";       |FINSI;
     |SI enTipo = 5;  eaTitulo = "RLC - ATRASOS";          |FINSI;
     |SI enTipo = 90; eaTitulo = "RLC - COMPLEMENTARIOS";  |FINSI;

     |HAZ IMPREArchi;

     nPasada = 1;
     |BUCLE cretam36A;
     |BUCLE cretam36A_L90;

     |FININF;
     |FINIMP;

     |SI swErrorDSArchi = 0;
         enTra      = 0;
         enAny      = #cretai07#2;
         enDesdeMes = #cretai07#3;
         |HAZ Archivando;
     |FINSI;
|FPRC;

|PROCESO ArchivaEmpresa;
     #labempre#0 = enEmp;
     |LEE 000#labempre.=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |PARA nTp = #cretai07#6;  |SI nTp [ #cretai07#7;  |HACIENDO nTp = nTp + 1;
           enTipo = -1;

           |SI nTp = 0;   enTipo = 0;  |FINSI;
           |SI nTp = 2;   enTipo = 2;  |FINSI;
           |SI nTp = 60;  enTipo = 0;  |FINSI;
           |SI nTp = 90;  enTipo = 90; |FINSI;

           |SI nTp ] 5;  |Y nTp [ 20;
               enTipo = 5;
           |FINSI;

           |SI enTipo ] 0;
               |HAZ EmiteTipo;
           |FINSI;
     |SIGUE;
|FPRC;

|PROCESO Archivado;
     |ABRE #labempre;
     |PARA enEmp = #cretai07#0;  |SI enEmp [ #cretai07#1;  |HACIENDO enEmp = enEmp + 1;
           |HAZ ArchivaEmpresa;
     |SIGUE;
     |CIERRA #labempre;
|FPRC;

|PROCESO ProcesaInforme;
     eaVengoDe = "cretai07";
     |HAZ MiraSiArchiva;
     |SI nArchi = 0;
         |HAZ Papel;
         |ACABA;
     |FINSI;

     |MENU aMenu;
     nOpcion = FSalida;
     |SI nOpcion = 1;  |HAZ Papel;      |FINSI;
     |SI nOpcion = 2;  |HAZ Archivado;  |FINSI;
     |SI nOpcion = 3;  |HAZ Papel;      |FINSI;
     |SI nOpcion = 3;  |HAZ Archivado;  |FINSI;

     |SI nOpcion ! 1;
         aAlfa = ":dsarchi/dpntz001;CLI";
         |SUB_EJECUTA aAlfa;
     |FINSI;
|FPRC;

|PROGRAMA;
     |CLS;
     |PINPA #0#cretai07;

     |ABRE #cretai07;
     |DDEFECTO #cretai07;
     |LEE 000#cretai07.p;
     |SI FSalida = 0;
          |BORRA 020#cretai07.a;
     |SINO;
          |HAZ Defectos;
     |FINSI;

     |PINDA #0#cretai07;

     |ENDATOS #1#cretai07;
     |SI FSalida = 0;
          |ABRET #cretam36;
          |ABRET #cretam37;

          |HAZ ProcesaInforme;

          |CIERRAT #cretam37;
          |CIERRAT #cretam36;
     |FINSI;

     |GRABA 000#cretai07.n;
     |CIERRA #cretai07;
|FPRO;
