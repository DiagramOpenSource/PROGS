|FICHEROS;
     nomz0009 #0;
     nomhicca;
     nomhicli;
     nomz00t9 #100,MANTE;
     labempre;
     labtraba;
     nomconst;
     nomcnama;
     nomocuma;
     nomz00l9;
|FIN;

|VARIABLES;
     aPassword = "";
     nArreglados = 0;
     aAlfa = "";

     {-1}menu = "";
     menu1 = "2400";
     menu2 = "1";
     menu3 = "";
     menu4 = "VRIC";
     menu5 = "Volver al lineal";
     menu6 = "Reparar seleccionados";
     menu7 = "Imprimir lista";
     menu8 = "Cancelar proceso";
     nOpcion = 0;

     aAlfa1 = "";
     aAlfa2 = "";
     aTra = "";
     nPorSSEmp = 0;
     nSSEmp = 0;
     nProrrataReal = 0;
     nDiferencia = 0;

     nAny = 0;
     nMes = 0;

     nProceso = 0;
     fFecha = @;
     aHora = "";
     aFich = "";
     aFich2 = "";
|FIN;

|PROCESO IniciaLog;
     |LEE 000#nomz00l9.u;
     |SI FSalida ! 0;
          nProceso = 01;
     |SINO;
          nProceso = #nomz00l9#0 + 1;
     |FINSI;

     #nomz00l9#0 = nProceso;
     fFecha = ~;
     |HORA aHora;
     #nomz00l9#1 = fFecha;
     #nomz00l9#2 = aHora;

     |DFICE aFich;
     |QBF aFich;

     aFich2 = aFich + "copiasnomhicca/";
     |MKDIR aFich2;

     aAlfa1 = aFich + "nomhicca.dat";
     aAlfa2 = nProceso;
     |QBF aAlfa2;
     aAlfa2 = ("00" + aAlfa2) % -102;
     aAlfa2 = aFich2 + "nomhic" + aAlfa2 + ".dat";
     |COPIA_FICHERO aAlfa1, aAlfa2;

     aAlfa1 = aFich + "nomhicca.ddx";
     aAlfa2 = nProceso;
     |QBF aAlfa2;
     aAlfa2 = ("00" + aAlfa2) % -102;
     aAlfa2 = aFich2 + "nomhic" + aAlfa2 + ".ddx";
     |COPIA_FICHERO aAlfa1, aAlfa2;

     aAlfa1 = aFich + "nomhicca.ixx";
     aAlfa2 = nProceso;
     |QBF aAlfa2;
     aAlfa2 = ("00" + aAlfa2) % -102;
     aAlfa2 = aFich2 + "nomhic" + aAlfa2 + ".ixx";
     |COPIA_FICHERO aAlfa1, aAlfa2;

     aAlfa2 = nProceso;
     |QBF aAlfa2;
     aAlfa2 = ("00" + aAlfa2) % -102;
     aAlfa2 = "nomhic" + aAlfa2;
     #nomz00l9#4 = aAlfa2;

     |GRABA 020#nomz00l9.n;
|FPRC;

|PROCESO listado;
     |PRINT;
|FPRC;

|DEFBUCLE listado;
     #100#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, listado;
|FIN;

|PROCESO Imprimir;
     |IMPRE 0;
     |SI FSalida = 0;
          |INFOR "nomz0009";
          |BUCLE listado;
          |PIEINF;
          |FININF;
     |FINSI;
     |FINIMP;
|FPRC;

|PROCESO nomz00t9;
     |SI #100#5 = 0; |ACABA; |FINSI;

     aAlfa1 = #100#0; |QBF aAlfa1; aAlfa1 = ("00000" + aAlfa1) % -105;
     aAlfa2 = #100#1; |QBF aAlfa2; aAlfa2 = ("00000" + aAlfa2) % -105;
     aTra = aAlfa1 + "." + aAlfa2;

     #nomhicca#0 = #100#0;
     #nomhicca#1 = #100#1;
     #nomhicca#2 = nMes;
     #nomhicca#3 = 0;
     #nomhicca#66 = nAny;
     |LEE 000#nomhicca.=;
     |SI FSalida = 0;
          #nomhicca#38 = #100#4;

          #labempre#0 = #nomhicca#0;
          |LEE 000#labempre.=;

          #labtraba#0 = #nomhicca#0;
          #labtraba#1 = #nomhicca#1;
          |LEE 000#labtraba.=;
          |SI FSalida = 0;
               #nomconst#0 = #labtraba#248;
               #nomconst#79 = "01.01.2011";
               |LEE 000#nomconst.=;
               |SI FSalida = 0;
                    #nomhicca#39 = #nomhicca#39 - #100#5;
                    #nomhicca#40 = #nomhicca#40 - #100#5;

                    #nomhicca#48 = #nomhicca#39 % #nomconst#7;
                    #nomhicca#49 = #nomhicca#40 % (#nomconst#13 + #nomconst#17);

                    nPorSSEmp = #nomconst#6 + #nomconst#12 + #nomconst#14 + #nomconst#16;

                    #nomcnama#6 = "01.01.2010";
                    #nomcnama#0 = #labempre#128;
                    |LEE 000#nomcnama.=;

                    aAlfa = #labtraba#204; |QBF aAlfa;
                    |SI nAny ] 13;
                         |SI aAlfa = "e";
                              aAlfa = "";
                         |FINSI;
                    |FINSI;

                    |SI aAlfa ! "";
                         |SI nAny [ 12;
                              #nomocuma#6 = "01.01.2010";
                         |SINO;
                              #nomocuma#6 = "01.01.2013";
                         |FINSI;
                         #nomocuma#0 = #labtraba#204;
                         |LEE 000#nomocuma.=
                         |SI FSalida = 0;
                              nPorSSEmp = nPorSSEmp + #nomocuma#2 + #nomocuma#3;
                         |FINSI;
                    |SINO;
                         nPorSSEmp = nPorSSEmp + #nomcnama#2 + #nomcnama#3;
                    |FINSI;

                    nSSEmp = #nomhicca#39 % nPorSSEmp;
                    nSSEmp = nSSEmp - #nomhicca#143 - #nomhicca#148 - #nomhicca#130;
                    nSSEmp = nSSEmp - #nomhicca#138 - #nomhicca#175;
                    |SI nSSEmp [ 0;
                         nSSEmp = 0;
                    |FINSI;
                    #nomhicca#45 = nSSEmp;

                    || Coste trabajador
                    #nomhicca#150 = #nomhicca#46 + #nomhicca#45;
                    || Liquido trabajador
                    #nomhicca#151 = #nomhicca#46 - #nomhicca#48 - #nomhicca#49 - #nomhicca#15;

                    #nomhicca#154 =  #nomhicca#150 + #nomhicca#152;
                    #nomhicca#155 =  #nomhicca#151 + #nomhicca#153;


                    |GRABA 101#nomhicca.a;
                    |LIBERA #nomhicca;
                    nArreglados = nArreglados + 1;
               |SINO;
                    aAlfa = #nomconst#79;
                    aAlfa = "    Constante " + aAlfa + " del trabajador " + aTra + " no encontrada";
                    |MENAV aAlfa;
               |FINSI;
          |SINO;
               aAlfa = "    Trabajador " + aTra + " no encontrado";
               |MENAV aAlfa;
          |FINSI;
     |SINO;
          aAlfa = "    Nomina del trabajador " + aTra + " no encontrada en historico";
          |MENAV aAlfa;
     |FINSI;
|FPRC;

|DEFBUCLE nomz00t9;
     #100#1;
     7;
     00001, 00001, "S";
     99999, 99999, "S";
     ;
     NULL, nomz00t9;
|FIN;

|PROCESO Reparador;
     |BUCLE nomz00t9;
|FPRC;

|PROCESO nomhicli;
     nProrrataReal = nProrrataReal + (#nomhicli#9 * #nomhicli#10);
|FPRC;

|DEFBUCLE nomhicli;
     #nomhicli#1;
     ;
     #nomhicca#0, #nomhicca#1, #nomhicca#66, #nomhicca#2, #nomhicca#3, 201;
     #nomhicca#0, #nomhicca#1, #nomhicca#66, #nomhicca#2, #nomhicca#3, 208;
     ;
     NULL, nomhicli;
|FIN;

|PROCESO nomhicca;
     nProrrataReal = 0;
     |BUCLE nomhicli;
     nProrrataReal = nProrrataReal ! 2;

     nDiferencia = #nomhicca#38 - nProrrataReal;

     |SI -0.02 [ nDiferencia; |Y nDiferencia [ 0.02;
          || asumo que no tengo diferencias
          nDiferencia = 0;
     |FINSI;

     || TODOS
          #labtraba#0 = #nomhicca#0;
          #labtraba#1 = #nomhicca#1;
          |LEE 000#labtraba.=;

          #100#0 = #nomhicca#0;
          #100#1 = #nomhicca#1;
          #100#2 = #labtraba#2;
          #100#3 = #nomhicca#38;
          #100#4 = nProrrataReal;
          #100#5 = nDiferencia;
          |SI #nomhicca#43 ! 0; |O #nomhicca#121 ! 0;
               #100#6 = "S";
          |SINO;
               #100#6 = "N";
          |FINSI;
          |SI nDiferencia ! 0;
               #100#7 = "S";
          |SINO;
               #100#7 = "N";
          |FINSI;
          |GRABA 101#100n;
          |LIBERA #100;
|FPRC;

|DEFBUCLE nomhicca;
     #nomhicca#1;
     ;
     #0#0, #0#2, nAny, nMes, 0;
     #0#1, #0#3, nAny, nMes, 0;
     be;
     NULL, nomhicca;
|FIN;

|PROCESO inferior;
     #100#0 = 00001;
     #100#1 = 00001;
|FPRC;

|PROCESO superior;
     #100#0 = 99999;
     #100#1 = 99999;
|FPRC;

|PROGRAMA;
     |CUADRO 0501 2380;
     |BLANCO 0602 2279;
     |PINTA 1320, "Introduzca Password : ";
     E_inf = "";
     E_sup = "20";
     aPassword = 1342 ? -1;
     |SI aPassword ! "<MYPASWD>     ";
     |Y  aPassword ! "<MYPASWD>               ";
         |MENAV "     Permiso denegado.";
         |ACABA;
     |FINSI;

     aAlfa1 = Usuario; |QBT aAlfa1;    aAlfa1 = ("g" + aAlfa1) % 108;
     |NOME_DAT #100 aAlfa1;
     |DELINDEX #100;
     |ABRE #100;

     |ABRE #labtraba;
     |ABRE #labempre;
     nArreglados = 0;
     |ABRE #nomhicca;
     |ABRE #nomhicli;
     |ABRE #nomconst;
     |ABRE #nomcnama;
     |ABRE #nomocuma;
     |ABRE #nomz00l9;

     |CLS;
     |PINPA #0#0;
     |PINDA #0#0;
     |ENDATOS #1#0;

     |SI FSalida = 0;
          nAny = #0#4 - 2000;
          nMes = #0#5;

          |BUCLE nomhicca;

          nOpcion = 0;
          |PARA; |SI nOpcion = 0; |HACIENDO;
               |ENTLINEAL #1#100, 2, 4, 22, 1, inferior, superior;
               |MENU menu;
               nOpcion = FSalida;
               |SI nOpcion = 1;
                    nOpcion = 0;
               |FINSI;
               |SI nOpcion = 2;
                    |CONFI "    S¿Desea continuar con el proceso?";
                    |SI FSalida = 0;
                         |HAZ IniciaLog;
                         |HAZ Reparador;
                         #nomz00l9#0 = nProceso;
                         |LEE 101#nomz00l9.=;
                         #nomz00l9#3 = nArreglados;
                         |GRABA 020#nomz00l9.a;
                         |LIBERA #nomz00l9;
                    |FINSI;
               |FINSI;
               |SI nOpcion = 3;
                    |HAZ Imprimir;
                    nOpcion = 0;
               |FINSI;
               |SI nOpcion > 3;
                    || me salgo
               |FINSI;
          |SIGUE;
     |FINSI;

     |CIERRA #nomz00l9;
     |CIERRA #nomocuma;
     |CIERRA #nomcnama;
     |CIERRA #nomconst;
     |CIERRA #labempre;
     |CIERRA #labtraba;
     |CIERRA #nomhicli;
     |CIERRA #nomhicca;
     |CIERRA #100;

     |SI nArreglados ! 0;
          aAlfa = nArreglados;
          |QBF aAlfa;
          aAlfa = "    Se ha rectificado la prorrata de pagas en " + aAlfa;
          |SI nArreglados = 1;
               aAlfa = aAlfa + " trabajador";
          |SINO;
               aAlfa = aAlfa + " trabajadores";
          |FINSI;
          |MENAV aAlfa;
     |SINO;
          |SI nOpcion [ 3;
               aAlfa = "    No se ha seleccionado ningun trabajador para rectificar";
               |MENAV aAlfa;
          |FINSI;
     |FINSI;
|FPRO;
