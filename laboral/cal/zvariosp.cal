|| PROCESO VARIADOS QUE SE REPITEN A LO LARGO DE LA NOMINA
|| PARA QUE NO PASE MAS

|FICHEROS;
     nomcontr;
     labempre;
     labtraba;
     labtrtra;
     labcontr;
     labmunic;
     labcentr;
     nomtraca;
     nomtraac;
     nomvarca;
     cretam00;
     cretam05;
     nomhicca;
     nom900de;
     nom900dt;
     nomdesam;
     nomcenes;
     nomcalac;

     copiadef@ #500;
|FIN;

|VARIABLES;
     &aMes = "";
     &nMes = 0;
     &nAny = 0;
     &nTopemes = 0;
     nBisiesto = 0;
     nNume = 0;
     nNume1 = 0;
     nNume2 = 0;
     nNume3 = 0;
     nNume4 = 0;

     aAlfa = "";
     &eaNom   = "";
     &eaApel1 = "";
     &eaApel2 = "";
     &aCadena    = "";
     aLong    = "";

     nInd     = 0;
     nLong    = 0;
     nCalc    = 0;
     nHayComa = 0;
     nPos     = 0;
     SwSalir  = 0;
     aAlfa1 = "";
     aAlfa2 = "";

     fFecha = @;
     &aFecha = "";
     fec = "";
     sw = 0;
     dia1 = "";
     mes1 = "";
     any1 = "";
     dia = 0;
     mes = 0;
     any = 0;
     tope = 0;
     men1 = "";

     nTope = 0;
     aAny = "";
     mod = 0;
     aError = "";
     aMensa = "";
     &eaEmail = "";
     &enEmpresa = 0;
     &enTrabajador = 0;
     nCampo = 0;

     &enConcepto = 0;
     &eaDescripcion = "";

     &swSigue = 0;
     nHandle = 0;

     &aCodPos = "";
     &nContMuni = 0;
     &aCodMuni = "";
     aMuni = "";
     &nMensajeF5 = 0;
     &aCodigoMunicipio = "";

     &aDocumento = "";
     &aTipoID = "";

     &fFechaDesde = @;
     &fFechaHasta = @;
     &nDias = 0;
     &nMeses = 0;
     &nAnys = 0;

     aDiaAlta = ""; nDiaAlta = 0;
     aMesAlta = ""; nMesAlta = 0;
     aAnyAlta = ""; nAnyAlta = 0;
     aDiaBaja = ""; nDiaBaja = 0;
     aMesBaja = ""; nMesBaja = 0;
     aAnyBaja = ""; nAnyBaja = 0;

     nume1 = 0;
     xmesnume = 0;
     xanynume = 0;
     xtopemes = 0;
     xmod = 0;

     aDias = "";
     aMeses = "";
     aAnys = "";

     &bus_emp = 0;
     &bus_tra = 0;
     &bus_cla = 0;
     &bus_imp = "";

     aFechaAlta = "";
     fFechaAlta = @;
     aFechaBaja = "";
     fFechaBaja = @;
     &fFechaIniPer = @;
     &fFechaFinPer = @;
     &aFechaIniParam = "";
     &aFechaFinParam = "";
     &enSwAlta = 0;
     &enMes = 0;
     &enAny = 0;

     nReg1 = 0;
     nReg1Cont = 0;
     nReg2 = 0;
     nReg2Cont = 0;
     &eaRegimen = "";
     &eaTipoXML = "";
     &eaReferencia = "";

     aAlfa0 = "";
     aAlfa3 = "";
     nContadorRef = 0;
     &efFecha = @;
     &eaHora = "";
     &eaFichero = "";

     swError = 0;
     swHorasLoc = 0;
     aHorasSalida = "";
     aMinutosSalida = "";
     aLonHoras = "";
     nLonHoras = 0;
     aHoras = "";
     aLon = ""
     nLon = 0;
     nCar = 0;
     &enDiasAlta = 0;
     &nEmpAUTO = 0;
     &nTraAUTO = 0;
     nEmp = 0;
     nTra = 0;
     aNif = "";
     nAnyHis = 0;
     nMesHis = 0;
     &promE_AUTO = 0;
     &promA_AUTO = 0;
     nBase = 0;
     nBaseHoras = 0;
     &nElMes = 0;
     &nElAny = 0;
     &enEmp = 0;
     &enCen = 0;

     &eaCadena = "";
     &eaCadenaSal = "";
     &eaCaracter1 = "";
     &eaCaracter2 = "";
     nAscii = 0;
     aCaracter = "";
     aCaracterSal = "";
     &swBasico = 0;
     &swMsDos = 0;
     nLongitud = 0;
     aLongitud = "";
     nBusca = 0;
     &enTipo = 0;
     &nTipo = 0;
     nFactor = 0;

     &enConvenio = 0;

     aBase = "";
     aMenu = "";
     aArchivo = "";
     aLinea = "";
     &aVersion = "";
     nCanal = 0;

     aCCC = "";
     aMesLiq = "";
     aAnyLiq = "";
     nMesLiq = 0;
     nMesDesde = 0;
     nAnyDesde = 0;
     nTrab_Alta = 0;
     nEmp1 = 0; nEmp2 = 0; nEmp3 = 0; nEmp4 = 0; nEmp5 = 0;
     nTra1 = 0; nTra2 = 0; nTra3 = 0; nTra4 = 0; nTra5 = 0;
     nTra0 = 0;
     &eaNafBusca = "";
     &enTraPorNaf = 0;
     nTrabAlta = 0;
     &eaCCC = "";
     &enEmpNaf = 0;
     nDesdeEmp = 0;
     nHastaEmp = 0;

     &enSwActiva = 0;
     aFabre = "";
     aMas = "";
     aFich = "";
     nRegProv = 0;

     &enEmpAtr = 0;
     &enAnyExig = 0;
     &enAnyCalc = 0;
     &enMesCont = 0;
     &enAnyCont = 0;
     &enTipoAtrasos = 0;
     nTipoProvMen = 0;
     nTipoProvHis = 0;
     swEncontrado = 0;
|FIN;

|PROCESO topemes_plb;
     || variables entrada: nMes, nAny
     || variable salida: nTopemes

     nBisiesto = 0;
     nNume = nAny $ 4;
     |SI nNume = 0;
          nBisiesto = 1;
     |FINSI;
     nTopemes = 31;
     |SI nMes = 2;
          nTopemes = 28 + nBisiesto;
     |FINSI;
     |SI nMes = 4; |O nMes = 6; |O nMes = 9; |O nMes = 11;
          nTopemes = 30;
     |FINSI;
|FPRC;

|PROCESO MesLetra_plb;
     |SI nMes = 1;  aMes = "Enero     "; |FINSI;
     |SI nMes = 2;  aMes = "Febrero   "; |FINSI;
     |SI nMes = 3;  aMes = "Marzo     "; |FINSI;
     |SI nMes = 4;  aMes = "Abril     "; |FINSI;
     |SI nMes = 5;  aMes = "Mayo      "; |FINSI;
     |SI nMes = 6;  aMes = "Junio     "; |FINSI;
     |SI nMes = 7;  aMes = "Julio     "; |FINSI;
     |SI nMes = 8;  aMes = "Agosto    "; |FINSI;
     |SI nMes = 9;  aMes = "Septiembre"; |FINSI;
     |SI nMes = 10; aMes = "Octubre   "; |FINSI;
     |SI nMes = 11; aMes = "Noviembre "; |FINSI;
     |SI nMes = 12; aMes = "Diciembre "; |FINSI;
|FPRC;

|PROCESO SeparaNom_plb;
/*
     VARIABLES:
     &aCadena = ENTRADA: el nombre + apellidos completo
     &eaNom   = SALIDA: el nombre
     &eaApel1 = SALIDA: el apellido 1
     &eaApel2 = SALIDA: el apellido 2
*/
     aAlfa = aCadena;
     eaNom = ""; eaApel1 = ""; eaApel2 = "";
     nInd = 1;
     aLong = aAlfa % 0; nLong = aLong;
     aAlfa1 = aAlfa -",";
     |SI FSalida ! 0;
          nCalc = FSalida + 98;
          aAlfa1 = aAlfa % nCalc;
          eaNom = aAlfa1;
          |PARA; |SI; |HACIENDO;
               aAlfa1 = eaNom % 0101;
               |SI aAlfa1 = " "; |O aAlfa1 = ",";
                    eaNom = eaNom % 0299;
               |SINO;
                    |SAL;
               |FINSI;
          |SIGUE;
          nHayComa = 1;
          aAlfa1 = aAlfa - ",";
          nCalc = ((FSalida / 100) ! 0) + 99;
          aAlfa = aAlfa % nCalc;
     |SINO;
          nHayComa = 0;
     |FINSI;
     aAlfa1 = "";
     |PARA nCalc = 1; |SI nCalc [ nLong; |HACIENDO nCalc = nCalc + 1;
          nPos = (nCalc * 100) + 1;
          aAlfa2 = aAlfa % nPos;
          |SI aAlfa2 = " "; |O aAlfa2 = ",";
               |SI aAlfa1 ! "DE"; |Y aAlfa1 ! "de"; |Y aAlfa1 ! "LA";
               |Y aAlfa1 ! "la"; |Y aAlfa1 ! "LAS"; |Y aAlfa1 ! "las";
               |Y aAlfa1 ! "LOS"; |Y aAlfa1 ! "los"; |Y aAlfa1 ! "DEL";
               |Y aAlfa1 ! "del"; |Y aAlfa1 ! "EL"; |Y aAlfa1 ! "el";
                    |SI nHayComa = 0;
                         |SI nInd = 1; eaNom   = eaNom   + aAlfa1 + " "; |FINSI;
                         |SI nInd = 2; eaApel1 = eaApel1 + aAlfa1 + " "; |FINSI;
                         |SI nInd ] 3; eaApel2 = eaApel2 + aAlfa1 + " "; |FINSI;
                         nInd = nInd + 1;
                    |SINO;
                         |SI nInd = 1; eaApel1 = eaApel1 + aAlfa1 + " "; |FINSI;
                         |SI nInd ] 2; eaApel2 = eaApel2 + aAlfa1 + " "; |FINSI;
                         nInd = nInd + 1;
                    |FINSI;
               |SINO;
                    |SI nHayComa = 0;
                         |SI nInd > 1; nInd = nInd - 1; |FINSI;
                         |SI nInd = 1; eaNom   = eaNom   + aAlfa1 + " "; |FINSI;
                         |SI nInd = 2; eaApel1 = eaApel1 + aAlfa1 + " "; |FINSI;
                         |SI nInd ] 3; eaApel2 = eaApel2 + aAlfa1 + " "; |FINSI;
                    |SINO;
                         ||SI nInd > 1; nInd = nInd - 1; |FINSI;
                         |SI nInd = 1; eaApel1 = eaApel1 + aAlfa1 + " "; |FINSI;
                         |SI nInd ] 2; eaApel2 = eaApel2 + aAlfa1 + " "; |FINSI;
                   |FINSI;
               |FINSI;
               aAlfa1 = "";
          |SINO;
               aAlfa1 = aAlfa1 + aAlfa2;
          |FINSI;
     |SIGUE;
     |QBF aAlfa1;
     |SI aAlfa1 ! "";
          |SI nHayComa = 0;
               |SI nInd = 1; eaApel1 = eaApel1 + aAlfa1 + " "; |FINSI;
               |SI nInd = 2; eaApel2 = eaApel2 + aAlfa1 + " "; |FINSI;
               |SI nInd ] 3; eaNom   = eaNom   + aAlfa1 + " "; |FINSI;
          |SINO;
               |SI nInd = 1; eaApel1 = eaApel1 + aAlfa1 + " "; |FINSI;
               |SI nInd ] 2; eaApel2 = eaApel2 + aAlfa1 + " "; |FINSI;
          |FINSI;
     |FINSI;

     |PARA; |SI SwSalir = 0; |HACIENDO;
          SwSalir = 1;
          aAlfa1 = eaApel1 % 0101;
          |SI aAlfa1 = " "; eaApel1 = eaApel1 % 0299; SwSalir = 0; |FINSI;
          aAlfa1 = eaApel2 % 0101;
          |SI aAlfa1 = " "; eaApel2 = eaApel2 % 0299; SwSalir = 0; |FINSI;
          |SI nHayComa = 0;
               aAlfa1 = eaNom   % 0101;
               |SI aAlfa1 = " "; eaNom   = eaNom   % 0299; SwSalir = 0; |FINSI;
          |FINSI;
     |SIGUE;
     |QBF eaNom; |QBF eaApel1; |QBF eaApel2;
|FPRC;

|PROCESO CompFecha_plb;
/*
     &aFecha = ENTRADA: la fecha alfanumerica a comprobar
*/
     fec = aFecha; sw = 0;
     |SI fec ! "          ";
          dia1 = fec % A102;
          mes1 = fec % A402;
          any1 = fec % A704;
          dia = dia1; mes = mes1; any = any1;
          |SI dia ! 0; |Y mes ! 0; |Y any ! 0;
               |SI mes [ 12; |Y mes ] 1;
                    |SI mes = 1; |O mes = 3; |O mes = 5; |O mes = 7; |O mes = 8; |O mes = 10; |O mes = 12;
                         tope = 31;
                    |SINO;
                         |SI mes = 2;
                              mod = any $ 4;
                              |SI mod = 0;
                                   tope = 29;
                              |SINO;
                                   tope = 28;
                              |FINSI;
                         |SINO;
                              tope = 30;
                         |FINSI;
                    |FINSI;
                    |SI dia [ tope;
                         sw = 1;
                    |FINSI;
               |FINSI;
          |FINSI;
     |SINO;
          sw = 1;
     |FINSI;
     |SI sw = 0;
         |MENAV "    ATENCION: Formato de fecha incorrecta. Utilice el formato DD.MM.AAAA";
         |ERROR;
     |FINSI;
|FPRC;

|PROCESO sustituye_tilde;
     nAscii = & aCaracter;
     aCaracterSal = aCaracter;
     |SI nAscii = 160; |O nAscii = 225;            || el caracter  
          |SI swBasico = 1;
               aCaracterSal = "á";
          |FINSI;
          |SI swMsDos = 1;
               aCaracterSal = " ";
          |FINSI;
     |FINSI;
     |SI nAscii = 130; |O nAscii = 233;            || el caracter ‚
          |SI swBasico = 1;
               aCaracterSal = "é";
          |FINSI;
          |SI swMsDos = 1;
               aCaracterSal = "‚";
          |FINSI;
     |FINSI;
     |SI nAscii = 161; |O nAscii = 237;            || el caracter ¡
          |SI swBasico = 1;
               aCaracterSal = "í";
          |FINSI;
          |SI swMsDos = 1;
               aCaracterSal = "¡";
          |FINSI;
     |FINSI;
     |SI nAscii = 162; |O nAscii = 243;            || el caracter ¢
          |SI swBasico = 1;
               aCaracterSal = "ó";
          |FINSI;
          |SI swMsDos = 1;
               aCaracterSal = "¢";
          |FINSI;
     |FINSI;
     |SI nAscii = 163; |O nAscii = 250;            || el caracter £
          |SI swBasico = 1;
               aCaracterSal = "ú";
          |FINSI;
          |SI swMsDos = 1;
               aCaracterSal = "£";
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Tildes_plb;
     eaCadenaSal = "";
     aLongitud = eaCadena % 0;
     nLongitud = aLongitud;
     |PARA nPos = 1; |SI nPos [ nLongitud; |HACIENDO nPos = nPos + 1;
          nBusca = (nPos * 100) + 1;
          aCaracter = eaCadena % nBusca;
          |HAZ sustituye_tilde;
          eaCadenaSal = eaCadenaSal + aCaracterSal;
     |SIGUE;
|FPRC;

|PROCESO CambiaTildes_plb;
     |HAZ Tildes_plb;
|FPRC;

/***** PROCESO PARA COMPROBAR QUE EL EMAIL ES CORRECTO (MAS O MENOS) ********/

|PROCESO CompEmail_plb;

     aError = "    ATENCION. El formato de email introducido no es correcto: ";
     nNume1 = 0;
     nNume2 = 0;
     aMensa = aError;
     aAlfa = eaEmail;
     |QBF aAlfa;
     |SI aAlfa ! "";
          aAlfa1 = aAlfa - "@";
          |SI aAlfa = aAlfa1;
               aMensa = aError + "falta la arroba ('@')";
          |FINSI;
          aAlfa1 = aAlfa - ".";
          |SI aAlfa = aAlfa1;
               aMensa = aError + "falta el punto ('.')";
          |FINSI;
/*
          aAlfa1 = aAlfa - "@";
          aAlfa = aAlfa1;
          aAlfa1 = aAlfa - "@";
          |SI aAlfa ! aAlfa1;
               aMensa = aError + "hay más de una arroba ('@')";
          |FINSI;
*/
          aAlfa1 = aAlfa;
          |QBT aAlfa;
          |SI aAlfa ! aAlfa1;
               aMensa = aError + "no se permiten espacios";
          |FINSI;
          |SI aMensa ! aError;
               |MENAV aMensa;
               |ERROR;
          |FINSI;
     |FINSI;
|FPRC;

/************* PROCESOS PARA LISTAR TRABAJADORES EN ALTA ********************/

|PROCESO labtraba_plb;
     |PARA nCampo = 0; |SI nCampo [ 253; |HACIENDO nCampo = nCampo + 1;
          #labtrtra#nCampo# = #labtraba#nCampo#;
     |SIGUE;
     |GRABA 020#labtrtra.n;
     |LIBERA #labtrtra;
|FPRC;

|DEFBUCLE labtraba_plb;
     #labtraba#1;
     44;
     enEmpresa, 00001, "A";
     enEmpresa, 99999, "A";
     ;
     NULL, labtraba_plb;
|FIN;

|PROCESO CargaTra_plb;
     aAlfa = Usuario; |QBF aAlfa;    aAlfa = "T" + aAlfa;
     |NOME_DAT #labtrtra#aAlfa#;
     |DELINDEX #labtrtra;
     |ABRE #labtrtra;
     |BUCLE labtraba_plb;
     |CIERRA #labtrtra;
|FPRC;

|PROCESO sup_labtraba;
     #labtraba#44 = "A";
     |SI enEmpresa = 0;
          #labtraba#0 = 99999;
     |SINO;
          #labtraba#0 = enEmpresa;
     |FINSI;

     #labtraba#1 = 99999;
|FPRC;

|PROCESO inf_labtraba;
     #labtraba#44 = "A";
     |SI enEmpresa = 0;
          #labtraba#0 = 00001;
     |SINO;
          #labtraba#0 = enEmpresa;
     |FINSI;

     #labtraba#1 = 00001;
|FPRC;

|PROCESO EnAlta_plb;
/*
          enTrabajador = 0;

          |HAZ CargaTra_plb;
          |ABRE #labtrtra;
          |LEE 000#labtrtra.p;
          |CONSULTA_CLAVE #2#labtrtra;
          |SI FSalida = 0;
               enTrabajador = #labtrtra#1;
          |FINSI;
          |CIERRA #labtrtra;
*/
          enTrabajador = 0;

          ||HAZ CargaTra_plb;
          |ABRE #labtraba;
          |CONSULTA_F_CLAVE #8#labtraba, inf_labtraba, sup_labtraba;
          |SI FSalida = 0;
               enEmpresa = #labtraba#0;
               enTrabajador = #labtraba#1;
          |FINSI;
          |CIERRA #labtraba;
|FPRC;

/************* PROCESO PARA DESCRIPCION DE LOS CONCEPTOS 900 ****************/

|| variable de entrada: enConcepto
|| variable de salida: eaDescripcion

|PROCESO Des900_plb;
     eaDescripcion = "";
     |SI enConcepto = 901; eaDescripcion = "N§ HORAS";  |FINSI;
     |SI enConcepto = 902; eaDescripcion = "INDEMNI.";  |FINSI;
     |SI enConcepto = 903; eaDescripcion = "CITRICOS";  |FINSI;
     |SI enConcepto = 904; eaDescripcion = "VACACION";  |FINSI;
     |SI enConcepto = 905; eaDescripcion = "HORAS TP";  |FINSI;
     |SI enConcepto = 906; eaDescripcion = "ANTIC. 1";  |FINSI;
     |SI enConcepto = 907; eaDescripcion = "ANTIC. 2";  |FINSI;
     |SI enConcepto = 908; eaDescripcion = "ESPECIE ";  |FINSI;
     |SI enConcepto = 923; eaDescripcion = "ESPECIE2";  |FINSI;
     |SI enConcepto = 924; eaDescripcion = "ESPECIE3";  |FINSI;
     |SI enConcepto = 909; eaDescripcion = "H.C.SEM.";  |FINSI;
     |SI enConcepto = 910; eaDescripcion = "H.T.SEM.";  |FINSI;
     |SI enConcepto = 911; eaDescripcion = "H.C.MES ";  |FINSI;
     |SI enConcepto = 912; eaDescripcion = "H.T.MES ";  |FINSI;
     |SI enConcepto = 913; eaDescripcion = "ALTA=CTO";  |FINSI;
     |SI enConcepto = 914; eaDescripcion = "D.ALTA  ";  |FINSI;
     |SI enConcepto = 915; eaDescripcion = "DIAS VAC";  |FINSI;
     |SI enConcepto = 916; eaDescripcion = "DED.S.S.";  |FINSI;
     |SI enConcepto = 917; eaDescripcion = "DIAS TC2";  |FINSI;
     |SI enConcepto = 918; eaDescripcion = "HORASTC2";  |FINSI;
     |SI enConcepto = 919; eaDescripcion = "BASEIRPF";  |FINSI;
     |SI enConcepto = 920; eaDescripcion = "P.INDEM.";  |FINSI;
     |SI enConcepto = 921; eaDescripcion = "ANTIC.PE";  |FINSI;
     |SI enConcepto = 925; eaDescripcion = "MED.VAR.";  |FINSI;
     |SI enConcepto = 926; eaDescripcion = "ANTIC. 3";  |FINSI;
     |SI enConcepto = 927; eaDescripcion = "BASE IT2";  |FINSI;
     |SI enConcepto = 928; eaDescripcion = "BASE FUN";  |FINSI;
     |SI enConcepto = 929; eaDescripcion = "BASE AGR";  |FINSI;
|FPRC;

|| PROCESOS PARA BUSCAR EL CODIGO DE MUNICIPIO POR EL CODIGO POSTAL

/*
     || variables de entrada:

     &aCodPos -> es el codigo postal del que queremos saber el cod. de municipio

     || variables de salida:

     &nContMuni ->  es el numero de municipios que he encontrado con ese
                    codigo postal
     &aCodMuni ->   es el codigo de municipio claro
*/

|PROCESO CuentaMunicipio_plb;
     nContMuni = nContMuni + 1;
     aCodMuni = #labmunic#1;
|FPRC;

|DEFBUCLE CuentaMunicipio_plb;
     #labmunic#1;
     ;
     aCodPos, "00000";
     aCodPos, "99999";
     ;
     NULL, CuentaMunicipio_plb;
|FIN;

|PROCESO BuscaMunicipio_plb;
     aAlfa2 = #labmunic#3;            || aAlfa2 = municipio de labmunic
     |QBT aAlfa2;
     aMuni = #labempre#6;   || aMuniEmp = municipio de labempre
     aMuni = aMuni > "";
     |QBT aMuni;

     aAlfa = aMuni - aAlfa2;
     |SI aAlfa ! aMuni;
          ||aAlfa2 esta en aMuni
          aCodMuni = #labmunic#1;
          nContMuni = nContMuni + 1;
     |FINSI;
|FPRC;

|DEFBUCLE BuscaMunicipio_plb;
     #labmunic#1;
     ;
     aCodPos, "00000";
     aCodPos, "99999";
     ;
     NULL, BuscaMunicipio_plb;
|FIN;

|PROCESO min_labmunic_plb;
     #labmunic#4 = aCodPos;
     #labmunic#1 = "00000";
|FPRC;

|PROCESO max_labmunic_plb;
     #labmunic#4 = aCodPos;
     #labmunic#1 = "99999";
|FPRC;

|PROCESO CodigoMuni_plb;
     nContMuni = 0;
     |BUCLE CuentaMunicipio_plb;

     |SI nContMuni > 1;
          nContMuni = 0;
          |BUCLE BuscaMunicipio_plb;
          |SI nContMuni > 1;
               nMensajeF5 = 1;
               aCodigoMunicipio = "";
               |SUB_EJECUTA "labmunic";
               |SI aCodigoMunicipio ! "";
                    aCodMuni = aCodigoMunicipio;
                    nContMuni = 1;
               |FINSI;
          |FINSI;
     |FINSI;

     |SI nContMuni = 0;
          nMensajeF5 = 1;
          aCodigoMunicipio = "";
          |SUB_EJECUTA "labmunic";
          |SI aCodigoMunicipio ! "";
               aCodMuni = aCodigoMunicipio;
               nContMuni = 1;
          |FINSI;
     |FINSI;
     ||ahora se supone que en aCodMuni tenemos el codigo del municipio
|FPRC;

|| devuelve el tipo de identificacion del titular: nif, cif, pasaporte, nie

||             variable entrada: aDocumento
||             variable salida:  aTipoID

|PROCESO TipoID_plb;
     aAlfa = aDocumento % 101; || tipo identificador titular
     |SI aAlfa ] "0"; |Y aAlfa [ "9";
          aTipoID = "1";            || tipo identificacion DNI
     |SINO;
          |SI aAlfa = "X"; |O aAlfa = "Y"; |O aAlfa1 = "Z";
               aTipoID = "6";        || tipo identificacion EXTRANJERO
          |SINO;
               |SI aAlfa ] "a"; |Y aAlfa [ "z";
                    aTipoID = "2";    || tipo identificacion PASAPORTE
               |SINO;
                    aTipoID = "9";    || tipo identificacion CIF
               |FINSI;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO diaanter_plb;                       || RUTINA DEL CALCULO duracion
     |SI xmesnume = 1;
          xtopemes = 31;
     |FINSI;
     |SI xmesnume = 2;
          xmod = xanynume $ 4;
          |SI xmod = 0;
               xtopemes = 29;
          |SINO;
               xtopemes = 28;
          |FINSI;
     |FINSI;
     |SI xmesnume = 3;   xtopemes = 31; |FINSI;
     |SI xmesnume = 4;   xtopemes = 30; |FINSI;
     |SI xmesnume = 5;   xtopemes = 31; |FINSI;
     |SI xmesnume = 6;   xtopemes = 30; |FINSI;
     |SI xmesnume = 7;   xtopemes = 31; |FINSI;
     |SI xmesnume = 8;   xtopemes = 31; |FINSI;
     |SI xmesnume = 9;   xtopemes = 30; |FINSI;
     |SI xmesnume = 10;  xtopemes = 31; |FINSI;
     |SI xmesnume = 11;  xtopemes = 30; |FINSI;
     |SI xmesnume = 12;  xtopemes = 31; |FINSI;
|FPRC;

|PROCESO Periodo_plb;
     aFecha = fFechaDesde;
     aDiaAlta = (aFecha % 102);
     aMesAlta = (aFecha % 402);
     aAnyAlta = (aFecha % -104);

     aFecha = fFechaHasta;
     aDiaBaja = (aFecha % 102);
     aMesBaja = (aFecha % 402);
     aAnyBaja = (aFecha % -104);

     nDiaAlta = aDiaAlta;
     nMesAlta = aMesAlta;
     nAnyAlta = aAnyAlta;
     nDiaBaja = aDiaBaja;
     nMesBaja = aMesBaja;
     nAnyBaja = aAnyBaja;

     nAnys = nAnyBaja - nAnyAlta;

     nMeses = nMesBaja - nMesAlta;
     |SI nMeses [ 0;
          |SI nAnys ! 0;
               nAnys = nAnys - 1;
               nMeses = 12 + nMeses;
          |FINSI;
     |FINSI;

     nDias = nDiaBaja - nDiaAlta + 1;
     nume1 = nDiaAlta - 1;
     xmesnume = nMesBaja;
     xanynume = nAnyBaja;
     |HAZ diaanter_plb;
     |SI nDiaBaja = nume1;
          || nMeses = nMeses + 1;
          nDias = 0;
     |SINO;
          |SI nDiaAlta = 1; |Y nDiaBaja = xtopemes;
               nMeses = nMeses + 1;
               nDias = 0;
          |FINSI;
     |FINSI;
     |SI nDias < 0;
          xmesnume = nMesAlta;
          xanynume = nAnyAlta;
          |HAZ diaanter_plb;
          nDias = xtopemes - nDiaAlta + 1;
          nDias = nDias + nDiaBaja;
          nMeses = nMeses - 1;
     |FINSI;
     |SI nMeses = 12;
          nAnys = nAnys + 1;
          nMeses = 0;
     |FINSI;

     aAnys = nAnys;
     aMeses = nMeses;
     aDias = nDias;
|FPRC;
/*
     |SI nAnys > 0;
          aDuracion = aAnys + " a¤o";
          |SI nAnys > 1;
               aDuracion = aDuracion + "s";
          |FINSI;
     |FINSI;
     |SI nMeses > 0;
          |SI aDuracion ! "";
               aDuracion = aDuracion + ", ";
          |FINSI;
          aDuracion = aDuracion + aMeses + " mes";
          |SI nMeses > 1;
               aDuracion = aDuracion + "es";
          |FINSI;
     |FINSI;
     |SI nDias > 0;
          |SI aDuracion ! "";
               aDuracion = aDuracion + ", ";
          |FINSI;
          aDuracion = aDuracion + aDias + " dia";
          |SI nDias > 1;
               aDuracion = aDuracion + "s";
          |FINSI;
     |FINSI;
*/

|PROCESO busqueda_plb; |TIPO 0;
     bus_emp = #labtraba#0;
     bus_tra = #labtraba#1;
     bus_cla = 0;
     bus_imp = "";
     |SI FSalida =  9;  bus_cla = 1;  |FINSI;
     |SI FSalida = 10;  bus_cla = 2;  |FINSI;
     |SI FSalida = 11;  bus_cla = 3;  |FINSI;
     |SI bus_cla ! 0;
          |HAZ bajasino_plb;
          |CIERRAT #labtraba;
          |SUB_EJECUTA "nombusca";
          |ABRET #labtraba;

          |MODULO aAlfa;
          |QBF aAlfa;
          |SI aAlfa = "labtraba";
               #labtraba#0 = bus_emp;   |PINTA #labtraba#0;
               #labtraba#1 = bus_tra;   |PINTA #labtraba#1;
          |FINSI;
          |SI aAlfa = "nomtraca";
               #nomtraca#0 = bus_emp;   |PINTA #nomtraca#0;
               #nomtraca#1 = bus_tra;   |PINTA #nomtraca#1;
          |FINSI;
          |SI aAlfa = "nomtraac";
               #nomtraac#0 = bus_emp;   |PINTA #nomtraac#0;
               #nomtraac#1 = bus_tra;   |PINTA #nomtraac#1;
          |FINSI;

          |PTEC 802;
          |PTEC 802;
     |FINSI;
|FPRC;

|PROCESO bajasino_plb;
     |CONFI "1200NIncluir trabajadores en baja? ";
     |SI FSalida = 0;
          bus_imp = "S"
     |SINO;
          bus_imp = "N";
     |FINSI;
|FPRC;

|PROCESO FormatoHoras_plb;
     swError = 0;
     swHorasLoc = 0;
     aHorasSalida = "";
     aMinutosSalida = "";
     aLonHoras = "";
     nLonHoras = 0;

     aHoras = eaHora;
     |QBT aHoras;
     |SI aHoras = "";
          |ACABA;
     |FINSI;
     aLon = aHoras % 0;
     nLon = aLon;
     |SI nLon ! 5;
          |PARA nCar = 1; |SI nCar [ nLon; |HACIENDO nCar = nCar + 1;
               nPos = (nCar * 100) + 1;
               aAlfa = aHoras % nPos;
               |SI swHorasLoc = 0;
                    |SI aAlfa = "."; |O aAlfa = ":"; |O aAlfa = ",";
                         swHorasLoc = 1;
                    |SINO;
                         aLonHoras = aHorasSalida % 0;
                         nLonHoras = aLonHoras;
                         |SI nLonHoras ! 2;
                              aHorasSalida = aHorasSalida + aAlfa;
                         |SINO;
                              aMinutosSalida = aMinutosSalida + aAlfa;
                         |FINSI;
                    |FINSI;
               |SINO;
                    aMinutosSalida = aMinutosSalida + aAlfa;
               |FINSI;
          |SIGUE;
          aHorasSalida = ("00" + aHorasSalida) % -102;
          aMinutosSalida = ("00" + aMinutosSalida) % -102;
          aAlfa1 = aHorasSalida;
          aAlfa2 = aMinutosSalida;
     |SINO;
          aAlfa1 = aHoras % 102;
          aAlfa2 = aHoras % -102;
          aHorasSalida = aAlfa1;
          aMinutosSalida = aAlfa2;
     |FINSI;

     nNume1 = aAlfa1;
     nNume2 = aAlfa2;
     |SI nNume1 < 0; |O nNume1 > 23;
          swError = 1;
     |FINSI;
     |SI nNume2 < 0; |O nNume2 > 59;
          swError = 2;
     |FINSI;
     |SI swError = 1;
          |MENAV "    ATENCION. Error de introducción de datos. Revise el número de horas";
          |ERROR;
          |ACABA;
     |FINSI;
     |SI swError = 2;
          |MENAV "    ATENCION. Error de introducción de datos. Revise el número de minutos";
          |ERROR;
          |ACABA;
     |FINSI;

     aAlfa = aHorasSalida + ":" + aMinutosSalida;
     eaHora = aAlfa;
|FPRC;

|PROCESO CompAltaTrab_plb;
/*
Comprueba si un trabajador esta de alta en un determinado mes/a¤o segun
las fechas de alta/baja de la fichas del trabajador y de variaciones del mes

ENTRADA:
- El labtraba ha de venir leido
- enMes: Mes
- enAny: A¤o

* si en lugar de en un mes quiero mirar desde-hasta un dia determinado
- aFechaIniParam: si viene con algo, es el dia de inicio periodo
- aFechaFinParam: lo mismo, fin de periodo

SALIDA:

- enSwAlta = 0 - no ha estado de alta
- enSwAlta = 1 - si esta de alta
- Si enSwAlta = 1 -> enDiasAlta son los dias del mes NATURALES en alta
- fFechaIniPer -> Fechas de inicio y Fin de periodo, teniendo en cuenta
- fFechaFinPer -> las fechas de alta y baja del trabajador
*/

     aFechaAlta = #labtraba#36;
     |QBF aFechaAlta;
     |SI aFechaAlta = "";
          enSwAlta = 0;
          enDiasAlta = 0;
          |ACABA;
     |FINSI;

     fFechaAlta = aFechaAlta;
     aFechaBaja = "";

     aAlfa = #labtraba#37;
     |QBF aAlfa;
     |SI aAlfa ! ""; |Y aAlfa ! "..........";
          aFechaBaja = aAlfa;
     |FINSI;

     |SALVA_FICHA #nomvarca;
     |ABRE #nomvarca;
     #nomvarca#0 = #labtraba#0;
     #nomvarca#1 = #labtraba#1;
     #nomvarca#2 = enMes;
     #nomvarca#3 = 0;
     |LEE 000#nomvarca.=;
     |SI FSalida = 0;
          aAlfa = #nomvarca#16;
          |QBF aAlfa;
          |SI aAlfa ! ""; |Y aAlfa ! "..........";
               aFechaBaja = aAlfa;
          |FINSI;
     |FINSI;
     |CIERRA #nomvarca;
     |REPON_FICHA #nomvarca;

     |SI aFechaBaja = ""; |O aFechaBaja = "..........";
          aFechaBaja = "31.12.2099";
     |FINSI;

     fFechaBaja = aFechaBaja;

     |SI aFechaIniParam = ""; |Y aFechaFinParam = "";
          || no me vienen dadas las fechas, es comprobar en un mes
          nMes = enMes;
          nAny = enAny;
          |HAZ topemes_plb;

          aMes = nMes; |QBF aMes; aMes = ("00" + aMes) % -102;
          aAny = nAny;

          aAlfa = "01" + "." + aMes + "." + aAny;
          fFechaIniPer = aAlfa;

          aAlfa1 = nTopemes;
          aAlfa = aAlfa1 + "." + aMes + "." + aAny;
          fFechaFinPer = aAlfa;
     |SINO;
          fFechaIniPer = aFechaIniParam;
          fFechaFinPer = aFechaFinParam;
     |FINSI;

     nNume1 = fFechaIniPer;
     nNume2 = fFechaFinPer;
     nNume3 = fFechaAlta;
     nNume4 = fFechaBaja;

     |SI nNume3 > nNume1;
          nNume1 = nNume3;
          fFechaIniPer = fFechaAlta;
     |FINSI;
     |SI nNume4 < nNume2;
          nNume2 = nNume4;
          fFechaFinPer = fFechaBaja;
     |FINSI;

     |SI fFechaAlta [ fFechaFinPer; |Y fFechaBaja ] fFechaIniPer;
          enSwAlta = 1;
          enDiasAlta = nNume2 - nNume1 + 1;
     |SINO;
          enSwAlta = 0;
     |FINSI;
|FPRC;

|PROCESO labtraba2_plb;
     |SI #labtraba#247 = 521; |O #labtraba#42 = "H";
          |ACABA;   || autonomos no los tendre en cuenta
     |FINSI;

     |HAZ CompAltaTrab_plb;

     |SI enSwAlta = 0;
          || tengo en cuenta solo los trabajadores de alta en el
          || periodo que busco, por si hubiera restos extra¤os de otro
          || regimen
          nRegProv = #labtraba#247;
          |ACABA;
     |FINSI;

     |SI #labtraba#42 = "A"; |O #labtraba#42 = "E";
          #labtraba#247 = "0163";
     |FINSI;

     |SI nReg1 = 0;
          nReg1 = #labtraba#247;
          nReg1Cont = nReg1Cont + 1;
     |SINO;
          |SI #labtraba#247 = nReg1;
               nReg1Cont = nReg1Cont + 1;
          |SINO;
               |SI nReg2 = 0;
                    nReg2 = #labtraba#247;
                    nReg2Cont = nReg2Cont + 1;
               |SINO;
                    |SI #labtraba#247 = nReg2;
                         nReg2Cont = nReg2Cont + 1;
                    |FINSI;
               |FINSI;
          |FINSI;
     |FINSI;

     nNume = nReg1Cont + nReg2Cont;
     |SI nNume = 5;
          |SI nReg1Cont > nReg2Cont;
               aAlfa = nReg1;
          |SINO;
               aAlfa = nReg2;
          |FINSI;
          |QBF aAlfa;
          aAlfa = ("0000" + aAlfa) % -104;
          eaRegimen = aAlfa;
          |ERROR10;
     |SINO;
          nNume = #labtraba#247;
          aAlfa = nNume;
          aAlfa = ("0000" + aAlfa) % -104;
          eaRegimen = aAlfa;
     |FINSI;

     |SI eaRegimen = "0613";
          eaRegimen = "0163";
     |FINSI;
|FPRC;

|DEFBUCLE labtraba2_plb;
     #labtraba#1;
     77;
     enEmp, 00001, enCen;
     enEmp, 99999, enCen;
     ;
     NULL, labtraba2_plb;
|FIN;

|PROCESO RegimenCCC_plb;
     /*
     Devuelve el Regimen de coticzacion de un CCC basandose en el Regimen
     que tienen los trabajadores

     ENTRADA:
     - enEmp - La empresa
     - enCen - El Centro
     - enAny - enMes: Periodo que quiero consultar

     SALIDA:

     - eaRegimen - El Regimen
     */

     |SALVA_FICHA #labtraba;

     nReg1 = 0;
     nReg1Cont = 0;
     nReg2 = 0;
     nReg2Cont = 0;

     |BUCLE labtraba2_plb;

     |REPON_FICHA #labtraba;

     |SI eaRegimen = "";
          aAlfa = nRegProv;
          aAlfa = ("0000" + aAlfa) % -104;
          eaRegimen = aAlfa;
     |FINSI;
|FPRC;

|PROCESO BuscaPorNaf_plb;
     #labempre#0 = #labtraba#0;
     |LEE 000#labempre.=;

     #labcentr#0 = #labtraba#0;
     #labcentr#1 = #labtraba#77;
     |LEE 000#labcentr.=;
     |SI FSalida = 0;
          aAlfa1 = #labcentr#10;
          |QBF aAlfa1;
          |SI aAlfa1 = "";
               aAlfa1 = #labempre#13;
          |FINSI;
     |FINSI;

     |SI #labtraba#45 = "421"; |O #labtraba#45 = "422";
          #nomcenes#0 = #labtraba#0;
          #nomcenes#13 = #labtraba#77;
          #nomcenes#1 = "87";
          |LEE 000#nomcenes.=;
          |SI FSalida = 0;
               aAlfa1 = #nomcenes#10;
          |FINSI;
     |FINSI;

     |QBF aAlfa1;
     aAlfa2 = eaCCC % 511;
     |SI aAlfa1 = aAlfa2;
          || el CCC al que pertenece el trabajador es el mismo que me viene
          || en el XML, ahora voy a ver si esta de alta en este periodo

          |HAZ CompAltaTrab_plb;
          |SI enSwAlta = 1;
               nTrabAlta = nTrabAlta + 1;
               |SI nEmp1 = 0;
                    nEmp1 = #labtraba#0;
                    nTra1 = #labtraba#1;
               |SINO;
                    |SI nEmp2 = 0;
                         nEmp2 = #labtraba#0;
                         nTra2 = #labtraba#1;
                    |SINO;
                         |SI nEmp3 = 0;
                              nEmp3 = #labtraba#0;
                              nTra3 = #labtraba#1;
                         |SINO;
                              |SI nEmp4 = 0;
                                   nEmp4 = #labtraba#0;
                                   nTra4 = #labtraba#1;
                              |SINO;
                                   |SI nEmp5 = 0;
                                        nEmp5 = #labtraba#0;
                                        nTra5 = #labtraba#1;
                                   |FINSI;
                              |FINSI;
                         |FINSI;
                    |FINSI;
               |FINSI;
          |SINO;
               nTra0 = #labtraba#1;
          |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE BuscaPorNaf;
     #labtraba#1;
     75;
     nDesdeEmp, 00001, eaNafBusca;
     nHastaEmp, 99999, eaNafBusca;
     ;
     NULL, BuscaPorNaf_plb;
|FIN;

|PROCESO BuscaNaf_plb;
/*
     Proceso para busqueda de codigo de trabajador por NAF, esta repetido
     por los procesos del creta, ir quitando cuando se vea conveniente

     ENTRADA:
     - eaCCC: el CCC del centro del trabajador (con el Regimen delante)
     - eaNafBusca: el NAF a buscar
     - enAny - enMes: Periodo que quiero consultar
     - enEmpNaf - con valor si quiero buscar solo en ese codigo de empresa

     SALIDA:
     - enTraPorNaf: el codigo de trabajador encontrado
*/


     |ABRE #labempre;
     |ABRE #labcentr;
     |ABRE #nomcenes;

     eaNafBusca = (eaNafBusca + "                  ") % 115;
     |SI enEmpNaf = 0;
          nDesdeEmp = 00001;
          nHastaEmp = 99999;
     |SINO;
          nDesdeEmp = enEmpNaf;
          nHastaEmp = enEmpNaf;
     |FINSI;
     nEmp1 = 0; nEmp2 = 0; nEmp3 = 0; nEmp4 = 0; nEmp5 = 0;
     nTra1 = 0; nTra2 = 0; nTra3 = 0; nTra4 = 0; nTra5 = 0;
     nTra0 = 0;

     |BUCLE BuscaPorNaf;
     |SI nTra1 ! 0;
          enTraPorNaf = nTra1;
     |SINO;
          enTraPorNaf = nTra0;
     |FINSI;


     |CIERRA #nomcenes;
     |CIERRA #labcentr;
     |CIERRA #labempre;
|FPRC;

|PROCESO RefCreta_plb;
     /*
     Proceso para crear la referencia del Creta, para identificar el fichero

     ENTRADA:
     - eaTipoXML - El tipo de XML que se va a crear
     - enMes - el mes
     - enAny - el a¤o
     - enTipo - el Tipo de Calculo

     SALIDA:
     - eaReferencia

     */

     |DDEFECTO #cretam00;
     |ABRE #cretam00; |LEE 000#cretam00.p; |CIERRA #cretam00;
     nFactor = #cretam00#11;

     |DDEFECTO #cretam05;
     |ABRE #cretam05;

     || clave "BASES"

     aAlfa0 = eaTipoXML;
     #cretam05#0 = aAlfa0;

     || a¤o

     aAlfa = enAny;
     |QBF aAlfa;
     aAlfa = aAlfa % -102;

     aAlfa1 = aAlfa;
     #cretam05#1 = aAlfa1;

     || MES

     aAlfa = enMes;
     |QBF aAlfa;
     aAlfa = ("00" + aAlfa) % -102;

     aAlfa2 = aAlfa;
     #cretam05#2 = aAlfa2;

     aAlfa3 = "999";
     #cretam05#3 = aAlfa3;

     |LEE 000#cretam05.];
     |SI FSalida = 0;
          |SI #cretam05#0 ! aAlfa0; |O #cretam05#1 ! aAlfa1; |O #cretam05#2 ! aAlfa2;
               |LEE 000#cretam05.a;
          |FINSI;
     |SINO;
          |LEE 000#cretam05.u;
     |FINSI;

     |SI FSalida = 0;
     |Y #cretam05#0 = aAlfa0; |Y #cretam05#1 = aAlfa1; |Y #cretam05#2 = aAlfa2;
          nContadorRef = #cretam05#3;
          |DDEFECTO #cretam05;
          #cretam05#0 = aAlfa0;
          #cretam05#1 = aAlfa1;
          #cretam05#2 = aAlfa2;

          nContadorRef = nContadorRef + 1;
          aAlfa = nContadorRef;
          |QBF aAlfa;
          aAlfa = ("000" + aAlfa) % -103;
          #cretam05#3 = aAlfa;
     |SINO;
          || es el primero !!!
          |DDEFECTO #cretam05;
          #cretam05#0 = aAlfa0;
          #cretam05#1 = aAlfa1;
          #cretam05#2 = aAlfa2;
          nContadorRef = 001;
          nContadorRef = nContadorRef + nFactor;

          aAlfa = nContadorRef;
          |QBF aAlfa;
          aAlfa = ("000" + aAlfa) % -103;
          #cretam05#3 = aAlfa;
     |FINSI;

     eaReferencia = #cretam05#0 + #cretam05#1 +  #cretam05#2 + #cretam05#3;

     #cretam05#4 = eaReferencia;
     #cretam05#5 = efFecha;
     #cretam05#6 = eaHora;
     #cretam05#7 = eaFichero;

     |SI eaTipoXML = "B";
          #cretam05#8 = "B";
          #cretam05#10 = "Fichero de Bases";
     |SINO;
          #cretam05#8 = "S";
          |SI eaTipoXML = "T";
               #cretam05#9 = "1";
               #cretam05#10 = "Solicitud Trabajadores y Tramos";
          |FINSI;
          |SI eaTipoXML = "R";
               #cretam05#9 = "2";
               #cretam05#10 = "Solicitud Borrador";
          |FINSI;
          |SI eaTipoXML = "C";
               #cretam05#9 = "4";
               #cretam05#10 = "Solicitud Calculos";
          |FINSI;
          |SI eaTipoXML = "F";
               #cretam05#9 = "3";
               #cretam05#10 = "Solicitud Confirmacion";
          |FINSI;
     |FINSI;

     #cretam05#15 = enTipo;

     |GRABA 020#cretam05.n;
     |LIBERA #cretam05;

     |CIERRA #cretam05;
|FPRC;

|PROCESO LeeReferencia_plb;
     /*
     Proceso para leer la referencia del Creta, para devolver el tipo de
     calculos, se usa en atrasos

     ENTRADA:
     - eaReferencia

     SALIDA:
     - nTipo - el Tipo de Calculo

     */

     |ABRE #cretam05;

     || aqui se atualizaran los datos de la referencia asociada

     #cretam05#0 = eaReferencia % 101;
     #cretam05#1 = eaReferencia % 202;
     #cretam05#2 = eaReferencia % 402;
     #cretam05#3 = eaReferencia % 603;
     |LEE 101#cretam05.=;
     |SI FSalida = 0;
          nTipo = #cretam05#15;
     |SINO;
          nTipo = 21;
          aAlfa = "    ATENCION: Referencia " + eaReferencia + " no encontrada. ";
          aAlfa = aAlfa + "Tipo de atrasos desconocido.";
          |MENAV aAlfa;
     |FINSI;
     |LIBERA #cretam05;

     |CIERRA #cretam05;
|FPRC;

/****************************************************************************/
/***********   CALCULO DEL PROMEDIO EN TRABAJADORES A TP  *******************/
/****************************************************************************/

|PROCESO BuscaBaseHis;
     #nomhicca#0 = #labtraba#0;
     #nomhicca#1 = #labtraba#1;
     #nomhicca#66 = nAnyHis;
     #nomhicca#2 = nMesHis;
     #nomhicca#3 = 0;
     |LEE 000#nomhicca.=;
     |SI FSalida = 0;
          nBase = nBase + #nomhicca#39 + #nomhicca#159;
          nBaseHoras = nBaseHoras + #nomhicca#41 + #nomhicca#42;
          nDias = nDias + #nomhicca#8;
     |FINSI;
|FPRC;

|PROCESO ElMesHis;
     nMesHis = nMesHis - 1;
     |SI nMesHis = 0
           nMesHis = 12;
           nAnyHis = nAnyHis - 1;
     |SINO;
           nAnyHis = nAnyHis;
     |FINSI;
|FPRC;

|DEFBUCLE labtraba;
     #labtraba#5;
     ;
     nEmp, aNif;
     nEmp, aNif;
     ;
     NULL, BuscaBaseHis;
|FIN;

|PROCESO labtraba3_plb;
     nMesHis = nElMes;
     nAnyHis = nElAny - 2000;

     |HAZ ElMesHis;
     |BUCLE labtraba;

     |HAZ ElMesHis;
     |BUCLE labtraba;

     |HAZ ElMesHis;
     |BUCLE labtraba;

     promE_AUTO = nBase / nDias;
     promA_AUTO = (nBase - nBaseHoras) / nDias;
|FPRC;

|PROCESO PromTP_plb;
     |ABRE #labtraba;
     |ABRE #nomhicca;
     #labtraba#0 = nEmpAUTO;
     #labtraba#1 = nTraAUTO;
     |LEE 000#labtraba.=;
     |SI FSalida = 0;
          nEmp = #labtraba#0;
          nTra = #labtraba#1;
          aNif = #labtraba#7;

          |HAZ labtraba3_plb;

     |FINSI;
     |CIERRA #nomhicca;
     |CIERRA #labtraba;
|FPRC;

|PROCESO sustituye_plb;
     || eaCadena: contiene la cadena donde vamos a buscar
     || eaCaracter1: contiene el caracter que queremos sustituir
     || eaCaracter2: contiene el caracter por el que lo vamos a sustituir
     || eaCadenaSal: contiene el resultado

     eaCadenaSal = "";
     aLongitud = eaCadena % 0;
     nLongitud = aLongitud;

     |QBF eaCadena;
     FSalida = 1;
     |PARA; |SI FSalida ! 0; |HACIENDO;
          aAlfa = eaCadena - eaCaracter1;
          |SI FSalida ! 0;
               aAlfa = FSalida;
               |QBF aAlfa;
               aAlfa = ("0000" + aAlfa) % -104;
               aAlfa1 = aAlfa % 102;
               aAlfa2 = aAlfa % 302;
               nNume1 = aAlfa1;
               nNume2 = aAlfa2;
               nNume2 = nNume2 + nNume1;

               nNume1 = nNume1 - 1;
               nNume1 = 100 + nNume1;

               nNume2 = (nNume2 * 100) + (nLongitud - nNume2 + 1);

               aAlfa1 = (eaCadena % nNume1);
               aAlfa2 = (eaCadena % nNume2);
               aAlfa1 = aAlfa1 % -101;
               aAlfa2 = aAlfa2 % 101;
               eaCadenaSal = (eaCadena % nNume1) + eaCaracter2 + (eaCadena % nNume2);

               eaCadena = eaCadenaSal;
          |SINO;
               eaCadenaSal = eaCadena;
               |SAL;
          |FINSI;
     |SIGUE;
|FPRC;

|PROCESO VersionNom_plb;
     |DBASS aBase;
     |QBF aBase;
     aMenu = aBase + "laboral/laboral.men";
     aArchivo = aMenu;

     |XABRE "U", aArchivo, nCanal;
     |SI FSalida ] 0;
          |XLEE nCanal, 100, aLinea;
     |FINSI;
     |XCIERRA nCanal;

     aLinea = aLinea - "#:PI Nominas y S.Social ";
     aVersion = aLinea;
|FPRC;

|PROCESO ActivaSN_plb;
     enSwActiva = 1;

     aFabre = "";
     |DIRECTORIO aFabre;
     aFabre = "rt  " + aFabre + "integral/bin/dsam0000.tab";
     |FABRE aFabre;
     |SI FSalida ] 0;
          |FCIERRA FSalida;
          |DBASS aBase;  |QBF aBase;
          aMas  = aBase + "integral/def/dsam0000.mas";
          aFich = aBase + "integral/fich/";

          |CARGA_DEF aMas, copiadef@;
          |SI FSalida ! 0;
              aAlfa = "0000 No puedo cargar el Def " + aMas;
              |MENAV aAlfa;
              |ACABA;
          |FINSI;

          |ABRE #500;
          #500#0 = enEmpresa;
          |LEE 000#500=;
          |SI FSalida = 0;
               |SI #500#85 = "I";
                    enSwActiva = 0;  || empresa no activa
               |FINSI;
          |FINSI;
          |CIERRA #500;
          |DESCARGA_DEF #copiadef@;
     |FINSI;
|FPRC;

/****************************************************************************/
/************  PROCESOS PARA LA BUSQUEDA DEL TIPO DE ATRASOS ****************/
/********   SEGUN A¤O EXIGIBILIDAD, A¤O CALCULO Y Y FECHA DE CONTROL   ******/
/****************************************************************************/

|PROCESO BuscaTipoLibreMen;
     swEncontrado = 1;
     |ERROR10;
|FPRC;

|DEFBUCLE BuscaTipoLibreMen;
     #nomcalac#1;
     ;
     enEmpAtr, 00001, 01, nTipo;
     enEmpAtr, 99999, 12, nTipo;
     ;
     NULL, BuscaTipoLibreMen;
|FIN;

|PROCESO buscaAtMes;
     nTipoProvMen = #nomcalac#3;
     |ERROR10;
|FPRC;

|DEFBUCLE buscaAtMes;
     #nomcalac#1;
     205;
     enEmpAtr, 00001, 01, nTipo, enAnyExig;
     enEmpAtr, 99999, 12, nTipo, enAnyExig;
     ;
     NULL, buscaAtMes;
|FIN;

|PROCESO buscaAtHis;
     nTipoProvHis = #nomhicca#3;
     |ERROR10;
|FPRC;

|DEFBUCLE buscaAtHis;
     #nomhicca#1;
     ;
     enEmpAtr, 00001, nAnyHis, 01, nTipo;
     enEmpAtr, 99999, nAnyHis, 12, nTipo;
     ;
     NULL, buscaAtHis;
|FIN;

|PROCESO nomcalac_at;
     enTipoAtrasos = #nomcalac#3;
     |ERROR10;
|FPRC;

|DEFBUCLE nomcalac_at;
     #nomcalac#1;
     205, 184, 203, 204;
     enEmpAtr, 00001, 01, 06, enAnyExig, enAnyCalc, enMesCont, enAnyCont;
     enEmpAtr, 99999, 12, 20, enAnyExig, enAnyCalc, enMesCont, enAnyCont;
     ;
     NULL, nomcalac_at;
|FIN;

|PROCESO nomhicca_at;
     enTipoAtrasos = #nomhicca#3;
     |ERROR10;
|FPRC;

|DEFBUCLE nomhicca_at;
     #nomhicca#1;
     226, 245, 246;
     enEmpAtr, 00001, nAnyHis, 01, 06, enAnyCalc, enMesCont, enAnyCont;
     enEmpAtr, 99999, nAnyHis, 12, 20, enAnyCalc, enMesCont, enAnyCont;
     ;
     NULL, nomhicca_at;
|FIN;

|PROCESO TipoAtrasos_plb;

/*
     Variables entrada:
     enEmpAtr = empresa de la que calculo atrasos
     enAnyExig = a¤o de exigibilidad (en el que se actualizaran al historico)
     enAnyCalc = a¤o de calculo
     enMesCont = mes de la fecha de control
     enAnyCont = a¤o de la fecha de control

     Variable salida:
     enTipoAtrasos = el tipo asignado
*/

     nAnyHis = enAnyExig - 2000;

     || primero busco si tengo en historico actualizado algo con este
     || a¤o exigibilidad, a¤o de calculo, fecha de control
     enTipoAtrasos = 0;
     |BUCLE nomhicca_at;

     |SI enTipoAtrasos = 0;
          || no tengo nada en historico actualizado con este a¤o exigibilidad,
          || a¤o de calculo, fecha de control
          || busco entonces en mensual de atrasos

          |BUCLE nomcalac_at;
     |FINSI;

     |SI enTipoAtrasos = 0;
          || tampoco tengo nada en el mensual, llega la hora de asignar el
          || tipo con el que se van a grabar estos atrasos

          || primero busco los atrasos en el a¤o de exigibilidad que pudiera
          || haber ya actualizados en el historico

          nTipoProvHis = 0;
          |PARA nTipo = 06; |SI nTipo [ 20; |HACIENDO nTipo = nTipo + 1;
               |BUCLE buscaAtHis;
          |SIGUE;

          || si el tipo sigue siendo cero, busco por ultimo en el mensual
          |SI enTipoAtrasos = 0;
               |PARA nTipo = 06; |SI nTipo [ 20; |HACIENDO nTipo = nTipo + 1;
                    |BUCLE buscaAtMes;
               |SIGUE;
          |FINSI;

          || si he encontrado ya algun tipo que se actualizara en este a¤o
          || uso como tipo el ultimo encontrado mas uno; si no, el tipo 06

          |SI nTipoProvMen = 0; |Y nTipoProvHis = 0;
               enTipoAtrasos = 06;
          |SINO;
               |SI nTipoProvMen = 0; |Y nTipoProvHis ! 0;
                    enTipoAtrasos = nTipoProvHis + 1;
               |FINSI;
               |SI nTipoProvMen ! 0;
                    enTipoAtrasos = nTipoProvMen + 1;
               |FINSI;
          |FINSI;

          || la ultima posibilidad, es que no haya encontrado este tipo de
          || atrasos por ningun sitio, pero que se est‚ calculando a la vez
          || y sin actualizar, atrasos de varios a¤os con distinta fecha de
          || calculo o exigibilidad
          || en este caso si ya hay atrasos en el mensual, los machacaria
          || con el tipo 6 de nuevo, asi que voy a buscar "en general" si hay
          || atrasos calculados del tipo asignado hasta que encuentre uno
          || libre

          swEncontrado = 0;
          nTipo = enTipoAtrasos;
          |BUCLE BuscaTipoLibreMen;
          |SI swEncontrado = 1;
               enTipoAtrasos = 0;
               |PARA nTipo = 06; |SI nTipo [ 20; |HACIENDO nTipo = nTipo + 1;
                    swEncontrado = 0;
                    |BUCLE BuscaTipoLibreMen;
                    |SI swEncontrado = 0;
                         enTipoAtrasos = nTipo;
                         |SAL;
                    |FINSI;
               |SIGUE;
          |FINSI;
     |FINSI;
|FPRC;
