|FICHEROS;
     vipe0001 #0;
     vipe0002 #2;
     vipe0003 #3,MANTE;
     vipe1001;

     labtraba #10;
     labcentr;
     nomhicca;
|FIN;

|VARIABLES;
     aDia = "";
     nDia = 0;
     aMes = "";
     nMes = 0;
     aFecha = "";
     fFecha = @;
     fAntFechaD = @;
     fAntFechaH = @;
     aAny = "";
     nAny = 0;

     aAnyD = "";
     nAnyD = 0;
     aAnyH = "";
     nAnyH = 0;
     aMesD = "";
     nMesD = 0;
     aMesH = "";
     nMesH = 0;
     aDiaD = "";
     nDiaD = 0;
     aDiaH = "";
     nDiaH = 0;
     aAlfa = "";
     aAlfa1 = "";
     aAlfa2 = "";
     aAlfa3 = "";
     alfa1 = "";
     alfa2 = "";
     fech1 = @;
     fech2 = @;
     nNume = 0;
     nume1 = 0;
     nume2 = 0;
     nume3 = 0;
     sdia = 0;
     smas = 0;
     sdife = 0;
     srest = 0;

     verrfec = "0000ATENCION !! Fecha erronea o formato incorrecto ";
     vdia1 = "";
     vme1 = "";
     vany1 = "";
     vdia = 0;
     vme = 0;
     vany = 0;
     vtope = 0;
     vsw = 0;
     vfec = "";
     vblanco = "          ";
     vpuntos = "..........";
     v = 0;
     mod = 0;

     swVengoDe = 0;
     nFechaDesde = 0;
     aFechaHasta = "";
     swCreaNueva = 0;
     nLinea = 0;
     nCampo = 0;
     nLineaAct = 0;
     swEstoy = 0;
     swCuenta = 0;
     nModo = 0;
     numer = 0;
     anynum = 0;
     bisiesto = 0;
     mesnum = 0;
     topemes = 0;
     FEstado = 0;
     swUltSemana = 0;
     nEntero = 0;
     nDeci = 0;
     aNif = "";
     aAntNif = "";
     aAntCod = "";
     swNoSigas = 0;
     nHorasRed = 0;
     nNume1 = 0;
     nNume2 = 0;
     nNume3 = 0;
|FIN;

/*****************   PROCESOS DESDE LA PANTALLA *****************************/

|PROCESO MaxCen;
     #labcentr#0 = #0#0;
     #labcentr#1 = 99;
|FPRC;

|PROCESO MinCen;
     #labcentr#0 = #0#0;
     #labcentr#1 = 01;
|FPRC;

|PROCESO Centros; |TIPO 6;
     |SI FSalida = 13;
          |ABRE #labcentr;
          |CONSULTA_F_CLAVE #1#labcentr, MinCen, MaxCen;
          |SI FSalida = 0;
               #0Campo = #labcentr#1;
               |PINTA #0Campo;
               #0#8 = #labcentr#2;
               |PINTA #0#8;
          |SINO;
               |ERROR;
          |FINSI;
     |SINO;
          |ABRE #labcentr;
          #labcentr#0 = #0#0;
          #labcentr#1 = #0#1;
          |LEE 000#labcentr.=;
          |SI FSalida = 0;
               #0#8 = #labcentr#2;
               |PINTA #0#8;
          |FINSI;
     |FINSI;
     |CIERRA #labcentr;
|FPRC;

|PROCESO PintaCentro;

|FPRC;

|PROCESO ulti_dia;  || halla el ultimo dia del mes
     numer = anynum $ 4;
     |SI numer = 0;
             bisiesto = 1;
     |SINO;
             bisiesto = 0;
     |FINSI;
     |SI mesnum =  1;  topemes = 31;            |FINSI;
     |SI mesnum =  2;  topemes = 28 + bisiesto; |FINSI;
     |SI mesnum =  3;  topemes = 31;            |FINSI;
     |SI mesnum =  4;  topemes = 30;            |FINSI;
     |SI mesnum =  5;  topemes = 31;            |FINSI;
     |SI mesnum =  6;  topemes = 30;            |FINSI;
     |SI mesnum =  7;  topemes = 31;            |FINSI;
     |SI mesnum =  8;  topemes = 31;            |FINSI;
     |SI mesnum =  9;  topemes = 30;            |FINSI;
     |SI mesnum = 10;  topemes = 31;            |FINSI;
     |SI mesnum = 11;  topemes = 30;            |FINSI;
     |SI mesnum = 12;  topemes = 31;            |FINSI;
|FPRC;

|PROCESO CorrectoSN;
     |SI #0#7 = "S";
          |SI #0#3 = 0;
               |MENAV "    Debe realizar una seleccion por fecha o por semana";
               #0#7 = "N";
               |PINTA #0#7;
          |FINSI;
     |FINSI;
     |SI #0#7 = "N";
          |POSICIONATE #0#2;
     |FINSI;
|FPRC;

|PROCESO BlancoFecha;
     #0#4 = "";
     |PINTA #0#4;
|FPRC;

|PROCESO fechacon1; |TIPO 0;
     vfec = #0Campo; vsw = 0;
     |SI vfec ! vblanco; |Y vfec ! vpuntos;
          vdia1 = vfec % A102;
          vme1 = vfec % A402;
          vany1 = vfec % A704;
          vdia = vdia1; vme = vme1; vany = vany1;
          |SI vdia ! 0; |Y vme ! 0; |Y vany > 1900;
               |SI vme [ 12; |Y vme ] 1;
                    |SI vme = 1; |O vme = 3; |O vme = 5;|O vme = 7;
                    |O vme = 8; |O vme = 10; |O vme = 12;
                         vtope = 31;
                    |SINO;
                         |SI vme = 2;
                              mod = vany $ 4;
                              |SI mod = 0;
                                   vtope = 29;
                              |SINO;
                                   vtope = 28;
                              |FINSI;
                         |SINO;
                              vtope = 30;
                         |FINSI;
                    |FINSI;
                    |SI vdia [ vtope;
                         vsw = 1;
                    |FINSI;
               |FINSI;
          |FINSI;
     |SINO;
          vsw = 1;
     |FINSI;
     |SI vsw = 0;
          |MENAV verrfec;
          |ERROR;
     |FINSI;
|FPRC;

|PROCESO PorDia;
     aAlfa = #0#4;
     |QBF aAlfa;
     |SI aAlfa ! "";
          swVengoDe = 2;
          aDia = aAlfa % 102;
          aMes = aAlfa % 402;
          aAny = aAlfa % -104;
          nDia = aDia;
          nMes = aMes;
          nAny = aAny;
          #0#2 = nAny;
          |PINTA #0#2;
          |HAZ mirasem;
     |FINSI;
|FPRC;

|PROCESO PorDia7; |TIPO 7;
     |HAZ PorDia;
|FPRC;

|PROCESO PorSemana;
     aAlfa = #0#3;
     |QBF aAlfa;
     |SI aAlfa ! "";
          nNume = aAlfa;
          |SI nNume ! 0;
               swVengoDe = 1;
               |HAZ deshas;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO mirasem; |TIPO 7;
     alfa1 = #0#2;
     alfa1 = "01.01." + alfa1;
     fech1 = alfa1;            || primero saca el dia de la semana
     fech2 = fech1 % 1;        ||/que fue el uno de enero
     alfa1 = fech2 % 11;

     |SI alfa1 = "Lunes";     sdia=1; smas=1; |FINSI;
     |SI alfa1 = "Martes";    sdia=7; smas=1; |FINSI;
     |SI alfa1 = "Miercoles"; sdia=6; smas=1; |FINSI;
     |SI alfa1 = "Jueves";    sdia=5; smas=1; |FINSI; ||saca el dia del mes que fue
     |SI alfa1 = "Viernes";   sdia=4; smas=1; |FINSI; ||el primer lunes del a¤o
     |SI alfa1 = "Sabado";    sdia=3; smas=1; |FINSI;
     |SI alfa1 = "Domingo";   sdia=2; smas=1; |FINSI;

     alfa1 = ("00" + sdia) % -102;  || construye el primer lunes del a¤o
     |SI swVengoDe ! 2;
          fech1 = ~;                     ||/como fecha inicial de calculo
     |SINO;
          fech1 = #0#4;
     |FINSI;

     alfa2 = fech1 % -104;
     alfa1 = alfa1 + ".01." + alfa2;
     fech1 = alfa1;
     nume1 = fech1;                 || calcula dias hasta primer lunes a¤o

     |SI swVengoDe ! 2;
          fech1 = ~;                     ||/como fecha inicial de calculo
     |SINO;
          fech1 = #0#4;
     |FINSI;
     nume2 = fech1;

     sdife = nume2 - nume1;         || calcula la diferencia desde el primer
     |SI sdife ] 0;
          srest = sdife $ 7;           || lunes hasta la fecha actual,la redondea,
          sdife = sdife + (7 - srest); || la divide por 7 y le suma 1 por la
          nNume = (sdife / 7) + smas;|| primera semana (en su caso !!!)
          #0#3 = nNume;
     |SINO;
          nNume = 1;
          #0#3 = nNume;
     |FINSI;
/*
     |ABRE #semcntcu;
     |LEE 000#semcntcu.p;
     |CIERRA #semcntcu;

     #0Campo = #0Campo - #semcntcu#3;
*/

     |SI nNume = 0; nNume = 52; |FINSI;
     #0#3 = nNume;

     |PINTA #0#3;

     |HAZ deshas;
|FPRC;

|PROCESO deshas; |TIPO 7;   || calcula segun semana, desde dia hasta dia
     nume2 = #0#3 - smas;
     nume2 = (nume2 * 7) + (sdia - 1);   || dias hasta
     nume1 = nume2 - 6;                  || dias desde

     nume3 = #0#2;
     nume3 = nume3 - 1;
     alfa1 = "31.12." + nume3;    || saca los dias que hay hasta el 31.12
     fech1 = alfa1;
     nume3 = fech1;

     nume1 = nume1 + nume3;
     fech1 = nume1;         || fecha desde
     #0#5 = fech1;

     nume2 = nume2 + nume3;
     fech1 = nume2;         || fecha hasta
     #0#6 = fech1;

     aAlfa = #0#5;
     aMesD = aAlfa % 402;
     nMesD = aMesD;
     aAnyD = aAlfa % -104
     nAnyD = aAnyD;

     aAlfa = #0#6;
     aMesH = aAlfa % 402;
     nMesH = aMesH;
     aAnyH = aAlfa % -104
     nAnyH = aAnyH;

     |SI nMesD ! nMesH;
          |SI swVengoDe = 2;
               aAlfa = #0#4;
               aMes = aAlfa % 402;
               nMes = aMes;
               aAny = aAlfa % -104;
               nAny = aAny;

               |SI nMesD = nMes;
                    mesnum = nMesD;
                    anynum = aAnyD;
                    |HAZ ulti_dia;

                    aAlfa = topemes;
                    aAlfa = aAlfa + "." + aMesD + "." + aAnyD;
                    #0#6 = aAlfa;
               |FINSI;
               |SI nMesH = nMes;
                    aAlfa = "01" + "." + aMesH + "." + aAnyH;
                    #0#5 = aAlfa;
               |FINSI;
          |SINO;
               mesnum = nMesD;
               anynum = aAnyD;
               |HAZ ulti_dia;

               aAlfa = topemes;
               aAlfa = aAlfa + "." + aMesD + "." + aAnyD;
               #0#6 = aAlfa;
          |FINSI;
     |FINSI;

     |PINTA #0#5;
     |PINTA #0#6;
|FPRC;

|PROCESO AnyDefecto; |TIPO 7;
     fFecha = ~;
     aFecha = fFecha;
     aAny = aFecha % -104;
     nAny = aAny;
     #0#2 = nAny;
     |PINTA #0#2;
|FPRC;

/*****************     FIN PROCESOS DESDE LA PANTALLA     *******************/

|PROCESO SinAlta; |TIPO 6;
     || aqui por si se habia puesto antes en No Modificable
     |CAMPO_MODIFICA #vipe0003#9, 1, "S";

     nModo = 0;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 1;
          |ERROR;
     |FINSI;
|FPRC;

|PROCESO Consulta; |TIPO 11;
     |SI FSalida = 13;
          |SI swCuenta = 0;
               |CONSULTA_CLAVE #1#vipe0003;
               swCuenta = 1;
          |SINO;
               swCuenta = 0;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO tipo5; |TIPO 5;
     |PTEC 809;
|FPRC;

|PROCESO CompFinIT;
     aAlfa = #vipe0003#5;     || fecha inicio horas
     aAlfa = aAlfa % 102;
     nNume = aAlfa;
     |SI #vipe0003#7 = "N"; |Y nNume = 1;    || horas normales
          #labtraba#0 = #vipe0003#0;    || empresa
          #labtraba#1 = #vipe0003#1;    || trabajador
          |LEE 000#labtraba.=;

          |SI #labtraba#89 ! 0; |O #labtraba#92 ! 0;
               aAlfa = "    ATENCION. El trabajador estaba de baja el ultimo día del";
               aAlfa = aAlfa + " mes anterior.";
               |MENAV aAlfa;
               aAlfa = "    Si continúa el trabajador se dará de alta automáticamente";
               aAlfa = aAlfa + " el primer día del mes actual."
               |MENAV aAlfa;
               aAlfa = "    NEstá seguro de que desea continuar?";
               |CONFI aAlfa;
               |SI FSalida = 0;
                    #labtraba#60 = "";
                    #labtraba#123 = "N";
                    #labtraba#125 = "          ";
                    #labtraba#89 = 0;
                    #labtraba#92 = 0;
                    |GRABA 020#labtraba.a;

                    |LIBERA #labtraba;

                    |ABRE #nomhicca;
                    #nomhicca#0 = #vipe0003#0;
                    #nomhicca#1 = #vipe0003#1;
                    #nomhicca#3 = 0;
                    #nomhicca#66 = #vipe0003#3 - 2000;
                    aAlfa = #vipe0003#5;     || fecha inicio horas
                    aAlfa = aAlfa % 402;
                    nMes = aAlfa;
                    nMes = nMes - 1;
                    |SI nMes = 0;
                         nMes = 12;
                         #nomhicca#66 = #nomhicca#66 - 1;
                    |FINSI;
                    #nomhicca#2 = nMes;
                    |LEE 101#nomhicca.=;
                    |SI FSalida = 0;
                         |PARA nNume1 = 27; |SI nNume1 [ 30; |HACIENDO nNume1 = nNume1 + 1;
                              aAlfa = #nomhicca#nNume1#;
                              |QBF aAlfa;
                              |SI aAlfa ! "";
                                   nNume2 = nNume1 - 8;
                                   nNume3 = nNume1 - 4;
                                   mesnum = nMes;
                                   anynum = #nomhicca#66 + 2000;
                                   |HAZ ulti_dia;
                                   |SI #nomhicca#nNume3# = 0;
                                        #nomhicca#nNume3# = topemes;
                                        |GRABA 020#nomhicca.a;
                                        |LIBERA #nomhicca;
                                   |FINSI;
                              |FINSI;
                         |FINSI;
                    |SIGUE;
                    |CIERRA #nomhicca;
               |SINO;
                    |ERROR;
               |FINSI;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO TipoHoras;
     |ABRE #vipe1001;
     #vipe1001#0 = #vipe0003#7;
     |LEE 000#vipe1001.=;
     |CIERRA #vipe1001;

     aFecha = #vipe0003#5;
     aDiaD = aFecha % 102;
     nDiaD = aDiaD;
     aFecha = #vipe0003#6;
     aDiaH = aFecha % 102;
     nDiaH = aDiaH;

     |SI #vipe1001#2 = "D";
          #vipe0003#9 = nDiaH - nDiaD + 1;
          |PINTA #vipe0003#9;
          |CAMPO_MODIFICA #vipe0003#9, 1, "N";
     |SINO;
          |CAMPO_MODIFICA #vipe0003#9, 1, "S";
     |FINSI;
|FPRC;

|PROCESO HorasAbs;
     #labtraba#0 = #vipe0003#0;    || empresa
     #labtraba#1 = #vipe0003#1;    || trabajador
     |LEE 000#labtraba.=;

     |SI #vipe1001#2 = "H";
          |SI #vipe0003#7 = "F"; |O #vipe0003#7 = "f";
               nNume = nDiaH - nDiaD + 1;
               nNume = nNume * #labtraba#47;
               |SI #vipe0003#9 > nNume;
                    aAlfa1 = #labtraba#47;
                    |QBF aAlfa1;
                    aAlfa = "    Las horas introducidas por dia superan ";
                    aAlfa = aAlfa + "la jornada diaria del trabajador (";
                    aAlfa = aAlfa + aAlfa1 + " horas)";
                    |MENAV aAlfa;
               |FINSI;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO tipo2; |TIPO 2;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 3;
          |ABRE #vipe0002;
          #vipe0002#0 = #vipe0003#0;
          #vipe0002#1 = #vipe0003#1;
          #vipe0002#5 = #vipe0003#5;
          |LEE 000#vipe0002.=;
          |SI FSalida = 0;
               |BORRA 020#vipe0002.a;
               |LIBERA #vipe0002;
          |FINSI;
          |CIERRA #vipe0002;
          |ACABA;
     |FINSI;

     |GRABA 020#vipe0003.a;
     |LIBERA #vipe0003;

     |ABRE #vipe0002;
     #vipe0002#0 = #vipe0003#0;
     #vipe0002#1 = #vipe0003#1;
     #vipe0002#5 = #vipe0003#5;
     |LEE 000#vipe0002.=;
     |PARA nCampo = 0; |SI nCampo [ 9; |HACIENDO nCampo = nCampo + 1;
          #vipe0002#nCampo# = #vipe0003#nCampo#;
     |SIGUE;
     |GRABA 020#vipe0002.a;
     |LIBERA #vipe0002;

     |SI swCreaNueva = 1;
          nFechaDesde = #vipe0002#6;
          nFechaDesde = nFechaDesde + 1;
          #vipe0002#5 = nFechaDesde;
          #vipe0002#6 = aFechaHasta;

          #vipe0002#7 = "N";            || tipo de hora
          #vipe0002#8 = "NORMAL";       || descripcion tipo de hora
          #vipe0002#9 = 0;              || numero de horas

          aFecha = #vipe0002#5;
          aMes = aFecha % 402;
          nMes = aMes;
          #vipe0002#10 = nMes;              || numero de horas

          |GRABA 020#vipe0002.n;
          |LIBERA #vipe0002;
          |CIERRA #vipe0002;

          swEstoy = 1;
          nLineaAct = #vipe0003#10;
          swCreaNueva = 0;
          |PTEC 806;
     |FINSI;
|FPRC;

|PROCESO compfecha;
     swCreaNueva = 0;
     |SI #vipe0003#6 ! Contenido;        || si se ha cambiado el hasta
          fFecha = Contenido;
          |SI #vipe0003#6 < #vipe0003#5;
               |ERROR;
               |ACABA;
          |FINSI;
          |SI #vipe0003#6 > fFecha;
               |ERROR;
               |ACABA;
          |FINSI;
          aFechaHasta = Contenido;
          swCreaNueva = 1;
     |FINSI;
|FPRC;

|PROCESO inferior;
     |SI FSalida = "POSICION";
          #vipe0003#10 = nLineaAct;
          |ACABA;
     |FINSI;
     #vipe0003#10 = 001;
|FPRC;

|PROCESO superior;
     #vipe0003#10 = 999;
|FPRC;

|PROCESO Presenta;
     |ABRE #labtraba;
     |ENTLINEAL #1#vipe0003, 1, 2, 22, 0, inferior, superior;
     |CIERRA #labtraba;
|FPRC;

|PROCESO cargatmp;
     nLinea = nLinea + 1;

     #labtraba#0 = #vipe0002#0;
     #labtraba#1 = #vipe0002#1;
     |LEE 000#labtraba.=;

     #vipe0003#0 = #vipe0002#0;
     #vipe0003#1 = #vipe0002#1;
     #vipe0003#2 = #vipe0002#2;
     #vipe0003#3 = #vipe0002#3;
     #vipe0003#4 = #vipe0002#4;
     #vipe0003#5 = #vipe0002#5;
     #vipe0003#6 = #vipe0002#6;
     #vipe0003#7 = #vipe0002#7;
     #vipe0003#8 = #vipe0002#8;
     #vipe0003#9 = #vipe0002#9;
     #vipe0003#10 = nLinea;


     |SI #labtraba#42 = "X";       || si son tp
          #vipe0003#11 = #labtraba#234;           || horas tiempo parcial
     |SINO;
          #vipe0003#11 = 40;                      || horas tiempo parcial
     |FINSI;

     |SI #labtraba#42 = "X";       || si son tp
          #vipe0003#12 = #labtraba#47;            || horas TC2
     |SINO;
          #vipe0003#12 = (40 / 7) ! 2;            || horas TC2
     |FINSI;                                      || jornada completa

     |GRABA 020#vipe0003.n;
     |LIBERA #vipe0003;
|FPRC;

|DEFBUCLE cargatmp;
     #vipe0002#1;
     11;
     #0#0, 00001, #0#5, #0#1;
     #0#0, 99999, #0#6, #0#1;
     ;
     NULL, cargatmp;
|FIN;

|PROCESO LaCargaDelTmp;
     alfa1 = Usuario; |QBT alfa1;    alfa1 = ("t" + alfa1) % 108;
     |NOME_DAT #3 alfa1;

     |DELINDEX #3;

     |ABRE #vipe0003;
     |ABRE #labtraba;
     nLinea = 0;
     |BUCLE cargatmp;
     |CIERRA #labtraba;
     |CIERRA #vipe0003;

     |PONCONTROL 23, "si";
|FPRC;

|PROCESO Comprueba2;
     aNif = #vipe0002#13;
     |QBF aNif;

     aAlfa1 = #vipe0002#0; aAlfa1 = ("00000" + aAlfa1) % -105;
     aAlfa2 = #vipe0002#1; aAlfa2 = ("00000" + aAlfa2) % -105;
     aAlfa = aAlfa1 + "." + aAlfa2;
     |SI aNif = aAntNif; |Y aNif ! ""; |Y fAntFechaH ] #vipe0002#5; |Y aAlfa ! aAntCod;
          aAntCod = aAntCod % -105;
          aAlfa = aAlfa2
          aAlfa = "    ATENCION: Los trabajadores " + aAntCod + " y " + aAlfa;
          aAlfa = aAlfa + " de alta en la semana con el mismo NIF: " + aNif;
          |MENAV aAlfa;
     |FINSI;
     aAntNif = aNif;
     aAlfa1 = #vipe0002#0; aAlfa1 = ("00000" + aAlfa1) % -105;
     aAlfa2 = #vipe0002#1; aAlfa2 = ("00000" + aAlfa2) % -105;
     aAntCod = aAlfa1 + "." + aAlfa2;
     fAntFechaD = #vipe0002#5;
     fAntFechaH = #vipe0002#6;
|FPRC;

|DEFBUCLE Comprueba2;
     #vipe0002#3;
     11;
     "            ", #0#0, 00001, #0#5, #0#1;
     "zzzzzzzzzzzz", #0#0, 99999, #0#6, #0#1;
     be;
     NULL, Comprueba2;
|FIN;

|PROCESO Comprobacion2;   || comprueba que ninguno de los que se hayan cargado
                          || al temporal tenga NIF duplicado
     |BUCLE Comprueba2;
|FPRC;

|PROCESO Redondea25;
     nEntero = 0;
     nDeci = 0;

     nEntero = (nNume - 0.49) ! 0;        || horas sin decimales
     nDeci = nNume - nEntero;
     |SI nDeci < 0; nDeci = 0; |FINSI;
     |SI nDeci > 0; |Y nDeci [ 0.125; nDeci = 0; |FINSI;
     |SI nDeci > 0.125; |Y nDeci [ 0.375; nDeci = 0.25; |FINSI;
     |SI nDeci > 0.375; |Y nDeci [ 0.625; nDeci = 0.50; |FINSI;
     |SI nDeci > 0.625; |Y nDeci [ 0.875; nDeci = 0.75; |FINSI;
     |SI nDeci > 0.875; nDeci = 1; |FINSI;
|FPRC;

|PROCESO LasFechas2;
     fFecha = #labtraba#36;
     |SI fFecha > #0#6;
          swNoSigas = 1;
     |FINSI;

     aAlfa = #labtraba#37;
     |QBF aAlfa;
     |SI aAlfa ! "";
          fFecha = #labtraba#37;
          |SI fFecha < #0#5;
               swNoSigas = 1;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO LasFechas;
     fFecha = #labtraba#36;
     |SI fFecha > #0#6;
          swNoSigas = 1;
     |FINSI;
     |SI fFecha < #0#5;
          #vipe0002#5 = #0#5;           || desde fecha
     |SINO;
          #vipe0002#5 = fFecha;
     |FINSI;

     aAlfa = #labtraba#37;
     |QBF aAlfa;
     |SI aAlfa ! "";
          fFecha = #labtraba#37;
          |SI fFecha < #0#5;
               swNoSigas = 1;
          |FINSI;
          |SI fFecha > #0#6;
               #vipe0002#6 = #0#6;           || hasta fecha
          |SINO;
               #vipe0002#6 = fFecha;
          |FINSI;
     |SINO;
          #vipe0002#6 = #0#6;           || hasta fecha
     |FINSI;
|FPRC;

|PROCESO carga;
     |SI #labtraba#99 = "G";
          |ACABA;
     |FINSI;

     |DDEFECTO #vipe0002;
     |SELECT #2#vipe0002;
     #vipe0002#0 = #labtraba#0;    || empresa
     #vipe0002#1 = #labtraba#1;    || trabajador
     #vipe0002#3 = #0#2;           || a¤o
     aFecha = #0#5;
     aMes = aFecha % 402;
     nMes = aMes;
     #vipe0002#10 = nMes;          || mes
     #vipe0002#4 = #0#3;           || semana
     |LEE 000#vipe0002.=;
     |SI FSalida ! 0;
          |SELECT #1#vipe0002;
          #vipe0002#0 = #labtraba#0;    || empresa
          #vipe0002#1 = #labtraba#1;    || trabajador
          #vipe0002#2 = #labtraba#2;    || nombre trabajador
          #vipe0002#3 = #0#2;           || a¤o
          #vipe0002#4 = #0#3;           || semana

          swNoSigas = 0;
          |HAZ LasFechas;
          |SI swNoSigas = 1;
               |ACABA;
          |FINSI;

          #vipe0002#7 = "N";            || tipo de hora
          #vipe0002#8 = "NORMAL";       || descripcion tipo de hora
          |SI swUltSemana = 1;
               |SI #labtraba#47 = 0;
                    aAlfa1 = #labtraba#0; aAlfa1 = ("00000" + aAlfa1) % -105;
                    aAlfa2 = #labtraba#1; aAlfa2 = ("00000" + aAlfa2) % -105;
                    aAlfa = "    ATENCION: el trabajador " + aAlfa1 + "." + aAlfa2;
                    aAlfa = aAlfa + " no tiene introducido ningun valor en el campo Horas TC2.";
                    |MENAV aAlfa;
                    aAlfa = "    S¿Desea incluir este trabajador? (Se cargarán por ";
                    aAlfa = aAlfa + "defecto 5.72 horas diarias.)"
                    |CONFI aAlfa;
                    |SI FSalida = 0;
                         #labtraba#47 = 5.72;
                    |SINO;
                         |ACABA;
                    |FINSI;
               |FINSI;

               nNume = #labtraba#47;
               |HAZ Redondea25;
               nHorasRed = nEntero + nDeci;
               #vipe0002#9 = nHorasRed;

               fech1 = #vipe0002#5;
               fech2 = #vipe0002#6;
               nume1 = fech1;
               nume2 = fech2;
               nume3 = nume2 - nume1 + 1;
               |SI nume3 = 7;      || la semana completa
                    || redondeo el total, si no se provoca un exceso al redondear por dia
                    nNume = #labtraba#47 * nume3;
                    |HAZ Redondea25;
                    #vipe0002#9 = nEntero + nDeci;
               |SINO;
                    || si no, pues como siempre
                    #vipe0002#9 = nume3 * #vipe0002#9;
               |FINSI;
          |SINO;
               #vipe0002#9 = 0;              || numero de horas
          |FINSI;

          aFecha = #vipe0002#5;
          aMes = aFecha % 402;
          nMes = aMes;
          #vipe0002#10 = nMes;              || numero de horas
          #vipe0002#11 = #labtraba#77;      || centro
          #vipe0002#13 = #labtraba#7;       || NIF

          |GRABA 020#vipe0002.n;
          |LIBERA #vipe0002;
     |SINO;
          swNoSigas = 0;
          |HAZ LasFechas2;         || compruebo fechas por si han cambiado
                                   || y el trabajador estuviese de baja
          |GRABA 020#vipe0002.a;
          |LIBERA #vipe0002;
     |FINSI;
|FPRC;

|DEFBUCLE carga;
     #labtraba#1;
     44, 77;
     #0#0, 00001, "A", #0#1;
     #0#0, 99999, "A", #0#1;
     ;
     NULL, carga;
|FIN;

|PROCESO LaCarga;
     |ABRE #vipe0002;

     |BUCLE carga;

     |CIERRA #vipe0002;
|FPRC;

|PROCESO UltimaSemana;
     swUltSemana = 0;
     mesnum = nMesD;
     anynum = aAnyD;
     |HAZ ulti_dia;
     aAlfa = #0#6;
     aAlfa = aAlfa % 102;
     nNume = aAlfa;
     |SI nNume = topemes;
          swUltSemana = 1;
     |FINSI;
|FPRC;

|PROCESO MensajeBaja;
     aAlfa1 = #vipe0002#0; aAlfa1 = ("00000" + aAlfa1) % -105;
     aAlfa2 = #vipe0002#1; aAlfa2 = ("00000" + aAlfa2) % -105;
     aAlfa = aAlfa1 + "." + aAlfa2;
     aAlfa3 = #vipe0002#9;
     aAlfa = "    ATENCION: trabajador " + aAlfa + " con " + aAlfa3;
     aAlfa = aAlfa + " horas introducidas esta semana esta en situacion ";
     aAlfa = aAlfa + "de baja.";
     |MENAV aAlfa;

     aAlfa = "    El trabajador se eliminara de la introduccion de ";
     aAlfa = aAlfa + "partes de esta semana.";
     |MENAV aAlfa;
|FPRC;

|PROCESO Comprueba1;
     #labtraba#0 = #vipe0002#0;
     #labtraba#1 = #vipe0002#1;
     |LEE 000#labtraba.=;

     swNoSigas = 0;
     |HAZ LasFechas2;    || compruebo fechas por si han cambiado
                         || y el trabajador estuviese de baja
     ||SI #labtraba#44 = "B"; |O swNoSigas = 1;
     |SI swNoSigas = 1;
          |SI #vipe0002#9 ! 0;
               |HAZ MensajeBaja;
          |FINSI;
          |BORRA 020#vipe0002.a;
     |SINO;
          || regrabo NIF por si acaso no estuviera grabado
          #vipe0002#13 = #labtraba#7;       || NIF
          |GRABA 020#vipe0002.a;
     |FINSI;
|FPRC;

|DEFBUCLE Comprueba1;
     #vipe0002#1;
     11;
     #0#0, 00001, #0#5, #0#1;
     #0#0, 99999, #0#6, #0#1;
     be;
     NULL, Comprueba1;
|FIN;

|PROCESO Comprobacion1;   || comprueba que ninguno de los que se hubieran
     |ABRE #labtraba;    || introducido este de baja
     |BUCLE Comprueba1;
     |CIERRA #labtraba;
|FPRC;

|PROGRAMA;
     |CLS;
     |PINPA #0#0;
     |PINDA #0#0;
     |PARA; |SI; |HACIENDO;
          |ENDATOS #1#0;
          |SI FSalida = -1;
               |SAL;
          |FINSI;
          |SI FSalida = 0;
               |HAZ UltimaSemana;
               |HAZ Comprobacion1;
               |HAZ LaCarga;
               |HAZ Comprobacion2;

               nLineaAct = 1;
               swEstoy = 1;
               |PARA; |SI swEstoy > 0; |HACIENDO;
                    |HAZ LaCargaDelTmp;
                    swEstoy = 0;
                    |HAZ Presenta;
               |SIGUE;
          |FINSI;
     |SIGUE;
|FPRO;
