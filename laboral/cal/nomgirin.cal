|FICHEROS;
     nomgirin #0;
     nomgirli;
     nomgirma;
     nomgirtm;
     nomir012;
     nomgirco;
     nomir009;

     labempre;
     labtraba;
|FIN;

|VARIABLES;
     fFecha = @;
     aAlfa = "";
     nNume = 0;
     nMesAnt = 0;
     &nTipoAnt = 0;
     &nBrutoAnt = 0;
     aParam = "";
     &nEmp = 0;
     &nTra = 0;
     &nInc = 0;
     &nMes = 0;
     &aMarca = "";

     &nAny = 0;
     &aParam2 = "";
     &swAviso = 0;
|FIN;

|PROCESO impresion;
     |SI aParam = ""; |O aParam = "nomgirca";
          nEmp = #nomgirli#0;
          nTra = #nomgirli#5;
          nInc = #nomgirli#6;
          nMes = #nomgirli#2;
          aMarca = #nomgirli#9;
     |SINO;
          nEmp = #nomgirtm#0;
          nTra = #nomgirtm#5;
          nInc = #nomgirtm#6;
          nMes = #nomgirtm#2;
          aMarca = #nomgirtm#9;
     |FINSI;

     #labempre#0 = nEmp;
     |LEE 000#labempre.=;

     #labtraba#0 = nEmp;
     #labtraba#1 = nTra;
     |LEE 000#labtraba.=;

     #nomgirma#0 = nInc;
     |LEE 000#nomgirma.=;

     #nomir009#0 = nEmp;
     |LEE 000#nomir009.=;

     |SI #nomir009#21 = 0; |O #nomgirco#1 = "N";
          |SI aParam2 = "PI";
               swAviso = 2;
          |SINO;
               swAviso = 1;
          |FINSI;
     |FINSI;

     |SI #0#7 [ #nomgirma#2; |Y #nomgirma#2 [ #0#8;
          || continua
     |SINO;
          |ACABA;
     |FINSI;

     || en las empresas desactivadas no saldran las tipo 0

     |SI #nomir009#21 = 0; |Y #nomgirma#2 = 0;
          |SI #0#9 = "N";
               |ACABA;
          |FINSI;
     |FINSI;

     |SI aParam = "nomrecib"; |O aParam = "nomactca";
          |SI #nomgirtm#9 ! "N";
               |ACABA;
          |FINSI;
     |FINSI;

     |SI nMes > 1;
          nMesAnt = nMes - 1;
     |SINO;
          nMesAnt = 0;
     |FINSI;

     nTipoAnt = 0;
     nBrutoAnt = 0;
     |SI nMesAnt ! 0;
          #nomir012#1 = nEmp;
          #nomir012#2 = nTra;
          #nomir012#3 = #0#6;
          #nomir012#4 = nMesAnt;
          |LEE 000#nomir012.=;
          |SI FSalida = 0;
               nTipoAnt = #nomir012#85;
               nBrutoAnt = #nomir012#84;
          |FINSI;
     |FINSI;

     |DDEFECTO #nomir012;
     #nomir012#1 = nEmp;
     #nomir012#2 = nTra;
     #nomir012#3 = #0#6;
     #nomir012#4 = nMes;
     |LEE 000#nomir012.=;

     |PRINT;
|FPRC;

|DEFBUCLE nomgirli;
     #nomgirli#2;
     6;
     #0#0, #0#2, #0#6, #0#4, 001;
     #0#1, #0#3, #0#6, #0#5, 998;
     ;
     NULL, impresion;
|FIN;

|DEFBUCLE nomgirtm;
     #nomgirtm#2;
     6;
     #0#0, #0#2, #0#6, #0#4, 001;
     #0#1, #0#3, #0#6, #0#5, 998;
     ;
     NULL, impresion;
|FIN;

|PROCESO Defectos;
     fFecha = ~;
     aAlfa = fFecha;
     aAlfa = aAlfa % 402;
     #0#4 = aAlfa;
     |PINTA #0#4;
     #0#5 = aAlfa;
     |PINTA #0#5;

     aAlfa = fFecha;
     aAlfa = aAlfa % -104;
     #0#6 = aAlfa;
     |PINTA #0#6;
|FPRC;

|PROGRAMA;
     aParam = PARAMETRO;
     |QBF aParam;
     |SI aParam = "PI";
          aParam2 = "PI";
          aParam = "";
     |FINSI;

     |SI aParam = "";
          |CLS;

          |PINPA #0#0;
          |PINDA #0#0;

          |HAZ Defectos;

          |ENDATOS #1#0;
     |SINO;
          #0#0 = 00001;
          #0#1 = 99999;
          #0#2 = 00001;
          #0#3 = 99999;
          #0#4 = 01;
          #0#5 = 12;
          #0#6 = nAny;
          |SI aParam = "nomgir00";
               #0#7 = 0;
               #0#8 = 2;
          |FINSI;
          |SI aParam = "nomrecib"; |O aParam = "nomactca";
               #0#7 = 2;
               #0#8 = 2;
          |FINSI;
          |SI aParam = "nomgirca";
               #0#0 = nEmp;
               #0#1 = nEmp;
               #0#2 = 00001;
               #0#3 = 99999;
               #0#4 = nMes;
               #0#5 = nMes;
               #0#6 = nAny;
               #0#7 = 0;
               #0#8 = 2;
          |FINSI;

          |SI aParam2 ! "";
               aParam2 = aParam2 % -102;
          |FINSI;

          aAlfa = Usuario;         |QBF aAlfa;
          aAlfa = "t" +  aAlfa;
          |NOME_DAT #nomgirtm#aAlfa#;

          FSalida = 0;
     |FINSI;

     || valores de swAviso:

     || 0 = no hay que dar ningun aviso
     || 1 = aviso, menu normal
     || 2 = aviso, menu PI

     swAviso = 0;

     |SI FSalida = 0;
          Impresora = "CrystalReport";
          |IMPRE -1;
          |INFOR "nomgirli";

          |ABRE #nomgirco; |LEE 000#nomgirco.p; |CIERRA #nomgirco;

          |ABRE #labempre;
          |ABRE #labtraba;
          |ABRE #nomgirma;
          |ABRE #nomgirtm;
          |ABRE #nomgirli;
          |ABRE #nomir012;
          |ABRE #nomir009;

          |SI aParam = ""; |O aParam = "nomgirca";
               |BUCLE nomgirli;
          |SINO;
               |BUCLE nomgirtm;
          |FINSI;

          |PIEINF;
          |FININF;

          |FINIMP;

          |CIERRA #nomir012;
          |CIERRA #nomir009;
          |CIERRA #nomgirli;
          |CIERRA #nomgirtm;
          |CIERRA #nomgirma;
          |CIERRA #labtraba;
          |CIERRA #labempre;
     |FINSI;
|FPRO;
