|FICHEROS;
     cretai05 #0;
     labempre;

     cretam31;
     cretam33;
     cretam00;
|FIN;

|VARIABLES;
     nCampo              = 0;
     nCampo1             = 0;
     nCampo2             = 0;
     FEstado             = 0;
     nMes                = 0;
     nAny                = 0;
     nNume               = 0;
     nTramos             = 0;
     nImpresion          = 0;
     nMesDesde           = 0;
     nMesHasta           = 0;
     nAnyDesde           = 0;
     nAnyHasta           = 0;
     nPasada             = 0;
     nOk                 = 0;
     nTp                 = 0;
     nDTp                = 0;
     nHTp                = 0;
     nOpcion             = 0;

     aReferencia         = "";
     aFecha              = "";
     aAlfa               = "";
     aAntCCC             = "";
     aEmp                = "";
     aNomEmp             = "";
     aFic                = "";

     fFecha              = @;

 {-1}aMenu               = "";
     aMenu1              = "2400";
     aMenu2              = "1";
     aMenu3              = "Archivo documental activado. Elija opci¢n: ";
     aMenu4              = "IAB";
     aMenu5              = "Imprimir";
     aMenu6              = "Archivar";
     aMenu7              = "Ambos";

     &nError             = 0;
     &eaVengoDe          = "";
     &nArchi             = 0;
     &swErrorDSArchi     = 0;
     &enEmp              = 0;
     &enTipo             = 0;
     &eaTitulo           = "";
     &enTra              = 0;
     &enAny              = 0;
     &enDesdeMes         = 0;

     aParam = "";
|FIN;

|PROCESO Defectos;
     fFecha = ~;
     aFecha = fFecha;
     aAlfa = aFecha % -104;
     nAny = aAlfa;

     aAlfa = aFecha % 402;
     nMes = aAlfa;
     nMes = nMes - 1;
     |SI nMes = 0;
          nMes = 12;
          nAny = nAny - 1;
     |FINSI;

     #cretai05#2 = nAny;
     #cretai05#3 = nMes;
     |PINTA #cretai05#2;
     |PINTA #cretai05#3;
|FPRC;

|PROCESO AvisoTipos;
     |SI #cretai05#Campo# = 01;
          aAlfa = "    ATENCION: con la selección 'Desde tipo 01'";
          aAlfa = aAlfa + " no se incluyen las liquidaciones L00";
          |MENAV aAlfa;
     |FINSI;
|FPRC;

|PROCESO AyudaTipos; |TIPO 7;
     |PUSHV 08622380;
     |ATRI R;
     ||            6         7
     ||            34567890123456789
     |PINTA 0863, "  - Opciones -   ";
     |PINTA 0963, " 00 :Normal      ";
     |PINTA 1063, " 02 :Finiquitos  ";
     |PINTA 1163, "5-20:Atrasos     ";
     |PINTA 1263, " 90 :Complement. ";
     |PINTA 1363, "                 ";
     |PINTA 1463, "                 ";
     |ATRI 0;
|FPRC;

|PROCESO Restaura;
     |POPV;
|FPRC;

|PROCESO ElIMPRE;
|FPRC;

|PROCESO ElFINIMP;
|FPRC;

|PROCESO cretam33;
     |SI nPasada = 0;
         nOk = 1;
         |ERROR10;
         |ACABA;
     |FINSI;

     |PRINT;
     nImpresion = nImpresion + 1;
|FPRC;

|DEFBUCLE cretam33;
     #cretam33#1;
     ;
     #cretam31#0, nAnyDesde, nMesDesde, #cretam31#3, #cretam31#4, "            ", 01, 000;
     #cretam31#0, nAnyHasta, nMesHasta, #cretam31#3, #cretam31#4, "zzzzzzzzzzzz", 99, 999;
     ;
     NULL, cretam33;
|FIN;

|PROCESO cretam31;
     |SI nPasada = 0;
         |SI nOk = 1;
             |ERROR10;
             |ACABA;
         |FINSI;
     |FINSI;

     |SI #cretam31#3 < 05; |O #cretam31#3 = 90;
          nMesDesde = #cretam31#2;
          nMesHasta = #cretam31#2;
          nAnyDesde = #cretam31#1;
          nAnyHasta = #cretam31#1;
     |SINO;
          nMesDesde = #cretam31#17;
          nMesHasta = #cretam31#19;
          nAnyDesde = #cretam31#18;
          nAnyHasta = #cretam31#20;
     |FINSI;

     |BUCLE cretam33;
     |PIEINF;
|FPRC;

|DEFBUCLE cretam31_L90;
     #cretam31#3;
     ;
     #cretai05#0, #cretai05#2, #cretai05#3, #cretai05#6, "               ";
     #cretai05#1, #cretai05#2, #cretai05#3, #cretai05#7, "zzzzzzzzzzzzzzz";
     ;
     NULL, cretam31;
|FIN;

|DEFBUCLE cretam31;
     #cretam31#1;
     ;
     #cretai05#0, #cretai05#2, #cretai05#3, #cretai05#6, "               ", 0000;
     #cretai05#1, #cretai05#2, #cretai05#3, #cretai05#7, "zzzzzzzzzzzzzzz", 2999;
     ;
     NULL, cretam31;
|FIN;

|PROCESO Papel;
     nOk  = 0;

     nPasada = 0;
     |BUCLE cretam31;

     |SI nOk = 0;
         |BUCLE cretam31_L90;
     |FINSI;

     |SI nOk = 0;
         aAlfa = "    No se han encontrado datos de RNT para imprimir ";
         aAlfa = aAlfa + "con la selección realizada.";
         |MENAV aAlfa;
         |ACABA;
     |FINSI;

     Impresora = "CrystalReport";
     |IMPRE -1;
     |SI FSalida ! 0;
         |MENAV "0000Impresora CrystalReport no definida, emision cancelada";
         |ACABA;
     |FINSI;

     |INFOR "cretai05";

     nPasada = 1;
     |BUCLE cretam31;
     |BUCLE cretam31_L90;

     |FININF;
     |FINIMP;
|FPRC;

|DEFBUCLE cretam31A_L90;
     #cretam31#3;
     ;
     enEmp, #cretai05#2, #cretai05#3, nDTp, "               ";
     enEmp, #cretai05#2, #cretai05#3, nHTp, "zzzzzzzzzzzzzzz";
     ;
     NULL, cretam31;
|FIN;

|DEFBUCLE cretam31A;
     #cretam31#1;
     ;
     enEmp, #cretai05#2, #cretai05#3, nDTp, "               ", 0000;
     enEmp, #cretai05#2, #cretai05#3, nHTp, "zzzzzzzzzzzzzzz", 2999;
     ;
     NULL, cretam31;
|FIN;

|PROCESO Mensaje;
     aEmp = "                                             ";

     aAlfa   = ("00000" + #labempre#0) % -105;
     aNomEmp = #labempre#2;
     aNomEmp = aNomEmp % 126;
     aEmp = "Empresa: " + aAlfa + " - " + aNomEmp;
     aEmp = " " + aEmp + " ";

     |CUADRO 1217 1619;
     |ATRI R;
     |PINTA 1318, "      - Procesando archivo de RNT -                 ";
     |PINTA 1418, "                                                    ";
     |PINTA 1518, "                                                    ";
     |PINTA 1518, aEmp;
     |ATRI 0;
|FPRC;

|PROCESO EmiteTipo;
     nDTp = nTp;
     nHTp = nTp;
     nOk  = 0;

     nPasada = 0;
     |BUCLE cretam31A;

     |SI nOk = 0;
         |BUCLE cretam31A_L90;
     |FINSI;

     |SI nOk = 0;
         |ACABA;
     |FINSI;

     |HAZ Mensaje;

     |SI enTipo = 0;  eaTitulo = "RNT - MES NORMAL";       |FINSI;
     |SI enTipo = 2;  eaTitulo = "RNT - FINIQUITOS";       |FINSI;
     |SI enTipo = 5;  eaTitulo = "RNT - ATRASOS";          |FINSI;
     |SI enTipo = 90; eaTitulo = "RNT - COMPLEMENTARIOS";  |FINSI;

     |HAZ IMPREArchi;

     nPasada = 1;
     |BUCLE cretam31A;
     |BUCLE cretam31A_L90;

     |FININF;
     |FINIMP;

     |SI swErrorDSArchi = 0;
         enTra      = 0;
         enAny      = #cretai05#2;
         enDesdeMes = #cretai05#3;
         |HAZ Archivando;
     |FINSI;
|FPRC;

|PROCESO ArchivaEmpresa;
     #labempre#0 = enEmp;
     |LEE 000#labempre.=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |PARA nTp = #cretai05#6;  |SI nTp [ #cretai05#7;  |HACIENDO nTp = nTp + 1;
           enTipo = -1;

           |SI nTp = 0;   enTipo = 0;  |FINSI;
           |SI nTp = 2;   enTipo = 2;  |FINSI;
           |SI nTp = 90;  enTipo = 90; |FINSI;

           |SI nTp ] 5;  |Y nTp [ 89;
               enTipo = 5;
           |FINSI;

           |SI enTipo ] 0;
               |HAZ EmiteTipo;
           |FINSI;
     |SIGUE;
|FPRC;

|PROCESO Archivado;
     |ABRE #labempre;
     |PARA enEmp = #cretai05#0;  |SI enEmp [ #cretai05#1;  |HACIENDO enEmp = enEmp + 1;
           |HAZ ArchivaEmpresa;
     |SIGUE;
     |CIERRA #labempre;
|FPRC;

|PROCESO ProcesaInforme;
     |SI aParam = "B";
          |HAZ Papel;      || En borrador, no dejo archivar
          |ACABA;
     |SINO;
          eaVengoDe = "cretai05";
          |HAZ MiraSiArchiva;
          |SI nArchi = 0;
              |HAZ Papel;
              |ACABA;
          |FINSI;

          |MENU aMenu;
          nOpcion = FSalida;
          |SI nOpcion = 1;  |HAZ Papel;      |FINSI;
          |SI nOpcion = 2;  |HAZ Archivado;  |FINSI;
          |SI nOpcion = 3;  |HAZ Papel;      |FINSI;
          |SI nOpcion = 3;  |HAZ Archivado;  |FINSI;

          |SI nOpcion ! 1;
              aAlfa = ":dsarchi/dpntz001;CLI";
              |SUB_EJECUTA aAlfa;
          |FINSI;
     |FINSI;
|FPRC;

|PROGRAMA;
     |CLS;
     |PINPA #0#cretai05;

     |ABRE #cretai05;
     |DDEFECTO #cretai05;
     |LEE 000#cretai05.p;
     |SI FSalida = 0;
          |BORRA 020#cretai05.a;
     |SINO;
          |HAZ Defectos;
     |FINSI;

     |PINDA #0#cretai05;

     |ENDATOS #1#cretai05;
     |SI FSalida = 0;
          aParam = PARAMETRO;
          |QBF aParam;

          |ABRE #cretam00;
          |LEE 000#cretam00.p;
          |CIERRA #cretam00;

          |ABRET #cretam31;
          |ABRET #cretam33;

          |HAZ ProcesaInforme;

          |CIERRAT #cretam33;
          |CIERRAT #cretam31;
     |FINSI;

     |GRABA 000#cretai05.n;
     |CIERRA #cretai05;
|FPRO;
