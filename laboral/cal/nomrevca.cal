|FICHEROS;
     nomrevca #0;
     nomrevli;
     nompagli;
     nomcontr;

     labtraba;
     labtracu;

     nomw0022,MANTE;

     trabajo@;
|FIN;

|VARIABLES;
     alfa1 = "";
     alfa2 = "";
     alfa3 = "";
     alfa4 = "";
     nume1 = 0;
     nume2 = 0;
     nume3 = 0;
     nume4 = 0;
     para1 = 0;
     para2 = 0;
     para3 = 0;
     para4 = 0;
     fech1 = @;
     fech2 = @;
     fech3 = @;
     fech4 = @;
     elsw1 = 0;
     elsw2 = 0;
     elsw3 = 0;
     elsw4 = 0;
     FEstado = 0;
     seleccio = 0;
     modo = 0;
     sw_rup = 0;
     nModo = 0;
     nModoCab = 0;
     swExiste = 0;
     aRegistro = "";
     nAny = 0;
     nMes = 0;
     nTip = 0;
     aNP = "";
     FEntrada = 0;

     nLin       = 0;
     nCampo     = 0;
     nTCampo    = 0;

     aAlfa      = "";
     aDef       = "";
     aMas       = "";
     aFicTmp    = "";
     aCampo     = "";
     aQueQuiero = "";

     &swModulo = 0;

     &enEmpresa = 0;
     &enRegistro = 0;
|FIN;
____________________________________/


|PROCESO LeeTrab;
     |ABRE #labtraba;
     #labtraba#0 = #nompagli#0;
     #labtraba#1 = #nompagli#5;
     |LEE 000#labtraba.=;
     |CIERRA #labtraba;
|FPRC;

|PROCESO CreaNuevo;
     |ABRE #nompagli;

     #nompagli#0 = #nomrevca#0;
     #nompagli#1 = nAny;
     #nompagli#2 = nMes;
     #nompagli#3 = nTip;
     #nompagli#4 = aNP;
     #nompagli#5 = #nomrevli#3;
     |LEE 101#nompagli.=;
     FEstado = FSalida;

     |HAZ LeeTrab;

     #nompagli#6  = #labtraba#2;    || nombre
     #nompagli#7  = #labtraba#70;   || forma de pago
     #nompagli#9  = #nomrevli#5;           || cuenta
     #nompagli#11 = "N";           || pagado
     #nompagli#12 = #nomrevli#6;          || liquido
     |SI #nomrevli#11 ! 0;
         #nompagli#12 = #nomrevli#11;          || liquido
     |FINSI;
     #nompagli#13 = "N";           || contabilizado
     #nompagli#14 = 00;
     |SI FEstado ! 0;
          |GRABA 020#nompagli.n;
     |SINO;
          |GRABA 020#nompagli.a;
     |FINSI;
     |CIERRA #nompagli;
|FPRC;

|PROCESO CreaReg;
     |SI #nompagli#5 ! #nomrevli#3;       || se supone, pero por si acaso
          nAny = #nompagli#1;
          nMes = #nompagli#2;
          nTip = #nompagli#3;
          aNP  = #nompagli#4;
          nAny = #nompagli#1;
          |ERROR10;
     |FINSI;
|FPRC;

|DEFBUCLE CreaReg;
     #nompagli#2;
     ;
     #nomrevca#0, aRegistro, 1999, 01, 0, " ", 00001;
     #nomrevca#0, aRegistro, 2999, 12, 9, "z", 99999;
     be;
     NULL, CreaReg;
|FIN;

|PROCESO BorraReg;
     ||BORRA 020#nompagli.a;
     #nompagli#10 = "";            || registro a cero
     |SI #nompagli#11 = "S";
          aAlfa = "    ATENCION: El registro a borrar ya se ha pagado.";
          aAlfa = aAlfa + " Debera revisar la tranferencias realizadas.";
     |FINSI;
     |SI #nompagli#13 = "S";
          aAlfa = "    ATENCION: El registro a borrar ya se ha pagado";
          aAlfa = aAlfa + " y contabilizado. Debera revisar tranferencias y asientos realizados.";
     |FINSI;
     |QBF aAlfa;
     |SI aAlfa ! "";
          |MENAV aAlfa;
     |FINSI;

     #nompagli#8 = "";             || fecha pago
     #nompagli#11 = "N";           || pagado (S/N)
     #nompagli#13 = "N";           || contabilizado (S/N)
     #nompagli#14 = 00;            || ordenante

     |GRABA 020#nompagli.a;
|FPRC;

|DEFBUCLE BorraReg;
     #nompagli#2;
     ;
     #nomrevca#0, aRegistro, 1999, 01, 0, " ", #nomrevli#3;
     #nomrevca#0, aRegistro, 2999, 12, 9, "z", #nomrevli#3;
     be;
     NULL, BorraReg;
|FIN;

|PROCESO ModifReg;
     #nompagli#12 = #nomrevli#6;          || liquido
     |SI #nomrevli#11 ! 0;
         #nompagli#12 = #nomrevli#11;          || liquido
     |FINSI;
     |GRABA 020#nompagli.a;
     swExiste = 1;
|FPRC;

|DEFBUCLE ModifReg;
     #nompagli#2;
     ;
     #nomrevca#0, aRegistro, 1999, 01, 0, " ", #nomrevli#3;
     #nomrevca#0, aRegistro, 2999, 12, 9, "z", #nomrevli#3;
     be;
     NULL, ModifReg;
|FIN;

|PROCESO BuscaReg;
     swExiste = 1;
|FPRC;

|DEFBUCLE BuscaReg;
     #nompagli#2;
     ;
     #nomrevca#0, aRegistro, 1999, 01, 0, " ", #nomrevli#3;
     #nomrevca#0, aRegistro, 2999, 12, 9, "z", #nomrevli#3;
     ;
     NULL, BuscaReg;
|FIN;

|PROCESO Registro;
     aRegistro = #nomrevca#1;
     aRegistro = ("000000" + aRegistro) % -106;

     |SI nModo = 1;
          swExiste = 0;
          |BUCLE BuscaReg;
          |SI swExiste = 0;
               |BUCLE CreaReg;
               |HAZ CreaNuevo;
          |FINSI;
     |FINSI;

     |SI nModo = 2;
          swExiste = 0;
          |BUCLE ModifReg;
          |SI swExiste = 0;   || NO HE ENCONTRADO AL TRABAJADOR, LO CREO
               |BUCLE CreaReg;
               |HAZ CreaNuevo;
          |FINSI;
     |FINSI;
     |SI nModo = 3;
          |BUCLE BorraReg;
     |FINSI;
|FPRC;

|PROCESO tipo2; |TIPO 2;
     |HAZ BuscaModulo;
     |SI swModulo = 2805;
          nModo = (FEntrada / 100) ! 0;
          |SI nModo = 2;
               |ERROR;
          |FINSI;
     |FINSI;

     |ABRE #nomcontr; |LEE 000#nomcontr.p; |CIERRA #nomcontr;
     #nomrevca#2 = #nomrevca#2 +. #nomrevli#6;
     #nomrevca#3 = #nomrevca#3 +. 1;

     |PINTA #nomrevca#2;
     |PINTA #nomrevca#3;

     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 3;
         |SI #nomrevli#12 ! "N";
             |MENAV "0000Quite la division con el F2 y luego borre.";
             |ERROR;
             |ACABA;
         |FINSI;
     |FINSI;

     |SI #nomcontr#42 = "N"; |ACABA; |FINSI;

     |HAZ Registro;
|FPRC;


|PROCESO nomrevli;
     nLin = nLin + 5;

     |PARA nCampo = 0;  |SI nCampo [ nTCampo;  |HACIENDO nCampo = nCampo + 1;
           #trabajo@#nCampo# = #nomrevli#nCampo#;
     |SIGUE;

     #trabajo@#2 = nLin;

     |GRABA 020#trabajo@.n;
     |LIBERA #trabajo@;

     |BORRA  020#nomrevli.a;
     |LIBERA #nomrevli;
|FPRC;

|DEFBUCLE nomrevli;
     #nomrevli#1;
     ;
     #nomrevca#0, #nomrevca#1, 00001;
     #nomrevca#0, #nomrevca#1, 99999;
     ;
     NULL, nomrevli;
|FIN;

|PROCESO trabajo;
     |PARA nCampo = 0;  |SI nCampo [ nTCampo;  |HACIENDO nCampo = nCampo + 1;
           #nomrevli#nCampo# = #trabajo@#nCampo#;
     |SIGUE;

     |GRABA 020#nomrevli.n;
     |LIBERA #nomrevli;

     #nomrevca#2 = #nomrevca#2 + #nomrevli#6;
     #nomrevca#3 = #nomrevca#3 + 1;
|FPRC;

|DEFBUCLE trabajo;
     #trabajo@#1003;
     ;
     #nomrevca#0, #nomrevca#1, 00001;
     #nomrevca#0, #nomrevca#1, 99999;
     ;
     NULL, trabajo;
|FIN;

|PROCESO Renumera;
     #nomrevca#2 = 0;
     #nomrevca#3 = 0;

     |DDEF aDef;
     aMas = aDef + "nomrevli.mas";
     |CARGA_DEF aMas, trabajo@;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     aQueQuiero = "|NC";
     aCampo     = #trabajo@#aQueQuiero#;
     nTCampo    = aCampo;
     nTCampo    = nTCampo - 1;

     aFicTmp = "nomrevli" + Usuario;
     |NOME_DAT #trabajo@#aFicTmp#;

     |ABRE #trabajo@;  |CIERRA #trabajo@;  |DELINDEX #trabajo@;

     nLin = 0;
     |ABRE #trabajo@;
     |BUCLE nomrevli;
     |CIERRA #trabajo@;

     |BUCLE trabajo;
     |DELINDEX #trabajo@;

     |DESCARGA_DEF #trabajo@;

     |PINTA #nomrevca#2;
     |PINTA #nomrevca#3;

     |PTEC 812;
     |PTEC 816;
|FPRC;

|PROCESO Tipo5;  |TIPO 5;
     nModoCab = (FEntrada / 100) ! 0;

     #nomrevli#12 = "N";
     |LINEAS_BI_ATRI #nomrevli#12, I, 0, -1;

     |SI nModoCab ! 2;  |ACABA;  |FINSI;

     |HAZ Renumera;

     #nomrevli#12 = "N";
     |LINEAS_BI_ATRI #nomrevli#12, I, 0, -1;
|FPRC;

|PROCESO Divide;
     |LEE 101#nomrevli.=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     #nomrevli#11 = #nomw0022#0;
     #nomrevli#6  = #nomw0022#1;
     |SI #nomw0022#2 = 0;
         #nomrevli#11 =  0;
         #nomrevli#12 = "N";
     |SINO;
         #nomrevli#11 = #nomw0022#0;
         #nomrevli#12 = "S";
     |FINSI;
     |GRABA 020#nomrevli.a;
     |LIBERA #nomrevli;

     nLin = #nomrevli#2;
     |LEE 101#nomrevli.s;
     |SI FSalida = 0;  |Y #nomrevli#12 = "2";  |Y #nomrevli#11 = #nomw0022#0;
         |SI #nomw0022#2 ! 0;
             #nomrevli#6  = #nomw0022#2;
             |GRABA 020#nomrevli.a;
         |SINO;
             |BORRA 020#nomrevli.a;
         |FINSI;

         |LIBERA #nomrevli;
         |ACABA;
     |FINSI;
     |LIBERA #nomrevli;

     #nomrevli#0 = #nomrevca#0;
     #nomrevli#1 = #nomrevca#1;
     #nomrevli#2 = nLin;
     |LEE 000#nomrevli.=;

     |SI #nomw0022#2 = 0;  |ACABA;  |FINSI;

     #nomrevli#0  = #nomrevca#0;
     #nomrevli#1  = #nomrevca#1;
     #nomrevli#2  = nLin + 1;
     #nomrevli#5  = #labtracu#5 + #labtracu#6 + #labtracu#8 + #labtracu#7;
     #nomrevli#6  = #nomw0022#2;
     #nomrevli#9  = #labtracu#12;
     #nomrevli#10 = #labtracu#13;
     #nomrevli#11 = #nomw0022#0;
     #nomrevli#12 = "2";
     |GRABA 020#nomrevli.n;
     |LIBERA #nomrevli;
|FPRC;

|PROCESO Tipo11;  |TIPO 11;
     |SI nModoCab = 4;  |ACABA;  |FINSI;
     |SI FSalida ! 10;  |ACABA;  |FINSI;

     |ABRE #labtracu;
     #labtracu#0 = #nomrevli#0;
     #labtracu#1 = #nomrevli#3;
     #labtracu#2 = 4;
     |LEE 000#labtracu.=;
     |SI FSalida ! 0;  |DDEFECTO #labtracu;  |FINSI;
     |CIERRA #labtracu;

     aAlfa = #labtracu#5 + #labtracu#6 + #labtracu#7 + #labtracu#8;  |QBT aAlfa;
     |SI aAlfa = "";
         |MENAV "0000No se puede dividir la transferencia, el trabajador no tiene la segunda cuenta.";
         |ERROR;
         |ACABA;
     |FINSI;

     |SI #nomrevli#12 = "2";
         |MENAV "0000Modifique el registro del banco 1";
         |ERROR;
         |ACABA;
     |FINSI;

     #nomw0022#0 = #nomrevli#6;
     #nomw0022#1 = 0;
     #nomw0022#2 = 0;
     |SI #nomrevli#11 ! 0;
         #nomw0022#0 = #nomrevli#11;
         #nomw0022#1 = #nomrevli#6;
         #nomw0022#2 = #nomrevli#11 - #nomrevli#6;
     |FINSI;

     |PUSHV 0501 2380;
     |PINPA #0#nomw0022;
     |PINDA #0#nomw0022;
     |ENDATOS #1#nomw0022;
     |SI FSalida = 0;
         |HAZ Divide;
         |HAZ Renumera;
     |FINSI;
     |POPV;

     |ERROR;
|FPRC;

||   --------------------- Cabecera

|PROCESO BuscaReg2;
     #nompagli#8 = "";        || fecha pago
     #nompagli#10 = "";       || registro
     #nompagli#11 = "N";      || pagado No
     #nompagli#13 = "N";      || contabilizado No
     |GRABA 020#nompagli.a;
|FPRC;

|DEFBUCLE BuscaReg2;
     #nompagli#2;
     ;
     #nomrevca#0, aRegistro, 1999, 01, 0, " ", #nomrevli#3;
     #nomrevca#0, aRegistro, 2999, 12, 9, "z", #nomrevli#3;
     be;
     NULL, BuscaReg2;
|FIN;

|PROCESO ActualizaReg;
     |BUCLE BuscaReg2;
|FPRC;

|DEFBUCLE ActualizaReg;
     #nomrevli#1;
     ;
     #nomrevca#0, #nomrevca#1, 00001;
     #nomrevca#0, #nomrevca#1, 99999;
     be;
     NULL, ActualizaReg;
|FIN;

|PROCESO Tipo2Cab; |TIPO 2;
     |ABRE #nomcontr; |LEE 000#nomcontr.p; |CIERRA #nomcontr;
     |SI #nomcontr#42 = "N"; |ACABA; |FINSI;
     nModoCab = (FEntrada / 100) ! 0;
     |SI nModoCab = 2;
          |SI #nomrevca#5 = "S";
               |MENAV "    ATENCION: La transferencia ya ha sido creada";
          |FINSI;
     |FINSI;
     |SI nModoCab = 3;
          |SI FSalida = 0;
               |ABRE #nompagli;
               aRegistro = #nomrevca#1;
               aRegistro = ("000000" + aRegistro) % -106;
               |BUCLE ActualizaReg;
               |CIERRA #nompagli;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Tipo15Log; |TIPO 15;
/*
     #nomrevli#17 = ~;
     |HORA aAlfa;
     #nomrevli#18 = aAlfa;
     aAlfa = Usuario % 810;
     #nomrevli#19 = aAlfa;
*/
|FPRC;

|PROCESO Tipo16Log; |TIPO 16;
/*
     #nomrevli#17 = ~;
     |HORA aAlfa;
     #nomrevli#18 = aAlfa;
     aAlfa = Usuario % 810;
     #nomrevli#19 = aAlfa;
*/
|FPRC;

|PROCESO Tipo20; |TIPO 20;
     |SI FSalida = "OPCION";
          FSalida = " Imprimir ";
     |SINO;
          enEmpresa = #0#0;
          enRegistro = #0#1;
          |SUB_EJECUTA_NP "nomytras";
     |FINSI;
|FPRC;
