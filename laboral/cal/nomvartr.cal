|FICHEROS;
     nomvartr;
     labtraba;
     temp0001@;
     nomcateg;
     nomconca;
     nomhortp;

     laborn03@;
     laborn07@;
|FIN;

|VARIABLES;
     nModo = 0;
     nPos1 = 0;
     nPos2 = 0;

     nContrato = 0;
     aAlfa = "";
     &nEmpExt = 0;
     &nTraExt = 0;
     &nTipoVar = 0;
     &nNuevoVal = 0;
     &aNuevoTra = "";
     &nNuevoAFI = 0;
     &nAntigVal = 0;
     &nAntigVal2 = 0;
     &aNuevoVal1 = "";
     &nNuevoVal2 = 0;
     &nNuevoVal3 = 0;
     &nNuevoVal4 = 0;
     &nNuevaCat = 0;
     &aNombreCat = "";
     &nHorasTC2 = 0;
     &aClaveSal1 = "";
     &aClaveSal2 = "";
     &aAntigOcu = "";
     &aNuevaOcu = "";
     &aNuevaDesOcu = "";
     aParam = "";
     &eaUsuario = "";
     &eaFecha = "";
     &eaHora = "";
     nCampo = 0;
     nCampo2 = 0;
     &eaFiniquito = "";
     &efFechaAlta = @;
     aAlfa1 = "";
     aAlfa2 = "";
     &eaVengoDe = "nomvartr";
     FEstado = 0;
     nCampo1 = 0;
     aBase = "";
     aFichLab = "";
     aMas = "";
     nNume1 = 0;
     nNume2 = 0;
     &nLabornet = 0;
     &swMeHeSalido = 0;
     &swCambioJor = 0;
|FIN;

|PROCESO CompruebaCat; |TIPO 0;
     |SI #nomvartr#2 = 2;
          |ABRE #nomcateg;
          #nomcateg#0 = #labtraba#87;
          #nomcateg#1 = #nomvartr#19;
          |LEE 000#nomcateg.=;
          |SI FSalida = 0;
               #nomvartr#19 = #nomcateg#1;
               |PINTA #nomvartr#19;
               aNombreCat = #nomcateg#2 % 115;
               |ATRI I;
               |PINTA 1655, aNombreCat;
               |ATRI 0;
          |SINO;
               aAlfa = "    ATENCION: Categoria no definida en convenio. Pulse F5 ";
               aAlfa = aAlfa + "para consultar.";
               |MENAV aAlfa;
               |ERROR;
          |FINSI;
          |CIERRA #nomcateg;
     |FINSI;
|FPRC;

|PROCESO ConsultaCat; |TIPO 0;
     |SI FSalida = 13;
          aAlfa = "SELECT nomcateg.1,nomcateg.2 FROM nomcateg INTO tm" + Usuario;
          aAlfa = aAlfa + ".def WHERE 0 == " + #labtraba#87;
          |SQL aAlfa;
          |SI FSalida ! 0;
               |MENAV "    No hay categorias definidas en el convenio";
               |ERROR;
               |ACABA;
          |FINSI;
          |DBASE aAlfa; |QBF aAlfa; aAlfa = aAlfa + "def/tm" + Usuario + ".mas";
          |CARGA_DEF aAlfa , temp0001@;
          |SI FSalida = 0;
               aAlfa = "tm" + Usuario; |NOME_DAT #temp0001@#aAlfa#;
               |ABRE #temp0001@;
               nModo = 2 + 4 + 8 + 16;
               nPos1 = 1455; nPos2 = 2380;
               |C_V #temp0001@#0, 1, "S";
               |C_V #temp0001@#1, 1, "S";
               |LINEAL_SIMPLE #1#temp0001@,nModo, nPos1, nPos2, NULL, NULL, NULL;
               |SI FSalida = 0;
                    #nomvartr#19 = #temp0001@#0;
                    |PINTA #nomvartr#19;
                    aNombreCat = #temp0001@#1;
                    aNombreCat = aNombreCat % 115;
                    |ATRI I;
                    |PINTA 1655, aNombreCat;
                    |ATRI 0;
               |SINO;
                    |ERROR;
               |FINSI;
               |CIERRA #temp0001@;
               |DELINDEX #temp0001@;
               |DESCARGA_DEF #temp0001@;
          |FINSI;
          |DBASE aAlfa; |QBF aAlfa; aAlfa = aAlfa + "def/tm" + Usuario + ".mas";
          |FBORRA aAlfa;
          |ACABA;
     |FINSI;
|FPRC;

|PROCESO Datos;
     #nomvartr#3 = #labtraba#45;             || contrato
     #nomvartr#4 = #labtraba#33;             || grupo tarifa
     #nomvartr#5 = #labtraba#204;            || ocupacion
     #nomvartr#6 = #labtraba#234;            || horas
     #nomvartr#7 = (#nomvartr#6 / 40 * 1000) ! 0;
     |PINTA #nomvartr#3;
     |PINTA #nomvartr#4;
     |PINTA #nomvartr#5;
     |PINTA #nomvartr#6;
     |PINTA #nomvartr#7;
     #nomvartr#24 = #labtraba#48;            || finiquito S/N
|FPRC;

|PROCESO Ayuda; |TIPO 7;
               ||      3         4         5
               ||  67890123456789012345678901234
     |PUSHV 15202380;
     |PINTA 1626, "                                            ";
     |PINTA 1726, "                                            ";
     |PINTA 1826, "                                            ";
     |PINTA 1926, "                                            ";
     |ATRI R;
     |PINTA 1626, " 1. Cambio de contrato o de  ";
     |PINTA 1726, "    de jornada               ";
     |PINTA 1826, " 2. Cambio de grupo cotiz.   ";
     |PINTA 1926, " 3. Cambio de ocupacion      ";
     |ATRI 0;
|FPRC;

|PROCESO Variacion;
     |POPV;

     |CAMPO_MODIFICA #nomvartr#22, 1, "N";          || campo ocupacion
     |CAMPO_VISUAL #nomvartr#22, 1, "N";
     |PINTA 1558, "   ";

     |SI #nomvartr#2 = 2; |O #nomvartr#2 = 3;
          |CAMPO_MODIFICA #nomvartr#10, 1, "N";
          |CAMPO_MODIFICA #nomvartr#11, 1, "N";
          |CAMPO_MODIFICA #nomvartr#12, 1, "N";
          |CAMPO_MODIFICA #nomvartr#13, 1, "N";
          |CAMPO_MODIFICA #nomvartr#15, 1, "N";
          |CAMPO_MODIFICA #nomvartr#18, 1, "N";
          |CAMPO_MODIFICA #nomvartr#19, 1, "N";
          |CAMPO_MODIFICA #nomvartr#14, 1, "N";
          |CAMPO_MODIFICA #nomvartr#21, 1, "N";
          |CAMPO_MODIFICA #nomvartr#20, 1, "N";

          |CAMPO_VISUAL #nomvartr#10, 1, "N";
          |CAMPO_VISUAL #nomvartr#11, 1, "N";
          |CAMPO_VISUAL #nomvartr#12, 1, "N";
          |CAMPO_VISUAL #nomvartr#13, 1, "N";
          |CAMPO_VISUAL #nomvartr#15, 1, "N";
          |CAMPO_VISUAL #nomvartr#18, 1, "N";
          |CAMPO_VISUAL #nomvartr#19, 1, "N";
          |CAMPO_VISUAL #nomvartr#14, 1, "N";
          |CAMPO_VISUAL #nomvartr#21, 1, "N";
          |CAMPO_VISUAL #nomvartr#20, 1, "N";

          |PINTA 1526, "                                           ";
          |PINTA 1626, "                                           ";
          |PINTA 1726, "                                           ";
          |PINTA 1826, "                                           ";
          |PINTA 1926, "                                           ";

          #nomvartr#9 = 0;
          |CAMPO_MODIFICA #nomvartr#9, 1, "S";
          |CAMPO_VISUAL #nomvartr#9, 1, "S";
          |PINTA #nomvartr#9;

          |SI #nomvartr#2 = 2;
               |CAMPO_MODIFICA #nomvartr#19, 1, "S";
               |CAMPO_VISUAL #nomvartr#19, 1, "S";
               |CAMPO_POSICION #nomvartr#19, 1, 1651;

               #nomvartr#19 = #labtraba#17;
               |PINTA #nomvartr#19;

               |ABRE #nomcateg;
               #nomcateg#0 = #labtraba#87;
               #nomcateg#1 = #labtraba#17;
               |LEE 000#nomcateg.=;
               aNombreCat = #nomcateg#2 % 115;
               |ATRI I;
               |PINTA 1655, aNombreCat;
               |ATRI 0;
               |CIERRA #nomcateg;

               aAlfa = "Nuevo Grupo Cotizacion :";
               |PINTA 1526, aAlfa;
               aAlfa = "Nueva categoria :";
               |PINTA 1626, aAlfa;
               |PINTA 1726, "-----------------------------";
          |FINSI;
          |SI #nomvartr#2 = 3;
               |CAMPO_MODIFICA #nomvartr#9, 1, "N";
               |CAMPO_VISUAL #nomvartr#9, 1, "N";
               |PINTA 1550, "              ";

               |CAMPO_MODIFICA #nomvartr#22, 1, "S";
               |CAMPO_VISUAL #nomvartr#22, 1, "S";
               |CAMPO_POSICION #nomvartr#22, 1, 1551;
               |PINTA #nomvartr#22;

               aAlfa = "Nueva ocupacion .......:";
               |PINTA 1526, aAlfa;
               |PINTA 1626, "-----------------------------";
          |FINSI;
     |FINSI;
     |SI #nomvartr#2 = 1;
          |CAMPO_MODIFICA #nomvartr#9, 1, "S";
          |CAMPO_VISUAL #nomvartr#9, 1, "S";
          |CAMPO_MODIFICA #nomvartr#18, 1, "S";
          |CAMPO_VISUAL #nomvartr#18, 1, "S";
          |CAMPO_MODIFICA #nomvartr#14, 1, "S";
          |CAMPO_VISUAL #nomvartr#14, 1, "S";
          |CAMPO_MODIFICA #nomvartr#20, 1, "S";
          |CAMPO_VISUAL #nomvartr#20, 1, "S";
          |CAMPO_MODIFICA #nomvartr#21, 1, "S";
          |CAMPO_VISUAL #nomvartr#21, 1, "S";

          aAlfa = "Nuevo Contrato ........:                    ";
          |PINTA 1526, aAlfa;
          #nomvartr#9 = #nomvartr#3;
          |PINTA #nomvartr#9;

                    ||      3         4         5         6
                    ||  678901234567890123456789012345678901
          ||PINTA 1626, "Tipo de Horas (D/S/M/A):     ";
          |PINTA 1626, "Horas Pactadas .....(A):     ";
          |PINTA 1726, "Horas Jornada Maxima(B):     ";
          |PINTA 1826, "Coeficiente .(A*1000/B):     (     )";
          |PINTA 1926, "Clave de Salario ......:            ";

          |ATRI I;
          ||PINTA 1640, "(D/S/M/A)";
          |PINTA 1646,       "(A)";
          |PINTA 1746,       "(B)";
          |PINTA 1839, "(A*1000/B)";
          |PINTA 1664, "Horas";
          |PINTA 1764, " TC2 ";
          |ATRI 0;

          |CAMPO_MODIFICA #nomvartr#10, 1, "S";
          |CAMPO_MODIFICA #nomvartr#11, 1, "S";
          |CAMPO_MODIFICA #nomvartr#12, 1, "S";
          |CAMPO_MODIFICA #nomvartr#13, 1, "S";
          |CAMPO_MODIFICA #nomvartr#14, 1, "S";
          |CAMPO_MODIFICA #nomvartr#15, 1, "S";
          |CAMPO_MODIFICA #nomvartr#20, 1, "S";
          |CAMPO_MODIFICA #nomvartr#21, 1, "S";

          |CAMPO_VISUAL #nomvartr#10, 1, "S";
          |CAMPO_VISUAL #nomvartr#11, 1, "S";
          |CAMPO_VISUAL #nomvartr#12, 1, "S";
          |CAMPO_VISUAL #nomvartr#13, 1, "S";
          |CAMPO_VISUAL #nomvartr#14, 1, "S";
          |CAMPO_VISUAL #nomvartr#15, 1, "S";
          |CAMPO_VISUAL #nomvartr#20, 1, "S";
          |CAMPO_VISUAL #nomvartr#21, 1, "S";

          |PINTA #nomvartr#10;
          |PINTA #nomvartr#11;
          |PINTA #nomvartr#12;
          |PINTA #nomvartr#13;
          |PINTA #nomvartr#14;
          |PINTA #nomvartr#15;
          |PINTA #nomvartr#18;
          |PINTA #nomvartr#20;
          |PINTA #nomvartr#21;
     |FINSI;
|FPRC;

|PROCESO Comprobaciones;
     |SI #nomvartr#2 = 1;
          nContrato = #nomvartr#Campo#;
          #nomvartr#14 = #labtraba#42;
          |SI 201 [ nContrato; |Y nContrato [ 299;
                #nomvartr#14 = "X";
          |FINSI;
          |SI 501 [ nContrato; |Y nContrato [ 599;
               #nomvartr#14 = "X";
          |FINSI;
          #nomvartr#21 = #labtraba#64;
          |PINTA #nomvartr#14;
          |PINTA #nomvartr#21;
     |FINSI;
     |SI #nomvartr#2 = 2;
          |SI #nomvartr#9 < 1; |O #nomvartr#9 > 12;
               |MENAV "    ATENCION: Valor no valido para el Grupo de Cotizacion";
               |ERROR;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO TP;
     |SI #nomvartr#2 = 1; |Y #nomvartr#18 = "P";
          |CAMPO_MODIFICA #nomvartr#10, 1, "S";
          |CAMPO_MODIFICA #nomvartr#11, 1, "S";
          |CAMPO_MODIFICA #nomvartr#12, 1, "S";
          |CAMPO_MODIFICA #nomvartr#13, 1, "S";
          |CAMPO_MODIFICA #nomvartr#15, 1, "S";
          |CAMPO_MODIFICA #nomvartr#20, 1, "S";
     |SINO;
          |CAMPO_MODIFICA #nomvartr#10, 1, "N";
          |CAMPO_MODIFICA #nomvartr#11, 1, "N";
          |CAMPO_MODIFICA #nomvartr#12, 1, "N";
          |CAMPO_MODIFICA #nomvartr#13, 1, "N";
          |CAMPO_MODIFICA #nomvartr#15, 1, "N";
          |CAMPO_MODIFICA #nomvartr#20, 1, "N";
     |FINSI;

     #nomvartr#11 = 0;
     #nomvartr#12 = 0;
     #nomvartr#13 = 0;
     #nomvartr#15 = 0;
     #nomvartr#20 = 0;

     |PINTA #nomvartr#11;
     |PINTA #nomvartr#12;
     |PINTA #nomvartr#13;
     |PINTA #nomvartr#14;
     |PINTA #nomvartr#15;
     |PINTA #nomvartr#20;

     |ABRE #nomconca;
     #nomconca#0 = #labtraba#87;
     |LEE 000#nomconca.=;
     |SI FSalida = 0;
          |SI #nomconca#162 = 0;
               #nomvartr#12 = 40;
          |SINO;
               #nomvartr#12 = #nomconca#162;
          |FINSI;
     |FINSI;
     |PINTA #nomvartr#12;
     |CIERRA #nomconca;
|FPRC;

|PROCESO coeficiente;
     #nomvartr#13 = ((#nomvartr#11 * 1000) / #nomvartr#12) ! 0;
     |SI #nomvartr#13 > 1000;
          #nomvartr#13 = 0;
     |FINSI;
     |PINTA #nomvartr#13;

     |SI #nomvartr#10 = "S";
          #nomvartr#15 = #nomvartr#11;
     |SINO;
          #nomvartr#15 = 0;
     |FINSI;
     |PINTA #nomvartr#15;
|FPRC;

|PROCESO HorasTC2;
     #nomvartr#20 = #nomvartr#15 / 7;
     |PINTA #nomvartr#20;
|FPRC;

|PROCESO HorasTP;
     |SI #nomvartr#15 = 0; |Y #nomvartr#2 = 4;
          aAlfa = "    Debera indicar el nuevo numero de horas TP para el trabajador";
          aAlfa = aAlfa + " en su ficha.";
          |MENAV aAlfa;
     |FINSI;
|FPRC;

|PROCESO PreClave; |TIPO 7;
     |SI #nomvartr#14 = "A"; |O #nomvartr#14 = "E";
     |O #nomvartr#14 = "M"; |O #nomvartr#14 = "D";
          #nomvartr#21 = #nomvartr#14; |PINTA #nomvartr#21;
     |FINSI;
     |SI #nomvartr#14 = "A"; |O #nomvartr#14 = "E";
          |CAMPO_MODIFICA #nomvartr#21, 1, "N";
     |SINO;
          |CAMPO_MODIFICA #nomvartr#21, 1, "S";
     |FINSI;
|FPRC;

|PROCESO Comp;
     |SI #nomvartr#17 = "A"; |Y #nomvartr#16 = "N";
          aAlfa = "    Es necesario realizar la copia para poder obtener ";
          aAlfa = aAlfa + "el Lote AFI";
          |MENAV aAlfa;
          |POSICIONATE #nomvartr#16;
     |FINSI;
     |SI #nomvartr#17 = "S"; |Y #nomvartr#16 = "N";
          aAlfa = "    Es necesario realizar la copia para poder obtener ";
          aAlfa = aAlfa + "el Lote AFI";
          |MENAV aAlfa;
          |POSICIONATE #nomvartr#16;
     |FINSI;
     |SI #nomvartr#17 = "S";
          || primero comprueba el RDN

          |SI #nomvartr#2 = 3;
               aAlfa = "    Este tipo de variación no se puede enviar a la ";
               aAlfa = aAlfa + "SS Online.";
               |MENAV aAlfa;
               #nomvartr#17 = "A";
               |PINTA #nomvartr#17;
               |ERROR;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Horario;
     |ABRE #laborn07@;    || el #laborn07
     |ABRE #nomhortp;         || carga_def del nomhortp

     #laborn07@#0 = "P";
     #laborn07@#1 = #laborn03@#1;
     #laborn07@#2 = #laborn03@#2;
     #laborn07@#3 = #laborn03@#3;
     #laborn07@#4 = #laborn03@#4;
     |LEE 000#laborn07@.=;
     |SI FSalida = 0;
          aAlfa1 = aNuevoTra % 105;
          aAlfa2 = aNuevoTra % -105;

          nNume1 = aAlfa1;
          nNume2 = aAlfa2;

          #nomhortp#0 = nNume1;
          #nomhortp#1 = nNume2;
          |LEE 101#nomhortp.=;
          FEstado = FSalida;

          |PARA nCampo = 2; |SI nCampo [ 66; |HACIENDO nCampo = nCampo + 1;
               nCampo1 = nCampo + 5;
               #nomhortp#nCampo# = #laborn07@#nCampo1#;
          |SIGUE;

          |SI FEstado = 0;
               |GRABA 020#nomhortp.a;
          |SINO;
               |GRABA 020#nomhortp.n;
          |FINSI;

          |BORRA 020#laborn07@.a;
          #laborn07@#0 = "A";
          |GRABA 020#laborn07@.n;
          |LIBERA #laborn07@;
     |FINSI;

     |CIERRA #nomhortp;
     |CIERRA #laborn07@;
|FPRC;

|PROCESO ActLabornet;
     |DBASS aBase;  |QBF aBase;
     aFichLab = aBase + "laboral/fich/";

     aMas  = aBase + "laboral/def/laborn03.mas";
     |CARGA_DEF aMas, laborn03@;
     |SI FSalida ! 0;
         aAlfa = "0000 No puedo cargar el Def " + aMas;
         |MENAV aAlfa;
         |DESCARGA_DEF #laborn03@;
         |ACABA;
     |FINSI;

     aMas  = aBase + "laboral/def/laborn07.mas";
     |CARGA_DEF aMas, laborn07@;
     |SI FSalida ! 0;
         aAlfa = "0000 No puedo cargar el Def " + aMas;
         |MENAV aAlfa;
         |DESCARGA_DEF #laborn03@;
         |DESCARGA_DEF #laborn07@;
         |ACABA;
     |FINSI;


     |PATH_DAT #laborn03@#aFichLab#;
     |PATH_DAT #laborn07@#aFichLab#;

     |ABRE #laborn03@;
     #laborn03@#0 = "P";         || estado: pendiente, consulta usuario
     #laborn03@#1 = eaUsuario;
     #laborn03@#2 = "";
     #laborn03@#3 = eaFecha;
     #laborn03@#4 = eaHora;
     |LEE 101#laborn03@.=;

     |PARA nCampo = 0; |SI nCampo [ 25; |HACIENDO nCampo = nCampo + 1;
          nCampo2 = nCampo + 5;
          #nomvartr#nCampo# = #laborn03@#nCampo2#;
     |SIGUE;
     #nomvartr#16 = "S";
     #nomvartr#17 = "S";

     swCambioJor = 0;
     |SI #laborn03@#38 = "S";
          swCambioJor = 1;
     |FINSI;

     FSalida = 0;
|FPRC;

|PROGRAMA;
     |CLS;

     aParam = PARAMETRO;
     |QBF aParam;
     |SI aParam = "";
          |PINPA #0#nomvartr;
          |PINDA #0#nomvartr;

          |CAMPO_MODIFICA #nomvartr#22, 1, "N";          || campo ocupacion
          |CAMPO_VISUAL #nomvartr#22, 1, "N";
          |PINTA 1558, "   ";

          |ENDATOS #1#nomvartr;
     |SINO;
          |HAZ ActLabornet;
     |FINSI;
     swMeHeSalido = 0;
     |SI FSalida = 0;
          nEmpExt = #nomvartr#0;
          nTraExt = #nomvartr#1;
          nTipoVar = #nomvartr#2;
          |SI nTipoVar = 1;
               || swCambioJor = 1 (viene de laborn03) si es un cambio de jornada
               nAntigVal = #nomvartr#3;        || contrato anterior
               nAntigVal2 = #nomvartr#6;        || horas TP anterior
               nNuevoVal = #nomvartr#9;        || nuevo contrato
               nNuevoVal2 = #nomvartr#15;      || y nuevas horas TP
               aClaveSal1 = #nomvartr#14;
               aClaveSal2 = #nomvartr#21;
               nHorasTC2 = #nomvartr#20;
          |FINSI;
          |SI nTipoVar = 2;
               nAntigVal = #nomvartr#4;
               nNuevoVal = #nomvartr#9;        || nuevo grupo cot.
               nNuevaCat = #nomvartr#19;
               || lo cargo despues, al hacer la copia en el nomtrapc
               || aNombreCat = #nomcateg#2 % 115;
          |FINSI;
          |SI nTipoVar = 3;
               || nAntigVal = #nomvartr#5;
               || nNuevoVal = #nomvartr#9;        || nueva ocupacion
               aAntigOcu = #nomvartr#5;
               aNuevaOcu = #nomvartr#22;
               aNuevaDesOcu = #nomvartr#23;
          |FINSI;
          aNuevoTra = "";
          nNuevoAFI = 0;
          eaFiniquito = #nomvartr#24;
          efFechaAlta = #nomvartr#25;
          |SI #nomvartr#16 = "S";
               |SUB_EJECUTA "nomtrapc;AUTO";
               |SI aNuevoTra ! "";
                    aAlfa = "    Se ha dado de alta con exito el nuevo trabajador: ";
                    aAlfa = aAlfa + aNuevoTra;
                    |MENAV aAlfa;
               |SINO;
                    aAlfa = "    No se ha podido dar de alta el nuevo trabajador";
                    |MENAV aAlfa;
                    swMeHeSalido = 1;
               |FINSI;
          |FINSI;
          |SI #nomvartr#17 = "A"; |Y aNuevoTra ! "";
               aAlfa = aNuevoTra % 105;
               nEmpExt = aAlfa;
               aAlfa = aNuevoTra % -105;
               nTraExt = aAlfa;
               |SI nTipoVar = 1;
                    nAntigVal = #nomvartr#3;
                    nNuevoVal = #nomvartr#9;
                    aNuevoVal1 = #nomvartr#10;
                    nNuevoVal2 = #nomvartr#11;
                    nNuevoVal3 = #nomvartr#12;
                    nNuevoVal4 = #nomvartr#13;
               |FINSI;
               |SI nTipoVar = 2;
                    nAntigVal = #nomvartr#4;
                    nNuevaCat = #nomvartr#19;
                    || aNombreCat ya tiene que venir cargado
               |FINSI;
               |SI nTipoVar = 3; nAntigVal = #nomvartr#5; |FINSI;

               |SI #labtraba#247 = 163; |O #labtraba#247 = 613;
                    |SUB_EJECUTA "labta611;AUTO";
               |SINO;
                    |SUB_EJECUTA "labparta;AUTO";
               |FINSI;
               |SI nNuevoAFI ! 0;
                    aAlfa = nNuevoAFI;
                    aAlfa = "    Se ha creado un nuevo movimiento AFI pendiente: " + nNuevoAFI;
                    |MENAV aAlfa;
               |SINO;
                    aAlfa = "    No se ha podido generar un nuevo movimiento AFI pendiente";
                    |MENAV aAlfa;
               |FINSI;
          |FINSI;
          |SI #nomvartr#17 = "S"; |Y aNuevoTra ! ""; |Y nLabornet = 0;
               || desde labornet se envian a NetContrata desde el laborn03
               aAlfa1 = aNuevoTra % 105;
               aAlfa2 = aNuevoTra % -105;
               |SI nTipoVar = 1;
                    aAlfa = "labnct07;"+ "04" + aAlfa1 + aAlfa2;
                    |SUB_EJECUTA aAlfa;
               |FINSI;
               |SI nTipoVar = 2;
                    aAlfa = "labnct07;"+ "03" + aAlfa1 + aAlfa2;
                    |SUB_EJECUTA aAlfa;
               |FINSI;
          |FINSI;
     |SINO;
          swMeHeSalido = 1;
     |FINSI;

     |SI aParam ! ""; |Y aNuevoTra ! ""; |Y swMeHeSalido = 0;
          |SI #laborn03@#23 = "P";
               |HAZ Horario;
          |FINSI;

          |CIERRA #laborn03@;

          |DESCARGA_DEF #laborn03@;
          |DESCARGA_DEF #laborn07@;
     |FINSI;
|FPRO;
