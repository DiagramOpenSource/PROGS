|FICHEROS;
     nomplant #0;
     labempre #1;
     labtraba #2;
     labcentr #3;
|FIN;

|VARIABLES;
     selecc = 0;
     empres = 0;
     centro = 0;
     frefer = 0;
     faltas = 0;
     fbajas = 0;
     pempre = 0;
     pcentr = 0;
     mensaje1 = "0000Seleccion vacia.";
     fecsys = @;
|FIN;

|PROCESO comprueba;
     |HAZ pinta;
     |SI empres = 0;  empres = #2#00;  |FINSI;
     |SI centro = 0;  centro = #2#77;  |FINSI;
     |SI empres ! #2#00;             || ruptura por empresa (y por centro a la vez)
          |HAZ centro;
          |HAZ empresa;
     |SINO;
          |SI centro ! #2#77;            || ruptura por centro de trabajo
              |HAZ centro;
          |FINSI;
     |FINSI;
     |HAZ limite;
     |HAZ pasalas;
     |HAZ acumula;
|FPRC;

|PROCESO refer;
     selecc = 1;
     empres = 0;
     centro = 0;
     fecsys = #0#02;  frefer = fecsys;  || pasa a dias fecha de referencia
|FPRC;

|DEFBUCLE nomplant0;
     #2#01;
     44;
     #0#0,     1, "A";
     #0#1, 99999, "A";
     ;
     refer, NULL, NULL, NULL, NULL, comprueba;
     #2#0, #2#77, #2#1;
|FIN;

|PROCESO actua; |TIPO 3;
     selecc = 0;
     |ABRE #1;
     |ABRE #3;
     |BUCLE nomplant0;
     |SI selecc = 0;
          |MENAV mensaje1;
     |SINO;
          |HAZ centro;
          |HAZ empresa;
     |FINSI;
     |CIERRA #1;
     |CIERRA #3;
|FPRC;

|| *************************************************************************
||                        SUBRUTINAS DEL PROCESO
|| *************************************************************************

|PROCESO pinta;
     |PINTA 2026, #2#00;
     |PINTA 2047, #2#01;
     |PINTA 2065, #2#77;
|FPRC;

|PROCESO pasalas;
    fecsys = #2#36;  faltas = fecsys;  || pasa a dias fecha de alta
    fecsys = #2#37;  fbajas = fecsys;  || pasa a dias fecha de baja
|FPRC;

|PROCESO limite;
     |SI #2#36 = "          "; |O #2#36 = "..........";   || si estan vacias
          #2#36 = "01.01.1900";                           || /las coge como
     |FINSI;                                              || /anteriores y
     |SI #2#37 = "          "; |O #2#37 = "..........";   || /posteriores
          #2#37 = "31.12.2999";
     |FINSI;
|FPRC;

|PROCESO acumula;
     |SI frefer ] faltas; |Y frefer [ fbajas;
          pempre = pempre + 1;
          pcentr = pcentr + 1;
     |FINSI;
|FPRC;

|PROCESO centro;
     #3#00 = empres;
     #3#01 = centro;
     |LEE 101#3=;
     |SI FSalida = 0;
          #3#11 = pcentr;
          |GRABA 020#3a;
     |FINSI;
     |LIBERA #3;

     pcentr = 0;
     centro = #2#77;
|FPRC;

|PROCESO empresa;
     #1#00 = empres;
     |LEE 101#1=;
     |SI FSalida = 0;
          #1#14 = pempre;
          |GRABA 020#1a;
     |FINSI;
     |LIBERA #1;
     pempre = 0;
     empres = #2#00;
|FPRC;
