|FICHEROS;
     labacu01 #0;
     labacu02 #100,MANTE;
     labacu03 #101;
     labacu04,MANTE;
     labempre;
     labtraba;
     nomir003;

     ClientPI@ #5000;
|FIN;

|VARIABLES;
     &eaEmailPI = "";

     aAlfa = "";
     aAlfa1 = "";
     aAlfa2 = "";
     aMensa = "";

     FEstado = 0;
     nume1 = 0;
     nCampoEmp = 0;
     nEmpre = 0;
     nCont = 0;
     nHastaCampo = 0;
     nEmpresa = 0;
     nMinGru = 0;
     nMaxGru = 0;
     swF11Emp = 0;
     npara1 = 0;
     nSw    = 0;

     nDEmpre  = 0;
     nHEmpre  = 0;

     nBoton   = 0;

     fecha = @;
     swIntegral = 0;

     &runnom = "labacu01";
     &aEDiscap = "";
     &nSwSisc = 0;
     nBoton1 = 0;
     nTrabajador = 0;
|FIN;

|PROCESO mensaEmp; |TIPO 7;
     |SI #0#2 = 0; |Y #0#19 = 0; |Y Campo ! 2; |Y Campo ! 19;
          |ACABA;
     |FINSI;
     |MENSA "    < F11 > consulta de empresas";
|FPRC;

|PROCESO BuscaEmpresa;
     FEstado = FSalida;

     |SI FSalida = 13; |O swF11Emp = 1;
          |ABRE #labempre;
          |CONSULTA_CLAVE #1#labempre;
          |SI FSalida = 0;
               #0Campo = #labempre#0;
               |PINTA #0Campo;
          |SINO;
               |ERROR;
          |FINSI;
          |CIERRA #labempre;
          swF11Emp = 0;
     |FINSI;

     |DFICE aAlfa1;
     |DFICB aAlfa2;

|FPRC;

|PROCESO F11Emp; |TIPO 66;
     swF11Emp = 1;
     |HAZ BuscaEmpresa;
|FPRC;

|PROCESO CompEmpre;
     |SI #0Campo ! 0;
          |ABRE #labempre;
          #labempre#0 = #0Campo;
          |LEE 000#labempre.=;
          |SI FSalida ! 0;
               aAlfa1 = #0Campo; aAlfa1 = "00000" + aAlfa1; aAlfa1 = aAlfa1 % -105;
               aMensa = "    La Empresa " + aAlfa1 + " no existe";
               |MENAV aMensa;
               |ERROR;
          |FINSI;
          |CIERRA #labempre;
     |FINSI;
|FPRC;

|PROCESO seleccion1; |TIPO 0;
     |SI #0#2 = 0;
          aAlfa1 = "S";
          aAlfa2 = "N";
          |PARA nume1 = 2; |SI nume1 [ 18; |HACIENDO nume1 = nume1 + 1;
               #0nume1 = 0;
               |PINTA #0nume1;
          |SIGUE;
     |SINO;
          |SI nMinGru ! 0;
               || no deberia estar asi, pero si lo quito me iguala a cero
               || el desde empresa y no debe, tengo tiempo de ver por que
               |PARA nume1 = nMinGru; |SI nume1 [ nMaxGru; |HACIENDO nume1 = nume1 + 1;
                    #0nume1 = 0;
               |SIGUE;
               |PINDA #0#0;
          |FINSI;
          aAlfa1 = "N";
          aAlfa2 = "S";
     |FINSI;

     aAlfa = runnom;
     |QBF aAlfa;
     aAlfa = aAlfa % -104;
     |SI aAlfa = "reci";      || si no me ponia como no modificable el campo
          nHastaCampo = 3;    || a¤o y otros en emision de recibos
     |SINO;
          nHastaCampo = 21;
     |FINSI;

     |PARA npara1 = 0; |SI npara1 [ nHastaCampo; |HACIENDO npara1 = npara1 + 1;
          |CAMPO_MODIFICA #0npara1, 1, aAlfa1;
     |SIGUE;
     |PARA npara1 = 2; |SI npara1 [ 19; |HACIENDO npara1 = npara1 + 1;
          |CAMPO_MODIFICA #0npara1, 1, aAlfa2;
     |SIGUE;
     |PARA npara1 = nMinGru; |SI npara1 [ nMaxGru; |HACIENDO npara1 = npara1 + 1;
          |CAMPO_MODIFICA #0npara1, 1, aAlfa1;
     |SIGUE;
|FPRC;

|PROCESO Trabajadores;
     |SI #labacu02#4 = " "; |ACABA; |FINSI;

     #labtraba#0 = #labacu02#0;
     #labtraba#1 = #labacu02#1;
     |LEE 000#labtraba.=;
     |SI FSalida ! 0;
          |ACABA;
     |FINSI;

     |SI nEmpre ! #labtraba#0;
          nEmpre = #labtraba#0;
          nCont = 1;
          nSwSisc = 0;
     |FINSI;

     #labempre#0 = #labtraba#0;
     |LEE 000#labempre.=;
     |SI FSalida ! 0; |DDEFECTO #labempre; |FINSI;
     |SI swIntegral = 1;
          #ClientPI@#0 = #labtraba#0;
          |LEE 000#ClientPI@.=;
          |SI FSalida = 0;
             eaEmailPI = #ClientPI@#71; |QBF eaEmailPI;
             |SI eaEmailPI = "";
                eaEmailPI = "infocursos@inforpyme.eu";
             |FINSI;
          |FINSI;
     |SINO;
          eaEmailPI = "infocursos@inforpyme.eu";
     |FINSI;

     fecha = ~;
     aAlfa = fecha % -104;

     #nomir003#0 = #labtraba#7;
     #nomir003#40 = aAlfa;
     #nomir003#41 = #labtraba#0;
     aEDiscap = "N";

     |LEE 000#nomir003.=;
     |SI FSalida ! 0;
          |LEE 000#nomir003.];
          |SI FSalida = 0;
               |LEE 000#nomir003.a;
               |SI #nomir003#0 = #labtraba#7; |Y #nomir003#41 = #labtraba#0; |Y FSalida = 0;
                    aEDiscap = #nomir003#6;
               |FINSI;
          |SINO;
               |LEE 000#nomir003.u;
               |SI FSalida = 0;
                    |SI #nomir003#0 = #labtraba#7; |Y #nomir003#41 = #labtraba#0;
                         aEDiscap = #nomir003#6;
                    |FINSI;
               |FINSI;
          |FINSI;
     |SINO;
          aEDiscap = #nomir003#6;
     |FINSI;
     nSwSisc = nSwSisc + nCont;
     nCont = nCont + 1;
     |PRINT;
     |SI nCont > 1;
          nCont = 0;
     |FINSI;
|FPRC;

|PROCESO grabatmp;
     |SI #0#2 ! 0;
        nSw = 0;
        |PARA npara1 = 2; |SI npara1 [ 19; |HACIENDO npara1 = npara1 + 1;
           |SI #labacu01#npara1# = #labtraba#0; nSw = 1; |SAL; |FINSI;
        |SIGUE;
        |SI nSw = 0; |ACABA; |FINSI;
     |FINSI;

     #labempre#0 = #labtraba#0;
     |LEE 000#labempre.=;
     |SI FSalida ! 0; |ACABA; |FINSI;

     |DDEFECTO #100;
     #100#0 = #labtraba#0;
     #100#1 = #labtraba#1;
     #100#2 = #labempre#2;
     #100#3 = #labtraba#2;
     |SI #labacu01#22 = "S";
          #100#4 = "*";
     |SINO;
          #100#4 = " ";
     |FINSI;
     |GRABA 020#100n;
     |LIBERA #100;
|FPRC;

|DEFBUCLE labtraba;
     #labtraba#1;
     44;
     nDEmpre, #0#20, "A";
     nHEmpre, #0#21, "A";
     ;
     NULL, grabatmp;
|FIN;

|PROCESO lectura;
     nCont = 1;
     |SI #0#2 = 0;
          nDEmpre = #0#0; nHEmpre = #0#1;   |BUCLE labtraba;
     |SINO;
          nDEmpre = 00000; nHEmpre = 99999; |BUCLE labtraba;
     |FINSI;
|FPRC;

|PROCESO sel_min;
     #100#0 = 00001;
     #100#1 = 00001;
|FPRC;

|PROCESO sel_max;
     #100#0 = 99999;
     #100#1 = 99999;
|FPRC;

|DEFBUCLE Trabajadores;
     #labacu02#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, Trabajadores;
|FIN;

|PROCESO CamposFijos;
     |PUSHV 0811 1568;

     |ABRE #labacu03;
     |LEE 000#labacu03.p;
     |SI FSalida ! 0;
        |DDEFECTO #labacu03;
        |GRABA 020#labacu03.c;
     |FINSI;

     |PINPA #0#labacu03; |PINDA #0#labacu03;
     |ENDATOS #1#labacu03;
     |SI FSalida = 0;
        |GRABA 020#labacu03.a;
     |FINSI;
     |CIERRA #labacu03;

     |POPV;
|FPRC;

|PROCESO Activa2; |TIPO 7;
     |SI #labacu04#1 = "S";
          |CAMPO_MODIFICA #labacu04#2, 1, "S";
     |SINO;
          |CAMPO_MODIFICA #labacu04#2, 1, "N";
          #labacu04#2 = "";
          |PINTA #labacu04#2;
     |FINSI;
|FPRC;

|PROCESO GrabaDatosEmpresa;
     #labacu02#5 = #labacu04#0;
     #labacu02#6 = #labacu04#1;
     #labacu02#7 = #labacu04#2;
     #labacu02#8 = #labacu04#3;
     |GRABA 020#labacu02.a;
     |LIBERA #labacu02;
|FPRC;

|DEFBUCLE GrabaDatosEmpresa;
     #labacu02#1;
     ;
     nEmpresa, 00001;
     nEmpresa, 99999;
     be;
     NULL, GrabaDatosEmpresa;
|FIN;

|PROCESO DatosEmpresa;
     |PUSHV 1014 2067;
     |ATRI R;
     nEmpresa = #labacu02#0;
     nTrabajador = #labacu02#1;

     #labacu04#0 = #labacu02#5;
     #labacu04#1 = #labacu02#6;
     #labacu04#2 = #labacu02#7;
     #labacu04#3 = #labacu02#8;

     |PINPA #0#labacu04;
     |PINDA #0#labacu04;

     aAlfa = nEmpresa;
     |QBF aAlfa;
     aAlfa = ("00000" + aAlfa) % -105;
     |ATRI R;
     |PINTA 1144, aAlfa;
     |ATRI 0;

     |PINTA #labacu04#0;
     |PINTA #labacu04#1;
     |PINTA #labacu04#2;
     |PINTA #labacu04#3;

     |ENDATOS #1#labacu04;
     |SI FSalida = 0;
          |BUCLE GrabaDatosEmpresa;
     |FINSI;

     |POPV;

     #labacu02#0 = nEmpresa;
     #labacu02#1 = nTrabajador;
     |LEE 000#labacu02.=;
     |PONCONTROL 23, "si";
     |PTEC 809;
|FPRC;

|PROGRAMA;
     |CLS;
     |CABEZA "Anexo de adhesion";
     |PINPA #0#0;
     |ABRE #0;
     |DDEFECTO #0;
     |LEE 000#0p;
     |SI FSalida = 0;  |BORRA 020#0a;  |FINSI;
     |CIERRA #0;
     |PINDA #0#0;

     |ABRE #labempre; |ABRE #labtraba; |ABRE #nomir003;
     |ABRE #100;
     |LEE 000#100p;
     |SI FSalida = 0;
        aAlfa = "    SHay datos de una seleccion anterior. " + &191 + " Usar dicha seleccion ?";
        |CONFI aAlfa;
        |SI FSalida = 0;
           FSalida = 0;
        |SINO;
           |CIERRA #100; |DELINDEX #100; |ABRE #100;
           |ENDATOS #1#0;
           |SI FSalida = 0; |HAZ lectura; |FINSI;
        |FINSI;
     |SINO;
        |ENDATOS #1#0;
        |SI FSalida = 0; |HAZ lectura; |FINSI;
     |FINSI;

     |SI FSalida = 0;
        |CONTROL_SIMPLE 0, "BOTON,{{197}}Rellenar campos fijos", 0659, 0678, CamposFijos;
        nBoton = FSalida;

        |CONTROL_SIMPLE 0, "BOTON,{{197}}    Datos empresa    ", 0759, 0778, DatosEmpresa;
        nBoton1 = FSalida;

        |ENTLINEAL #1#100, -1, 4, 22, 1, sel_min, sel_max;

        aAlfa = "    S¿Desea continuar con la generación del informe?";
        |CONFI aAlfa;
        |SI FSalida = 0;
           |ABRE #labacu03;
           |LEE 000#labacu03.p;
           |CIERRA #labacu03;

           Impresora = "CrystalReport";
           |IMPRE -1;
           |SI FSalida = 0;
              |INFOR "labacu01";
              |SI FSalida = 0;
                 swIntegral = 0;
                 |DBASS aAlfa; |QBF aAlfa;
                 aAlfa1 = aAlfa + "integral/def/dsam0000.mas";
                 |CARGA_DEF aAlfa1, ClientPI@;
                 |SI FSalida = 0;
                    swIntegral = 1;
                    |ABRE #ClientPI@;
                    |BUCLE Trabajadores;
                    |CIERRA #ClientPI@; |DESCARGA_DEF #ClientPI@;
                 |SINO;
                    swIntegral = 0;
                    |BUCLE Trabajadores;
                 |FINSI;
                 |PIEINF; |FININF;
              |FINSI;
              |FINIMP;
           |FINSI;
        |SINO;
           aAlfa = "    El proceso ha sido cancelado por el usuario";
           |MENAV aAlfa;
        |FINSI;
        |FIN_CONTROL_WINDOWS nBoton;
        |FIN_CONTROL_WINDOWS nBoton1;
     |FINSI;
     |CIERRA #100;
     |CIERRA #nomir003; |CIERRA #labtraba; |CIERRA #labempre;
|FPRO;
