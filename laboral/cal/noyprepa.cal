|FICHEROS;
    noyprepa #0;
    noyantig #3;    || destinos/antig.
    noytrant #4;    || destinos/antig. trab.
    noypresc #5;    || control prestamos cabs.
    noypresl #6;    || control prestamos lins.
    nomvarca #7;    || variaciones cabs.
    nomvarli #8;    || variaciones lins.
    noysusti #10;   || sustituciones trab.
|FIN;

|VARIABLES;
    alfa1 = "";
    alfa2 = "";
    alfa3 = "";
    alfa4 = "";
    nume1 = 0;
    nume2 = 0;
    nume3 = 0;
    nume4 = 0;
    para1 = 0;
    para2 = 0;
    para3 = 0;
    para4 = 0;
    fech1 = @;
    fech2 = @;
    fech3 = @;
    fech4 = @;
    elsw1 = 0;
    elsw2 = 0;
    elsw3 = 0;
    elsw4 = 0;
    FEstado = 0;
    seleccio = 0;
    modo = 0;

    &runnom = "";
    concepto = 0;
    importe = 0;
    trienios = 0;
    elmes1 = 0;
    elmes2 = 0;
    elany1 = 0;
    elany2 = 0;
    salte_1 = 0;
    salte_2 = 0;

    &ay_emp = 0;
    &ay_tra = 0;
    &ay_mes = 0;
    &ay_any = 0;
    &ay_ant = "";
     aAlfa = "";
|FIN;
____________________________________/ COMUNES
|PROCESO control_antig;
|DDEFECTO #3;
|ABRE #3;
    #3#0 = ay_any;
|LEE 000#3=;
|SI FSalida ! 0;
    ||MENAV "    No existen tabla de Destinos/Antiguedad ...";
|FINSI;
|CIERRA #3;
|FPRC;

|PROCESO var_cabs;
|ABRE #7;
    #7#0 = ay_emp;  || empresa
    #7#1 = ay_tra;  || trabajador
    #7#2 = ay_mes;  || mes
    #7#3 = 0;       || tipo
|LEE 000#7=;
|SI FSalida ! 0;
    |GRABA 020#7n;
|FINSI;
|CIERRA #7;
|FPRC;

|PROCESO var_lins;
|ABRE #8;
    #8#0 = ay_emp;  || empresa
    #8#1 = ay_tra;  || trabajador
    #8#2 = ay_mes;  || mes
    #8#3 = 0;       || tipo
    #8#4 = concepto;     || concepto
|LEE 101#8=;
    FEstado = FSalida;
    alfa1 = "1";      alfa1 = "          " + alfa1;  alfa1=alfa1% -110;
    #8#5 = alfa1;    || unidades
    alfa1 = importe;  alfa1 = "          " + alfa1;  alfa1=alfa1% -110;
    #8#6 = alfa1;    || importe
|SI FEstado = 0;    |GRABA 020#8a; |FINSI;
|SI FEstado ! 0;    |GRABA 020#8n; |FINSI;
|LIBERA #8;
|CIERRA #8;
|FPRC;
____________________________________/ ANTICIPOS
|PROCESO cal_dest;
    nume1 = #4#2;                       || nivel complemento destino
|SI nume1 ! 0; importe = importe + #3nume1;  |FINSI;
|FPRC;

|PROCESO cal_anti;
    importe = importe + (#4#4 * #3#31);      || trienios A1
    importe = importe + (#4#5 * #3#32);      || trienios A2
    importe = importe + (#4#6 * #3#33);      || trienios C1
    importe = importe + (#4#7 * #3#34);      || trienios C2
    importe = importe + (#4#8 * #3#35);      || trienios E
    importe = importe + (#4#9 * #3#41);      || trienios B

    elany1 = 0;     elany2 = 0;    elmes1 = 0;    elmes2 = 0;
    alfa1 = ay_ant;
    alfa2 = alfa1 %  402;     elmes1 = alfa2;     elmes2 = ay_mes;
    alfa2 = alfa1 % -104;     elany1 = alfa2;     elany2 = ay_any;
    nume1 = elany2 - elany1;
|SI elmes2 < elmes1;     nume1 = nume1 - 1;  |FINSI;
    trienios = (nume1 - (nume1 $ 3)) / 3;
    aAlfa = #4#3; |QBF aAlfa;

|SI aAlfa = "A1";    importe = importe + (trienios * #3#31);      |FINSI;
|SI aAlfa = "A2";    importe = importe + (trienios * #3#32);      |FINSI;
|SI aAlfa = "C1";    importe = importe + (trienios * #3#33);      |FINSI;
|SI aAlfa = "C2";    importe = importe + (trienios * #3#34);      |FINSI;
|SI aAlfa = "E";     importe = importe + (trienios * #3#35);      |FINSI;
|SI aAlfa = "B";     importe = importe + (trienios * #3#41);      |FINSI;
|FPRC;

|PROCESO cal_suel;
aAlfa = #4#3; |QBF aAlfa;
|SI aAlfa = "A1";    importe = #3#36;    |FINSI;
|SI aAlfa = "A2";    importe = #3#37;    |FINSI;
|SI aAlfa = "C1";    importe = #3#38;    |FINSI;
|SI aAlfa = "C2";    importe = #3#39;    |FINSI;
|SI aAlfa = "E";     importe = #3#40;    |FINSI;
|SI aAlfa = "B";     importe = #3#42;    |FINSI;
|FPRC;

|PROCESO anti_dest;
|ABRE #4;
    #4#0 = ay_emp;       || empresa
    #4#1 = ay_tra;       || trabajador
|LEE 000#4=;
|SI FSalida = 0;
        concepto = 101;
        importe = 0;
    |HAZ cal_suel;
    |SI importe ! 0;
        |HAZ var_lins;
        |HAZ var_cabs;
    |FINSI;

        concepto = 102;
        importe = 0;
    |HAZ cal_anti;
    |SI importe ! 0;
        |HAZ var_lins;
        |HAZ var_cabs;
    |FINSI;

        concepto = 110;
        importe = 0;
    |HAZ cal_dest;
    |SI importe ! 0;
        |HAZ var_lins;
        |HAZ var_cabs;
    |FINSI;
|FINSI;
|CIERRA #4;
|FPRC;
____________________________________/ PRESTAMOS
|PROCESO cal_pres;
    #6#0 = #5#0;                   || empresa
    #6#1 = #5#1;                   || trabajador
    #6#2 = #5#2;                   || prestamo
    #6#4 = (ay_any * 100) + ay_mes;|| a¤o-mes (AAAAMM)
|LEE 101#6=;
|SI FSalida = 0;|Y #6#6 = "N";
        importe = importe + #6#5;       || acumula importe
        #6#6 = "S";                     || act. liquidado
    |GRABA 020#6a;
    |SI FSalida = 0;
            #5#9 = #5#9 - #6#5;             || act. pte. liquidar
        |GRABA 020#5a;
    |FINSI;
|FINSI;
|LIBERA #6;
|FPRC;

|PROCESO prestamos1;
|SI #5#9 ! 0;                 || si hay algo para liquidar
        concepto = #5#4;
    |HAZ cal_pres;
    |SI importe ! 0;
        |HAZ var_lins;
        |HAZ var_cabs;
    |FINSI;
|FINSI;
|LIBERA #5;
|FPRC;

|DEFBUCLE prestamos;
 #5#1;
 ;
 ay_emp, ay_tra,  1;
 ay_emp, ay_tra, 99;
 be;
 NULL, prestamos1;
|FIN;

|PROCESO prestamos;
    importe = 0;
|ABRE #6;
|SELECT #2#6;
|BUCLE prestamos;
|CIERRA #6;
|FPRC;
____________________________________/ PROCESO PRINCIPAL
|PROGRAMA;
|ABRE #10;
    #10#0 = ay_emp;
    #10#1 = ay_tra;
    #10#6 = ay_any;
    #10#2 = ay_mes;
|LEE 000#10=;                 || si es un sustituto no lo hace
    FEstado = FSalida;        ||/desde el calculo de nominas
|CIERRA #10;                  ||/lo hace desde el proceso de sustituciones!!!

|SI FEstado ! 0;|O runnom = "noysusti";
    |HAZ control_antig;       || lee tarifas
    |HAZ anti_dest;           || calcula antiguedad/destino/sueldos
    |HAZ prestamos;           || calcula anticipos por prestamos
|FINSI;
|FPRO;
