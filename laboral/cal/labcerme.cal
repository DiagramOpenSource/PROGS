|FICHEROS;
     labcerme #0;
     labempre #1;
     labtraba #2;
     labcentr;
     nomvarca #9;
     nomcontr;
     nomcalli;
     nomhicli;
     nomcaldl;
     pdfimtmp #100;

     tempmant@ #200;
     labpdfli;
|FIN;

|VARIABLES;
     nMesBaja = 0;
     nAnyBaja = 0;
     nDiasVac = 0;
     afechaBaja = "";
     aMesBaja = "";
     aAnyBaja = "";
     fFecha = @;
     fecsys = @;
     fecalf = "";
     alfa1 = "";
     &aMes = "";
     &nMes = 0;
     xmesnume = 0;
     aFechaBaja = "";

     BaseHE = 0;
     nBaseCot = 0;
     nDiasCot = 0;
     i = 0;
     nBuscaMeses = 0;
     nume4 = 0;
     nume1 = 0;
     nCampo = 0;
     nBaseMat = 0;
     nAnyLin = 0;
     nAny = 0;
     aEstoyEn = "";
     FEstado = 0;
     aFechaIni = "";
     aAlfa = "";

     nEmp = 0;
     nTra = 0;
     fFechaDesde = @;
     fFechaAlta = @;
     aAlfa1 = "";
     aAlfa2 = "";
     aAlfa3 = "";
     aNif = "";
     aCampo = "";
     aValor = "";
     &aArchivo = "";
     nModo = 0;
     nAscii = 0;
     aCaracter = "";
     aCaracterSal = "";
     aCadenaSal = "";
     aCadena = "";
     aLongitud = "";
     nLongitud = 0;
     nPos = 0;
     nBusca = 0;
     nTipo = 0;
     &aFecha = "";
     nNume = 0;
     aTipoCampo = "";
     aValorC = "";
     &nProcedencia = "labcerme";
     &nContador = 0;
     &nTipoSalida = 0;
     &nHoja = 0;
     &nTotalHojas = 0;
     &aListaFich = "";
     swTP = 0;
     numer = 0;
     anynum = 0;
     mesnum = 0;
     bisiesto = 0;
     topemes = 0;
     nDias = 0;
     hmes = 0;
     hany = 0;
     aAny = "";
     aFecha1 = "";
     fFecha1 = @;
     aFecha2 = "";
     fFecha2 = @;
     aFechaAlta = "";
     fFechaBaja = @;
     nFecha1 = 0;
     nFecha2 = 0;
     aDia = "";
|FIN;

|PROCESO FechaFirma; |TIPO 7;
     aAlfa = #0#83;
     |QBF aAlfa;
     fecsys = ~;
     |SI aAlfa = ""; |O aAlfa = "00.00.0000";
          #0#83 = fecsys;
     |FINSI;
     |PINTA #0#83;
|FPRC;

|PROCESO Fecha;
     aFecha = #0Campo;
     |HAZ CompFecha_plb;
|FPRC;

|PROCESO BuscaVac;
     |ABRE #nomcalli;
     |ABRE #nomhicli;
     #nomcalli#0 = #0#0;
     #nomcalli#1 = #0#1;
     #nomcalli#2 = nMesBaja;
     #nomcalli#3 = 2;
     #nomcalli#4 = 125;
     |LEE 000#nomcalli.=;
     |SI FSalida = 0;
          nDiasVac = (#nomcalli#11 + 0.49) ! 0;
     |SINO;
          #nomhicli#0 = #0#0;
          #nomhicli#1 = #0#1;
          #nomhicli#12 = nAnyBaja;
          #nomhicli#2 = nMesBaja;
          #nomhicli#3 = 2;
          #nomhicli#4 = 125;
          |LEE 000#nomhicli.=;
          |SI FSalida = 0;
               nDiasVac = (#nomhicli#11 + 0.49) ! 0;
          |FINSI;
          |CIERRA #nomcalli;
          |CIERRA #nomhicli;
     |FINSI;
|FPRC;

|PROCESO PintaFechas;
     nDiasVac = #0#6;
     aFechaBaja = #0#4;
     |SI nDiasVac ! 0;
          #0#5 = "S";
          #0#6 = nDiasVac;
          fFecha = aFechaBaja;
          fFecha = fFecha + 1;
          #0#7 = fFecha;
          fFecha = fFecha + nDiasVac;
          #0#8 = fFecha;

          |PINTA #0#5;
          |PINTA #0#6;
          |PINTA #0#7;
          |PINTA #0#8;
     |FINSI;
|FPRC;

|PROCESO finiquito;
     |SI #0Campo = Contenido;
          |ACABA;
     |FINSI;

     nDiasVac = 0;
     aFechaBaja = #0#4;
     |QBF aFechaBaja;
     |SI aFechaBaja ! "";
          aMesBaja = aFechaBaja % 402;
          aAnyBaja = aFechaBaja % -104;
          nMesBaja = aMesBaja;
          nAnyBaja = aAnyBaja;
          nAnyBaja = nAnyBaja - 2000;

          |HAZ BuscaVac;

          |HAZ PintaFechas;
     |FINSI;
|FPRC;

|PROCESO variacion; |TIPO 7;
     |SI #0#4 ! "          ";
          |ACABA;
     |FINSI;

     |ABRE #2;
     #2#0 = #0#0;
     #2#1 = #0#1;
     |LEE 000#2=;

     fecsys = ~;
     fecalf = fecsys;
     alfa1 = fecalf % 402;
     aMes = alfa1;
     nMes = aMes;

     nMes = nMes + 1;
     |SI nMes = 13;
          nMes = 01;
     |FINSI;
     xmesnume = nMes - 5;
     |SI xmesnume < 1;
          xmesnume = 1;
     |FINSI;
     |ABRE #9;
     #9#0 = #0#0;    || empresa
     #9#1 = #0#1;    || trabajador
     #9#3 = 0;       || tipo

     aFechaBaja = "";
     |PARA nMes = nMes; |SI nMes ] xmesnume; |HACIENDO nMes = nMes - 1;
          #9#2 = nMes;   || mes
          |LEE 000#9=;
          |SI FSalida = 0;
               |SI #9#16 ! ".........."; |Y #9#16 ! "          ";
                    aFechaBaja = #9#16;
                    |SAL;
               |FINSI;
          |FINSI;
          |SI nMes = 1; |Y xmesnume = 1;     || si no en diciembre va mal
               nMes = 13;                    || me voy a diciembre
               xmesnume = 10;                || y miro un par de meses
          |FINSI;                            || mas atras
     |SIGUE;
     |CIERRA #9;

     |SI aFechaBaja = "";
          |SI #2#37 ! ".........."; |Y #2#37 ! "          ";
               aFechaBaja = #2#37;
          |FINSI;
     |FINSI;
     #0#4 = aFechaBaja;

     |PINTA #0#4;
     |CIERRA #2;
|FPRC;

|PROCESO ActivaVac; |TIPO 7;
     aFechaBaja = #0#4;
     |QBF aFechaBaja;
     |SI aFechaBaja = "";
          |CAMPO_MODIFICA #0#5, 1, "N";
          |CAMPO_MODIFICA #0#6, 1, "N";
          |CAMPO_MODIFICA #0#7, 1, "N";
          |CAMPO_MODIFICA #0#8, 1, "N";

          #0#5 = "N";
          #0#6 = 0;
          #0#7 = "";
          #0#8 = "";

          |PINTA #0#5;
          |PINTA #0#6;
          |PINTA #0#7;
          |PINTA #0#8;
     |SINO;
          |CAMPO_MODIFICA #0#5, 1, "S";
          |CAMPO_MODIFICA #0#6, 1, "S";
          |CAMPO_MODIFICA #0#7, 1, "S";
          |CAMPO_MODIFICA #0#8, 1, "S";
     |FINSI;
|FPRC;

|PROCESO ActivaDis; |TIPO 7;
     |SI #0#9 = "N";
          |CAMPO_MODIFICA #0#10, 1, "N";
          |CAMPO_MODIFICA #0#11, 1, "N";

          #0#10 = "";
          #0#11 = "";

          |PINTA #0#10;
          |PINTA #0#11;
     |SINO;
          |CAMPO_MODIFICA #0#10, 1, "S";
          |CAMPO_MODIFICA #0#11, 1, "S";
     |FINSI;
|FPRC;

|PROCESO ActivaGua; |TIPO 7;
     |SI #0#12 = "N";
          |CAMPO_MODIFICA #0#13, 1, "N";

          #0#13 = "";

          |PINTA #0#13;
     |SINO;
          |CAMPO_MODIFICA #0#13, 1, "S";
     |FINSI;
|FPRC;

|PROCESO ulti_dia;  || halla el ultimo dia del mes
    numer = anynum $ 4;
|SI numer = 0;
        bisiesto = 1;
|SINO;
        bisiesto = 0;
|FINSI;
|SI mesnum =  1;  topemes = 31;            |FINSI;
|SI mesnum =  2;  topemes = 28 + bisiesto; |FINSI;
|SI mesnum =  3;  topemes = 31;            |FINSI;
|SI mesnum =  4;  topemes = 30;            |FINSI;
|SI mesnum =  5;  topemes = 31;            |FINSI;
|SI mesnum =  6;  topemes = 30;            |FINSI;
|SI mesnum =  7;  topemes = 31;            |FINSI;
|SI mesnum =  8;  topemes = 31;            |FINSI;
|SI mesnum =  9;  topemes = 30;            |FINSI;
|SI mesnum = 10;  topemes = 31;            |FINSI;
|SI mesnum = 11;  topemes = 30;            |FINSI;
|SI mesnum = 12;  topemes = 31;            |FINSI;
|FPRC;

|PROCESO Dias;
     nDias = 0;

     aMes = hmes;
     |QBF aMes;
     aMes = ("00" + aMes) % -102;
     anynum = hany + 2000;
     aAny = anynum;
     aFecha1 = "01." + aMes + "." + aAny;
     fFecha1 = aFecha1;

     mesnum = hmes;
     |HAZ ulti_dia;
     aDia = topemes;
     aFecha2 = aDia + "." + aMes + "." + aAny;
     fFecha2 = aFecha2;

     aFechaAlta = #labtraba#36;
     aFechaBaja = #labtraba#37;
     |QBF aFechaBaja;
     |SI aFechaBaja = ""; |O aFechaBaja = "..........";
          aFechaBaja = "31.12.2099";
     |FINSI;

     fFechaAlta = aFechaAlta;
     fFechaBaja = aFechaBaja;

     |SI fFechaAlta > fFecha1;
          fFecha1 = fFechaAlta;
     |FINSI;
     |SI fFechaBaja < fFecha2;
          fFecha2 = fFechaBaja;
     |FINSI;

     nFecha1 = fFecha1;
     nFecha2 = fFecha2;
     nDias = fFecha2 - fFecha1 + 1;
|FPRC;

|PROCESO suma;
     BaseHE = 0;
     nBaseCot = 0;
     nDiasCot = 0;
     |PARA i = 1; |SI i [ nBuscaMeses; |HACIENDO i = i + 1;
          #200#0 = nEmp;
          #200#1 = nTra;
          #200#66 = nume4;     || a¤o
          #200#2 = nume1;     || mes
          #200#3 = 0;         || tipo
          |LEE 000#200=;
          |SI FSalida = 0;
               |SI swTP = 0;
                    BaseHE = BaseHE + #200#41 + #200#42;
                    nBaseCot = nBaseCot + #200#39;
                    nDiasCot = nDiasCot + #200#8;
               |SINO;
                    nCampo = (i * 5) + 17;
                    #0nCampo = nume4 + 2000;
                    |PINTA #0nCampo;

                    nCampo = nCampo + 1;
                    #0nCampo = nume1;
                    |PINTA #0nCampo;

                    hmes = #200#2;
                    hany = #200#66;
                    |HAZ Dias;
                    nCampo = nCampo + 1;
                    || #0nCampo = #0nCampo + #200#8;
                    #0nCampo = #0nCampo + nDias;
                    |PINTA #0nCampo;

                    nAny = #200#66;
                    nMes = #200#2;

                    nCampo = nCampo + 1;
                    #0nCampo = #0nCampo + #200#39 + nBaseMat;
                    |SI aEstoyEn = "M";
                         #0nCampo = #0nCampo + #200#117 + #200#130;
                    |FINSI;
                    |SI aEstoyEn = "H";
                         #0nCampo = #0nCampo + #200#159 + #200#172;
                    |FINSI;
                    |PINTA #0nCampo;

                    nCampo = nCampo + 1;
                    #0nCampo = #0nCampo + #200#40 + nBaseMat;
                    |SI aEstoyEn = "M";
                         #0nCampo = #0nCampo + #200#117 + #200#130;
                    |FINSI;
                    |SI aEstoyEn = "H";
                         #0nCampo = #0nCampo + #200#159 + #200#172;
                    |FINSI;
                    |PINTA #0nCampo;
               |FINSI;
          |FINSI;
          |SI nume1 < 12;
               nume1 = nume1 + 1;
          |SINO;
               nume1 = 1;
               nume4 = nume4 + 1;
          |FINSI;
     |SIGUE;
|FPRC;

|PROCESO BuscaBases;
     |ABRE #200;
     |SI swTP = 0
          #200#0 = nEmp;
          #200#1 = nTra;
          #200#2 = nMes;
          #200#3 = 0;
          |SI aEstoyEn = "H";
               #200#66 = nAny;
          |FINSI;
          |LEE 000#200=;
          |SI FSalida = 0;
               #0#14 = #0#14 + #200#39;
               #0#15 = #0#15 + #200#8;
               #0#16 = #0#16 + #200#40;
               #0#17 = #0#17 + #200#8;
               |SI aEstoyEn = "M";
                    #0#14 = #0#14 + #200#117 + #200#130;
                    #0#16 = #0#16 + #200#117 + #200#130;
               |FINSI;
               |SI aEstoyEn = "H";
                    #0#14 = #0#14 + #200#159 + #200#172;
                    #0#16 = #0#16 + #200#159 + #200#172;
               |FINSI;
          |FINSI;
     |SINO;
          || tiempo parcial, ja vorem
          nume1 = nMes + 1;  || mes desde (12 meses anteriores)
          nume4 = nAny - 1;  || a¤o desde (12 meses anteriores)
          |SI nume1 = 13;
               nume1 = 1;
               nume4 = nume4 + 1;
          |FINSI;
          nBuscaMeses = 12;
          |HAZ suma;
     |FINSI;

     || en busca de las horas extras

     |SI swTP = 0
          nume1 = nMes + 1;  || mes desde (12 meses anteriores)
          nume4 = nAny - 1;  || a¤o desde (12 meses anteriores)
          |SI nume1 = 13;
               nume1 = 1;
               nume4 = nume4 + 1;
          |FINSI;
          nBuscaMeses = 12;
          |HAZ suma;
          |SI BaseHE > 0;
               #0#18 = #0#18 + BaseHE;
               #0#19 = #0#19 + nDiasCot;
               #0#20 = #0#20 + BaseHE;
               #0#21 = #0#21 + nDiasCot;
          |SINO;
               #0#18 = 0;
               #0#19 = 0;
               #0#20 = 0;
               #0#21 = 0;
          |FINSI;

          |PINTA #0#14;
          |PINTA #0#15;
          |PINTA #0#16;
          |PINTA #0#17;
          |PINTA #0#18;
          |PINTA #0#19;
          |PINTA #0#20;
          |PINTA #0#21;
     |FINSI;

     |CIERRA #200;
|FPRC;

|PROCESO AcumBases;
     aFechaIni = #0#2;
     aAlfa = aFechaIni % 402;
     nMes = aAlfa;
     aAlfa = aFechaIni % -104;
     nAny = aAlfa;
     nAny = nAny - 2000;
     nMes = nMes - 1;
     |SI nMes = 0;
          nMes = 12;
          nAny = nAny - 1;
     |FINSI;


     |DBASE aAlfa; |QBF aAlfa; aAlfa = aAlfa + "def/nomhicca.mas";
     |CARGA_DEF aAlfa, tempmant@;
     |SI FSalida = 0;
          aEstoyEn = "H";
          |HAZ BuscaBases;
     |SINO;
          aAlfa = "    ATENCION: Error en CARGA_DEF: " + aAlfa;
          |MENAV aAlfa;
     |FINSI;

     |DESCARGA_DEF #tempmant@;

     |SI FEstado ! 0;    || no he encontrado nada en Hco, busco mensual
          |DBASE aAlfa; |QBF aAlfa; aAlfa = aAlfa + "def/nomcalca.mas";
          |CARGA_DEF aAlfa, tempmant@;
          |SI FSalida = 0;
               aEstoyEn = "M";
               |HAZ BuscaBases;
          |SINO;
               aAlfa = "    ATENCION: Error en CARGA_DEF: " + aAlfa;
               |MENAV aAlfa;
          |FINSI;

          |DESCARGA_DEF #tempmant@;
     |FINSI;
|FPRC;

|PROCESO labtraba;
||     fFechaAlta = #labtraba#36;
||     |SI fFechaAlta [ fFechaDesde;
          nEmp = #labtraba#0;
          nTra = #labtraba#1;
          |HAZ AcumBases;
||     |FINSI;
|FPRC;

|DEFBUCLE labtraba;
     #labtraba#5;
     ;
     nEmp, aNif;
     nEmp, aNif;
     ;
     NULL, labtraba;
|FIN;

|PROCESO Bases; |TIPO 7;
     |SI #0#14 ! 0;
          |SI FSalida ! 13;
               |ACABA;
          |FINSI;
     |FINSI;

     |PDEFECTO #0, 14, 82;
     |PARA nCampo = 14; |SI nCampo [ 81; |HACIENDO nCampo = nCampo + 1;
          |PINTA #0nCampo;
     |SIGUE;

     |ABRE #labtraba;
     #labtraba#0 = #0#0;
     #labtraba#1 = #0#1;
     |LEE 000#labtraba.=;
     |CIERRA #labtraba;

     |SI #labtraba#42 = "T"; |O #labtraba#42 = "X";
          swTP = 1;
     |SINO;
          swTP = 0;
     |FINSI;

     aFechaIni = #0#2;
     aAlfa = aFechaIni % 402;
     nMes = aAlfa;
     aAlfa = aFechaIni % -104;
     nAny = aAlfa;

     aNif = #labtraba#7;
     nEmp = #labtraba#0;
     nume1 = nMes + 1;  || mes desde (12 meses anteriores)
     nume4 = nAny - 1;  || a¤o desde (12 meses anteriores)
     |SI nume1 = 13;
          nume1 = 1;
          nume4 = nume4 + 1;
     |FINSI;
     aAlfa1 = "01";
     aAlfa2 = nume1; |QBF aAlfa2; aAlfa2 = ("00" + aAlfa2) % -102;
     aAlfa3 = nume4; |QBF aAlfa2;
     aAlfa = aAlfa1 + "." + aAlfa2 + "." + aAlfa3;
     fFechaDesde = aAlfa;

     |BUCLE labtraba;

|FPRC;

|PROCESO FuerzaBases;
     |SI FSalida = 13;
          |HAZ Bases;
          |ERROR;
     |FINSI;
|FPRC;

/************ PROCESO PARA LA GRABACION DE LOS CAMPOS ***********************/
/************ Y LA EMISION DEL PDF ******************************************/

|PROCESO sustituye2;
     nAscii = & aCaracter;
     aCaracterSal = aCaracter;
     |SI nAscii = 160;             || el caracter  
          aCaracterSal = "\341";
     |FINSI;
     |SI nAscii = 130;             || el caracter ‚
          aCaracterSal = "\351";
     |FINSI;
     |SI nAscii = 161;             || el caracter ¡
          aCaracterSal = "\355";
     |FINSI;
     |SI nAscii = 162; |O nAscii = 243;            || el caracter ¢
          aCaracterSal = "\363";
     |FINSI;
     |SI nAscii = 163;             || el caracter £
          aCaracterSal = "\372";
     |FINSI;
     |SI nAscii = 164;             || el caracter ¤
          aCaracterSal = "\361";
     |FINSI;
     |SI nAscii = 241;             || el caracter ¤ (otro codigo ¨?)
          aCaracterSal = "\361";
     |FINSI;
     |SI nAscii = 165;             || el caracter ¥
          aCaracterSal = "\321";
     |FINSI;
     |SI nAscii = 129;
          aCaracterSal = "\374";   || el caracter u con dieresis
     |FINSI;
     |SI nAscii = 154;
          aCaracterSal = "\334";   || el caracter U (mayuscula) con dieresis
     |FINSI;
     |SI nAscii = 167;
          aCaracterSal = "\272";   || el caracter §
     |FINSI;
     |SI nAscii = 166;
          aCaracterSal = "\252";   || el caracter ¦
     |FINSI;
     |SI aCaracter = "(";
          aCaracterSal = "\(";
     |FINSI;
     |SI aCaracter = ")";
          aCaracterSal = "\)";
     |FINSI;
|FPRC;

|PROCESO sustituye;
     aCadenaSal = "";
     aLongitud = aCadena % 0;
     nLongitud = aLongitud;
     |PARA nPos = 1; |SI nPos [ nLongitud; |HACIENDO nPos = nPos + 1;
          nBusca = (nPos * 100) + 1;
          aCaracter = aCadena % nBusca;
          |HAZ sustituye2;
          aCadenaSal = aCadenaSal + aCaracterSal;
     |SIGUE;
|FPRC;

|PROCESO GrabaTemporal;
     aAlfa1 = aValor % 101;
     |SI aAlfa1 = ".";
          aValor = "";
     |FINSI;
     |QBF aValor;
     aCadena = aValor;
     |HAZ sustituye;
     aValor = aCadenaSal;
     #pdfimtmp#0 = aCampo;
     #pdfimtmp#1 = aValor;
     #pdfimtmp#2 = nTipo;
     |GRABA 020#pdfimtmp.n;
|FPRC;

|PROCESO Excepciones;
     |SI aCampo = "OET_1.1_CodPostal";
          aAlfa1 = #labempre#7;
          |QBF aAlfa1;
          aAlfa1 = ("00" + aAlfa1) % -102;
          aAlfa2 = #labempre#8;
          |QBF aAlfa2;
          aAlfa2 = ("000" + aAlfa2) % -103;

          aValor = aAlfa1 + aAlfa2;
     |FINSI;

     |SI aCampo = "OEV_1.1_CodigoCuenta1";
          aAlfa = #labcentr#10;
          |QBF aAlfa;
          aValor = aAlfa % 102;
     |FINSI;

     |SI aCampo = "OEV_1.1_CodigoCuenta2";
          aAlfa = #labcentr#10;
          |QBF aAlfa;
          aValor = aAlfa % -109;
     |FINSI;

     |SI aCampo = "OET_2.1_CodPostal";
          aAlfa1 = #labtraba#8;
          |QBF aAlfa1;
          aAlfa1 = ("00" + aAlfa1) % -102;
          aAlfa2 = #labtraba#9;
          |QBF aAlfa2;
          aAlfa2 = ("000" + aAlfa2) % -103;

          aValor = aAlfa1 + aAlfa2;
     |FINSI;

     |SI aCampo = "OEV_2.1_NAF1";
          aAlfa = #labtraba#75;
          |QBF aAlfa;
          aValor = aAlfa % 102;
     |FINSI;

     |SI aCampo = "OEV_2.1_NAF2";
          aAlfa = #labtraba#75;
          |QBF aAlfa;
          aValor = aAlfa % -110;
     |FINSI;

     |SI aCampo = "2-DESEMPLEO";
          nNume = #labtraba#248;
          |SI nNume = 508; |O nNume = 512; |O nNume = 514;
          |O nNume = 708; |O nNume = 710; |O nNume = 713; |O nNume = 716;
               aValor = "No";
          |SINO;
               aValor = "Si";
          |FINSI;
     |FINSI;
     |SI aCampo = "dia";
          aAlfa = #0#83;
          |QBF aAlfa;
          |SI aAlfa ! "";
               aValor = aAlfa % 102;
          |FINSI;
     |FINSI;
     |SI aCampo = "De_mes";
          aAlfa = #0#83;
          |QBF aAlfa;
          |SI aAlfa ! "";
               aValor = aAlfa % 402;
               nMes = aValor;
               |HAZ MesLetra_plb;
               aValor = aMes;
          |FINSI;
     |FINSI;
     |SI aCampo = "anno";
          aAlfa = #0#83;
          |QBF aAlfa;
          |SI aAlfa ! "";
               aValor = aAlfa % -102;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO labpdfli;
     nTipo = 00;
     aValor = "";
     nCampo = #labpdfli#4;
     aCampo = #labpdfli#2;
     |QBF aCampo;

     |SI nCampo ! -1;
          |SI #labpdfli#3 = "labempre";
               aValor = #labempre#nCampo#;
          |FINSI;
          |SI #labpdfli#3 = "labtraba";
               aValor = #labtraba#nCampo#;
          |FINSI;
          |SI #labpdfli#3 = "labcentr";
               aValor = #labcentr#nCampo#;
          |FINSI;
          |SI #labpdfli#3 = "labcerme";
               aValor = #labcerme#nCampo#;
          |FINSI;
     |FINSI;

     |QBF aValor;

     |HAZ Excepciones;

     |SI aValor ! "";
          aTipoCampo = #labpdfli#5;
          |QBF aTipoCampo;
          |SI aTipoCampo = "F";
               aValor = (aValor % 102) + "/" + (aValor % 402) + "/" + (aValor % 704);
          |FINSI;
          |SI aTipoCampo = "C";
               aValorC = #labpdfli#6;
               |QBF aValorC;
               |SI aValorC ! "";
                    |SI aValor = aValorC;
                         aValor = #labpdfli#7;
                    |SINO;
                         aValor = "";
                    |FINSI;
               |SINO;
                    aValor = "";
               |FINSI;
          |FINSI;
          |SI aTipoCampo = "P";
               aCampo = #labpdfli#2;
               |QBF aCampo;
               nPos = #labpdfli#6;
               nPos = (nPos * 100) + 1
               aValor = aValor % nPos;
          |FINSI;
          |SI aValor ! "";
               aCampo = #labpdfli#2;
               |HAZ GrabaTemporal;
          |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE labpdfli;
     #labpdfli#1;
     ;
     "labcerme", 001;
     "labcerme", 999;
     ;
     NULL, labpdfli;
|FIN;

|PROCESO CargaCampos;
     |BUCLE labpdfli;
|FPRC;

|PROCESO ImprePDF;
     aAlfa1 = Usuario; |QBT aAlfa1;    aAlfa1 = ("c" + aAlfa1) % 108;
     |NOME_DAT #100 aAlfa1;

     |DELINDEX #pdfimtmp;
     |ABRE #pdfimtmp;

     |HAZ CargaCampos;

     |CIERRA #pdfimtmp;

     aArchivo = "cert_mat_pat.pdf";

     |SUB_EJECUTA "pdfgraba";

     |SUB_EJECUTA "pdfimpre";
|FPRC;

|PROCESO Informe;
     |ABRE #labempre;
     #labempre#0 = #0#0;
     |LEE 000#labempre.=;
     |CIERRA #labempre;

     |ABRE #labtraba;
     #labtraba#0 = #0#0;
     #labtraba#1 = #0#1;
     |LEE 000#labtraba.=;
     |CIERRA #labtraba;

     |ABRE #labcentr;
     #labcentr#0 = #labtraba#0;
     #labcentr#1 = #labtraba#77;
     |LEE 000#labcentr.=;
     |CIERRA #labcentr;

     |HAZ ImprePDF;
|FPRC;

|PROCESO Tipo3; |TIPO 3;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 1;
          |CONFI "    S¿Desea emitir el certificado?";
          |SI FSalida = 0;
               |HAZ Informe;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Tipo20; |TIPO 20;
     |SI FSalida = "OPCION";
          FSalida = "Emisión";
     |SINO;
          |HAZ Informe;
     |FINSI;
|FPRC;
