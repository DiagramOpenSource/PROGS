|FICHEROS;
     nomz0005 #0;
     labempre;
     labtraba;
     nomcalca;
     nomhicca;
     nomconca;
     nomcopat;

     nomcot02;
|FIN;

|VARIABLES;
     &aEmp = "";
     &aTra = "";
     &aMes = "";
     &aDonde = "";
     aResta = "";
     nResta = 0;
     &nProrrata = 0;
     &nCalculada = 0;
     nDesdeMes = 0;
     nHastaMes = 0;
     &nImporte = 0;
     &nBase = 0;
     &nDiferencia = 0;
     &nMes = 0;
     &nBonif = 0;

     &nAny = 0;
     &nTopemes = 0;
     &nMinimoComp = 0;
     &nDiasAlta = 0;
     nCoeficiente = 0;
     &nCoefTP = 0;

     nMinimo = 0;
     aAlfa = "";
     aAlfa1 = "";
     aAlfa2 = "";
     fFecha = @;

     nEmp = 0;
     nTra = 0;
     nNumTrab = 0;

     nDiasAltaMan = 0;
     nDiasITMan = 0;
     nMinimoJor = 0;
     nJornadas = 0;
     aOrigen = "";
     swEncontrado = 0;

     nAnyXX = 0;

     nElMes = 0;
     nElAny = 0;
     aAlfa3 = "";
     aIniPer = "";
     fIniPer = @;
     aFinPer = "";
     fFinPer = @;
     aFechaAlta = "";
     aFechaBaja = "";
     fFechaAlta = @;
     fFechaBaja = @;
     aFechaCambio = "";
     fFechaCambio = @;
     nFechaCambio = 0;
     nNume = 0;
     nNume1 = 0;
     nNume2 = 0;
     nDiasAny1 = 0;
     nDiasAny2 = 0;
     alfa1 = ""; alfa2 = ""; alfa3 = "";
     nume1 = 0; nume2 = 0; nume3 = 0; nume4 = 0;
     nume5 = 0; nume6 = 0; nume7 = 0; nume8 = 0;
     nPropPrac = 0;

     &nBaseDia = 0;
     &nMinCompDia = 0;
     &nContrato = 0;

     swTodos = 0;
     swSigue = 0;
|FIN;

|PROCESO DiasCot;
     aAlfa1 = #labtraba#75; |QBF aAlfa1;
     aAlfa2 = #nomcot02#5; |QBF aAlfa2;
     |SI aAlfa1 ! aAlfa2;    || no haria falta, pero ha salido
          |ACABA;                       || algun caso raro
     |FINSI;

     swEncontrado = 1;

     |SI #nomcot02#27 = "ALT";
          nDiasAlta = nDiasAlta + #nomcot02#22;
     |FINSI;
|FPRC;

|DEFBUCLE DiasCot;
     #nomcot02#1;
     ;
     nEmp, nAny, nMes, 0, "               ", "            ", 01, nTra;
     nEmp, nAny, nMes, 0, "zzzzzzzzzzzzzzz", "zzzzzzzzzzzz", 31, nTra;
     ;
     NULL, DiasCot;
|FIN;

|PROCESO DosPeriodos;
     aAlfa1 = "01";
     aAlfa2 = nElMes; |QBF aAlfa2; aAlfa2 = ("00" + aAlfa2) % -102;
     aAlfa3 = nElAny;
     aIniPer = aAlfa1 + "." + aAlfa2 + "." + aAlfa3;
     fIniPer = aIniPer;

     aAlfa1 = nTopemes;
     aAlfa2 = nElMes; |QBF aAlfa2; aAlfa2 = ("00" + aAlfa2) % -102;
     aAlfa3 = nElAny;
     aFinPer = aAlfa1 + "." + aAlfa2 + "." + aAlfa3;
     fFinPer = aFinPer;

     aFechaAlta = #labtraba#36;
     |SI aFechaBaja = "";
          aFechaBaja = aFinPer;
     |FINSI;

     fFechaAlta = aFechaAlta;
     fFechaBaja = aFechaBaja;

     |SI fFechaAlta ] fIniPer;
          fIniPer = fFechaAlta;
     |FINSI;
     |SI fFechaBaja [ fFinPer;
          fFinPer = fFechaBaja;
     |FINSI;

     aFechaCambio = #labtraba#34;
     aAlfa = aFechaCambio % -104;
     nNume = aAlfa;
     nNume = nNume + 1;
     aAlfa = nNume;
     aFechaCambio = (aFechaCambio % 106) + aAlfa;
     fFechaCambio = aFechaCambio;
     nFechaCambio = fFechaCambio;

     nNume1 = fIniPer;
     nNume2 = fFinPer;

     |SI nNume1 > nFechaCambio; |O nNume2 < nFechaCambio;
          || NO ESTA COMPRENDIDO ENTRE ESTAS FECHAS
          |SI nNume1 > nFechaCambio;
               || la fecha de inicio de periodo es superior a la fecha en que
               || se cambia de a¤o (raro, pero asi es), los dias pertenecen
               || todos al a¤o 2
               nDiasAny2 = nNume2 - nNume1 + 1;
          |FINSI;
          |SI nNume2 < nFechaCambio;
               || la fecha de fin de periodo es anterior a la fecha en que se
               || cambiaba de a¤o, todos los dias perteneceran al a¤o 1
               nDiasAny1 = nNume2 - nNume1 + 1;
          |FINSI;
     |SINO;
          nDiasAny1 = nFechaCambio - nNume1
          nDiasAny2 = nNume2 - nFechaCambio + 1;
     |FINSI;
|FPRC;

|PROCESO EnPracticas;
     alfa1 = #labtraba#34 % -104;         || anyo alta
     alfa2 = #labtraba#34 % -602;         || mes alta
     alfa3 = #labtraba#34 % 102;          || dia alta
     nume2 = alfa1;                || anyo alta
     nume3 = alfa2;                || mes alta
     nume1 = #0#4 - nume2;
     nume4 = #0#5 - nume3;
     nume5 = alfa3;                || dia alta

     alfa1 = aFechaBaja % -104;         || anyo baja
     alfa2 = aFechaBaja % -602;         || mes baja
     alfa3 = aFechaBaja % 102;          || dia baja
     nume6 = alfa1;                || anyo baja
     nume7 = alfa2;                || mes baja
     nume8 = alfa3;                || dia baja

     |SI nume1 = 0;      || dif a¤o calculo y a¤o alta es cero
           nPropPrac = #labtraba#232 / 100;
           || estoy en el primer a¤o
     |FINSI;

     |SI nume1 = 1;      || puede ser que este en el primer a¤o,
                          || en el segundo o en el mes de cambio
          |SI nume4 < 0; || primer a¤o
               nPropPrac = #labtraba#232 / 100;
          |FINSI;
          |SI nume4 = 0; || mes de cambio a¤o
                nDiasAny1 = 0;
                nDiasAny2 = 0;
                |HAZ DosPeriodos;
                nPropPrac = (nDiasAny1 / nDiasAlta) * (#labtraba#232 / 100);  || primer a¤o
                nPropPrac = nPropPrac + (nDiasAny2 / nDiasAlta) * (#labtraba#233 / 100);  || segundo a¤o
          |FINSI;
          |SI nume4 > 0; || segundo a¤o
                nPropPrac = #labtraba#233 / 100;
          |FINSI;
     |FINSI;

     |SI nume1 = 2;      || segundo a¤o,
          nPropPrac = #labtraba#233 / 100;  || entre 1 y 2 a¤os en alta
     |FINSI;

     |SI nume1 = 3;      || estoy en el a¤o que cumplo el tercer a¤o
                         || puede ser que este antes de cumplirlo, en el mes
                         || que cumplo tres o que ya los haya cumplido

           |SI nume4 < 0; || aun no ha cumplido el tercer a¤o
                nPropPrac = #labtraba#233 / 100;  || entre 2 y 3 a¤os en alta
           |FINSI;
           |SI nume4 = 0; || el mes que termino el tercer a¤o
                nPropPrac = #labtraba#233 / 100;
                || a la fuerza con el porcentaje del segundo y tercer a¤o,
                || no puedo estar una parte en practicas del segundo a¤o
                || y otra parte con proporcion 1 por llevar mas de dos
                || a¤os ya que eso deberia estar en otro contrato
           |FINSI;
           |SI nume4 > 0; || mas de tres a¤os
                         || nPropPrac = #labtraba#233 / 100; || mas de tres a¤os en alta
                || en teoria imposible, no puedo estar mas de tres
                || a¤os en practicas

                nPropPrac = 1; || mas de tres a¤os en alta
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO comprobacion;
     |SI aOrigen = "M";
          nEmp = #nomcalca#0;
          nTra = #nomcalca#1;
          nDiasAltaMan = #nomcalca#8;
          nDiasITMan = #nomcalca#31 + #nomcalca#131 + #nomcalca#32;
          nDiasITMan = nDiasITMan +  #nomcalca#65 + #nomcalca#194;
          nBase = #nomcalca#37 + #nomcalca#38;
          nJornadas = #nomcalca#11;
          aFechaBaja = #nomcalca#5;
          nContrato = #nomcalca#9;
     |FINSI;
     |SI aOrigen = "H";
          nEmp = #nomhicca#0;
          nTra = #nomhicca#1;
          nDiasAltaMan = #nomhicca#8;
          nDiasITMan = #nomhicca#31 + #nomhicca#173 + #nomhicca#32;
          nDiasITMan = nDiasITMan +  #nomhicca#65 + #nomhicca#236;
          nBase = #nomhicca#37 + #nomhicca#38;
          nJornadas = #nomhicca#11;
          aFechaBaja = #nomhicca#5;
          nContrato = #nomhicca#9;
     |FINSI;

     #labempre#0 = nEmp;
     |LEE 000#labempre.=;

     #labtraba#0 = nEmp;
     #labtraba#1 = nTra;
     |LEE 000#labtraba.=;

     |SI #labtraba#42 = "H";
          |ACABA;
     |FINSI;

     nDiasAlta = 0;
     swEncontrado = 0;
     |BUCLE DiasCot;
     |SI swEncontrado = 0;
          nDiasAlta = nDiasAltaMan - nDiasITMan;
          |SI nDiasITMan ! 0;
               |SI #labtraba#42 = "T"; |O #labtraba#42 = "D";
               |O #labtraba#42 = "E";
                    || nada
               |SINO;
                    |SI nTopemes = 31; |Y nDiasAltaMan = 30;
                         nDiasAlta = nDiasAlta + 1;
                    |FINSI;
                    |SI nTopemes = 28;
                         nDiasAlta = nDiasAlta - 2;
                    |FINSI;
               |FINSI;
          |FINSI;
     |FINSI;

     |SI nDiasAlta = nTopemes;
          nCoeficiente = 1;
     |SINO;
          |SI #labtraba#42 = "T"; |O #labtraba#42 = "D";
               nCoeficiente = nDiasAlta / nTopemes;
          |SINO;
               nCoeficiente = nDiasAlta / 30;
          |FINSI;
     |FINSI;

     #nomconca#0 = #labtraba#87;
     |LEE 000#nomconca.=;

     nCoefTP = 1;
     |SI #labtraba#42 = "X"; |O #labtraba#42 = "T";
          |SI #labtraba#234 ! 0;
               |SI #nomconca#162 = 0;
                    #nomconca#162 = 40;
               |FINSI;

               nCoefTP = #labtraba#234 / #nomconca#162;
          |FINSI;
     |FINSI;

     nCoefTP = nCoefTP ! 3;

     nPropPrac = 1;
     |SI #labtraba#45 = 420; |O #labtraba#45 = 520;
     |O #labtraba#45 = 421;
          |SI #labtraba#232 ! 0; |Y #labtraba#233 ! 0; |Y #labtraba#208 = "S";
               |HAZ EnPracticas;
          |FINSI;
     |FINSI;

     nMinCompDia = nMinimo / 30;
     |SI #labtraba#42 ! "E";
          nMinimoComp = nMinimo * nCoeficiente * nCoefTP * nPropPrac;
          nMinCompDia = nMinCompDia * nCoefTP * nPropPrac;
     |SINO;
          nMinimoComp = nMinimoJor * nJornadas;
          nMinCompDia = nMinimoJor;
          nDiasAlta = nJornadas;
     |FINSI;

     |SI nCoeficiente = 1; |Y #labtraba#42 ! "E";
          nBaseDia = nBase / 30;
     |SINO;
          nBaseDia = (nBase / nDiasAlta) ! 2;
     |FINSI;

     swSigue = 0;

     |SI nBase > 0; |Y nBaseDia < nMinCompDia;
     |Y nDiasAlta > 0;
          swSigue = 1;
     |FINSI;
     |SI swTodos = 1;
          swSigue = 1;
     |FINSI;
     |SI swSigue = 1;
          |SI nNumTrab = 0;
               Impresora = "CrystalReport";
               |IMPRE -1;
               |INFOR "nomz0005";
          |FINSI;

          |PRINT;
          nNumTrab = nNumTrab + 1;
     |FINSI;
|FPRC;

|DEFBUCLE nomhicca;
     #nomhicca#1;
     ;
     #0#0, #0#2, nAnyXX, nDesdeMes, 0;
     #0#1, #0#3, nAnyXX, nHastaMes, 0;
     ;
     NULL, comprobacion;
|FINSI;

|DEFBUCLE nomcalca;
     #nomcalca#1;
     ;
     #0#0, #0#2, nDesdeMes, 0;
     #0#1, #0#3, nHastaMes, 0;
     ;
     NULL, comprobacion;
|FINSI;

|PROCESO Tipo3;
     |ABRE #labempre;
     |ABRE #labtraba;
     |ABRE #nomconca;
     |ABRE #nomcopat;

     |SI #0#5 > 12;
          #0#5 = #0#5 - 12;
          swTodos = 1;
     |FINSI;

     |SI FSalida = 0;
          nAnyXX = #0#4 - 2000;
          nDesdeMes = #0#5;
          #0#6 = #0#5;
          nHastaMes = #0#6;

          nMes = #0#5;
          nAny = #0#4;

          nElMes = nMes;
          nElAny = nAny;

          |HAZ topemes_plb;

          #nomcopat#0 = 500;
          aAlfa = #0#4;
          aAlfa = "01.01." + aAlfa;
          fFecha = aAlfa;
          #nomcopat#79 = fFecha;
          |LEE 000#nomcopat.=;
          nMinimo = #nomcopat#89 + (#nomcopat#89 / 6);
          nMinimo = nMinimo ! 2;

          #nomcopat#0 = 702;
          aAlfa = #0#4;
          aAlfa = "01.01." + aAlfa;
          fFecha = aAlfa;
          #nomcopat#79 = fFecha;
          |LEE 000#nomcopat.=;
          nMinimoJor = #nomcopat#30;

          aOrigen = "M";
          |BUCLE nomcalca;
          aOrigen = "H";
          |BUCLE nomhicca;
          |SI nNumTrab ! 0;
               |PIEINF;
               |FININF;
               |FINIMP;
          |SINO;
               aAlfa = "    No se ha ncontrado ningún trabajador por debajo ";
               aAlfa = aAlfa + "del SMI con la selección realizada";
               |MENAV aAlfa;
          |FINSI;
     |FINSI;

     |CIERRA #nomcopat;
     |CIERRA #nomconca;
     |CIERRA #labtraba;
     |CIERRA #labempre;
|FPRC;

|PROGRAMA;
     |CLS;
     |PINPA #0#0;

     |ABRE #0;
     |DDEFECTO #0;
     |LEE 000#0p;
     |SI FSalida = 0;
          |BORRA 020#0a;
     |FINSI;

     |PINDA #0#0;

     |ENDATOS #1#0;
     |SI FSalida = 0;
          |HAZ Tipo3;
     |FINSI;
     |GRABA 020#0n;
|FPRO;
