|FICHEROS;
     mod00275 #0;

     mod00262;      || jornadas que vienen de IDAL
     mod00270,MANTE;      || temporal
     mod00276,MANTE;      || buscador

     labempre;
     labtraba;
     labcentr;

     nomvarca;
     nomfical;

     gomdetjr;
     nomvarli;
|FIN;

|VARIABLES;
     || variables que me traje del mod00270 para pintar el calendario

    alfa1 = "";
    alfa2 = "";
    alfa3 = "";
    alfa4 = "";
    nume1 = 0;
    nume2 = 0;
    nume3 = 0;
    nume4 = 0;
    para1 = 0;
    para2 = 0;
    para3 = 0;
    para4 = 0;
    fech1 = @;
    fech2 = @;
    fech3 = @;
    fech4 = @;
    elsw1 = 0;
    elsw2 = 0;
    elsw3 = 0;
    elsw4 = 0;
    FEstado = 0;
    seleccio = 0;
    modo = 0;
    sw_rup = 0;

    ant_tipo = "";
     nModo = 0;

     nDiaMes = 0;
     nFilNum = 0;
     nColIni = 0;
     nColNum = 0;
     nColCam = 0;
     nPaso = 0;
     nCampo = 0;
     topemes = 0;
     nPos = 0;
     a = 0;
     nDiaSem = 0;

     aAlfa1 = "";
     aAlfa = "";
     nNume = 0;

     aAnyo = "";
     aMes = "";
     mesnum = 0;
     anynum = 0;
     aFecha = "";
     fFecha = @;
     fDiaLet = @;
     aDiaLet = "";
     nPosHoras = 0;

     swFestivo = 0;
     nFechaNume = 0;
     nCampoF = 0;
     nCampoF1 = 0;
     nCampoF2 = 0;
     nCampoF3 = 0;
     aAlfa2 = "";
     aAlfa3 = "";
     nNume1 = 0;
     nNume2 = 0;
     nNume3 = 0;
     swAlta = 0;
     nEmp = 0;
     nTra = 0;
     fFechaAlta = @;
     fFechaBaja = @;
     nDia = 0;
     swSoloAlta = 0;
     nTotalHoras = 0;

     numer = 0;
     menos = 0;
     bisiesto = 0;
     fecsys = @;
     i = 0;

     nTotalJor = 0;
     nCampoDia = 0;
     nCampo2 = 0;

     aNif = "";
     swUno = 0;
     nDesdeEmp = 0;
     nHastaEmp = 0;
     nDesdeTra = 0;
     nHastaTra = 0;

     fFechaIni = @;
     fFechaFin = @;
     aFechaAlta = "";
     aFechaBaja = "";
     &nTopemes = 0;
     &nMes = 0;
     &nAny = 0;
     aLista = "";
     nPosicion = 0;

     nJornadas = 0;
     nJornadasIT = 0;
     fDia = @;
     nCampoCod = 0;
     nPosMenosUno = 0;

     nCampo1 = 0;
     nCampo3 = 0;
     swIT = 0;
     nDesde = 0;
     nHasta = 0;

     nTrabajador = 0;
     swSigue = 0;

     aLocalizado = "";
     aCodigo = "";
     aNombre = "";
     aNIF = "";
     swBusqueda = 0;
     swHayAlguno = 0;
|FIN;

|PROCESO DesdeHasta;
     |SI FSalida = 17;
     |FINSI;
|FPRC;

|PROCESO Buscador;
     || por Codigo
     |SI aCodigo ! "00000";
          |PARA nCampo = 41; |SI nCampo [ 71; |HACIENDO nCampo = nCampo + 1;
               aAlfa1 = #mod00270#nCampo#;
               |QBF aAlfa1;
               aAlfa1 = ("00000" + aAlfa1) % -105;
               aAlfa = aAlfa1 - aCodigo;
               |SI aAlfa ! aAlfa1;
                    aLocalizado = #mod00270#4;
                    |SAL;
               |FINSI;
          |SIGUE;
          |SI aLocalizado ! "";
               |ERROR10;
          |FINSI;
     |FINSI;

     || por Nombre
     aAlfa1 = #mod00270#5;
     |QBF aAlfa1;
     |SI aNombre ! "";
          aAlfa = aAlfa1 - aNombre;
          |SI aAlfa ! aAlfa1;
               aLocalizado = #mod00270#4;
               |ERROR10;
          |FINSI;
     |FINSI;

     || por NIF
     aAlfa1 = #mod00270#4;
     |QBF aAlfa1;
     |SI aNIF ! "";
          aAlfa = aAlfa1 - aNIF;
          |SI aAlfa ! aAlfa1;
               aLocalizado = #mod00270#4;
               |ERROR10;
          |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE Buscador;
     #mod00270#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, Buscador;
|FIN;

|PROCESO Busqueda; |TIPO 11;
     |SI FSalida = 13;
          swBusqueda = 1;
          |PUSHV 09381780;
          |PINPA #0#mod00276;
          |PINDA #0#mod00276;
          |ENDATOS #1#mod00276;
          |POPV;

          aCodigo = #mod00276#0; |QBF aCodigo; aCodigo = ("00000" + aCodigo) % -105;
          aNombre = #mod00276#1; |QBF aNombre;
          aNIF = #mod00276#2; |QBF aNIF;

          |SI aCodigo ! "00000"; |O aNombre ! ""; |O aNIF ! "";
               aLocalizado = "";
               |BUCLE Buscador;
               |PONCONTROL 23, "SI";
          |FINSI;
     |FINSI;
     |SI FSalida = 14;
          |HAZ LeeTemporal;
          aAlfa = "    Se han actualizado las variaciones y el calendario del ";
          aAlfa = aAlfa + "trabajador seleccionado";
          |MENAV aAlfa;
     |FINSI;
|FPRC;

|PROCESO Campos; |TIPO 80;
     |CAMPO_ANCHO #labempre#2, 1, 25;
|FPRC;

|PROCESO CompJornada;
     swIT = 0;
     nCampoCod = Campo + 33;
     nDiaMes = Campo - 7;

     |DDEFECTO #nomvarca;
     #nomvarca#0 = #labtraba#0;
     #nomvarca#1 = #mod00270#nCampoCod#;
     #nomvarca#2 = #0#2;
     #nomvarca#3 = 0;
     |LEE 000#nomvarca.=;
     |SI FSalida = 0;
          |PARA nCampo3 = 12; |SI nCampo3 [ 15; |HACIENDO nCampo3 = nCampo3 + 1;
               nDesde = 0;
               nHasta = 0;
               |SI #nomvarca#nCampo3# ! " ";
                    nCampo1 = nCampo3 - 8;
                    nCampo2 = nCampo3 - 4;
                    |SI #nomvarca#nCampo1# = 0;
                         #nomvarca#nCampo1# = 1;
                    |FINSI;
                    nDesde = #nomvarca#nCampo1#;

                    |SI #nomvarca#nCampo2# = 0;
                         #nomvarca#nCampo2# = topemes;
                    |FINSI;
                    nHasta = #nomvarca#nCampo2#;

                    |SI nDesde [ nDiaMes; |Y nDiaMes [ nHasta;
                         swIT = 1;
                    |FINSI;
               |SINO;
                    |SI nCampo3 = 12;
                         |SI #labtraba#89 ! 0; |O #labtraba#92 ! 0;
                              swIT = 1;
                         |FINSI;
                    |FINSI;
               |FINSI;
          |SIGUE;
     |FINSI;

     |SI swIT = 1;
          |SI #mod00270#Campo# = "X";
               aAlfa = "    Este día está comprendido dentro de una IT según ";
               aAlfa = aAlfa + "las variaciones del trabajador.";
               |MENAV aAlfa;
               |ERROR;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO PintaCosas; |TIPO 7;
     nJornadas = 0;
     nJornadasIT = 0;
     |PARA nCampo = 8; |SI nCampo [ 38; |HACIENDO nCampo = nCampo + 1;
          |SI #mod00270#nCampo# = "X";
               nJornadas = nJornadas + 1;
          |FINSI;
          |SI #mod00270#nCampo# = "I";
               nJornadasIT = nJornadasIT + 1;
          |FINSI;
     |SIGUE;
     #mod00270#6 = nJornadas;
     #mod00270#72 = nJornadasIT;
     |PINTA #mod00270#6;
     |PINTA #mod00270#72;

     |SI Campo = 8;
          |CAMPO_VISUAL #mod00270#73, 1, "S";
          |PINTA 2038, "³Ficha activa d¡a ..:       ³";
          |PINTA #mod00270#73;
     |FINSI;

     nCampo = Campo + 33;
     #mod00270#73 = #mod00270#nCampo#;
     |PINTA #mod00270#73;
|FPRC;

/****************************************************************************/
/************************* ACTUALIZACION LABORAL ****************************/
/****************************************************************************/

|PROCESO BorraCosas;
     |PARA nCampo = 8; |SI nCampo [ 38; |HACIENDO nCampo = nCampo + 1;
          nCampo1 = nCampo + 33;
          nTrabajador = #mod00270#nCampo1#;
          |SI nTrabajador ! 00000;
               |DDEFECTO #gomdetjr;
               #gomdetjr#0 = #mod00270#0;
               #gomdetjr#1 = nTrabajador;
               #gomdetjr#2 = #mod00270#2;
               #gomdetjr#3 = #mod00270#3;
               |LEE 101#gomdetjr.=;
               |SI FSalida = 0;
                    |BORRA 020#gomdetjr.a;
               |FINSI;
               |LIBERA #gomdetjr;

               |DDEFECTO #nomvarli;
               #nomvarli#0 = #mod00270#0;
               #nomvarli#1 = nTrabajador;
               #nomvarli#2 = #mod00270#3;
               #nomvarli#3 = 0;
               #nomvarli#4 = 903;
               |LEE 101#nomvarli.=;
               |SI FSalida = 0;
                    |BORRA 020#nomvarli.a;
               |FINSI;

               #nomvarli#4 = 101;
               |LEE 101#nomvarli.=;
               |SI FSalida = 0;
                    |BORRA 020#nomvarli.a;
               |FINSI;

               #nomvarli#4 = 922;
               |LEE 101#nomvarli.=;
               |SI FSalida = 0;
                    |BORRA 020#nomvarli.a;
               |FINSI;

               |LIBERA #nomvarli;
          |FINSI;
     |SIGUE;
|FPRC;

|PROCESO GrabaNomvarli;
     |SI FSalida = 0;
          aAlfa = #nomvarli#5;
          |QBF aAlfa;
          nNume = aAlfa;
          nNume = nNume + 1
          aAlfa = nNume; |QBF aAlfa; aAlfa = ("          " + aAlfa) % -110;
          #nomvarli#5 = aAlfa;
          |GRABA 020#nomvarli.a;
     |SINO;
          aAlfa = "         1";
          #nomvarli#5 = aAlfa;
          |SI #nomvarli#4 = 903;
               #nomvarli#6 = "Jornadas";
          |FINSI;
          |SI #nomvarli#4 = 922;
               #nomvarli#6 = "Jornadas IT";
          |FINSI;
          |GRABA 020#nomvarli.n;
     |FINSI;
     |LIBERA #nomvarli;
|FPRC;

|PROCESO GrabaJornada;
     |DDEFECTO #nomvarca;
     #nomvarca#0 = #mod00270#0;
     #nomvarca#1 = nTrabajador;
     #nomvarca#2 = #mod00270#3;
     #nomvarca#3 = 0;
     |LEE 101#nomvarca.=;
     |SI FSalida ! 0;
          |GRABA 020#nomvarca.n;
     |FINSI;

     |DDEFECTO #nomvarli;
     #nomvarli#0 = #mod00270#0;
     #nomvarli#1 = nTrabajador;
     #nomvarli#2 = #mod00270#3;
     #nomvarli#3 = 0;

     |SI #mod00270#nCampo# = "X";
          #nomvarli#4 = 101;
          |LEE 101#nomvarli.=;
          |HAZ GrabaNomvarli;

          #nomvarli#4 = 903;
          |LEE 101#nomvarli.=;
          |HAZ GrabaNomvarli;
     |FINSI;

     |SI #mod00270#nCampo# = "I";
          #nomvarli#4 = 922;
          |LEE 101#nomvarli.=;
          |HAZ GrabaNomvarli;
     |FINSI;

     |LIBERA #nomvarca;
|FPRC;

|PROCESO LeeTemporal;
     |HAZ BorraCosas;

     nJornadas = 0;
     nJornadasIT = 0;
     |PARA nCampo = 8; |SI nCampo [ 38; |HACIENDO nCampo = nCampo + 1;
          nCampo1 = nCampo + 33;
          nTrabajador = #mod00270#nCampo1#;

          |SI nTrabajador ! 00000;
               |SI #mod00270#nCampo# = "X";
                    nJornadas = nJornadas + 1;
               |FINSI;
               |SI #mod00270#nCampo# = "I";
                    nJornadasIT = nJornadasIT + 1;
               |FINSI;

               |SI #mod00270#nCampo# = "X"; |O #mod00270#nCampo# = "I";
                    |DDEFECTO #gomdetjr;
                    #gomdetjr#0 = #mod00270#0;
                    #gomdetjr#1 = nTrabajador;
                    #gomdetjr#2 = #mod00270#2;
                    #gomdetjr#3 = #mod00270#3;
                    |LEE 101#gomdetjr.=;
                    FEstado = FSalida;

                    nCampo2 = nCampo - 4;
                    #gomdetjr#nCampo2# = #mod00270#nCampo#;
                    |SI FEstado = 0;
                         |GRABA 020#gomdetjr.a;
                    |SINO;
                         |GRABA 020#gomdetjr.n;
                    |FINSI;
                    |LIBERA #gomdetjr;

                    |HAZ GrabaJornada;
               |FINSI;
          |FINSI;
     |SIGUE;
|FPRC;

|DEFBUCLE LeeTemporal;
     #mod00270#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, LeeTemporal;
|FIN;

|PROCESO Actualizacion;
     |BUCLE LeeTemporal;
|FPRC;

/****************************************************************************/
/************************* AUXILIARES CALENDARIO ****************************/
/****************************************************************************/

|PROCESO BuscaDia;
     swFestivo = 0;
     |ABRE #nomfical;
     |DDEFECTO #nomfical;
     #nomfical#0 = #labtraba#156;
     #nomfical#1 = #0#1;
     |LEE 000#nomfical.=;

     fDiaLet = fFecha % 1;
     aDiaLet = fDiaLet;
     aDiaLet = aDiaLet % 1103;

     |SI aDiaLet = "Lun"; |Y #nomfical#2 = 1; swFestivo = 1; |FINSI;
     |SI aDiaLet = "Mar"; |Y #nomfical#3 = 1; swFestivo = 1; |FINSI;
     |SI aDiaLet = "Mie"; |Y #nomfical#4 = 1; swFestivo = 1; |FINSI;
     |SI aDiaLet = "Jue"; |Y #nomfical#5 = 1; swFestivo = 1; |FINSI;
     |SI aDiaLet = "Vie"; |Y #nomfical#6 = 1; swFestivo = 1; |FINSI;
     |SI aDiaLet = "Sab"; |Y #nomfical#7 = 1; swFestivo = 1; |FINSI;
     |SI aDiaLet = "Dom"; |Y #nomfical#8 = 1; swFestivo = 1; |FINSI;

     aFecha = fFecha;
     aAlfa = (aFecha % 402) + (aFecha % 102);
     nFechaNume = aAlfa;

     |PARA nCampoF = 9; |SI nCampoF [ 96; |HACIENDO nCampoF = nCampoF + 3;
          nCampoF1 = nCampoF;
          nCampoF2 = nCampoF + 1;
          nCampoF3 = nCampoF + 2;

          aAlfa1 = #nomfical#nCampoF1#; |QBF aAlfa1;
          aAlfa2 = #nomfical#nCampoF2#; |QBF aAlfa2;
          nNume3 = #nomfical#nCampoF3#;
          |SI aAlfa1 ! ""; |Y aAlfa2 ! ""; |Y nNume3 = 1;
               aAlfa1 = (aAlfa1 % 302) + (aAlfa1 % 102);
               nNume1 = aAlfa1;
               aAlfa2 = (aAlfa2 % 302) + (aAlfa2 % 102);
               nNume2 = aAlfa2;
               |SI nNume1 [ nFechaNume; |Y nFechaNume [ nNume2;
                    swFestivo = 1;
                    |SAL;
               |FINSI;
          |FINSI;
     |SIGUE;

     |SI swFestivo = 0;
          |PARA nCampoF = 99; |SI nCampoF [ 157; |HACIENDO nCampoF = nCampoF + 2;
               nCampoF1 = nCampoF;
               nCampoF3 = nCampoF + 1;

               aAlfa1 = #nomfical#nCampoF1#; |QBF aAlfa1;
               nNume3 = #nomfical#nCampoF3#;
               |SI aAlfa1 ! ""; |Y nNume3 = 1;
                    aAlfa1 = (aAlfa1 % 302) + (aAlfa1 % 102);
                    nNume1 = aAlfa1;
                    |SI nNume1 = nFechaNume;
                         swFestivo = 1;
                         |SAL;
                    |FINSI;
               |FINSI;
          |SIGUE;
     |FINSI;
     |CIERRA #nomfical;
|FPRC;

|PROCESO CompDia;
     swAlta = 0;
     swFestivo = 0;
     swIT = 0;

     nCampoCod = nDiaMes + 40;

     #labtraba#0 = nEmp;
     #labtraba#1 = #mod00270#nCampoCod#;
     |LEE 000#labtraba.=;

     aAlfa = #labtraba#36;
     fFechaAlta = aAlfa;

     aAlfa = #labtraba#37;
     |QBF aAlfa;
     |SI aAlfa = ""; |O aAlfa = "..........";
          fFechaBaja = "31.12.2099";
     |SINO;
          fFechaBaja = aAlfa;
     |FINSI;

     |DDEFECTO #nomvarca;
     #nomvarca#0 = #labtraba#0;
     #nomvarca#1 = #mod00270#nCampoCod#;
     #nomvarca#2 = #0#2;
     #nomvarca#3 = 0;
     |LEE 000#nomvarca.=;
     |SI FSalida = 0;
          aAlfa = #nomvarca#16;
          |QBF aAlfa;
          |SI aAlfa = ""; |O aAlfa = "..........";
               || me quedo con la que venia
          |SINO;
               fFechaBaja = aAlfa;
          |FINSI;
     |FINSI;

     aAlfa1 = nDiaMes; |QBF aAlfa1; aAlfa1 = ("00" + aAlfa1) % -102;
     aAlfa2 = #0#2; |QBF aAlfa2; aAlfa2 = ("00" + aAlfa2) % -102;
     aAlfa3 = #0#1;
     aAlfa = aAlfa1 + "." + aAlfa2 + "." + aAlfa3;
     fFecha = aAlfa;

     swAlta = 1;

     nCampoCod = nDiaMes + 40;
     |SI #mod00270#nCampoCod# = 00000;
          swAlta = 0;
     |FINSI;

     |DDEFECTO #nomvarca;
     #nomvarca#0 = #labtraba#0;
     #nomvarca#1 = #mod00270#nCampoCod#;
     #nomvarca#2 = #0#2;
     #nomvarca#3 = 0;
     |LEE 000#nomvarca.=;
     |SI FSalida = 0;
          |PARA nCampo3 = 12; |SI nCampo3 [ 15; |HACIENDO nCampo3 = nCampo3 + 1;
               nDesde = 0;
               nHasta = 0;
               |SI #nomvarca#nCampo3# ! " ";
                    nCampo1 = nCampo3 - 8;
                    nCampo2 = nCampo3 - 4;
                    |SI #nomvarca#nCampo1# = 0;
                         #nomvarca#nCampo1# = 1;
                    |FINSI;
                    nDesde = #nomvarca#nCampo1#;

                    |SI #nomvarca#nCampo2# = 0;
                         #nomvarca#nCampo2# = topemes;
                    |FINSI;
                    nHasta = #nomvarca#nCampo2#;

                    |SI nDesde [ nDiaMes; |Y nDiaMes [ nHasta;
                         swIT = 1;
                    |FINSI;
               |SINO;
                    |SI nCampo3 = 12;
                         |SI #labtraba#89 ! 0; |O #labtraba#92 ! 0;
                              swIT = 1;
                         |FINSI;
                    |FINSI;
               |FINSI;
          |SIGUE;
     |FINSI;

     |SI swAlta = 0;
          || me da igual festivo o no
     |SINO;
          |HAZ BuscaDia;
     |FINSI;
|FPRC;

|PROCESO Incidencias;
     nEmp = #mod00270#0;
     nTra = #mod00270#1;
     |HAZ CompDia;

     |SI swAlta = 0;
          |ATRI R;
     |SINO;
          |SI swFestivo = 1;
               |ATRI S;
          |SINO;
               |SI swIT = 1;
                    |ATRI I;
               |SINO;
                    |ATRI 0;
               |FINSI;
          |FINSI;
     |FINSI;

     aAlfa = nDiaMes;
     |QBF aAlfa;
     aAlfa = ("  " + aAlfa) % -102;
     nPosMenosUno = nPos - 1;
     |SI nDiaMes [ 9;
          |PINTA nPosMenosUno, aAlfa;
     |SINO;
          |PINTA nPos, aAlfa;
     |FINSI;

     |SI swFestivo = 1; |O swAlta = 0;
          nCampoCod = nDiaMes + 7;
          |CAMPO_MODIFICA #mod00270#nCampoCod#, 1, "N";
     |FINSI;

     |ATRI 0;
|FPRC;

/****************************************************************************/
/*******************  PROCESOS PARA PINTAR EL CALENDARIO ********************/
/****************************************************************************/

|PROCESO ulti_dia;  || halla el ultimo dia del mes
    numer = anynum $ 4;
|SI numer = 0;
        bisiesto = 1;
|SINO;
        bisiesto = 0;
|FINSI;
|SI mesnum =  1;  topemes = 31;            |FINSI;
|SI mesnum =  2;  topemes = 28 + bisiesto; |FINSI;
|SI mesnum =  3;  topemes = 31;            |FINSI;
|SI mesnum =  4;  topemes = 30;            |FINSI;
|SI mesnum =  5;  topemes = 31;            |FINSI;
|SI mesnum =  6;  topemes = 30;            |FINSI;
|SI mesnum =  7;  topemes = 31;            |FINSI;
|SI mesnum =  8;  topemes = 31;            |FINSI;
|SI mesnum =  9;  topemes = 30;            |FINSI;
|SI mesnum = 10;  topemes = 31;            |FINSI;
|SI mesnum = 11;  topemes = 30;            |FINSI;
|SI mesnum = 12;  topemes = 31;            |FINSI;
|FPRC;

|PROCESO cabecera;
     |BLANCO 1039 1679;
     |ATRI R;
     |PINTA 1039, " Lun ";
     |PINTA 1045, " Mar ";
     |PINTA 1051, " Mi‚ ";
     |PINTA 1057, " Jue ";
     |PINTA 1063, " Vie ";
     |PINTA 1069, " S b ";
     |PINTA 1075, " Dom ";
     |ATRI 0;
|FPRC;

|PROCESO Repinta; |TIPO 13;
     nModo = (FEntrada / 100) ! 0;
     |HAZ dias;
|FPRC;

|PROCESO Repinta2; |TIPO 0;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 1;
          |HAZ dias;
     |FINSI;
|FPRC;

|PROCESO Inicializa;
     |PARA i = 8; |SI i [ 38; |HACIENDO i = i + 1;
          |CAMPO_MODIFICA #mod00270#i#, 1, "S";
     |SIGUE;
|FPRC;

|PROCESO calendario;
     nDiaMes = 1;
     nFilNum = 1100;
     ||nColIni = 15;
     ||nColCam = 23;
     nColIni = 33;
     nColCam = 44;
     nPaso = 6;
     nCampo = 8;
     |HAZ cabecera;
     |HAZ Inicializa;
     |PARA nFilNum = nFilNum; |SI nDiaMes [ topemes; |HACIENDO nFilNum = nFilNum + 100;
     |PARA nDiaSem = nDiaSem; |SI nDiaSem [ 7;  |HACIENDO nDiaSem = nDiaSem + 1;
          nColNum = nColIni + (nDiaSem * nPaso);
          nPos = nFilNum + nColNum;
          |SI nDiaMes < 10;
               nPos = nPos + 1;
               a = 1;
          |SINO;
               a = 0;
          |FINSI;
          |PINTA nPos, nDiaMes;

          |HAZ Incidencias;

          nColCam = nPos + 3 - a;
          |CAMPO_POSICION #mod00270#nCampo#, 1, nColCam;
          |CAMPO_VISUAL   #mod00270#nCampo#, 1, "S";
          |PINTA #mod00270#nCampo#;
          |SI nDiaMes < 10;
               nPos = nPos - 1;
          |FINSI;
          nDiaMes = nDiaMes + 1;
          |SI nDiaMes > topemes; |SAL; |FINSI;
          nCampo = nCampo + 1;
     |SIGUE;
     nDiaSem = 1;
     |SIGUE;

     topemes = topemes + 8
     |PARA nCampo = topemes; |SI nCampo [ 38; |HACIENDO nCampo = nCampo + 1;
          |CAMPO_MODIFICA #mod00270#nCampo#, 1, "N";
          |CAMPO_VISUAL #mod00270#nCampo#, 1, "N";
     |SIGUE;
|FPRC;

|PROCESO dias; |TIPO 7;
     aAnyo = #mod00270#2;
     aMes  = #mod00270#3;
     aMes = "00" + aMes;
     aMes = aMes % -102;
     aFecha = "01" + "." + aMes + "." + aAnyo;
     fFecha = aFecha;
     fDiaLet = fFecha % 1;
     aDiaLet = fDiaLet;
     aDiaLet = aDiaLet % 1103;
     |SI aDiaLet = "Lun"; nDiaSem = 1; |FINSI;
     |SI aDiaLet = "Mar"; nDiaSem = 2; |FINSI;
     |SI aDiaLet = "Mie"; nDiaSem = 3; |FINSI;
     |SI aDiaLet = "Jue"; nDiaSem = 4; |FINSI;
     |SI aDiaLet = "Vie"; nDiaSem = 5; |FINSI;
     |SI aDiaLet = "Sab"; nDiaSem = 6; |FINSI;
     |SI aDiaLet = "Dom"; nDiaSem = 7; |FINSI;
     mesnum = #mod00270#3;
     anynum = #mod00270#2;
     |HAZ ulti_dia;
     |HAZ calendario;

     |CAMPO_MODIFICA #mod00270#73, 1, "N";
     |CAMPO_VISUAL #mod00270#73, 1, "N";
     |PINTA 2038, "³                           ³"
/*
     |PARA nCampo = 8; |SI nCampo [ 71; |HACIENDO nCampo = nCampo + 1;
          |SI nCampo = 39; |O nCampo = 40;
               || nada
          |SINO;
               #mod00270#nCampo# = "";
               |PINTA #mod00270#nCampo#;
          |FINSI;
     |SIGUE;
*/
|FPRC;

/****************************************************************************/
/********************* CARGA INICIAL DEL TEMPORAL ***************************/
/****************************************************************************/

|PROCESO labtraba;
     aAlfa = #labtraba#36;
     |QBF aAlfa;
     |SI aAlfa ! "";
          fFechaAlta = aAlfa;
     |FINSI;

     aAlfa = #labtraba#37;
     |QBF aAlfa;
     |SI aAlfa ! "";
          fFechaBaja = aAlfa;
     |SINO;
          fFechaBaja = "31.12.2099";
     |FINSI;

     |DDEFECTO #nomvarca;
     #nomvarca#0 = #labtraba#0;
     #nomvarca#1 = #labtraba#1;
     #nomvarca#2 = #0#2;
     #nomvarca#3 = 0;
     |LEE 000#nomvarca.=;
     |SI FSalida = 0;
          aAlfa = #nomvarca#16;
          |QBF aAlfa;
          |SI aAlfa = ""; |O aAlfa = "..........";
               || me quedo con la que venia
          |SINO;
               fFechaBaja = aAlfa;
          |FINSI;
     |FINSI;

     nNume = nCampo - 40;
     aAlfa1 = nNume; |QBF aAlfa1; aAlfa1 = ("00" + aAlfa1) % -102;
     aAlfa2 = #0#2; |QBF aAlfa2; aAlfa2 = ("00" + aAlfa2) % -102;
     aAlfa3 = #0#1; |QBF aAlfa3;
     aAlfa = aAlfa1 + "." + aAlfa2 + "." + aAlfa3;
     fDia = aAlfa;

     |SI fFechaAlta [ fDia; |Y fDia [ fFechaBaja;
          #mod00270#nCampo# = #labtraba#1;
     |FINSI;
|FPRC;

|DEFBUCLE labtraba;
     #labtraba#7;
     ;
     #mod00270#4, #mod00270#0, 00001;
     #mod00270#4, #mod00270#0, 99999;
     ;
     NULL, labtraba;
|FIN;

|PROCESO Relleno;
     |PARA nCampo = 41; |SI nCampo [ 71; |HACIENDO nCampo = nCampo + 1;
          |SI #mod00270#nCampo# = 00000;
               |BUCLE labtraba;
               |GRABA 020#mod00270.a;
               |LIBERA #mod00270;
          |FINSI;
     |SIGUE;
|FPRC;

|DEFBUCLE Relleno;
     #mod00270#1;
     ;
     "            ";
     "zzzzzzzzzzzz";
     be;
     NULL, Relleno;
|FIN;

|PROCESO mod00262;
     #gomdetjr#0 = #mod00262#0;
     #gomdetjr#1 = #mod00262#1;
     #gomdetjr#2 = #0#1;
     #gomdetjr#3 = #0#2;
     |LEE 000#gomdetjr.=;
     |SI FSalida = 0;
          |ACABA;        || ya lo te puesto antes, en el gomdetjr
     |FINSI;

     #labtraba#0 = #mod00262#0;
     #labtraba#1 = #mod00262#1;
     |LEE 000#labtraba.=;

     swSigue = 1;
     |HAZ Comprueba;

     |SI swSigue = 0;
          |ACABA;
     |FINSI;

     |SI swUno = 1;
          |SI aNif = #labtraba#7;
               || SIGUE
          |SINO;
               |ACABA;
          |FINSI;
     |FINSI;

     |DDEFECTO #mod00270
     #mod00270#4 = #labtraba#7;
     |LEE 101#mod00270.=;
     FEstado = FSalida;

     #mod00270#0 = #mod00262#0;
     #mod00270#1 = #mod00262#1;
     #mod00270#2 = #mod00262#2;
     #mod00270#3 = #mod00262#3;
     #mod00270#5 = #labtraba#2;

     nCampoDia = #mod00262#4 + 7;
     #mod00270#nCampoDia# = "X";
     nCampo = nCampoDia + 33;
     #mod00270#nCampo# = #mod00262#1;

     |SI FEstado = 0;
          #mod00270#6 = #mod00270#6 + 1;
          #mod00270#7 = #mod00270#7 + 1;
          |GRABA 020#mod00270.a;
     |SINO;
          #mod00270#6 = 1;
          #mod00270#7 = 1;
          |GRABA 020#mod00270.n;
          swHayAlguno = 1;
     |FINSI;
     |LIBERA #mod00270;
|FPRC;

|DEFBUCLE mod00262;
     #mod00262#1;
     ;
     nDesdeEmp, nDesdeTra, #0#1, #0#2, 01;
     nHastaEmp, nHastaTra, #0#1, #0#2, 31;
     ;
     NULL, mod00262;
|FIN;

|PROCESO Comprueba;
     |SI #0#3 [ #labtraba#77; |Y #labtraba#77 [ #0#4;
          || adelante
     |SINO;
          swSigue = 0;
     |FINSI;

     aFechaBaja = "";
     #nomvarca#0 = #labtraba#0;
     #nomvarca#1 = #labtraba#1;
     #nomvarca#2 = #0#2;
     #nomvarca#3 = 0;
     |LEE 000#nomvarca.=;
     |SI FSalida = 0;
          aAlfa = #nomvarca#16;
          |QBF aAlfa;
          |SI aAlfa ! "";
               aFechaBaja = aAlfa;
          |FINSI;
     |FINSI;

     |SI aFechaBaja = "";
          aFechaBaja = #labtraba#37;
          |QBF aFechaBaja;
     |FINSI;

     |SI #0#5 ! "01.01.0000"; |Y #0#6 ! "01.01.0000";
          |SI aFechaBaja ! "";
               fFechaBaja = aFechaBaja;
               |SI #0#5 [ fFechaBaja; |Y fFechaBaja [ #0#6;
                    || continua
               |SINO;
                    swSigue = 0;
               |FINSI;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO gomdetjr;
     #labtraba#0 = #gomdetjr#0;
     #labtraba#1 = #gomdetjr#1;
     |LEE 000#labtraba.=;

     swSigue = 1;
     |HAZ Comprueba;

     |SI swSigue = 0;
          |ACABA;
     |FINSI;

     |DDEFECTO #mod00270;
     #mod00270#4 = #labtraba#7;
     |LEE 101#mod00270.=;
     FEstado = FSalida;

     #mod00270#0 = #gomdetjr#0;
     #mod00270#1 = #gomdetjr#1;
     #mod00270#2 = #gomdetjr#2;
     #mod00270#3 = #gomdetjr#3;
     #mod00270#4 = #labtraba#7;
     #mod00270#5 = #labtraba#2;

     |PARA nCampo = 4; |SI nCampo [ 34; |HACIENDO nCampo = nCampo + 1;
          nCampo1 = nCampo + 4;
          nCampo2 = nCampo + 37;
          |SI #gomdetjr#nCampo# = "X"; |O #gomdetjr#nCampo# = "I";
               #mod00270#nCampo1# = #gomdetjr#nCampo#;
               #mod00270#nCampo2# = #labtraba#1;
               |SI #gomdetjr#nCampo# = "X";
                    #mod00270#6 = #mod00270#6 + 1;
               |FINSI;
               |SI #gomdetjr#nCampo# = "I";
                    #mod00270#72 = #mod00270#72 + 1;
               |FINSI;
          |FINSI;
     |SIGUE;

     |SI FEstado = 0;
          |GRABA 020#mod00270.a;
     |SINO;
          |GRABA 020#mod00270.n;
          swHayAlguno = 1;
     |FINSI;

     |LIBERA #mod00270;
|FPRC;

|DEFBUCLE gomdetjr;
     #gomdetjr#1;
     ;
     #0#0, 00001, #0#1, #0#2;
     #0#0, 99999, #0#1, #0#2;
     ;
     NULL, gomdetjr;
|FIN;

|PROCESO CargaTmp;
     aAlfa = Usuario; |QBT aAlfa; aAlfa = "tm_mod00270_" + aAlfa;
     |NOME_DAT #mod00270#aAlfa#;
     |DELINDEX #mod00270;

     |ABRE #mod00270;

     nDesdeEmp = #0#0;
     nHastaEmp = #0#0;
     nDesdeTra = 00001;
     nHastaTra = 99999;
     |SI swUno = 1;
          nDesdeTra = #0#1;
          nHastaTra = #0#1;
     |FINSI;

     |BUCLE gomdetjr;
     |BUCLE mod00262;
     |BUCLE Relleno;

     |CIERRA #mod00270;
|FPRC;

/****************************************************************************/

|PROCESO inferior;
     #mod00270#4 = "            ";
|FPRC;

|PROCESO superior;
     #mod00270#4 = "zzzzzzzzzzzz";
|FPRC;

|PROGRAMA;
     |CLS;
     |ABRE #0;
     |LEE 001#0p;
     |SI FSalida ! 0;
          |DDEFECTO #0;
          |GRABA 020#0n;
     |FINSI;
     |LEE 101#0=;
     |PINPA #0#0;
     |PINDA #0#0;
     |ENDATOS #1#0;
     |SI FSalida = 0;
          |ABRE #labtraba;
          |ABRE #nomvarca;
          |ABRE #nomvarli;
          |ABRE #gomdetjr;

          || lo primero es cargar los datos

          swHayAlguno = 0;
          |HAZ CargaTmp;

          |SI swHayAlguno = 1;
               || ahora activo los campos del calendario

               |HAZ dias;

               |ENTLINEAL #1#mod00270, 1, 2, 22, 0, inferior, superior;

               aAlfa = "    S¿Desea actualizar los cambios en el calendario ";
               aAlfa = aAlfa + "y en variaciones para el cálculo de la nómina?";
               |CONFI aAlfa;
               |SI FSalida = 0;
                    |HAZ Actualizacion;
               |FINSI;
          |SINO;
               aAlfa = "    No se han encontrado trabajadores con horas en IDAL ni ";
               aAlfa = aAlfa + "calendario de jornadas con la selección realizada ";
               |MENAV aAlfa;
          |FINSI;

          |CIERRA #gomdetjr;
          |CIERRA #nomvarli;
          |CIERRA #nomvarca;
          |CIERRA #labtraba;

          |GRABA 020#0a;
          |LIBERA #0;
          |CIERRA #0;
     |FINSI;
|FPRO;
