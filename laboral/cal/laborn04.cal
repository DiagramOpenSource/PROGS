|FICHEROS;
     laborn04;           || este def el del lineal
     nomvarca;
     nomvarli;
     nomtraca;
     nomtrali;
     nomabsca;
     nomabsli;
     labtraba;
     nomconca;
     nompmail;
     labornx4;
|FIN;

|VARIABLES;
     &eaDirDest  = "";
     &eaAsunto   = "";
     &eaTexto    = "";
     &eaFichero  = "";
     aAlfa1 = "";

     aParam = "";
     &eaUsuario = "";
     &eaFecha = "";
     &eaHora = "";
     aTipo = "";
     sw = 0;
     aAlfa = "";
     FEstado = 0;
     nMes = 0;

     nDesde = 0;
     nHasta = 0;
     nCampo = 0;
     nCampo1 = 0;
     nCampo2 = 0;
     nCampo3 = 0;
     nCampo4 = 0;
     nCampo5 = 0;
     nCampo6 = 0;
     nCampo7 = 0;
     nNume = 0;
     nNume1 = 0;
     nNume2 = 0;
     nNume3 = 0;
     nNume4 = 0;
     nNume5 = 0;
     nNume6 = 0;
     nLimpio = 0;
     nConcepto = 0;
     nUnidades = 0;
     nImporte = 0;
     aMes = "";
     nAny = 0;
     aAny = "";
     aDesde = "";
     aHasta = "";
     fFechaDesde = @;
     fFechaHasta = @;
     aFechaDesde = "";
     aFechaHasta = "";
     nHoras = 0;
     aUsuarioD = "";
     aUsuarioH = "";
     aHosp = "";
     aReca = "";
     aFecR = "";
     aSiEsX = "";

     &eaVengoDe = "laborn04";
     &eaEmpresa = "";

     &eaParam       = "";
     &enFSalida     = 0;
     &efFecha       = @;
     &enBorrado     = 0;
     &enActualizado = 0;
     &eaEstado      = "";
     &eaMotiv       = "";
     &nLabornet     = 0;

     aDescripcion = "";
     nLinea = 0;
     nPorERE = 0;
     nActual = 0;
     swSigue = 0;
|FIN;

|PROCESO ImporteTra;
     |ABRE #nomtraca;
     |ABRE #nomtrali;
     #nomtraca#0 = #laborn04#5;
     #nomtraca#1 = #laborn04#6;
     |LEE 101#nomtraca.=;
     FEstado = FSalida;

     |SI FEstado ! 0;
          |GRABA 020#nomtraca.n;
     |SINO;
          |GRABA 020#nomtraca.a;
     |FINSI;

     |LIBERA #nomtraca;

     #nomtrali#0 = #laborn04#5;
     #nomtrali#1 = #laborn04#6;
     #nomtrali#2 = nConcepto;
     |LEE 000#nomtrali.=;
     FEstado = FSalida;

     aAlfa = nUnidades;
     aAlfa = ("          " + aAlfa) % -110;
     #nomtrali#3 = aAlfa;

     aAlfa = nImporte;
     |SI nUnidades ! 0; |Y nImporte = 0;
          aAlfa = "";
          || con importe cero, paso solo unidades e importe en blanco
          || por si fuera un concepto en que solo se pasan unidades, el importe
          || se queda en blanco y lo toma del convenio
     |FINSI;
     aAlfa = ("            " + aAlfa) % -112;
     #nomtrali#4 = aAlfa;
     #nomtrali#5 = aDescripcion;

     |SI FEstado ! 0;
          |GRABA 020#nomtrali.n;
     |SINO;
          |GRABA 020#nomtrali.a;
     |FINSI;
     |LIBERA #nomtrali;

     |CIERRA #nomtrali;
     |CIERRA #nomtraca;
|FPRC;

|PROCESO VariacionMes;
     nMes = #laborn04#7;

     |ABRE #nomvarca;
     |ABRE #nomvarli;
     #nomvarca#0 = #laborn04#5;
     #nomvarca#1 = #laborn04#6;
     #nomvarca#2 = nMes;
     #nomvarca#3 = 0;
     |LEE 101#nomvarca.=;
     FEstado = FSalida;

     |SI FEstado ! 0;
          |GRABA 020#nomvarca.n;
     |SINO;
          |GRABA 020#nomvarca.a;
     |FINSI;

     |LIBERA #nomvarca;

     #nomvarli#0 = #laborn04#5;
     #nomvarli#1 = #laborn04#6;
     #nomvarli#2 = nMes;
     #nomvarli#3 = 0;
     #nomvarli#4 = nConcepto;
     |LEE 000#nomvarli.=;
     FEstado = FSalida;

     aAlfa = nUnidades;
     aAlfa = ("          " + aAlfa) % -110;
     #nomvarli#5 = aAlfa;

     aAlfa = nImporte;
     |SI nUnidades ! 0; |Y nImporte = 0;
          aAlfa = "";
          || con importe cero, paso solo unidades e importe en blanco
          || por si fuera un concepto en que solo se pasan unidades, el importe
          || se queda en blanco y lo toma del convenio
     |FINSI;
     aAlfa = ("            " + aAlfa) % -112;
     #nomvarli#6 = aAlfa;

     |SI FEstado ! 0;
          |GRABA 020#nomvarli.n;
     |SINO;
          |GRABA 020#nomvarli.a;
     |FINSI;
     |LIBERA #nomvarli;

     |CIERRA #nomvarli;
     |CIERRA #nomvarca;
|FPRC;

|PROCESO AbsVacHue;
     nMes = #laborn04#7;
     aMes = nMes;
     aMes = ("00" + aMes) % -102;
     nAny = #laborn04#102;
     aAny = nAny;

     |ABRE #labtraba;
     #labtraba#0 = #laborn04#5;
     #labtraba#1 = #laborn04#6;
     |LEE 000#labtraba.=;
     |CIERRA #labtraba;

     |ABRE #nomabsca;

     #nomabsca#0 = #laborn04#5;
     #nomabsca#1 = #laborn04#6;
     |LEE 101#nomabsca.=;
     FEstado = FSalida;

     |SI FEstado ! 0;
          |SI #labtraba#47 ! 0;
               #nomabsca#2 = #labtraba#47;
          |SINO;
               #nomabsca#2 = 8;
          |FINSI;

          |GRABA 020#nomabsca.n;
     |FINSI;
     |LIBERA #nomabsca;

     |ABRE #nomconca;
     #nomconca#0 = #labtraba#87;
     |LEE 000#nomconca.=;
     |CIERRA #nomconca;

     |ABRE #nomabsli;
     #nomabsli#0 = #laborn04#5;
     #nomabsli#1 = #laborn04#6;
     aDesde = nDesde;
     aDesde = ("00" + aDesde) % -102;
     aHasta = nHasta;
     aHasta = ("00" + aHasta) % -102;

     aFechaDesde = aDesde + "." + aMes + "." + aAny;
     fFechaDesde = aFechaDesde;
     #nomabsli#2 = fFechaDesde;
     |LEE 000#nomabsli.=
     FEstado = FSalida;

     aFechaHasta = aHasta + "." + aMes + "." + aAny;
     fFechaHasta = aFechaHasta;
     #nomabsli#3 = fFechaHasta;

     |SI aTipo = "F"; #nomabsli#4 = "A"; |FINSI;
     |SI aTipo = "H"; #nomabsli#4 = "H"; |FINSI;
     |SI aTipo = "V"; #nomabsli#4 = "V"; |FINSI;

     |SI aTipo = "F";
          #nomabsli#5 = #nomconca#147;
     |SINO;
          #nomabsli#5 = "N";
     |FINSI;

     #nomabsli#6 = nHoras;

     |SI FEstado = 0;
          |GRABA 020#nomabsli.a;
     |SINO;
          |GRABA 020#nomabsli.n;
     |FINSI;

     |CIERRA #nomabsli;
|FPRC;

|PROCESO EnfAccMatPat;
     nMes = #laborn04#7;

     |ABRE #nomvarca;

     #nomvarca#0 = #laborn04#5;
     #nomvarca#1 = #laborn04#6;
     #nomvarca#2 = nMes;
     #nomvarca#3 = 0;
     |LEE 101#nomvarca.=;
     FEstado = FSalida;

     |SI FEstado ! 0;
          |GRABA 020#nomvarca.c;
     |SINO;
          |GRABA 020#nomvarca.a;
     |FINSI;

     |SI nLimpio = 0;
          |PARA nNume = 12; |SI nNume [ 15; |HACIENDO nNume = nNume + 1;
               nNume1 = nNume - 8;
               nNume2 = nNume - 4;
               #nomvarca#nNume1# = 0;
               #nomvarca#nNume2# = 0;
               #nomvarca#nNume# = " ";
               nLimpio = 1;
          |SIGUE;
     |FINSI;

     |PARA nNume = 12; |SI nNume [ 15; |HACIENDO nNume = nNume + 1;
          |SI #nomvarca#nNume# = " ";
               nNume1 = nNume - 8;
               nNume2 = nNume - 4;
               nNume3 = nNume + 19;
               nNume4 = nNume + 23;
               nNume5 = nNume + 27;
               nNume6 = nNume + 35;

               #nomvarca#nNume1# = nDesde;
               #nomvarca#nNume2# = nHasta;
               #nomvarca#nNume# = aTipo;
               #nomvarca#nNume3# = aHosp;
               #nomvarca#nNume4# = aReca;
               #nomvarca#nNume5# = aFecR;
               #nomvarca#nNume6# = nPorERE;
               |SAL;
          |FINSI;
     |SIGUE;

     |SI #laborn04#141 ! 0;
          #nomvarca#17 = #laborn04#141;
     |FINSI;
     |SI #laborn04#142 ! 0;
          #nomvarca#18 = #laborn04#142;
     |FINSI;

     #nomvarca#43 = #laborn04#143;

     |GRABA 020#nomvarca.a;

     |LIBERA #nomvarca;

     |CIERRA #nomvarca;
|FPRC;

|PROCESO Anticipos;
     nMes = #laborn04#7;

     |ABRE #nomvarca;

     #nomvarca#0 = #laborn04#5;
     #nomvarca#1 = #laborn04#6;
     #nomvarca#2 = nMes;
     #nomvarca#3 = 0;
     |LEE 101#nomvarca.=;
     FEstado = FSalida;

     |SI FEstado ! 0;
          |GRABA 020#nomvarca.c;
     |SINO;
          |GRABA 020#nomvarca.a;
     |FINSI;

     #nomvarca#28 = #laborn04#133; #nomvarca#19 = #laborn04#134;
     #nomvarca#29 = #laborn04#135; #nomvarca#20 = #laborn04#136;
     #nomvarca#45 = #laborn04#137; #nomvarca#44 = #laborn04#138;
     #nomvarca#30 = #laborn04#139; #nomvarca#21 = #laborn04#140;

     |GRABA 020#nomvarca.a;

     |LIBERA #nomvarca;

     |CIERRA #nomvarca;
|FPRC;

|PROCESO Actualizacion;
     nLimpio = 0;
     nLinea = 0;
     |PARA nCampo = 14; |SI nCampo [ 59; |HACIENDO nCampo = nCampo + 5;
          nLinea = nLinea + 1;
          nCampo1 = nCampo - 2;
          nCampo2 = nCampo - 1;
          nCampo3 = nCampo + 2;
          nCampo4 = (((nCampo - 9) / 5) * 3) + 100;
          nCampo5 = (((nCampo - 9) / 5) * 3) + 101;
          nCampo6 = (((nCampo - 9) / 5) * 3) + 102;
          nCampo7 = nLinea + 166;
          |SI #laborn04#nCampo# ! " ";
               aTipo = #laborn04#nCampo#;
               nDesde = #laborn04#nCampo1#;
               nHasta = #laborn04#nCampo2#;
               nHoras = #laborn04#nCampo3#;
               aHosp = #laborn04#nCampo4#;
               aReca = #laborn04#nCampo5#;
               aFecR = #laborn04#nCampo6#;
               nPorERE = #laborn04#nCampo7#;
               |SI aTipo = "F"; |O aTipo = "V"; |O aTipo = "H";
                    |HAZ AbsVacHue;
               |SINO;
                    |HAZ EnfAccMatPat;
               |FINSI;
          |FINSI;
     |SIGUE;

     nLinea = 0;
     |PARA nCampo = 62; |SI nCampo [ 98; |HACIENDO nCampo = nCampo + 4;
          nCampo1 = nCampo + 1;
          nCampo2 = nCampo + 2;
          nCampo3 = nCampo + 3;
          nLinea = nLinea + 1;
          nCampo4 = nLinea + 156;
          |SI #laborn04#nCampo# ! 0;
               nConcepto = #laborn04#nCampo#;
               aDescripcion = #laborn04#nCampo1#;
               nUnidades = #laborn04#nCampo2#;
               nImporte = #laborn04#nCampo3#;

               || este cambio es a partir de que dejemos poner unidades en laborbox
               || 08.05.2018
               |SI nUnidades = 0; |Y nImporte = 0;
                    || se queda asi
               |FINSI;
               |SI nUnidades = 0; |Y nImporte ! 0;
                    nUnidades = 1;
                    || para que el importe tenga efecto en la nomina
               |FINSI;

               |SI #laborn04#nCampo4# = "M";
                    |HAZ VariacionMes;
               |SINO;
                    |HAZ ImporteTra;
               |FINSI;
          |FINSI;
     |SIGUE;

     |SI #laborn04#134 ! 0; |O #laborn04#136 ! 0;
     |O #laborn04#138 ! 0; |O #laborn04#140 ! 0;
          |HAZ Anticipos;
     |FINSI;

     swSigue = 0;
     |SI #laborn04#156 ! 0; |Y #laborn04#155 ! 0;
          swSigue = 1;
     |FINSI;
     |SI #laborn04#156 = 0; |Y #laborn04#155 = 0;
          swSigue = 1;
          || tambien este caso, puede que antes tuviera ajuste y ahora
          || ya no quiero
     |FINSI;
     || el otro caso es tipo distinto de 0 e importe 0, esto quiere decir
     || que tenia ajuste y lo sigo teniendo pero este no cambia
     || no puedo (no tiene sentido) enviar un ajuste con tipo distinto
     || de cero e importe cero en otro caso
     |SI swSigue = 1;
          |ABRE #labtraba;
          #labtraba#0 = #laborn04#5;
          #labtraba#1 = #laborn04#6;
          |LEE 101#labtraba.=;
          |SI FSalida = 0;
               nActual = 0;
               |SI #labtraba#97 = 1; |O #labtraba#97 = 2; |O #labtraba#97 = 5;
                    nActual = 1;
               |FINSI;
               |SI #labtraba#97 = 3; |O #labtraba#97 = 4; |O #labtraba#97 = 5;
                    nActual = 2;
               |FINSI;

               |SI nActual ! #laborn04#155;
               |O #labtraba#98 ! #laborn04#156;
                    |SI #laborn04#155 = 0;
                         #labtraba#97 = 0;
                    |FINSI;
                    |SI #laborn04#155 = 1;
                         #labtraba#97 = 2;
                    |FINSI;
                    |SI #laborn04#155 = 2;
                         #labtraba#97 = 4;
                    |FINSI;
                    #labtraba#98 = #laborn04#156;
                    #labtraba#207 = "M";

                    |SI #labtraba#97 = 0;
                         #labtraba#98 = 0;
                    |FINSI;

                    |GRABA 020#labtraba.a;
               |FINSI;
          |FINSI;
          |LIBERA #labtraba;
          |CIERRA #labtraba;
     |FINSI;
|FPRC;

|PROCESO Email;
     || Emite correo electronico al usuario de que la variacion del trabajador
     ||    se ha hecho efectiva
     aAlfa = ("00000" + #laborn04#6) % -105;
     eaTexto = &13 + &10 + "El administrador ha hecho efectiva la incidencia del trabajador (" + aAlfa + ") "; |QBF eaTexto;
     aAlfa = ("00000" + #laborn04#5) % -106;
     eaTexto = eaTexto + " de la empresa (" + aAlfa + ") "; |QBF eaTexto;

     eaTexto = eaTexto + &13 + &10 + &13 + &10;
     eaTexto = eaTexto + "Tipo de incidencia: " + #laborn04#15; |QBF eaTexto;

     eaTexto = eaTexto + &13 + &10 + &13 + &10;
     eaTexto = eaTexto + "Actualización con fecha " + #laborn04#3 + " a las " + #laborn04#4 + &13 + &10;

     |ABRE #labtraba;
     #labtraba#0 = #laborn04#5;
     #labtraba#1 = #laborn04#6;
     |LEE 000#labtraba.=;
     |CIERRA #labtraba;

     aAlfa  = ("00000" + #laborn04#5) % -106;
     aAlfa1 = ("00000" + #laborn04#6) % -105;
     eaAsunto  = "Efectiva incidencia trabajador (" + aAlfa + "/" + aAlfa1 + ") " + #labtraba#2; |QBF eaAsunto;
     eaFichero = "";
     eaDirDest = "";

     aAlfa = #laborn04#5; |QBF aAlfa;
     eaEmpresa = ("00000" + aAlfa) % -105;
     |HAZ ConsEmail2;
|FPRC;

|PROCESO Actualiza;
|| xxxxxxxxxxxxxxxxxxxx
     nLabornet = 1;
     aAlfa = "    ATENCION: se va a iniciar el proceso de actualizacion";
     aAlfa = aAlfa + " de los datos del trabajador solicitado";
     |MENAV aAlfa;
     aAlfa = "    S¿Desea continuar?";
     |CONFI aAlfa;
     |SI FSalida = 0;
          |HAZ Actualizacion;
          |HAZ LeeFichaActual;

          || primero me paso todos los campos del registro que voy a borrar
          || a un temporal, ya que ha habido varios fallos en los que al
          || borrar el registro pierde la clave, asi me aseguro

          |DDEFECTO #labornx4;
          |PARA nCampo = 0; |SI nCampo [ 177; |HACIENDO nCampo = nCampo + 1;
               #labornx4#nCampo# = #laborn04#nCampo#;
          |SIGUE;

          |BORRA 020#laborn04.a;

          |PARA nCampo = 0; |SI nCampo [ 177; |HACIENDO nCampo = nCampo + 1;
               #laborn04#nCampo# = #labornx4#nCampo#;
          |SIGUE;

          #laborn04#0 = "A";
          #laborn04#144 = Usuario % 0810;
          #laborn04#145 = ~;
          |HORA aAlfa;
          #laborn04#146 = aAlfa;
          |GRABA 020#laborn04.n;
          |LIBERA #laborn04;

          aAlfa = "    Se han creado con éxito las incidencias en las ";
          aAlfa = aAlfa + "variaciones del trabajador";
          |MENAV aAlfa;
          |ABRE #nompmail; |LEE 000#nompmail.p; |CIERRA #nompmail;
          |SI #nompmail#11 = "S";
               |HAZ Email;
          |FINSI;

          enActualizado = 1;
     |FINSI;
     |PONCONTROL 23, "SI";
     |PTEC 808;
|FPRC;

|PROCESO Consulta;
     eaUsuario = #laborn04#1;      || permito modificar
     eaFecha = #laborn04#3;
     eaHora = #laborn04#4;
     |SUB_EJECUTA "laborn14;CONSUL";

     |ACABA;
|FPRC;

|PROCESO Modificacion;
     eaUsuario = #laborn04#1;      || permito modificar
     eaFecha = #laborn04#3;
     eaHora = #laborn04#4;
     |SUB_EJECUTA "laborn14;MODIF";

     || lo vuelvo a leer para tener el cuenta posibles modificaciones
     |LEE 000#laborn04.=;
     |ACABA;
|FPRC;

|PROCESO Opciones; |TIPO 0;
     |SI enFSalida ! 0;
          FSalida = enFSalida;
     |FINSI;
     || consulta de datos introducidos
     |SI FSalida = 10;
          |SI aParam = "USUARIO"; |O aParam = "ADMIN";  || consulta el usuario los datos que
               |HAZ Modificacion;
          |FINSI;
          |SI aParam = "HISTO"; |O aParam = "HISTOUSU";
               |HAZ Consulta;           || consulta de datos introducidos
          |FINSI;                       || o ya actualizados, solo visualizar
     |FINSI;

     |SI FSalida = 11;  |Y #laborn04#0 = "P"; |Y aParam = "ADMIN";
          |HAZ Modificacion;
          |HAZ Actualiza;
     |FINSI;

     |SI FSalida = 12;  |Y #laborn04#0 = "P";
          enBorrado = 1;
          |SI aSiEsX = "";
              |CONFI "0000N¿Está seguro de que desea rechazar esta solicitud?";
              |SI FSalida ! 0;
                  enBorrado = 0;
                  |ERROR;
                  |ACABA;
              |FINSI;
          |FINSI;

          |LEE 101#laborn04.=;
          |BORRA 020#laborn04.a;
          |LIBERA #laborn04;

          #laborn04#0   = "R";
          #laborn04#151 = eaMotiv;
          |GRABA 020#laborn04.n;
          |LIBERA #laborn04;

          |PONCONTROL 23, "SI";
          |PTEC 808;
     |FINSI;

     |SI FSalida = 13;
          ||MENAV "    Impresion";

          ||HAZ Imprimir;
     |FINSI;
|FPRC;

|PROCESO Mensa;  |TIPO 7;

     |SI #laborn04#0 = "P";        || pendientes de actualizar
          |SI aParam = "USUARIO";
               |MENSA "0000 <F2> Ver/modificar Datos. <F4> Borrar registro.";
          |SINO;
               |MENSA "0000 <F2> Ver datos. <F3> Actualizar incidencias. <F4> Borrar registro.";
          |FINSI;
     |SINO;                   || ya actualizados
          |MENSA "0000 <F2> Consultar Datos.";
     |FINSI;
|FPRC;


|PROCESO inferior;
     #laborn04#0 = aTipo;
     #laborn04#1 = aUsuarioD;
     #laborn04#2 = "";
     #laborn04#3 = "01.01.0000";
     #laborn04#4 = "";
|FPRC;

|PROCESO superior;
     #laborn04#0 = aTipo;
     #laborn04#1 = aUsuarioH;
     #laborn04#2 = "zzzzzzzzzz";
     #laborn04#3 = "31.12.2999";
     #laborn04#4 = "zzzzzzzz";
|FPRC;

|PROCESO inferiora;
     #laborn04#0 = aTipo;
     #laborn04#1 = aUsuarioD;
     #laborn04#2 = "";
     #laborn04#3 = "01.01.0000";
     #laborn04#4 = "";
     #laborn04#5 = 0;
|FPRC;

|PROCESO superiora;
     #laborn04#0 = aTipo;
     #laborn04#1 = aUsuarioH;
     #laborn04#2 = "zzzzzzzzzz";
     #laborn04#3 = "31.12.2999";
     #laborn04#4 = "zzzzzzzz";
     #laborn04#5 = 999999;
|FPRC;

|PROCESO LeeFichaActual;
     #laborn04#1 = eaUsuario;
     #laborn04#3 = efFecha;
     #laborn04#4 = eaHora;
     #laborn04#0 = eaEstado;
     |LEE 101#laborn04.=;
|FPRC;

|PROGRAMA;
     |SI enFSalida ! 12;
          |CLS;
     |FINSI;
     |CABEZA "Incidencias";

     aAlfa = Usuario % 0810;
     |SI aParam = "ADMIN"; |O aParam = "HISTO";
          aUsuarioD = "";
          aUsuarioH = "zzzzzzzzzz";
     |FINSI;
     |SI aParam = "USUARIO"; |O aParam = "HISTOUSU";
          aUsuarioD = aAlfa;
          aUsuarioH = aAlfa;
     |FINSI;

     |ABRE #laborn04;
     |SI aParam = "ADMIN"; |O aParam = "USUARIO";
          aTipo = "P";   || los pendientes de actualizar
          |HAZ LeeFichaActual;
          |HAZ Opciones;
     |FINSI;
     |SI aParam = "HISTO"; |O aParam = "HISTOUSU";
          aTipo = "A";   || consulta del historico, los ya actualizados
          |HAZ LeeFichaActual;
          |HAZ Opciones;
     |FINSI;
     |CIERRA #laborn04;
|FPRO;

|PROCESO Tipo80n04;  |TIPO 80;
     |SI PARAMETRO = "";  |ACABA;  |FINSI;

     aSiEsX = PARAMETRO % 101;
     |SI aSiEsX = "X";
         aAlfa     = PARAMETRO % 202;   enFSalida = aAlfa;
         eaEstado  = PARAMETRO % 401;
         eaUsuario = PARAMETRO % 510;
         aAlfa     = PARAMETRO % 1510;  efFecha = aAlfa;
         eaHora    = PARAMETRO % 2508;
         eaParam   = PARAMETRO % 3310;  |QBF eaParam;
         aParam    = eaParam;
     |SINO;
          aParam = PARAMETRO;  |QBF aParam;
          |SI eaParam ! "";
              eaParam = aParam;
          |FINSI;
     |FINSI;
|FPRC;
