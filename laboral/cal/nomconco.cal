|FICHEROS;
     nomconco #0;
     nomcopat;
     copiadef@ #500;
|FIN;

|VARIABLES;
     nConstante = 0;

     fFechaOri = @;
     fFechaDes = @;

     nCampo = 0;

     aAlfa = "";
     aPath = "";
     aDef = "";
     aFic = "";
|FIN;

|PROCESO nomcopat;
     nConstante = #nomcopat#0;
     |SALVA_FICHA #nomcopat;

     #nomcopat#0 = nConstante;
     #nomcopat#79 = fFechaDes;
     |LEE 101#nomcopat.=;
     |SI FSalida ! 0;
          || primero si no existe la creo con los valores del a¤o pasado

          #nomcopat#0 = nConstante;
          #nomcopat#79 = fFechaOri;
          |LEE 000#nomcopat.=;

          #nomcopat#79 = fFechaDes;
          |GRABA 020#nomcopat.n;
          |LIBERA #nomcopat;
     |FINSI;

     || vuelvo a leerla
     #nomcopat#0 = nConstante;
     #nomcopat#79 = fFechaDes;
     |LEE 101#nomcopat.=;

     || ahora leo la constante origen para el nuevo a¤o, que ya tiene que
     || estar creada, lo hago con un CARGA_DEF

     #500#0 = #0#2;
     #500#79 = fFechaDes;
     |LEE 000#500=;
     |SI FSalida = 0;    || (tiene que serlo)
          #nomcopat#89 = #500#89;
          |PARA nCampo = 20; |SI nCampo [ 73; |HACIENDO nCampo = nCampo + 1;
               #nomcopat#nCampo# = #500#nCampo#;
          |SIGUE;

          |PARA nCampo = 82; |SI nCampo [ 87; |HACIENDO nCampo = nCampo + 1;
               #nomcopat#nCampo# = #500#nCampo#;
          |SIGUE;

          |PARA nCampo = 89; |SI nCampo [ 91; |HACIENDO nCampo = nCampo + 1;
               #nomcopat#nCampo# = #500#nCampo#;
          |SIGUE;

          |SI #500#0 ] 700; |Y #500#0 [ 799;      || CC agrarios
               #nomcopat#6 = #500#6;
          |FINSI;

          |GRABA 020#nomcopat.a;

     |FINSI;
|FPRC;

|DEFBUCLE nomcopat;
     #nomcopat#1;
     ;
     #0#0, fFechaOri;
     #0#1, fFechaOri;
     be;
     NULL, nomcopat;
|FIN;

|PROGRAMA;
     |CLS;

     |ABRE #0;
     |LEE 001#0p;
     |SI FSalida ! 0;
          |DDEFECTO #0;
          |GRABA 020#0n;
     |FINSI;
     |LEE 101#0=;

     |PINPA #0#nomconco;
     |PINDA #0#nomconco;

     |ENDATOS #1#0;

     |SI FSalida = 0;
          |ABRE #nomcopat;

          |DBASE aPath;
          aDef = aPath + "/def/nomcopat";
          aFic = aPath + "/fich/";
          |CARGA_DEF aDef, copiadef@;

          |ABRE #500;

          fFechaOri = #0#3;
          fFechaDes = #0#4;

          |BUCLE nomcopat;

          |CIERRA #500;
          |CIERRA #nomcopat;

          |DESCARGA_DEF #copiadef@;
     |FINSI;

     |GRABA 020#0a;
     |LIBERA #0;
     |CIERRA #0;
|FPRO;
