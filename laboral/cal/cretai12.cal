|FICHEROS;
     cretai12 #0;

     cretam3d;
     cretat3d;

     labempre;
     labcentr;
     nomcenes;
|FIN;

|VARIABLES;
     aDesdeEstado = "";
     aHastaEstado = "";

     aAntCCC = "";
     aAlfa = "";
     nCampo = 0;

     aIBANDS = "";
     aSILTRA = "";
     &swCoinciden = 0;
     aCCC = "";

     nDesdeEmpresa = 0;
     nHastaEmpresa = 0;
     swImprime = 0;
     aUltImpreso = "";
|FIN;

|PROCESO ElPrint;
     |SI swImprime = 0;
          Impresora = "CrystalReport";
          |IMPRE -1;
          |INFOR "cretai12";
     |FINSI;
     |PRINT;
     swImprime = 1;
|FPRC;

|PROCESO BuscaCCCLabempre;
     aIBANDS = #labempre#135;
     |QBF aIBANDS;
|FPRC;

|DEFBUCLE BuscaCCCLabempre;
     #labempre#1;
     13;
     00001, aCCC;
     99999, aCCC;
     ;
     NULL, BuscaCCCLabempre;
|FIN;

|PROCESO BuscaCCCLabcentr;
     aIBANDS = #labcentr#28;
     |QBF aIBANDS;
     |SI aIBANDS = "";
          #labempre#0 = #labcentr#0;
          |LEE 000#labempre.=;
          |SI FSalida = 0;
               aIBANDS = #labempre#135;
          |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE BuscaCCCLabcentr;
     #labcentr#1
     10;
     00001, 001, aCCC;
     99999, 999, aCCC;
     ;
     NULL, BuscaCCCLabcentr;
|FIN;

|PROCESO BuscaCCCNomcenes;
     #labcentr#0 = #nomcenes#0;
     #labcentr#1 = #nomcenes#13;
     |LEE 000#labcentr.=;
     |SI FSalida = 0;
          aIBANDS = #labcentr#28;
          |QBF aIBANDS;
          |SI aIBANDS = "";
               #labempre#0 = #nomcenes#0;
               |LEE 000#labempre.=;
               |SI FSalida = 0;
                    aIBANDS = #labempre#135;
               |FINSI;
          |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE BuscaCCCNomcenes;
     #nomcenes#1
     10;
     00001, 001, "87", aCCC;
     99999, 999, "87", aCCC;
     ;
     NULL, BuscaCCCNomcenes;
|FIN;

|PROCESO BuscaPorCCC;
     aCCC = aCCC % -111;
     aCCC = aCCC + "    ";
     |BUCLE BuscaCCCLabcentr;
     |SI aIBANDS = "";
          |BUCLE BuscaCCCNomcenes;
     |FINSI;
     |SI aIBANDS = "";
          |BUCLE BuscaCCCLabempre;
     |FINSI;
|FPRC;

|PROCESO cretam3d;
     |SI aAntCCC ! "";
          |SI aAntCCC ! #cretam3d#4;
          |Y aUltImpreso ! #cretat3d#4;
               aCCC = #cretat3d#4;
               aIBANDS = "";
               |HAZ BuscaPorCCC;
               aSILTRA = #cretat3d#25;
               |QBF aIBANDS;
               |QBF aSILTRA;
               |SI aIBANDS = aSILTRA;
                    swCoinciden = 1;
               |SINO;
                    swCoinciden = 0;
               |FINSI;

               |HAZ ElPrint;
               aUltImpreso = #cretat3d#4;
          |FINSI;
     |FINSI;

     |SI #cretam3d#15 ! "R9790"; |Y #cretam3d#17 ! "R9790";
     |Y #cretam3d#19 ! "R9790"; |Y #cretam3d#21 ! "R9790";
     |Y #cretam3d#23 ! "R9790";
          |PARA nCampo = 0; |SI nCampo [ 28; |HACIENDO nCampo = nCampo + 1;
               #cretat3d#nCampo# = #cretam3d#nCampo#;
          |SIGUE;
     |FINSI;

     aAntCCC = #cretam3d#4;
     |QBF aAntCCC;
|FPRC;

|DEFBUCLE cretam3d;
     #cretam3d#1;
     9;
     nDesdeEmpresa, "               ", "        ", aDesdeEstado;
     nHastaEmpresa, "zzzzzzzzzzzzzzz", "zzzzzzzz", aHastaEstado;
     ;
     NULL, cretam3d;
|FIN;

|PROCESO ElUltimo;
     |SI aAntCCC ! "";
          |SI aAntCCC ! #cretam3d#4;
          |Y aUltImpreso ! #cretat3d#4;
               aCCC = #cretat3d#4;
               aIBANDS = "";
               |HAZ BuscaPorCCC;
               aSILTRA = #cretat3d#25;
               |QBF aIBANDS;
               |QBF aSILTRA;
               |SI aIBANDS = aSILTRA;
                    swCoinciden = 1;
               |SINO;
                    swCoinciden = 0;
               |FINSI;
               |HAZ ElPrint;
          |FINSI;
     |FINSI;
|FPRC;

|PROGRAMA;
     |CLS;

     |ABRE #0;
     |PINPA #0#0;

     |DDEFECTO #0;
     |LEE 000#0p;
     |SI FSalida = 0;
          |BORRA 020#0a;
     |FINSI;

     |PINDA #0#0;

     |ENDATOS #1#0;
     |SI FSalida = 0;
          aAlfa = Usuario; |QBT aAlfa; aAlfa = "tm_cretat3d_" + aAlfa;
          |NOME_DAT #cretat3d#aAlfa#;
          |DELINDEX #cretat3d;
          |ABRE #cretat3d;

          |ABRE #labempre;
          |ABRE #labcentr;
          |ABRE #nomcenes;

          aDesdeEstado = #0#2;
          aHastaEstado = #0#2;
          |SI #0#2 = "T";
               aDesdeEstado = " ";
               aHastaEstado = "z";
          |FINSI;

          nDesdeEmpresa = #0#0;
          nHastaEmpresa = #0#1;

          |BUCLE cretam3d;

          || para que imprima el ultimo

          |HAZ ElUltimo;

          nDesdeEmpresa = 00000;
          nHastaEmpresa = 00000;
          aAntCCC = "";

          |BUCLE cretam3d;

          |SI swImprime = 1;
               |PIEINF;
               |FININF;
               |FINIMP;
          |SINO;
               aAlfa = "    No se han encontrado datos de respuesta de Comunicación de ";
               aAlfa = aAlfa + "Cuentas con la selección realizada.";
               |MENAV aAlfa;
          |FINSI;

          |CIERRA #nomcenes;
          |CIERRA #labcentr;
          |CIERRA #labempre;

          |DELINDEX #cretat3d;
          |CIERRA #cretat3d;
     |FINSI;

     |GRABA 000#0n;
     |CIERRA #0;
|FPRO;
