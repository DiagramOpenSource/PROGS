|FICHEROS;
     labaux01 #0;

     labempre;
     labcentr;
     labtraba;

     labregis;
     labhisto;

     labcontr;

     pdfimtmp #100;
|FIN;

|VARIABLES;
     nAscii = 0;
     aCaracter = "";
     aCaracterSal = "";
     aCadenaSal = "";
     aCadena = "";
     aLongitud = "";
     nLongitud = 0;
     nPos = 0;
     nBusca = 0;
     aValor = "";
     aAlfa1 = "";
     aCampo = "";
     nTipo = 0;
     aEmp = "";
     aTra = "";
     aCon = "";
     &nMes = 0;
     &aMes = "";
     FEstado = 0;

     &swCopia = 0;
     &aArchivo = "";
     aAlfa = "";
     nCampo = 0;

     &aProcedencia = "";
     &aListaFich = "";
     &aListaFichNor = "";
     &aListaFichCop = "";
     &aListaImpares = "";
     &swDondeEstoy = 0;
     &nContador = 0;
|FIN;

|PROCESO Tipo12;|TIPO 12;
|FPRC;

|PROCESO sustituye2HC;
     nAscii = & aCaracter;
     aCaracterSal = aCaracter;
     |SI nAscii = 160;             || el caracter  
          aCaracterSal = "\341";
     |FINSI;
     |SI nAscii = 130;             || el caracter ‚
          aCaracterSal = "\351";
     |FINSI;
     |SI nAscii = 161;             || el caracter ¡
          aCaracterSal = "\355";
     |FINSI;
     |SI nAscii = 162; |O nAscii = 243;            || el caracter ¢
          aCaracterSal = "\363";
     |FINSI;
     |SI nAscii = 163;             || el caracter £
          aCaracterSal = "\372";
     |FINSI;
     |SI nAscii = 164;             || el caracter ¤
          aCaracterSal = "\361";
     |FINSI;
     |SI nAscii = 241;             || el caracter ¤ (otro codigo ¨?)
          aCaracterSal = "\361";
     |FINSI;
     |SI nAscii = 165;             || el caracter ¥
          aCaracterSal = "\321";
     |FINSI;
     |SI nAscii = 129;
          aCaracterSal = "\374";   || el caracter u con dieresis
     |FINSI;
     |SI nAscii = 154;
          aCaracterSal = "\334";   || el caracter U (mayuscula) con dieresis
     |FINSI;
     |SI nAscii = 167;
          aCaracterSal = "\272";   || el caracter §
     |FINSI;
     |SI nAscii = 166;
          aCaracterSal = "\252";   || el caracter ¦
     |FINSI;
     |SI aCaracter = "(";
          aCaracterSal = "\(";
     |FINSI;
     |SI aCaracter = ")";
          aCaracterSal = "\)";
     |FINSI;
     |SI aCaracter = "€";       || la C-edilla o C trencada mayuscula
          aCaracterSal = "Ç";
     |FINSI;
     |SI aCaracter = "‡";       || la c-edilla o c trencada minuscula
          aCaracterSal = "ç";
     |FINSI;
|FPRC;

|PROCESO sustituyeHC;
     aCadenaSal = "";
     aLongitud = aCadena % 0;
     nLongitud = aLongitud;
     |PARA nPos = 1; |SI nPos [ nLongitud; |HACIENDO nPos = nPos + 1;
          nBusca = (nPos * 100) + 1;
          aCaracter = aCadena % nBusca;
          |HAZ sustituye2HC;
          aCadenaSal = aCadenaSal + aCaracterSal;
     |SIGUE;
|FPRC;

|PROCESO GrabaTemporalHC;
     |QBF aValor;
     aCadena = aValor;
     |HAZ sustituyeHC;
     aValor = aCadenaSal;
     aAlfa1 = aValor % 101;
     |SI aAlfa1 = ".";
          aValor = "";
     |FINSI;
     #pdfimtmp#0 = aCampo;
     #pdfimtmp#1 = aValor;
     #pdfimtmp#2 = nTipo;
     |GRABA 020#pdfimtmp.n;
|FPRC;

|PROCESO CargaCamposHC;
     #labtraba#0 = #0#0;
     #labtraba#1 = #0#1;
     |LEE 000#labtraba.=;

     aCampo = "fechaini";      || FECHA INICIO CONTRATO
     aValor = #0#5;
     |HAZ GrabaTemporalHC;

     aCampo = "oereg";         || SERVICIO PUBLICO EMPLEO
     aValor = #0#6;
     |HAZ GrabaTemporalHC;

     aCampo = "fechareg";      || FECHA ALTA SEPE
     aValor = #0#7;
     |HAZ GrabaTemporalHC;

     aCampo = "idcont";        || ID CONTRATO
     aValor = #0#8;
     |HAZ GrabaTemporalHC;

     aCampo = "numhormax";   || NUMERO DE HORAS MAXIMAS
     aValor = #labaux01#2;
     |HAZ GrabaTemporalHC;

     |SI #labaux01#11 ! 0;
          aCampo = "voluntarias";   || NUMERO DE HORAS VOLUNTARIAS
          aValor = #labaux01#11;
          |HAZ GrabaTemporalHC;
     |FINSI;

     aCampo = "porchorpac";  || PORCENTAJE HORAS PACTADAS
     aValor = #labaux01#3;
     |HAZ GrabaTemporalHC;

     aCampo = "numdiasant";  || NUMERO DIAS ANTELACION
     aValor = #labaux01#4;
     |HAZ GrabaTemporalHC;

     aCampo = "munifirma";
     aValor = #0#9;
     |HAZ GrabaTemporalHC;

     aCampo = "diafirma";
     aValor = #0#10 % 102;
     |HAZ GrabaTemporalHC;

     aCampo = "mesfirma";
     aValor = #0#10 % 402;
     nMes = aValor;
     |HAZ MesLetra_plb;
     aValor = aMes;
     |HAZ GrabaTemporalHC;

     aCampo = "a\361ofirma";
     aValor = #0#10 % -102;
     |HAZ GrabaTemporalHC;

     |SI swCopia = 1;
          aCampo = "firma1";
          aValor = "El/la trabajador/a";
          |HAZ GrabaTemporalHC;

          aCampo = "firma2";
          aValor = "El/la representante de la empresa";
          |HAZ GrabaTemporalHC;

          aCampo = "firma3";
          aValor = "El/la representante legal del menor, si procede";
          |HAZ GrabaTemporalHC;

          aCampo = "trabajador";   || TRABAJADOR
          aValor = #labtraba#2;
          |HAZ GrabaTemporalHC;

          aCampo = "repre_emp";   || REPRESENTANTE EMPRESA
          aValor = #labempre#15;
          |HAZ GrabaTemporalHC;

          aCampo = "repre_legal";   || REPRESENTANTE LEGAL
          aValor = #labtraba#20;
          |HAZ GrabaTemporalHC;

          aCampo = "copiabasica";
          aValor = "";
          |HAZ GrabaTemporalHC;
     |FINSI;

     |SI swCopia = 2;
          aCampo = "firma1";
          aValor = "El/la representante sindical";
          |HAZ GrabaTemporalHC;

          aCampo = "firma2";
          aValor = "El/la representante de la empresa";
          |HAZ GrabaTemporalHC;

          |ABRE #labcentr;
          #labcentr#0 = #labtraba#0;
          #labcentr#1 = #labtraba#77;
          |LEE 000#labcentr.=;
          |CIERRA #labcentr;

          aCampo = "trabajador";   || REPRESENTANTE SINDICAL
          aValor = #labcentr#12;
          |HAZ GrabaTemporalHC;

          aCampo = "repre_emp";   || REPRESENTANTE EMPRESA
          aValor = #labempre#15;
          |HAZ GrabaTemporalHC;

          aCampo = "copiabasica";
          aValor = "(COPIA B\301SICA)";
          |HAZ GrabaTemporalHC;
     |FINSI;

     aCampo = "registro";
     aEmp = #labtraba#0; aEmp = "00000" + aEmp; aEmp = aEmp % -105;
     aTra = #labtraba#1; aTra = "00000" + aTra; aTra = aTra % -105;
     aCon = #labregis#0; aCon = "000000" + aCon; aCon = aCon % -105;
     aValor = aEmp + " / " + aTra + " / " + aCon;
     |HAZ GrabaTemporalHC;
|FPRC;

|PROCESO CargaCamposPan;
     |LEE 101#0=;
     FEstado = FSalida;

     |ABRE #labempre;
     |ABRE #labtraba;
     |ABRE #labregis;
     |ABRE #labhisto;

     #labtraba#0 = #0#0;
     #labtraba#1 = #0#1;
     |LEE 000#labtraba.=;

     |SELECT #2#labregis;
     #labregis#1 = #0#0;
     #labregis#2 = #0#1;
     |LEE 000#labregis.=;
     |SI FSalida ! 0;
          |MENAV "    ATENCION: Registro de contrato no encontrado";
     |SINO;
          #labhisto#100 = #labregis#0;
          #labhisto#103 = "  ";
          |LEE 000#labhisto.=;
          |SI FSalida ! 0;
               |MENAV "    ATENCION: Contrato no encontrado";
          |SINO;
               aAlfa = #0#5; |QBF aAlfa;
               |SI aAlfa = ""; |O aAlfa = "..........";
                    #0#5 = #labhisto#47;     || FECHA INICIO CONTRATO
                    |PINTA #0#5;
               |FINSI;

               aAlfa = #0#6; |QBF aAlfa;
               |SI aAlfa = ""; |O aAlfa = "..........";
                   #0#6 = #labhisto#45;   || SERVICIO PUBLICO EMPLEO
                   |PINTA #0#6;
               |FINSI;

               aAlfa = #0#9; |QBF aAlfa;
               |SI aAlfa = ""; |O aAlfa = "..........";
                   #0#9 = #labhisto#46;     || localidad firma
                   |PINTA #0#9;
               |FINSI;

               aAlfa = #0#10; |QBF aAlfa;
               |SI aAlfa = ""; |O aAlfa = "..........";
                   #0#10 = #labhisto#47;     || fecha firma
                   |PINTA #0#10;
               |FINSI;
          |FINSI;

          aAlfa = #0#7; |QBF aAlfa;
          |SI aAlfa = ""; |O aAlfa = "..........";
               #0#7 = #labregis#21;      || FECHA ALTA SEPE
               |PINTA #0#7;
          |FINSI;

          aAlfa = #0#8; |QBF aAlfa;
          |SI aAlfa = ""; |O aAlfa = "..........";
               #0#8 = #labregis#14;      || ID CONTRATO
               |PINTA #0#8;
          |FINSI;
     |FINSI;

     |SI FEstado = 0;
          |GRABA 020#0a;
     |FINSI;
     |LIBERA #0;

     |CIERRA #labempre;
     |CIERRA #labtraba;
     |CIERRA #labregis;
     |CIERRA #labhisto;
|FPRC;

|PROCESO ImpresionHC;
     |ABRE #labempre;
     |ABRE #labcentr;
     |ABRE #labtraba;
     |ABRE #labregis;
     |ABRE #labhisto;

     |ABRE #labcontr; |LEE 000#labcontr.p; |CIERRA #labcontr;

     |PARA swCopia = 1; |SI swCopia [ 2; |HACIENDO swCopia = swCopia + 1;
          |SI swCopia = 2; |Y #labcontr#37 = 0;
               |SAL;
          |FINSI;

          aAlfa1 = Usuario; |QBT aAlfa1;    aAlfa1 = ("c" + aAlfa1) % 108;
          |NOME_DAT #100 aAlfa1;
          |DELINDEX #pdfimtmp;
          |ABRE #pdfimtmp;

          aArchivo = "pe-200.pdf";
          |HAZ CargaCamposHC;

          |SUB_EJECUTA "pdfgraba";
     |SIGUE;

     |SUB_EJECUTA "pdfimpre";

     |CIERRA #labhisto;
     |CIERRA #labregis;
     |CIERRA #pdfimtmp;
     |CIERRA #labtraba;
     |CIERRA #labcentr;
     |CIERRA #labempre;
|FPRC;

|PROCESO Pantalla; |TIPO 7;
     |SI aProcedencia ! "";
          |PARA nCampo = 5; |SI nCampo [ 10; |HACIENDO nCampo = nCampo + 1;
               |CAMPO_MODIFICA #0nCampo, 1, "N";
               |CAMPO_VISUAL #0nCampo, 1, "N";
          |SIGUE;
          |BLANCO 14012080;
     |SINO;
          |HAZ CargaCamposPan;
     |FINSI;
|FPRC;

|PROCESO Boton; |TIPO 20;
     aAlfa = FSalida;
     |SI aAlfa = "OPCION";
          |HAZ CargaCamposPan;
          FSalida = "Imprimir";
          |ACABA;
     |FINSI;

     |LEE 101#0=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |HAZ ImpresionHC;
|FPRC;
