|FICHEROS;
     labempre;
     labcentr;
     nomcenes;
     labtraba;
     labcontr;

     labparta;
     labta611;
     labnct15;
     labnct16;
     labmunic;

     silcon06;
     silcon15;

     nomtmp06,MANTE;
     labnct19,MANTE;
     labnct10;

     :integral/dsam0103;
     nomconca;
     labemett;
|FIN;

|VARIABLES;
     aAlfa = "";
     aAlfa1 = "";
     aAlfa2 = "";
     aAlfa3 = "";
     nNume = 0;
     &contador = 0;
     tipodc = "";

     nNivel = 0;
     aCCC = "";
     swConsultaEmp = 0;
     nOpcionSS = 0;
     &eaCodNif = "";
     aCodPos = "";
     nContMuni = 0;
     aCodMuni = "";
     &aCadena = "";
     &eaNom = "";
     &eaApel1 = "";
     &eaApel2 = "";
     nNumTra = 0;
     aRuta = "";
     &eaCadena = "";
     &eaCadenaSal = "";
     &eaCaracter1 = "";
     &eaCaracter2 = "";
     aFichero = "";
     fFecha = @;
     aFecha = "";
     aHora = "";
     aRutaBAT = "";
     nHandle = 0;
     aFich = "";
     aRutaSal = "";
     aLon = "";
     nLon = 0;
     nPunt = 0;
     nPos = 0;
     aCaracter = "";
     aMuni = "";
     nMensajeF5 = 0;
     aCodigoMunicipio = "";
     swSigue = 0;
     aOrigen = "";
     aDestino = "";
     {1}aContiene = "";
     aRutaErrores = "";
     nHandleBAT = 0;
     aUnidad = "";
     aResto = "";
     nSwEtiq = 0;
     aEtiqu = "";
     aError = "";
     nNumChars = 0;
     nNume1 = 0;
     nNume2 = 0;
     &aParam = "";
     aUsuario = "";

     fFechaEnvio = @;
     aHoraEnvio = "";
     nEmp = 0;
     nTra = 0;
     aRespuesta = "";
     nHandle1 = 0;
     aLinea = "";
     aEtiqueta = "";
     aBusca = "";
     aNif = "";
     aUno = "";
     aEspacios = "";
     aRegimen = "";
     swRespuesta = 0;
|FIN;

/****************************************************************************/
/********************************* INFORME ***********************************/
/****************************************************************************/

|PROCESO labnct16;
     |PRINT;
|FPRC;

|DEFBUCLE labnct16;
     #labnct16#1;
     ;
     00001, 00001, fFechaEnvio, aHoraEnvio, 0001;
     99999, 99999, fFechaEnvio, aHoraEnvio, 9999;
     ;
     NULL, labnct16;
|FIN;

|PROCESO Informe;
     Impresora = "CrystalReport";
     |IMPRE -1;
     |INFOR "labnct15";

     |BUCLE labnct16;

     |PIEINF;
     |FININF;
     |FINIMP;
|FPRC;


/****************************************************************************/
/************************* REGISTRO DE ENVIOS *******************************/
/****************************************************************************/

|PROCESO RegistroMas;
     |ABRE #labnct10;

     #labnct10#0 = #labtraba#0;
     #labnct10#1 = #labtraba#1;
     #labnct10#11 = #labtraba#77;

     |SI nOpcionSS = 1;
          #labnct10#2 = 001;
          #labnct10#3 = "Alta";
     |FINSI;
     |SI nOpcionSS = 2;                 || para la baja
          #labnct10#2 = 002;
          #labnct10#3 = "Baja";
     |FINSI;

     fFecha = ~;
     aFecha = fFecha;
     #labnct10#4 = fFecha;         || fecha

     |HORA aHora;
     |QBF aHora;
     #labnct10#5 = aHora;          || hora

     aUsuario = Usuario % 810;
     |QBF aUsuario;
     #labnct10#6 = aUsuario;       || usuario
     #labnct10#7 = "labnct15";

     #labnct10#8 = #labtraba#45;             || tipo de contrato
     #labnct10#9 = #labtraba#36;             || fecha de alta
     #labnct10#10 = #labtraba#37;            || fecha de baja

     |GRABA 020#labnct10.n;
     |LIBERA #labnct10;
     |CIERRA #labnct10;
|FPRC;

/****************************************************************************/
/*************************** ENVIO POR CONECTASS ****************************/
/****************************************************************************/

|PROCESO Envio;
     swSigue = 0;

     aContiene = "";
     |ESPECIFICA "RBASS", aContiene;
     aAlfa = aContiene1;
     |QBF aAlfa;
     aAlfa = aAlfa + "conectass";
     #labcontr#63 = aAlfa;

     aRuta = #labcontr#51;
     |QBF aRuta;
     aAlfa = aRuta % -101;
     |SI aAlfa ! "\"; |Y aAlfa ! "/";        || ruta de los XML
          aRuta = aRuta + "/";
     |FINSI;

     || ruta para errores
     aAlfa1 = #labcontr#63;
     |QBF aAlfa1;
     aAlfa = aAlfa1 % -101;
     |SI aAlfa ! "\"; |Y aAlfa ! "/";        || ruta de los XML
          aAlfa1 = aAlfa1 + "\";
     |FINSI;

     aAlfa = aAlfa1 + "ERRORES";
     aRutaErrores = aAlfa;

     aAlfa = aRutaErrores + "\ERRORESNET.XML";
     |RFBORRA aAlfa;

     |DBASE aOrigen; |QBF aOrigen;
     aOrigen = aOrigen + "ejecutables/rdn.exe";
     aDestino = #labcontr#63; |QBF aDestino; aDestino = aDestino + "rdn.exe";

     || bien, esto es por el tema del VDesk, que en una maquina virtual pare-
     || cia que no funcionaba bien el ejecutar el rdn en el RSYSTEM directamente
     || asi que lo que hice fue hacer un bat y ejecutar el BAT

     |XABRE "CBW", aRutaBAT, nHandleBAT;

     aAlfa = #labcontr#63; |QBF aAlfa;
     aRuta = aAlfa; |HAZ CompRuta; aAlfa = aRuta;
     aAlfa2 = aAlfa + "ERRORES\";
     aRuta = aAlfa2; |HAZ CompRuta; aAlfa2 = aRuta;

     aUnidad = aAlfa % 102;
     aResto = aAlfa - aUnidad;

     aAlfa = "@echo off" + &13 + &10;
     |XGRABA nHandleBAT, aAlfa;

     aUnidad = aUnidad + &13 + &10;
     |XGRABA nHandleBAT, aUnidad;

     aAlfa = "CD " + aResto + &13 + &10;
     |XGRABA nHandleBAT, aAlfa;

     aAlfa1 = #labcontr#62; |QBF aAlfa1;
     aAlfa = "rdn.exe " + aAlfa1 + " " + aFich + " " + aAlfa2;

     aAlfa = "start " + aAlfa + &13 + &10;
     |XGRABA nHandleBAT, aAlfa;

     |XCIERRA nHandleBAT;

     |RSYSTEM aRutaBAT;

     |PULSATECLA;
     |PULSATECLA;
     |PULSATECLA;
     |PULSATECLA;
     |PULSATECLA;
|FPRC;

|PROCESO QuitaBlancos;
     |SI aAlfa = "";
          |ACABA;
     |FINSI;
     aEspacios = "";
     |PARA nPos = 1; |SI nPos [ 99; |HACIENDO nPos = nPos + 1;
          nNume = (nPos * 100) + 1;
          aUno = aAlfa % nNume;
          |SI aUno = " ";
               aEspacios = aEspacios + " ";
          |SINO;
               |SAL;
          |FINSI;
     |SIGUE;
     aAlfa = aAlfa - aEspacios;
|FPRC;

|PROCESO GrabaRegistro;
     |ABRE #labempre;
     #labempre#0 = nEmp;
     |LEE 000#labempre.=;
     |CIERRA #labempre;

     |ABRE #labtraba;
     #labtraba#0 = nEmp;
     #labtraba#1 = nTra;
     |LEE 000#labtraba.=;
     |CIERRA #labtraba;

     |ABRE #labnct16;
     #labnct16#0 = nEmp;
     #labnct16#1 = nTra;
     #labnct16#2 = fFechaEnvio;
     #labnct16#3 = aHoraEnvio;
     |SI nOpcionSS = 1;
          #labnct16#4 = "A";
     |FINSI;
     |SI nOpcionSS = 2;
          #labnct16#4 = "B";
     |FINSI;
     #labnct16#5 = #labtraba#2;
     #labnct16#6 = #labempre#2;
     #labnct16#7 = #labtraba#7;
     #labnct16#8 = aRespuesta;
     aAlfa = aError % 104;
     #labnct16#9 = aAlfa;
     aAlfa = aAlfa + " ";
     aAlfa = aError - aAlfa;
     #labnct16#10 = aAlfa;

     |GRABA 020#labnct16.n;
     |LIBERA #labnct16;
     |CIERRA #labnct16;

|FPRC;

|PROCESO BuscaTraba;
     nEmp = #labnct16#0;
     nTra = #labnct16#1;
|FPRC;

|DEFBUCLE BuscaTraba;
     #labnct16#1;
     7;
     00001, 00001, fFechaEnvio, aHoraEnvio, 00000, aNif;
     99999, 99999, fFechaEnvio, aHoraEnvio, 00000, aNif;
     ;
     NULL, BuscaTraba;
|FIN;

|PROCESO Respuesta;
     || examinamos la respuesta que nos da

     aFecha = fFechaEnvio;
     aFecha = (aFecha % -104) + (aFecha % 402) + (aFecha % 102);

     aContiene = "";
     |ESPECIFICA "RBASS", aContiene;
     aAlfa = aContiene1;
     |QBF aAlfa;
     aAlfa = aAlfa + "conectass";
     aRuta = aAlfa + "/ERRORES/abss/" + aFecha + "/respuesta.xml";

     |XABRE "C", aRuta, nHandle1; nHandle1 = FSalida;
     |SI FSalida ] 0;
          swRespuesta = 1;
          |PARA; |SI FSalida ] 0; |HACIENDO;
               |XLEE nHandle1, 250, aLinea;

               aAlfa = aLinea - "</RESPUESTA_SSOCIAL>";
               |SI FSalida ! 0;
                    |SAL;
               |FINSI;

               aLinea = aLinea - "<trabajador>";
               |SI FSalida ! 0;
                    |PARA; |SI FSalida ] 0; |HACIENDO;
                         |XLEE nHandle1, 250, aLinea;

                         aEtiqueta = "dnitraba";
                         aBusca = "<" + aEtiqueta + ">";
                         aLinea = aLinea - aBusca;
                         |SI FSalida ! 0;
                              aBusca = "</" + aEtiqueta + ">";
                              aLinea = aLinea - aBusca;
                              aNif = aLinea;
                              |QBF aNif;
                              aAlfa = aNif; |HAZ QuitaBlancos; aNif = aAlfa;

                              nEmp = 00000;
                              nTra = 00000;
                              |BUCLE BuscaTraba;

                              |SI nEmp ! 00000; |Y nTra ! 00000;
                                   || encontrado
                              |FINSI;
                         |FINSI;

                         aEtiqueta = "respuesta";
                         aBusca = "<" + aEtiqueta + ">";
                         aLinea = aLinea - aBusca;
                         |SI FSalida ! 0;
                              aBusca = "</" + aEtiqueta + ">";
                              aLinea = aLinea - aBusca;
                              aRespuesta = aLinea;
                              |QBF aRespuesta;
                              aAlfa = aRespuesta; |HAZ QuitaBlancos; aRespuesta = aAlfa;
                         |FINSI;

                         aEtiqueta = "errores";
                         aBusca = "<" + aEtiqueta + ">";
                         aLinea = aLinea - aBusca;
                         |SI FSalida ! 0;
                              aEtiqueta = "*";
                              aBusca = aEtiqueta;
                              aLinea = aLinea - aBusca;
                              |SI FSalida ! 0;         || linea a guardar
                                   aEtiqueta = "errores";
                                   aBusca = "</" + aEtiqueta + ">";
                                   aLinea = aLinea - aBusca;
                                   |SI FSalida ! 0;
                                        || la linea contenia error y fin de
                                        || campo
                                        |QBF aLinea;
                                        aError = aLinea
                                        aAlfa = aError; |HAZ QuitaBlancos; aError = aAlfa;
                                        |HAZ GrabaRegistro;
                                        |SAL;
                                   |SINO;
                                        || la linea solo contiene error
                                        aError = aLinea
                                        aAlfa = aError; |HAZ QuitaBlancos; aError = aAlfa;
                                        |HAZ GrabaRegistro;
                                   |FINSI;
                              |FINSI;
                         |FINSI;

                         aEtiqueta = "errores";
                         aBusca = "</" + aEtiqueta + ">";
                         aLinea = aLinea - aBusca;
                         |SI FSalida ! 0;
                              || fin errores, no pasa nada
                         |FINSI;

                         aLinea = aLinea - "</trabajador>";
                         |SI FSalida ! 0;
                              |SAL;
                         |FINSI;
                    |SIGUE;
               |FINSI;
          |SIGUE;
     |SINO;
          aAlfa = "    Respuesta no encontrada: " + aRuta;
          |QBF aAlfa;
          |MENAV aAlfa;
          swRespuesta = 0;
     |FINSI;
     |XCIERRA nHandle1;
|FPRC;


/****************************************************************************/
/*********************** BAJAS MASIVAS CONECTASS ****************************/
/****************************************************************************/

|PROCESO Linea;
     |QBF aAlfa;
     aCadena = aAlfa;
     |HAZ QuitaRaros;
     |SI nNivel = 1;
          aCadena = aCadena;
     |FINSI;
     |SI nNivel = 2;
          aCadena = "  " + aCadena;
     |FINSI;
     |SI nNivel = 3;
          aCadena = "  " + "  " + aCadena;
     |FINSI;
     |SI nNivel = 4;
          aCadena = "  " + "  " + "  " + aCadena;
     |FINSI;
     |SI nNivel = 5;
          aCadena = "  " + "  " + "  " + "  " + aCadena;
     |FINSI;
     |SI nNivel = 6;
          aCadena = "  " + "  " + "  " + "  " + "  " + aCadena;
     |FINSI;
     aCadena = aCadena + &13 + &10;
     aAlfa = aCadena;
     |XGRABA nHandle, aAlfa;
|FPRC;

|PROCESO CompRuta;
     aRutaSal = "";
     |QBF aRuta;
     aLon = aRuta % 0;
     nLon = aLon;
     |PARA nPunt = 1; |SI nPunt [ nLon; |HACIENDO nPunt = nPunt + 1;
          nPos = (nPunt * 100) + 1;
          aCaracter = aRuta % nPos;
          |SI aCaracter = "/";
               aRutaSal = aRutaSal + "\";
          |SINO;
               aRutaSal = aRutaSal + aCaracter;
          |FINSI;
     |SIGUE;
     aCaracter = aRutaSal % -101;
     |SI aCaracter ! "\";
          aRutaSal = aRutaSal + "\";
     |FINSI;
     aRuta = aRutaSal;

     eaCadena = aRuta;
     eaCaracter1 = "/";
     eaCaracter2 = "\";
     |HAZ sustituye_plb;
     aRuta = eaCadenaSal;
|FPRC;

|PROCESO TipoDocumentoDoc;
     aAlfa = #labtraba#7; |QBF aAlfa;
     aAlfa1 = aAlfa % 0101;
     |SI aAlfa1 ] "0"; |Y aAlfa1 [ "9";
          aAlfa = "D";
     |SINO;
          |SI aAlfa1 = "X"; |O aAlfa1 = "Y"; |O aAlfa1 = "Z";
               aAlfa = "E";
          |SINO;
               aAlfa1 = #labtraba#76; |QBF aAlfa1;
               aAlfa2 = aAlfa1 % 0599; aAlfa1 = aAlfa1 % 0103;
               |SI aAlfa1 = "ESP"; |O aAlfa1 = "724"; |O aAlfa1 = "250";
                |O aAlfa1 = "276"; |O aAlfa1 = "056"; |O aAlfa1 = "442";
                |O aAlfa1 = "380"; |O aAlfa1 = "208"; |O aAlfa1 = "372";
                |O aAlfa1 = "826"; |O aAlfa1 = "300"; |O aAlfa1 = "620";
                |O aAlfa1 = "040"; |O aAlfa1 = "246"; |O aAlfa1 = "752";
                |O aAlfa1 = "233"; |O aAlfa1 = "428"; |O aAlfa1 = "440";
                |O aAlfa1 = "616"; |O aAlfa1 = "203"; |O aAlfa1 = "703";
                |O aAlfa1 = "705"; |O aAlfa1 = "348"; |O aAlfa1 = "470";
                |O aAlfa1 = "196"; |O aAlfa2 = "HOLANDA";
                    aAlfa = "U";
               |SINO;
                    aAlfa = "W";
               |FINSI;
          |FINSI;
     |FINSI;
     tipodc = aAlfa;
|FPRC;

|PROCESO CuentaMunicipio;
     nContMuni = nContMuni + 1;
     aCodMuni = #labmunic#1;
|FPRC;

|DEFBUCLE CuentaMunicipio;
     #labmunic#1;
     ;
     aCodPos, "00000";
     aCodPos, "99999";
     ;
     NULL,CuentaMunicipio;
|FIN;

|PROCESO BuscaMunicipio;
     aAlfa2 = #labmunic#3;            || aAlfa2 = municipio de labmunic
     |QBT aAlfa2;
     aMuni = #labempre#6;   || aMuniEmp = municipio de labempre
     aMuni = aMuni > "";
     |QBT aMuni;

     aAlfa = aMuni - aAlfa2;
     |SI aAlfa ! aMuni;
          ||aAlfa2 esta en aMuni
          aCodMuni = #labmunic#1;
          nContMuni = nContMuni + 1;
     |FINSI;
|FPRC;

|DEFBUCLE BuscaMunicipio;
     #labmunic#1;
     ;
     aCodPos, "00000";
     aCodPos, "99999";
     ;
     NULL,BuscaMunicipio;
|FIN;

|PROCESO min_labmunic;
     #labmunic#4 = aCodPos;
     #labmunic#1 = "00000";
|FPRC;

|PROCESO max_labmunic;
     #labmunic#4 = aCodPos;
     #labmunic#1 = "99999";
|FPRC;

|PROCESO CodigoMuni;
     nContMuni = 0;
     |BUCLE CuentaMunicipio;

     |SI nContMuni > 1;
          nContMuni = 0;
          |BUCLE BuscaMunicipio;
          |SI nContMuni > 1;
               nMensajeF5 = 1;
               aCodigoMunicipio = "";
               |SUB_EJECUTA "labmunic";
               |PULSATECLA;
               |PULSATECLA;
               |PULSATECLA;
               |PULSATECLA;
               |PULSATECLA;
               |SI aCodigoMunicipio ! "";
                    aCodMuni = aCodigoMunicipio;
                    nContMuni = 1;
               |FINSI;
          |FINSI;
     |FINSI;

     |SI nContMuni = 0;
          nMensajeF5 = 1;
          aCodigoMunicipio = "";
          |SUB_EJECUTA "labmunic";
          |PULSATECLA;
          |PULSATECLA;
          |PULSATECLA;
          |PULSATECLA;
          |PULSATECLA;
          |SI aCodigoMunicipio ! "";
               aCodMuni = aCodigoMunicipio;
               nContMuni = 1;
          |FINSI;
     |FINSI;
     ||ahora se supone que en aCodMuni tenemos el codigo del municipio
|FPRC;


|PROCESO DatosAlta;
     nNivel = 3;
     aAlfa = "<DATOS_ALTA_AFILIACION>";
     |HAZ Linea;

     aCCC = #labempre#13;

     |ABRE #labcentr;
     |ABRE #nomcenes;
     |SI swConsultaEmp = 0;
          |SI #labtraba#45 ! "421";
          |Y #labtraba#45 ! "085"; |Y #labtraba#45 ! "097";
               #labcentr#0 = #labtraba#0;
               #labcentr#1 = #labtraba#77;
               |LEE 000#labcentr.=;
               |SI FSalida = 0;
                    aCCC = #labcentr#10;
               |SINO;
                    aAlfa1 = #labtraba#0; |QBF aAlfa1; aAlfa1 = ("00000" + aAlfa1) % -105;
                    aAlfa2 = #labtraba#1; |QBF aAlfa2; aAlfa2 = ("00000" + aAlfa2) % -105;
                    aAlfa3 = #labtraba#77; |QBF aAlfa3; aAlfa3 = ("000" + aAlfa3) % -103;
                    aAlfa = "    ATENCION: No se encuentra el centro " + aAlfa3;
                    aAlfa = aAlfa + " del trabajador " + aAlfa1 + "." + aAlfa2;
                    |MENAV aAlfa;
               |FINSI;
          |SINO;
               #nomcenes#0 = #labtraba#0;
               #nomcenes#13 = #labtraba#77;
               #nomcenes#1 = "87";
               |LEE 000#nomcenes.=;
               |SI FSalida = 0;
                    aCCC = #nomcenes#10;
               |SINO;
                    |MENAV "    ATENCION: No se encuentra el centro de formación";
               |FINSI;
          |FINSI;
     |SINO;
          |SI #labtraba#45 ! "421";
          |Y #labtraba#45 ! "085"; |Y #labtraba#45 ! "097";
               #labcentr#0 = #labtraba#0;
               #labcentr#1 = #labtraba#77;
               |LEE 000#labcentr.=;
               |SI FSalida = 0;
                    aCCC = #labcentr#10;
               |SINO;
                    |MENAV "    ATENCION: No se encuentra el centro del trabajador";
               |FINSI;
          |SINO;
               #nomcenes#0 = #labtraba#0;
               #nomcenes#13 = #labtraba#77;
               #nomcenes#1 = "87";
               |LEE 000#nomcenes.=;
               |SI FSalida = 0;
                    aCCC = #nomcenes#10;
               |SINO;
                    |MENAV "    ATENCION: No se encuentra el centro de formación";
               |FINSI;
          |FINSI;
     |FINSI;
     |CIERRA #nomcenes;
     |CIERRA #labcentr;

     nNivel = 4;

     aAlfa1 = "";
     |ABRE #silcon06;
     |ABRE #silcon15;

     #silcon15#0 = #labempre#0;
     #silcon15#1 = #labcentr#1;
     |LEE 000#silcon15.=;
     |SI FSalida = 0;
          aAlfa1 = #silcon15#2;
          |QBF aAlfa1;
     |SINO;
          #silcon06#0 = #labempre#0;
          |LEE 000#silcon06.=;
          |SI FSalida = 0;
               aAlfa1 = #silcon06#2;
               |QBF aAlfa1;
          |FINSI;
     |FINSI;

     |SI aAlfa1 = "";
          |MENAV "    ATENCION: no se ha encontrado el régimen de la empresa";
     |FINSI;

     |CIERRA #silcon15;
     |CIERRA #silcon06;

     aAlfa1 = ("0000" + aAlfa1) % -104;
     |SI aAlfa1 = "0613"; aAlfa1 = "0163"; |FINSI;
     aAlfa2 = aCCC; |QBF aAlfa2;
     aAlfa = "<CCC>" + aAlfa1 + aAlfa2 + "</CCC>";
     |HAZ Linea;

     aAlfa = #labtraba#45;
     aAlfa = "<CONTRATO>" + aAlfa + "</CONTRATO>";
     |HAZ Linea;

     |MODULO aAlfa;
     |QBF aAlfa;
     |SI aAlfa = "labta611";
          |SI #labta611#2 = "A"; aAlfa1 = #nomtmp06#6; |FINSI;
          |SI #labta611#2 = "B"; aAlfa1 = #labtraba#36; |FINSI;
     |FINSI;
     |SI aAlfa = "labparta";
          |SI #labparta#2 = "A"; aAlfa1 = #nomtmp06#6; |FINSI;
          |SI #labparta#2 = "B"; aAlfa1 = #labtraba#36; |FINSI;
     |FINSI;
     |SI aAlfa = "labnct15";
          aAlfa1 = #nomtmp06#6;
     |FINSI;

     |QBF aAlfa1;

     |SI aAlfa1 ! ""; |Y aAlfa1 ! "          "; |Y aAlfa1 ! "..........";
          aAlfa = (aAlfa1 % 102) + "/" + (aAlfa1 % 402) + "/" + (aAlfa1 % -104);
     |FINSI;
     aAlfa = "<FECHA_ALTA>" + aAlfa + "</FECHA_ALTA>";
     |HAZ Linea;

     aAlfa = #labtraba#33;
     |QBF aAlfa;
     aAlfa = ("00" + aAlfa) % -102;
     aAlfa = "<GRUPO_TARIFA>" + aAlfa + "</GRUPO_TARIFA>";
     |HAZ Linea;

     nNume1 = #labtraba#234;
     nNume2 = 40;
     |ABRE #nomconca;
     #nomconca#0 = #labtraba#87;
     |LEE 000#nomconca.=;
     |SI FSalida = 0;
          |SI #nomconca#162 ! 0;
               nNume2 = #nomconca#162;
          |FINSI;
     |FINSI;
     |CIERRA #nomconca;

     aAlfa = "N";
     |SI #labtraba#42 = "M"; |O #labtraba#42 = "X";
          |SI #labtraba#33 ] 08;
               aAlfa = "S";
          |FINSI;
     |FINSI;
     aAlfa = "<ICOTIZMENSUAL>" + aAlfa + "</ICOTIZMENSUAL>";
     |HAZ Linea;

     |SI #labtraba#42 = "A";
          aAlfa = "1";
          aAlfa = "<MODCOTI>" + aAlfa + "</MODCOTI>";
          |HAZ Linea;
     |FINSI;
     |SI #labtraba#42 = "E";
          aAlfa = "2";
          aAlfa = "<MODCOTI>" + aAlfa + "</MODCOTI>";
          |HAZ Linea;
     |FINSI;

     nNume = (((nNume1 / nNume2) * 1000) ! 0);
     aAlfa = nNume;
     |QBF aAlfa;
     |SI nNume = 0;
          aAlfa = "";
     |FINSI;

     aAlfa = "<COEFICIENTE>" + aAlfa + "</COEFICIENTE>";
     |HAZ Linea;

     aAlfa = #labtraba#204;
     |QBF aAlfa;
     aAlfa = "<OCUPACION>" + aAlfa + "</OCUPACION>";
     |HAZ Linea;

     |SI #labempre#68 = "S";
          aAlfa = "";
          |ABRE #labemett;
          #labemett#0 = #labcentr#0;
          #labemett#1 = #labcentr#1;
          |LEE 000#labemett.=;
          |SI FSalida = 0;
               aAlfa = #labemett#5;
          |FINSI;
          |CIERRA #labemett;
          |QBF aAlfa;
          |SI aAlfa ! "";
               aAlfa = "<TIPO_CESION>02</TIPO_CESION>";
               |HAZ Linea;

               nNume = #labemett#4;
               aRegimen = nNume;
               |QBF aRegimen;
               aRegimen = ("0000" + aRegimen) % -104;

               aAlfa = #labemett#5;
               |QBF aAlfa;
               aLon = aAlfa % 0;
               nLon = aLon;
               |SI nLon = 12;
                    aAlfa = "<NSS_CESION>" + aAlfa + "</NSS_CESION>";
               |SINO;
                    aAlfa = "<CCC_CESION>" + aRegimen + aAlfa + "</CCC_CESION>";
               |FINSI;
               |HAZ Linea;
          |FINSI;
     |FINSI;

     |SI nOpcionSS = 1;
          || tengo el convenio leido de un poco mas arriba
          aAlfa = #nomconca#142;   || para que preguntarlo antes?
          |QBF aAlfa;
          aAlfa = "<CONVENIOCOLECTIVO>" + aAlfa + "</CONVENIOCOLECTIVO>";
          |HAZ Linea;
     |FINSI;

     |SI nOpcionSS = 2;
          aAlfa = "";
          aAlfa1 = #nomtmp06#6;
          |QBF aAlfa1;
          |SI aAlfa1 ! "";
               aAlfa = (aAlfa1 % 102) + "/" + (aAlfa1 % 402) + "/" + (aAlfa1 % -104);
          |FINSI;

          aAlfa = "<FECHA_BAJA>" + aAlfa + "</FECHA_BAJA>";
          |HAZ Linea;

          |SI #nomtmp06#7 = 90; #nomtmp06#7 = 91; |FINSI;
          |SI #nomtmp06#7 = 98; #nomtmp06#7 = 99; |FINSI;
          aAlfa = #nomtmp06#7;
          |QBF aAlfa;

          nNume = aAlfa;
          |SI nNume = 00;
               aAlfa = "";
          |FINSI;

          aAlfa = "<CAUSABAJA>" + aAlfa + "</CAUSABAJA>";
          |HAZ Linea;
          aAlfa = "";
          aAlfa1 = #nomtmp06#9;
          |QBF aAlfa1;
          |SI aAlfa1 ! ""; |Y aAlfa1 ! "01.01.0000";
               aAlfa = (aAlfa1 % 102) + "/" + (aAlfa1 % 402) + "/" + (aAlfa1 % -104);

               aAlfa = "<FECHAVACACIONES>" + aAlfa + "</FECHAVACACIONES>";
               |HAZ Linea;
          |FINSI;
     |FINSI;

     nNivel = 3;
     aAlfa = "</DATOS_ALTA_AFILIACION>";
     |HAZ Linea;
|FPRC;

|PROCESO Domicilio;
     eaCodNif = #labtraba#7;
     |HAZ LeeNifs;

     nNivel = 3;
     aAlfa = "<DOMICILIO>";
     |HAZ Linea;

     nNivel = 4;

     aAlfa = #dsam0103#3;  |QBF aAlfa;
     aAlfa = "<NUMERO>" + aAlfa + "</NUMERO>";
     |HAZ Linea;

     aAlfa = #dsam0103#16;  |QBF aAlfa;
     aAlfa = "<ESCALERA>" + aAlfa + "</ESCALERA>";
     |HAZ Linea;

     aAlfa = #dsam0103#11;  |QBF aAlfa;
     aAlfa = "<PUERTA>" + aAlfa + "</PUERTA>";
     |HAZ Linea;

     aAlfa = #dsam0103#9;  |QBF aAlfa;
     aAlfa = "<TIPO_VIA>" + aAlfa + "</TIPO_VIA>";
     |HAZ Linea;

     aCodPos = (("00" + #labtraba#8) % -102) + (("000" + #labtraba#9) % -103);
     |HAZ CodigoMuni;
     |SI nContMuni = 0;
          aAlfa1 = "";
     |SINO;
          aAlfa1 = aCodMuni;
     |FINSI;
     aAlfa = "<CODIGO_MUNICIPIO>" + aAlfa1 +  "</CODIGO_MUNICIPIO>";
     |HAZ Linea;

     aAlfa = #dsam0103#10;  |QBF aAlfa;
     aAlfa = "<PISO>" + aAlfa + "</PISO>";
     |HAZ Linea;

     aAlfa = #dsam0103#2;  |QBF aAlfa;
     aAlfa = "<VIA>" + aAlfa + "</VIA>";
     |HAZ Linea;

     nNivel = 3;
     aAlfa = "</DOMICILIO>";
     |HAZ Linea;
|FPRC;

|PROCESO Representante;
     nNivel = 3;
     aAlfa = "<REPRESENTANTE>";
     |HAZ Linea;

     nNivel = 4;
     aAlfa = #labtraba#22;
     aAlfa1 = aAlfa % 103;
     |SI aAlfa1 = "...";
          aAlfa = "";
     |FINSI;
     aAlfa = "<DNI>" + aAlfa + "</DNI>";
     |HAZ Linea;

     aAlfa = #labtraba#20;
     aAlfa1 = aAlfa % 103;
     |SI aAlfa1 = "...";
          aAlfa = "";
     |FINSI;
     aAlfa = "<NOMBRE>" + aAlfa + "</NOMBRE>";
     |HAZ Linea;

     aAlfa = #labtraba#23;
     aAlfa1 = aAlfa % 103;
     |SI aAlfa1 = "...";
          aAlfa = "";
     |FINSI;
     aAlfa = "<PARENTESCO>" + aAlfa + "</PARENTESCO>";
     |HAZ Linea;

     nNivel = 3;
     aAlfa = "</REPRESENTANTE>";
     |HAZ Linea;
|FPRC;

|PROCESO Trabajadores;
     nNivel = 2;
     aAlfa = "<TRABAJADOR>";
     |HAZ Linea;

     nNivel = 3;
     aAlfa = nOpcionSS;
     aAlfa = "<OPCIONSS>" + aAlfa + "</OPCIONSS>";
     |HAZ Linea;

     aCadena = #labtraba#2;
     |HAZ SeparaNom_plb;

     nNivel = 3;

     aAlfa = "<NOMBRE>" + eaNom + "</NOMBRE>";
     |HAZ Linea;
     aAlfa = "<PRIMER_APELLIDO>" + eaApel1 + "</PRIMER_APELLIDO>";
     |HAZ Linea;
     aAlfa = "<SEGUNDO_APELLIDO>" + eaApel2 + "</SEGUNDO_APELLIDO>";
     |HAZ Linea;

     aAlfa1 = #labtraba#11; |QBF aAlfa1;
     |SI aAlfa1 ! ""; |Y aAlfa1 ! "          "; |Y aAlfa1 ! "..........";
          aAlfa = (aAlfa1 % 102) + "/" + (aAlfa1 % 402) + "/" + (aAlfa1 % -104);
     |FINSI;
     aAlfa = "<FECHA_NACIMIENTO>" + aAlfa + "</FECHA_NACIMIENTO>";
     |HAZ Linea;

     aAlfa = #labtraba#7;  |QBF aAlfa;
     aAlfa = "<NUMERO_DOCUMENTO>" + aAlfa + "</NUMERO_DOCUMENTO>";
     |HAZ Linea;

     |HAZ TipoDocumentoDoc;
     aAlfa = "<TIPO_DOCUMENTO>" + aAlfa + "</TIPO_DOCUMENTO>";
     |HAZ Linea;

     aAlfa = "";
     |SI #labtraba#252 = "V"; aAlfa = "1"; |FINSI;
     |SI #labtraba#252 = "M"; aAlfa = "2"; |FINSI;
     aAlfa = "<SEXO>" + aAlfa + "</SEXO>";
     |HAZ Linea;

     aAlfa = #labtraba#244; |QBF aAlfa;
     aAlfa = "<TELEFONO>" + aAlfa + "</TELEFONO>";
     |HAZ Linea;

     aAlfa = #labtraba#75; |QBF aAlfa;
     aAlfa = "<NUMERO_SEGURIDAD_SOCIAL>" + aAlfa + "</NUMERO_SEGURIDAD_SOCIAL>";
     |HAZ Linea;

     aAlfa = #labtraba#76; |QBF aAlfa;
     aAlfa1 = aAlfa % 0599; aAlfa = aAlfa % 0103;
     |SI aAlfa = "ESP";                   aAlfa = "724"; |FINSI;
     |SI aAlfa < "000"; |O aAlfa > "999"; aAlfa = "724"; |FINSI;
     aAlfa = "<NACIONALIDAD>" +  aAlfa + "</NACIONALIDAD>";
     |HAZ Linea;

     aAlfa = "<PAIS>" + "724" + "</PAIS>";
     |HAZ Linea;

     aAlfa = #labtraba#73; |QBF aAlfa;
     aAlfa = "<ESTUDIOS>" + aAlfa + "</ESTUDIOS>";
     |HAZ Linea;

     |HAZ Domicilio;

     |HAZ Representante;

     |HAZ DatosAlta;

     nNivel = 2;
     aAlfa = "</TRABAJADOR>";
     |HAZ Linea;

     nNumTra = nNumTra + 1;
|FPRC;

|PROCESO nomtmp06_online;
     |SI #nomtmp06#4 ! "*";
         |BORRA 020#nomtmp06.a;
         |LIBERA #nomtmp06;

         |ACABA;
     |FINSI;

     |ABRE #labempre;
     #labempre#0 = #nomtmp06#0;
     |LEE 000#labempre.=;
     |CIERRA #labempre;

     |ABRE #labtraba;
     #labtraba#0 = #nomtmp06#0;
     #labtraba#1 = #nomtmp06#1;
     |LEE 000#labtraba.=;
     |CIERRA #labtraba;

     |HAZ Trabajadores;

     contador = contador + 1;
     |HAZ RegistroMas;

     |ABRE #labnct16;
     #labnct16#0 = #labtraba#0;
     #labnct16#1 = #labtraba#1;
     #labnct16#2 = fFechaEnvio;
     #labnct16#3 = aHoraEnvio;
     |SI nOpcionSS = 1;
          #labnct16#4 = "A";
     |FINSI;
     |SI nOpcionSS = 2;
          #labnct16#4 = "N";
     |FINSI;
     #labnct16#5 = #labtraba#2;
     #labnct16#6 = #labempre#2;
     #labnct16#7 = #labtraba#7;
     #labnct16#8 = "GENERADO";
     #labnct16#9 = "0000";
     #labnct16#10 = "MOVIMIENTO PREPARADO PARA ENVIO";

     |GRABA 020#labnct16.n;
     |LIBERA #labnct16;
     |CIERRA #labnct16;

|FPRC;

|DEFBUCLE nomtmp06_online;
     #nomtmp06#1;
     ;
     INICIO;
     FINAL;
     be;
     NULL, nomtmp06_online;
|FIN;

|PROCESO GeneraXML;
     |ABRE #labcontr;
     |LEE 000#labcontr.p;
     |CIERRA #labcontr;

     |ABRE #labempre;
     |ABRE #labtraba;

     aRuta = #labcontr#51;
     |QBF aRuta;
     |HAZ CompRuta;
     aRuta = aRuta + "NETCONTRATA\"
     |RMKDIR aRuta;
     aRuta = aRuta + "SSONLINE\"
     |RMKDIR aRuta;

     eaCadena = aRuta;
     eaCaracter1 = "/";
     eaCaracter2 = "\";
     |HAZ sustituye_plb;
     aRuta = eaCadenaSal;

     |MODULO aAlfa;
     |QBF aAlfa;
     |SI aAlfa = "labta611";
          |SI #labta611#2 = "A"; nOpcionSS = 1; |FINSI;
          |SI #labta611#2 = "B"; nOpcionSS = 2; |FINSI;
     |FINSI;
     |SI aAlfa = "labparta";
          |SI #labparta#2 = "A"; nOpcionSS = 1; |FINSI;
          |SI #labparta#2 = "B"; nOpcionSS = 2; |FINSI;
     |FINSI;
     |SI aAlfa = "labnct15";
          |SI #labnct15#2 = "A"; nOpcionSS = 1; |FINSI;
          |SI #labnct15#2 = "B"; nOpcionSS = 2; |FINSI;
     |FINSI;

     aAlfa = nOpcionSS;
     aFichero = "OPCION" + aAlfa;

     |MODULO aAlfa;
     |QBF aAlfa;
     |SI aAlfa = "labta611"; aAlfa1 = #labta611#0; |FINSI;
     |SI aAlfa = "labparta"; aAlfa1 = #labparta#0; |FINSI;
     |SI aAlfa = "labnct15"; aAlfa1 = ""; |FINSI;

     |QBF aAlfa1; aAlfa1 = ("00000" + aAlfa1) % -105;
     aAlfa2 = "MASIVO";

     aFichero = aFichero + "_" + aAlfa1 + "_" + aAlfa2;

     fFecha = ~;
     aFecha = fFecha;
     aFecha = (aFecha % 102) + (aFecha % 402) + (aFecha % 904);
     aFichero = aFichero + "_" + aFecha;
     |HORA aHora;
     |QBF aHora;
     aHora = aHora - ":" - ":";
     aFichero = aFichero + "_" + aHora + ".xml";

     fFechaEnvio = fFecha;
     aHoraEnvio = aHora;

     aRutaBAT = aRuta + "ejecuta.bat";
     aRuta = aRuta + aFichero;
     |XABRE "CBW", aRuta, nHandle;
     |SI FSalida ] 0;
          aFich = aRuta;

          nNivel = 1;
          aAlfa = "<?xml version=" + &34 + "1.0" + &34 + " encoding=" + &34 + "ISO-8859-1" + &34 + " ?>";
          |HAZ Linea;

          aAlfa = "<ALTASBAJASMULTIPLES>";
          |HAZ Linea;

          |ABRE #labnct16;
          |BUCLE nomtmp06_online;
          |CIERRA #labnct16;

          nNivel = 1;
          aAlfa = "</ALTASBAJASMULTIPLES>";
          |HAZ Linea;

          |CIERRA #labtraba;
          |CIERRA #labempre;
     |SINO;
          aAlfa1 = #labcontr#51;
          |QBF aAlfa1;
          aAlfa1 = aAlfa1 + "\NETCONTRATA\SSONLINE\"
          aAlfa = "    ATENCION. No se ha podido grabar el fichero 'ejecuta.bat' ";
          aAlfa = aAlfa + "en la carpeta " + aAlfa1;
          |MENAV aAlfa;

          aAlfa = #labcontr#51;
          |QBF aAlfa;
          aAlfa = "    Compruebe que la ruta especificada en el Control de la Aplicación";
          aAlfa = aAlfa + " para depositar los ficheros XML, es válida.";
          |MENAV aAlfa;
     |FINSI;
     |XCIERRA nHandle;
|FPRC;

|PROCESO min;
     #labnct16#2 = fFechaEnvio;
     #labnct16#3 = aHoraEnvio;
     #labnct16#0 = 00001;
     #labnct16#1 = 00001;
     #labnct16#9 = 0000;
|FPRC;

|PROCESO max;
     #labnct16#2 = fFechaEnvio;
     #labnct16#3 = aHoraEnvio;
     #labnct16#0 = 99999;
     #labnct16#1 = 99999;
     #labnct16#9 = 9999;
|FPRC;

|PROCESO BajaOnline;
     contador = 0;
     |HAZ GeneraXML;
     |SI contador ! 0;
          aAlfa = contador;
          aAlfa = "    Se han generado " + aAlfa + " movimientos ";
          aAlfa = aAlfa + "pendientes de "
          |SI nOpcionSS = 1;
               aAlfa = aAlfa + "alta";
          |FINSI;
          |SI nOpcionSS = 2;
               aAlfa = aAlfa + "baja";
          |FINSI;
          |MENAV aAlfa;
          |MENAV "    A continuación se realizará el envío a través de ConectaSS";

          |HAZ Envio;
          |MENAV "    Pulse OK cuando haya finalizado el proceso de envío para recoger las respuestas";

          swRespuesta = 0;
          |HAZ Respuesta;

          |SI swRespuesta = 1;
               |HAZ Informe;
          |FINSI;
     |FINSI;
|FPRC;
