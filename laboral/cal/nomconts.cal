|FICHEROS;
    nomconts;
    nomconta;
    nomcontb;

    labempre;
    labempax;      || aux. empresas
    labemphi;      || aux. empresas historico
    labtraba;

    nomcalca #1;   || mensual
    nomhicca #2;   || historico cabs, mensuales y atrasos
    nomcalac #3;   || atrasos cabs.

    nomcalli #4;   || lineas mensual
    nomhicli #5;   || lineas historico
    nomcalal #6;   || lineas atrasos

    nomauxi1;
    nomcontc;
    nomcontd,MANTE;

    labcentr;
    cretam00;
    cretam01;
    cretam02;
|FIN;

|VARIABLES;
     nPara1   = 0;
     aAlfa    = "";
     aAlfa1   = "";
     aAlfa2   = "";
     aAlfa3   = "";
     aLongVar = "";
     nLongVar = 0;
     nPosVar  = 0;
     nCarVar  = 0;
     aCarVar  = "";
     nNume1   = 0;
     fFech1   = @;
     p_mes    = "";
     nUltimo  = 0;
     dia1     = "";
     dia2     = "";
     mes1     = "";
     mes2     = "";
     any1     = "";
     any2     = "";
     any3     = "";
     any4     = "";
     topes    = 0;
     fech     = "";
     sw_d     = 0;
     sw_m     = 0;
     sw_a     = 0;
     sw_b     = 0;

     nSw          = 0;
     aIP          = "";
     nLocal       = 0;
     aPathEmision = "";
     aFichero     = "";
     aFicsecu     = "";
     aOrigen      = "";
     aDestino     = "";
     nEmpresa     = 0;
     nTrabajador  = 0;
     nCentro      = 0;

     nMes         = 0;
     nTipo        = 0;
     nAnyCreta    = 0;
     nMesCreta    = 0;
     swTTc1       = 0;
     swTTc1Emp    = 0;
     nDTipCreta   = 0;
     nHTipCreta   = 0;
     nExisCCC     = 0;
     nHayNom      = 0;
     nHayTC1      = 0;

     nDevengos=0;
     nSegSocial=0;
     nSSCargo= 0;
     nRetencionIRPF=0;
     nEspecie=0;
     nOtrasDeducciones=0;
     nAnticipos=0;
     nAnticipo1=0;
     nAnticipo2=0;
     nLiquido=0;
     nImporteTC1=0;
     nImporteSST=0;
     FEstado = 0;
     p_alfa1 = "";
     p_error = 0;
     p_lugar = 0;
     p_cuan3 = 0;
     p_cuan4 = 0;
     p_cuan5 = 0;
     p_alfa2 = "";
     p_alfa3 = "";
     p_alfa4 = "";
     p_alfa5 = "";
     p_nume1 = 0;
     p_nume2 = 0;
     p_ceros = "";

     nAsiento  = 0;
     nApunte   = 0;
     nNumcam   = 0;
     nDesglo   = 0;
     nImpor    = 0;
     nImporNCS = 0;
     nCuadra   = 0;
     sep_campo = "";
     ant_area  = "";
     aConte    = "";
     carga     = "";
     signe     = "";
     espac     = "";
     aNombre   = "";
     nOk       = 0;

     aAbre    = "";
     fgrab    = "";
     fcanl    = 0;
     sw_rup   = 0;

     aDNI     = "";
     aArea    = "";
     aSeccion = "";
     aCentro  = "";
     nSwCentro = 0;
     aNomTraba = "";
     nNumFicheros = 0;

     nOpera   = 0;
     nDesde1  = 0;
     nDesde2  = 0;
     nHasta1  = 0;
     nHasta2  = 0;

     &bus_emp = 0;
     &bus_tra = 0;
     &bus_cla = 0;

     &nXDesdeTrabajador=0;
     &nXHastaTrabajador=0;
     &nXDesdeCentro=0;
     &nXHastaCentro=0;
     &nXDesdeMes=0;
     &nXHastaMes=0;
     &nXAnyo=0;
|FIN;
____________________________________/ CALCULOS PANTALLA
|PROCESO ClControl; |TIPO 6;
   |SI FSalida = 10;
       |SUB_EJECUTA "nomcontb";
       |ERROR;
   |FINSI;
|FPRC;

|PROCESO no_atrasost; |TIPO 7;
     |SI #nomconts#39 = "T";
          aAlfa1 = "S";
     |SINO;
          aAlfa1 = "N";
     |FINSI;
     |C_M #nomconts#41, 1, aAlfa1;
|FPRC;

|PROCESO defecto; |TIPO 7;
     |SI #nomconts#39 = "H";|O #nomconts#39 = "T";
          fFech1 = ~;
          aAlfa1 = fFech1;
          aAlfa1 = aAlfa1 % -102;
          #nomconts#Campo# = aAlfa1;
          |PINTA #nomconts#Campo#;
     |FINSI;
|FPRC;

|PROCESO LaFuente;
  |SI #nomconts#39 = "A"; |O #nomconts#39 = "T";
      |C_V #nomconts#43,1,"S";
      |C_M #nomconts#43,1,"S";
      |PINTA 1136, "De"; |PINTA 1160, "A Mes:    [      ]";
      |PINTA #nomconts#43;
  |SINO;
      |C_V #nomconts#43,1,"N";
      |C_M #nomconts#43,1,"N";
      |PINTA 1136, "  "; |PINTA 1160, "                  ";
  |FINSI;
|FPRC;

|PROCESO mirames; |TIPO 7;
    fFech1 = ~;
    aAlfa1 = fFech1;
    aAlfa1 = aAlfa1 % 402;
    #nomconts#Campo# = aAlfa1;
    |PINTA #nomconts#Campo#;
|FPRC;

|PROCESO pintames; |TIPO 0;
     |C_M #nomconts#Campo#,0,aAlfa1;
     |SI aAlfa1 = "N"; |ACABA; |FINSI;

     |SI Campo = 43;
         |SI #nomconts#42 > #nomconts#43;
             |MENAV "    Error en seleccion Mes Hasta";
             |ERROR;
         |FINSI;
     |SINO;
         #nomconts#43 = #nomconts#42;
         |SI #nomconts#39 = "A"; |O #nomconts#39 = "T";
             |PINTA #nomconts#43;
         |FINSI;
     |FINSI;

     |SI #nomconts#Campo# =  1;  p_mes = "ENERO     ";  nUltimo = 31; |FINSI;
     |SI #nomconts#Campo# =  2;  p_mes = "FEBRERO   ";  nUltimo = 28; |FINSI;
     |SI #nomconts#Campo# =  3;  p_mes = "MARZO     ";  nUltimo = 31; |FINSI;
     |SI #nomconts#Campo# =  4;  p_mes = "ABRIL     ";  nUltimo = 30; |FINSI;
     |SI #nomconts#Campo# =  5;  p_mes = "MAYO      ";  nUltimo = 31; |FINSI;
     |SI #nomconts#Campo# =  6;  p_mes = "JUNIO     ";  nUltimo = 30; |FINSI;
     |SI #nomconts#Campo# =  7;  p_mes = "JULIO     ";  nUltimo = 31; |FINSI;
     |SI #nomconts#Campo# =  8;  p_mes = "AGOSTO    ";  nUltimo = 31; |FINSI;
     |SI #nomconts#Campo# =  9;  p_mes = "SEPTIEMBRE";  nUltimo = 30; |FINSI;
     |SI #nomconts#Campo# = 10;  p_mes = "OCTUBRE   ";  nUltimo = 31; |FINSI;
     |SI #nomconts#Campo# = 11;  p_mes = "NOVIEMBRE ";  nUltimo = 30; |FINSI;
     |SI #nomconts#Campo# = 12;  p_mes = "DICIEMBRE ";  nUltimo = 31; |FINSI;

     |ATRI I;
     |SI Campo = 42; |PINTA 1148, p_mes; |FINSI;
     |SI Campo = 43;
          p_mes = p_mes % 106;
          |PINTA 1171, p_mes;
     |FINSI;
     |ATRI 0;
|FPRC;

|PROCESO desde_hasta; |TIPO 0;

     aAlfa1 = "S"; aAlfa2 = "N";

     |SI #nomconts#1 = 0;
         aAlfa2 = "S"; aAlfa1 = "N";
         |PARA nPara1 = 2; |SI nPara1 [ 11; |HACIENDO nPara1 = nPara1 + 1;
               #nomconts#nPara1# = 0;
               |PINTA #nomconts#nPara1#;
         |SIGUE;
     |SINO;
          #nomconts#12 =     1; |PINTA #nomconts#12;
          #nomconts#13 = 99999; |PINTA #nomconts#13;
          #nomconts#44 =     1; |PINTA #nomconts#44;
          #nomconts#45 = 99999; |PINTA #nomconts#45;
          #nomconts#46 =     1; |PINTA #nomconts#46;
          #nomconts#47 =   999; |PINTA #nomconts#47;
     |FINSI;

     |PARA nPara1 = 2; |SI nPara1 [ 11; |HACIENDO nPara1 = nPara1 + 1;
           |C_M #nomconts#nPara1#, 1, aAlfa1;
     |SIGUE;
     |C_M #nomconts#12, 1, aAlfa2;
     |C_M #nomconts#13, 1, aAlfa2;
     |C_M #nomconts#44, 1, aAlfa2;
     |C_M #nomconts#45, 1, aAlfa2;
     |C_M #nomconts#46, 1, aAlfa2;
     |C_M #nomconts#47, 1, aAlfa2;
|FPRC;

|PROCESO lee_emp; |TIPO 0;
     |C_M #nomconts#Campo#, 0 , aAlfa1;
     |SI aAlfa1 = "N"; |ACABA; |FINSI;

     |SI #nomconts#Campo# ! 0;
         |ABRE #labempre;
         #labempre#0 = #nomconts#Campo#;
         |LEE 000#labempre.=;
         |SI FSalida ! 0;
             |MENAV "    Empresa INEXISTENTE ...";
             |ERROR;
         |FINSI;
         |CIERRA #labempre;
     |FINSI;
|FPRC;

|PROCESO pin_emp; |TIPO 7;
     |C_M #nomconts#Campo#, 0 , aAlfa1;
     |SI aAlfa1 = "N"; |ACABA; |FINSI;

     |DDEFECTO #labempre;
     |SI #nomconts#Campo# ! 0;
         |ABRE #labempre;
          #labempre#0 = #nomconts#Campo#;
          |LEE 000#labempre.=;
          |CIERRA #labempre;
     |FINSI;
     |ATRI I;   |PINTA 1437, #labempre#2;  |ATRI 0;
|FPRC;

|PROCESO QueNomina;
     aAlfa1 = "S";
     |SI #nomconts#34 = "N";
         aAlfa1 = "N";
         |PDEFECTO #nomconts, 14, 21;
         |PDEFECTO #nomconts, 24, 31;
         #nomconts#49 = "N";
         |PINDA #1#nomconts;
     |FINSI;

     |C_M #nomconts#14, 1, aAlfa1; |C_M #nomconts#24, 1, aAlfa1;
     |C_M #nomconts#15, 1, aAlfa1; |C_M #nomconts#25, 1, aAlfa1;
     |C_M #nomconts#16, 1, aAlfa1; |C_M #nomconts#26, 1, aAlfa1;
     |C_M #nomconts#17, 1, aAlfa1; |C_M #nomconts#27, 1, aAlfa1;
     |C_M #nomconts#18, 1, aAlfa1; |C_M #nomconts#28, 1, aAlfa1;
     |C_M #nomconts#19, 1, aAlfa1; |C_M #nomconts#29, 1, aAlfa1;
     |C_M #nomconts#20, 1, aAlfa1; |C_M #nomconts#30, 1, aAlfa1;
     |C_M #nomconts#49, 1, aAlfa1;
|FPRC;

|PROCESO QueSeguros;
     aAlfa1 = "S";
     |SI #nomconts#35 = "N";
         aAlfa1 = "N";
         |PDEFECTO #nomconts, 21, 24;
         |PDEFECTO #nomconts, 31, 34;
         |PINDA #1#nomconts;
     |FINSI;

     |C_M #nomconts#21, 1, aAlfa1; |C_M #nomconts#31, 1, aAlfa1;
     |C_M #nomconts#22, 1, aAlfa1; |C_M #nomconts#32, 1, aAlfa1;
     |C_M #nomconts#23, 1, aAlfa1; |C_M #nomconts#33, 1, aAlfa1;
|FPRC;

|PROCESO min12;
    #nomcontd#0 = "            ";
|FPRC;

|PROCESO max12;
    #nomcontd#0 = "zzzzzzzzzzzz";
|FPRC;

|PROCESO asignacion; |TIPO 0;
    |C_M #nomconts#49, 0, aAlfa1;

    |SI #nomconts#49 = "S";|Y FSalida = 0; |Y aAlfa1 = "S";
        |PUSHV 0501 2380;
        |ENTLINEAL #1#nomcontd, 1, 3, 22, 1, min12, max12;
        |POPV;
    |FINSI;
|FPRC;

|| ... nomcontd

|PROCESO busqueda; |TIPO 11;
    bus_emp = 0;
    bus_tra = 0;
    bus_cla = 0;
    |SI FSalida =  9;  bus_cla = 1;  |FINSI;
    |SI FSalida = 10;  bus_cla = 2;  |FINSI;
    |SI FSalida = 11;  bus_cla = 3;  |FINSI;
    |SI bus_cla ! 0;
        |SUB_EJECUTA "nombusca";
        |ABRE #labtraba;
        |DDEFECTO #labtraba;
        #labtraba#0 = bus_emp;
        #labtraba#1 = bus_tra;
        |LEE 000#labtraba.=;
        |SI FSalida = 0;
            #nomcontd#0 = #labtraba#7;   |PINTA #nomcontd#0;
            #nomcontd#1 = #labtraba#2;   |PINTA #nomcontd#1;
            |ERROR;
            |PTEC 802;
        |FINSI;
        |CIERRA #labtraba;
    |FINSI;
|FPRC;

|PROCESO selecciona; |TIPO 7;

    aAlfa1 = "N";
    |SI #nomconts#24 = 2; aAlfa1 = "S"; |FINSI;
    |C_M #nomcontd#2, 1, aAlfa1;
    |SI aAlfa1 = "S"; |Y #nomcontd#2 = "            ";
        aAlfa1 = #nomconts#14; |QBF aAlfa1;
        aAlfa1 = aAlfa1 + (("00000" + #labtraba#1) % -105);
        #nomcontd#2 =  aAlfa1; |PINTA #nomcontd#2;
    |FINSI;

    aAlfa1 = "N";
    |SI #nomconts#25 = 2; aAlfa1 = "S"; |FINSI;
    |C_M #nomcontd#3, 1, aAlfa1;
    |SI aAlfa1 = "S"; |Y #nomcontd#3 = "            ";
        aAlfa1 = #nomconts#15; |QBF aAlfa1;
        aAlfa1 = aAlfa1 + (("00000" + #labtraba#1) % -105);
        #nomcontd#3 = aAlfa1; |PINTA #nomcontd#3;
    |FINSI;

    aAlfa1 = "N";
    |SI #nomconts#26 = 2; aAlfa1 = "S"; |FINSI;
    |C_M #nomcontd#4, 1, aAlfa1;
    |SI aAlfa1 ="S"; |Y #nomcontd#4 = "            ";
        aAlfa1 = #nomconts#16; |QBF aAlfa1;
        aAlfa1 = aAlfa1 + (("00000" + #labtraba#1) % -105);
        #nomcontd#4 = aAlfa1; |PINTA #nomcontd#4;
    |FINSI;

    aAlfa1 = "N";
    |SI #nomconts#27 = 2; aAlfa1 = "S"; |FINSI;
    |C_M #nomcontd#5, 1, aAlfa1;
    |SI aAlfa1="S"; |Y #nomcontd#5 = "            ";
        aAlfa1= #nomconts#17; |QBF aAlfa1;
        aAlfa1 = aAlfa1 + (("00000" + #labtraba#1) % -105);
        #nomcontd#5 = aAlfa1; |PINTA #nomcontd#5;
    |FINSI;

    aAlfa1 = "N";
    |SI #nomconts#28 = 2; aAlfa1 = "S"; |FINSI;
    |C_M #nomcontd#6, 1, aAlfa1;
    |SI aAlfa1 = "S"; |Y #nomcontd#6 = "            ";
        aAlfa1 = #nomconts#18; |QBF aAlfa1;
        aAlfa1 = aAlfa1 + (("00000" + #labtraba#1) % -105);
        #nomcontd#6 = aAlfa1; |PINTA #nomcontd#6;
    |FINSI;

    aAlfa1 = "N";
    |SI #nomconts#29 = 2; aAlfa1 = "S"; |FINSI;
    |C_M #nomcontd#8, 1, aAlfa1;
    |SI aAlfa1 = "S"; |Y #nomcontd#8 = "            ";
        aAlfa1 = #nomconts#19; |QBF aAlfa1;
        aAlfa1 = aAlfa1 + (("00000" + #labtraba#1) % -105);
        #nomcontd#8 = aAlfa1; |PINTA #nomcontd#8;
    |FINSI;

    aAlfa1 = "N";
    |SI #nomconts#30 = 2; aAlfa1 = "S"; |FINSI;
    |C_M #nomcontd#7, 1, aAlfa1;
    |SI aAlfa1 = "S"; |Y #nomcontd#7 = "            ";
        aAlfa1 = #nomconts#20; |QBF aAlfa1;
        aAlfa1 = aAlfa1 + (("00000" + #labtraba#1) % -105);
        #nomcontd#7 = aAlfa1; |PINTA #nomcontd#7;
    |FINSI;

|FPRC;

|PROCESO comprueba; |TIPO 7;
    |ABRE #labtraba;
    |SELECT #2#labtraba;
    #labtraba#7 = #nomcontd#0;
    |LEE 000#labtraba.=;
    |SI FSalida = 0;
        #nomcontd#1 = #labtraba#2;
        |PINTA #nomcontd#1;
    |SINO;
        |MENAV "    D.N.I. inaccesible o inexistente. Utilice la consulta (<F3>) ...";
        |PTEC 807;
    |FINSI;
    |CIERRA #labtraba;
|FPRC;

|PROCESO Tipo7C36; |TIPO 7;
    nNume1 = 2000 + #nomconts#40; aAlfa1 = nNume1;
    aAlfa2 = ("00" + #nomconts#42) % -102;
    aAlfa3 = "31";
    |SI #nomconts#42 = 4; |O #nomconts#42 = 6;
       |O #nomconts#42 = 9; |O #nomconts#42 = 11;
        aAlfa3 = "30";
    |FINSI;
    |SI #nomconts#42 = 2;
        aAlfa3 = "28";
        |SI nNume1 = 2004; |O nNume1 = 2008; |O nNume1 = 2012;
           |O nNume1 = 2016; |O nNume1 = 2020; |O nNume1 = 2024;
             aAlfa3 = "29";
       |FINSI;
    |FINSI;
    aAlfa1 = aAlfa3 + "." + aAlfa2 + "." + aAlfa1;
    #nomconts#36 = aAlfa1;
    |PINTA #nomconts#36;
|FPRC;

|| ***************************** Procesos Varios
|| ... Procesos nuevos de Creta
|PROCESO ControlTc1;
   nMesCreta = nXDesdeMes;
   nAnyCreta = 2000 + nXAnyo;
   |SI nXAnyo ] 80;  nAnyCreta = 1900 + nXAnyo;  |FINSI;

   swTTc1 = 1;
   |ABRE #cretam00;
   |DDEFECTO #cretam00;
   |LEE 001#cretam00.p;
   |CIERRA #cretam00;

   |SI #cretam00#1 = "N"; swTTc1 = 0; |FINSI;  || No Activado
   |SI #cretam00#8 ! "R"; swTTc1 = 0; |FINSI;  || No Real, en practicas

   |SI #cretam00#10 > nAnyCreta; swTTc1 = 0; |FINSI;  || No se ha puesto en marcha
   |SI #cretam00#10 = nAnyCreta;
       |Y #cretam00#9 > nMesCreta;
          swTTc1 = 0;
   |FINSI;
|FPRC;

|| /////////////////////////////////////////////////////////////////////////
|PROCESO CopiaANombre;
     aDestino = #nomcontb#11; |QBF aDestino;
     |SI #nomcontb#16 ! 0;
         aAlfa1   = ("00000" + nEmpresa) % -105;
         aAlfa2   = ("00"    + #nomconts#40) % -102;
         aAlfa3   = ("00"    + #nomconts#42) % -102;
         |SI #nomcontb#16 = 1;
             aDestino = aDestino + aAlfa1 + aAlfa2 + aAlfa3 + ".txt";
         |SINO;
             aDestino = aDestino + "DATOSEXT"  +  "." + aAlfa1 + aAlfa2 + aAlfa3;
         |FINSI;
     |FINSI;

     |SI nLocal = 0;
         |ENVIA_FICHERO aOrigen, aDestino;
     |SINO;
         |COPIA_FICHERO aOrigen, aDestino;
     |FINSI;
     |SI FSalida = 0;
         nNumFicheros = nNumFicheros + 1;
     |FINSI;
|FPRC;
|| /////////////////////////////////////////////////////////////////////////

|PROCESO grabalo;
    |XGRABA fcanl, fgrab;
|FPRC;
|| /////////////////////////////////////////////////////////////////////////

|| ... Procesos nuevos de Creta
|PROCESO LeoCretam02;

   |SI swTTc1Emp = -1; swTTc1Emp = 1; |FINSI;
   |SI #cretam02#22 ! "C";
       swTTc1Emp   = 0;
       nImporteTC1 = 0;
       |ERROR10;
       |ACABA;
   |FINSI;

   nImporteTC1 = nImporteTC1 + #cretam02#16;
|FPRC;

|DEFBUCLE LeoCretam02;
   #cretam02#1;
   ;
   nEmpresa, nAnyCreta, nMesCreta,  nDTipCreta, "               ";
   nEmpresa, nAnyCreta, nMesCreta,  nHTipCreta, "zzzzzzzzzzzzzzz";
   ;
   NULL, LeoCretam02;
|FIN;

|PROCESO Tc1Creta;

    swTTc1Emp = -1;
    nDTipCreta = 0;
    nHTipCreta = 4;
    |SI #nomconts#39 = "A"; nDTipCreta = 5;            nHTipCreta = 9;            |FINSI;
    |SI #nomconts#39 = "T"; nDTipCreta = #nomconts#41; nHTipCreta = #nomconts#41; |FINSI;

    |BUCLE LeoCretam02;
|FPRC;

|PROCESO Plabemphi;
     |SI #labemphi#2 ! 0; |Y #labemphi#2 ! 2;
        |Y #labemphi#2 ! 8; |Y #labemphi#2 ! 9;
          |Y #labemphi#2 ! 10; |Y #labemphi#2 ! 18; |Y #labemphi#2 ! 19;
          |SI #nomconts#39 = "H";    |ACABA;   |FINSI;
     |SINO;
          |SI #nomconts#39 = "T";    |ACABA;   |FINSI;
     |FINSI;
     nImporteTC1 = nImporteTC1 + #labemphi#20;
|FPRC;

|DEFBUCLE Blabemphi;
     #labemphi#1;
     ;
     nEmpresa, nXDesdeCentro, nXAnyo, nXDesdeMes,  0;
     nEmpresa, nXHastaCentro, nXAnyo, nXDesdeMes, 19;
     ;
     NULL, Plabemphi;
|FIN;

|PROCESO Plabempax;
     |SI #labempax#2 ! 0; |Y #labempax#2 ! 2;
          |Y #labempax#2 ! 8; |Y #labempax#2 ! 9;
            |Y #labempax#2 ! 10; |Y #labempax#2 ! 18; |Y #labempax#2 ! 19;
          |SI #nomconts#39 = "M";    |ACABA;   |FINSI;
     |SINO;
          |SI #nomconts#39 = "A";    |ACABA;   |FINSI;
     |FINSI;
     nImporteTC1 = nImporteTC1 + #labempax#20;
|FPRC;

|DEFBUCLE Blabempax;
     #labempax#1;
     ;
     nEmpresa,  nXDesdeCentro, nXDesdeMes,  0;
     nEmpresa,  nXHastaCentro, nXDesdeMes, 19;
     ;
     NULL, Plabempax;
|FIN;

|PROCESO PaseTC1;

    nImporteTC1=0;

    swTTc1Emp = -1;
    |SI swTTc1 = 1; |HAZ Tc1Creta; |FINSI;

    |SI swTTc1Emp  = -1;
        |SI #nomconts#39 = "M";|O #nomconts#39 = "A";
            |BUCLE Blabempax;     || asiento del TC1 (en pagas NO!)
        |SINO;
            |BUCLE Blabemphi;     || asiento del TC1 historico (en pagas NO!)
        |FINSI;
    |FINSI;
|FPRC;

|| /////////////////////////////////////////////////////////////////////////
|PROCESO nombre;
     aNombre = "";
     |SI nNumcam = 14;
         aNombre = "SUELDOS Y SALARIOS";
         |SI nOk = 1;  aNombre = aNomTraba;  |FINSI;
     |FINSI;
     |SI nNumcam = 15; aNombre = "H.P., ACREEDORA RETENCIONES PRACT."; |FINSI;
     |SI nNumcam = 16; aNombre = "ORG.S.SOCIAL, ACREEDORES"; |FINSI;
     |SI nNumcam = 17; aNombre = "REMUNERACIONES PENDIENTES DE PAGO."; |FINSI;
     |SI nNumcam = 18; aNombre = "ANTICIPOS DE REMUNERACIONES."; |FINSI;
     |SI nNumcam = 19; aNombre = "ANTICIPOS DE REMUNERACIONES."; |FINSI;
     |SI nNumcam = 20; aNombre = "INGRESOS POR SERVICIOS AL PERSONAL"; |FINSI;

     |SI nNumcam = 21; aNombre = "SEGURIDAD SOCIAL A CARGO EMPRESA"; |FINSI;
     |SI nNumcam = 22; aNombre = "ORG.S.SOCIAL, ACREEDORES"; |FINSI;
     |SI nNumcam = 23; aNombre = "ORG. SEG.SOCIAL, DEUDORES"; |FINSI;

     fgrab  = aNombre + sep_campo;
     |HAZ grabalo;
|FPRC;

|PROCESO ref_cta;  || campos en blanco
     fgrab = sep_campo +  sep_campo;
     |HAZ grabalo;
|FPRC;

|PROCESO descrip;
    |SI #nomcontb#10 ! 0;
        aAlfa1 = #nomconts#37;
        |SI nNumcam ] 21;
            |QBF aAlfa1;
            aAlfa1 = "SEGUROS SOCIALES " + aAlfa1;
        |FINSI;
        nNume1 = 100 + #nomcontb#10;
        espac  = " " * 50;
        fgrab  = ((aAlfa1 + espac) % nNume1) + sep_campo;
        |HAZ grabalo;
    |FINSI;
|FPRC;

|PROCESO impor_haber;
    |SI #nomcontb#9 ! 0;
        |SI signe = "H";|O #nomcontb#8 = 0;
            aAlfa1 = nImpor;
        |SINO;
            aAlfa1 = "0";
        |FINSI;
        |QBT aAlfa1;
        nNume1 = (100 + #nomcontb#9) * -1;
        espac  = " " * 20;
        fgrab  = ((espac + aAlfa1) % nNume1) + sep_campo;
        |HAZ grabalo;
    |FINSI;
|FPRC;

|PROCESO impor_debe;
    |SI #nomcontb#8 ! 0;
        |SI signe = "D";|O #nomcontb#9 = 0;
            aAlfa1 = nImpor;
        |SINO;
            aAlfa1 = "0";
        |FINSI;
        |QBT aAlfa1;
        nNume1 = (100 + #nomcontb#8) * -1;
        espac  = " " * 20;
        fgrab  = ((espac + aAlfa1) % nNume1) + sep_campo;
        |HAZ grabalo;
    |FINSI;
|FPRC;

|PROCESO impor;
      aAlfa1 = nImpor;
      |QBT aAlfa1;
      espac  = " " * 20;
      fgrab  = ((espac + aAlfa1) % -112) + sep_campo;
      |HAZ grabalo;
|FPRC;

|PROCESO QuitaPuntos;
     |QBT aAlfa;
     aLongVar = aAlfa % 0;
     nLongVar = aLongVar;
     aAlfa1   = "";
     |PARA nCarVar = 1;  |SI nCarVar [ nLongVar;  |HACIENDO nCarVar = nCarVar + 1;
           nPosVar = (nCarVar * 100) + 1;
           aCarVar = aAlfa % nPosVar;

           |SI  aCarVar ! ",";
                aAlfa1 = aAlfa1 + aCarVar;
           |SINO;
                aAlfa1 = aAlfa1 + ".";
           |FINSI;
      |SIGUE;

      aAlfa = aAlfa1;
|FPRC;

|PROCESO impor_NCS;
      nImporNCS = nImpor;
      |SI signe = "H";
          nImporNCS = - nImpor;
      |FINSI;
      aAlfa = nImporNCS;
      |HAZ QuitaPuntos;
      espac  = " " * 20;
      fgrab  = ((espac + aAlfa) % -110) + sep_campo;
      |HAZ grabalo;
|FPRC;

|PROCESO sign;
    |SI #nomcontb#7 ! 0;
        aAlfa1 = signe;
        |QBT aAlfa1;
        nNume1 = 100 + #nomcontb#7;
        fgrab = (aAlfa1 % nNume1) + sep_campo;
        |HAZ grabalo;
    |FINSI;
|FPRC;

|PROCESO cuenta;
    |SI #nomcontb#6 ! 0;
        p_alfa1 = aConte;
        p_alfa1 = (p_alfa1 + "            ") % 112;
        p_error = 0;
        p_lugar = 0;
        p_cuan3 = 0;
        p_cuan4 = 0;
        p_alfa3 = "";
        p_alfa4 = "";
        |PARA p_nume1 = 1; |SI p_nume1 [ 12; |HACIENDO p_nume1 = p_nume1 + 1;
              p_nume2 = (p_nume1 * 100) + 1;
              p_alfa2 = p_alfa1 % p_nume2;
              |SI p_alfa2 ! " ";
                  |SI p_alfa2 = ".";
                       p_lugar = p_nume1;
                  |SINO;
                       |SI p_lugar = 0; p_alfa3=p_alfa3+p_alfa2; p_cuan3=p_cuan3+1; |FINSI;
                       |SI p_lugar ! 0; p_alfa4=p_alfa4+p_alfa2; p_cuan4=p_cuan4+1; |FINSI;
                  |FINSI;
              |FINSI;
        |SIGUE;
        |SI p_lugar ! 0;
            p_cuan5 = #nomcontb#6;
            p_nume1 = p_cuan5 - (p_cuan3 + p_cuan4);
            |SI p_nume1 > 0;
                p_ceros = "0" * p_nume1;
                p_alfa5 = p_alfa3 + p_ceros + p_alfa4;
            |SINO;
                p_error = 1;
            |FINSI;
        |SINO;
             p_alfa5 = p_alfa1;
        |FINSI;
        aConte = p_alfa5;

        fgrab = aConte + sep_campo;
        |HAZ grabalo;
    |FINSI;
|FPRC;

|PROCESO apunte;
    |SI #nomcontb#5 ! 0;
        nApunte = nApunte + 1;
        aAlfa1  = nApunte; |QBT aAlfa1;

        nNume1  = 100 + #nomcontb#5;
        fgrab   = aAlfa1 % nNume1;
        nNume1  = nNume1 * -1;
        fgrab   = ("0000000000" + fgrab) % nNume1;
        fgrab = fgrab + sep_campo;
        |HAZ grabalo;
    |FINSI;
|FPRC;

|PROCESO asiento;

    |SI #nomcontb#4 ! 0;
        aAlfa1 = nAsiento; |QBT aAlfa1;
        nNume1 = 100 + #nomcontb#4;
        fgrab  = aAlfa1 % nNume1;
        nNume1 = nNume1 * -1;
        fgrab  = ("0000000000" + fgrab) % nNume1;
        fgrab  = fgrab + sep_campo;
        |HAZ grabalo;
    |FINSI;
|FPRC;

|PROCESO fecha;
    aAlfa1 = #nomconts#36;       || desglose de la fecha
    dia1 = aAlfa1 % 101;
    dia2 = aAlfa1 % 201;
    mes1 = aAlfa1 % 401;
    mes2 = aAlfa1 % 501;
    any1 = aAlfa1 % 701;
    any2 = aAlfa1 % 801;
    any3 = aAlfa1 % 901;
    any4 = aAlfa1 % 1001;

    aAlfa1 = #nomcontb#3 - "AA";
    |SI FSalida ! 0;
        aAlfa1 = aAlfa1 - "AA";
        |SI FSalida = 0;
            any1 = any3; any3 = "";
            any2 = any4; any4 = "";
        |FINSI;
    |FINSI;

    aAlfa1 = #nomcontb#3; |QBT aAlfa1;
    aAlfa2 = aAlfa1 % 0;   topes = aAlfa2;

    fech = ""; sw_d = 0;  sw_m = 0; sw_a = 0; sw_b = 0;

    |PARA nPara1 = 1; |SI nPara1 [ topes; |HACIENDO nPara1 = nPara1 + 1;
          nNume1 = (nPara1 * 100) + 1;
          aAlfa2 = aAlfa1 % nNume1;
          carga  = aAlfa2;
          |SI aAlfa2 = "D";|Y sw_d = 0; carga = dia1; aAlfa2 = ""; sw_d = 1; |FINSI;
          |SI aAlfa2 = "D";|Y sw_d = 1; carga = dia2; aAlfa2 = ""; sw_d = 2; |FINSI;
          |SI aAlfa2 = "M";|Y sw_m = 0; carga = mes1; aAlfa2 = ""; sw_m = 1; |FINSI;
          |SI aAlfa2 = "M";|Y sw_m = 1; carga = mes2; aAlfa2 = ""; sw_m = 2; |FINSI;
          |SI aAlfa2 = "A";|Y sw_a = 0; carga = any1; aAlfa2 = ""; sw_a = 1; |FINSI;
          |SI aAlfa2 = "A";|Y sw_a = 1; carga = any2; aAlfa2 = ""; sw_a = 2; |FINSI;
          |SI aAlfa2 = "A";|Y sw_a = 2; carga = any3; aAlfa2 = ""; sw_a = 3; |FINSI;
          |SI aAlfa2 = "A";|Y sw_a = 3; carga = any4; aAlfa2 = ""; sw_a = 4; |FINSI;
          |SI aAlfa2 = "a";|Y sw_b = 0; carga = any3; aAlfa2 = ""; sw_b = 1; |FINSI;
          |SI aAlfa2 = "a";|Y sw_b = 1; carga = any4; aAlfa2 = ""; sw_b = 2; |FINSI;
          fech = fech + carga;
    |SIGUE;
    fgrab = fech + sep_campo;
    |HAZ grabalo;
|FPRC;

|PROCESO diario;
    |SI #nomcontb#2 ! 0;
        aAlfa1 = #nomconts#50; |QBT aAlfa1;
        nNume1 = 100 + #nomcontb#2;
        fgrab  = aAlfa1 % nNume1;
        nNume1 = nNume1 * -1;
        fgrab  = ("0000000000" + fgrab) % nNume1;
        fgrab  = fgrab + sep_campo;
        |HAZ grabalo;
    |FINSI;
|FPRC;

|PROCESO PrConta;
        |HAZ diario;
        |HAZ fecha;
        |HAZ asiento;
        |HAZ apunte;
        |HAZ cuenta;
        |HAZ sign;
        |HAZ impor_debe;
        |HAZ impor_haber;
        |HAZ descrip;
|FPRC;

|PROCESO PrSAGE;
        |HAZ asiento;
        |HAZ fecha;
        |HAZ cuenta;
        |HAZ descrip;
        |HAZ sign;
        |HAZ impor;
        |HAZ ref_cta;
        |HAZ nombre;
|FPRC;

|PROCESO PrNCS;
        |HAZ cuenta;
        |HAZ fecha;

        fgrab  = " 7";      || Codigo concepto fijo
        |HAZ grabalo;
        fgrab  = "      ";  || Numero documento
        |HAZ grabalo;

        |HAZ impor_NCS;
        fgrab  = "  ";      || Codigo IVA %
        |HAZ grabalo;
        fgrab  = "      ";  || Fecha Origen
        |HAZ grabalo;
        fgrab  = "  ";      || Clave Operacion
        |HAZ grabalo;

        |HAZ descrip;
        |HAZ asiento;
        fgrab  = " ";       || Tipo Operacion
        |HAZ grabalo;
        fgrab  = "0";       || Serie Asiento
        |HAZ grabalo;
        fgrab  = "        ";|| Fecha Operacion
        |HAZ grabalo;
|FPRC;

|PROCESO apuntes;
    |SI nImpor ! 0;
        |SI #nomcontb#16 = 0;  |HAZ PrConta; |FINSI;
        |SI #nomcontb#16 = 1;  |HAZ PrSAGE;  |FINSI;
        |SI #nomcontb#16 = 2;  |HAZ PrNCS;   |FINSI;

        fgrab = "";
        |SI #nomcontb#13 ! -1;  fgrab = fgrab + &#nomcontb#13; |FINSI;
        |SI #nomcontb#14 ! -1;  fgrab = fgrab + &#nomcontb#14; |FINSI;
        |SI fgrab ! "";
            |HAZ grabalo;
        |FINSI;
    |FINSI;
|FPRC;

|| ... Segundo Asiento

|PROCESO sstmp1_6;
    |SI sw_rup = 0;
        sw_rup = 1;
        ant_area = #nomconts#12;
    |FINSI;
    |SI ant_area ! #nomconts#12;
        |HAZ apuntes;
        nCuadra = nCuadra + nImpor;
        ant_area =  #nomconts#12;
        nImpor = 0;
    |FINSI;
    nImpor = nImpor + #nomcontc#9;         || importe

    aConte = #nomcontc#nNumcam#;           || cuenta
    |QBF aConte;
    aConte = aConte + ant_area;
|FPRC;

|DEFBUCLE sstmp1;        || por areas
    #nomcontc#3;
    ;
    nEmpresa, "   ",      1;
    nEmpresa, "zzzz", 99999;
    ;
    NULL, sstmp1_6;
|FIN;

________________________________________ ASIENTO 1

|PROCESO tmp1_1;
    #labtraba#0 = #nomcontc#0;
    #labtraba#1 = #nomcontc#1;
    |LEE 020#labtraba.=;
    aNomTraba = #labtraba#2;
|FPRC;

|PROCESO tmp2_1;
    |SI sw_rup = 0;
        sw_rup = 1;
        aDNI = #nomcontc#11;
    |FINSI;
    |SI aDNI ! #nomcontc#11;
        |HAZ apuntes;
        aDNI = #nomcontc#11;
        nImpor = 0;
    |FINSI;

    |HAZ tmp0_1;

    aConte = #nomconts#nNumcam#;
    |QBF aConte;
    aConte = aConte + #nomcontc#1;

    |HAZ tmp1_1;

    nOk = 0;
    #nomcontd#0 = #nomcontc#11;
    |LEE 041#nomcontd.=;
    |SI FSalida = 0; |Y #nomconts#49 = "S";
        |SI nNumcam = 14;|Y #nomcontd#2 ! "            ";    aConte = #nomcontd#2; nOk = 1; |FINSI;
        |SI nNumcam = 15;|Y #nomcontd#3 ! "            ";    aConte = #nomcontd#3; |FINSI;
        |SI nNumcam = 16;|Y #nomcontd#4 ! "            ";    aConte = #nomcontd#4; |FINSI;
        |SI nNumcam = 17;|Y #nomcontd#5 ! "            ";    aConte = #nomcontd#5; |FINSI;
        |SI nNumcam = 18;|Y #nomcontd#6 ! "            ";    aConte = #nomcontd#6; |FINSI;
        |SI nNumcam = 19;|Y #nomcontd#8 ! "            ";    aConte = #nomcontd#8; |FINSI;
        |SI nNumcam = 20;|Y #nomcontd#7 ! "            ";    aConte = #nomcontd#7; |FINSI;
    |FINSI;

|FPRC;

|DEFBUCLE tmp2;            || por trabajadores
     #nomcontc#2;
     ;
     nEmpresa, "            ",     1;
     nEmpresa, "zzzzzzzzzzzz", 99999;
     ;
     NULL, tmp2_1;
|FIN;

|PROCESO tmp1_6;
    |SI sw_rup = 0;
        sw_rup = 1;
        ant_area = #nomcontc#12;
    |FINSI;
    |SI ant_area ! #nomcontc#12;
        |HAZ apuntes;
        ant_area = #nomcontc#12;
        nImpor = 0;
    |FINSI;

    |HAZ tmp0_1;

    aConte = #nomconts#nNumcam#;
    |QBF aConte;
    aConte = aConte + ant_area;
|FPRC;

|DEFBUCLE tmp1;          || por areas
    #nomcontc#3;
    ;
    nEmpresa, "   ",      1;
    nEmpresa, "zzzz", 99999;
    ;
    NULL, tmp1_6;
|FIN;

|PROCESO tmp0_5;
   aConte = #nomconts#nNumcam#;
   |HAZ apuntes;
|FPRC;

|PROCESO tmp0_1;   || ... Importe
   |SI nNumcam [ 18;
       nNume1 = nNumcam - 12;
   |SINO;
       |SI nNumcam = 19; nNume1 = 8; |FINSI;
       |SI nNumcam = 20; nNume1 = 7; |FINSI;
   |FINSI;
   nImpor = nImpor + #nomcontc#nNume1#;
|FPRC;

|DEFBUCLE tmp0;          || global
   #nomcontc#1;
   ;
   nEmpresa,     1;
   nEmpresa, 99999;
   ;
   NULL, tmp0_1, NULL, NULL, tmp0_5;
|FIN;

|PROCESO ApteCta;
    nOk    = 0;
    sw_rup = 0;
    nImpor = 0;
    nDesglo = nNumcam + 10;  || puntero desglose

    |SI #nomconts#nDesglo# = 0;  |BUCLE tmp0;                 |FINSI;
    |SI #nomconts#nDesglo# = 1;  |BUCLE tmp1;  |HAZ apuntes;  |FINSI;
    |SI #nomconts#nDesglo# = 2;  |BUCLE tmp2;  |HAZ apuntes;  |FINSI;
|FPRC;

|PROCESO ApteCtaSS;
    nOk     = 0;
    sw_rup  = 0;
    nCuadra = 0;
    nImpor  = nImporteTC1 - nImporteSST;
    nDesglo = nNumcam + 10;

    |SI #nomconts#31 = 0; |Y #nomconts#32 = 0;
        |SI nImpor < 0;
            nImpor =  -nImpor;
            |SI signe = "D";
                signe = "H";
            |SINO;
                signe = "D";
            |FINSI;
            |SI nNumcam = 22; nNumcam = 23; |FINSI;
        |FINSI;
    |FINSI;

    |SI #nomconts#nDesglo# = 0;
        aConte = #nomconts#nNumcam#;
        |HAZ apuntes;
    |FINSI;

    nImpor = 0;
    |SI #nomconts#nDesglo# = 1;
        |BUCLE sstmp1;
        nImpor = ((nImporteTC1 - nImporteSST) - nCuadra);
        |HAZ apuntes;
    |FINSI;
|FPRC;

|PROCESO PaseContabilidad;
    |SI nSw = 0;
        aAbre = aPathEmision + aFicsecu;
        |XABRE "BW", aAbre, fcanl;

        nAsiento = #nomconts#38;
        nAsiento = nAsiento - 1;
        nSw = 1;
    |FINSI;

    |SI #nomconts#34 = "S";
        nAsiento = nAsiento + 1;
        nApunte  = 0;
        nNumcam = 14;  signe = "D";  |HAZ ApteCta;   || total devengo
        nNumcam = 15;  signe = "H";  |HAZ ApteCta;   || retencion IRPF
        nNumcam = 16;  signe = "H";  |HAZ ApteCta;   || retencion S.S.
        nNumcam = 18;  signe = "H";  |HAZ ApteCta;   || anticipos 1
        nNumcam = 20;  signe = "H";  |HAZ ApteCta;   || anticipos 2
        nNumcam = 19;  signe = "H";  |HAZ ApteCta;   || en especie
        nNumcam = 17;  signe = "H";  |HAZ ApteCta;   || liquido
    |FINSI;

|| ----------------------------------------------------------------------
    nApunte = 0;

    |SI #nomconts#35 = "S"; |Y nImporteTC1 ! 0;
        nNumcam = 21; signe  = "D"; |HAZ ApteCtaSS;
        nNumcam = 22; signe  = "H"; |HAZ ApteCtaSS;
    |FINSI;

    |SI #nomcontb#16 ! 0;
        || ... ... |SI #nomconts#48 ] 50; |Y #nomconts#48 [ 59;
         |XCIERRA fcanl;
         |HAZ CopiaANombre;
         nSw = 0;
     |FINSI;

     |DELINDEX #nomcontc;
|FPRC;

____________________________________/ GRABACION DEL TEMPORAL

|PROCESO LeeLabtraba;
     |DDEFECTO #labtraba;
     #labtraba#0 = nEmpresa;
     #labtraba#1 = nTrabajador;
     |LEE 000#labtraba.=;

     aDNI=#labtraba#7;
     aNomTraba=#labtraba#2;
     nCentro=#labtraba#77;
     aArea=#labtraba#62;
     aSeccion=#labtraba#63;

     |DDEFECTO #labcentr;
     #labcentr#0 = nEmpresa;
     #labcentr#1 = nCentro;
     |LEE 000#labcentr.=;
     aCentro = #labcentr#10;

     nSwCentro = 0;
     |SI nCentro < nXDesdeCentro; |O nCentro > nXHastaCentro;
         nSwCentro = 1;
     |FINSI;
|FPRC;

|PROCESO GrabaNomconts;
    |DDEFECTO #nomcontc;
    #nomcontc#0 = nEmpresa;
    #nomcontc#1 = nTrabajador;
    |LEE 101#nomcontc.=;
    FEstado = FSalida;

    #nomcontc#2 = #nomcontc#2 + nDevengos;                      || total brutos
    #nomcontc#3 = #nomcontc#3 + nRetencionIRPF;                 || total reten. IRPF
    #nomcontc#4 = #nomcontc#4 + nSegSocial;                     || total aport. S.S.
    #nomcontc#5 = #nomcontc#5 + nLiquido;                       || liquido
    #nomcontc#6 = #nomcontc#6 + nAnticipo1;                     || total anticipos 1
    #nomcontc#7 = #nomcontc#7 + nEspecie;                       || total en especie
    #nomcontc#8 = #nomcontc#8 + nAnticipo2 + nOtrasDeducciones; || total anticipos 2
    #nomcontc#9 = #nomcontc#9 + nSSCargo;                       || ss empresa

    #nomcontc#11 = aDNI;
    #nomcontc#12 = nCentro;

    nImporteSST= nImporteSST + nSegSocial;

    |SI #nomconts#34 = "S";
        |SI FEstado = 0;    |GRABA 020#nomcontc.a;     |FINSI;
        |SI FEstado ! 0;    |GRABA 020#nomcontc.n;     |FINSI;
        nHayNom = 1;
    |FINSI;

    |LIBERA #nomcontc;
|FPRC;

|PROCESO ObtieneImportesM;
     || devengos

     nDevengos=#1#46+#1#16;

     || aportacion a la seguridad social
     nSegSocial=#1#48+#1#49+#1#50+#1#51+#1#164;
     nSSCargo= nSegSocial + #1#45 - (#1#57+#1#58);  || tc1 en nomina

     || retencion IRPF
     nRetencionIRPF=#1#75+#1#17;

     || especie
     nEspecie=#1#54;

     || otras deducciones / embargos
     nOtrasDeducciones=#1#104; || tique 668_757  29.08.2011

     || anticipos
     nAnticipos=#1#52+#1#53+#1#89;
     nAnticipo1=#1#52;
     nAnticipo2=#1#53+#1#89;

     || liquido a percibir
     nLiquido=nDevengos-nSegSocial-nRetencionIRPF-nEspecie-nOtrasDeducciones-nAnticipos;  || importe
|FPRC;

|PROCESO ObtieneImportesH;
     || devengos
     ||  nDevengos=#2#113+#2#18+#2#16;

     nDevengos=#2#46+#2#16;

     || aportacion a la seguridad social
     nSegSocial=#2#48+#2#49+#2#50+#2#51+#2#206;
     nSSCargo=nSegSocial+#2#45-(#2#57+#2#58);  || tc1 en nomina

     || retencion IRPF
     nRetencionIRPF=#2#114+#2#17;

     || especie
     nEspecie=#2#54;

     || otras deducciones
     nOtrasDeducciones=#2#146;

     || anticipos
     nAnticipos=#2#52+#2#53+#2#131;
     nAnticipo1=#2#52;
     nAnticipo2=#2#53+#2#131;

     || liquido a percibir
     nLiquido=nDevengos-nSegSocial-nRetencionIRPF-nEspecie-nOtrasDeducciones-nAnticipos;  || importe

|FPRC;

|PROCESO ObtieneImportesA;
     || devengos
     nDevengos=#3#74+#3#18+#3#16;

     || aportacion a la seguridad social
     nSegSocial=#3#48+#3#49+#3#50+#3#51;
     nSSCargo=nSegSocial+#3#45-(#3#57+#3#58);  || tc1 en nomina

     || retencion IRPF
     nRetencionIRPF=#3#75+#3#17;

     || especie
     nEspecie=#3#54;

     || otras deducciones
     nOtrasDeducciones=0;

     || anticipos
     nAnticipos=#3#52+#3#53+#3#89;
     nAnticipo1=#3#52;
     nAnticipo2=#3#53+#3#89;

     || liquido a percibir
     nLiquido=nDevengos-nSegSocial-nRetencionIRPF-nEspecie-nOtrasDeducciones-nAnticipos;  || importe

|FPRC;

|PROCESO PCalculo;
     |SI #nomconts#39="M";
         nTrabajador=#1#1;
         nMes=#1#2;
         nTipo=#1#3;
     |FINSI;

     |SI #nomconts#39="H";|O #nomconts#39="T";
          nTrabajador=#2#1;
          nMes=#2#2;
          nTipo=#2#3;
     |FINSI;

     |SI #nomconts#39="A";
          nTrabajador=#3#1;
          nMes=#3#2;
          nTipo=#3#3;
     |FINSI;

     |HAZ LeeLabtraba;
     |SI nSwCentro = 1; |ACABA; |FINSI;

     |SI #nomconts#39="M";
         |HAZ ObtieneImportesM;
     |FINSI;

     |SI #nomconts#39="H";|O #nomconts#39="T";
          |HAZ ObtieneImportesH;
     |FINSI;

     |SI #nomconts#39="A";
          |HAZ ObtieneImportesA;
     |FINSI;

     |HAZ GrabaNomconts;
|FPRC;

|DEFBUCLE BCalculoM;
     #1#1;
     ;
     nEmpresa,nXDesdeTrabajador,nXDesdeMes,0;
     nEmpresa,nXHastaTrabajador,nXHastaMes,4;
     ;
     NULL,PCalculo;
|FIN;

|DEFBUCLE BCalculoH;
     #2#1;
     ;
     nEmpresa,nXDesdeTrabajador,nXAnyo,nXDesdeMes,0;
     nEmpresa,nXHastaTrabajador,nXAnyo,nXHastaMes,4;
     ;
     NULL,PCalculo;
|FIN;

|DEFBUCLE BCalculoA;
     #3#1;
     ;
     nEmpresa,nXDesdeTrabajador,nXDesdeMes,5;
     nEmpresa,nXHastaTrabajador,nXHastaMes,9;
     ;
     NULL,PCalculo;
|FIN;

|DEFBUCLE BCalculoT;
     #2#1;
     ;
     nEmpresa,nXDesdeTrabajador,nXAnyo,nXDesdeMes,#nomconts#41;
     nEmpresa,nXHastaTrabajador,nXAnyo,nXHastaMes,#nomconts#41;
     ;
     NULL,PCalculo;
|FIN;

|PROCESO conta1;

    nEmpresa = #labempre#0;
    nImporteTC1=0;
    nImporteSST=0;

    nHayNom = 0;
    nHayTC1 = 0;
    |ABRE #nomcontc;

    |SI #nomconts#39="M";   || seleccion mensual
        |BUCLE BCalculoM;
    |FINSI;

    |SI #nomconts#39="H";   || seleccion historico
        |BUCLE BCalculoH;
    |FINSI;

    |SI #nomconts#39="A";   || seleccion atrasos
        |BUCLE BCalculoA;
    |FINSI;

    |SI #nomconts#39="T";   || seleccion atrasos historico
        |BUCLE BCalculoT;
    |FINSI;

    |SI #nomconts#35 = "S";
        |HAZ PaseTC1;
    |FINSI;
    |CIERRA #nomcontc;

    |SI nHayNom = 1; |O nHayTC1 = 1;
        |HAZ PaseContabilidad;
    |FINSI;
|FPRC;

|DEFBUCLE labempre;
   #labempre#1;
   ;
   #nomconts#12;
   #nomconts#13;
   ;
   NULL, conta1;
|FIN;

|PROCESO tipo3; |TIPO 3;

    |ABRE #nomcontb;
    #nomcontb#0 = #nomconts#48;
    |LEE 020#nomcontb.=;
    |SI FSalida ! 0;
        |MENAV "0000Error en fichero de Control de Formatos";
        |CIERRA #nomcontb;
        |ACABA;
    |FINSI;
    |CIERRA #nomcontb;
    sep_campo = #nomcontb#12; |QBF sep_campo;

    nLocal = 0;
    aIP    = "";
    |IP_REMOTO aIP; |QBF aIP;
    |SI aIP = "";
        nLocal = 1;
    |FINSI;

    aFichero = Usuario % 108;
    |NOME_DAT #nomcontc#aFichero#;
    |DELINDEX #nomcontc;

    aAlfa1 = Usuario % 108;
    |DBASE aPathEmision;
    aPathEmision = aPathEmision + "tmp";        |MKDIR aPathEmision;
    aPathEmision = aPathEmision + "/" + aAlfa1; |MKDIR aPathEmision;
    aPathEmision = aPathEmision + "/";

    aFicsecu = aAlfa1 + ".txt";
    aOrigen  = aPathEmision + aFicsecu;

    nXDesdeMes=#nomconts#42;
    nXHastaMes=#nomconts#42;
    |SI #nomconts#39 = "A"; |O #nomconts#39 = "T";
         nXHastaMes = #nomconts#43;
    |FINSI;
    nXAnyo=#nomconts#40;

    |HAZ ControlTc1;

    nSw = 0;
    |ABRE #labtraba;
    |ABRE #labcentr;
    |ABRE #nomauxi1;
    |ABRE #nomcontd
    |SI #nomconts#1 ! 0;
        nXDesdeTrabajador=1;      nXDesdeCentro=0;
        nXHastaTrabajador=99999;  nXHastaCentro=99;
        |ABRE #labempre;
        |PARA nPara1 = 1; |SI nPara1 [ 11; |HACIENDO nPara1 = nPara1 + 1;
              |SI #nomconts#nPara1# ! 0;
                  #labempre#0 = #nomconts#nPara1#;
                  |LEE 041#labempre.=;
                  |SI FSalida = 0;
                      |HAZ conta1;
                  |FINSI;
              |FINSI;
        |SIGUE;
        |CIERRA #labempre;

    |SINO;
        nXDesdeTrabajador=#nomconts#44;  nXDesdeCentro=#nomconts#46;
        nXHastaTrabajador=#nomconts#45;  nXHastaCentro=#nomconts#47;

        |BUCLE labempre;
    |FINSI;

    |CIERRA #nomcontd
    |CIERRA #nomauxi1;
    |CIERRA #labcentr;
    |CIERRA #labtraba;

    |SI #nomcontb#16 = 0;
         |SI nSw = 1;
             |XCIERRA fcanl;
             |HAZ CopiaANombre;
         |FINSI;
     |FINSI;
     |SI nNumFicheros ! 0;
         |SI #nomcontb#16 = 0;
             aAlfa1 = "0000Copiado el Fichero ";
         |SINO;
             aAlfa1 = "0000Copiados los Ficheros en ";
             |SI nNumFicheros = 1;
                 aAlfa1 = "0000Copiado el Fichero en ";
             |FINSI;
         |FINSI;
         aAlfa1 = aAlfa1 + #nomcontb#11; |QBF aAlfa1;
         |MENAV aAlfa1;
     |SINO;
         |MENAV "0000Seleccion vacia";
     |FINSI;
|FPRC;

|PROCESO Igualdad;
   |DDEFECTO #nomconts;
   |PARA nPara1 = 1; |SI nPara1 [ 18; |HACIENDO nPara1 = nPara1 + 1;
         #nomconts#nPara1# = #nomconta#nPara1#;
   |SIGUE;
   #nomconts#19 = #nomconta#43;
   #nomconts#20 = #nomconta#19;
   #nomconts#21 = #nomconta#20;
   #nomconts#22 = #nomconta#21;
   #nomconts#24 = #nomconta#36;
   #nomconts#25 = #nomconta#37;
   #nomconts#26 = #nomconta#38;
   #nomconts#27 = #nomconta#39;
   #nomconts#28 = #nomconta#40;
   #nomconts#29 = #nomconta#41;
   #nomconts#30 = #nomconta#42;
   #nomconts#31 = #nomconta#45;
   #nomconts#32 = #nomconta#46;

   #nomconts#36 = #nomconta#31;
   #nomconts#37 = #nomconta#33;
   #nomconts#38 = #nomconta#35;
   #nomconts#39 = "M";
   #nomconts#42 = #nomconta#32;
   #nomconts#43 = #nomconta#32;
   #nomconts#48 = #nomconta#34;
|FPRC;


|PROCESO Contb_2019;
   |SI nOpera = 0;
       nSw = 1;
   |FINSI;

   |SI nOpera = 1;
       #nomcontb#16 = 1;
       |GRABA 020#nomcontb.a;
   |FINSI;

   |LIBERA #nomcontb;
|FPRC;

|DEFBUCLE Contb_2019;
   #nomcontb#1;
   16;
   nDesde1, nDesde2;
   nHasta1, nHasta2;
   be;
   NULL, Contb_2019;
|FIN;

|PROCESO CambioContb;
   nSw    = 0;
   nDesde1 = 01; nDesde2 = 1;
   nHasta1 = 99; nHasta2 = 2;
   nOpera = 0; |BUCLE Contb_2019;
   |SI nSw = 0;
       nDesde1 = 50; nDesde2 = 0;
       nHasta1 = 59; nHasta2 = 0;
       nOpera = 1; |BUCLE Contb_2019;
   |FINSI;
|FPRC;

|PROGRAMA;
   |CLS;
   |CABEZA "Exportar asiento Contable";
   |DDEFECTO #nomconts;

   |PINPA #0#nomconts;
   |ABRE #nomconts;
   |LEE 101#nomconts.p;
   |SI FSalida ! 0;
       |ABRE #nomconta;
       |LEE 041#nomconta.p;
       |SI FSalida = 0;
           |HAZ Igualdad;
       |FINSI;
       |CIERRA #nomconta;

       |GRABA 020#nomconts.b;
   |FINSI;

   |SI #nomconts#51 = " ";
       |HAZ CambioContb;
   |FINSI;

   |PINDA #0#nomconts;
   |ENDATOS #1#nomconts;
   |SI FSalida = 0;
       |HAZ tipo3;
       #nomconts#51 = "x";
       |GRABA 020#nomconts.a;
   |FINSI;
   |LIBERA #nomconts;
   |CIERRA #nomconts;
|FPRO;
