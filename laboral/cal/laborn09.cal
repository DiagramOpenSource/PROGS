|FICHEROS;
     laborn09;           || este def el del lineal
     nomvarca;
     nomvarli;
     nomcontr;

     labtraba;
     nompmail;
     labornx9;

     :integral/dsam0017;
     :integral/dsam0018;
     nomembca;
     nomembli;
|FIN;

|VARIABLES;
     &eaDirDest  = "";
     &eaAsunto   = "";
     &eaTexto    = "";
     &eaFichero  = "";

     &aParam = "";
     &eaUsuario = "";
     &eaFecha = "";
     &efFecha = @;
     &eaHora = "";
     aTipo = "";
     sw = 0;
     aAlfa  = "";
     aAlfa1 = "";
     aAlfa2 = "";
     FEstado = 0;
     nMes = 0;
     aUsuarioD = "";
     aUsuarioH = "";
     &nEmpExt = 0;
     &nTraExt = 0;
     &eaVengoDe = "laborn09";
     &eaEmpresa = "";
     &nEmpAUTO = 0;
     &nTraAUTO = 0;
     &nMesAUTO = 0;
     &nAnyAUTO = 0;
     &nTipAUTO = 0;
     &aFechaBaja = "";
     &fFechaBaja = @;
     &nCausaBaja = 0;
     &aCausaBaja = "";
     &aTipoContr = "";
     nNume = 0;
     aSiEsX = "";

     &eaParam       = "";
     &enFSalida     = 0;
     &enBorrado     = 0;
     &eaEstado      = "";
     &eaMotiv       = "";
     &enActualizado = 0;

     &enEmpresa = 0;
     &enTrabajador = 0;
     &enDiasVac = 0;
     nCampo = 0;
     nRegistro = 0;
     swEmbargo = 0;
     fFecha = @;
     nAny = 0;
|FIN;

|PROCESO GrabaEmbargo;
     #nomembca#0 = #laborn09#5;
     #nomembca#1 = #laborn09#6;
     #nomembca#2 = #laborn09#8;
     #nomembca#3 = #laborn09#9;
     #nomembca#4 = #laborn09#13;
     |SI #nomembca#4 = 3;
          #nomembca#4 = 5;
     |FINSI;
     #nomembca#5 = #laborn09#26;
     #nomembca#6 = #nomembca#6 + #laborn09#14;
     #nomembca#8 = nRegistro;
     #nomembca#9 = #laborn09#27;
     #nomembca#10 = #laborn09#28;

     aAlfa = #nomembca#9;
     |QBF aAlfa;
     |SI aAlfa = "";
          #nomembca#9 = "S";
          #nomembca#10 = "T";
     |FINSI;

     |SI swEmbargo = 0;
          |GRABA 020#nomembca.n;
     |SINO;
          |GRABA 020#nomembca.a;
     |FINSI;
     |LIBERA #nomembca;

     fFecha = ~;
     aAlfa = fFecha;
     aAlfa = aAlfa % -104;
     nAny = aAlfa;

     |DDEFECTO #nomembli;
     #nomembli#0 = #laborn09#5;
     #nomembli#1 = #laborn09#6;
     #nomembli#2 = nAny;
     |LEE 101#nomembli.=
     |SI FSalida ! 0;
          #nomembli#27 = #laborn09#14;
          #nomembli#28 = 0;
          #nomembli#29 = #laborn09#14;
          |GRABA 020#nomembli.n;
     |SINO;
          #nomembli#27 = #nomembli#27 + #laborn09#14;
          #nomembli#29 = #nomembli#29 + #laborn09#14;
          |GRABA 020#nomembli.a;
     |FINSI;
     |LIBERA #nomembli;
|FPRC;

|PROCESO Actualizacion;
     || grabacion del embargo en sus tablas correspondientes

     |ABRE #dsam0017;
     |ABRE #dsam0018;
     |ABRE #nomembca;
     |ABRE #nomembli;

     #dsam0017#0 = #laborn09#5;
     #dsam0017#1 = #laborn09#7;
     #dsam0017#2 = 99;
     |LEE 101#dsam0017.];
     |SI FSalida ! 0;
          |LEE 101#dsam0017.a;
          |SI FSalida = 0; |Y #dsam0017#0 = #laborn09#5; |Y #dsam0017#1 = #laborn09#7;
               nRegistro = #dsam0017#2 + 1;
          |SINO;
               nRegistro = 1;
          |FINSI;
     |SINO;
          |SI #dsam0017#0 = #laborn09#5; |Y #dsam0017#1 = #laborn09#7;
               nRegistro = #dsam0017#2 + 1;
          |SINO;
               |LEE 101#dsam0017.a;
               |SI FSalida = 0; |Y #dsam0017#0 = #laborn09#5; |Y #dsam0017#1 = #laborn09#7;
                    nRegistro = #dsam0017#2 + 1;
               |SINO;
                    nRegistro = 1;
               |FINSI;
          |FINSI;
     |FINSI;
     |DDEFECTO #dsam0017;

     #dsam0017#0 = #laborn09#5;
     #dsam0017#1 = #laborn09#7;
     #dsam0017#2 = nRegistro;

     #dsam0017#3 = #laborn09#10;
     #dsam0017#5 = #laborn09#11;
     #dsam0017#6 = #laborn09#12;
     #dsam0017#13 = "S";
     #dsam0017#7 = #laborn09#13;
     |SI #dsam0017#7 = 3;
          #dsam0017#7 = 5;
     |FINSI;
     |SI #laborn09#13 = 1; |O #laborn09#13 = 2;
          #dsam0017#11 = #laborn09#26;
     |SINO;
          #dsam0017#11 = 0;
     |FINSI;
     #dsam0017#8 = #laborn09#14;
     #dsam0017#10 = #laborn09#14;
     #dsam0017#14 = #laborn09#27;
     #dsam0017#15 = #laborn09#28;
     |GRABA 020#dsam0017.n;
     |LIBERA #dsam0017;

     swEmbargo = 0;
     |DDEFECTO #nomembca;
     #nomembca#0 = #laborn09#5;
     #nomembca#1 = #laborn09#6;
     |LEE 101#nomembca.=;
     |SI FSalida ! 0;
          swEmbargo = 0;
     |SINO;
          swEmbargo = 1;
     |FINSI;
     |HAZ GrabaEmbargo;

     |CIERRA #nomembli;
     |CIERRA #nomembca;
     |CIERRA #dsam0018;
     |CIERRA #dsam0017;
|FPRC;

|PROCESO Email;
     || Emite correo electronico al usuario de que la baja del trabajador
     ||    se ha hecho efectiva
     aAlfa = ("00000" + #laborn09#6) % -105;
     eaTexto = &13 + &10 + "El administrador ha hecho efectivo el embargo del trabajador (" + aAlfa + ") " + #laborn09#9; |QBF eaTexto;
     aAlfa = ("00000" + #laborn09#5) % -106;
     eaTexto = eaTexto + " de la empresa (" + aAlfa + ") " + #laborn09#8; |QBF eaTexto;

     eaTexto = eaTexto + &13 + &10 + &13 + &10;
     eaTexto = eaTexto + " Actualizacion con fecha " + #laborn09#3 + " a las " + #laborn09#4 + &13 + &10;

     aAlfa  = ("00000" + #laborn09#5) % -106;
     aAlfa1 = ("00000" + #laborn09#6) % -105;
     eaAsunto  = "Efectivo embargo trabajador (" + aAlfa + "/" + aAlfa1 + ") " + #laborn09#9; |QBF eaAsunto;
     eaFichero = "";
     eaDirDest = "";

     aAlfa = #laborn09#5; |QBF aAlfa;
     eaEmpresa = ("00000" + aAlfa) % -105;
     |HAZ ConsEmail2;
|FPRC;

|PROCESO Actualiza;
     aAlfa = "    ATENCION: se procederá a la actualización de la ";
     aAlfa = aAlfa + "solicitud de embargo seleccionada";
     |MENAV aAlfa;
     aAlfa = "    S¿Desea continuar?";
     |CONFI aAlfa;
     |SI FSalida = 0;
          |HAZ Actualizacion;

          || primero me paso todos los campos del registro que voy a borrar
          || a un temporal, ya que ha habido varios fallos en los que al
          || borrar el registro pierde la clave, asi me aseguro

          |DDEFECTO #labornx9;
          |PARA nCampo = 0; |SI nCampo [ 28; |HACIENDO nCampo = nCampo + 1;
               #labornx9#nCampo# = #laborn09#nCampo#;
          |SIGUE;

          |BORRA 020#laborn09.a;

          |PARA nCampo = 0; |SI nCampo [ 28; |HACIENDO nCampo = nCampo + 1;
               #laborn09#nCampo# = #labornx9#nCampo#;
          |SIGUE;

          #laborn09#0 = "A";
          #laborn09#18 = Usuario % 0810;
          #laborn09#19 = ~;
          |HORA aAlfa;
          #laborn09#20 = aAlfa;
          |GRABA 020#laborn09.n;

          enEmpresa = #labtraba#0;
          enTrabajador = #labtraba#1;
          ||HAZ ActualizaLabornet;
          || en principio esto no

          enActualizado = 1;

          |ABRE #nompmail; |LEE 000#nompmail.p; |CIERRA #nompmail;
          |SI #nompmail#11 = "S";
               |HAZ Email;
          |FINSI;

          aAlfa = "    La solicitud de Embargo ha sido actualizada ";
          aAlfa = aAlfa + "con éxito.";
          |MENAV aAlfa;
     |FINSI;
     |PONCONTROL 23, "SI";
     |PTEC 808;
|FPRC;

|PROCESO Consulta;
     eaUsuario = #laborn09#1;      || permito modificar
     eaFecha = #laborn09#3;
     eaHora = #laborn09#4;
     |SUB_EJECUTA "laborn19;CONSUL";
     |ACABA;
|FPRC;

|PROCESO Modificacion;
     eaUsuario = #laborn09#1;      || permito modificar
     eaFecha = #laborn09#3;
     eaHora = #laborn09#4;
     |SUB_EJECUTA "laborn19;MODIF";
     |ACABA;
|FPRC;

|PROCESO Opciones; |TIPO 0;
     |SI enFSalida ! 0;
          FSalida = enFSalida;
     |FINSI;

     || consulta de datos introducidos
     |SI FSalida = 10;
          |SI aParam = "USUARIO"; |O aParam = "ADMIN";  || consulta el usuario los datos que
               |HAZ Modificacion;
          |FINSI;
          |SI aParam = "HISTO"; |O aParam = "HISTOUSU";
               |HAZ Consulta;           || consulta de datos introducidos
          |FINSI;                       || o ya actualizados, solo visualizar
          |ACABA;
     |FINSI;

     |SI FSalida = 11;  |Y #laborn09#0 = "P"; |Y aParam = "ADMIN";
          |HAZ Modificacion;
          |LEE 000#laborn09.=;
          |HAZ Actualiza;
          |ACABA;
     |FINSI;

     |SI FSalida = 12;  |Y #laborn09#0 = "P";
          enBorrado = 1;

          |LEE 101#laborn09.=;
          |BORRA 020#laborn09.a;
          |LIBERA #laborn09;

          #laborn09#0  = "R";
          #laborn09#22 = eaMotiv;

          |GRABA 020#laborn09.n;
          |LIBERA #laborn09;
     |FINSI;
|FPRC;

|PROCESO RecogeCampos;
     #laborn09#1 = eaUsuario;
     #laborn09#3 = efFecha;
     #laborn09#4 = eaHora;
     #laborn09#0 = eaEstado;
|FPRC;

|PROGRAMA;
     |SI enFSalida ! 12;
          |CLS;
     |FINSI;
     |CABEZA "Embargos";

     aAlfa = Usuario % 0810;

     |ABRE #laborn09;
     |SI aParam = "ADMIN"; |O aParam = "USUARIO";
          aTipo = "P";   || los pendientes de actualizar
          |HAZ RecogeCampos;
          |HAZ Opciones;
     |FINSI;
     |SI aParam = "HISTO"; |O aParam = "HISTOUSU";
          aTipo = "A";   || consulta del historico, los ya actualizados
          |HAZ RecogeCampos;
          |LEE 000#laborn09.=;
          |HAZ Opciones;
     |FINSI;
     |CIERRA #laborn09;
|FPRO;

|PROCESO Tipo80n08;  |TIPO 80;
     |SI PARAMETRO = "";  |ACABA;  |FINSI;

     aSiEsX = PARAMETRO % 101;
     |SI aSiEsX = "X";
         aAlfa     = PARAMETRO % 202;   enFSalida = aAlfa;
         eaEstado  = PARAMETRO % 401;
         eaUsuario = PARAMETRO % 510;
         aAlfa     = PARAMETRO % 1510;  efFecha = aAlfa;
         eaHora    = PARAMETRO % 2508;
         eaParam   = PARAMETRO % 3310;  |QBF eaParam;
         aParam    = eaParam;
     |SINO;
          aParam = PARAMETRO;  |QBF aParam;
          |SI eaParam ! "";
              eaParam = aParam;
          |FINSI;
     |FINSI;
|FPRC;
