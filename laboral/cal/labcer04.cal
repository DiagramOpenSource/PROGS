|FICHEROS;
     labcer04 #0;

     labcer01 #1;
     labcer02 #2;
     labcer03 #3;
     labcer05 #5,MANTE;
     labcer06;

     labempre;
     labcentr;
     labtraba;
     labcontr;

     gomccchi;
|FIN;

|VARIABLES;
     aParam = "";
     nEmpresa = 0;
     nCampanya = 0;
     aMarca = "";
     aHora = "";
     aFecha = "";
     fFecha = @;
     aEmpresa = "";

     aAlfa       = "";
     aAlfa1      = "";
     aAlfa2      = "";
     aApel1      = "";
     aApel2      = "";
     aNom        = "";
     aNume       = "";
     aLong       = "";
     aCampo      = "";
     nInd = 0;
     nLong = 0;
     nCalc = 0;
     nHayComa = 0;
     nPos = 0;
     SwSalir = 0;

     nNivel = 0;
     nHandle = 0;
     aRuta = "";
     aFichero = "";

     swHayMov = 1;
     nTrabajador = 0;
     aDesdeSel = "";
     aHastaSel = "";
     aRegimen = "";

     nNumTra = 0;
     nNumMov = 0;

     aFechaIniPer = "";
     aFechaFinPer = "";
     aInfComSit = "";
     aRepre = "";
     aNifRepre = "";
     nPeriodo = 0;
     aCCC = "";
     &eaNom = "";
     &eaApel1 = "";
     &eaApel2 = "";
     &aCadena = "";
|FIN;

|PROCESO Fichero;
     nEmpresa = #0#2;
     nCampanya = #0#3;

     aEmpresa = nEmpresa;
     aEmpresa = ("00000" + aEmpresa) % -105;

     |HORA aHora;
     fFecha = ~;
     aFecha = fFecha;

     aHora = aHora - ":" - ":";
     aFecha = aFecha - "." - ".";
     aFichero = aEmpresa + "_" + aFecha + "_" + aHora + "_cer.xml"

     #0#1 = aFichero;
     |PINTA #0#1;
|FPRC;

|PROCESO Ayuda; |TIPO 7;
     |MENSA "    Pulse <F5> para marcar/desmarcar un trabajador o <F6> marcar/desmarcar todos";
|FPRC;

|PROCESO inferior;
     #5#0 = nEmpresa;
     #5#1 = nCampanya;
     #5#5 = 00001;
     #5#7 = 0001;
     #5#12 = "N";
|FPRC;

|PROCESO superior;
     #5#0 = nEmpresa;
     #5#1 = nCampanya;
     #5#5 = 99999;
     #5#7 = 9999;
     #5#12 = "N";
|FPRC;

|PROCESO marcatodos;
     #5#15 = aMarca;
     |GRABA 020#5a;
|FPRC;

|DEFBUCLE marcatodos;
     #5#2;
     ;
     nEmpresa, nCampanya, 00001, 0000, "N";
     nEmpresa, nCampanya, 99999, 9999, "N";
     ;
     NULL, marcatodos;
|FIN;

|PROCESO marca;
     |SI FSalida = 13;
          |SI #5#15 = "*";
               #5#15 = " ";
          |SINO;
               #5#15 = "*";
          |FINSI;
          |GRABA 020#5a;
     |FINSI;
     |SI FSalida = 14;
          |SI aMarca = " ";
               aMarca = "*";
          |SINO;
               aMarca = " ";
          |FINSI;
          |BUCLE marcatodos;
          |PONCONTROL 23, "si";
     |FINSI;
|FPRC;

|PROCESO Seleccion;
     |SI #0#0 = 2;
          |PUSHV 05012380;
          |ENTLINEAL #2#5, -3, 4, 20, 1, inferior, superior;
          |POPV;
     |FINSI;
|FPRC;

|PROCESO LeeEmpresa;
     |ABRE #labempre;
     #labempre#0 = nEmpresa;
     |LEE 000#labempre.=;
     |CIERRA #labempre;
|FPRC;

|PROCESO LeeTrab;
     |ABRE #labtraba;
     #labtraba#0 = nEmpresa;
     #labtraba#1 = nTrabajador;
     |LEE 000#labtraba.=;
     |CIERRA #labtraba;
|FPRC;

|PROCESO Linea;
     |QBF aAlfa;
     aCadena = aAlfa;
     |HAZ QuitaRaros;
     |SI nNivel = 1;
          aCadena = aCadena;
     |FINSI;
     |SI nNivel = 2;
          || aCadena = &09 + aCadena;
          aCadena = "  " + aCadena;
     |FINSI;
     |SI nNivel = 3;
          aCadena = "  " + "  " + aCadena;
     |FINSI;
     |SI nNivel = 4;
          aCadena = "  " + "  " + "  " + aCadena;
     |FINSI;
     |SI nNivel = 5;
          aCadena = "  " + "  " + "  " + "  " + aCadena;
     |FINSI;
     |SI nNivel = 6;
          aCadena = "  " + "  " + "  " + "  " + "  " + aCadena;
     |FINSI;
     aCadena = aCadena + &13 + &10;
     aAlfa = aCadena;
     |XGRABA nHandle, aAlfa;
|FPRC;

|PROCESO ActualizaReg;
     #3#12 = "S";
     #3#13 = ~;
     #3#14 = aFichero;
     #3#15 = " ";
     |GRABA 020#3a;
|FPRC;

|PROCESO Intervalos;
     nNivel = 5;
     aAlfa = "<Intervalo_Actividad>";
     |HAZ Linea;

     nNumMov = nNumMov + 1;

     nNivel = 6;
     aAlfa = #3#8;
     aAlfa = (aAlfa % -104) + (aAlfa % 402) + (aAlfa % 102);
     aAlfa = "<Fecha_Inicio>" + aAlfa + "</Fecha_Inicio>";
     |HAZ Linea;

     nNivel = 6;
     aAlfa = #3#9;
     aAlfa = (aAlfa % -104) + (aAlfa % 402) + (aAlfa % 102);
     aAlfa = "<Fecha_Fin>" + aAlfa + "</Fecha_Fin>";
     |HAZ Linea;

     nNivel = 6;
     aAlfa = #3#10; aAlfa = ("00" + aAlfa) % -102;
     aAlfa = "<Codigo_Actividad>" + aAlfa + "</Codigo_Actividad>";
     |HAZ Linea;

     |HAZ ActualizaReg;

     nNivel = 5;
     aAlfa = "</Intervalo_Actividad>";
     |HAZ Linea;
|FPRC;

|DEFBUCLE Intervalos;
     #3#2;
     15;
     nEmpresa, nCampanya, #2#5, 0001, "N", aDesdeSel;
     nEmpresa, nCampanya, #2#5, 9999, "N", aHastaSel;
     ;
     NULL, Intervalos;
|FIN;

|PROCESO Periodo;
     nNivel = 4;
     aAlfa = "<Periodo_Actividad>";
     |HAZ Linea;

     || de la linea 0000 que he leido antes

     nNivel = 5;
     aAlfa = #3#8;
     aAlfa = (aAlfa % -104) + (aAlfa % 402) + (aAlfa % 102);
     aFechaIniPer = aAlfa;
     aAlfa = "<Fecha_Inicio_Periodo>" + aAlfa + "</Fecha_Inicio_Periodo>";
     |HAZ Linea;

     nNivel = 5;
     aAlfa = #3#9;
     aAlfa = (aAlfa % -104) + (aAlfa % 402) + (aAlfa % 102);
     aFechaFinPer = aAlfa;
     aAlfa = "<Fecha_Fin_Periodo>" + aAlfa + "</Fecha_Fin_Periodo>";
     |HAZ Linea;

     aAlfa = #3#17;
     aAlfa = ("00" + aAlfa) % -102;
     aInfComSit = aAlfa;

     |BUCLE Intervalos;

     nNivel = 4;
     aAlfa = "</Periodo_Actividad>";
     |HAZ Linea;
|FPRC;

|PROCESO Inf_Com_Sit;
     nNivel = 4;
     aAlfa = "<Informacion_Complementaria_Situacion>";
     |HAZ Linea;

     nNivel = 5;
     |SI aInfComSit = "03"; |O aInfComSit = "04";
          aAlfa = "<Fecha>" + aFechaIniPer + "</Fecha>";
     |SINO;
          aAlfa = "<Fecha>" + aFechaFinPer + "</Fecha>";
     |FINSI;
     |HAZ Linea;

     nNivel = 5;
     aAlfa = "<Codigo>" + aInfComSit + "</Codigo>";
     |HAZ Linea;

     nNivel = 4;
     aAlfa = "</Informacion_Complementaria_Situacion>";
     |HAZ Linea;
|FPRC;

|PROCESO Datos_Actividad;
     nNivel = 3;
     aAlfa = "<Datos_Actividad>";
     |HAZ Linea;


     #3#0 = nEmpresa;
     #3#1 = nCampanya;
     #3#5 = nTrabajador;
     #3#7 = 0000;
     |LEE 000#3=;
     |SI FSalida = 0;
          |SI #labtraba#42 = "C"; |O #labtraba#42 = "F";
               nNivel = 4;
               aAlfa = #3#16; aAlfa = ("00" + aAlfa) % -102;
               aAlfa = "<Coeficiente_Actividad>" + aAlfa + "</Coeficiente_Actividad>";
               |HAZ Linea;
          |FINSI;

          |HAZ Periodo;

          |HAZ Inf_Com_Sit;

          nNivel = 3;
          aAlfa = "</Datos_Actividad>";
          |HAZ Linea;
     |SINO;
          || error: no tengo fecha inicio-fin de intervalo (linea 0000)
     |FINSI;
|FPRC;


|PROCESO CuentaMov;
     swHayMov = 1;
     |ERROR10;
|FPRC;

|DEFBUCLE CuentaMov;
     #3#2;
     15;
     nEmpresa, nCampanya, #2#5, 0000, "N", aDesdeSel;
     nEmpresa, nCampanya, #2#5, 9999, "N", aHastaSel;
     ;
     NULL, CuentaMov;
|FIN;

|PROCESO Trabajador;
     swHayMov = 0;
     nTrabajador = #2#5;
     |SI #0#0 = 1;
          aDesdeSel = " ";
          aHastaSel = " ";
     |SINO;
          aDesdeSel = "*";
          aHastaSel = "*";
     |FINSI;
     |BUCLE CuentaMov;   || comprueba si hay movimientos por trabajador
     |SI swHayMov = 1;
          nNumTra = nNumTra + 1;

          nNivel = 2;
          aAlfa = "<Datos_Trabajador>";
          |HAZ Linea;

          |HAZ LeeTrab;
          nNivel = 3;
          aAlfa = #labtraba#7; |QBF aAlfa;
          aAlfa = "<NIF_NIE>" + aAlfa + "</NIF_NIE>";
          |HAZ Linea;

          |HAZ Datos_Actividad;

          nNivel = 2;
          aAlfa = "</Datos_Trabajador>";
          |HAZ Linea;
     |FINSI;
|FPRC;

|DEFBUCLE Trabajador;
     #2#1;
     ;
     nEmpresa, nCampanya, 00001;
     nEmpresa, nCampanya, 99999;
     ;
     NULL, Trabajador;
|FIN;

|PROCESO BuscaRegimen;
     |ABRE #labtraba;
     #labtraba#0 = nEmpresa;
     #labtraba#1 = #labcer02#5;
     |LEE 000#labtraba.=;

     aRegimen = #labtraba#247;
     |QBF aRegimen;
     aRegimen = ("0000" + aRegimen) % -104
     |CIERRA #labtraba;

     #1#0 = nEmpresa;
     #1#1 = nCampanya;
     |LEE 000#1=;
     aAlfa = #1#4;
     aAlfa = aAlfa % -104;
     nPeriodo = aAlfa;
     |SI nPeriodo ] 2012;
          |SI aRegimen = "0613";
               aRegimen = "0163";
          |FINSI;
     |SINO;
          |SI aRegimen = "0163";
               aRegimen = "0613";
          |FINSI;
     |FINSI;
     |ERROR10;
|FPRC;

|DEFBUCLE BuscaRegimen;
     #2#1;
     ;
     nEmpresa, nCampanya, 00001;
     nEmpresa, nCampanya, 99999;
     ;
     NULL, BuscaRegimen;
|FIN;

|PROCESO LeeRepre;
     aRepre = "";
     aNifRepre = "";

     |ABRE #labcer06;
     |LEE 000#labcer06.p;
     |SI FSalida = 0;
          aRepre = #labcer06#1;
          |QBF aRepre;
          |SI aRepre ! "";
               aRepre = #labcer06#1;
               aNifRepre = #labcer06#2;
          |FINSI;
     |FINSI;
     |CIERRA #labcer06;

     |SI aRepre = "";
          aRepre = #labempre#15;
          aNifRepre = #labempre#17;
     |FINSI;
|FPRC;

|PROCESO BuscaCCCEmp;
     |ABRE #gomccchi;
     #gomccchi#0 = #labempre#0;
     #gomccchi#1 = 00;
     |LEE 000#gomccchi.=;
     |SI FSalida = 0;
          |SI nPeriodo [ 2011;
               aCCC = #gomccchi#2;
          |FINSI;
     |FINSI;
     |CIERRA #gomccchi;
|FPRC;

|PROCESO ElProceso;
     |ABRE #1;
     |ABRE #2;
     |ABRE #3;
     |ABRE #labcontr;
     |ABRE #labcentr;
     |ABRE #gomccchi;
     |LEE 000#labcontr.p;
     |CIERRA #labcontr;

     |HAZ LeeEmpresa;

     aRuta = #labcontr#51;
     |QBF aRuta;
     aAlfa = aRuta % -101;
     |SI aAlfa ! "\"; |Y aAlfa ! "/";
          aRuta = aRuta + "/";
     |FINSI;
     aRuta = aRuta + "Certific@2/";

     |RMKDIR aRuta;

     aRuta = aRuta + aFichero;
     |XABRE "CBW", aRuta, nHandle;
     |SI FSalida ] 0;
          nNivel = 1;
          aAlfa = "<?xml version=" + &34 + "1.0" + &34 + " encoding=" + &34 + "ISO-8859-1" + &34 + " ?>";
          |HAZ Linea;
          aAlfa = "<Comunicacion_Unica xmlns:xsi=" +  &34 + "http://www.w3.org/2001/XMLSchema-instance" +  &34;
          |HAZ Linea;

          |HAZ LeeRepre;

          nNivel = 2;
          aAlfa = "xsi:noNamespaceSchemaLocation=" + &34 + "EsquemaComunicacionPeriodosActividadUnicav2.xsd" + &34 + ">";
          |HAZ Linea;
          aAlfa = "<Datos_Representante>";
          |HAZ Linea;

          nNivel = 3;
          aAlfa = aNifRepre;
          |QBF aAlfa;
          aAlfa = "<CIF_NIF>" + aAlfa + "</CIF_NIF>";
          |HAZ Linea;

          aCadena = aRepre;
          |HAZ SeparaNom_plb;
          aApel1 = eaApel1;
          aApel2 = eaApel2;
          aNom = eaNom;

          aAlfa = "<Nombre>" + aNom + "</Nombre>";
          |HAZ Linea;
          aAlfa = "<Apellido1>" + aApel1 + "</Apellido1>";
          |HAZ Linea;
          aAlfa = "<Apellido2>" + aApel2 + "</Apellido2>";
          |HAZ Linea;

          nNivel = 2;
          aAlfa = "</Datos_Representante>";
          |HAZ Linea;

          nNivel = 2;
          aAlfa = "<Datos_Empresa>";
          |HAZ Linea;

          nNivel = 3;
          aAlfa = #labempre#10;
          |QBF aAlfa;
          aAlfa = "<CIF_NIF>" + aAlfa + "</CIF_NIF>";
          |HAZ Linea;

          |BUCLE BuscaRegimen;
          aCCC = #labempre#13;
          |HAZ BuscaCCCEmp;
          |QBF aCCC;
          aAlfa = aRegimen + aCCC;
          aAlfa = "<CCC>" + aAlfa + "</CCC>";
          |HAZ Linea;

          nNivel = 2;
          aAlfa = "</Datos_Empresa>";
          |HAZ Linea;

          || DATOS TRABAJADOR

          |BUCLE Trabajador;

          nNivel = 1;
          aAlfa = "</Comunicacion_Unica>";
          |HAZ Linea;
     |FINSI;
     |XCIERRA nHandle;

     |SI nNumMov ! 0;
          aRuta = aRuta - aFichero;
          aAlfa = "    El fichero se ha generado correctamente en " + aRuta;
          |MENAV aAlfa;

          aAlfa1 = nNumTra;
          aAlfa2 = nNumMov;

          aAlfa = "    Se incluyen un total de " + aAlfa2 + " movimientos ";
          aAlfa = aAlfa + "en " + aAlfa1 + " trabajadores.";
          |MENAV aAlfa;
     |SINO;
          aAlfa = "    No se han encontrado movimientos pendientes de envío";
          |MENAV aAlfa;
     |FINSI;
     |CIERRA #gomccchi;
     |CIERRA #labcentr;
     |CIERRA #3;
     |CIERRA #2;
     |CIERRA #1;
|FPRC;

|PROGRAMA;
     |CLS;

     aParam = PARAMETRO;
     |QBF aParam;

     |SI aParam = "";
          |CAMPO_MODIFICA #0#2, 1, "S";
          |CAMPO_MODIFICA #0#3, 1, "S";
     |SINO;
          aEmpresa = aParam % 105;
          nEmpresa = aEmpresa;
          aAlfa = aParam % 610;
          nCampanya = aAlfa;
          #0#2 = nEmpresa;
          #0#3 = nCampanya;
          |HAZ Fichero;
          |CAMPO_MODIFICA #0#2, 1, "N";
          |CAMPO_MODIFICA #0#3, 1, "N";
     |FINSI;
     |PINPA #0#0;
     |PINDA #0#0;
     |ENDATOS #1#0;
     |SI FSalida = 0;
          nEmpresa = #0#2;
          nCampanya = #0#3;
          |HAZ ElProceso;
     |FINSI;
|FPRO;
