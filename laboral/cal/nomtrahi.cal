|FICHEROS;
     nomtrahi #0;
     labempre #1;
     labtraba #2;
     nomtrtmp #200;
|FIN;

|VARIABLES;
     &aNif = "";
     aDef = "";
     aAlfa = "";

     &aNom = "";
     &aEmp = "";
     &aTra = "";
     &aFal = "";
     &aFba = "";
     &aFvt = "";
     &aCen = "";
     &aCon = "";
     &aCat = "";
     &aAre = "";
     &aSec = "";
     &aTip = "";
     &aEpi = "";
     &aNomEmp = "";
     &aGru = "";
     &aMot = "";

     aCenAnt = "";
     aConAnt = "";
     aCatAnt = "";
     aAreAnt = "";
     aSecAnt = "";
     aTipAnt = "";
     aEpiAnt = "";
     aGruAnt = "";

     alfa1 = "";
     alfa2 = "";
     x = "";
     final = "";
     nume1 = 0;
     nume2 = 0;
     lugar = 0;
     nifnum = 0;
     resto = 0;
     corta = 0;
     letrasnif = "TRWAGMYFPDXBNJZSQVHLCKE";
     letra = "";
     &eaVarDni  = "";
     &enCalcCif = 0;

     aParam = "";
     &nModo = 0;
     nModoLineal = 0;
     nPos1 = 0;
     nPos2 = 0;

     &enCambiaEmp = 0;
     &enCambiaTra = 0;
     &eaProcedencia = "";
|FIN;

/*--------------------------------------------------------------------------*/
/*                         CALCULOS DESDE EL MANTENIMIENTO                  */
/*--------------------------------------------------------------------------*/

|PROCESO LeeEmpresa;
     |SI #0#0 ! 0;
          |ABRE #labempre;
          #labempre#0 = #0#0;
          |LEE 000#labempre.=;
          |SI FSalida ! 0;
               |MENAV "    ATENCION: Clave inexistente en el fichero EMPRESAS";
               |ERROR;
               aAlfa = "                                         ";
               |PINTA 1239, aAlfa;
          |SINO;
               aAlfa = #labempre#2;
               |COLOR 1,7;
               |ATRI I;
               |PINTA 1239, aAlfa;
               |ATRI 0;
          |FINSI;
          |CIERRA #labempre;
     |SINO;
          |ERROR;
          aAlfa = "                                         ";
          |PINTA 1239, aAlfa;
     |FINSI;
|FPRC;

|PROCESO LeeTrabaja;
     |SI #0#1 ! 0; |O #0#0 ! 0;
          |ABRE #labtraba;
          #labtraba#0 = #0#0;
          #labtraba#1 = #0#1;
          |LEE 000#labtraba.=;
          |SI FSalida ! 0;
               |MENAV "    ATENCION: Clave inexistente en el fichero TRABAJADORES";
               |ERROR;
               aAlfa = "                                         ";
               |PINTA 1439, aAlfa;
          |SINO;
               aAlfa = #labtraba#2;
               |COLOR 1,7;
               |ATRI I;
               |PINTA 1439, aAlfa;
               |ATRI 0;
          |FINSI;
          |CIERRA #labtraba;
     |SINO;
          aAlfa = "                                         ";
          |PINTA 1439, aAlfa;
     |FINSI;
|FPRC;

|PROCESO nif;
     eaVarDni  = #0Campo;
     enCalcCif = 0;
     |HAZ CalculoDNI;
     |SI #0Campo ! eaVarDni;
          #0Campo = eaVarDni;
          |PINTA #0Campo;
     |FINSI;
|FPRC;

|PROCESO marca;
     |SI Campo = 2;
          |SI #0Campo = "X";
               |CAMPO_MODIFICA #0#0, 1, "S";
               |CAMPO_MODIFICA #0#1, 1, "S";
               |CAMPO_MODIFICA #0#3, 1, "N";
               |CAMPO_MODIFICA #0#4, 1, "N";
               #0#3 = " ";
               |PINTA #0#3;
          |SINO;
               |CAMPO_MODIFICA #0#0, 1, "N";
               |CAMPO_MODIFICA #0#1, 1, "N";
               #0#0 = "00000"; |PINTA #0#0;
               #0#1 = "00000"; |PINTA #0#1;
               |CAMPO_MODIFICA #0#3, 1, "S";
          |FINSI;
     |FINSI;
     |SI Campo = 3;
          |SI #0Campo = "X";
               |CAMPO_MODIFICA #0#0, 1, "N";
               |CAMPO_MODIFICA #0#1, 1, "N";
               #0#0 = "00000"; |PINTA #0#0;
               #0#1 = "00000"; |PINTA #0#1;
               #0#2 = " ";
               |PINTA #0#2;
               |CAMPO_MODIFICA #0#4, 1, "S";
          |SINO;
               #0#4 = "";
               |PINTA #0#4;
               |CAMPO_MODIFICA #0#4, 1, "N";
               |SI #0#2 = " "; |Y FSalida ! 2;
                    |ERROR;
               |FINSI;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO NoPuedo;
     |SI #0Campo = "S"; |Y #0#2 = " "; |Y #0#3 = " ";
          |CAMPO_MODIFICA #0#2, 1, "S";
          |CAMPO_MODIFICA #0#3, 1, "S";
          |MENAV "    ATENCION: Seleccion vacia";
     |FINSI;
|FPRC;

/*--------------------------------------------------------------------------*/
/*                         CALCULOS DEL PROCESO                             */
/*--------------------------------------------------------------------------*/

|PROCESO imptraba;
     aEmp = #200#0;  aEmp = "00000" + aEmp; aEmp = aEmp % -105;
     aTra = #200#1;  aTra = "00000" + aTra; aTra = aTra % -105;
     aFal = #200#4;
     aFba = #200#5;
     aFvt = #200#16;
     aCen = #200#6; aCen = "000" + aCen;    aCen = aCen % -103;
     aCon = #200#7; aCon = "000" + aCon;   aCon = aCon % -103;
     aCat = #200#8; aCat = "000" + aCat;    aCat = aCat % -103;
     aAre = #200#9;
     aSec = #200#10;
     aTip = #200#11; aTip = "000" + aTip;   aTip = aTip % -103;
     aEpi = #200#12; aEpi = "000" + aEpi;   aEpi = aEpi % -103;
     aNom = #200#2;
     aNif = #200#3;
     aNomEmp = #200#13;
     aGru = #200#14; aGru = "00" + aGru;    aGru = aGru % -102;
     aMot = #200#15 % 116;
     |SI aMot = "Motivo de baja: ";
          aMot = #200#15 % 1783;
     |SINO;
          aMot = #200#15;
     |FINSI;
     aAlfa = aCenAnt + aConAnt + aCatAnt + aAreAnt + aSecAnt + aTipAnt + aEpiAnt + aGruAnt;
     |QBF aAlfa;
     |SI aAlfa ! "";
          |SI aCenAnt ! aCen; aCen = aCen + "(*)"; |FINSI;
          |SI aConAnt ! aCon; aCon = aCon + "(*)"; |FINSI;
          |SI aCatAnt ! aCat; aCat = aCat + "(*)"; |FINSI;
          |SI aAreAnt ! aAre; aSec = aSec + "(*)"; |FINSI;
          |SI aSecAnt ! aSec; aSec = aSec + "(*)"; |FINSI;
          |SI aTipAnt ! aTip; aTip = aTip + "(*)"; |FINSI;
          |SI aEpiAnt ! aEpi; aEpi = aEpi + "(*)"; |FINSI;
          |SI aGruAnt ! aGru; aGru = aGru + "(*)"; |FINSI;
     |FINSI;
     |PRINT;
     aCenAnt = aCen % 103;
     aConAnt = aCon % 103;
     aCatAnt = aCat % 103;
     aAreAnt = aAre % 104;
     aSecAnt = aSec % 104;
     aTipAnt = aTip % 103;
     aEpiAnt = aEpi % 103;
     aGruAnt = aGru % 102;
|FPRC;

|DEFBUCLE imptraba;
     #nomtrtmp#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, imptraba;
|FIN;

|PROCESO coptraba;
     #labempre#0 = #labtraba#0;
     |LEE 000#labempre.=;

     aAlfa = #labtraba#36;
     |QBF aAlfa;
     |SI aAlfa ! ""; |Y aAlfa ! "..........";
          #200#4 = #labtraba#36;   || fecha alta
     |SINO;
          #200#4 = "00.00.0000";
     |FINSI;
     #200#0 = #labtraba#0;    || empresa
     #200#1 = #labtraba#1;    || trabajador
     #200#2 = #labtraba#2;    || nombre
     #200#3 = #labtraba#7;    || nif
     #200#5 = #labtraba#37;   || fecha baja
     #200#16 = #labtraba#177;   || fecha vto. contrato
     #200#6 = #labtraba#77;   || centro
     #200#7 = #labtraba#87;   || convenio
     #200#8 = #labtraba#17;   || categoria
     #200#9 = #labtraba#62;   || area
     #200#10 = #labtraba#63;  || seccion
     #200#11 = #labtraba#45;  || contrato
     #200#12 = #labtraba#32;  || epigrafe

     #200#13 = #labempre#2;   || empresa
     #200#14 = #labtraba#33;  || grupo de tarifa
     #200#15 = #labtraba#184; || motivo de la baja
     |GRABA 020#200n;
|FPRC;
/*
|DEFBUCLE coptraba;
     #labtraba#2;
     ;
     aNif;
     aNif;
     ;
     NULL, coptraba;
|FIN;
*/

|DEFBUCLE coptraba;
     #labtraba#1;
     7;
     00001, 00001, aNif;
     99999, 99999, aNif;
     ;
     NULL, coptraba;
|FIN;

|PROCESO labtraba;
     aAlfa = #0#2;
     aAlfa = aAlfa + #0#3;
     |QBF aAlfa;
     |SI aAlfa = "";
          |MENAV "    Debe marcar alguna opcion";
     |FINSI;
     |SI #0#2 = "X";
          #labtraba#0 = #0#0;
          #labtraba#1 = #0#1;
          |LEE 000#labtraba.=;
          |SI FSalida ! 0;
               |MENAV "    ATENCION: Trabajador no encontrado";
          |SINO;
               aNif = #labtraba#7;
          |FINSI;
     |FINSI;
     |SI #0#3 = "X";
          aNif = #0#4;
     |FINSI;

     aAlfa = Usuario;  |QBF aAlfa;    aAlfa = "nh" + aAlfa;
     |NOME_DAT #200 aAlfa;         || tmp contador de trabajadores
     |DELINDEX #200;   || Se carga el temporal que pudiera existir
     |ABRE #200;
     |BUCLE coptraba;
     |CIERRA #200;
     |SI aParam = "";
          |BUCLE imptraba;
     |FINSI;
|FPRC;

|PROCESO AvisoCrystal; |TIPO 7;
     |PUSHV 06691080;
     |ATRI R;
                || 67         8
                || 901234567890
     |PINTA 0669, "  INFORME   ";
     |PINTA 0769, " DISPONIBLE ";
     |PINTA 0869, " EN FORMATO ";
     |PINTA 0969, "  CRYSTAL   ";
     |PINTA 1069, "  REPORTS.  ";
     |ATRI 0;
|FPRC;

|PROCESO tipo3;
     |ABRE #labtraba;
     |ABRE #labempre;

     |SI aParam = "";
          |SI #0#2 = " "; |Y #0#3 = " ";
               |CAMPO_MODIFICA #0#2, 1, "S";
               |CAMPO_MODIFICA #0#3, 1, "S";
               |MENAV "    ATENCION: Seleccion vacia";
          |FINSI;

          Impresora = "Crystal Reports";
          |IMPRE 0;

          |SI FSalida = 0;
               |INFOR "nomtrahi";
               |HAZ labtraba;
               |PIEINF;
               |FININF;
          |FINSI;

          |FINIMP;
     |SINO;
          #0#2 ="X";
          aAlfa = aParam % 105;
          #0#0 = aAlfa;
          aAlfa = aParam % -105;
          #0#1 = aAlfa;
          #0#5 = "S";

          |HAZ labtraba;


          |ABRE #200;
          nModoLineal = 2 + 4 + 8 + 16;
          |SI eaProcedencia = "labtraba"; |O eaProcedencia = "labempre";
               || del labtraba, en realidad, pero pasa por el labempre
               || y la variable se cambia
               |PUSHV 08012380;
               nPos1 = 1003; nPos2 = 2278;
          |FINSI;
          |SI eaProcedencia = "nomw0004";
               |PUSHV 07102380;
               nPos1 = 0811; nPos2 = 2279;
          |FINSI;

          |SELECT #4#200;
          #200#0 = #0#0;
          #200#1 = #0#1;
          |LEE 000#200=;

          enCambiaEmp = 0;
          enCambiaTra = 0;

          |LINEAL_SIMPLE #1#200, nModoLineal, nPos1, nPos2, NULL, NULL, NULL;
          |SI FSalida = 0;
               enCambiaEmp = #200#0;
               enCambiaTra = #200#1;
          |FINSI;
          |CIERRA #200;

          |POPV;
     |FINSI;

     |CIERRA #labtraba;
     |CIERRA #labempre;
|FPRC;

|PROGRAMA;
     aParam = PARAMETRO;
     |QBF aParam;

     |SI aParam = "";
          |CLS;
          |PINPA #0#0;
          |PINDA #0#0;
          |ENDATOS #1#0;
          |SI FSalida = 0;
               |HAZ tipo3;
          |FINSI;
     |SINO;
          |HAZ tipo3;
     |FINSI;
|FPRO;
