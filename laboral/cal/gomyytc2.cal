|FICHEROS;
     gomyytc2 #0;    || emision TC2
     labempax #2;    || aux. empresas
     nomtepig #4;    || aux. epigrafes
     nomcalca #6;    || calculos cabs.
     tsilcon6 #23;
     tsilcon9;
|FIN;

|VARIABLES;
    runnom = "gomyytc2";
    sw1 = 0;
    &Desde_emp = 0;
    &Hasta_emp = 0;
    &Mes = 0;
    &Anyo = 0;
    &EURODB = 0;
    &eaFuente = "";
    &nEmpresa = 0;
    &nTrabaj = 0;
    &nMes = 0;
    &nAnyo = 0;
    &nDesdeTipo = 0;
    &nHastaTipo = 0;
    &eaVengode = "";
    &swSelTra = 0;
     nEmp = 0;
     nTra = 0;
     swStop = 0;
     nDesdeCen = 0;
     nHastaCen = 0;
|FIN;

|INCLUYE i_recog;
|INCLUYE i_cuadr;
|INCLUYE i_2ytc2;
____________________________________/ CALCULOS DE PANTALLA
|PROCESO defecto; |TIPO 7;
    fecsys = ~;
    alfa1 = fecsys;
    alfa1 = alfa1 % -104;
    #0Campo = alfa1;
    #0Campo = #0Campo - menos;
|PINTA #0Campo;
|FPRC;

|PROCESO pintames; |TIPO 0;
|SI #0Campo =  1;  p_mes = "ENERO     ";  |FINSI;
|SI #0Campo =  2;  p_mes = "FEBRERO   ";  |FINSI;
|SI #0Campo =  3;  p_mes = "MARZO     ";  |FINSI;
|SI #0Campo =  4;  p_mes = "ABRIL     ";  |FINSI;
|SI #0Campo =  5;  p_mes = "MAYO      ";  |FINSI;
|SI #0Campo =  6;  p_mes = "JUNIO     ";  |FINSI;
|SI #0Campo =  7;  p_mes = "JULIO     ";  |FINSI;
|SI #0Campo =  8;  p_mes = "AGOSTO    ";  |FINSI;
|SI #0Campo =  9;  p_mes = "SEPTIEMBRE";  |FINSI;
|SI #0Campo = 10;  p_mes = "OCTUBRE   ";  |FINSI;
|SI #0Campo = 11;  p_mes = "NOVIEMBRE ";  |FINSI;
|SI #0Campo = 12;  p_mes = "DICIEMBRE ";  |FINSI;
|PINTA 1730, p_mes;
|FPRC;

|PROCESO mirames; |TIPO 7;
    fecsys = ~;
    alfa1 = fecsys;
    alfa1 = alfa1 % 402;
    #0Campo = alfa1;
    #0Campo = #0Campo - 1;
    menos = 0;
|SI #0Campo = 0;
        #0Campo = 12;
        menos = 1;
|FINSI;
|PINTA #0Campo;
|FPRC;

|PROCESO clasi_liq; |TIPO 0;
    sw1 = 0;
|SI #0#7 = "L00";  sw1 = 1;  |FINSI;
|SI #0#7 = "L02";  sw1 = 1;  |FINSI;
|SI #0#7 = "L03";  sw1 = 1;  |FINSI;
|SI #0#7 = "L04";  sw1 = 1;  |FINSI;
|SI #0#7 = "L07";  sw1 = 1;  |FINSI;
|SI #0#7 = "L09";  sw1 = 1;  |FINSI;
|SI #0#7 = "L10";  sw1 = 1;  |FINSI;
|SI #0#7 = "L11";  sw1 = 1;  |FINSI;
|SI sw1 = 0;
    |MENAV "    Calificador no reconocido ...";
    |ERROR;
|FINSI;
|FPRC;

____________________________________/ GRABACION DEL TEMPORAL (incluye)
|PROCESO temporal1;
|HAZ temporal2;
|FPRC;

|DEFBUCLE gomyytc20;
 #6#1;
 ;
 #0#0,     0, #0#2, s_tat;
 #0#1, 99999, #0#2, s_tat;
 e;
 NULL, temporal1;
|FIN;

|PROCESO CompCentro;
     #labtraba#0 = nEmp;
     #labtraba#1 = nTra;
     |LEE 000#labtraba.=;
     |SI FSalida = 0;
          #tsilcon9#0 = #labtraba#0;
          #tsilcon9#1 = #labtraba#77;
          |LEE 000#tsilcon9.=;
          |SI FSalida = 0;
               |SI #tsilcon9#3 ! "*";
                    swStop = 1;
               |FINSI;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO temporal1_RED;
     #0#2 = nMes;
     #0#3 = nAnyo;
     |SELECT #2#23;
     #23#1 = #6#0;
     #23#2 = #6#1;
     |LEE 000#23=;
     |SI swSelTra = 0;
          swStop = 0;
          nEmp = #nomcalca#0;
          nTra = #nomcalca#1;
          |HAZ CompCentro;
          |SI swStop = 1;
               |ACABA;
          |FINSI;

          |HAZ temporal2;
     |SINO;
          |SI FSalida = 0;|Y #23#7 = "*";
               |HAZ temporal2;
          |SINO;
               |ACABA;
          |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE gomyytc20_RED;
 #6#1;
 ;
 nEmpresa,     0, nMes, nDesdeTipo;
 nEmpresa, 99999, nMes, nHastaTipo;
 e;
 NULL, temporal1_RED;
|FIN;

|PROCESO temporal0;
     |SI eaVengode ! "silcon16";
          |BUCLE gomyytc20;
     |SINO;
          |BUCLE gomyytc20_RED;
     |FINSI;
|FPRC;
____________________________________/ INICIO Y FIN DEL PROCESO
|PROCESO impre; |TIPO 3;

     Desde_emp = #0#0;
     Hasta_emp = #0#1;
     Mes = #0#2;
     Anyo = #0#3;

     |HAZ principal;
|FPRC;
_________________________/ BORRA AUXILIARES
|PROCESO bor_empresa;
     |BORRA 020#2a;
|FPRC;

|DEFBUCLE bor_empresa;
     #2#1;
     50;
     #7#0, 001, #0#2, s_tat, "A";
     #7#0, 999, #0#2, s_tat, "A";
     be;
     NULL, bor_empresa;
|FIN;

|PROCESO bor_epi1;
|BORRA 020#4a;
|FPRC;

|DEFBUCLE bor_epi;
 #4#1;
 ;
 #7#0, 001, #0#2,   0, s_tat;
 #7#0, 999, #0#2, 999, s_tat;
 ;
 NULL, bor_epi1;
|FIN;

|PROCESO borrado;
|BUCLE bor_epi;
|BUCLE bor_empresa;
|FPRC;

|PROCESO BuscaBonifFP;          || si encuentro alguno es porque la bonif YA
     swBonifFPCont = 0;       || se ha tenido en cuenta en la empresa
|FPRC;

|DEFBUCLE BuscaBonifFPMen;
     #2#1;
     35;
     antempre, 000, #0#2, 0, "X";
     antempre, 999, #0#2, 9, "X";
     ;
     NULL, BuscaBonifFP;
|FINSI;

|PROCESO FormCont99;
     #2#12 = #2#12 + nBonifFPCont;      || bonif./subven. 601
     #2#95 = #2#95 + nBonifFPCont;
     #2#35 = "X";
     |GRABA 020#2a;
     |ERROR10;
|FPRC;

|DEFBUCLE FormCont99;
     #2#1;
     ;
     antempre, 99, #0#2, 0;
     antempre, 99, #0#2, 9;
     be;
     NULL, FormCont99;
|FIN;

|PROCESO nombofli99;
     nBonifFPCont = nBonifFPCont + #nombofli#3;
     swBonifFPCont = 1;
|FPRC;

|DEFBUCLE nombofli99;
     #nombofli#1;
     ;
     antempre, #0#3, #0#2, 001, " ", "B";
     antempre, #0#3, #0#2, 999, "F", "P";
     ;
     NULL, nombofli99;
|FIN;

|PROCESO FormContCen;
     |SI #nombofli#4 = 99;
          |SI #2#95 = 0; || es 99 y ya esta puesta, otra vez no;
               #2#12 = #2#12 + nBonifFPCont;      || bonif./subven. 601
               #2#95 = #2#95 + nBonifFPCont;
               #2#35 = "X";
               |GRABA 020#2a;
          |FINSI;
     |SINO;
          #2#12 = #2#12 + nBonifFPCont;      || bonif./subven. 601
          #2#95 = #2#95 + nBonifFPCont;
          #2#35 = "X";
          |GRABA 020#2a;
     |FINSI;
     |ERROR10;
|FPRC;

|DEFBUCLE FormContCen;
     #2#1;
     ;
     antempre, nDesdeCen, #0#2, 0;
     antempre, nHastaCen, #0#2, 9;
     be;
     NULL, FormContCen;
|FIN;

|PROCESO nombofliCen;
     nBonifFPCont = #nombofli#3;
     swBonifFPCont = 1;
     |SI #nombofli#4 = 99;
          nDesdeCen = 0;
          nHastaCen = 99;
          |BUCLE FormContCen;
     |SINO;
          |SI #nombofli#4 = #2#18;
               nDesdeCen = #nombofli#4;
               nHastaCen = #nombofli#4;
               |BUCLE FormContCen;
          |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE nombofliCen;
     #nombofli#1;
     ;
     antempre, #0#3, #0#2, 001, " ", "B";
     antempre, #0#3, #0#2, 999, "F", "P";
     ;
     NULL, nombofliCen;
|FIN;

|PROCESO BonifFPCont;
     |SALVA_FICHA #2;
     |SI #labempre#127 = 0; || si hay recargo de mora no se bonifica
          |SI #labempre#45 = "N";  || por centros NO, todo al 99
               |BUCLE nombofli99;
               |BUCLE FormCont99;
          |SINO;
               |BUCLE nombofliCen;
          |FINSI;
     |FINSI;
     |REPON_FICHA #2;
|FPRC;

____________________________________/ GRABA AUX. DE EMPRESAS Y EPIGRAFES
|PROCESO auxiliar;      || ACTUALIZA FICHERO AUXILIAR DE EMPRESAS
     #2#00 = antempre;   || codigo empresa
     #2#01 = #0#2;       || mes
     #2#02 = s_tat;       || tipo
     |SI #1#45 = "N";     || centro de trabajo
          #2#18 = 99;
     |SINO;
          #2#18 = antcentr;
     |FINSI;
     |LEE 101#2=;
     FEstado = FSalida;
     |SI FEstado ! 0;
          |DDEFECTO #2;
     |FINSI;
     #2#00 = antempre;   || codigo empresa
     #2#01 = #0#2;       || mes
     #2#02 = s_tat;       || tipo
     #2#03 = #2#03 + t_aux1a;    || base cont. general, alta, con des.
     #2#04 = #2#04 + t_aux2a;    || base cont. general, alta, sin des.
     #2#07 = #2#07 + t_aux3a;    || base cont. jornadas, alta, con des
     #2#08 = #2#08 + t_aux4a;    || base cont. jornadas, alta, sin des

     |SI #0#3 ] 2012;
          #2#83 = #2#83 + t_aux1a_01;    || base cont. jornadas, alta, sin des
     |FINSI;

     #2#70 = #2#70 + t_aux1b;    || base cont. general, IT, con des.
     #2#71 = #2#71 + t_aux2b;    || base cont. general, IT, sin des.
     #2#72 = #2#72 + t_aux3c;    || base cont. jornadas, IT, con des, pago emp
     #2#72 = #2#73 + t_aux3b;    || base cont. jornadas, IT, con des, pago SS
     #2#73 = #2#74 + t_aux4b;    || base cont. jornadas, IT, sin des (fogasa)

     #2#48 = #2#48 + t_basefoga;  || total base FOGASA + DESEMPLEO

     || #2#49 = #2#49 + t_cuotades1;  || total cuota desempleo trab+emp

     #2#60 = #2#60 + t_basedestot1;
     #2#62 = t_tipodestot1;
     #2#61 = #2#61 + t_basedestot2;
     #2#63 = t_tipodestot2;
     #2#65 = #2#65 + t_basedestot3;
     #2#66 = t_tipodestot3;

     #2#75 = #2#75 + t_basedesemp1;
     #2#76 = t_tipodesemp1;
     #2#77 = #2#77 + t_basedesemp2;
     #2#78 = t_tipodesemp2;
     #2#79 = #2#79 + t_basedesemp3;
     #2#80 = t_tipodesemp3;

     #2#05 = #2#05 + t_08;       || horas extra 1
     #2#06 = #2#06 + t_09;       || horas extra 2

     #2#09 = #2#09 + t_12;       || importe I.L.T.
     #2#10 = #2#10 + t_14;       || importe accidentes

     #2#11 = #2#11 + t_bonif;    || bonificaciones contingencias
     #2#12 = #2#12 + t_reducc;   || bonificaciones inem

     #2#13 = #2#13 + t_enfe1;    || base enfermedad
     #2#14 = #2#14 + t_15;       || numero de jornadas reales (*)

     #2#15 = #2#15 + t_epig9;    || fijos con desempleo (*)
     #2#16 = 0;
     #2#17 = #2#17 + t_fijos;    || total trabajadores fijos (*)
     |SI #1#45 = "N";     || centro de trabajo
          #2#18 = 99;
     |SINO;
          #2#18 = antcentr;
     |FINSI;
     #2#19 = #2#19 + t_event;    || total trabajadores eventuales (*)
     #2#50 = "A";                || regimen agrario
     #2#51 = #2#51 + t_aux3;   || contingencias comunes 106 (ctos.<7dias)

     || #2#64 = #2#64 + t_base4;       || total base extranjeros temporales
     || #2#65 = #2#65 + t_aux4;        || total cuota fija extranjeros temporales

     |SI t_por4 > 0;
          #2#66 = t_por4;        || % cotiz. extranjeros temporales
     |FINSI;

     || #2#62 = g_pordesem;    || % desempleo en alta
     || #2#63 = g_porit;       || % desempleo en IT
     || uso estos tres campos del abreviado, ya que no se usan en el agrario
     || el #62 es nuevo y para agrarios lo uso como % desempleo

     ||SI #0#3 [ 2011; |O #7#21 ! 01;
          #2#57 = #3#6 + #3#7;
     ||FINSI;

     |SI #0#3 ] 2012;
          #2#84 = t_tipo_G01;
     |FINSI;

     |SI #0#3 ] 2012;                        || FP
          #2#85 = #2#85 + t_aux5a;
          #2#86 = #3#16 + #3#17;
     |FINSI;

     |SI #0#3 ] 2012;                        || cont emp, normal y G01
          #2#89 = #2#89 + t_aux6;
          #2#90 = #3#6;                      || %
          #2#91 = #2#91 + t_aux6_01;         || cc emp G01
          #2#92 = t_tipo_G01_emp;            || %
     |FINSI;

     || en este campo me guardo el % cont. comunes
     #2#67 = #2#67 + t_nofog;

     |SI #0#3 ] 2012;
          #2#87 = #2#87 + nRedCCAlta + nRedCCEnf + nRedCCMat + nRedCCAcc + nRedCCEnfEmp;
          #2#88 = #2#88 + nRedDesEnf + nRedDesMat + nRedDesAcc + nRedDesEnfEmp;
     |FINSI;
     #2#96 = #2#96 + t_aux7b;   || base cot. solidaridad
     #2#97 = #2#97 + t_aux7c;   || cuota cot. solidaridad

     |SI FEstado ! 0;  |GRABA 020#2n;  |FINSI;
     |SI FEstado = 0;  |GRABA 020#2a;  |FINSI;
     |LIBERA #2;
|FPRC;

|PROCESO epigrafe;      || GRABA EN FICHERO DE EPIGRAFES PARA TC1
|ABRE #4;               || se abre y cierra aqui para que este libre para
|DDEFECTO #9;           ||                                   / el BUCLE 2
|SI t_epigr = #3#2;
        por_ilt = #3#3;  || guarda (%) ILT
        por_ims = #3#4;  || guarda (%) IMS
|SINO;
        #9#0 = t_epigr;
    |LEE 020#9=;         || lee en maestro de epigrafes
        por_ilt = #9#1;  || guarda (%) ILT
        por_ims = #9#2;  || guarda (%) IMS
|FINSI;
    #4#0 = antempre;    || codigo de empresa
    #4#1 = #0#2;        || mes
    #4#2 = t_epigr;     || epigrafe
    #4#9 = s_tat;           || tipo
    || #4#10 = 99;         || centro de trabajo
    |SI #1#45 = "N";
         #4#10 = 99;
    |SINO;
         #4#10 = antcentr;
    |FINSI;
|LEE 101#4=;            || lee en trabajo de epigrafes
    FEstado = FSalida;
|SI FEstado ! 0;
    |DDEFECTO #4;
        t_epig4 = t_epig4 + 1;  || contador de epigrafes diferentes por empresa
|FINSI;
    #4#0 = antempre;    || codigo de empresa
    #4#1 = #0#2;        || mes
    #4#2 = t_epigr;     || epigrafe
    #4#3 = por_ilt;     || (%) I.L.T.
    #4#4 = por_ims;     || (%) I.M.S.
    #4#5 = #4#5 + t_epig0;     || base contingencias
    #4#6 = #4#6 + t_epig2;     || nro. trabajadores
    #4#7 = #4#5 % por_ilt;     || aplica porcentaje I.L.T.
    #4#8 = #4#5 % por_ims;     || aplica porcentaje I.M.S.
    #4#9 = s_tat;           || tipo
    |SI #1#45 = "N";
         #4#10 = 99;
    |SINO;
         #4#10 = antcentr;
    |FINSI;
    || #4#10 = 99;         || centro de trabajo
|SI FEstado ! 0;  |GRABA 020#4n;  |FINSI;
|SI FEstado = 0;  |GRABA 020#4a;  |FINSI;
    t_epig0 = 0;           || inicializa puente total epigrafe
    t_epig1 = 0;           || inicializa acumulador total epigrafe
    || t_epig2 = 0;           || inicializa acumulador trabajadores
|LIBERA #4;
|CIERRA #4;
|FPRC;
____________________________________/ ACTUALIZA PLANTILLA EN EMPRESA Y CENTRO
|PROCESO empresas;      || ACTUALIZA PLANTILLA EN EMPRESA
    t_trab1 = t_traba - t_altas;  || calcula trabajadores mes anterior
    t_trab2 = t_traba - t_bajas;  || calcula trabajadores que quedan
|ABRE #1;
    #1#0 = antempre;
|LEE 121#1=;
|SI FSalida = 0;
        #1#14 = t_trab2;
    |GRABA 020#1a;
|FINSI;
|LIBERA #1;
|CIERRA #1;
|SI #0#4 = "N";
        t_trpo1 = t_trab1;   t_trpo2 = t_trab2;  || variables puente
        t_altpo = t_altas;   t_bajpo = t_bajas;  || /para el informe
|FINSI;
    t_traba = 0;     || acumulador numero de trabajadores empresa
    t_altas = 0;     || acumulador altas de trabajadores empresa
    t_bajas = 0;     || acumulador bajas de trabajadores empresa
|FPRC;

|PROCESO centros;      || ACTUALIZA PLANTILLA EN CENTRO DE TRABAJO
    t_trce1 = t_trcen - t_altce;  || calcula trabajadores mes anterior
    t_trce2 = t_trcen - t_bajce;  || calcula trabajadores que quedan
|ABRE #10;
    #10#0 = antempre;
    #10#1 = antcentr;
|LEE 121#10=;
|SI FSalida = 0;
        #10#11 = t_trce2;
    |GRABA 020#10a;
|FINSI;
|LIBERA #10;
|CIERRA #10;
|SI #0#4 = "S";
        t_trpo1 = t_trce1;   t_trpo2 = t_trce2;  || variables puente
        t_altpo = t_altce;   t_bajpo = t_bajce;  || /para el informe
|FINSI;
    t_trcen = 0;     || acumulador numero de trabajadores centro
    t_altce = 0;     || acumulador altas de trabajadores centro
    t_bajce = 0;     || acumulador bajas de trabajadores centro
|FPRC;

|PROGRAMA;
/*
     |MENAV "    Opcion en desarrollo debido a cambios legales.";
     |ACABA;
*/
|SI eaVengode ! "silcon16";
     |CLS;
     |CABEZA "EMISION MODELO TC2/8";
     |ABRE #0;
     |LEE 110#0p;
     |SI FSalida ! 0;
          |DDEFECTO #0;
          |GRABA 020#0b;
     |FINSI;
     |PINPA #0#0;
     |PINDA #0#0;
     |ENDATOS #1#0;
     s_ele = "L00";
     |SI FSalida = 0; |HAZ impre; |FINSI;

     |GRABA 020#0a;
     |LIBERA #0;
|SINO;              || vengo del silcon16
     alfa2 = Usuario; |QBT alfa2;    alfa2 = ("u" + alfa2) % 108;
     |NOME_DAT #23 alfa2;
     alfa3 = Usuario; |QBT alfa3;    alfa3 = ("v" + alfa3) % 108;
     |NOME_DAT #tsilcon9#alfa3#;
     |ABRE #23;
     |ABRE #tsilcon9;
     |ABRE #labtraba;

     |HAZ impre;

     |CIERRA #labtraba;
     |CIERRA #tsilcon9;
     |CIERRA #23;
|FINSI;
|FPRO;
