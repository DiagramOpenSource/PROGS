|FICHEROS;
    nomir001 #0;
    labempre #1;    || empresas
    labtraba #2;    || trabajadores
    nomhicca #3;    || historico cabs.
    nomcalca #4;    || calculos cabs.
    labregis #5;    || registro contratos
    nomcalcu;       || calculo de nomina

    nomir002 #101;  || asignar codigo
    nomir003 #102;  || parametros de calculo
    nomir004 #103;  || ayuda para pintar campos en formato estandar
    nomir005 #104;  || presentacion de resultados
    nomir007 #106;  || fechas de alta/baja modificadas

    nomir008 #2000;      || presentacion resultados calculo masivo
    nomcalcu #2001;      || seleccion de calculo reg. general
    nomir009 #2002;      || control de regularizacion por empresas
    nomcontr #2003;      || control de regularizacion general
    gomcalcu #2004;      || seleccion de calculo agrarios

    copcalcu@ #200;      || copia del def del calculo de nominas (normal o agrario)
    nomgruec;
    nomgruel;
    nomvarca;

     nomcalca;
     nom400eu;
     nomir012,MANTE;
|FIN;

|VARIABLES;
     &nElTop = 12;
     &nElMesIRPF = 0;
     &nElAnyIRPF = 0;
     &nDiferencias = 0;
     &nVEmp = 0;
     &aVDNI = "";
     &nVAny = 0;

     nSwAlgo = 0;
     aAlfa = "";
     aAlfa1 = "";
     aAlfa2 = "";
     aAlfa3 = "";
     aAlfa4 = "";
     nNume1 = 0;
     nNume2 = 0;
     nNume3 = 0;
     nNume4 = 0;
     fFech1 = @;
     fFech2 = @;
     fFech3 = @;
     fFech4 = @;
     nPara1 = 0;
     nPara2 = 0;
     nPara3 = 0;
     nPara4 = 0;
     nModo = 0;
     nOpcion = 0;
     nActualizados = 0;
     aDActua = "";
     aHActua = "";
     nInkey = 0;
     nCuantos = 0;
 {-1}pMenu = "";
     pMenu1 = "2400";
     pMenu2 = "3";
     pMenu3 = "";
     || pMenu4 = "TSER";
     pMenu4 = "TSEIR";
     pMenu5 = "Actualizar Todas";
     pMenu6 = "Actualizar Seleccion";
     pMenu7 = "Editar Seleccion";
     pMenu8 = "Informe Previo";
     pMenu9 = "SaliR";
     || pMenu8 = " SaliR ";
     nDEmpr = 0;
     nHEmpr = 0;
     nDTrab = 0;
     nHTrab = 0;
     salte_1 = 0;
     salte_2 = 0;
     nSwSiLeToca = 0;
     &aMes = "";
     &aAnyo = "";

     &swTipoMedio = 0;
     &nRete1_PA = 0;
     &nRete2_PA = 0;
     &nRete3_PA = 0;
     &swAvisaMedio = 0;

     &nTIPOEst = 0;
     &aRaya = "";
     &aEnero = "";
     &aEstim = "";
     &nEst = 0;
     swCab = 0;
     &swFiniq = 0;
     &nEmp = 0;
     &nTra = 0;
     nTipo = 0;
     &eaVengoDe = "";
     aDef = "";
     FCargaDef = 0;
     nDesdeEmp = 0;
     nHastaEmp = 0;
     nDesdeTra = 0;
     nHastaTra = 0;
     &nJorAgr = 0;
     swPorGrupos = 1;
     nGrupo = 0;
     nCampoG = 0;
     nCausaBaja = 0;

     alfa2 = "";
     alfa3 = "";
     mes = 0;
     any = 0;
     &swModulo = 0;
     &nEnEmpresa = 0;
     &nRetEstAnu = 0;
     FEstado = 0;
     nNume = 0;
     &swCalPagas = "";
     nNroTrab = 0;
     nTotalTrab = 0;
     nPan = 0;
|FIN;

|PROCESO Actualiza_1;
     |DDEFECTO #nomir009;
     #nomir009#0 = #nomir008#0;
     |LEE 000#nomir009.=;

     |ABRET #labtraba;
     |SELECT #1#labtraba;
     #labtraba#0 = #nomir008#0;
     #labtraba#1 = #nomir008#1;
     |LEE 101#labtraba.=;
     |SI FSalida = 0;
          |SI #labtraba#29 = 2;         || pido confirmacion antes
               aAlfa1 = #labtraba#0; aAlfa1 = "00000" + aAlfa1; aAlfa1 = aAlfa1 % -105;
               aAlfa2 = #labtraba#1; aAlfa2 = "00000" + aAlfa2; aAlfa2 = aAlfa2 % -105;
               aAlfa3 = #labtraba#30;
               aAlfa4 = #nomir008#7;
               aAlfa = "    ATENCION: Trabajador " + aAlfa1 + "." + aAlfa2;
               aAlfa = aAlfa + " cambia el tipo de retencion IRPF del " + aAlfa3;
               aAlfa = aAlfa + "% al " + aAlfa4 + "%";
               |MENAV aAlfa;
               aAlfa = "     Desea cambiar el tipo de retencion?";
               |CONFI aAlfa;
               |SI FSalida ! 0;
                    |LIBERA #labtraba;
                    |CIERRAT #labtraba;
                    |ACABA;
               |FINSI;
          |FINSI;
          |SI swTipoMedio = 0;
               |SI #nomir009#16 = 0;|O #nomir009#16 = 1; #labtraba#30 = #nomir008#7; |FINSI;
               |SI #nomir009#16 = 0;|O #nomir009#16 = 2; #labtraba#174= #nomir008#7; |FINSI;
               |SI #nomir009#16 = 0;|O #nomir009#16 = 3; #labtraba#175= #nomir008#7; |FINSI;
          |SINO;
               |SI #nomir009#16 = 0;|O #nomir009#16 = 1; #labtraba#30 = #nomir008#7; |FINSI;
               ||SI #nomir009#16 = 0;|O #nomir009#16 = 2; #labtraba#174= nRete2_PA; |FINSI;
               ||SI #nomir009#16 = 0;|O #nomir009#16 = 3; #labtraba#175= nRete3_PA; |FINSI;
               |SI #nomir009#16 = 0;|O #nomir009#16 = 2; #labtraba#174 = #nomir008#7; |FINSI;
               |SI #nomir009#16 = 0;|O #nomir009#16 = 3; #labtraba#175 = #nomir008#7; |FINSI;
          |FINSI;
          nEnEmpresa = #labtraba#0;
          |HAZ BuscaModulo;
          |SI swModulo = 3145;
               |SI #labtraba#216 > 0; |Y #labtraba#30 > #labtraba#216;
                    aAlfa1 = #labtraba#0; aAlfa1 = "00000" + aAlfa1; aAlfa1 = aAlfa1 % -105;
                    aAlfa2 = #labtraba#1; aAlfa2 = "00000" + aAlfa2; aAlfa2 = aAlfa2 % -105;
                    aAlfa = "    ATENCION: Trabajador " + aAlfa1 + "." + aAlfa2;
                    aAlfa = aAlfa + ": el tipo de retencion IRPF calculado ";
                    aAlfa = aAlfa + "es mayor que el solicitado";
                    |MENAV aAlfa;
               |FINSI;
          |FINSI;
          |GRABA 020#labtraba.a;
          |SI FSalida = 0;
               nActualizados = nActualizados + 1;
          |FINSI;
     |FINSI;
     |LIBERA #labtraba;
     |CIERRAT #labtraba;
|FPRC;

|DEFBUCLE Actualiza;
     #nomir008#1;
     9;
         1,     1, aDActua;
     99999, 99999, aHActua;
     ;
     NULL, Actualiza_1;
|FIN;

|PROCESO Actualiza;
     nActualizados = 0;
     |BUCLE Actualiza;
     |MENSA "    Actualizando tipos de IRPF en trabajadores";
     |SI nActualizados ! 0;
          aAlfa1 = "    Actualizadas [" + nActualizados + "] regularizaciones ...";
          |MENAV aAlfa1;
          |CONFI "1200NDesea imprimir listado de trabajadores con regularizacion?"
          |SI FSalida = 0;
               |HAZ Informe;
          |FINSI;
     |SINO;
          |MENAV "    Seleccion vacia ...";
     |FINSI;
|FPRC;

|PROCESO Selecciona; |TIPO 0;
     nInkey = FSalida;

     |SI nInkey = 13;
          |SI #nomir008#9 = "  ";
               #nomir008#9 = "**";
          |SINO;
               #nomir008#9 = "  ";
          |FINSI;
          |GRABA 020#nomir008.a;
          |PINTA #nomir008#9;
          |ERROR;
     |FINSI;
     |SI nInkey = 14;
          |ABRE #nomir005;
          #nomir005#0 = #nomir008#14;
          #nomir005#1 = nElAnyIRPF;
          #nomir005#44= #nomir008#0;
          |LEE 101#nomir005.=;
          |SI FSalida = 0;
               |MENSA "     ";
               |PUSHV 0501 2380;
               |PINPA #0#nomir005;
               |PINDA #0#nomir005;
               |PAUSA;
               |POPV;
               |MENSA "0000<F5> marcar <F6> resultados <F7> parametros <F8> estimacion <F9> Imprimir  ";
          |FINSI;
          |LIBERA #nomir005;
          |CIERRA #nomir005;
          |ERROR;
     |FINSI;
     |SI nInkey = 15;
          |PUSHV 0501 2380;
          |DDEFECTO #nomir003;
          |ABRE #nomir003;
          #nomir003#0 = #nomir008#14;
          #nomir003#40 = nElAnyIRPF;
          #nomir003#41 = #nomir008#0;
          |LEE 101#nomir003.=;
          |SI FSalida = 0;
               |MENSA "     ";
               nPan = 1;
               |PARA; |SI; |HACIENDO;
                    |SI FSalida = 4;
                         nPan = nPan - 1;
                    |FINSI;
                    |SI FSalida = 5;
                         nPan = nPan + 1;
                    |FINSI;
                    |SI FSalida = 1;
                         |PINPA #0#102; |PINDA #0#102;
                         |SAL;
                    |FINSI;
                    |SI nPan [ 1;
                         nPan = 1;
                         |PINPA #0#102; |PINDA #0#102;
                         |PAUSA;
                    |FINSI;
                    |SI nPan ] 2;
                         nPan = 2;
                         |PINPA #1#102; |PINDA #1#102;
                         |PAUSA;
                    |FINSI;
               |SIGUE;
               |MENSA "0000<F5> marcar <F6> resultados <F7> parametros <F8> estimacion <F9> Imprimir  ";
          |FINSI;
          |LIBERA #nomir003;
          |CIERRA #nomir003;
          |POPV;
          |ERROR;
     |FINSI;
     |SI nInkey = 16;
          |ABRE #nomir012;
          #nomir012#1 = #nomir008#0;
          #nomir012#2 = #nomir008#1;
          #nomir012#3 = nElAnyIRPF;
          #nomir012#4 = nElMesIRPF;
          |LEE 101#nomir012.=;
          |SI FSalida = 0;
               |PUSHV 0501 2380;
               |MENSA "     ";
               |PINPA #0#nomir012;
               |PINDA #0#nomir012;
               |PAUSA;
               |POPV;
               |MENSA "0000<F5> marcar <F6> resultados <F7> parametros <F8> estimacion <F9> Imprimir  ";
          |FINSI;
          |LIBERA #nomir012;
          |CIERRA #nomir012;
          |ERROR;
     |FINSI;
     |SI nInkey = 17;
          nEmp = #nomir008#0;
          nTra = #nomir008#1;
          |SUB_EJECUTA "nominf02;REG";
          |ERROR;
     |FINSI;
|FPRC;

|PROCESO NoAlta; |TIPO 7;
     |MENSA "0000<F5> marcar <F6> resultados <F7> parametros <F8> estimacion <F9> Imprimir  ";
     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 1;
          |PTEC 808;
     |FINSI;
|FPRC;

|PROCESO Tipo5; |TIPO 5;
     #nomir008#15 = "M";
     |LINEAS_BI_ATRI #nomir008#15, S, D, -1;
|FPRC;

|PROCESO Min008;
     #nomir008#0 = 1;
     #nomir008#1 = 1;
|FPRC;

|PROCESO Max008;
     #nomir008#0 = 99999;
     #nomir008#1 = 99999;
|FPRC;

|PROCESO Edita;
     |ABRE #labtraba;
     |ENTLINEAL #1#nomir008, 2, 2, 19, 0, Min008, Max008;
     |CIERRA #labtraba;
|FPRC;

|PROCESO Seleccion;
     aDActua = "**";
     aHActua = "**";
     |HAZ Actualiza;
     |SI nActualizados ! 0;
          nOpcion = 99;
     |FINSI;
|FPRC;

|PROCESO Todo;
     aDActua = "  ";
     aHActua = "zz";
     |HAZ Actualiza;
     nOpcion = 99;
|FPRC;

|PROCESO Presenta;
     |PUSHV 0401 2380;
     |PINPA #0#nomir008;
     |MENSA "0000<F5> marcar <F6> resultados <F7> parametros <F8> estimacion <F9> Imprimir  ";
     |PTEC 806;
     |ENTLINEAL #1#nomir008, 2, 4, 19, 0, Min008, Max008;
     |PARA; |SI; |HACIENDO;
          nActualizados = 0;
          |MENU pMenu;
          nOpcion = FSalida;
          pMenu2 = nOpcion;
          |SI nOpcion = 1;    |HAZ Todo;     |FINSI;
          |SI nOpcion = 2;    |HAZ Seleccion;|FINSI;
          |SI nOpcion = 3;    |HAZ Edita;    |FINSI;
          |SI nOpcion = 4;    |HAZ Informe;  |FINSI;
          |SI nOpcion ! 1;|Y nOpcion ! 2;|Y nOpcion ! 3; |Y nOpcion ! 4;
               |SAL;
          |FINSI;
     |SIGUE;
     |SI nOpcion = 99;
          |VERSIONRTME aAlfa1;
          aAlfa1 = aAlfa1 % 101;
          |SI aAlfa1 = "8";
               |CONFI "    SDesea recalcular las nominas a continuacion S/N: ";
               |SI FSalida = 0;
                    |PTEC 802;          || Intro
                    |PTEC 815;          || AvPag
                    |PTEC 802;          || Intro
               |FINSI;
          |SINO;
               |SI swFiniq = 0;
                    |MENAV "    Deberá recalcular las nóminas a continuación.";
               |SINO;
                    aAlfa1 = "    Deberá recalcular el finiquito a continuación ";
                    aAlfa1 = aAlfa1 + "para tener en cuenta el %IRPF regularizado.";
                    |MENAV aAlfa1;
               |FINSI;
          |FINSI;
     |FINSI;
     |POPV;
|FPRC;

|PROCESO Pintalo;
     |PINTA 2171, #nomir008#0;
     |PINTA 2271, #nomir008#1;
     nNroTrab = nNroTrab + 1;
     aAlfa1 = nNroTrab; |QBF aAlfa1;
     aAlfa2 = nTotalTrab; |QBF aAlfa2;
     aAlfa = "    Estimando ingresos y comprobando regularización. Trabajador " + aAlfa1 + " de " + aAlfa2 + ".";
     |MENSA aAlfa;
|FPRC;

|PROCESO Graba012_8;
     #nomir012#1 = #nomir008#0;
     #nomir012#2 = #nomir008#1;
     #nomir012#3 = nElAnyIRPF;
     #nomir012#4 = nElMesIRPF;
     |LEE 101#nomir012.=;

     |SI FSalida = 0;
          #nomir012#85 = #nomir008#4;
          #nomir012#86 = #nomir008#7;
          #nomir012#106 = #nomir009#15;
          #nomir012#107 = #labtraba#29;
          |GRABA 020#nomir012.a;
          |LIBERA #nomir012;
     |FINSI;
|FPRC;

|PROCESO Regulariza_1;
     |HAZ Pintalo;
     |HAZ LeeTrabajador;
     |DDEFECTO #nomir001;
     #nomir001#0 = #labtraba#7;         || nif
     #nomir001#2 = nElAnyIRPF;          || ejercicio
     #nomir001#3 = #labtraba#0;         || empresa
     |LEE 101#nomir001.=;
     |SI FSalida ! 0;
          #nomir001#1 = #labtraba#2;    || apellidos y nombre
          #nomir001#4 = #labtraba#1;    || cod.trabajador
          #nomir001#5 = "N";            || actualizado NO
          |GRABA 020#nomir001.b;
     |SINO;
          #nomir001#5 = "N";            || actualizado NO
          |GRABA 020#nomir001.a;
     |FINSI;

     |HAZ Punto02;            || esta en el nomir001

     |HAZ LeeTrabajador;
     ||HAZ Los400;
     |SI swFiniq = 1; |Y #103#0 = #103#2;
          #nomir008#7 = #nomir008#4;
          || si es finiquito y tengo los mismos ingresos que tenia porque lo que
          || tenga de finiquito sea exento de IRPF, para que no me diga que tengo
          || que regularizar a cero igualo los porcentajes para excluirlo de la
          || lista de trabajadores a regularizar 26.11.2008
     |FINSI;
     |SI #nomir008#4 = #nomir008#7; |Y swTipoMedio ! 1;
          |BORRA 020#nomir008.a;
          |HAZ Graba012_8;
     |SINO;
          |DDEFECTO #nomir009;
          #nomir009#0 = #nomir008#0;
          |LEE 000#nomir009.=;
          |SI #nomir009#15 = 0; |O swTipoMedio = 1;
               |SI swTipoMedio = 1;
                    swAvisaMedio = 1;
                    #nomir008#15 = "M";
                    #nomir008#16 = nTIPOEst;
               |FINSI;
               |SI #nomir008#4 > #nomir008#7; |Y #labtraba#29 = 2;
                    |BORRA 020#nomir008.a;   || tipo "2" regularizacion, IRPF
                    || fijo avisando si sube, pero si baja ni siquiera lo
                    || tiene que mostrar
                    |HAZ Graba012_8;
               |SINO;                        || resto de casos, lo de siempre
                    |GRABA 020#nomir008.a;
                    nCuantos = nCuantos + 1;
                    |HAZ Graba012_8;
               |FINSI;
          |SINO;
               |SI #nomir008#4 < #nomir008#7;
                    |SI #nomir009#15 = 1;
                         |GRABA 020#nomir008.a;
                         nCuantos = nCuantos + 1;
                    |SINO;
                         |BORRA 020#nomir008.a;
                    |FINSI;
                    |HAZ Graba012_8;
               |FINSI;
               |SI #nomir008#4 > #nomir008#7;
                    |SI #nomir009#15 = 2;
                         |GRABA 020#nomir008.a;
                         nCuantos = nCuantos + 1;
                    |SINO;
                         |BORRA 020#nomir008.a;
                    |FINSI;
                    |HAZ Graba012_8;
               |FINSI;
          |FINSI;
     |FINSI;
     |LIBERA #nomir001;
     |LIBERA #nomir008;
|FPRC;

|DEFBUCLE Regulariza;
     #nomir008#1;
     ;
         1,     1;
     99999, 99999;
     be;
     NULL, Regulariza_1;
|FIN;

|PROCESO SiBaja2;
     nCausaBaja = 0;

     |ABRE #nomvarca;
     #nomvarca#0 = #2#0;
     #nomvarca#1 = #2#1;
     #nomvarca#2 = nElMesIRPF;
     #nomvarca#3 = 0;
     |LEE 000#nomvarca.=;
     |SI FSalida = 0;
          aAlfa1 = #nomvarca#16;
          |QBF aAlfa1;
          |SI aAlfa1 ! ""; |Y aAlfa1 ! "..........";
               nCausaBaja = #nomvarca#46;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Opcion1;
     nNume1 = #nomir008#12;        || alta
     nNume2 = #nomir008#13;        || baja
     |SI nNume2 < nNume1;
          aAlfa1 = "31.12." + #copcalcu@#9;
          #nomir008#13 = aAlfa1;
     |FINSI;

     aAlfa1 = #nomir008#13 % -104;
     nNume1 = aAlfa1;
     nNume2 = #copcalcu@#9;
     |SI nNume1 < nNume2;
          aAlfa1 = "31.12." + #copcalcu@#9;
          #nomir008#13 = aAlfa1;
     |FINSI;
|FPRC;

|PROCESO Opcion2;
     aAlfa1 = "31.12." + #copcalcu@#9;
     #nomir008#13 = aAlfa1;
|FPRC;

|PROCESO RevisaPeriodo;
     |DDEFECTO #nomir009;
     |ABRE #nomir009;
     #nomir009#0 = #labempre#0;
     |LEE 000#nomir009.=;
     |CIERRA #nomir009;

     |HAZ SiBaja2;

     |SI nCausaBaja = 8; |O nCausaBaja = 9; |O nCausaBaja = 10;
     |O nCausaBaja = 11; |O nCausaBaja = 12;
          || si es motivo de baja de continuar contrato
          || estimo siempre hasta final de a¤o
          |HAZ Opcion2;
          |ACABA;
     |FINSI;

     |SI #nomir009#17 = 2;                   || parametro de estimar hasta 31/12
          alfa2 = #nomcalca#5 % 402;
          alfa3 = #nomcalca#5 % -104;
          mes = alfa2;
          any = alfa3;
          |SI mes = nElMesIRPF; |Y any = nElAnyIRPF;     || soy baja este mes y ya tengo
               || si soy baja este mes, aunque tenga el parametro a "2"
               || no estimo mas de este mes, actuo como el parametro a "1"

               |HAZ Opcion1;
          |SINO;
               || periodo completo
               |HAZ Opcion2;
          |FINSI;
     |SINO;
          |SI #nomir009#17 = 3;
               || periodo completo, teniendo en cuenta fecha de alta y de baja
               || novedad 24.04.2008

               || pero claro, si ya tengo otra ficha ... NO
                aAlfa1 = "01.01." + #copcalcu@#9;
                #nomir008#12 = aAlfa1;

                aAlfa1 = "31.12." + #copcalcu@#9;
                #nomir008#13 = aAlfa1;
          |SINO;
               || la opcion uno, segun fechas de alta y baja
               |HAZ Opcion1;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO LeePeriodo;
     |ABRE #106;
     #106#0 = #nomir008#0;
     #106#1 = #nomir008#1;
     |LEE 101#106=;
     |SI FSalida = 0;
          fFech1 = #106#2;   nNume1 = fFech1;
          |SI nNume1 > 0;    #nomir008#12 = #106#2;    |FINSI;
          fFech1 = #106#3;    nNume1 = fFech1;
          |SI nNume1 > 0;    #nomir008#13 = #106#3;    |FINSI;
     |FINSI;
     |LIBERA #106;
     |CIERRA #106;
|FPRC;

|PROCESO LeeTrabajador;
     |ABRET #labtraba;
     |SELECT #1#labtraba;
     |DDEFECTO #labtraba;
     #labtraba#0 = #nomir008#0;
     #labtraba#1 = #nomir008#1;
     |LEE 000#labtraba.=;
     |CIERRAT #labtraba;
|FPRC;

|PROCESO LeeEmpresa;
     |DDEFECTO #labempre;
     #labempre#0 = #nomir008#0;
     |LEE 000#labempre.=;
|FPRC;

|PROCESO chequea2;
/*
     |ABRE #nomcalca;
     #nomcalca#0 = #labtraba#0;
     #nomcalca#1 = #labtraba#1;
     #nomcalca#2 = nElMesIRPF;
     #nomcalca#3 = nTipo;
     |LEE 000#nomcalca.=;
     |SI FSalida = 0;
*/
          alfa2 = #nomcalca#5 % 402;
          alfa3 = #nomcalca#5 % -104;
          mes = alfa2;
          any = alfa3;
          |SI mes = nElMesIRPF; |Y any = nElAnyIRPF;     || soy baja este mes y ya tengo
               |ABRE #nomvarca;              || la nomina calculada,
               #nomvarca#0 = #labtraba#0;    || busco en el nomvarca y si el motivo
               #nomvarca#1 = #labtraba#1;    || de baja no es 08 (cambio de algo)
               #nomvarca#2 = nElMesIRPF;         || entonces no recalculo
               #nomvarca#3 = nTipo;
               |LEE 000#nomvarca.=;
               |SI FSalida = 0;
                    |SI #nomcalca#5 = #nomvarca#16; |Y #nomvarca#46 ! 08;
                    |Y #nomvarca#46 ! 09; |Y #nomvarca#46 ! 10;
                         nSwSiLeToca = 1;
                    |FINSI;
               |FINSI;
               |CIERRA #nomvarca;
          |FINSI;
/*
     |FINSI;
     |CIERRA #nomcalca;
*/
|FPRC;

|PROCESO chequeaBloqueo;
     |SI #nomcalca#145 = "S";
          nSwSiLeToca = 1;
     |FINSI;
|FPRC;

|PROCESO SiLeToca;
     nSwSiLeToca = 0;
     nPara4 = nElMesIRPF + 20;
     |SI #nomcontr#nPara4# = "N";
          nSwSiLeToca = 1;
     |FINSI;

     #nomir009#0 = #nomcalca#0;
     |LEE 000#nomir009.=;
     |SI FSalida = 0;
          nPara4 = nElMesIRPF;
          |SI #nomir009#nPara4# = "N"; |Y swFiniq = 0;
               nSwSiLeToca = 1;
          |SINO;
               nSwSiLeToca = 0;
          |FINSI;
     |FINSI;

     |ABRE #labtraba;
     #labtraba#0 = #nomcalca#0;
     #labtraba#1 = #nomcalca#1;
     |LEE 000#labtraba.=;
     |SI #labtraba#29 = 0;
          nSwSiLeToca = 1;
     |FINSI;

     |SI #labtraba#42 = "E";  || los que son agrarios eventuales no regularizan
          || nSwSiLeToca = 1; || ya si, 12.09.2012
     |FINSI;

     |SI #copcalcu@#11 = "N";
          |HAZ chequea2;
     |FINSI;

     |HAZ chequeaBloqueo;

     |SI eaVengoDe = "gomcalcu";
          |SI #labtraba#247 ! 613; |Y #labtraba#247 ! 163;
               nSwSiLeToca = 1;
          |FINSI;
     |FINSI;
     |SI eaVengoDe = "nomcalcu";
          |SI #labtraba#247 = 613; |O #labtraba#247 = 163;
               nSwSiLeToca = 1;
          |FINSI;
     |FINSI;

     |CIERRA #labtraba;

     |DDEFECTO #labempre;               || comprueba si la empresa esta
     #labempre#0 = #labtraba#0;         || desactivada en los calculos
     |LEE 000#labempre.=;
     |SI #labempre#50 = "S";
          nSwSiLeToca = 1;
     |FINSI;
|FPRC;

|PROCESO SelCentros;
     || solo para seleccion desde-hasta !!!
     |SI #copcalcu@#21 = 0; |Y #copcalcu@#63 = 0;
          |ABRE #labtraba;
          #labtraba#0 = #nomcalca#0;
          #labtraba#1 = #nomcalca#1;
          |LEE 000#labtraba.=;
          |SI FSalida = 0;
               |SI #labtraba#77 < #copcalcu@#71; |O #labtraba#77 > #copcalcu@#72;
                    nSwSiLeToca = 1;
               |FINSI;
          |FINSI;
          |CIERRA #labtraba;
     |FINSI;
|FPRC;

|PROCESO CargaInicial_1;
     |HAZ SiLeToca;
     |HAZ SelCentros;
     |SI nSwSiLeToca = 1;     |ACABA;   |FINSI;

     |DDEFECTO #nomir008;
     #nomir008#0 = #nomcalca#0;         || empresa
     #nomir008#1 = #nomcalca#1;         || trabajador
     |LEE 000#nomir008.=;
     |SI FSalida ! 0;
          |HAZ LeeEmpresa;
          |HAZ LeeTrabajador;
          #nomir008#4 = #labtraba#30;        || tipo actual
          #nomir008#10 = #labempre#2;        || nombre empresa
          #nomir008#11 = #labtraba#2;        || nombre trabajador
          #nomir008#12 = #nomcalca#4;        || fecha de alta
          #nomir008#13 = #nomcalca#5;        || fecha de baja
          |SI #nomcalca#5 = "..........";|O #nomcalca#5 = "          ";
               #labregis#1 = #nomcalca#0;
               #labregis#2 = #nomcalca#1;
               |LEE 000#labregis.=;                  || fecha de baja de contratos
               |SI FSalida = 0;
                    |SI #labregis#15 = 0;
                         #nomir008#13 = #labregis#8;
                    |SINO;
                         #nomir008#13 = #labregis#20;
                    |FINSI;
               |FINSI;
               aAlfa = #labtraba#177;
               |QBF aAlfa;
               |SI aAlfa ! "";
                    #nomir008#13 = aAlfa;
               |FINSI;
          |FINSI;
          |HAZ LeePeriodo;
          |HAZ RevisaPeriodo;
          #nomir008#14 = #labtraba#7;        || DNI
          #nomir008#17 = #labtraba#45;        || DNI
          |GRABA 020#nomir008.n;
          |SI FSalida = 0;
               nSwAlgo = nSwAlgo + 1;
               nTotalTrab = nTotalTrab + 1;
          |FINSI;

     |FINSI;
|FPRC;

|PROCESO Regulariza_nomir008;
     |HAZ CargaInicial_1;
|FPRC;

|DEFBUCLE nomcalcg;
     #nomcalca#1;
     ;
     #nomgruel#1, 00001, nElMesIRPF, nTipo;
     #nomgruel#1, 99999, nElMesIRPF, nTipo;
     ;
     NULL, Regulariza_nomir008;
|FIN;

|PROCESO CalGrupo;       || para cada una hace el proceso de siemrpe
     |BUCLE nomcalcg;
|FPRC;

|DEFBUCLE Grupo;         || lee empresas del grupo
     #nomgruel#1;
     ;
     nGrupo, 00001;
     nGrupo, 99999;
     ;
     NULL, CalGrupo;
|FIN;

|PROCESO PorGrupos;
     swPorGrupos = 1;
     |PARA nCampoG = 63; |SI nCampoG [ 70; |HACIENDO nCampoG = nCampoG + 1;
          nGrupo = #copcalcu@#nCampoG#;
          |BUCLE Grupo;
     |SIGUE;
|FPRC;

|DEFBUCLE DesdeHasta;
     #nomcalca#1;
     ;
     nDesdeEmp, nDesdeTra, nElMesIRPF, nTipo;
     nHastaEmp, nHastaTra, nElMesIRPF, nTipo;
     ;
     NULL, Regulariza_nomir008;
|FIN;

|PROCESO Salteada;
|PARA salte_1 = 21; |SI salte_1 [ 41; |HACIENDO salte_1 = salte_1 + 1;
     salte_2 = salte_1 + 21;
     #nomcalca#0 = #copcalcu@#salte_1#;
     #nomcalca#1 = #copcalcu@#salte_2#;
     #nomcalca#2 = nElMesIRPF;
     #nomcalca#3 = 0;
     |LEE 000#nomcalca.=;
     |SI FSalida = 0;
          |HAZ Regulariza_nomir008;
     |FINSI;
|SIGUE;
|FPRC;

|PROCESO Imprimelo;
     #labtraba#0 = #nomir008#0;
     #labtraba#1 = #nomir008#1;
     |LEE 000#labtraba.=;
     nEst = #nomir008#16;
     |PRINT;
|FPRC;

|DEFBUCLE Impresion;
     #nomir008#1;
     9;
         1,     1, aDActua;
     99999, 99999, aHActua;
     ;
     NULL, Imprimelo;
|FIN;

|PROCESO BuscaMedios;
     swCab = 1;
|FPRC;

|DEFBUCLE BuscaMedios;
     #nomir008#1;
     9, 15;
         1,     1, aDActua, "M";
     99999, 99999, aHActua, "M";
     ;
     NULL, BuscaMedios;
|FIN;

|PROCESO Cabecera;
     swCab = 0;

     |BUCLE BuscaMedios;

     |SI swCab = 1;
          aRaya = "-----";
          aEnero = "ENERO";
          aEstim = "%Est.";
     |SINO;
          aRaya = "";
          aEnero = "";
          aEstim = "";
     |FINSI;
|FPRC;

|PROCESO fecha;
     aMes = #copcalcu@#8;
     aMes = "00" + aMes;
     aMes = aMes % -102;
     aAnyo = #copcalcu@#9;

     |SI nOpcion = 4;         || si vengo "Informe Previo"
          aDActua = "  ";
          aHActua = "zz";
     |FINSI;

     |HAZ Cabecera;           || a ver si imprimo o no la parte de %ENERO
                              || de la cabecera
|FPRC;

|PROCESO Informe;
     |IMPRE 0;
     |SI FSalida = 0;
          |ABRE #labtraba;
          |INFOR "nomir008";
          |HAZ fecha;
          |BUCLE Impresion;
          |PIEINF;
          |FININF;
          |CIERRA #labtraba;
     |SINO;
          |MENAV "0000Proceso abortado.";
     |FINSI;
     |FINIMP;
|FPRC;

|PROGRAMA;
     |SI nElMesIRPF = 0;|O nElAnyIRPF = 0;
          |MENAV "    Parametros incorrectos [nomir008] ...";
          |ACABA;
     |FINSI;
     |SI nDiferencias = 1;
          |SI nVEmp = 0;
               |MENAV "    Parametros incorrectos [nomir008] ...";
               |ACABA;
          |FINSI;
     |FINSI;

     |DDEF aDef;
     aDef = aDef + eaVengoDe;
     |CARGA_DEF aDef, copcalcu@;
     FCargaDef = FSalida;

     aAlfa = Usuario;         |QBF aAlfa;
     aAlfa = "n" +  aAlfa;
     |NOME_DAT #copcalcu@#aAlfa#;

     |SI FCargaDef ! 0;
          |ACABA;
     |FINSI;

     |DDEFECTO #nomcontr;
     |ABRE #nomcontr;
     |LEE 000#nomcontr.p;
     |CIERRA #nomcontr;

     |ABRET #labempre;
     |ABRET #nomir001;
     |ABRET #nomcalca;

     aAlfa1 = Usuario;   |QBF aAlfa1;   aAlfa1 = "1" + aAlfa1;
     |NOME_DAT #2000 aAlfa1;
     |DELINDEX #nomir008;
     |ABRET #copcalcu@;
     |ABRET #nomir008;
     |ABRET #nomir009;
     |ABRE #nomir012;

     |ABRET #labregis;
     |SELECT #2#labregis;
     |SI nDiferencias = 0;
          |LEE 000#copcalcu@.p;
          |SI FSalida = 0; |O swFiniq = 1;
               |SI swFiniq = 0;    || Regularizacion Mensual
                    nTipo = 0;
               |SINO;              || NUEVO 09.09.2004: Regularizamos en Finiquito
                    #copcalcu@#0 = nEmp;
                    #copcalcu@#1 = nEmp;
                    #copcalcu@#2 = nTra;
                    #copcalcu@#3 = nTra;
                    #copcalcu@#8 = nElMesIRPF;
                    #copcalcu@#9 = nElAnyIRPF;
                    #copcalcu@#21 = 0;
                    #copcalcu@#63 = 0;
                    nTipo = 2;
               |FINSI;
               |SI #copcalcu@#21 = 0;
                    |SI #copcalcu@#63 = 0;
                         nDesdeEmp = #copcalcu@#0;
                         nHastaEmp = #copcalcu@#1;
                         nDesdeTra = #copcalcu@#2;
                         nHastaTra = #copcalcu@#3;
                         |BUCLE DesdeHasta;
                    |SINO;
                         |HAZ PorGrupos;
                    |FINSI;
               |SINO;
                    |HAZ Salteada;
               |FINSI;
          |FINSI;
     |SINO;
          |MENAV "    probando ...";
     |FINSI;
     |CIERRAT #copcalcu@;
     |CIERRAT #nomir008;
     |CIERRAT #labregis;

     |SI nSwAlgo ! 0;
          |MENSA "    Regularizando tipos de IRPF ...";
          nElTop = nElMesIRPF - 1;
          nCuantos = 0;
          |BUCLE Regulariza;
          aAlfa1 = "    Generadas [" + nCuantos + "] regularizaciones de [" + nSwAlgo + "] posibles ...";
          |MENAV aAlfa1;
          |SI nCuantos ! 0;
               |SI swAvisaMedio = 1;
                    aAlfa1 = "    ATENCION: Aplicado el tipo medio de IRPF del año ";
                    aAlfa1 = aAlfa1 + "anterior a los trabajadores con fondo gris";
                    |MENAV aAlfa1;
               |FINSI;
               |HAZ Presenta;
          |FINSI;
     |FINSI;

     |CIERRAT #labempre;
     |CIERRAT #nomir001;
     |CIERRAT #nomcalca;
     |CIERRAT #nomir009;
     |CIERRA #nomir012;

     |SI FCargaDef = 0;
          |DESCARGA_DEF #copcalcu@;
     |FINSI;

     |DELINDEX #nomir008;
|FPRO;
