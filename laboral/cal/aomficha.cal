|FICHEROS;
    aomficha #0;
    labempre #1;         || empresas
    labtraba #2;         || trabajadores
    aomobras #3;         || obras
    aomhoras #4;         || horas
    aomentra #5;         || entrada de horas
    aomficim #6;         || ayuda impresion
|FIN;

|VARIABLES;
    alfa1 = "";
    alfa2 = "";
    alfa3 = "";
    alfa4 = "";
    nume1 = 0;
    nume2 = 0;
    nume3 = 0;
    nume4 = 0;
    para1 = 0;
    para2 = 0;
    para3 = 0;
    para4 = 0;
    fech1 = @;
    fech2 = @;
    fech3 = @;
    fech4 = @;
    elsw1 = 0;
    elsw2 = 0;
    elsw3 = 0;
    elsw4 = 0;
    FEstado = 0;
    seleccio = 0;
    modo = 0;
    sw_rup = 0;

    p_mes = "";
    bisiesto = 0;
    topemes = 0;
    dfech = @;
    hfech = @;
    ant_obra = 0;
    ant_empr = 0;
    ant_trab = 0;
    ant_hora = 0;
    &horas = 0;
    &desta = 0;
    &dperi = @;
    &hperi = @;
    diasemana = "";
    diames = 0;
|FIN;
____________________________________/ DESDE CAMPOS
|PROCESO defecto; |TIPO 7;
    fech1 = ~;
    alfa1 = fech1;
    alfa1 = alfa1 % -104;
    #0Campo = alfa1;
|PINTA #0Campo;
|FPRC;

|PROCESO mirames; |TIPO 7;
    fech1 = ~;
    alfa1 = fech1;
    alfa1 = alfa1 % 402;
    #0Campo = alfa1;
|PINTA #0Campo;
|FPRC;

|PROCESO pintames; |TIPO 0;
    bisiesto = 0;
    nume1 = #0#5 $ 4;
|SI nume1 = 0;
        bisiesto = 1;
|FINSI;
|SI #0#4 =  1;  p_mes = "ENERO     ";  topemes = 31; |FINSI;
|SI #0#4 =  2;  p_mes = "FEBRERO   ";  topemes = 28+bisiesto; |FINSI;
|SI #0#4 =  3;  p_mes = "MARZO     ";  topemes = 31; |FINSI;
|SI #0#4 =  4;  p_mes = "ABRIL     ";  topemes = 30; |FINSI;
|SI #0#4 =  5;  p_mes = "MAYO      ";  topemes = 31; |FINSI;
|SI #0#4 =  6;  p_mes = "JUNIO     ";  topemes = 30; |FINSI;
|SI #0#4 =  7;  p_mes = "JULIO     ";  topemes = 31; |FINSI;
|SI #0#4 =  8;  p_mes = "AGOSTO    ";  topemes = 31; |FINSI;
|SI #0#4 =  9;  p_mes = "SEPTIEMBRE";  topemes = 30; |FINSI;
|SI #0#4 = 10;  p_mes = "OCTUBRE   ";  topemes = 31; |FINSI;
|SI #0#4 = 11;  p_mes = "NOVIEMBRE ";  topemes = 30; |FINSI;
|SI #0#4 = 12;  p_mes = "DICIEMBRE ";  topemes = 31; |FINSI;
|PINTA 1252, p_mes;

    alfa2 = #0#4;   alfa2 = ("00" + alfa2) % -102;

    alfa1 = "01." + alfa2 + "." + #0#5;
    dfech = alfa1;

    alfa1 = topemes + "." + alfa2 + "." + #0#5;
    hfech = alfa1;
|FPRC;
____________________________________/
|PROCESO horas_dia;
    alfa1 = #5#0;
    alfa1 = alfa1 % 102;
    diames = alfa1;
    diames = diames + 31;
    #6diames = #6diames + #5#5;
|FPRC;

|PROCESO detalle;
|DDEFECTO #4;  #4#0 = ant_hora;  |LEE 000#4=;   || horas
|PRINT;
    horas = 0;
    desta = 0;
    ant_hora = #5#4;
|FPRC;

|PROCESO abre_informe;
    ant_obra = #5#1; ant_empr = #5#2;    ant_trab = #5#3;   ant_hora = #5#4;
|DDEFECTO #1;  #1#0 = #5#2;                  |LEE 000#1=;   || empresa
|DDEFECTO #2;  #2#0 = #5#2;   #2#1 = #5#3;   |LEE 000#2=;   || trabajador
|DDEFECTO #3;  #3#0 = #5#1;                  |LEE 000#3=;   || obras
|PDEFECTO #6, 32, 63;
    dperi = #2#36;  |SI dperi < dfech;  dperi = dfech; |FINSI;

|SI #2#37 = "          ";|O #2#37 = ".........."; #2#37 = "31.12.9999"; |FINSI;
    hperi = #2#37;  |SI hperi > hfech;  hperi = hfech; |FINSI;

|INFOR "aomficha";
|FPRC;

|PROCESO entrada1;
|SI sw_rup = 0;
        sw_rup = 1;
    |HAZ abre_informe;
|FINSI;

|SI ant_obra ! #5#1;|O ant_empr ! #5#2;|O ant_trab ! #5#3;
    |HAZ detalle;
    |PIEINF;
    |FININF;
    |HAZ abre_informe;
|SINO;
    |SI ant_hora ! #5#4;
        |HAZ detalle;
    |FINSI;
|FINSI;

|DDEFECTO #4;  #4#0 = #5#4;  |LEE 000#4=;   || horas
    horas = horas + #5#5;
    desta = desta + #5#6;
|HAZ horas_dia;
|FPRC;

|DEFBUCLE entrada;
 #5#4;
 ;
 #0#6, #0#0, #0#2,  0, #0#8, dfech;
 #0#7, #0#1, #0#3, 99, #0#9, hfech;
 ;
 NULL, entrada1;
|FIN;

|PROCESO carga_semana;
    diames = 0;
|PARA fech1 = dfech; |SI fech1 [ hfech; |HACIENDO fech1 = fech1 + 1;
        diames = diames + 1;
        fech2 = fech1 % 1;
        alfa1 = fech2;
        diasemana = alfa1 % 11;
    |SI diasemana = "Lunes";     #6diames = "L"; |FINSI;
    |SI diasemana = "Martes";    #6diames = "M"; |FINSI;
    |SI diasemana = "Miercoles"; #6diames = "X"; |FINSI;
    |SI diasemana = "Jueves";    #6diames = "J"; |FINSI;
    |SI diasemana = "Viernes";   #6diames = "V"; |FINSI;
    |SI diasemana = "Sabado";    #6diames = "S"; |FINSI;
    |SI diasemana = "Domingo";   #6diames = "D"; |FINSI;
|SIGUE;
|FPRC;

|PROCESO tipo3; |TIPO 3;
|IMPRE 0;
|SI FSalida = 0;
    |HAZ carga_semana;
    |ABRE #1;
    |ABRE #2;
    |ABRE #3;
    |ABRE #4;
    |BUCLE entrada;
    |SI sw_rup ! 0;
        |HAZ detalle;
        |PIEINF;
        |FININF;
    |FINSI;
    |CIERRA #1;
    |CIERRA #2;
    |CIERRA #3;
    |CIERRA #4;
|SINO;
    |MENAV "    Proceso abortado ...";
|FINSI;
|FINIMP;
|FPRC;
