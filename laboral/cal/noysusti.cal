|FICHEROS;
    noysusti #0;
    labtraba #2;    || trabajadores
    nomvarca #7;    || variaciones cabs.
    nomvarli #8;    || variaciones lins.
    noysustc #10;   || control sustituciones cabs.
    noysustl #11;   || control sustituciones lins.
    nomcatli #12;   || aux. categorias lins.
    nomtrali #13;   || aux. trabajadores lins.
    nomconli #14;   || convenios lins.
|FIN;

|VARIABLES;
    alfa1 = "";
    alfa2 = "";
    alfa3 = "";
    alfa4 = "";
    nume1 = 0;
    nume2 = 0;
    nume3 = 0;
    nume4 = 0;
    para1 = 0;
    para2 = 0;
    para3 = 0;
    para4 = 0;
    fech1 = @;
    fech2 = @;
    fech3 = @;
    fech4 = @;
    elsw1 = 0;
    elsw2 = 0;
    elsw3 = 0;
    elsw4 = 0;
    FEstado = 0;
    seleccio = 0;
    modo = 0;

    &runnom = "noysusti";
    p_mes = "";
    bisiesto = 0;
    topemes = 0;
    concepto = 0;
    diez = "          ";
    unid = "";
    unid1 = 0;
    unid2 = 0;
    valo = "";
    valo1 = 0;
    valo2 = 0;
    coefi1 = 0;
    coefi2 = 0;
    v_emp = 0;
    v_tra = 0;
    v_con = 0;
    v_cat = 0;
    w_con = 0;
    w_cat = 0;

    &ay_emp = 0;
    &ay_tra = 0;
    &ay_mes = 0;
    &ay_any = 0;
    &ay_ant = "";
    &EURODB = 0;
|FIN;
____________________________________/ PROCESOS PANTALLA
|PROCESO mirames; |TIPO 7; ||CALCULA EL MES POR DEFECTO SEGUN FECHA SISTEMA
    fech1 = ~;
    alfa1 = fech1;
    alfa1 = alfa1 % 402;
    #0Campo = alfa1;
|PINTA #0Campo;
|FPRC;

|PROCESO pintames; |TIPO 0;      || PINTA EL MES LITERAL
|SI #0Campo =  1;  p_mes = "ENERO     ";  topemes = 31;  |FINSI;
|SI #0Campo =  2;  p_mes = "FEBRERO   ";  topemes = 28+bisiesto;  |FINSI;
|SI #0Campo =  3;  p_mes = "MARZO     ";  topemes = 31;  |FINSI;
|SI #0Campo =  4;  p_mes = "ABRIL     ";  topemes = 30;  |FINSI;
|SI #0Campo =  5;  p_mes = "MAYO      ";  topemes = 31;  |FINSI;
|SI #0Campo =  6;  p_mes = "JUNIO     ";  topemes = 30;  |FINSI;
|SI #0Campo =  7;  p_mes = "JULIO     ";  topemes = 31;  |FINSI;
|SI #0Campo =  8;  p_mes = "AGOSTO    ";  topemes = 31;  |FINSI;
|SI #0Campo =  9;  p_mes = "SEPTIEMBRE";  topemes = 30;  |FINSI;
|SI #0Campo = 10;  p_mes = "OCTUBRE   ";  topemes = 31;  |FINSI;
|SI #0Campo = 11;  p_mes = "NOVIEMBRE ";  topemes = 30;  |FINSI;
|SI #0Campo = 12;  p_mes = "DICIEMBRE ";  topemes = 31;  |FINSI;
|PINTA 1335, p_mes;
|FPRC;

|PROCESO defecto; |TIPO 7; ||calcula el a¤o por defecto segun fecha sistema
    fech1 = ~;
    alfa1 = fech1;
    alfa1 = alfa1 % -104;
    #0Campo = alfa1;
|PINTA #0Campo;

    nume1 = #0Campo $ 4;
|SI nume1 = 0; bisiesto = 1;  |SINO;    bisiesto = 0;  |FINSI;
|FPRC;
____________________________________/ PROCESOS COMUNES
|PROCESO lee_valores;
    unid = diez;
    valo = diez;
|ABRE #12;
    #12#0 = v_con;       || convenio
    #12#1 = v_cat;       || categoria
    #12#2 = concepto;    || concepto
|LEE 000#12=;
|SI FSalida = 0;               || lee aux. categorias lins.
    |SI #12#3 ! diez;     unid = #12#3;  |FINSI;
    |SI #12#4 ! diez;     valo = #12#4;  |FINSI;
|FINSI;
|CIERRA #12;

|ABRE #13;
    #13#0 = v_emp;       || empresa
    #13#1 = v_tra;       || trabajador
    #13#2 = concepto;    || concepto
|LEE 000#13=;
|SI FSalida = 0;               || lee aux. trabajadores lins.
    |SI #13#3 ! diez;     unid = #13#3;  |FINSI;
    |SI #13#4 ! diez;     valo = #13#4;  |FINSI;
|FINSI;
|CIERRA #13;

|ABRE #8;
    #8#0 = v_emp;        || empresa
    #8#1 = v_tra;        || trabajador
    #8#2 = #0#2;         || mes
    #8#3 = 0;            || tipo
    #8#4 = concepto;     || concepto
|LEE 000#8=;
|SI FSalida = 0;               || lee variaciones lins.
    |SI #8#5 ! diez;     unid = #8#5;  |FINSI;
    |SI #8#6 ! diez;     valo = #8#6;  |FINSI;
|FINSI;
|CIERRA #8;

|HAZ cal_unid;
|FPRC;

|PROCESO cal_unid;
|SI #14#3 = "D";
        unid = topemes;
|FINSI;
|SI #14#3 = "M";
    |SI #2#42="D";|O #2#42="T";                           unid = topemes; |FINSI;
    |SI #2#42="M";|O #2#42="X";|O #2#42="L";|O #2#42="N"; unid = 30;      |FINSI;
|FINSI;
|FPRC;
____________________________________/ REGRABA VARIACIONES MODIFICADAS
|PROCESO variaci1;
    concepto = #8#4;
|HAZ lee_conv;
|SI FEstado = 0;
        v_emp = #0#3;
        v_tra = #0#4;
        v_con = w_con;
        v_cat = w_cat;
    |HAZ lee_valores;
|FINSI;
|FPRC;

|PROCESO variaci0;
|ABRE #7;
    #7#0 = v_emp;
    #7#1 = v_tra;
    #7#2 = #0#2;
    #7#3 = 0;
|LEE 000#7=;
|SI FSalida = 0;
    |BUCLELIN 0variaci1#7;
|FINSI;
|CIERRA #7;
|FPRC;
____________________________________/ GRABA VARIACIONES
|PROCESO var_cabs;
|ABRE #7;
    #7#0 = v_emp;   || empresa
    #7#1 = v_tra;   || trabajador
    #7#2 = #0#2;    || mes
    #7#3 = 0;       || tipo
|LEE 000#7=;
|SI FSalida ! 0;
    |GRABA 020#7n;
|FINSI;
|CIERRA #7;
|FPRC;

|PROCESO var_lins;
|ABRE #8;
    #8#0 = v_emp;   || empresa
    #8#1 = v_tra;   || trabajador
    #8#2 = #0#2;    || mes
    #8#3 = 0;       || tipo
    #8#4 = concepto;     || concepto
|LEE 101#8=;
    FEstado = FSalida;
    alfa1 = unid;   alfa1 = "          " + alfa1;  alfa1 = alfa1 % -110;
    #8#5 = alfa1;   || unidades
    alfa1 = valo;   alfa1 = "          " + alfa1;  alfa1 = alfa1 % -110;
    #8#6 = alfa1;   || importe
|SI FEstado = 0;    |GRABA 020#8a; |FINSI;
|SI FEstado ! 0;    |GRABA 020#8n; |FINSI;
|LIBERA #8;
|CIERRA #8;
|FPRC;

|PROCESO lee_conv;
|DDEFECTO #14;
|ABRE #14;
    #14#0 = #2#87;
    #14#2 = concepto;
|LEE 000#14=;
    FEstado = FSalida;
|CIERRA #14;
|FPRC;

|PROCESO coefi;
    coefi1 = 0;
    coefi2 = 0;
|SI #2#42 = "M";|O #2#42 = "X";|O #2#42 = "L";|O #2#42 = "N";
        coefi2 = #0#5 / 30;
|FINSI;
|SI #2#42 = "D";|O #2#42 = "T";
        coefi2 = #0#5 / topemes;
|FINSI;
|SI coefi2 > 1; coefi2 = 1;    |FINSI;
    coefi1 = 1 - coefi2;
|FPRC;

|PROCESO sustitu2;
    nume1 = (unid1 * valo1) * coefi1;        || valor sustituto
    nume2 = (unid2 * valo2) * coefi2;        || valor sustituido
    nume3 = nume1 + nume2;                   || total
    nume4 = (nume3 / unid1) ! (2+EURODB);    || nuevo valor

    unid = unid1;
    valo = nume4;
|FPRC;

|PROCESO sustitu1;
    concepto = #11#1;
|HAZ lee_conv;
|SI FEstado = 0;
        v_emp = #0#0;    v_tra = #0#1;  v_con = #2#87; v_cat = #2#17;
    |HAZ lee_valores;
        unid1 = unid;    valo1 = valo;       || sustituto

        v_emp = #0#3;    v_tra = #0#4;  v_con = w_con; v_cat = w_cat;
    |HAZ lee_valores;
        unid2 = unid;    valo2 = valo;       || sustituido

    |SI unid1 ! 0;|O unid2 ! 0;|O valo1 ! 0;|O valo2 ! 0;
        |HAZ sustitu2;                       || calcula nuevos valores

            v_emp = #0#0;    v_tra = #0#1;
        |HAZ var_lins;                       || los graba
        |HAZ var_cabs;
    |FINSI;
|FINSI;
|FPRC;

|PROCESO sustitu0;
|ABRE #10;
    #10#0 = w_con;
|LEE 000#10=;
|SI FSalida = 0;               || lee control sustituciones
    |BUCLELIN 2sustitu1#10;
|FINSI;
|CIERRA #10;
|FPRC;
____________________________________/ PROCESO PRINCIPAL
|PROCESO sustituciones;
|GRABA 020#0a;
|LIBERA #0;         || para poder leerlo en el subejecuta
|CIERRAT #0;

|PARA para1 = 0; |SI para1 [ 3; |HACIENDO para1 = para1 + 3;
        para2 = para1 + 1;
    |ABRE #2;
        #2#0 = #0para1;
        #2#1 = #0para2;
    |LEE 000#2=;
    |SI FSalida = 0;         || lee el trabajador sustituto
        |SI #2#42 = "L";|O #2#42 = "N";
                ay_emp = #2#0;
                ay_tra = #2#1;
                ay_mes = #0#2;
                ay_any = #0#6;
                ay_ant = #2#34;
            |SUB_EJECUTA "noyprepa.run";
        |FINSI;
    |FINSI;
    |CIERRA #2;
|SIGUE;

|ABRET #0;
|LEE 121#0=;             || deja como estaba

|ABRE #2;
    #2#0 = #0#0;
    #2#1 = #0#1;
|LEE 000#2=;
|SI FSalida = 0;
    |HAZ coefi;
    |HAZ sustitu0;
|FINSI;
|CIERRA #2;
|FPRC;

|PROCESO lee_otro;
|DDEFECTO #2;
|ABRE #2;
    #2#0 = #0#3;
    #2#1 = #0#4;
|LEE 020#2=;
    w_con = #2#87;
    w_cat = #2#17;
|CIERRA #2;
|FPRC;

|PROCESO var_bor;
|ABRE #7;
    #7#0 = #0#0;
    #7#1 = #0#1;
    #7#2 = #0#2;
    #7#3 = 0;
|LEE 000#7=;
|SI FSalida = 0;
    |BUCLELIN 1NULL#7;
    |BORRA 020#7a;
|FINSI;
|CIERRA #7;
|FPRC;

|PROCESO tipo30; |TIPO 30;
|ATRI P;  |MENSA "    ...Preparando Variaciones...";   |ATRI 0;
|HAZ var_bor;
|SI #0#5 ! 0;
    |HAZ lee_otro;
    |HAZ sustituciones;
|FINSI;
|BLIN 4;
|FPRC;
