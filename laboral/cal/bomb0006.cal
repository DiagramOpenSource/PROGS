|FICHEROS;
     bomb0000;
     bomb0006 #0;
     bomb0007 #1,MANTE;
     labempre;      || mantenimiento empresas
     labtraba;      || mantenimiento trabajadores
     nomcateg;      || mentenimiento categorias
     bomb0004;      || mantenimiento comentarios
     bomb0008;      || comentarios modificados manualmente por trabajador
|FIN;

|VARIABLES;
     Puntero = 0;
     DocRecibo = 0;
     Comodin = "";

     nBaffer = 0;
     nTotalLinea = 0; nTope = 0; nVari1 = 0; nVari2 = 0;

     &enModoEdita  = 0;
     &enTotalVaria = 0;
     &enCampoAncho = 0;
     &eaNombreList  = "";
     &enCuadroIni   = 0;
     &enCuadroFin   = 0;

{100}&epPuntEdita   = "";

     &aAlojamiento = "";
     aAlfa = "";
     aCodigo = "";
     nCodigo = 0;
     aAny = "";
     nModo = 0;
     swFuerzaBorrado = 0;
|FIN;

|PROCESO Tipo;
     |CAMPO_MODIFICA #1#5, 1, "S";

     |CAMPO_MODIFICA #1#7, 1, "S";

     |SI #1#4 = 1;          || para todos
          |CAMPO_MODIFICA #1#5, 1, "N";
          #1#5 = 0;
          #1#6 = "";
          |PINTA #1#5;
          |PINTA #1#6;

          |CAMPO_MODIFICA #1#7, 1, "N";
          #1#7 = 0;
          #1#8 = "";
          |PINTA #1#7;
          |PINTA #1#8;
     |FINSI;
     |SI #1#4 = 2;            || por categoria
          |CAMPO_MODIFICA #1#7, 1, "N";
          #1#7 = 0;
          #1#8 = "";
          |PINTA #1#7;
          |PINTA #1#8;
     |FINSI;
     |SI #1#4 = 3;            || por trabajador
          |CAMPO_MODIFICA #1#5, 1, "N";
          #1#5 = 0;
          #1#6 = "";
          |PINTA #1#5;
          |PINTA #1#6;
     |FINSI;
|FPRC;

|PROCESO GrabaSecuencial;
     IepPuntEdita  = epPuntEdita1 <-;
     |PARA nTotalLinea = 0;  |SI nTotalLinea [ enTotalVaria;  |HACIENDO nTotalLinea = nTotalLinea + 1;
          |AL_BUF nBaffer, nTotalLinea, epPuntEdita;
          IepPuntEdita = IepPuntEdita + 1;
     |SIGUE;
|FPRC;

|PROCESO Edicion;
     nTope  = nTotalLinea - 1;
     nVari1 = 0;
     nVari2 = 0;
     enModoEdita = enModoEdita + 100;
     |EDITA nBaffer, 1, nTope, enCuadroIni, enCuadroFin, enModoEdita, eaNombreList, enCampoAncho, nVari1, nVari2;
     enModoEdita = enModoEdita $ 100;
|FPRC;

|PROCESO CogePuntero;
     IepPuntEdita  = epPuntEdita1 <-;
     |PARA nTotalLinea = 0;  |SI nTotalLinea [ enTotalVaria;  |HACIENDO nTotalLinea = nTotalLinea + 1;
          |DEL_BUF nBaffer, nTotalLinea, epPuntEdita;
          IepPuntEdita = IepPuntEdita + 1;
     |SIGUE;
|FPRC;

|PROCESO Edita;
     |PUSHV 0501 2380;
     |DIMENSIONA  200, nBaffer, 0;

     |HAZ GrabaSecuencial;
     |HAZ Edicion;
     |HAZ CogePuntero;

     |FINDIM nBaffer;
|FPRC;

|PROCESO comentario;
     |ABRE #bomb0008;
     |PARA Puntero = 1; |SI Puntero [ 5; |HACIENDO Puntero = Puntero + 1;
          IepPuntEdita  = epPuntEdita1 <-;
          IepPuntEdita = IepPuntEdita + (Puntero - 1);

          #bomb0008#0 = #bomb0007#0;    || empresa
          #bomb0008#1 = #bomb0007#1;    || A¤o
          #bomb0008#2 = #bomb0007#2;    || Mes
          #bomb0008#3 = #bomb0007#3;    || Linea bomb0007
          #bomb0008#4 = Puntero;        || Linea comentario particular
          |LEE 101#bomb0008.=;
          |SI FSalida ! 0;
               epPuntEdita  = "";
          |SINO;
               epPuntEdita  = #bomb0008#5;
               |BORRA 020#bomb0008.a;
               |LIBERA #bomb0008;
          |FINSI;
     |SIGUE;
     |CIERRA #bomb0008;

     |PUSHV 0501 2080;

     |HAZ Edita;

     |POPV;

     |ABRE #bomb0008;
     |PARA Puntero = 1;  |SI Puntero [ 5;  |HACIENDO Puntero = Puntero + 1;
          IepPuntEdita  = epPuntEdita1 <-;
          IepPuntEdita = IepPuntEdita + (Puntero - 1);

          |DDEFECTO #bomb0008;
          #bomb0008#0 = #bomb0007#0;    || empresa
          #bomb0008#1 = #bomb0007#1;    || A¤o
          #bomb0008#2 = #bomb0007#2;    || Mes
          #bomb0008#3 = #bomb0007#3;    || Linea bomb0007
          #bomb0008#4 = Puntero;        || Linea comentario particular
          |LEE 101#bomb0008.=;
          |SI FSalida ! 0;
               Comodin = epPuntEdita; |QBF Comodin;
               |SI Comodin ! "";
                    #bomb0008#5 = epPuntEdita;
                    |GRABA 020#bomb0008.n;
               |FINSI;
          |SINO;
               Comodin = epPuntEdita; |QBF Comodin;
               |SI Comodin ! "";
                    #bomb0008#5 = epPuntEdita;
                    |GRABA 020#bomb0008.a;
               |FINSI;
          |FINSI;
     |SIGUE;
     |CIERRA #bomb0008;
|FPRC;

|PROCESO borra; |TIPO 1;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 3; |O swFuerzaBorrado = 1;
          |ABRE #bomb0008;
          |PARA Puntero = 1; |SI Puntero [ 5; |HACIENDO Puntero = Puntero + 1;
               #bomb0008#0 = #bomb0007#0;    || empresa
               #bomb0008#1 = #bomb0007#1;    || A¤o
               #bomb0008#2 = #bomb0007#2;    || Mes
               #bomb0008#3 = #bomb0007#3;    || Linea bomb0007
               #bomb0008#4 = Puntero;        || Linea comentario particular
               |LEE 101#bomb0008.=;
               |SI FSalida = 0;
                    |BORRA 020#bomb0008.a;
                    |LIBERA #bomb0008;
               |FINSI;
          |SIGUE;
          |CIERRA #bomb0008;
     |FINSI;
|FPRC;

|PROCESO ModificaCom;
     |SI FSalida = 15;
          #bomb0007#9 = 0;
          #bomb0007#10 = "MANUAL";
          #bomb0007#11 = "*";
          |PINTA #bomb0007#9;
          |PINTA #bomb0007#10;
          |PINTA #bomb0007#11;

          enTotalVaria = 5;
          enModoEdita  = 0;
          enCampoAncho = 60;
          eaNombreList  = "";
          enCuadroIni   = 1814;
          enCuadroFin   = 2267;

          enModoEdita = 0;
          |HAZ comentario;
     |FINSI;
|FPRC;

|PROCESO ManteCom;
     |SI FSalida = 14;
          |SUB_EJECUTA "bomb0004";
          |ERROR;
     |FINSI;
|FPRC;

|PROCESO MinCom;
     #bomb0004#0 = 001;
|FPRC;

|PROCESO MaxCom;
     #bomb0004#0 = 999;
|FPRC;

|PROCESO ConsultaCom;
     |SI FSalida = 13;
          |ABRE #bomb0004;
          |CONSULTA_F_CLAVE #1#bomb0004, MinCom, MaxCom;
          |SI FSalida = 0;
               #bomb0007#9 = #bomb0004#0;    || codigo categoria
               #bomb0007#10 = #bomb0004#1;    || nombre categoria
               |PINTA #bomb0007#9;
               |PINTA #bomb0007#10;
          |FINSI;
          |CIERRA #bomb0004;
     |FINSI;
|FPRC;

|PROCESO CompruebaCom;
     |SI #bomb0007#9 ! 0; |Y #bomb0007#11 = "*";  || antes tenia comentario
          swFuerzaBorrado = 1;
          |HAZ borra;                             || manual y lo estoy cambiando
          #bomb0007#11 = " ";                     || lo tengo que borrar
          |PINTA #bomb0007#11;
          swFuerzaBorrado = 0;
     |FINSI;
     |SI #bomb0007#11 = "*";
          |ACABA;
     |FINSI;
     |ABRE #bomb0004;
     |SI #bomb0007#9 = 0;
          aAlfa = "    El codigo del comentario no puede estar vacio.";
          |MENAV aAlfa;
          |ERROR;
          |ACABA;
     |SINO;
          #bomb0004#0 = #bomb0007#9;    || comentario
          |LEE 000#bomb0004.=;
          |SI FSalida ! 0;
               aAlfa = "    El comentario categoria no existe. Pulse < F5 > para consulta de comentarios";
               |MENAV aAlfa;
               |ERROR;
               |ACABA;
          |SINO;
               #bomb0007#10 = #bomb0004#1;    || nombre categoria
               |PINTA #bomb0007#10;
          |FINSI;
     |FINSI;
     |CIERRA #bomb0004;
|FPRC;

|PROCESO MinCat;
     #nomcateg#0 = #labempre#1;
     #nomcateg#1 = 001;
|FPRC;

|PROCESO MaxCat;
     #nomcateg#0 = #labempre#1;
     #nomcateg#1 = 999;
|FPRC;

|PROCESO ConsultaCat;
     |SI FSalida = 13;
          |ABRE #nomcateg;
          |CONSULTA_F_CLAVE #1#nomcateg, MinCat, MaxCat;
          |SI FSalida = 0;
               #bomb0007#5 = #nomcateg#1;    || codigo categoria
               #bomb0007#6 = #nomcateg#2;    || nombre categoria
               |PINTA #bomb0007#5;
               |PINTA #bomb0007#6;

          |FINSI;
          |CIERRA #nomcateg;
     |FINSI;
|FPRC;

|PROCESO CompruebaCat;
     |ABRE #nomcateg;
     |SI #bomb0007#5 = 0;
          |SI #bomb0007#4 = 2;
               aAlfa = "    El codigo de la categoria no puede estar vacio ";
               aAlfa = aAlfa + "para comentarios tipo 2.";
               |MENAV aAlfa;
               |ERROR;
               |ACABA;
          |FINSI;
     |SINO;
          #nomcateg#0 = #labempre#1;    || convenio
          #nomcateg#1 = #bomb0007#5;    || categoria
          |LEE 000#nomcateg.=;
          |SI FSalida ! 0;
               aAlfa = "    La categoria no existe. Pulse < F5 > para consulta de categorias";
               |MENAV aAlfa;
               |ERROR;
               |ACABA;
          |SINO;
               #bomb0007#6 = #nomcateg#2;    || nombre categoria
               |PINTA #bomb0007#6;
          |FINSI;
     |FINSI;
     |CIERRA #nomcateg;
|FPRC;

|PROCESO MinTra;
     #labtraba#0 = #bomb0007#0;
     #labtraba#1 = 00001;
|FPRC;

|PROCESO MaxTra;
     #labtraba#0 = #bomb0007#0;
     #labtraba#1 = 99999;
|FPRC;

|PROCESO ConsultaTra;
     |SI FSalida = 13;
          |ABRE #labtraba;
          |CONSULTA_F_CLAVE #1#labtraba, MinTra, MaxTra;
          |SI FSalida = 0;
               #bomb0007#7 = #labtraba#1;    || codigo trabajador
               #bomb0007#8 = #labtraba#2;    || nombre trabajador
               |PINTA #bomb0007#7;
               |PINTA #bomb0007#8;
          |FINSI;
          |CIERRA #labtraba;
     |FINSI;
|FPRC;

|PROCESO CompruebaTra;
     |ABRE #labtraba;
     |SI #bomb0007#7 = 0;
          |SI #bomb0007#4 = 3;
               aAlfa = "    El codigo del trabajador no puede estar vacio ";
               aAlfa = aAlfa + "para comentarios tipo 3.";
               |MENAV aAlfa;
               |ERROR;
               |ACABA;
          |FINSI;
     |SINO;
          #labtraba#0 = #bomb0007#0;    || empresa
          #labtraba#1 = #bomb0007#7;    || trabajador
          |LEE 000#labtraba.=;
          |SI FSalida ! 0;
               aAlfa = "    El trabajador no existe. Pulse < F5 > para consulta de trabajadores";
               |MENAV aAlfa;
               |ERROR;
               |ACABA;
          |SINO;
               #bomb0007#8 = #labtraba#2;
               |PINTA #bomb0007#8;
          |FINSI;
     |FINSI;
     |CIERRA #labtraba;
|FPRC;
