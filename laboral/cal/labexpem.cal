|FICHEROS;
     labexpem #0;
     labexpet,MANTE;
     nomcalca;
     labtraba;
     labempre;
     labcentr;
|FIN;

|VARIABLES;
     aAlfa1 = "";
     aAlfa2 = "";
     aAlfa3 = "";
     aAlfa4 = "";
     nNume1 = 0;
     nNume2 = 0;
     nNume3 = 0;
     nNume4 = 0;
     nPara1 = 0;
     nPara2 = 0;
     nPara3 = 0;
     nPara4 = 0;
     fFech1 = @;
     fFech2 = @;
     fFech3 = @;
     fFech4 = @;
     nFEstado = 0;
     nSeleccio = 0;
     nSeleccio2 = 0;
     nModo = 0;

     &p_mes = "";
     nAnyo2 = 0;
     &aRazon = "";
     &aFecha1 = "";
     &aFecha2 = "";
     nPropTP = 0;
|FIN;

____________________________________/
|PROCESO DefeTraba; |TIPO 0;
     |DDEFECTO #labtraba;
     |ABRE #labtraba;
     #labtraba#0 = #labexpem#0;
     #labtraba#1 = #labexpet#0;
     |LEE 000#labtraba.=;
     |SI FSalida ! 0;
          |MENAV "0000Trabajador inaccesible o inexistente.";
          |ERROR;
     |SINO;
          |SI #labtraba#77 < #0#7; |O #labtraba#77 > #0#8;
               aAlfa1 = #labtraba#77; aAlfa1 = "00" + aAlfa1; aAlfa1 = aAlfa1 % -102;
               aAlfa1 = "0000Trabajador inaccesible por pertenecer al centro " + aAlfa1 + ".";
               |MENAV aAlfa1;
               |ERROR;
          |SINO;
               #labexpet#1 = #labtraba#2;    |PINTA #labexpet#1;
               #labexpet#2 = #labtraba#7;    |PINTA #labexpet#2;
          |FINSI;
     |FINSI;
     |CIERRA #labtraba;
|FPRC;
____________________________________/
|PROCESO Defecto; |TIPO 7;    || calcula el a¤o por defecto segun sistema
     fFech1 = ~;
     aAlfa1 = fFech1;
     aAlfa1 = aAlfa1 % -104;
     #0Campo = aAlfa1;
     |PINTA #0Campo;
|FPRC;

|PROCESO Mirames; |TIPO 7;    || calcula el mes por defecto segun sistema
     fFech1 = ~;
     aAlfa1 = fFech1;
     aAlfa1 = aAlfa1 % 402;
     #0Campo = aAlfa1;
     |PINTA #0Campo;
|FPRC;

|PROCESO Pintames; |TIPO 0;   || pinta el nombre del mes
     |SI #0Campo =  1; p_mes = "ENERO     ";  |FINSI;
     |SI #0Campo =  2; p_mes = "FEBRERO   ";  |FINSI;
     |SI #0Campo =  3; p_mes = "MARZO     ";  |FINSI;
     |SI #0Campo =  4; p_mes = "ABRIL     ";  |FINSI;
     |SI #0Campo =  5; p_mes = "MAYO      ";  |FINSI;
     |SI #0Campo =  6; p_mes = "JUNIO     ";  |FINSI;
     |SI #0Campo =  7; p_mes = "JULIO     ";  |FINSI;
     |SI #0Campo =  8; p_mes = "AGOSTO    ";  |FINSI;
     |SI #0Campo =  9; p_mes = "SEPTIEMBRE";  |FINSI;
     |SI #0Campo = 10; p_mes = "OCTUBRE   ";  |FINSI;
     |SI #0Campo = 11; p_mes = "NOVIEMBRE ";  |FINSI;
     |SI #0Campo = 12; p_mes = "DICIEMBRE ";  |FINSI;
     |PINTA 0821, p_mes;
|FPRC;
____________________________________/
|PROCESO DatosEmpresa;
     |ABRE #labempre;
     #labempre#0 = #labexpem#0;
     |LEE 000#labempre.=;
     |CIERRA #labempre;

     |SI #0#7 ! #0#8;   || cojo los datos de la empresa
          aRazon = #labempre#2;
     |SINO;              || cojo los datos del centro de trabajo
          |ABRE #labcentr;
          #labcentr#0 = #0#0;
          #labcentr#1 = #0#7;
          |LEE 000#labcentr.=;
          |SI FSalida ! 0;
               aAlfa1 = #0#7;  aAlfa1 = "00" + #0#7; aAlfa1 = aAlfa1 % -102;
               aAlfa1 = "    No existe el centro " + aAlfa1 + ".";
               |MENAV aAlfa1;
          |SINO;
               aRazon = #labcentr#2;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO labexpet;
     nSeleccio2 = 1;

     #labtraba#0 = #nomcalca#0;
     #labtraba#1 = #nomcalca#1;
     |LEE 020#labtraba.=;

     |SI #labexpet#3 ! "01.01.0000";
          aFecha1 = #labexpet#3;
     |SINO;
          aFecha1 = "";
     |FINSI;

     |SI #labexpet#4 ! "01.01.0000";
          aFecha2 = #labexpet#4;
     |SINO;
          aFecha2 = "";
     |FINSI;

     |PRINT;
|FPRC;

|DEFBUCLE labexpet;
     #labexpet#2;
     ;
     INICIO;
     FINAL;
     ;
     NULL, labexpet;
|FIN;

|PROCESO min;
     #labexpet#0 = 1;
|FPRC;

|PROCESO max;
     #labexpet#0 = 99999;
|FPRC;

|PROCESO nomcalca;
     #labtraba#0 = #nomcalca#0;
     #labtraba#1 = #nomcalca#1;
     |LEE 000#labtraba.=;
     |SI FSalida ! 0;    |ACABA;   |FINSI;

     |SI #labtraba#77 < #0#7; |O #labtraba#77 > #0#8;
          |ACABA;
     |FINSI;
     nSeleccio = 1;

     |DDEFECTO #labexpet;
     #labexpet#0 = #nomcalca#1;         || codigo
     #labexpet#1 = #labtraba#2;         || apellidos y nombre
     #labexpet#2 = #labtraba#7;         || DNI

     nPropTP = 100;
     |SI #nomcalca#119 ! 0; |Y #nomcalca#119 ! 100; nPropTP = #nomcalca#119; |FINSI;
     |SI #nomcalca#120 ! 0; |Y #nomcalca#120 ! 100; nPropTP = #nomcalca#120; |FINSI;
     |SI #nomcalca#121 ! 0; |Y #nomcalca#121 ! 100; nPropTP = #nomcalca#121; |FINSI;
     |SI #nomcalca#122 ! 0; |Y #nomcalca#122 ! 100; nPropTP = #nomcalca#122; |FINSI;

     |SI nPropTP = 100;
          |SI #nomcalca#78 = 0;
               #labexpet#3 = "*****";             || JSR
          |SINO;
               #labexpet#3 = nPropTP;             || JSR
          |FINSI;
     |SINO;
          #labexpet#3 = nPropTP;             || JSR
     |FINSI;

     |SI nPropTP = 100;
          #labexpet#4 = #nomcalca#8;         || dias trabajados
          #labexpet#5 = #nomcalca#78;        || dias desempleo
     |SINO;
          #labexpet#4 = #nomcalca#8 - #nomcalca#78;        || dias desempleo
          #labexpet#5 = #nomcalca#78 / (nPropTP / 100);        || dias desempleo
     |FINSI;

     #labexpet#6 = "";                  || observaciones

     |GRABA 020#labexpet.n;
|FPRC;

|DEFBUCLE nomcalca;
     #nomcalca#1;
     ;
     #labexpem#0,     1, #labexpem#2, 0;
     #labexpem#0, 99999, #labexpem#2, 0;
     ;
     NULL, nomcalca;
|FIN;

|PROCESO Tipo3; |TIPO 3;
     aAlfa1 = #0#1;
     aAlfa1 = aAlfa1 % -102;
     nAnyo2 = aAlfa1;

     aAlfa1 = Usuario;
     |QBF aAlfa1;
     |NOME_DAT #labexpet#aAlfa1#;
     |DELINDEX #labexpet;
     |ABRE #labtraba;

     |ABRE #labexpet;
     |BUCLE nomcalca;
     |CIERRA #labexpet;

     |SI nSeleccio = 0;
          |MENAV "0000No se encuentran recibos calculados en el mes informado.";
          |MENAV "0000Podra rellenar manualmente la relacion.";
     |FINSI;

     |ENTLINEAL #1#labexpet, 1, 3, 20, 0, min, max;

     |IMPRE 0;
     |SI FSalida = 0;
          |INFOR "labexpet";
          |HAZ DatosEmpresa;
          |BUCLE labexpet;
          |SI nSeleccio2 ! 0;
               |PIEINF;
          |SINO;
               |MENAV "0000Seleccion vacia.";
          |FINSI;
          |FININF;
     |SINO;
          |MENAV "0000Proceso abortado.";
     |FINSI;
     |FINIMP;

     |CIERRA #labtraba;
     |DELINDEX #labexpet;
|FPRC;
