|FICHEROS;
     nomagicm #0;
     labempre #1;    || empresas
     nomhicca #5;    || historico cabs.
     nomhicli #6;    || historico lins.
     nomtmpca #9;    || tmp. calculos cabs.
     nomtmpli #15;   || tmp. calculos lins.
     nomagide #16,MANTE;  || desglose total devengado
     nomagitm #19;   || tmp. comparacion

     :/basica/def/agifigen #12;   || clientes AGICLI

     :/contagen/def/cacuenta #24;   || cuentas AGICONT
     :/contagen/def/canempre #25;   || empresas AGICONT
|FIN;

|VARIABLES;
    alfa1 = "";
    alfa2 = "";
    alfa3 = "";
    alfa4 = "";
    nume1 = 0;
    nume2 = 0;
    nume3 = 0;
    nume4 = 0;
    para1 = 0;
    para2 = 0;
    para3 = 0;
    para4 = 0;
    fech1 = @;
    fech2 = @;
    fech3 = @;
    fech4 = @;
    elsw1 = 0;
    elsw2 = 0;
    elsw3 = 0;
    elsw4 = 0;
    FEstado = 0;
    seleccio = 0;
    modo = 0;
    sw_rup = 0;

    p_mes = "";
    path_agicon = "";
    path_empres = "";
    path_agicli = "";
    ultimo = 0;
    sw_agicli = 0;
    cod_empre = 0;
    cod_perio = 0;
    cod_anyo  = 0;
    deven = 0;
    desgl = 0;
    rirpf = 0;
    rsegu = 0;
    liqui = 0;
    antic = 0;
    espec = 0;
    el503 = 0;
    el504 = 0;
    eltc1 = 0;
    signe = "";
    impor = 0;
    conte = "";
    digcu = 0;
    conce = 0;
    descr = "";
    empre = 0;
    cuantos = 0;

    p_mensa = "    LONGITUD DE CUENTA FUERA DE NIVEL!";
    d_diari = 0;
    d_opera = "";
    d_impor = 0;
    &modcon = 0;
    &codcon = 0;
    importe = 0;
    opera = "";
    ressum = 0;
    ini = 0;
    dig1 = 0;
    dig3 = 0;
    dig4 = 0;
    dig5 = 0;
    dig6 = 0;
    dig7 = 0;
    dig8 = 0;
    dig9 = 0;
    fecalf = "";
    mes = "";
    mesfec = 0;
    mesac = 0;
    a = 0;
    b = 0;
    c = 0;
    ult_apunt = 0;
    p_alfa1 = "";
    p_error = 0;
    p_lugar = 0;
    p_cuan3 = 0;
    p_cuan4 = 0;
    p_cuan5 = 0;
    p_alfa2 = "";
    p_alfa3 = "";
    p_alfa4 = "";
    p_alfa5 = "";
    p_nume1 = 0;
    p_nume2 = 0;
    p_ceros = "";

    juan = 0;
    errant = 0;
    cuini = "            ";
    cufin = "999999999998";
    diini = 0;
    difin = 9;
    fecini = @;
    fecfin = @;
    tiini = "D";
    tifin = "H";
    diain = 0;
    diafi = 99;
    prim = 0;
    cuant = "";
    lon = 0;
    aa = 0;
    mensa = "";
    aar = "";
    traba = "";
    errcuent = "aaaaaaaaaaaa";
    mensa2   = "     No esta definida esta cuenta en Cuentas de Balance";
    uno = "";
    dos = "";
    mascara = "";
    nivel = 0;

    nPara1 = 0;
    nPara2 = 0;
    &nDiferen = 0;
    nMes = 0;
    &nCod = 0;
    &nPer = 0;
    &aNom = "";
    &aCta = "";
    &nSdo = 0;
    fFech1 = @;
    aAlfa1 = "";
    &nTotCon = 0;
    &nTotLab = 0;
    aCuenta = "";
    &nSwLin = 0;
    nSwRup = 0;

    &eaPathContagen = "";
    &EURODB = 0;
|FIN;
____________________________________/
|PROGRAMA;
 |HAZ LeeEURODB;
 |CLS;
 |CABEZA "Comprobacion Enlace";
 |DDEFECTO #0;
 |PINPA #0#0;
 |PINDA #0#0;
 |HAZ tipo8;
 |ABRE #0;
 |LEE 101#0p;
 |SI FSalida ! 0;
     |GRABA 020#0b;
 |FINSI;
 |PINDA #0#0;
 |ENDATOS #1#0;
 |SI FSalida = 0;
     |IMPRE 0;
     |SI FSalida = 0;
         |HAZ tipo3;
     |SINO;
         |MENAV "0000Proceso abortado ...";
     |FINSI;
     |FINIMP;
     |GRABA 020#0a;
 |FINSI;
 |LIBERA #0;
 |CIERRA #0;
|FPRO;
____________________________________/ CALCULOS PANTALLA
|PROCESO mayus; |TIPO 0;
    #0Campo = #0Campo > #0Campo;
|PINTA #0Campo;
|FPRC;

|PROCESO tipo8; |TIPO 8;
     |HAZ BusquedaApli;
     |SI eaPathContagen = "";
         |MENAV "     No existe Aplicacion de Contabilidad";
         |PTEC 807;
         |ACABA;
     |FINSI;

     path_agicon = eaPathContagen;  |QBF path_agicon;
     path_agicon = path_agicon + "fich/";
     alfa1 = eaPathContagen;
     alfa1 = alfa1 + "pan/";
     |PATH_PAN #24 alfa1;            || para el alta de cuentas (camaapua)
|FPRC;

|PROCESO desde_hasta; |TIPO 0;
|PARA para1 = 2; |SI para1 [ 11; |HACIENDO para1 = para1 + 1;
    |CAMPO_MODIFICA #0para1, 1, "S";
|SIGUE;
|CAMPO_MODIFICA #0#12, 1, "N";
|CAMPO_MODIFICA #0#13, 1, "N";

|SI #0#1 = 0;
    |PARA para1 = 2; |SI para1 [ 11; |HACIENDO para1 = para1 + 1;
        |CAMPO_MODIFICA #0para1, 1, "N";
    |SIGUE;
    |CAMPO_MODIFICA #0#12, 1, "S";
    |CAMPO_MODIFICA #0#13, 1, "S";
|FINSI;
|FPRC;

|PROCESO lee_emp; |TIPO 0;
|SI #0Campo ! 0;
    |ABRE #1;
        #1#0 = #0Campo;
    |LEE 000#1=;
    |SI FSalida ! 0;
        |MENAV "    Empresa INEXISTENTE ...";
        |ERROR;
    |FINSI;
    |CIERRA #1;
|FINSI;
|FPRC;

|PROCESO pin_emp; |TIPO 7;
|DDEFECTO #1;
|SI #0Campo ! 0;
    |ABRE #1;
        #1#0 = #0Campo;
    |LEE 000#1=;
    |CIERRA #1;
|FINSI;
|ATRI I;   |PINTA 1036, #1#2;  |ATRI 0;
|FPRC;

|PROCESO desglose; |TIPO 0;
    alfa1 = #0#37;
|C_M #0#38, 1, alfa1;
|FPRC;

|PROCESO min;
    #16#0 = #0#38;
    #16#1 = 101;
|FPRC;

|PROCESO max;
    #16#0 = #0#38;
    #16#1 = 999;
|FPRC;

|PROCESO edita; |TIPO 0;
|SI FSalida = 9;|Y #0#37 = "S";
    |PUSHV 0501 2380;
    |ENTLINEAL #1#16, 2, 3, 22, 1, min, max;
    |POPV;
    |ERROR;
|FINSI;
|FPRC;

|PROCESO Defecto; |TIPO 7;    || calcula el a¤o por defecto segun sistema
     fFech1 = ~;
     aAlfa1 = fFech1;
     aAlfa1 = aAlfa1 % -102;
     #0Campo = aAlfa1;
     |PINTA #0Campo;
|FPRC;
____________________________________/ COMPARACION DE CUENTAS
|PROCESO nomagitm_1;
     |SI nSwRup = 0;
          nSwRup = 1;
          aCuenta = #19#0;
     |FINSI;

     |SI aCuenta ! #19#0;
          |SI nTotCon ! 0;|O nTotLab ! 0;
               nSwLin = 1;
               |PRINT;
          |FINSI;
          aCuenta = #19#0;
          nTotCon = 0;
          nTotLab = 0;
     |FINSI;


     |DDEFECTO #24;
     #24#0 = aCuenta;
||      #24#1 = #19#1;
     |LEE 000#24=;
     |SI FSalida ! 0;
          aCta = "<CUENTA INEXISTENTE>";
     |SINO;
          aCta = #24#2;
     |FINSI;

     nMes = (#19#2 * 3) + 11;
     |SI #19#5 ! #24nMes;          || si el saldo es distinto
          nSdo = #24nMes;
          nDiferen = nDiferen + 1;
          nSwLin = 0;
          |PRINT;
          nTotCon = nTotCon + #24nMes;
          nTotLab = nTotLab + #19#5;
     |FINSI;
|FPRC;

|DEFBUCLE nomagitm;
     #19#1;
     ;
     "            ", 0,  1;
     "zzzzzzzzzzzz", 9, 12;
     ;
     NULL, nomagitm_1;
|FIN;

|PROCESO Compara;
     nDiferen = 0;
     |INFOR "nomagicm";

     |ABRE #24;
     |BUCLE nomagitm;
     |SI nTotCon ! 0;|O nTotLab ! 0;
          nSwLin = 1;
          |PRINT;
     |FINSI;
     |CIERRA #24;

     |SI nDiferen ! 0;
          |PIEINF;
     |FINSI;
     |FININF;
|FPRC;
____________________________________/ GRABACION DE ASIENTOS
|PROCESO la_cuenta;
  p_alfa1 = conte;
  p_error = 0;
  p_lugar = 0;
  p_cuan3 = 0;
  p_cuan4 = 0;
  p_alfa3 = "";
  p_alfa4 = "";
  |PARA p_nume1 = 1; |SI p_nume1 [ 12; |HACIENDO p_nume1 = p_nume1 + 1;
        p_nume2 = (p_nume1 * 100) + 1;
        p_alfa2 = p_alfa1 % p_nume2;
        |SI p_alfa2 ! " ";
            |SI p_alfa2 = ".";
                p_lugar = p_nume1;
            |SINO;
                |SI p_lugar=0; p_alfa3=p_alfa3+p_alfa2; p_cuan3=p_cuan3+1; |FINSI;
                |SI p_lugar!0; p_alfa4=p_alfa4+p_alfa2; p_cuan4=p_cuan4+1; |FINSI;
            |FINSI;
        |FINSI;
  |SIGUE;
  |SI p_lugar ! 0;
      |SI dig3 = 1;  p_cuan5 = dig4;  digcu = 1;  |FINSI;
      |SI dig3 = 2;  p_cuan5 = dig5;  digcu = 2;  |FINSI;
      |SI dig3 = 3;  p_cuan5 = dig6;  digcu = 3;  |FINSI;
      |SI dig3 = 4;  p_cuan5 = dig7;  digcu = 4;  |FINSI;
      |SI dig3 = 5;  p_cuan5 = dig8;  digcu = 5;  |FINSI;
      |SI dig3 = 6;  p_cuan5 = dig9;  digcu = 6;  |FINSI;
      p_nume1 = p_cuan5 - (p_cuan3 + p_cuan4);
      |SI p_nume1 > 0;
          p_ceros = "0" * p_nume1;
          p_alfa5 = p_alfa3 + p_ceros + p_alfa4;
      |SINO;
          |MENAV p_mensa;
          p_error = 1;
      |FINSI;
  |SINO;
      p_alfa5 = p_alfa1;
  |FINSI;
  conte = p_alfa5;
|FPRC;

|PROCESO apuntes;
|SI impor ! 0;
    |HAZ la_cuenta;
    |SI conte ! "            ";|Y digcu ! 0;
        |DDEFECTO #19;
        #19#0 = conte;    || cuenta
        #19#1 = digcu;    || digito
        #19#2 = #0#32;    || mes
        |LEE 101#19=;
        |SI FSalida ! 0; |GRABA 020#19b;     |FINSI;
        |SI signe = "D";
                #19#3 = #19#3 + impor;       || debe
        |SINO;
                #19#4 = #19#4 + impor;       || haber
        |FINSI;
        #19#5 = #19#3 - #19#4;               || saldo
        |GRABA 020#19a;
        |LIBERA #19;
    |FINSI;
|FINSI;
|FPRC;
____________________________________/ CREA TEMPORAL SEGUN FUENTE DE CALCULO
|PROCESO lee_nomcaltl1;
|PARA para1 = 0; |SI para1 [ 11; |HACIENDO para1 = para1 + 1;
        #15para1 = #6para1;
|SIGUE;
|GRABA 020#15n;
|FPRC;

|DEFBUCLE lee_nomcaltl;
 #6#1;
 ;
 #1#0,     1, #0#41, #0#32, #0#40, 101;
 #1#0, 99999, #0#41, #0#32, #0#40, 999;
 ;
 NULL, lee_nomcaltl1;
|FIN;

|PROCESO lee_nomcaltc1;
|PARA para1 = 0; |SI para1 [ 65; |HACIENDO para1 = para1 + 1;
        #9para1 = #5para1;
|SIGUE;
    #9#66 = #5#105;      || dias V
    #9#67 = #5#106;      || (%)ret.2
    #9#68 = #5#107;      || (%)ret.3
    #9#69 = #5#108;      || base irpf 2
    #9#70 = #5#109;      || base irpf 3
    #9#71 = #5#110;      || ret. 2
    #9#72 = #5#111;      || ret. 3
    #9#73 = #5#112;      || prest.enf.empresa
    #9#74 = #5#113;      || total bases
    #9#75 = #5#114;      || total ret.
    #9#76 = #5#117;      || dto.pagas huelga
    #9#77 = #5#118;      || dto.pagas absent.
    #9#78 = #5#119;      || dias D
    #9#79 = #5#121;      || base bonif. cc 2
    #9#80 = #5#122;      || base bonif. cp 2
    #9#81 = #5#123;      || cuota bonif. 2
    #9#82 = #5#124;      || (%) bonif. 1
    #9#83 = #5#125;      || (%) bonif. 2
    #9#84 = #5#126;      || base ilt bonif. 1
    #9#85 = #5#127;      || base ilt bonif. 2
    #9#89 = #5#131;      || anticipos 3
|GRABA 020#9n;
|FPRC;

|DEFBUCLE lee_nomcaltc;
 #5#1;
 ;
 #1#0,     1, #0#41, #0#32, #0#40;
 #1#0, 99999, #0#41, #0#32, #0#40;
 ;
 NULL, lee_nomcaltc1;
|FIN;

|PROCESO lee_nomhicli1;
|PARA para1 = 0; |SI para1 [ 11; |HACIENDO para1 = para1 + 1;
        #15para1 = #6para1;
|SIGUE;
|GRABA 020#15n;
|FPRC;

|DEFBUCLE lee_nomhicli;
 #6#1;
 ;
 #1#0,     1, #0#41, #0#32, 0, 101;
 #1#0, 99999, #0#41, #0#32, 4, 999;
 ;
 NULL, lee_nomhicli1;
|FIN;

|PROCESO lee_nomhicca1;
|PARA para1 = 0; |SI para1 [ 65; |HACIENDO para1 = para1 + 1;
        #9para1 = #5para1;
|SIGUE;
    #9#66 = #5#105;      || dias V
    #9#67 = #5#106;      || (%)ret.2
    #9#68 = #5#107;      || (%)ret.3
    #9#69 = #5#108;      || base irpf 2
    #9#70 = #5#109;      || base irpf 3
    #9#71 = #5#110;      || ret. 2
    #9#72 = #5#111;      || ret. 3
    #9#73 = #5#112;      || prest.enf.empresa
    #9#74 = #5#113;      || total bases
    #9#75 = #5#114;      || total ret.
    #9#76 = #5#117;      || dto.pagas huelga
    #9#77 = #5#118;      || dto.pagas absent.
    #9#78 = #5#119;      || dias D
|GRABA 020#9n;
|FPRC;

|DEFBUCLE lee_nomhicca;
 #5#1;
 ;
 #1#0,     1, #0#41, #0#32, 0;
 #1#0, 99999, #0#41, #0#32, 4;
 ;
 NULL, lee_nomhicca1;
|FIN;

|PROCESO temporal;
    alfa1 = Usuario;  |QBF alfa1;  alfa1 = alfa1 + "ca";  |NOME_DAT #9 alfa1;
    alfa1 = Usuario;  |QBF alfa1;  alfa1 = alfa1 + "li";  |NOME_DAT #15 alfa1;
|DELINDEX #9;
|DELINDEX #15;
|ABRE #9;
|ABRE #15;
|SI #0#39 = "H"; |BUCLE lee_nomhicca; |BUCLE lee_nomhicli;  |FINSI;
|SI #0#39 = "T"; |BUCLE lee_nomcaltc; |BUCLE lee_nomcaltl;  |FINSI;
|CIERRA #9;
|CIERRA #15;
|FPRC;
____________________________________/ CALCULA IMPORTES
|PROCESO nomcalli1;
    importe = (#15#10 * #15#11) ! EURODB;    || pesetas devengadas

    #16#0 = #0#38;  || nro.desglose
    #16#1 = #15#4;  || concepto
|LEE 101#16=;
|SI FSalida = 0;|Y #0#37 = "S";
        #16#4 = #16#4 + importe;             || conceptos desglosados
        desgl = desgl + importe;             || total ctos. desglosados
    |GRABA 020#16a;
|SINO;
        deven = deven + importe;             || conceptos sin desglose
|FINSI;
|LIBERA #16;
|FPRC;

|PROCESO prestaciones;
|| estos conceptos no se graban como lineas en el calculo

    #15#4 = 503;    #15#10 = 1;    #15#11 = el503;     |HAZ nomcalli1;
    #15#4 = 504;    #15#10 = 1;    #15#11 = el504;     |HAZ nomcalli1;
|FPRC;

|DEFBUCLE nomcalli;
 #15#1;
 ;
 #1#0,     1, #0#32, 0, 101;
 #1#0, 99999, #0#32, 99, 599;       || las funciones 900 no entran
 ;
 NULL, nomcalli1;
|FIN;

|PROCESO nomcalca1;
    rirpf = rirpf + #9#75 + #9#17;                     || total ret.IRPF
    rsegu = rsegu + #9#48 + #9#49 + #9#50 + #9#51;     || total aport. SS
    antic = antic + #9#52 + #9#53 + #9#89;             || total anticipos
    espec = espec + #9#54;                             || total especie
    el503 = el503 + #9#57;                             || prest.enf.SS
    el504 = el504 + #9#58;                             || prest.acc.SS
|FPRC;

|DEFBUCLE nomcalca;
 #9#1;
 ;
 #1#0,     1, #0#32, 0;
 #1#0, 99999, #0#32, 9;
 ;
 NULL, nomcalca1;
|FIN;

|PROCESO borra_desglose1;
    #16#4 = 0;
|GRABA 020#16a;
|LIBERA #16;
|FPRC;

|DEFBUCLE borra_desglose;
 #16#1;
 ;
 #0#38, 101;
 #0#38, 999;
 be;
 NULL, borra_desglose1;
|FIN;

|PROCESO calcula;
|BUCLE borra_desglose;
    desgl = 0;      || total devengado desglosado (DEBE)
    deven = 0;      || total devengado sin desglosar (DEBE)
    rirpf = 0;      || retencion IRPF   (HABER)
    rsegu = 0;      || retencion S.S.   (HABER)
    liqui = 0;      || liquido          (HABER)
    antic = 0;      || anticipos        (HABER)
    espec = 0;      || en especie       (HABER)
    el503 = 0;      || prest.enf.SS     (DEBE)
    el504 = 0;      || prest.enf.SS     (DEBE)

|HAZ temporal;
|BUCLE nomcalca;         || calcula el HABER (menos liquido)
|ABRE #16;
|BUCLE nomcalli;         || calcula el DEBE (menos prestaciones SS)
|HAZ prestaciones;       || calcula el DEBE (solo prestaciones SS)
|CIERRA #16;
    liqui = (deven+desgl)-(rirpf+rsegu+antic+espec);   || liquido
|FPRC;
____________________________________/ COMPRUEBA Y BUSCA EN AGIFIGEN
|PROCESO LeeConta;
    cod_empre = #1#0;
    cod_anyo  = #0#41;

    |ABRE #25;
    |SELECT #4#25;
    #25#2  = cod_empre;
    #25#26 = cod_anyo;
    |LEE 000#25=;
    |SI FSalida = 0;
        sw_agicli = 1;
        dig1 = #25#11;       || desde mes
        dig3 = #25#13;       || nro. niveles
        dig4 = #25#14;       || 1 nivel
        dig5 = #25#15;       || 2 nivel
        dig6 = #25#16;       || 3 nivel
        dig7 = #25#17;       || 4 nivel
        dig8 = #25#18;       || 5 nivel
        dig9 = #25#19;       || 6 nivel
        nCod = #25#2;        || codigo empresa
        nPer = #25#3;        || periodo empresa
        aNom = #25#1;        || nombre empresa
        cod_perio = #25#3;
   |FINSI;
   |SELECT #1#25;
   |CIERRA #25;
|FPRC;

|PROCESO lee_agifigen;
   |ABRE #12;
   #12#0 = #1#0;
   |LEE 000#12=;
   |SI FSalida = 0;
       |HAZ LeeConta;
   |FINSI;
   |CIERRA #12;
|FPRC;
____________________________________/ LECTURA Y CALCULOS PRINCIPALES
|PROCESO desglose1;
 |SI #16#4 ! 0;
     signe = "D";
     impor = #16#4;
     conte = #16#2;
     |HAZ apuntes;
 |FINSI;
|FPRC;

|DEFBUCLE desglose;
 #16#1;
 ;
 #0#38, 101;
 #0#38, 999;
 ;
 NULL, desglose1;
|FIN;

|PROCESO conta2;         || contabilizacion
|SI #0#37 = "S";    |BUCLE desglose;    |FINSI;
    signe = "D"; impor = deven; conte = #0#14; |HAZ apuntes;
    signe = "H"; impor = rirpf; conte = #0#15; |HAZ apuntes;
    signe = "H"; impor = rsegu; conte = #0#16; |HAZ apuntes;
    signe = "H"; impor = liqui; conte = #0#17; |HAZ apuntes;
    signe = "H"; impor = antic; conte = #0#18; |HAZ apuntes;
    signe = "H"; impor = espec; conte = #0#19; |HAZ apuntes;
|FPRC;

|PROCESO conta1;         || comprobaciones y calculos
|ATRI P;  |PINTA 1036, #1#2;  |ATRI 0;

    sw_agicli = 0;
|HAZ lee_agifigen;
|SI sw_agicli = 1;
        alfa1 = cod_empre;    alfa1 = ("00000" + alfa1) % -105;
        path_empres = path_agicon + alfa1 + cod_perio + "/";
    |PATH_DAT #24 path_empres;

    alfa1 = Usuario;  |QBF alfa1;  alfa1 = alfa1 + "tm";  |NOME_DAT #19 alfa1;
    |DELINDEX #19;
    |ABRE #19;
    |PARA nPara1 = #0#42; |SI nPara1 [ #0#43; |HACIENDO nPara1 = nPara1 + 1;
        #0#39 = "H";          || fuente (historico)
        #0#32 = nPara1;       || mes historico
        |HAZ calcula;
        |HAZ conta2;
    |SIGUE;
    |SI #0#44 = "S";
        |PARA nPara2 = 5; |SI nPara2 [ 20; |HACIENDO nPara2 = nPara2 + 1;
            |PARA nPara1 = #0#42; |SI nPara1 [ #0#43; |HACIENDO nPara1 = nPara1 + 1;
                #0#39 = "T";      || fuente (atrasos)
                #0#40 = nPara2;   || tipo atrasos
                #0#32 = nPara1;   || mes atrasos
                |HAZ calcula;
                |HAZ conta2;
            |SIGUE;
        |SIGUE;
    |FINSI;
    |CIERRA #19;

    |HAZ Compara;

    |DELINDEX #9;
    |DELINDEX #15;
    |DELINDEX #19;
|FINSI;
|FPRC;

|DEFBUCLE labempre;
 #1#1;
 ;
 #0#12;
 #0#13;
 ;
 NULL, conta1;
|FIN;

|PROCESO tipo3;
|SI #0#1 ! 0;
    |ABRE #1;
    |PARA empre = 1; |SI empre [ 11; |HACIENDO empre = empre + 1;
            #1#0 = #0empre;
        |LEE 000#1=;
        |SI FSalida = 0;
            |HAZ conta1;
        |FINSI;
    |SIGUE;
    |CIERRA #1;
|SINO;
    |BUCLE labempre;
|FINSI;
|FPRC;
