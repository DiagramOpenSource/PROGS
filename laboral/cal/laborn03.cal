|FICHEROS;
     laborn03;           || este def el del lineal
     labcodco;

     labtraba@;

     labor001;           || empresas labornet
     nompmail;
     labornx3;
     labcomjc,MANTE;
|FIN;

|VARIABLES;
     &eaDirDest  = "";
     &eaAsunto   = "";
     &eaTexto    = "";
     &eaFichero  = "";

     &nLabornet  = 0;
     &enNumLinea = 0;
     &enEmpLabornet = 0;
     &enTraLabornet = 0;

     &nEmpExt    = 0;
     &nTraExt    = 0;
     &fFechaAlta = @;
     &aTipoContr = "";

     aParam = "";
     &eaUsuario = "";
     &eaFecha = "";
     &eaHora = "";
     aTipo = "";
     sw = 0;
     aAlfa = "";
     nCalc = 0;
     aUsuarioD = "";
     aUsuarioH = "";
     &eaVengoDe = "laborn03";
     &eaEmpresa = "";
     &aNuevoTra = "";
     nNume = 0;

     &eaParam       = "";
     &enFSalida     = 0;
     &efFecha       = @;
     &enBorrado     = 0;
     &enActualizado = 0;
     &eaEstado      = "";
     &eaMotiv       = "";

     aAlfa1 = "";
     aAlfa2 = "";
     aSiEsX = "";

     &enEmpresa = 0;
     &enTrabajador = 0;
     &enHoras = 0;
     &enHorasMax = 0;
     &enCoeficiente = 0;

     || datos para el nuevo contrato
     &enHorasSemana = 0;
     &eaTipoJornada = "";
     &eaPrimerDia = "";
     &eaUltimoDia = "";
     &eaSalario = "";
     &enDurMeses = 0;
     &enDurDias = 0;
     &eaFechaIni = "";
     &eaFechaFin = "";
     &eaContrato = "";
     &enJorAnt = 0;
     nCampo = 0;
     &swMeHeSalido = 0;
     &enEmpAntLabornet = 0;
     &enTraAntLabornet = 0;
     &enRegistroCont = 0;
     &swCambioJor = 0;
     FEstado = 0;
|FIN;

|PROCESO Email;
     || Emite correo electronico al usuario de que la variacion del trabajador
     ||    se ha hecho efectiva

     |DDEF aAlfa; |QBF aAlfa;
     aAlfa = aAlfa + "labtraba.mas";
     |CARGA_DEF aAlfa, labtraba@;
     |SI FSalida = 0;
        |DFICE aAlfa; |QBF aAlfa;
        |PATH_DAT #labtraba@#aAlfa#;
        |ABRE #labtraba@;
        #labtraba@#0 = #laborn03#5;
        #labtraba@#1 = #laborn03#6;
        |LEE 000#labtraba@.=;
        |SI FSalida ! 0; |DDEFECTO #labtraba@; |FINSI;
        aAlfa = #labtraba@#2; |QBF aAlfa;
        |CIERRA #labtraba@; |DESCARGA_DEF #labtraba@;
     |FINSI;

     eaTexto = &13 + &10 + "El administrador ha hecho efectiva la variacion del trabajador (";
     aAlfa = (("00000" + #laborn03#5) % -0106) + "/" + (("00000" + #laborn03#6) % -105) + ") " + aAlfa; |QBF aAlfa;
     eaTexto = eaTexto + aAlfa; |QBF eaTexto;

     eaTexto = eaTexto + &13 + &10 + &13 + &10;
     eaTexto = eaTexto + " Actualizacion con fecha " + #laborn03#3 + " a las " + #laborn03#4 + &13 + &10;

     eaAsunto  = "Efectivo variacion trabajador (" + aAlfa;
     eaFichero = "";
     eaDirDest = "";

     aAlfa = #laborn03#5; |QBF aAlfa;
     eaEmpresa = ("00000" + aAlfa) % -105;
     |HAZ ConsEmail2;
|FPRC;

|PROCESO CambioJornada;
     enJorAnt = #laborn03#11;
     || aAlfa = "labcomjc;" + aNuevoTra;

     ||SUB_EJECUTA aAlfa;

     aAlfa = aNuevoTra;
     aAlfa = aNuevoTra % 105;
     nNume = aAlfa;
     enEmpLabornet = nNume;

     aAlfa = aNuevoTra;
     aAlfa = aNuevoTra % -105;
     nNume = aAlfa;
     enTraLabornet = nNume;

     |PUSHV 05012380;

     |ABRE #labcomjc;
     #labcomjc#0 = enEmpLabornet;
     #labcomjc#1 = enTraLabornet;

     |CLS;
     |PINPA #0#labcomjc;
     |PINDA #0#labcomjc;

     |LEE 000#labcomjc.=;
     FEstado = FSalida;

     #labcomjc#10 = #laborn03#11;
     #labcomjc#3 = #laborn03#20;
     |PINTA #labcomjc#10;
     |PINTA #labcomjc#3;

     |SI FEstado = 0;
          |ENDATOS #2#labcomjc;
     |SINO;
          |ENDATOS #1#labcomjc;
     |FINSI;

     |SI FSalida = 0;
          |SI FEstado = 0;
               |GRABA 020#labcomjc.a;
          |SINO;
               |GRABA 020#labcomjc.n;
          |FINSI;
     |FINSI;

     |CIERRA #labcomjc;

     |POPV;
|FPRC;

|PROCESO Contrato;
     || Impresion del contrato
     ||  Selecciono del tipo de contrato indicado el que necesito
     aAlfa = "SELECT * FROM labcodco INTO 00" + Usuario + " WHERE 1 == " + (("000" + #laborn03#14) % -103);
     |SQL aAlfa;
     aAlfa = FSalida;
     |QBF aAlfa;
     aAlfa = aAlfa - "Seleccionados:";
     nCalc = FSalida + 85; aAlfa = aAlfa % nCalc;
     nCalc = aAlfa;
     |SI nCalc = 0;
        |MENAV "    Código de contrato no encontrado"; |ERROR; |ACABA;
     |FINSI;

     aAlfa = "00" + Usuario; |NOME_DAT #labcodco#aAlfa#;
     |ABRE #labcodco;
     |SI nCalc > 1;
          |MENAV "    Hay más de una opción desde la que emitir este contrato. Seleccione el correcto";
          |CONSULTA_CLAVE #1#labcodco;
          |SI FSalida = 0;
               enNumLinea = #labcodco#0;
          |FINSI;
     |SINO;
          |LEE 000#labcodco.p;
          enNumLinea = #labcodco#0;
     |FINSI;

     aAlfa = aNuevoTra;
     aAlfa = aNuevoTra % 105;
     nNume = aAlfa;
     enEmpLabornet = nNume;

     aAlfa = aNuevoTra;
     aAlfa = aNuevoTra % -105;
     nNume = aAlfa;
     enTraLabornet = nNume;

     enEmpAntLabornet = #laborn03#5;
     enTraAntLabornet = #laborn03#6;

     enHorasSemana = #laborn03#16;
     eaTipoJornada = #laborn03#23;
     eaContrato = #laborn03#14;

     || el resto de datos desde aqui no los sabemos aun
     || eaPrimerDia = #laborn01#206;
     || eaUltimoDia = #laborn01#207;
     || eaSalario = #laborn01#208;
     || enDurMeses = #laborn01#203;
     || enDurDias = #laborn01#204;
     || eaObra = #laborn01#234;

     aAlfa = #labcodco#3; |QBF aAlfa;
     |SUB_EJECUTA aAlfa;
     |CIERRA #labcodco;
|FPRC;

|PROCESO Actualiza;
     nLabornet = 1;
     aAlfa = "    ATENCION: se va a iniciar el proceso de actualización";
     aAlfa = aAlfa + " de los datos del trabajador solicitado";
     |MENAV aAlfa;
     aAlfa = "    S¿Desea continuar?";
     |CONFI aAlfa;
     |SI FSalida = 0;
          |SUB_EJECUTA "nomvartr;AUTO";
          |SI swMeHeSalido = 1;
               |MENAV "    Proceso cancelado";
          |SINO;
               eaUsuario = #laborn03#1;      || ha introducido, permito modificar
               eaFecha = #laborn03#3;
               eaHora = #laborn03#4;
               swCambioJor = 0;
               |SI #laborn03#7 = 1;
                    |SI #laborn03#38 = "S";
                         swCambioJor = 1;
                         |HAZ CambioJornada;
                    |SINO;
                         |HAZ Contrato;
                    |FINSI;
                    |SI #laborn03#7 = 1; |O #laborn03#7 = 2;
                         aAlfa = "    S¿Desea enviar la variación online a través de ConectaSS?";
                         |CONFI aAlfa;
                         |SI FSalida = 0;
                              aAlfa1 = enEmpLabornet;  aAlfa1 = "00000" + aAlfa1; aAlfa1 = aAlfa1 % -105;
                              aAlfa2 = enTraLabornet;  aAlfa2 = "00000" + aAlfa2; aAlfa2 = aAlfa2 % -105;
                              aAlfa = aAlfa1 + aAlfa2;

                              eaVengoDe = "laborn03";
                              enHoras = #laborn03#16;
                              enHorasMax = #laborn03#17;
                              enCoeficiente = #laborn03#18;
                              |SI #laborn03#7 = 1;
                                   aAlfa = "labnct07;"+ "04" + aAlfa;
                              |FINSI;
                              |SI #laborn03#7 = 2;
                                   aAlfa = "labnct07;"+ "03" + aAlfa;
                              |FINSI;
                              |SUB_EJECUTA aAlfa;
                         |FINSI;
                    |FINSI;
               |FINSI;

               |ABRE #nompmail; |LEE 000#nompmail.p; |CIERRA #nompmail;
               |SI #nompmail#11 = "S";
                    |HAZ Email;
               |FINSI;

               enEmpresa = #laborn03#5;
               enTrabajador = #laborn03#6;
               |HAZ ActualizaLabornet;

               enEmpresa = enEmpLabornet;
               enTrabajador = enTraLabornet;
               |HAZ ActualizaLabornet;

               #laborn03#43 = enEmpLabornet;
               #laborn03#44 = enTraLabornet;
               #laborn03#45 = enRegistroCont;

/*
               || sincronizacion de esta empresa
               nEmpExt = #laborn03#5;
               aAlfa = nEmpExt;
               aAlfa = "labor001;" + aAlfa;
               |SUB_EJECUTA aAlfa;
*/

               || primero me paso todos los campos del registro que voy a borrar
               || a un temporal, ya que ha habido varios fallos en los que al
               || borrar el registro pierde la clave, asi me aseguro

               |DDEFECTO #labornx3;
               |PARA nCampo = 0; |SI nCampo [ 45; |HACIENDO nCampo = nCampo + 1;
                    #labornx3#nCampo# = #laborn03#nCampo#;
               |SIGUE;

               |BORRA 020#laborn03.a;

               |PARA nCampo = 0; |SI nCampo [ 45; |HACIENDO nCampo = nCampo + 1;
                    #laborn03#nCampo# = #labornx3#nCampo#;
               |SIGUE;

               #laborn03#0 = "A";
               #laborn03#34 = Usuario % 0810;
               #laborn03#35 = ~;
               |HORA aAlfa;
               #laborn03#36 = aAlfa;
               |GRABA 020#laborn03.n;
               |LIBERA #laborn03;

               enActualizado = 1;
          |FINSI;
     |FINSI;

     |PONCONTROL 23, "SI";
     |PTEC 808;
|FPRC;

|PROCESO Consulta;
     eaUsuario = #laborn03#1;      || permito modificar
     eaFecha = #laborn03#3;
     eaHora = #laborn03#4;
     |SUB_EJECUTA "laborn13;CONSUL";
     |ACABA;
|FPRC;

|PROCESO Modificacion;
     eaUsuario = #laborn03#1;      || permito modificar
     eaFecha = #laborn03#3;
     eaHora = #laborn03#4;
     |SUB_EJECUTA "laborn13;MODIF";
     |ACABA;
|FPRC;

|PROCESO Opciones; |TIPO 0;
     |SI enFSalida ! 0;
          FSalida = enFSalida;
     |FINSI;
     || consulta de datos introducidos
     |SI FSalida = 10;
          |SI aParam = "USUARIO"; |O aParam = "ADMIN";  || consulta el usuario los datos que
               |HAZ Modificacion;
          |FINSI;
          |SI aParam = "HISTO"; |O aParam = "HISTOUSU";
               |HAZ Consulta;           || consulta de datos introducidos
          |FINSI;                       || o ya actualizados, solo visualizar
          |ACABA;
     |FINSI;

     |SI FSalida = 11;  |Y #laborn03#0 = "P"; |Y aParam = "ADMIN";
          swMeHeSalido = 0;
          |HAZ Modificacion;
          |SI swMeHeSalido = 0;
               |HAZ Actualiza;
          |SINO;
               |MENAV "    Proceso cancelado";
          |FINSI;
          |ACABA;
     |FINSI;

     |SI FSalida = 12;  |Y #laborn03#0 = "P";
          enBorrado = 1;

          |LEE 101#laborn03.=;
          |BORRA 020#laborn03.a;
          |LIBERA #laborn03;

          #laborn03#0   = "R";
          #laborn03#39  = eaMotiv;
          |GRABA 020#laborn03.n;
          |LIBERA #laborn03;
     |FINSI;
|FPRC;

|PROCESO Mensa;  |TIPO 7;

     |SI #laborn03#0 = "P";        || pendientes de actualizar
          |SI aParam = "USUARIO";
               |MENSA "0000 <F2> Ver/modificar Datos. <F4> Borrar registro.";
          |SINO;
               |MENSA "0000 <F2> Ver datos. <F3> Actualizar variacion. <F4> Borrar registro.";
          |FINSI;
     |SINO;                   || ya actualizados
          |MENSA "0000 <F2> Consultar Datos.";
     |FINSI;
|FPRC;


|PROCESO inferior;
     #laborn03#0 = aTipo;
     #laborn03#1 = aUsuarioD;
     #laborn03#2 = "";
     #laborn03#3 = "01.01.0000";
     #laborn03#4 = "";
|FPRC;

|PROCESO superior;
     #laborn03#0 = aTipo;
     #laborn03#1 = aUsuarioH;
     #laborn03#2 = "zzzzzzzzzz";
     #laborn03#3 = "31.12.2999";
     #laborn03#4 = "zzzzzzzz";
|FPRC;

|PROCESO inferiora;
     #laborn03#0 = aTipo;
     #laborn03#1 = aUsuarioD;
     #laborn03#2 = "";
     #laborn03#3 = "01.01.0000";
     #laborn03#4 = "";
     #laborn03#5 = 0;
|FPRC;

|PROCESO superiora;
     #laborn03#0 = aTipo;
     #laborn03#1 = aUsuarioH;
     #laborn03#2 = "zzzzzzzzzz";
     #laborn03#3 = "31.12.2999";
     #laborn03#4 = "zzzzzzzz";
     #laborn03#5 = 999999;
|FPRC;

|PROCESO RecogeCampos;
     #laborn03#1 = eaUsuario;
     #laborn03#3 = efFecha;
     #laborn03#4 = eaHora;
     #laborn03#0 = eaEstado;
|FPRC;

|PROGRAMA;
     |SI enFSalida ! 12;
          |CLS;
     |FINSI;
     |CABEZA "Variaciones";

     aAlfa = Usuario % 0810;
     |SI aParam = "ADMIN"; |O aParam = "HISTO";
          aUsuarioD = "";
          aUsuarioH = "zzzzzzzzzz";
     |FINSI;
     |SI aParam = "USUARIO"; |O aParam = "HISTOUSU";
          aUsuarioD = aAlfa;
          aUsuarioH = aAlfa;
     |FINSI;

     |ABRE #laborn03;
     sw = 1;
     |PARA; |SI sw ! 0; |HACIENDO;
          sw = 0;
          |SI aParam = "ADMIN"; |O aParam = "USUARIO";
               aTipo = "P";   || los pendientes de actualizar
               |SI eaParam = "";
                    |ENTLINEAL #1#laborn03, -2, 4, 22, 1, inferior, superior;
               |SINO;
                    |HAZ RecogeCampos;
                    |LEE 101#laborn03.=;
                    |HAZ Opciones;
               |FINSI;
          |FINSI;
          |SI aParam = "HISTO"; |O aParam = "HISTOUSU";
               aTipo = "A";   || consulta del historico, los ya actualizados
               |SI eaParam = "";
                    |ENTLINEAL #2#laborn03, -4, 4, 22, 1, inferiora, superiora;
               |SINO;
                    |HAZ RecogeCampos;
                    |LEE 000#laborn03.=;
                    |HAZ Opciones;
               |FINSI;
          |FINSI;
     |SIGUE;
     |CIERRA #laborn03;
|FPRO;

|PROCESO Tipo80n03;  |TIPO 80;
     |SI PARAMETRO = "";  |ACABA;  |FINSI;

     aSiEsX = PARAMETRO % 101;
     |SI aSiEsX = "X";
         aAlfa     = PARAMETRO % 202;   enFSalida = aAlfa;
         eaEstado  = PARAMETRO % 401;
         eaUsuario = PARAMETRO % 510;
         aAlfa     = PARAMETRO % 1510;  efFecha = aAlfa;
         eaHora    = PARAMETRO % 2508;
         eaParam   = PARAMETRO % 3310;  |QBF eaParam;
         aParam    = eaParam;
     |SINO;
          aParam = PARAMETRO;  |QBF aParam;
          |SI eaParam ! "";
              eaParam = aParam;
          |FINSI;
     |FINSI;
|FPRC;
