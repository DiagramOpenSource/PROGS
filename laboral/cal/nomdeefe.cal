|FICHEROS;
    nomdeefe #0;
    labempre #1;
    labtraba #2;
    nomhicca #3; || historico
    nomficmo #4;
    nomcalca #5; || mensual
    nomcalac #6; || atrasos
|FIN;

|VARIABLES;
    seleccio = 0;
    swcab = 0;
    devenga = 0;
    deducir = 0;
    swbaja = 0;
    mes = 0;
    any = 0;
    trabaja = 0;
    tipo = 0;
    guarda = 0;
    t01 = 0;
    t02 = 0;
    t03 = 0;
    t04 = 0;
    t05 = 0;
    t06 = 0;
    t07 = 0;
    t08 = 0;
    t09 = 0;
    t10 = 0;
    t11 = 0;
    t12 = 0;
    t13 = 0;
    t14 = 0;
    t15 = 0;
    t16 = 0;
    t17 = 0;
    t18 = 0;
    t19 = 0;
    t20 = 0;
    dia = 0;
    fecsys = @;
    fecalf = "";
    alfa1 = "";
    p_mes = "";
    mensaje1 = "    ATENCION!!! Esta empresa emite paga y mes en un solo recibo ...";
    &liquido = 0;
    &talon = "";
    bisiesto = 0;
    anyo = 0;
    topemes = 0;
    nume1 = 0;
    &mesos=0;
    antempre = 0;
    anttraba = 0;
    &EURODB = 0;
     &nEmpPag = 0;
     &nAnyPag = 0;
     &nMesPag = 0;
     &nTipPag = 0;
     &aNorPag = "";
     &nTraPag = 0;
     &fFecPag = @;
     &aRegPag = "";
|FIN;

|PROCESO PParametro; |TIPO 7;
     |SI PARAMETRO="M"; |O PARAMETRO="H"; |O PARAMETRO="A";
          |CAMPO_MODIFICA #0#7,1,"N";
          #0#7=PARAMETRO;
     |FINSI;
|FPRC;

|PROCESO noentres; |TIPO 7;
|SI #0#7="M";
    |SI #0#1 = "M";
          |CAMPO_MODIFICA #0#2, 1, "S";
    |SINO;
          |CAMPO_MODIFICA #0#2, 1, "N";
    |FINSI;
|FINSI;
|FPRC;

|PROCESO FuenteDatos;|TIPO 0;
     |SI #0#7="M"; |O #0#7="H";
          |SI #0#7="M";
               alfa1="MENSUAL  ";
          |SINO;
               alfa1="HISTORICO";
          |FINSI;

          |CAMPO_VISUAL #0#1,1,"S";
          |CAMPO_VISUAL #0#2,1,"S";
          |CAMPO_VISUAL #0#3,1,"S";

          |CAMPO_VISUAL #0#8,1,"N";
          |CAMPO_VISUAL #0#9,1,"N";
          |CAMPO_VISUAL #0#10,1,"N";

          |CAMPO_POSICION #0#4,2,1537;

          |PINTA 1415,"Mes ................:                           ";
          |PINTA 1515,"A¤o ................:    ";

          |PINTA 1713,"³ Mes / Paga .........:                           ³";
          |PINTA 1813,"³ Tipo Recibos .......:                           ³";
          |PINTA 1913,"ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ";

          |PINTA #0#1;
          |PINTA #0#2;
          |PINTA #0#3;
          |PINTA #0#4;
     |FINSI;
     |SI #0#7="A";
          alfa1="ATRASOS  ";
          |CAMPO_VISUAL #0#1,1,"N";
          |CAMPO_VISUAL #0#2,1,"N";
          |CAMPO_VISUAL #0#3,1,"N";

          |CAMPO_VISUAL #0#8,1,"S";
          |CAMPO_VISUAL #0#9,1,"S";
          |CAMPO_VISUAL #0#10,1,"S";

          |CAMPO_POSICION #0#4,2,1533;

          |PINTA 1415,"Desde Mes ......:       Hasta Mes ......:       ";
          |PINTA 1515,"A¤o ............:           ";
          |PINTA 1713,"³ Tipo Desglose :                                 ³";
          |PINTA 1813,"ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ";
          |PINTA 1913,"                                                   ";

          |PINTA #0#8;
          |PINTA #0#9;
          |PINTA #0#10;
          |PINTA #0#4;
     |FINSI;

     |PINTA 0840,alfa1;
     |PINTA #0#7;
|FPRC;

|| ************************************************************************
||                    CALCULOS DESDE EL MANTENIMIENTO
|| ************************************************************************

|PROCESO compru; |TIPO 8;
|ABRE #4;
|LEE 040#4p;
|SI FSalida ! 0;
    |MENAV "    ATENCION!!! Fichero `Tipos de Moneda` sin definir ...";
|FINSI;
|CIERRA #4;
|FPRC;

|PROCESO defecto; |TIPO 7; ||calcula el a¤o por defecto segun fecha sistema
    fecsys = ~;
    alfa1 = fecsys;
    alfa1 = alfa1 % -102;
    #0Campo = alfa1;
|PINTA #0Campo;
|FPRC;

|PROCESO mirames; |TIPO 7; ||CALCULA EL MES POR DEFECTO SEGUN FECHA SISTEMA
    fecsys = ~;
    alfa1 = fecsys;
    alfa1 = alfa1 % 402;
    #0Campo = alfa1;
|PINTA #0Campo;
|FPRC;

|PROCESO pintames; |TIPO 0;      || PINTA EL MES LITERAL
|SI #0#4 < 80;
        anyo = #0#4 + 2000;
|SINO;
        anyo = #0#4 + 1900;
|FINSI;

    nume1 = anyo $ 4;
|SI nume1 = 0;
        bisiesto = 1;
|SINO;
        bisiesto = 0;
|FINSI;
|SI #0Campo =  1;  p_mes = "ENERO     ";  topemes = 31; |FINSI;
|SI #0Campo =  2;  p_mes = "FEBRERO   ";  topemes = 28 + bisiesto; |FINSI;
|SI #0Campo =  3;  p_mes = "MARZO     ";  topemes = 31; |FINSI;
|SI #0Campo =  4;  p_mes = "ABRIL     ";  topemes = 30; |FINSI;
|SI #0Campo =  5;  p_mes = "MAYO      ";  topemes = 31; |FINSI;
|SI #0Campo =  6;  p_mes = "JUNIO     ";  topemes = 30; |FINSI;
|SI #0Campo =  7;  p_mes = "JULIO     ";  topemes = 31; |FINSI;
|SI #0Campo =  8;  p_mes = "AGOSTO    ";  topemes = 31; |FINSI;
|SI #0Campo =  9;  p_mes = "SEPTIEMBRE";  topemes = 30; |FINSI;
|SI #0Campo = 10;  p_mes = "OCTUBRE   ";  topemes = 31; |FINSI;
|SI #0Campo = 11;  p_mes = "NOVIEMBRE ";  topemes = 30; |FINSI;
|SI #0Campo = 12;  p_mes = "DICIEMBRE ";  topemes = 31; |FINSI;
|PINTA 1443, p_mes;
|FPRC;

|PROCESO miratipo; |TIPO 0;   || CHEQUEA MES/PAGA SEGUN LA CLAVE RECIBO EXTRA
|SI #0Campo = "P"; |Y #1#25 = 2;
    |MENAV mensaje1;
    |ERROR;
|FINSI;
|FPRC;

|PROCESO RegPagado;
     nEmpPag = #labtraba#0;
     nAnyPag = 2000 + #0#4;
     |SI #0#7 = "M";
          nMesPag = #nomcalca#2;
          nTipPag = #nomcalca#3;
     |FINSI;
     |SI #0#7 = "H";
          nMesPag = #nomhicca#2;
          nTipPag = #nomhicca#3;
     |FINSI;
     |SI #0#7 = "A";
          nMesPag = #nomcalac#2;
          nTipPag = #nomcalac#3;
     |FINSI;
     |SI #0#1 = "M";
          aNorPag = "N";
     |SINO;
          aNorPag = "P";
     |FINSI;
     nTraPag = #labtraba#1;
     fFecPag = ~;
     aRegPag = "";
     |HAZ Pagado;
|FPRC;


|| ************************************************************************
||                            SUBRUTINAS
|| ************************************************************************
|PROCESO desglose;
    trabaja = liquido;
|PARA tipo = 1; |SI tipo[20; |HACIENDO tipo = tipo + 1;
        guarda = tipo + 20;              || puntero para campos de trabajo
        #4guarda = 0;
    |SI #4tipo [ trabaja;
            #4guarda = ((trabaja / #4tipo) - .50);     || de paso redondea
            trabaja = (trabaja - (#4guarda * #4tipo)) ! EURODB;
    |FINSI;
    |HAZ acumula;
|SIGUE;
|HAZ RegPagado;
|FPRC;

|PROCESO acumula;
|SI tipo =  1;  t01 = t01 + #4guarda;  |FINSI;
|SI tipo =  2;  t02 = t02 + #4guarda;  |FINSI;
|SI tipo =  3;  t03 = t03 + #4guarda;  |FINSI;
|SI tipo =  4;  t04 = t04 + #4guarda;  |FINSI;
|SI tipo =  5;  t05 = t05 + #4guarda;  |FINSI;
|SI tipo =  6;  t06 = t06 + #4guarda;  |FINSI;
|SI tipo =  7;  t07 = t07 + #4guarda;  |FINSI;
|SI tipo =  8;  t08 = t08 + #4guarda;  |FINSI;
|SI tipo =  9;  t09 = t09 + #4guarda;  |FINSI;
|SI tipo = 10;  t10 = t10 + #4guarda;  |FINSI;
|SI tipo = 11;  t11 = t11 + #4guarda;  |FINSI;
|SI tipo = 12;  t12 = t12 + #4guarda;  |FINSI;
|SI tipo = 13;  t13 = t13 + #4guarda;  |FINSI;
|SI tipo = 14;  t14 = t14 + #4guarda;  |FINSI;
|SI tipo = 15;  t15 = t15 + #4guarda;  |FINSI;
|SI tipo = 16;  t16 = t16 + #4guarda;  |FINSI;
|SI tipo = 17;  t17 = t17 + #4guarda;  |FINSI;
|SI tipo = 18;  t18 = t18 + #4guarda;  |FINSI;
|SI tipo = 19;  t19 = t19 + #4guarda;  |FINSI;
|SI tipo = 20;  t20 = t20 + #4guarda;  |FINSI;
|FPRC;

|PROCESO totalemp;
|PARA tipo = 21; |SI tipo [ 40; |HACIENDO tipo = tipo + 1;
    |SI tipo = 21;  #4tipo = t01;  |FINSI;
    |SI tipo = 22;  #4tipo = t02;  |FINSI;
    |SI tipo = 23;  #4tipo = t03;  |FINSI;
    |SI tipo = 24;  #4tipo = t04;  |FINSI;
    |SI tipo = 25;  #4tipo = t05;  |FINSI;
    |SI tipo = 26;  #4tipo = t06;  |FINSI;
    |SI tipo = 27;  #4tipo = t07;  |FINSI;
    |SI tipo = 28;  #4tipo = t08;  |FINSI;
    |SI tipo = 29;  #4tipo = t09;  |FINSI;
    |SI tipo = 30;  #4tipo = t10;  |FINSI;
    |SI tipo = 31;  #4tipo = t11;  |FINSI;
    |SI tipo = 32;  #4tipo = t12;  |FINSI;
    |SI tipo = 33;  #4tipo = t13;  |FINSI;
    |SI tipo = 34;  #4tipo = t14;  |FINSI;
    |SI tipo = 35;  #4tipo = t15;  |FINSI;
    |SI tipo = 36;  #4tipo = t16;  |FINSI;
    |SI tipo = 37;  #4tipo = t17;  |FINSI;
    |SI tipo = 38;  #4tipo = t18;  |FINSI;
    |SI tipo = 39;  #4tipo = t19;  |FINSI;
    |SI tipo = 40;  #4tipo = t20;  |FINSI;
|SIGUE;
|PIEINF;
|FININF;
|FPRC;

|PROCESO desglosa;     || DESGLOSA EN DIA, MES Y ANYO UNA FECHA DD.MM.AAAA
    alfa1 = fecalf %  102;   dia = alfa1;
    alfa1 = fecalf %  402;   mes = alfa1;
    alfa1 = fecalf % -104;   any = alfa1;
|SI alfa1 = "....";
        dia = 0;
        mes = 0;
        any = 0;
|FINSI;
|FPRC;

|| ************************************************************************
||   Historico
|| ************************************************************************

|PROCESO efectivH;   || EMITE EFECTIVOS
|SI swcab = 0;
        swcab = 1;
    |INFOR "nomhiefe";
|FINSI;
|HAZ leetrabaH;
|SI #2#70 = "E";
        seleccio = 1;
    |SI #0#1 = "M";  |HAZ mensualH;  |FINSI;
    |SI #0#1 = "P";  |HAZ pagasH;    |FINSI;
        liquido = devenga - deducir;
    |HAZ desglose;
    |PRINT;
|FINSI;
|FPRC;

|DEFBUCLE nomhiefe0;
 #3#1;
 ;
 #0#0,#0#5,#0#4,#0#3,#0#2;
 #0#0,#0#6,#0#4,#0#3,#0#2;
 e;
 NULL, efectivH;
|FIN;


|PROCESO leetrabaH;    || LEE EL TRABAJADOR
    #2#0 = #3#0;
    #2#1 = #3#1;
|LEE 020#2=;
|SI FSalida ! 0;
    |DDEFECTO #2;
|FINSI;
    swbaja = 0;
    fecalf = #3#5;
|HAZ desglosa;
|SI mes = #0#3; |Y any = #0#4; |Y dia ! topemes;    swbaja = 1;    |FINSI;
|FPRC;

|PROCESO mensualH;   || CALCULO LIQUIDO PARA CLAVE 'M'
    devenga = #3#46;
    deducir = #3#48 +#3#49 +#3#50 +#3#51 +#3#52 +#3#53 +#3#54 +#3#114;
|SI swbaja = 1; |O #1#25 = 2;
        devenga = devenga + #3#16;   || si es baja este mes o la paga va
        deducir = deducir + #3#17;   || /junto con el recibo, acumula la paga
    |SI #1#25 = 2;                   || si las pagas van en un solo recibo
        deducir = deducir + #3#128;  || mete anticipos de paga extra
    |FINSI;
|FINSI;
|FPRC;

|PROCESO pagasH;   || CALCULO LIQUIDO PARA CLAVE 'P'
    devenga = #3#16;
    deducir = #3#17 + #3#128;        || mete anticipos paga extra
|FPRC;



|PROCESO PHistorico;
|IMPRE 0;
|SI FSalida = 0;
    |ABRE #1;
    |ABRE #2;
    |BUCLE nomhiefe0;
    |SI seleccio = 1;
        |HAZ totalemp;
    |SINO;
        |MENAV "    Seleccion vacia ...";
    |FINSI;
    |CIERRA #1;
    |CIERRA #2;
|SINO;
    |MENAV "    Proceso abortado ...";
|FINSI;
|FINIMP;
|FPRC;

|| ************************************************************************
||   Mensual
|| ************************************************************************

|PROCESO efectivM;   || EMITE EFECTIVOS
|SI swcab = 0;
        swcab = 1;
    |INFOR "nomefect";
|FINSI;
|HAZ leetrabaM;
|SI #2#70 = "E";
        seleccio = 1;
    |SI #0#1 = "M";  |HAZ mensualM;  |FINSI;
    |SI #0#1 = "P";  |HAZ pagasM;    |FINSI;
        liquido = devenga - deducir;
    |HAZ desglose;
    |PRINT;
     |HAZ RegPagado;
|FINSI;
|FPRC;

|DEFBUCLE nomefect0;
#5#1;
;
#0#0,#0#5,#0#3,#0#2;
#0#0,#0#6,#0#3,#0#2;
e;
NULL, efectivM;
|FIN;


|PROCESO leetrabaM;    || LEE EL TRABAJADOR
    #2#0 = #5#0;
    #2#1 = #5#1;
|LEE 020#2=;
|SI FSalida ! 0;
    |DDEFECTO #2;
|FINSI;
    swbaja = 0;
    fecalf = #5#5;
|HAZ desglosa;
|SI mes = #0#3; |Y any = #0#4; |Y dia ! topemes;    swbaja = 1;    |FINSI;
|FPRC;

|PROCESO mensualM;   || CALCULO LIQUIDO PARA CLAVE 'M'
    devenga = #5#46;
    deducir = #5#48 +#5#49 +#5#50 +#5#51 +#5#52 +#5#53 +#5#54 +#5#75;
|SI swbaja = 1; |O #1#25 = 2;
        devenga = devenga + #5#16;   || si es baja este mes o la paga va
        deducir = deducir + #5#17;   || /junto con el recibo, acumula la paga
    |SI #1#25 = 2;                   || si las pagas van en un solo recibo
        deducir = deducir + #5#86;   || mete anticipos de paga extra
    |FINSI;
|FINSI;
|FPRC;

|PROCESO pagasM;   || CALCULO LIQUIDO PARA CLAVE 'P'
    devenga = #5#16;
    deducir = #5#17 + #5#86;
|FPRC;

|PROCESO PMensual;
|ABRE #1;
|ABRE #2;
|IMPRE 0;
|SI FSalida = 0;
    |BUCLE nomefect0;
    |SI seleccio = 1;
        |HAZ totalemp;
    |SINO;
        |MENAV "    * SELECCION VACIA *";
    |FINSI;
|SINO;
    |MENAV "    ATENCION!!! Proceso Abortado ...";
|FINSI;
|FINIMP;
|CIERRA #1;
|CIERRA #2;
|FPRC;

|| ************************************************************************
||   Atrasos
|| ************************************************************************

|PROCESO limpiaA;
|PARA tipo = 21; |SI tipo [ 40; |HACIENDO tipo = tipo + 1;
        #4tipo = 0;
|SIGUE;
    t01=0; t02=0; t03=0; t04=0; t05=0; t06=0; t07=0; t08=0; t09=0; t10=0;
    t11=0; t12=0; t13=0; t14=0; t15=0; t16=0; t17=0; t18=0; t19=0; t20=0;
|FPRC;

|PROCESO efectivA;   || EMITE EFECTIVOS
|SI #0#8 = 2;  |HAZ rupturaA;  |FINSI;
|HAZ leetrabaA;
|SI #2#70 = "E";
    |SI swcab = 0;
            swcab = 1;
            seleccio = 1;
        |HAZ refundi2A;
        |INFOR "nomatefe";
    |FINSI;
    |SI #0#8 = 1; |HAZ efec1A; |FINSI;
    |SI #0#8 = 2; |HAZ efec2A; |FINSI;
|FINSI;
|FPRC;

|DEFBUCLE nomatefe0;
#6#1;
;
#0#0, #0#5, #0#9, 5;
#0#0, #0#6, #0#10, 5;
e;
NULL, efectivA;
|FIN;

|DEFBUCLE nomatefe1;
#6#1;
;
#0#0, #0#5, mesos, 5;
#0#0, #0#6, mesos, 5;
e;
NULL, efectivA;
|FIN;


|PROCESO rupturaA;
|SI antempre = 0;|Y anttraba = 0; antempre = #6#0; anttraba = #6#1; |FINSI;
|SI antempre ! #6#0; |O anttraba ! #6#1;
        liquido = devenga - deducir;
    |HAZ desglose;
    |PRINT;
        antempre = #6#0;
        anttraba = #6#1;
    |HAZ refundi2A;
|FINSI;
|FPRC;

|PROCESO efec1A;     || EMITE TALONES (POR MESES)
|HAZ mensualA;
    liquido = devenga - deducir;
|HAZ desglose;
|PRINT;
|FPRC;

|PROCESO efec2A;     || EMITE TALONES (REFUNDIDO)
|HAZ refundi1A;
|HAZ RegPagado;
|FPRC;

|PROCESO leetrabaA;    || LEE EL TRABAJADOR
    #2#0 = #6#0;
    #2#1 = #6#1;
|LEE 020#2=;
|SI FSalida ! 0;
    |DDEFECTO #2;
|FINSI;
    swbaja = 0;
    fecalf = #6#5;
|HAZ desglosa;
|SI mes = #0#10; |Y any = #0#4;
        swbaja = 1;
|FINSI;
|FPRC;

|PROCESO mensualA;   || CALCULO LIQUIDO PARA CLAVE 'M'
    devenga = #6#46;
    deducir = #6#48 +#6#49 +#6#50 +#6#51 +#6#52 +#6#53 +#6#54 +#6#75;
|SI swbaja = 1; |O #1#25 = 2;
        devenga = devenga + #6#16;   || si es baja este mes o la paga va
        deducir = deducir + #6#17;   || /junto con el recibo, acumula la paga
|FINSI;
|FPRC;

|PROCESO refundi1A;   || CALCULO LIQUIDO PARA CLAVE 'M'(REFUNDIDO)
    devenga = devenga +#6#46;
    deducir = deducir +#6#48 +#6#49 +#6#50 +#6#51 +#6#52 +#6#53 +#6#54 +#6#75;
|SI swbaja = 1; |O #1#25 = 2;
        devenga = devenga + #6#16;   || si es baja este mes o la paga va
        deducir = deducir + #6#17;   || /junto con el recibo, acumula la paga
|FINSI;
|FPRC;

|PROCESO refundi2A;
    devenga = 0;
    deducir = 0;
|FPRC;

|PROCESO pagasA;   || CALCULO LIQUIDO PARA CLAVE 'P'
    devenga = #6#16;
    deducir = #6#17;
|FPRC;

|PROCESO PAtrasos; |TIPO 3;   || LANZA EL PROCESO
|ABRE #1;
|ABRE #2;
|IMPRE 0;
|SI FSalida = 0;
    |SI #0#8 = 2;
            seleccio = 0;
            swcab = 0;
        |BUCLE nomatefe0;
        |SI seleccio = 1;
                liquido = devenga - deducir;
            |HAZ desglose;
            |PRINT;
            |HAZ totalemp;
            |HAZ limpiaA;
        |SINO;
            |MENAV "    * SELECCION VACIA *";
        |FINSI;
    |SINO;
        |PARA mesos = #0#9; |SI mesos [ #0#10; |HACIENDO mesos = mesos + 1;
                seleccio = 0;
                swcab = 0;
            |BUCLE nomatefe1;
            |SI seleccio = 1;
                |HAZ totalemp;
                |HAZ limpiaA;
            |FINSI;
        |SIGUE;
    |FINSI;
|SINO;
    |MENAV "    ATENCION!!! Proceso Abortado ...";
|FINSI;
|FINIMP;
|CIERRA #1;
|CIERRA #2;
|FPRC;


|PROCESO impre; |TIPO 3;   || LANZA EL PROCESO
     |SI #0#7="M";
          |HAZ PMensual;
     |FINSI;
     |SI #0#7="H";
          |HAZ PHistorico;
     |FINSI;
     |SI #0#7="A";
          |HAZ PAtrasos;
     |FINSI;
|FPRC;
