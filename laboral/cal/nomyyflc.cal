|FICHEROS;
     nomyyflc;
     labempre;            || empresas
     labtraba;            || trabajadores
     labcentr;            || centros
     nomtrflc;            || temporal FLC
     nomcalca;            || calculos cabs.
     nomcenes;            || centros especiales
     nomflctm;            || tmp trabajadores
|FIN;

|VARIABLES;
     alfa1 = "";
     alfa2 = "";
     alfa3 = "";
     alfa4 = "";
     nume1 = 0;
     nume2 = 0;
     nume3 = 0;
     nume4 = 0;
     para1 = 0;
     para2 = 0;
     para3 = 0;
     para4 = 0;
     fech1 = @;
     fech2 = @;
     fech3 = @;
     fech4 = @;
     FEstado = 0;
     seleccio = 0;
     p_mes = "";
     cta_co = "";
     baseP = 0;
     baseM = 0;
     baseIT = 0;
     diff = 0;
     swNoSigas = 0;
     aCNAE = "";

     aEmp                = "";
     aAlfa               = "";
     aNomEmp             = "";

     nOk                 = 0;
     nOpcion             = 0;
     nPas                = 0;

     &aEjemplar = "";
     &ss1 = "";
     &ss2 = "";
     &ss3 = "";
     &trab = 0;
     &base = 0;
     &tipo = 0;
     &cuot = 0;
     &dmes = 0;
     &dany = 0;
     &hmes = 0;
     &hany = 0;
     &pago = "";
     &recp = 0;
     &recc = 0;
     &impo = 0;

     &eaVengoDe          = "";
     &nArchi             = 0;
     &swErrorDSArchi     = 0;
     &enDesdeMes         = 0;
     &enAny              = 0;
     &eaTitulo           = "";

     {-1}aMenu           = "";
         aMenu1          = "2400";
         aMenu2          = "1";
         aMenu3          = "Archivo documental activado. Elija opci¢n: ";
         aMenu4          = "IAB";
         aMenu5          = "Imprimir";
         aMenu6          = "Archivar";
         aMenu7          = "Ambos";

     nBaseERE = 0;
|FIN;

|INCLUYE i_tempo;
____________________________________/ CALCULOS DE PANTALLA

|PROCESO AvisoCrystal; |TIPO 7;
     |PINTA 0847, "ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿";
     |PINTA 0947, "³ Este modelo se imprime en     ³";
     |PINTA 1047, "³  formato                      ³";
     |PINTA 1147, "³                               ³";
     |PINTA 1247, "ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ";
     |ATRI I;
     |PINTA 1061, "CRYSTAL REPORTS.";
     |ATRI 0;
|FPRC;

|PROCESO defecto; |TIPO 7; ||calcula el a¤o por defecto segun fecha sistema
     fech1 = ~;
     alfa1 = fech1;
     alfa1 = alfa1 % -104;
     #nomyyflc#Campo# = alfa1;
     |PINTA #nomyyflc#Campo#;
|FPRC;

|PROCESO mirames; |TIPO 7; ||CALCULA EL MES POR DEFECTO SEGUN FECHA SISTEMA
     fech1 = ~;
     alfa1 = fech1;
     alfa1 = alfa1 % 402;
     #nomyyflc#Campo# = alfa1;
     |PINTA #nomyyflc#Campo#;
|FPRC;

|PROCESO pintames; |TIPO 0;      || PINTA EL MES LITERAL
     |SI #nomyyflc#Campo# =  1;  p_mes = "ENERO     ";  |FINSI;
     |SI #nomyyflc#Campo# =  2;  p_mes = "FEBRERO   ";  |FINSI;
     |SI #nomyyflc#Campo# =  3;  p_mes = "MARZO     ";  |FINSI;
     |SI #nomyyflc#Campo# =  4;  p_mes = "ABRIL     ";  |FINSI;
     |SI #nomyyflc#Campo# =  5;  p_mes = "MAYO      ";  |FINSI;
     |SI #nomyyflc#Campo# =  6;  p_mes = "JUNIO     ";  |FINSI;
     |SI #nomyyflc#Campo# =  7;  p_mes = "JULIO     ";  |FINSI;
     |SI #nomyyflc#Campo# =  8;  p_mes = "AGOSTO    ";  |FINSI;
     |SI #nomyyflc#Campo# =  9;  p_mes = "SEPTIEMBRE";  |FINSI;
     |SI #nomyyflc#Campo# = 10;  p_mes = "OCTUBRE   ";  |FINSI;
     |SI #nomyyflc#Campo# = 11;  p_mes = "NOVIEMBRE ";  |FINSI;
     |SI #nomyyflc#Campo# = 12;  p_mes = "DICIEMBRE ";  |FINSI;
     |PINTA 1432, p_mes;
|FPRC;
____________________________________/ LECTURA DEL TEMPORAL E IMPRIME
____________________________________/ PREPARA TEMPORAL
|PROCESO cta_cotizacion;
     #labempre#0 = #nomtrflc#0;
     |LEE 000#labempre.=;
     |SI FSalida ! 0;  |DDEFECTO #labempre;  |FINSI;

     aCNAE = #labempre#128;

     |SI #labtraba#45 = "087"; |O #labtraba#45 = "097";
                               |O #labtraba#45 = "085";
                               |O #labtraba#45 = "064";
                               |O #labtraba#45 = "065";
                               |O #labtraba#45 = "421";
         |DDEFECTO #nomcenes;
         #nomcenes#0 = #labtraba#0;
         #nomcenes#13= #labtraba#77;
         |SI #labtraba#45 = "097";  |O #labtraba#45 = "085";
                                    |O #labtraba#45 = "421";
             #nomcenes#1 = "87";
         |SINO;
             |SI #labtraba#45 = "065";
                 #nomcenes#1 = "64";
             |SINO;
                 #nomcenes#1 = #labtraba#45 % 2;
             |FINSI;
         |FINSI;

         |LEE 000#nomcenes.=;
         |SI FSalida ! 0;  |DDEFECTO #nomcenes;  |FINSI;

         cta_co = #nomcenes#10;      || cta.cto. del centro de trabajo especial
    |SINO;
         cta_co = #labempre#13;       || cta.cot. de la empresa
         #labcentr#0 = #labtraba#0;
         #labcentr#1 = #labtraba#77;
         |LEE 000#labcentr.=;
         |SI FSalida ! 0;  |DDEFECTO #labcentr;  |FINSI;

         cta_co = #labcentr#10;        || cta.cot. centro de trabajo
         aCNAE  = #labcentr#22; |QBF aCNAE;
         |SI aCNAE = "";
             aCNAE = #labempre#128; |QBF aCNAE;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO nomcalca;
     nOk = 0;
     #labtraba#0 = #nomcalca#0;
     #labtraba#1 = #nomcalca#1;
     |LEE 000#labtraba.=;
     |SI FSalida = 0;  |Y #labtraba#87 ] #nomyyflc#2;
                       |Y #labtraba#87 [ #nomyyflc#3;
                       |Y #labtraba#42  ! "H";
                       |Y #labtraba#247 ! 163;
                       |Y #labtraba#247 ! 613;
          nOk = 1;
     |FINSI;

     |SI nOk = 0;  |ACABA;  |FINSI;

     seleccio = 1;
     |DDEFECTO #nomtrflc;
     #nomtrflc#0 = #nomcalca#0;             || empresa
     |HAZ cta_cotizacion;

     #nomtrflc#13 = cta_co;
     |LEE 101#nomtrflc.=;
     |SI FSalida ! 0;
         |DDEFECTO #nomtrflc;
          #nomtrflc#0 = #nomcalca#0;             || empresa
          #nomtrflc#13 = cta_co;
          |GRABA 020#nomtrflc.b;
     |FINSI;

     #nomflctm#0 = #labtraba#0;
     #nomflctm#1 = #labtraba#7;
     #nomflctm#2 = #nomyyflc#4;
     |LEE 000#nomflctm.=;
     |SI FSalida ! 0;
         #nomtrflc#1 = #nomtrflc#1 + 1;          || nro. trabajadores

         |DDEFECTO #nomflctm;
         #nomflctm#0 = #labtraba#0;
         #nomflctm#1 = #labtraba#7;
         #nomflctm#2 = #nomyyflc#4;
         |GRABA 020#nomflctm.n;
     |FINSI;
     |LIBERA #nomflctm;

     |SI #nomcalca#65 = 0;     || tenemos maternidad
         baseP = #nomcalca#40;
     |SINO;
         baseP = #nomcalca#40;
         baseM = (#nomcalca#35 * #nomcalca#65) ! 2;

         baseIT = (#nomcalca#35 * (#nomcalca#65 + #nomcalca#31 + #nomcalca#32)) ! 2;
         |SI baseIT ! #nomcalca#62;        || comparo base en baja de mto. con base en baja real
             diff = baseIT - #nomcalca#62;  || si son distintas ajusto restando diferencia
             baseM = baseM - diff;  || a base de comunes de y profesionales, esto es
         |FINSI;                    || por lo del swAjuste3 en nomcal_3

         baseP = baseP + baseM;
     |FINSI;

     |SI #nomcalca#78 ! 0;
          nBaseERE = #nomcalca#128;
          |SI #labtraba#188 = "S";
               nBaseERE = nBaseERE - ((nBaseERE % #labtraba#219) ! 2);
          |FINSI;
          baseP = baseP + nBaseERE;
     |FINSI;

     baseP = baseP + #nomcalca#132; || base Control IT

     #nomtrflc#2 = #nomtrflc#2 + baseP;     || base A.T. y E.P.
     #nomtrflc#3 = #nomyyflc#8;              || (%) a aplicar
     #nomtrflc#4 = #nomtrflc#2 % #nomtrflc#3;       || cuota
     #nomtrflc#5 = #nomyyflc#4;              || desde mes
     #nomtrflc#6 = #nomtrflc#5;              || hasta mes
     #nomtrflc#7 = #nomyyflc#5;              || desde a¤o
     #nomtrflc#8 = #nomtrflc#7;              || hasta a¤o
     #nomtrflc#9 = #nomyyflc#6;              || forma de pago
     #nomtrflc#10 = #nomyyflc#7;             || (%) recargo
     #nomtrflc#11 = #nomtrflc#4 % #nomtrflc#10;     || cuota recargo
     #nomtrflc#12 = #nomtrflc#4 + #nomtrflc#11;     || importe a ingresar
     #nomtrflc#14 = aCNAE;

     |GRABA 020#nomtrflc.a;
     |LIBERA #nomtrflc;
|FPRC;

|DEFBUCLE nomcalca;
     #nomcalca#1;
     ;
     #nomyyflc#0,     1, #nomyyflc#4, 0;
     #nomyyflc#1, 99999, #nomyyflc#4, 9;
     ;
     NULL, nomcalca;
|FIN;

|PROCESO CreaTempos;
     |PINTA 2305, "Preparando Temporal ...";

     |ABRE #labempre;
     |ABRE #labtraba;
     |ABRE #labcentr;
     |ABRE #nomtrflc;
     |ABRE #nomcenes;
     |ABRE #nomflctm;
     |BUCLE nomcalca;
     |CIERRA #labempre;
     |CIERRA #labtraba;
     |CIERRA #labcentr;
     |CIERRA #nomtrflc;
     |CIERRA #nomcenes;
     |CIERRA #nomflctm;

     |BLIN 23;
|FPRC;


|PROCESO CargaVar;               || al renombrar el fichero
     trab = #nomtrflc#1;         || nro. trabajadores  ||/hay que utilizar puentes
     base = #nomtrflc#2;         || base AT y ET
     tipo = #nomtrflc#3;         || (%)tipo
     cuot = #nomtrflc#4;         || cuota
     dmes = #nomtrflc#5;         || desde mes
     dany = #nomtrflc#7;         || desde a¤o
     hmes = #nomtrflc#6;         || hasta mes
     hany = #nomtrflc#8;         || hasta a¤o
     pago = #nomtrflc#9;         || forma pago
     recp = #nomtrflc#10;        || (%) recargo
     recc = #nomtrflc#11;        || cuota recargo
     impo = #nomtrflc#12;        || importe a ingresar
|FPRC;

|PROCESO lee_empresa;
     #labempre#0 = #nomtrflc#0;
     |LEE 000#labempre.=;
     |SI FSalida ! 0;  |DDEFECTO #labempre;  |FINSI;

     alfa1 = #nomtrflc#13;|QBF alfa1;
     alfa2 = alfa1 % 0;
     nume1 = alfa2;
     nume1 = (nume1 - 4) + 300;
     ss1   = alfa1 % 102;
     ss2   = alfa1 % nume1;
     ss3   = alfa1 % -102;

     #labempre#128 = #nomtrflc#14;
|FPRC;

|PROCESO nomtrflcP;
     |HAZ lee_empresa;
     |SI #labempre#42 ! "S";  |ACABA;  |FINSI;
     |SI #nomtrflc#2 [ 0;     |ACABA;  |FINSI;

     |HAZ CargaVar;

     aEjemplar = "Ejemplar para la oficina recaudadora";
     |PRINT;

     aEjemplar = "Ejemplar para el interesado";
     |PRINT;
|FPRC;

|DEFBUCLE nomtrflcP;
     #nomtrflc#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, nomtrflcP;
|FIN;

|PROCESO Papel;
     Impresora = "CrystalReport";
     |IMPRE -1;
     |SI FSalida ! 0;
         |MENAV "    Proceso abortado ...";
         |ACABA;
     |FINSI;

     |INFOR "nomyyflc";
     |PINTA 2305, "Imprimiendo ...";
     |ABRE #labempre;
     |BUCLE nomtrflcP;
     |CIERRA #labempre;

     |PIEINF;
     |FININF;
     |FINIMP;
|FPRC;

|PROCESO Mensaje;
     aEmp = "                                             ";

     aAlfa   = ("00000" + #labempre#0) % -105;
     aNomEmp = #labempre#2;
     aNomEmp = aNomEmp % 126;
     aEmp = "Empresa: " + aAlfa + " - " + aNomEmp;
     aEmp = " " + aEmp + " ";

     |CUADRO 1217 1619;
     |ATRI R;
     |PINTA 1318, "      - Procesando archivo de FLC -                 ";
     |PINTA 1418, "                                                    ";
     |PINTA 1518, "                                                    ";
     |PINTA 1518, aEmp;
     |ATRI 0;
|FPRC;

|PROCESO nomtrflcA;
     |SI nPas = 0;
         |SI #nomtrflc#2 [ 0;  |ACABA;  |FINSI;

         nOk = 1;
         |ACABA;
     |FINSI;

     |SI #nomtrflc#2 [ 0;  |ACABA;  |FINSI;

     alfa1 = #nomtrflc#13;|QBF alfa1;
     alfa2 = alfa1 % 0;
     nume1 = alfa2;
     nume1 = (nume1 - 4) + 300;
     ss1   = alfa1 % 102;
     ss2   = alfa1 % nume1;
     ss3   = alfa1 % -102;

     #labempre#128 = #nomtrflc#14;

     |HAZ CargaVar;

     aEjemplar = "Ejemplar para la oficina recaudadora";
     |PRINT;

     aEjemplar = "Ejemplar para el interesado";
     |PRINT;
|FPRC;

|DEFBUCLE nomtrflcA;
     #nomtrflc#1;
     ;
     #labempre#0, "               ";
     #labempre#0, "zzzzzzzzzzzzzzz";
     ;
     NULL, nomtrflcA;
|FIN;

|PROCESO labempre;
     nPas = 0;
     nOk  = 0;
     |BUCLE nomtrflcA;
     |SI nOk = 0;  |ACABA;  |FINSI;

     |HAZ Mensaje;
     |HAZ IMPREArchi;

     nPas = 1;
     |BUCLE nomtrflcA;

     |PIEINF;
     |FININF;
     |FINIMP;

     |SI swErrorDSArchi = 0;
         |HAZ Archivando;
     |FINSI;
|FPRC;

|DEFBUCLE labempre;
     #labempre#1;
     42;
     #nomyyflc#0, "S";
     #nomyyflc#1, "S";
     ;
     NULL, labempre;
|FIN;

|PROCESO Archivado;
     enDesdeMes   = #nomyyflc#4;
     enAny        = #nomyyflc#5;
     eaTitulo     = "FLC";

     |ABRET #nomtrflc;
     |BUCLE labempre;
     |CIERRAT #nomtrflc;
|FPRC;

|PROCESO imprime; |TIPO 3;
     |HAZ nom_usu;

     alfa1 = nom_tmp;
     |NOME_DAT #nomtrflc#alfa1#;
     |ABRE #nomtrflc; |CIERRA #nomtrflc;  |DELINDEX #nomtrflc;

     alfa2 = Usuario; |QBF alfa2;    alfa1 = "l" + alfa2;
     |NOME_DAT #nomflctm#alfa2#;
     |ABRE #nomflctm;  |CIERRA #nomflctm;  |DELINDEX #nomflctm;

     |HAZ CreaTempos;

     |SI seleccio = 0;
         |MENAV "    Seleccion vacia ...";
         |DELINDEX #nomtrflc;
         |DELINDEX #nomflctm;
         |ACABA;
     |FINSI;

     eaVengoDe = "nomyyflc";
     |HAZ MiraSiArchiva;
     |SI nArchi = 0;
         |HAZ Papel;

         |ACABA;
     |FINSI;

     |MENU aMenu;
     nOpcion = FSalida;
     |SI nOpcion = 1;  |HAZ Papel;      |FINSI;
     |SI nOpcion = 2;  |HAZ Archivado;  |FINSI;
     |SI nOpcion = 3;  |HAZ Papel;      |FINSI;
     |SI nOpcion = 3;  |HAZ Archivado;  |FINSI;

     |DELINDEX #nomtrflc;
     |DELINDEX #nomflctm;
|FPRC;
