 |FICHEROS;
     euro0102 #0;

     euro0001;
     euro0002;
     euro0003;

     nomvarca;
     nomvarli;
|FIN;

|VARIABLES;
     aNume = "";
     aAlfa = "";
     swLimpia = 0;
     swInfor = 0;
     nImporte = 0;
     nNume = 0;
     nNume1 = 0;
     nNume2 = 0;

     swAlguno = 0;
|FIN;

|PROCESO Formatea;
     |QBF aNume;
     aAlfa = aNume % -301;
     |SI aAlfa = ".";
          |ACABA;
     |FINSI;
     aAlfa = aNume % -201;
     |SI aAlfa = ".";
          aNume = aNume + "0";
          |ACABA;
     |FINSI;
     |SI aNume ! "0";
          aNume = aNume + ",00";
     |FINSI;
|FPRC;

|PROCESO euro0002;
     |SI swLimpia = 1;
          #nomvarli#0 = #euro0002#0;
          #nomvarli#1 = #euro0002#1;
          #nomvarli#2 = #euro0002#3;
          #nomvarli#3 = 0;
          #nomvarli#4 = #euro0002#6;
          |LEE 101#nomvarli.=;
          |SI FSalida = 0;
               |BORRA 020#nomvarli.a;
               |LIBERA #nomvarli;
          |FINSI;
     |SINO;
          swInfor = 0;
          #euro0003#0 = #euro0002#0;
          #euro0003#1 = #euro0002#7;
          |LEE 000#euro0003.=;
          |SI FSalida = 0;
               || es un concepto INFOR
               || en principio pongo unidades e importe, y si llega otro
               || con el mismo codigo acumulo todo el importe en una unidad

               |SI #euro0003#2 ! 0;
                    swInfor = 1;
               |FINSI;
          |FINSI;

          |DDEFECTO #nomvarli;
          #nomvarli#0 = #euro0002#0;
          #nomvarli#1 = #euro0002#1;
          #nomvarli#2 = #euro0002#3;
          #nomvarli#3 = 0;
          #nomvarli#4 = #euro0002#6;
          |LEE 101#nomvarli.=;
          |SI FSalida ! 0;
               || unidades
               aNume = #euro0002#8;
               |QBF aNume;
               aNume = ("          " + aNume) % -110;
               #nomvarli#5 = aNume;

               || importe
               |SI swInfor = 1;
                    aNume = #euro0003#2;
                    |HAZ Formatea;
                    aNume = ("            " + aNume) % -112;
                    #nomvarli#6 = aNume;
               |FINSI;
               |GRABA 020#nomvarli.n;
          |SINO;
               |SI swInfor = 1;
                    || me guardo el importe
                    nNume1 = #nomvarli#5;
                    nNume2 = #nomvarli#6;
                    nImporte = nNume1 * nNume2;

                    || unidades
                    nNume = 1;
                    aNume = nNume;
                    |QBF aNume;
                    aNume = ("          " + aNume) % -110;
                    #nomvarli#5 = aNume;

                    || importe nuevo (sobre 1 unidad)
                    nImporte = nImporte + (#euro0002#8 * #euro0003#2);
                    aNume = nImporte;
                    |HAZ Formatea;
                    aNume = ("            " + aNume) % -112;
                    #nomvarli#6 = aNume;
               |SINO;
                    || unidades
                    nNume = #nomvarli#5;
                    nNume = nNume + #euro0002#8;
                    aNume = nNume;
                    |QBF aNume;
                    aNume = ("          " + aNume) % -110;
                    #nomvarli#5 = aNume;
               |FINSI;

               |GRABA 020#nomvarli.a;
          |FINSI;
          |LIBERA #nomvarli;
          swAlguno = 1;
     |FINSI;
|FPRC;

|DEFBUCLE euro0002;
     #euro0002#1;
     ;
     #euro0001#0, #euro0001#1, #euro0001#2, #euro0001#3, 0, 01;
     #euro0001#0, #euro0001#1, #euro0001#2, #euro0001#3, 0, 99;
     ;
     NULL, euro0002;
|FIN;

|PROCESO euro0001;
     |BUCLE euro0002;

     |SI swLimpia = 0;
          #nomvarca#0 = #euro0001#0;
          #nomvarca#1 = #euro0001#1;
          #nomvarca#2 = #euro0001#3;
          #nomvarca#3 = 0;
          |LEE 101#nomvarca.=;
          |SI FSalida ! 0;
               |GRABA 020#nomvarca.n;
               |LIBERA #nomvarca;
          |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE euro0001;
     #euro0001#1;
     ;
     #0#0, #0#2, #0#4, #0#5, 0;
     #0#1, #0#3, #0#4, #0#5, 0;
     ;
     NULL, euro0001;
|FIN;

|PROCESO Pase;
     swLimpia = 1;
     |BUCLE euro0001;
     swLimpia = 0;
     |BUCLE euro0001;
|FPRC;

|PROGRAMA;
     |CLS;
     |PINPA #0#0;

     |ABRE #0;
     |DDEFECTO #0;
     |LEE 000#0p;
     |SI FSalida = 0;
          |BORRA 020#0a;
     |FINSI;

     |PINDA #0#0;

     |ENDATOS #1#0;
     |SI FSalida = 0;
          |ABRE #nomvarca;
          |ABRE #nomvarli;

          |ABRE #euro0001;
          |ABRE #euro0002;
          |ABRE #euro0003;

          |HAZ Pase;

          |SI swAlguno = 1;
               |MENAV "    Se ha realizado el pase a variaciones.";
          |SINO;
               |MENAV "    No se han encontrado registros importados para pasar a variaciones.";
          |FINSI;

          |CIERRA #euro0003;
          |CIERRA #euro0002;
          |CIERRA #euro0001;

          |CIERRA #nomvarli;
          |CIERRA #nomvarca;
     |FINSI;

     |GRABA 000#0n;
     |CIERRA #0;
|FPRO;
