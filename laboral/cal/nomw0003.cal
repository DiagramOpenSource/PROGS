|FICHEROS;
     nomw0003 #0;
     nomw0004 #1;
     nomconca #2;
     nomauxi2 #3;
     labpadre #4;
     labempre #5;
     nomauxi1 #6;   || Areas
     labcentr #7;   || Centros
     temp0001@;
     nomtmpce #500;
     labtrtra #501;

     labtraba,MANTE;
|FIN;

|VARIABLES;
     aQueQuiero  = "";
     aCampo      = "";
     aAlfa1      = "";
     aAlfa2      = "";

     nTCampo     = 0;
     nCampo      = 0;
     nTrabajador = 0;
     nModo       = 0;
     nModo2      = 0;

     &enEmpresa = 0;
     &enModo    = 0;
     &orden     = "";
     &pasa      = "";

     &em_borr = 0;
     &tr_borr = 0;
     &nif_borr = "";

     aAlfa = "";
     nPos1 = 0;
     nPos2 = 0;
     nNuevo = 0;
     &eaProcedencia = "nomw0003";
     &swHayGAD = 0;
     &nArchi = 0;
     &swModulo = 0;
     &swFacturacion = 0;
     &swIDAL = 0;
|FIN;

________________________/ BORRADO DE RELACIONES /____________________________

|PROCESO Tipo2; |TIPO 2;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo ! 3;  |ACABA;  |FINSI;
     |CONFI  "0000NEsta seguro de dar de baja el Trabajador ";
     |SI FSalida ! 0;
          |ERROR;
     |SINO;
          |HAZ aviso_tr;
          em_borr = #0#0;
          tr_borr = #0#1;
          nif_borr = #0#7;
          |SUB_EJECUTA "nomborr1.run";
          |SUB_EJECUTA "nomborr2.run";
          |HAZ t_aviso9;
     |FINSI;
|FPRC;

|PROCESO aviso_tr;
|PUSHV 05012380;
|HAZ t_color_f;
|CUADRO 09 30 11 71;
||            1234567890123456789012345678901234567890
|HAZ t_color_c;
|PINTA 1031, "         Borrando Relaciones ...        ";
|ATRI 0;
|FPRC;

|PROCESO t_color_c;        || activa el color caracter en avisos
|COLOR 14, 0;
|ATRI 0;
|FPRC;

|PROCESO t_color_f;        || activa el color graficos en avisos
|COLOR 4, 0;
|ATRI 0;
|FPRC;

|PROCESO t_aviso9;         || desde calculo 'descuadre', 'modbaj'
|POPV;
|FPRC;

|RUTINA ClaveBaja1;
     #0#0 = enEmpresa;
     #0#7 = "            ";
|FRUT;

|RUTINA ClaveAlta1;
     #0#0 = enEmpresa;
     #0#7 = "zzzzzzzzzzzz";
|FRUT;

|RUTINA ClaveBaja2;
     #0#0 = enEmpresa;
     #0#2 = " " * 40;
|FRUT;

|RUTINA ClaveAlta2;
     #0#0 = enEmpresa;
     #0#2 = "z" * 40;
|FRUT;

|PROCESO PintaDir;
     #labcentr#0 = enEmpresa;
     #labcentr#1 = #nomtmpce#1;
     |LEE 000#labcentr.=;
     |SI FSalida = 0;
          aAlfa = "                              ";
          |MENSA aAlfa;
          aAlfa = #labcentr#3; |QBF aAlfa;
          aAlfa = aAlfa + ", " + #labcentr#4; |QBF aAlfa;
          aAlfa = aAlfa + " - "+ #labcentr#5; |QBF aAlfa;
          aAlfa = aAlfa + " (" + #labcentr#6; |QBF aAlfa;
          aAlfa = "     " + aAlfa + ")";
          aAlfa = aAlfa % 175;
          |MENSA aAlfa;
     |FINSI;
|FPRC;

|PROCESO labtraba13;
     |PARA nCampo = 0; |SI nCampo [ 12; |HACIENDO nCampo = nCampo + 1;
          #501nCampo = #labtraba#nCampo#;
     |SIGUE;
     #501#17 = #labtraba#17;
     #501#18 = #labtraba#18;
     #501#19 = #labtraba#19;
     |PARA nCampo = 33; |SI nCampo [ 45; |HACIENDO nCampo = nCampo + 1;
          #501nCampo = #labtraba#nCampo#;
     |SIGUE;
     |GRABA 020#501n;
     |LIBERA #501;
|FPRC;

|DEFBUCLE labtraba13;
     #labtraba#1;
     ;
     enEmpresa, 00001;
     enEmpresa, 99999;
     ;
     NULL, labtraba13;
|FIN;

|PROCESO tmp_labtraba;
     aAlfa = Usuario; |QBF aAlfa;    aAlfa = "T" + aAlfa;
     |NOME_DAT #501 aAlfa;
     |DELINDEX #501;
     |ABRE #501;
     |BUCLE labtraba13;
     |CIERRA #501;
|FPRC;

|PROCESO Consulta;  |TIPO 11;
     nNuevo = 0;

     |SI swModulo = 000009;
          || el sistema nuevo, solo para Millenium

          |SI FSalida = 10;
               nTrabajador = #0#1;
               |ABRE #501;
               |CONSULTA_CLAVE #6#501;
               |SI FSalida = 0;
                    nTrabajador = #501#1;
                    |PTEC 806;
               |FINSI;
               |PTEC 806;
               |CIERRA #501;
               |ACABA;
          |FINSI;

          |SI FSalida = 11;
               |ABRE #501;
               |CONSULTA_CLAVE #5#501;
               nTrabajador = #0#1;
               |SI FSalida = 0;
                    nTrabajador = #501#1;
                    |PTEC 806;
               |FINSI;
               |PTEC 806;
               |CIERRA #501;
               |ACABA;
          |FINSI;
     |SINO;
          || el sistema de siempre

          |SI FSalida = 10;
              |CONSULTA_F_CLAVE #6#0, ClaveBaja2, ClaveAlta2;
              nTrabajador = #0#1;
              |PTEC 806;
              |ACABA;
          |FINSI;

          |SI FSalida = 11;
              |CONSULTA_F_CLAVE #5#0, ClaveBaja1, ClaveAlta1;
              nTrabajador = #0#1;
              |ACABA;
          |FINSI;
     |FINSI;

     |SI FSalida = 13;
          aAlfa1 = #0#0; aAlfa1 = ("00000" + aAlfa1) % -105;
          aAlfa2 = #0#1; aAlfa2 = ("00000" + aAlfa2) % -105;
          orden  = aAlfa1 + aAlfa2;
          pasa   = "T";
          |SUB_EJECUTA "nombmp01";
          |ERROR;
     |FINSI;

     |SI FSalida = 12;
          nModo2 = (FEntrada / 100) ! 0;
          |SI nModo2 = 2;
               |SALVA_FICHA #labtraba;
          |FINSI;

          aAlfa = "SELECT labtraba.0,labtraba.77,labtraba.1,labtraba.2 ";
          aAlfa = aAlfa + "FROM labtraba INTO tm" + Usuario;
          aAlfa = aAlfa + " WHERE 0 == " + #labtraba#0;
          |SQL aAlfa;
          |SI FSalida ! 0;
               |MENAV "    No hay registros"; |ERROR; |ACABA;
          |FINSI;
          aAlfa = "tm" + Usuario; |NOME_DAT #500 aAlfa;
          |ABRE #labcentr;
          |ABRE #nomtmpce;
          |SELECT #2#nomtmpce;
          |LEE 000#nomtmpce.p;
          |SELECT #1#nomtmpce;
          nModo = 2 + 4 + 8 + 16;
          nPos1 = 1515; nPos2 = 2380;
          |LINEAL_SIMPLE #2#nomtmpce,nModo, nPos1, nPos2, NULL, NULL, PintaDir;
          |SI FSalida = 0; |Y nModo2 = 1;
               #labtraba#1 = #nomtmpce#2 + 1;
               |PINTA #labtraba#1;
               nNuevo = #labtraba#1;
          |FINSI;

          |CIERRA #nomtmpce;
          |DELINDEX #nomtmpce;
          |CIERRA #labcentr;
          |SI nModo2 = 2;
               |REPON_FICHA #labtraba;
          |FINSI;
          |ACABA;
     |FINSI;
|FPRC;

|PROCESO Modo;  |TIPO 0;
     nModo = (FEntrada / 100) ! 0;
|FPRC;

|PROCESO Tipo7;  |TIPO 7;
     #0#0 = enEmpresa;

     |ABRE #2;
     #2#0 = #0#87;
     |LEE 040#2=;
     |SI FSalida ! 0;  |DDEFECTO #2;  |FINSI;
     |CIERRA #2;

     |ABRE #3;
     #3#0 = #0#62;
     #3#1 = #0#63;
     |LEE 040#3=;
     |SI FSalida ! 0;  |DDEFECTO #3;  |FINSI;
     |CIERRA #3;

     |ABRE #nomauxi1;
     #nomauxi1#0 = #0#62;
     |LEE 040#nomauxi1.=;
     |SI FSalida ! 0;  |DDEFECTO #nomauxi1;  |FINSI;
     |CIERRA #nomauxi1;

     |ABRE #labcentr;
     #labcentr#0 = #0#0;
     #labcentr#1 = #0#77;
     |LEE 040#labcentr.=;
     |SI FSalida ! 0;  |DDEFECTO #labcentr;  |FINSI;
     |CIERRA #labcentr;

     |ATRI I;
     |PINTA 0931, #2#1;       || Convenio
     |PINTA 1332, #3#2;       || Seccion
     |PINTA 1232, #nomauxi1#1;     || Area
     |PINTA 0831, #labcentr#2;     || Centro
     |ATRI i;

     |SI nNuevo ! 0;
          #labtraba#1 = nNuevo;
     |FINSI;

     |SI #labtraba#1 = 00000;
          #labtraba#1 = 00001;
     |FINSI;

     ||HAZ Activa142;
|FPRC;

|PROCESO Tipo3;  |TIPO 3;
     aQueQuiero = "|NC";
     aCampo     = #0#aQueQuiero#;
     nTCampo   = aCampo;
     nTCampo   = nTCampo - 1;

     |ABRE #5;
     #5#0 = #0#0;
     |LEE 040#5=;
     |SI FSalida ! 0;  |DDEFECTO #5;  |FINSI;
     |CIERRA #5;

     |SI nModo ! 1;
         |PARA nCampo = 0;  |SI nCampo [ nTCampo;  |HACIENDO nCampo = nCampo + 1;
               #1nCampo = #0nCampo;
         |SIGUE;
     |SINO;
         |DDEFECTO #1;
         #1#0   = #0#0;
         #1#1   = #0#1;
         #1#87  = #5#1;
         #1#156 = #5#34;
     |FINSI;

     Control = "nomw0004";

     |PINPA #0#1;
     |PINDA #0#1;
     |HAZ Activa142;
     |HAZ Tipo7;
     |SI nModo = 1;
         |ENDATOS #1#1;
     |SINO;
         |ENDATOS #2#1;
     |FINSI;
     |SI FSalida = 0;
         Control = "nomw0003";

         |PARA nCampo = 0;  |SI nCampo [ nTCampo;  |HACIENDO nCampo = nCampo + 1;
               #nomw0003#nCampo# = #nomw0004#nCampo#;
               #labtraba#nCampo# = #nomw0004#nCampo#;
         |SIGUE;

         #0#0 = enEmpresa;
     |SINO;
          |ERROR;
     |FINSI;

     |PULSATECLA;
|FPRC;

|RUTINA ClaveBaja0;
     |SI FSalida = "POSICION";
         #0#1 = nTrabajador;
         nTrabajador = 0;
         |ACABA;
     |FINSI;

     #0#0 = enEmpresa;
     #0#1 = 00001;
|FRUT;

|RUTINA ClaveAlta0;
     #0#0 = enEmpresa;
     #0#1 = 99999;
     |PONCONTROL 23, "si";
     |PTEC 808;
|FRUT;

|PROGRAMA;
     nTrabajador = 0;
     |HAZ MiraSiArchiva;
     swHayGAD = 0;
     |SI nArchi = 1;
          swHayGAD = 1;
     |FINSI;
     |HAZ BotonRED;

     |HAZ BuscaModulo;

     |SI swModulo = 000009;
          || el sistema nuevo, solo para Millenium

          |HAZ tmp_labtraba;
     |FINSI;

     |PARA; |SI; |HACIENDO;
          |ENTLINEAL #1#0, -2, enModo, 22, 1, ClaveBaja0, ClaveAlta0;
          |SI nTrabajador = 0;  |SAL;  |FINSI;
     |SIGUE;

     |HAZ FinBotonRED;

     |DELINDEX #501;
|FPRO;
