|FICHEROS;
     cretap25 #0;
     cretam38;
     cretam02;
     cretae02;

     labempre;
     labcentr;
     nomcenes;
     labtraba;
|FIN;

|VARIABLES;
     &eaFichero = "";
     &aSoloFichero = "";

     nNume = 0;
     aLon = "";
     nLon = 0;
     nEspacio = 0;
     aEspacio = "";
     nBlancos = 0;
     nBlancosIzd = 0;
     nBlancosDer = 0;
     aBlancosIzd = "";
     aBlancosDer = "";

     aCadena = "";
     aCadenaSal = "";

     FLinea = 0;
     nNroLinea = 0;
     aNroLinea = "";
     aAlfa = "";

     nHandle = 0;
     aLinea = "";
     &aTotalLineas = "";
     swAcaba = 0;

     aEmpresa = "";
     aCCC = "";
     nAnyDesde = 0;
     nMesDesde = 0;
     aAnyDesde = "";
     aMesDesde = "";
     nTipo = 0;
     aDia = "";
     aMes = "";
     aAny = "";
     aEtiqueta = "";
     nLoc = 0;
     aValor = "";
     aCampoFecha = "";
     aDiaRec = "";
     aMesRec = "";
     aAnyRec = "";
     aHoraRec = "";
     aEtiquetaIni = "";
     aEtiquetaFin = "";
     aMesHasta = "";
     aAnyHasta = "";
     aTipo = "";

     nEmp = 0;
     nTra = 0;
     swEncontrada = 0;
     aAlfa1 = "";
     aAlfa2 = "";
     &enMes = 0;
     &enAny = 0;
     &enSwAlta = 0;

     aNaf = "";
     aEstado = "";
     fFechaRec = @;

     aTipoIPF = "";
     aNumeroIPF = "";
     aSiglas = "";

     aDiaDesdeTramo = "";
     aMesDesdeTramo = "";
     aAnyDesdeTramo = "";
     aFechaDesdeTramo = "";
     aDiaHastaTramo = "";
     aMesHastaTramo = "";
     aAnyHastaTramo = "";
     aFechaHastaTramo = "";

     aDias = "";
     aHoras = "";
     aDescripcion = "";
     nConcepto = 0;
     nImporte = 0;
     aCentro = "";

     nCampo = 0;
     nCampo1 = 0;
     nCampo2 = 0;
     nCampo3 = 0;
     aTipoSuma = "";
     nBase = 0;
     aTipoDCL = "";
     aCodErrCCC1 = ""; aCodErrCCC2 = ""; aCodErrCCC3 = ""; aCodErrCCC4 = ""; aCodErrCCC5 = "";
     aDesErrCCC1 = ""; aDesErrCCC2 = ""; aDesErrCCC3 = ""; aDesErrCCC4 = ""; aDesErrCCC5 = "";
     aTipoRespuesta = "";
     eaReferencia = "";
     nErrores = 0;
     swCCCCorrecto = 0;
     nTipoFichero = 0;
     FEstado = 0;

     nMesHasta = 0;
     nAnyHasta = 0;
     nMesControl = 0;
     nAnyControl = 0;
     nMesClave = 0;
     nAnyClave = 0;
     aMesClave = "";
     aAnyClave = "";
     nMesDatos = 0;
     swNoActiva = 0;
     swAtrasos = 0;
     swSigue = 0;
|FIN;

|PROCESO Centra;
     aLon = aCadena % 0;
     nLon = aLon;
     nBlancos = nEspacio - nLon;
     nNume = nBlancos / 2;
     nBlancosIzd = nNume ! 0;
     nBlancosDer = nBlancos - nBlancosIzd;
     aBlancosIzd = " " * nBlancosIzd;
     aBlancosDer = " " * nBlancosDer;
     aCadenaSal = aBlancosIzd + aCadena + aBlancosDer;

|FPRC;

/****************************************************************************/
/****************************** REGISTROS ***********************************/
/****************************************************************************/

|PROCESO RegistroCCC;
     |DDEFECTO #cretam38;

     |ABRE #cretam38;
     #cretam38#0 = nEmp;
     #cretam38#1 = nAnyClave;
     #cretam38#2 = nMesClave;
     #cretam38#3 = nTipo;
     #cretam38#4 = aCCC;
     #cretam38#12 = aTipoRespuesta;
     #cretam38#13 = eaReferencia;

     |LEE 101#cretam38.=;
     FEstado = FSalida;

     #cretam38#6 = aEmpresa;
     #cretam38#14 = aSoloFichero;

     |SI nErrores = 0; |Y swCCCCorrecto = 1;
          #cretam38#9 = "A";
     |SINO;
          #cretam38#9 = "R";
     |FINSI;

     #cretam38#15 = aCodErrCCC1;
     #cretam38#16 = aDesErrCCC1;
     #cretam38#17 = aCodErrCCC2;
     #cretam38#18 = aDesErrCCC2;
     #cretam38#19 = aCodErrCCC3;
     #cretam38#20 = aDesErrCCC3;
     #cretam38#21 = aCodErrCCC4;
     #cretam38#22 = aDesErrCCC4;
     #cretam38#23 = aCodErrCCC5;
     #cretam38#24 = aDesErrCCC5;

     |SI FEstado = 0;
          |GRABA 020#cretam38.a;
     |SINO;
          |GRABA 020#cretam38.n;
     |FINSI;

     |LIBERA #cretam38;
     |CIERRA #cretam38;

     |SI aTipoRespuesta = "F";
          |DDEFECTO #cretam02;

          |ABRE #cretam02;

          |PARA nMesDatos = nMesDesde;
          |SI nMesDatos [ nMesHasta;
          |HACIENDO nMesDatos = nMesDatos + 1;
               #cretam02#0 = nEmp;
               #cretam02#1 = nAnyClave;
               #cretam02#2 = nMesDatos;
               #cretam02#3 = nTipo;
               #cretam02#4 = aCCC;
               swSigue = 0;
               |LEE 101#cretam02.=;
               |SI FSalida = 0;
                    swSigue = 1;
               |SINO;
                    |SI nTipo ] 5; |Y nTipo [ 20;
                         #cretam02#1 = nAnyDesde;
                         |LEE 101#cretam02.=;
                         |SI FSalida = 0;
                              swSigue = 1;
                         |FINSI;
                    |FINSI;
               |FINSI;
               |SI swSigue = 1;
                    |SI aTipoRespuesta = "F";
                         |SI nErrores = 0; |Y swCCCCorrecto = 1;
                              |SI eaReferencia = "C.OFICIO";
                              |O eaReferencia = "On-Line";
                                   #cretam02#7 = "O";
                              |SINO;
                                   #cretam02#7 = "F";
                              |FINSI;
                         |FINSI;
                    |FINSI;
                    |GRABA 020#cretam02.a;
               |FINSI;
               |LIBERA #cretam02;
          |SIGUE;
          |CIERRA #cretam02;
     |FINSI;

     |SI aTipoRespuesta = "F"; |O aTipoRespuesta = "C";
          || solo confirmacion o calculos
          || el resto de respuestas se graban en sus procesos
          |DDEFECTO #cretae02;
          |ABRE #cretae02;
          #cretae02#0 = nEmp;
          #cretae02#1 = nAnyClave;
          #cretae02#2 = nMesClave;
          #cretae02#3 = nTipo;
          #cretae02#4 = aCCC;
          #cretae02#6 = eaReferencia;   || las de oficio no estaran nunca
          |LEE 101#cretae02.=;
          |SI FSalida = 0;
               |SI nErrores = 0; |Y swCCCCorrecto = 1;
                    #cretae02#8 = "C";
               |SINO;
                    #cretae02#8 = "E";
               |FINSI;
               #cretae02#14 = aSoloFichero;
               |GRABA 020#cretae02.a;
          |FINSI;
          |LIBERA #cretae02;
          |CIERRA #cretae02;
     |FINSI;
|FPRC;

|PROCESO cretae02_atrasos;
     |SI nMesDesde = #cretae02#17;
     |Y nMesHasta = #cretae02#18;
     |Y nAnyDesde = #cretae02#19;
          || este es el tpo de atrasos que buscaba
          nTipo = #cretae02#3;
     |FINSI;
|FPRC;

|DEFBUCLE cretae02_atrasos;
     #cretae02#1;
     ;
     00001, nAnyControl, nMesControl, 05, aCCC, "        ";
     99999, nAnyControl, nMesControl, 20, aCCC, "zzzzzzzz";
     ;
     NULL, cretae02_atrasos;
|FIN;

|PROCESO BuscaTipoAtrasos;
     nTipo = 0;
     |BUCLE cretae02_atrasos;
|FPRC;

/****************************************************************************/
/***************************   BORRADO PREVIO *******************************/
/****************************************************************************/

|PROCESO Borra_cretam38;
     |BORRA 020#cretam38.a;
     |LIBERA #cretam38;
|FPRC;

|DEFBUCLE Borra_cretam38;
     #cretam38#1;
     ;
     00001, nAnyDesde, nMesClave, nTipo, aCCC, aTipoRespuesta, eaReferencia;
     99999, nAnyDesde, nMesClave, nTipo, aCCC, aTipoRespuesta, eaReferencia;
     ;
     NULL, Borra_cretam38;
|FIN;

/****************************************************************************/
/*********** PROCESOS PARA BUSCAR CODIGOS DE EMP, TRA Y CEN *****************/
/****************************************************************************/

|PROCESO BuscaCCC;
     nEmp = #cretam02#0;
     swEncontrada = 1;
|FPRC;

|DEFBUCLE BuscaCCC_At;
     #cretam02#1;
     19;
     00001, 2000, nMesDesde, nTipo, aCCC, nAnyDesde;
     99999, 2999, nMesHasta, nTipo, aCCC, nAnyDesde;
     ;
     NULL, BuscaCCC;
|FIN;

|DEFBUCLE BuscaCCC;
     #cretam02#1;
     ;
     00001, nAnyDesde, nMesDesde, nTipo, aCCC;
     99999, nAnyDesde, nMesDesde, nTipo, aCCC;
     ;
     NULL, BuscaCCC;
|FIN;

|PROCESO BuscaPorCCC;
     aEmpresa = "";
     swEncontrada = 0;
     swNoActiva = 0;
     |SI swAtrasos = 0;
          |BUCLE BuscaCCC;
     |SINO;
          |BUCLE BuscaCCC_At;
     |FINSI;
     |SI swEncontrada = 1;
          #labempre#0 = nEmp;
          |LEE 000#labempre.=;
          |SI FSalida = 0; |Y #labempre#67 = "S";
               aEmpresa = #labempre#2;
          |SINO;
               swNoActiva = 1;
          |FINSI;
     |FINSI;
|FPRC;

/****************************************************************************/
/********************   TIPOS DE DATOS COMUNES  *****************************/
/****************************************************************************/

|PROCESO TipoFecha;
     aDia = "";
     aMes = "";
     aAny = "";

     |PARA; |SI; |HACIENDO;
          |HAZ LeeLinea;

          aEtiqueta = "Dia";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               aDia = aValor;
          |FINSI;

          aEtiqueta = "Mes";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               aMes = aValor;
          |FINSI;

          aEtiqueta = "Anho";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               aAny = aValor;
          |FINSI;

          aEtiqueta = aCampoFecha;
          |HAZ BuscaEtiFin;
          |SI nLoc = 1;
               |SAL;
          |FINSI;
     |SIGUE;
|FPRC;

|PROCESO FechaHoraRec;
     |PARA; |SI; |HACIENDO;
          |HAZ LeeLinea;

          aEtiqueta = "FechaRecaudacion";
          |HAZ BuscaEtiIni;
          |SI nLoc = 1;
               aCampoFecha = "FechaRecaudacion";
               |HAZ TipoFecha;
               aDiaRec = aDia;
               aMesRec = aMes;
               aAnyRec = aAny;
               aAlfa = aDiaRec + "." + aMesRec + "." + aAnyRec;
               fFechaRec = aAlfa;
          |FINSI;

          aEtiqueta = "HoraRecaudacion";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               aHoraRec = (aValor % 102) + ":" + (aValor % 302) + ":" + (aValor % 502);
          |FINSI;

          aEtiqueta = "FechaHoraRecaudacion";
          |HAZ BuscaEtiFin;
          |SI nLoc = 1;
               |SAL;
          |FINSI;
     |SIGUE;
|FPRC;

|PROCESO CCC;
     |PARA; |SI; |HACIENDO;
          |HAZ LeeLinea;

          aEtiqueta = "Regimen";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               aCCC = aValor;
          |FINSI;

          aEtiqueta = "Provincia";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               aCCC = aCCC + aValor;
          |FINSI;

          aEtiqueta = "Numero";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               aCCC = aCCC + aValor;
          |FINSI;

          aEtiqueta = "Ccc";
          |HAZ BuscaEtiFin;
          |SI nLoc = 1;
               |SAL;
          |FINSI;
     |SIGUE;
|FPRC;

/****************************************************************************/
/***********     PROCESOS PARA BUSCAR LAS ETIQUETAS       *******************/
/****************************************************************************/

|PROCESO BuscaEtiFin;
     nLoc = 0;
     aEtiqueta = "</" + aEtiqueta + ">";
     aAlfa = aLinea - aEtiqueta;
     |SI aAlfa ! aLinea;
          nLoc = 1;
     |FINSI;
|FPRC;

|PROCESO BuscaEtiIni;
     nLoc = 0;
     aEtiqueta = "<" + aEtiqueta;
     aAlfa = aLinea - aEtiqueta;
     |SI aAlfa ! aLinea;
          nLoc = 1;
     |FINSI;
|FPRC;

|PROCESO BuscaEtiIniFin;
     aValor = "";
     nLoc = 0;

     aEtiquetaIni = "<" + aEtiqueta + ">";
     aEtiquetaFin = "</" + aEtiqueta + ">";
     aAlfa = aLinea - aEtiquetaIni - aEtiquetaFin;
     |SI aAlfa ! aLinea;
          aValor = aAlfa;
          |SI aEtiqueta = "Descripcion";
               |QBF aValor;
          |SINO;
               |QBT aValor;
          |FINSI;
          nLoc = 1;
     |FINSI;
|FPRC;

|PROCESO LeeLinea;
     |XLEE nHandle, 250, aLinea;
     FLinea = FSalida;
     nNroLinea = nNroLinea + 1;
     aNroLinea = nNroLinea;
     aAlfa = " Linea " + aNroLinea + " de " + aTotalLineas;

     |ATRI R;
     aCadena = aAlfa; nEspacio = 35; |HAZ Centra;
     |PINTA 2223, aCadenaSal;
     |ATRI 0;
|FPRC;

|PROCESO TipoErrorCCC;
     |PARA; |SI; |HACIENDO;
          |HAZ LeeLinea;

          aEtiqueta = "CodigoErr";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;

               |SI aCodErrCCC1 = "";
                    aCodErrCCC1 = aValor;
               |SINO;
                    |SI aCodErrCCC2 = "";
                         aCodErrCCC2 = aValor;
                    |SINO;
                         |SI aCodErrCCC3 = "";
                              aCodErrCCC3 = aValor;
                         |SINO;
                              |SI aCodErrCCC4 = "";
                                   aCodErrCCC4 = aValor;
                              |SINO;
                                   |SI aCodErrCCC5 = "";
                                        aCodErrCCC5 = aValor;
                                   |FINSI;
                              |FINSI;
                         |FINSI;
                    |FINSI;
               |FINSI;
          |FINSI;

          aEtiqueta = "Descripcion";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               |SI aDesErrCCC1 = "";
                    aDesErrCCC1 = aValor;
               |SINO;
                    |SI aDesErrCCC2 = "";
                         aDesErrCCC2 = aValor;
                    |SINO;
                         |SI aDesErrCCC3 = "";
                              aDesErrCCC3 = aValor;
                         |SINO;
                              |SI aDesErrCCC4 = "";
                                   aDesErrCCC4 = aValor;
                              |SINO;
                                   |SI aDesErrCCC5 = "";
                                        aDesErrCCC5 = aValor;
                                   |FINSI;
                              |FINSI;
                         |FINSI;
                    |FINSI;
               |FINSI;
          |FINSI;

          aEtiqueta = "Errores";
          |HAZ BuscaEtiFin;
          |SI nLoc = 1;
               |SAL;
          |FINSI;
     |SIGUE;
|FPRC;

|PROCESO Liquidacion;
     aCodErrCCC1 = ""; aCodErrCCC2 = ""; aCodErrCCC3 = ""; aCodErrCCC4 = ""; aCodErrCCC5 = "";
     aDesErrCCC1 = ""; aDesErrCCC2 = ""; aDesErrCCC3 = ""; aDesErrCCC4 = ""; aDesErrCCC5 = "";

     |PARA; |SI; |HACIENDO;
          |HAZ LeeLinea;

          aEtiqueta = "Ccc";
          |HAZ BuscaEtiIni;
          |SI nLoc = 1;
               |HAZ CCC;
          |FINSI;

          aEtiqueta = "PeriodoDesde";
          |HAZ BuscaEtiIni;
          |SI nLoc = 1;
               aCampoFecha = "PeriodoDesde";
               |HAZ TipoFecha;
               aMesDesde = aMes;
               aAnyDesde = aAny;
               nMesDesde = aMesDesde;
               nAnyDesde = aAnyDesde;
          |FINSI;

          aEtiqueta = "PeriodoHasta";
          |HAZ BuscaEtiIni;
          |SI nLoc = 1;
               aCampoFecha = "PeriodoHasta";
               |HAZ TipoFecha;
               aMesHasta = aMes;
               nMesHasta = aMes;
               aAnyHasta = aAny;
               nAnyHasta = aAny;
          |FINSI;

          aEtiqueta = "Tipo";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               swAtrasos = 0;
               aTipo = aValor;
               |SI aAlfa = "L00"; nTipo = 00; |FINSI;
               |SI aAlfa = "L13"; nTipo = 02; |FINSI;
               |SI aAlfa = "L90"; nTipo = 90; |FINSI;
               |SI aAlfa = "L91"; nTipo = 91; |FINSI;

               |SI aAlfa = "L00"; |O aAlfa = "L13"; |O aAlfa = "L90";
               |O aAlfa = "L91";
                    nMesClave = nMesDesde;
                    nAnyClave = nAnyDesde;

                    aMesClave = aMesDesde;
                    aAnyClave = aAnyDesde;
               |FINSI;
               |SI aTipo = "L03";
                    swAtrasos = 1;
                    nTipo = 999;
                    || son atrasos, hay que buscar el nTipo
                    || pero esto lo hare despues en la fecha de control
               |FINSI;

               || con el tipo a 999, en atrasos no borrara nada, lo
               || hara despues

               |BUCLE Borra_cretam38;
               |SELECT #1#cretam38;
          |FINSI;

          aEtiqueta = "FechaControl";
          |HAZ BuscaEtiIni;
          |SI nLoc = 1;
               aCampoFecha = "FechaControl";
               |HAZ TipoFecha;

               nMesControl = aMes;
               nAnyControl = aAny;

               aMesClave = aMes;
               aAnyClave = aAny;

               nMesClave = nMesControl;
               nAnyClave = nAnyControl;

               || si encuentro este campo es que estoy en atrasos
               || y ya tengo leido que es tipo L03, ahora ire a buscar que
               || tipo de atrasos eran

               |HAZ BuscaTipoAtrasos;

               |SI nTipo = 00;
                    aAlfa = "    ATENCION. No se ha encontrado registro de Atrasos ";
                    aAlfa = aAlfa + " del mes " + aMesDesde + " al " + aMesHasta;
                    aAlfa = aAlfa + " de " + aAnyDesde + " en CCC: " + aCCC;
                    |MENAV aAlfa;
               |FINSI;

               |BUCLE Borra_cretam38;
               |SELECT #1#cretam38;
          |FINSI;

          aEtiqueta = "FechaHoraRecaudacion";
          |HAZ BuscaEtiIni;
          |SI nLoc = 1;
               |HAZ FechaHoraRec;
          |FINSI;

          aEtiqueta = "Errores";
          |HAZ BuscaEtiIni;
          |SI nLoc = 1;
               |HAZ TipoErrorCCC;

               |SI aCodErrCCC1 = "R9998";    || solicitud aceptada
               |O aCodErrCCC2 = "R9998"; |O aCodErrCCC3 = "R9998";
               |O aCodErrCCC4 = "R9998"; |O aCodErrCCC5 = "R9998";
                    swCCCCorrecto = 1;
               |FINSI;

               |SI aCodErrCCC1 = "R9742";    || confirmado de oficio
               |O aCodErrCCC2 = "R9742"; |O aCodErrCCC3 = "R9742";
               |O aCodErrCCC4 = "R9742"; |O aCodErrCCC5 = "R9742";
                    swCCCCorrecto = 1;
               |FINSI;

               |SI aCodErrCCC1 = "R9529";    || La liquidacion esta calculada
               |O aCodErrCCC2 = "R9529"; |O aCodErrCCC3 = "R9529";
               |O aCodErrCCC4 = "R9529"; |O aCodErrCCC5 = "R9529";
                    swCCCCorrecto = 1;
               |FINSI;

               |SI aCodErrCCC1 = "R9566"; |O aCodErrCCC2 = "R9566";
               |O aCodErrCCC3 = "R9566"; |O aCodErrCCC4 = "R9566";
               |O aCodErrCCC5 = "R9566";
                    swCCCCorrecto = 1;
               |FINSI;
          |FINSI;

          aEtiqueta = "Liquidacion";         || Fin de la liquidacion
          |HAZ BuscaEtiFin;
          |SI nLoc = 1;
               |SAL;
          |FINSI;
     |SIGUE;
|FPRC;

|PROCESO Lectura;
     FLinea = 0;

     aAlfa = eaFichero;
     |XABRE "", aAlfa, nHandle; nHandle = FSalida;
     |SI FSalida ] 0;
          |PARA;
          |SI FLinea ] 0; |Y swAcaba = 0;
          |HACIENDO;
               FLinea = 0;
               |HAZ LeeLinea;

               aEtiqueta = "ReferenciaExterna";
               |HAZ BuscaEtiIniFin;
               |SI nLoc = 1;
                    eaReferencia = aValor;

                    || B - Respuesta a un fichero de Bases
                    || T - Respuesta a una solicitud de Trabajadores y Tramos
                    ||     (solo la respuesta, el fichero es otro XML)
                    || R - Respuesta a una solicitud de Borrador
                    || C - Respuesta a una solicitud de calculos
                    || F - Respuesta a una solicitud de confirmacion
                    ||     o confirmacion de Oficio

                    aTipoRespuesta = eaReferencia % 101;

                    |SI eaReferencia = "C.OFICIO";
                    |O eaReferencia = "On-Line";
                         aTipoRespuesta = "F";
                    |FINSI;
               |FINSI;

               aEtiqueta = "Liquidacion";
               |HAZ BuscaEtiIni;
               |SI nLoc = 1;
                    || Comenzamos una nueva liquidacion
                    nErrores = 0;
                    swCCCCorrecto = 0;
                    nEmp = 0;
                    |HAZ Liquidacion;

                    || se acaba la liquidacion, guardamos registro

                    swNoActiva = 0;
                    |SI nEmp = 0;
                         |HAZ BuscaPorCCC;
                    |FINSI;
                    /*
                    || si hago esto y hay alguna no activa, se sale y no con-
                    || tinua leyendo el fichero
                    |SI swNoActiva = 1;
                         |ACABA;
                    |FINSI;
                    */

                    |HAZ RegistroCCC;
               |FINSI;
          |SIGUE;
     |FINSI;

     |XCIERRA nHandle;
|FPRC;

|PROGRAMA;
     |ABRE #cretam38;

     |ABRE #labempre;
     |ABRE #labcentr;
     |ABRE #nomcenes;
     |ABRE #labtraba;

     |HAZ Lectura;

     |CIERRA #cretam38;

     |CIERRA #labempre;
     |CIERRA #labcentr;
     |CIERRA #nomcenes;
     |CIERRA #labtraba;
|FPRO;
