|FICHEROS;
    aomentr2 #0;         || entrada horas II
    aomentrv #1,MANTE;   || ventana entrada horas

    labempre #10;        || empresas
    labtraba #11;        || trabajadores
    aomhoras #12;        || tipos de hora
    aomparam #13;
    aovi0001;

     gomantho@;
|FIN;

|VARIABLES;
     aAlfa = "";
     aDef = "";
     aEmp = "";
     aFic = "";
     aPath = "";
     aSerExp = "";
     nNumExp = 0;
     nSw = 0;
     aEstado = "";
     nError = 0;

     &enForma = 0;
     &efFecha = @;
     &enUni = 0;
     &enHora = 0;
     &enObra = 0;
     &enSubobra = 0;
     &enTraba = 0;
     &eaSerPre = "";
     &enNumPre = 0;
     &eaNivel = "";
     &eaCapitulo = "";
     &eaPartida = "";
     &eaDesParti = "";
     &eaPrecioHora = 0;

    alfa1 = "";
    alfa2 = "";
    alfa3 = "";
    alfa4 = "";
    nume1 = 0;
    nume2 = 0;
    nume3 = 0;
    nume4 = 0;
    para1 = 0;
    para2 = 0;
    para3 = 0;
    para4 = 0;
    fech1 = @;
    fech2 = @;
    fech3 = @;
    fech4 = @;
    elsw1 = 0;
    elsw2 = 0;
    elsw3 = 0;
    elsw4 = 0;
    FEstado = 0;
    seleccio = 0;
    modo = 0;
    sw_rup = 0;

    fecha = @;
    mes = 0;
    any = 0;
    bisiesto = 0;
    topemes = 0;
    sw_enalta = 0;
    uno_nuevo = 0;
|FIN;
____________________________________/
|PROCESO defecto;
    fech1 = ~;
    alfa1 = fech1;
    alfa2 = alfa1 % 402;     mes = alfa2;
    alfa2 = alfa1 % -104;    any = alfa2;

    nume1 = any $ 4;
|SI nume1 = 0;
        bisiesto = 1;
|SINO;
        bisiesto = 0;
|FINSI;

|SI mes =  1;  topemes = 31;             |FINSI;
|SI mes =  2;  topemes = 28 + bisiesto;  |FINSI;
|SI mes =  3;  topemes = 31;             |FINSI;
|SI mes =  4;  topemes = 30;             |FINSI;
|SI mes =  5;  topemes = 31;             |FINSI;
|SI mes =  6;  topemes = 30;             |FINSI;
|SI mes =  7;  topemes = 31;             |FINSI;
|SI mes =  8;  topemes = 31;             |FINSI;
|SI mes =  9;  topemes = 30;             |FINSI;
|SI mes = 10;  topemes = 31;             |FINSI;
|SI mes = 11;  topemes = 30;             |FINSI;
|SI mes = 12;  topemes = 31;             |FINSI;
|FPRC;

|PROCESO fechas;
|HAZ defecto;

    alfa1 = topemes; alfa1 = "00" + alfa1;   alfa1 = alfa1 % -102;
    alfa2 = mes;     alfa2 = "00" + alfa2;   alfa2 = alfa2 % -102;
    alfa3 = any;

    alfa4 = "01."       + alfa2 + "." + alfa3;  fech1 = alfa4; || desde fecha
    alfa4 = alfa1 + "." + alfa2 + "." + alfa3;  fech2 = alfa4; || hasta fecha

    #0#2 = fech1;  |PINTA #0#2;
    #0#3 = fech2;  |PINTA #0#3;
|FPRC;

|PROCESO min1;
    #1#1 = #0#0;              || obra
    #1#7 = #0#1;              || subobra
    #1#0 = #0#2;              || fecha
    #1#2 = 1;                 || empresa
    #1#3 = 1;                 || trabajador
    #1#4 = 0;                 || tipos de hora
|FPRC;

|PROCESO max1;
    #1#1 = #0#0;              || obra
    #1#7 = #0#1;              || subobra
    #1#0 = #0#3;              || fecha
    #1#2 = 99999;             || empresa
    #1#3 = 99999;             || trabajador
    #1#4 = 99;                || tipos de hora
|FPRC;

|PROGRAMA;
|CLS;
|CABEZA "ENTRADA HORAS II";
|DDEFECTO #0;
|PINPA #0#0;
|PINDA #0#0;
|HAZ fechas;
|ENDATOS #1#0;
|PARA; |SI FSalida = 0; |HACIENDO;
        fecha = "00.00.0000";
        sw_enalta = 0;
    |SI #0#4 = 1;   |ENTLINEAL #3#1, 3, 1, 22, 0, min1, max1;    |FINSI;
    |SI #0#4 = 2;   |ENTLINEAL #5#1, 4, 1, 22, 0, min1, max1;    |FINSI;
    |ENDATOS #1#0;
|SIGUE;
|FPRO;
____________________________________/
|PROCESO fecha7; |TIPO 7;
    modo = (FEntrada / 100) ! 0;
|SI sw_enalta = 1;
    |PTEC 802;
    |PTEC 802;
    |PTEC 802;
    |PTEC 802;
|SINO;
    |SI uno_nuevo = 1;|Y #0#4 = 1;
            uno_nuevo = 0;
        |SI modo ! 1;
            |PTEC 809;
            |PTEC 806;
            |PTEC 50;
            |PTEC 49;
            |PTEC 49;
            |PTEC 51;
        |FINSI;
    |SINO;
        |SI modo = 1;
            |SI fecha ! "00.00.0000";
                    #1#0 = fecha;
            |SINO;
                    #1#0 = #0#3;
            |FINSI;
            |PINTA #1#0;
        |FINSI;
    |FINSI;
|FINSI;
|FPRC;

|PROCESO traba7; |TIPO 7;
    modo = (FEntrada / 100) ! 0;
|SI uno_nuevo = 1;|Y #0#4 = 2;
        uno_nuevo = 0;
    |SI modo ! 1;
        |PTEC 809;
        |PTEC 806;
        |PTEC 57;
        |PTEC 57;
        |PTEC 57;
        |PTEC 57;
    |FINSI;
|FINSI;
|FPRC;

|PROCESO arreglo; |TIPO 7;
    modo = (FEntrada / 100) ! 0;
|SI modo = 1;
        sw_enalta = 1;
|FINSI;
|FPRC;

|PROCESO mas_arreglos; |TIPO 7;
    sw_enalta = 0;
|FPRC;

|PROCESO fecha0; |TIPO 0;
|HAZ pin_semana;
    modo = (FEntrada / 100) ! 0;
|SI FSalida = 0;|Y modo = 1;
    |SI #1#0 > #0#3;
        |MENAV "    Fecha fuera de seleccion ...";
        |ERROR;
    |SINO;
            fecha = #1#0;
    |FINSI;
|FINSI;
|FPRC;

|PROCESO tipo13; |TIPO 13;
|HAZ pin_semana;
|HAZ pin_empre;
|HAZ pin_traba;
|HAZ pin_horas;
|FPRC;

|PROCESO pin_semana;
|PINTA 1602, "           ";
    fech1 = #1#0 % 1;
    alfa1 = fech1;
    alfa1 = alfa1 % 11;
|ATRI I;
|PINTA 1602, alfa1;
|ATRI 0;
|FPRC;

|PROCESO pin_empre; |TIPO 0;
|DDEFECTO #10;
|ABRE #10;
    #10#0 = #1#2;
|LEE 000#10=;
|CIERRA #10;
|ATRI I;  alfa1 = #10#2 % 131; |PINTA 1802, alfa1;     |ATRI 0;
|FPRC;

|PROCESO pin_traba; |TIPO 0;
|DDEFECTO #11;
|ABRE #11;
    #11#0 = #1#2;
    #11#1 = #1#3;
|LEE 000#11=;
|CIERRA #11;
|ATRI I;  alfa1 = #11#2 % 131; |PINTA 2002, alfa1;     |ATRI 0;
|FPRC;

|PROCESO pin_horas; |TIPO 0;
|DDEFECTO #12;
|ABRE #12;
    #12#0 = #1#4;
|LEE 000#12=;
|CIERRA #12;
|ATRI I;  alfa1 = #12#2;       |PINTA 2202, alfa1;     |ATRI 0;
|FPRC;

|PROCESO tipo11; |TIPO 11;
|SI FSalida = 9;
    |PTEC 77;
    |PTEC 802;           || modificacion
    |PTEC 802;
    |PTEC 802;
|FINSI;
|SI FSalida = 12;
    |PTEC 66;
    |PTEC 802;           || baja
    |PTEC 802;
    |PTEC 802;
|FINSI;
|FPRC;

|PROCESO tipo3; |TIPO 3;
    modo = (FEntrada / 100) ! 0;
|SI modo = 1;
        uno_nuevo = 1;
|FINSI;
|FPRC;

|PROCESO BuscaImporte;
     || busca importe de hora de trabajo para el modulo de VIPEI
     eaPrecioHora = 0;
     |ABRE #aovi0001;
     #aovi0001#0 = #labtraba#87;
     #aovi0001#2 = #labtraba#17;
     |LEE 000#aovi0001.=;
     |SI FSalida = 0;
          eaPrecioHora = #aovi0001#101;
     |FINSI;
     |CIERRA #aovi0001;
|FPRC;

|PROCESO Tipo2; |TIPO 2;
     |ABRE #13; |LEE 000#13p; |CIERRA #13;
     enForma = 0 +. 1;
     efFecha = #1#0;
     enUni = #1#5;
     enHora = #1#4;
     enObra = #1#1;
     enSubobra = #1#7;
     enTraba = #1#3;
     eaSerPre = #1#8;
     enNumPre = #1#9;
     eaNivel = #13#4;
     eaCapitulo = #1#11;
     eaPartida = #1#13;
     eaDesParti = #1#14;
     |HAZ BuscaImporte;
     |HAZ ActGestion;
|FPRC;

|PROCESO ConsultaChequeo;
     nError = 1;
     |ABRE #13; |LEE 000#13p; |CIERRA #13;
     |HAZ ExisteEmpresa;
     |SI FSalida ! 0;
          |MENAV "0000ERROR en configuracion empresa gestion...";
     |SINO;
          aSerExp = (("00000" + #0#0) % -105);
          nNumExp = #0#1;
          |DBASS aAlfa;
          aPath = aAlfa + #13#1; |QBF aPath;
          aEmp = ("00000" + #13#2) % -105;
          aDef = aPath + "/def/gomantho";
          |CARGA_DEF aDef, gomantho@;
          |SI FSalida = 0;
               aFic = aPath + "/fich/" + aEmp + "/";
               |PATH_DAT #gomantho@#aFic#;
               |ABRE #gomantho@;
               #gomantho@#0 = aSerExp;
               #gomantho@#1 = nNumExp;
               |SI nSw = 0;             || Chequeo
                    |LEE 000#gomantho@.=;
                    nError = FSalida;
                    aEstado = #gomantho@#9;
                    #0#5 = #gomantho@#29;
                    #0#6 = #gomantho@#3;
                    |PINTA #0#5;
                    |PINTA #0#6;
               |SINO;                   || Consulta
                    |LEE 000#gomantho@.];
                    |CONSULTA_CLAVE #1#gomantho@;
                    nError = FSalida;
                    |SI FSalida = 0;
                         #0#0 = #gomantho@#0;
                         #0#1 = #gomantho@#1;
                         aEstado = #gomantho@#9;
                         |PINTA #0#0;
                         |PINTA #0#1;
                         #0#5 = #gomantho@#29;
                         #0#6 = #gomantho@#3;
                         |PINTA #0#5;
                         |PINTA #0#6;
                    |FINSI;
               |FINSI;
               |CIERRA #gomantho@;
               |DESCARGA_DEF #gomantho@;
          |FINSI;
     |FINSI;
     FSalida = nError;
|FPRC;

|PROCESO Mensaje;
     |SI aEstado = "T";
          |MENAV "0000ERROR, Obra Terminada...";
          |ERROR;
     |SINO;
          |SI aEstado = "C";
               || OK
          |SINO;
               |SI aEstado = "E"; aEstado = "Espera"; |FINSI;
               |SI aEstado = "P"; aEstado = "Parada"; |FINSI;
               aAlfa = "0000ATENCION, Obra en estado: " + aEstado;
               |SI Campo = 1;
                    |MENAV aAlfa;
               |FINSI;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Consulta; |TIPO 66;
     nSw = 1;
     |HAZ ConsultaChequeo;
     |SI FSalida ! 0;
          |ERROR;
     |SINO;
          |HAZ Mensaje;
     |FINSI;
|FPRC;

|PROCESO Chequeo; |TIPO 0;
     |SI Campo = 0; |Y FSalida = 1;
          |ERROR;                 || Tecla ESC
          |ACABA;
     |FINSI;

     nSw = 0;
     |HAZ ConsultaChequeo;
     |SI FSalida ! 0;
          |MENAV "0000ERROR, No existe Expediente (Obra)";
          |ERROR;
     |SINO;
          |HAZ Mensaje;
     |FINSI;
|FPRC;
