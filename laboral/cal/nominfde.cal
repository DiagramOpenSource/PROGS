|FICHEROS;
     nominfde #0;
     labempre;
     labcentr;
     labtraba;
     nomcalca;
|FIN;

|VARIABLES;
     swSigue = 0;

     aAlfa1 = "";
     aAlfa2 = "";

     nNume1 = 0;
     nNume2 = 0;
     fFecha = @;

     fFechaBaja = @;
     fFechaDesde = @;
     fFechaHasta = @;

     nContratoD = 0;
     nContratoH = 0;
     nContrato  = 0;

     nDEmpre             = 0;
     nHEmpre             = 0;
     nOk                 = 0;
     nPasada             = 0;
     nOpcion             = 0;

     aDEstado            = "";
     aHEstado            = "";
     aAlfa               = "";
     aEmp                = "";
     aNomEmp             = "";

     {-1}aMenu           = "";
         aMenu1          = "2400";
         aMenu2          = "1";
         aMenu3          = "Archivo documental activado. Elija opci¢n: ";
         aMenu4          = "IAB";
         aMenu5          = "Imprimir";
         aMenu6          = "Archivar";
         aMenu7          = "Ambos";

     &eaVengoDe          = "";
     &eaPeriodos         = "";
     &enDesdeMes         = 0;
     &enAny              = 0;
     &nArchi             = 0;
     &swErrorDSArchi     = 0;
|FIN;

|PROCESO Defectos;
     fFecha = ~;
     aAlfa = fFecha;
     aAlfa = "01.01." + (aAlfa % -104);
     #0#8 = aAlfa;
     aAlfa = "31.12." + (aAlfa % -104);
     #0#9 = aAlfa;
|FPRC;

|PROCESO nomcalca;
     aAlfa = #nomcalca#5;
     |QBF aAlfa;
     |SI aAlfa ! "";
          fFechaBaja = #nomcalca#5;
          |SI fFechaDesde [ fFechaBaja; |Y fFechaBaja [ fFechaHasta;
                swSigue = swSigue + 1;
                #labtraba#37 = #nomcalca#5;
          |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE nomcalca;
     #nomcalca#1;
     ;
     #labtraba#0, #labtraba#1, nNume1, 0;
     #labtraba#0, #labtraba#1, nNume2, 0;
     ;
     NULL, nomcalca;
|FIN;

|PROCESO labtraba;
     swSigue = 0;

     fFechaBaja  = #labtraba#37;
     fFechaDesde = #0#8;
     fFechaHasta = #0#9;

     nContratoD  = #0#6;
     nContratoH  = #0#7;
     nContrato   = #labtraba#45;

     |SI fFechaDesde [ fFechaBaja; |Y fFechaBaja [ fFechaHasta;
           swSigue = swSigue + 1;
     |FINSI;

     aAlfa1 = #0#8;
     aAlfa1 = aAlfa1 % 402;
     aAlfa2 = #0#9;
     aAlfa2 = aAlfa2 % 402;

     nNume1 = aAlfa1;
     nNume2 = aAlfa2;

     |SI #0#11 = "S";
          |BUCLE nomcalca;
     |FINSI;

     |SI nContratoD [ nContrato; |Y nContrato [ nContratoH;
           swSigue = swSigue + 1;
     |FINSI;

     |SI swSigue ] 2;
         |SI nPasada = 0;
             nOk = 1;
             |ERROR10;
             |ACABA;
          |FINSI;

          #labempre#0 = #labtraba#0;
          |LEE 000#labempre.=

          #labcentr#0 = #labtraba#0;
          #labcentr#1 = #labtraba#77;
          |LEE 000#labcentr.=

          |PRINT;
     |FINSI;
|FPRC;

|DEFBUCLE labtraba;
     #labtraba#1;
     77, 44;
     nDEmpre, #0#4, #0#2, aDEstado;
     nHEmpre, #0#5, #0#3, aHEstado;
     ;
     NULL, labtraba;
|FIN;

|PROCESO Papel;
     nOk = 0;
     nPasada = 0;  |BUCLE labtraba;
     |SI nOk = 0;
         |ACABA;
     |FINSI;

     Impresora = "CrystalReport";
     |IMPRE -1;
     |SI FSalida ! 0;
         |MENAV "    Proceso cancelado ...";
         |ACABA;
     |FINSI;

     |INFOR "nominfde";
     |SI FSalida ! 0;
         |FINIMP;
         |ACABA;
     |FINSI;

     nPasada = 1;  |BUCLE labtraba;

     |PIEINF;
     |FININF;
     |FINIMP;
|FPRC;

|PROCESO Mensaje;
     aEmp = "                                             ";

     aAlfa   = ("00000" + #labempre#0) % -105;
     aNomEmp = #labempre#2;
     aNomEmp = aNomEmp % 126;
     aEmp = "Empresa: " + aAlfa + " - " + aNomEmp;
     aEmp = " " + aEmp + " ";

     |CUADRO 1217 1619;
     |ATRI R;
     |PINTA 1318, "           - Procesando archivo de bajas -          ";
     |PINTA 1418, "                                                    ";
     |PINTA 1518, "                                                    ";
     |PINTA 1518, aEmp;
     |ATRI 0;
|FPRC;

|PROCESO labempre;
     nDEmpre = #labempre#0;
     nHEmpre = #labempre#0;

     nOk     = 0;
     nPasada = 0;  |BUCLE labtraba;
     |SI nOk = 0;
         |ACABA;
     |FINSI;

     #labempre#0 = #labtraba#0;
     |LEE 000#labempre.=;
     |SI FSalida ! 0;  |DDEFECTO #labempre;  |FINSI;

     |HAZ Mensaje;
     |HAZ IMPREArchi;

     nPasada = 1;  |BUCLE labtraba;

     |PIEINF;
     |FININF;

     |SI swErrorDSArchi = 0;
         |HAZ Archivando;
     |FINSI;
|FPRC;

|DEFBUCLE labempre;
     #labempre#1;
     ;
     #nominfde#0;
     #nominfde#1;
     ;
     NULL, labempre;
|FIN;

|PROCESO Archivado;
     eaPeriodos = #nominfde#8 + " a " +  #nominfde#9;

     aAlfa = #nominfde#8 % 0402;  enDesdeMes = aAlfa;
     aAlfa = #nominfde#8 % -104;  enAny      = aAlfa;

     |BUCLE labempre;
|FPRC;

|PROCESO T3;  |TIPO 3;
     nDEmpre = #nominfde#0;
     nHEmpre = #nominfde#1;

     |SI #0#10 = "T";
         aDEstado = "A";
         aHEstado = "B";
     |SINO;
         aDEstado = #0#10;
         aHEstado = #0#10;
     |FINSI;

     eaVengoDe = "nominfde";
     |HAZ MiraSiArchiva;
     |SI nArchi = 0;
         |HAZ Papel;
         |ACABA;
     |FINSI;

     |MENU aMenu;
     nOpcion = FSalida;
     |SI nOpcion = 1;  |HAZ Papel;      |FINSI;
     |SI nOpcion = 2;  |HAZ Archivado;  |FINSI;
     |SI nOpcion = 3;  |HAZ Papel;      |FINSI;
     |SI nOpcion = 3;  |HAZ Archivado;  |FINSI;
|FPRC;

|PROGRAMA;
     |CLS;
     |HAZ Defectos;

     |ABRET #labempre;
     |ABRET #labcentr;
     |ABRET #nomcalca;

     |PINPA #0#0;
     |PINDA #0#0;
     |ENDATOS #1#0;

     |CIERRAT #nomcalca;
     |CIERRAT #labcentr;
     |CIERRAT #labempre;
|FPRO;
