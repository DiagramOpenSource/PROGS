|FICHEROS;
     hotm0048 #0;
     nomhicca #1;
     labtraba #2;
     labempre #3;
     hotm0051 #51;
|FIN;

|VARIABLES;
    seleccio = 0;
    centro = 0;
    sw = 0;
    empresa = 0;
    tinf_tc1 = 0;
    tinf_sss = 0;
    tinf_dse = 0;
    tinf_enf = 0;
    ginf_bru = 0;
    ginf_ded = 0;
    ginf_liq = 0;
    cinf_bru = 0;
    cinf_ded = 0;
    cinf_liq = 0;
    nume1 = 0;
    nume2 = 0;
    fecsys = @;
    alfa1 = "";
    error0       = "     Proceso ABORTADO !!! ";
    informe = "nomyreno";
    anyo = "";
    fechasys = @;
    &ano = 0;
    &p_mes = " ";
    &swlin = 0;
    &varinf1 = "";
    &varinf2 = "";

    &inf_bru = 0;
    &inf_tan = 0;
    &inf_ded = 0;
    &inf_liq = 0;

    &pinf_bru = 0;
    &pinf_ded = 0;
    &pinf_liq = 0;

    &tinf_bru = 0;
    &tinf_ded = 0;
    &tinf_liq = 0;

    cinf_dto = 0;
    ginf_dto = 0;
    &inf_dto = 0;
    &pinf_dto = 0;
    &tinf_dto = 0;

    tip = "";
    &fecha = @;
    &cabeza = "";
    grupo = " ";
    nAnyo = 0;
    nTipo = 0;
|FIN;

|INCLUYE i_yreno;

******************************************************************************
   LLAMADAS DE CAMPOS
******************************************************************************

|PROCESO mirames; |TIPO 7; ||CALCULA EL MES POR DEFECTO SEGUN FECHA SISTEMA
    fecsys = ~;
    alfa1 = fecsys;
    alfa1 = alfa1 % 402;
    #0Campo = alfa1;
|PINTA #0Campo;
|FPRC;

|PROCESO pintames; |TIPO 0;      || PINTA EL MES LITERAL
|SI #0Campo =  1;  p_mes = "ENERO     ";  |FINSI;
|SI #0Campo =  2;  p_mes = "FEBRERO   ";  |FINSI;
|SI #0Campo =  3;  p_mes = "MARZO     ";  |FINSI;
|SI #0Campo =  4;  p_mes = "ABRIL     ";  |FINSI;
|SI #0Campo =  5;  p_mes = "MAYO      ";  |FINSI;
|SI #0Campo =  6;  p_mes = "JUNIO     ";  |FINSI;
|SI #0Campo =  7;  p_mes = "JULIO     ";  |FINSI;
|SI #0Campo =  8;  p_mes = "AGOSTO    ";  |FINSI;
|SI #0Campo =  9;  p_mes = "SEPTIEMBRE";  |FINSI;
|SI #0Campo = 10;  p_mes = "OCTUBRE   ";  |FINSI;
|SI #0Campo = 11;  p_mes = "NOVIEMBRE ";  |FINSI;
|SI #0Campo = 12;  p_mes = "DICIEMBRE ";  |FINSI;
|ATRI I;
|PINTA 1336, p_mes;
|ATRI 0;
|FPRC;

|PROCESO pintanyo;|TIPO 7;
    fechasys = ~;
    anyo = fechasys %  - 102;
    #0#5 = anyo;
|PINTA #0#5;
    ano = #0#5;
|FPRC;

******************************************************************************
   LECTURA E IMPRESION
******************************************************************************

|PROCESO imprimir;

     || Leo datos del Labtraba
     #2#0 = #hotm0051#0;
     #2#1 = #hotm0051#3;
     |LEE 000#2=;

     || Asigno datos del centro, area y seccion
     #2#62 = #hotm0051#6;     || Area
     #2#63 = #hotm0051#8;     || Seccion
     #2#77 = #hotm0051#4;     || Centro

    #3#0 = #2#0;
|LEE 020#3=;
|SI FSalida ! 0;  |DDEFECTO #3;  |FINSI;
|SI #3#36 ! "N";
        #1#0 = #2#0;   || Empresa
        #1#1 = #2#1;   || Trabajador
        #1#2 = #0#2;   || Mes
        #1#3 = #0#4;   || Tipo
        #1#66 = #0#5;  || Anyo
    |LEE 040#1=;
    |SI FSalida = 0; |Y #1#16 ! 0;
        |SI sw = 0;
                empresa = #2#0;
                centro = #2#77;
                sw = 1;
                grupo = #2#62;
                seleccio = 1;
        |FINSI;
        |SI empresa ! #2#0;                    || ruptura por empresa
            |HAZ rup_areas;
            |HAZ rup_centr;
            |HAZ rup_empre;
        |SINO;
            |SI centro ! #2#77;                        || ruptura por centro
                |HAZ rup_areas;
                |HAZ rup_centr;
            |SINO;
                |SI grupo ! #2#62;                     || ruptura por areas
                    |HAZ rup_areas;
                |FINSI;
            |FINSI;
        |FINSI;
        |HAZ detalle;
        |PRINT;
        |HAZ centros;
        |HAZ grupos;
        ||HAZ totales;
    |FINSI;
|FINSI;
|FPRC;

|PROCESO rup_empre;
    swlin = 2;
    FSalida = "PAGINA";
|PRINT;
|HAZ limptotal;
    empresa = #2#0;
    swlin = 0;
|FPRC;

|PROCESO rup_centr;
|HAZ lee_centro;
    swlin = 1;
    varinf1 = "Centro: " + #4#1 + "/" + #4#2;
|HAZ puecentro;
|PRINT;
|HAZ limparcial;
    centro = #2#77;
    swlin = 0;
|FPRC;

|PROCESO rup_areas;
|SI #0#6 = "S";
    |HAZ lee_area;
        swlin = 1;
        varinf1 = "Area: " + #5#0 + "/" +  #5#1;
    |HAZ puegrupos;
    |PRINT;
    |HAZ limpgrupos;
        grupo = #2#62;
        swlin = 0;
|FINSI;
|FPRC;

|DEFBUCLE hotm00480;
     #hotm0051#1;
     4;
     #0#0, nAnyo, #0#2,     1, #0#8;
     #0#1, nAnyo, #0#2, 99999, #0#9;
     ;
     NULL, NULL, NULL, NULL, NULL, imprimir;
     #hotm0051#0, #hotm0051#4, #hotm0051#6, #hotm0051#3;
|FIN;

|DEFBUCLE hotm00481;
     #hotm0051#1;
     4;
     #0#0, nAnyo, #0#2,     1, #0#8;
     #0#1, nAnyo, #0#2, 99999, #0#9;
     ;
     NULL, NULL, NULL, NULL, NULL, imprimir;
     #hotm0051#0, #hotm0051#4, #hotm0051#3;
|FIN;

|DEFBUCLE hotm00482;
     #hotm0051#1;
     4;
     #0#0, nAnyo, #0#2,     1, #0#8;
     #0#1, nAnyo, #0#2, 99999, #0#9;
     ;
     NULL, NULL, NULL, NULL, NULL, imprimir;
||      #hotm0051#0, #hotm0051#4, #hotm0051#6, #2#2, #2#1;
     #hotm0051#0, #hotm0051#4, #hotm0051#6, #hotm0051#10, #hotm0051#3;
|FIN;

|DEFBUCLE hotm00483;
     #hotm0051#1;
     4;
     #0#0, nAnyo, #0#2,     1, #0#8;
     #0#1, nAnyo, #0#2, 99999, #0#9;
     ;
     NULL, NULL, NULL, NULL, NULL, imprimir;
||     #2#0, #2#77, #2#2, #2#1;
     #hotm0051#0, #hotm0051#4, #hotm0051#10, #hotm0051#3;
|FIN;

|PROCESO imprime;|TIPO 3;
     |SI #0#4 = 0; tip = "MENSUAL - ";   |FINSI;
     |SI #0#4 = 2; tip = "FINIQUITO - "; |FINSI;
     |SI #0#4 = 5; tip = "ATRASOS - ";   |FINSI;
     fecha = #0#3;
     cabeza = "RESUMEN HISTORICO PAGAS " + tip + p_mes;
     |HAZ centra;
     |ABRE #1;
     |ABRE #2;
     |ABRE #3;
     |ABRE #hotm0051;
     |IMPRE 0;
     |SI FSalida = 0;
          |INFOR informe;
          seleccio = 0;
          nAnyo = #0#5 + 2000;
          |SI #0#7 = 1;
              |SI #0#6 = "S";  |BUCLE hotm00480;  |FINSI;
              |SI #0#6 = "N";  |BUCLE hotm00481;  |FINSI;
          |SINO;
              |SI #0#6 = "S";  |BUCLE hotm00482;  |FINSI;
              |SI #0#6 = "N";  |BUCLE hotm00483;  |FINSI;
          |FINSI;
          |SI seleccio = 1;
              |HAZ rup_areas;
              |HAZ rup_centr;
              |PIEINF;
          |FINSI;
          |FININF;
     |SINO;
          |MENAV error0;
     |FINSI;
     |FINIMP;
     |CIERRA #1;
     |CIERRA #2;
     |CIERRA #3;
     |CIERRA #hotm0051;
|FPRC;

******************************************************************************
    CARGA DE VARIABLES
******************************************************************************

|PROCESO detalle;
    inf_bru = #1#16;                              || Importe bruto
    inf_tan = #1#129; inf_tan = inf_tan ! 2;      || Porcentaje
    inf_dto = #1#128;                             || Anticipo P.E.
    inf_ded = #1#17;                              || Deduccion
    inf_liq = inf_bru - inf_ded - inf_dto;        || Liquido
|FPRC;

|PROCESO grupos;
    ginf_bru = ginf_bru + inf_bru;       || Importe bruto
    ginf_ded = ginf_ded + inf_ded;       || Deduccion
    ginf_dto = ginf_dto + inf_dto;       || Anticipo P.E.
    ginf_liq = ginf_liq + inf_liq;       || Liquido
|FPRC;

|PROCESO centros;
    cinf_bru = cinf_bru + inf_bru;       || Importe bruto
    cinf_dto = cinf_dto + inf_dto;       || Anticipo P.E.
    cinf_ded = cinf_ded + inf_ded;       || Deduccion
    cinf_liq = cinf_liq + inf_liq;       || Liquido
|FPRC;

|PROCESO &totales;
    tinf_bru = tinf_bru + inf_bru;        || Importe bruto
    tinf_dto = tinf_dto + inf_dto;        || Anticipo P.E.
    tinf_ded = tinf_ded + inf_ded;        || Deduccion
    tinf_liq = tinf_liq + inf_liq;        || Liquido
|FPRC;
******************************************************************************
    PASE DE VARIABLES
******************************************************************************

|PROCESO puecentro;
    pinf_bru = cinf_bru;
    pinf_dto = cinf_dto;
    pinf_ded = cinf_ded;
    pinf_liq = cinf_liq;
|FPRC;

|PROCESO puegrupos;
    pinf_bru = ginf_bru;
    pinf_dto = ginf_dto;
    pinf_ded = ginf_ded;
    pinf_liq = ginf_liq;
|FPRC;

******************************************************************************
    LIMPIEZA DE VARIABLES
******************************************************************************

|PROCESO limpgrupos;
    ginf_bru = 0;
    ginf_dto = 0;
    ginf_ded = 0;
    ginf_liq = 0;
|FPRC;

|PROCESO limparcial;
    cinf_bru = 0;
    cinf_dto = 0;
    cinf_ded = 0;
    cinf_liq = 0;
|FPRC;

|PROCESO limptotal;
    tinf_bru = 0;
    tinf_dto = 0;
    tinf_ded = 0;
    tinf_liq = 0;
|FPRC;

******************************************************************************
   RUTINAS
******************************************************************************

|PROCESO centra;         || CENTRA TITULO DEL INFORME
    alfa1 = cabeza % 0;
    nume1 = alfa1;
    nume2 = ((79 - nume1) / 2) - 0.5;
    nume2 = nume2 ! 0;
    alfa1 = " " * nume2;
    cabeza = alfa1 + cabeza;
|FPRC;
