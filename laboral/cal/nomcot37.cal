|FICHEROS;
     nomcot37 #0;
     nomcot38;

     nomcalca;
     nomhicca;
     nomcot08;

     labempre;
     labtraba;
|FIN;

|VARIABLES;
     aAlfa = "";
     aAlfa1 = "";
     nCampo = 0;
     nCampo1 = 0;
     nCampo2 = 0;
     swDiferentes = 0;
     swHayRegistros = 0;
     swEncontrada = 0;
     swSigue = 0;
     nNume1 = 0;
     nNume2 = 0;
     nDiferencia = 0;

     nAny = 0;
     nCampoHis = 0;
|FIN;

|PROCESO AbreImp;
     Impresora = "CrystalReport";
     |IMPRE -1;
     |SI FSalida = 0;
          |INFOR "nomcot37";
     |FINSI;
|FPRC;

|PROCESO imprime;
     |SI swHayRegistros = 0;
          |HAZ AbreImp;
          swHayRegistros = 1;
     |FINSI;

     #labempre#0 = #nomcot38#0;
     |LEE 000#labempre.=;

     #labtraba#0 = #nomcot38#0;
     #labtraba#1 = #nomcot38#1;
     |LEE 000#labtraba.=;

     |PRINT;
|FPRC;

|DEFBUCLE nomcot38;
     #nomcot38#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, imprime;
|FIN;

|PROCESO Informe;
     |LEE 000#nomcot38.p;
     |SI FSalida = 0;
          |BUCLE nomcot38;

          |PIEINF;
          |FININF;
          |FINIMP;
     |SINO;
          |MENAV "    No se han encontrado diferencias";
     |FINSI;
|FPRC;

|PROCESO GrabaDifHis;
     |DDEFECTO #nomcot38;
     #nomcot38#0 = #nomhicca#0;
     #nomcot38#1 = #nomhicca#1;
     #nomcot38#2 = #nomhicca#2;
     #nomcot38#3 = #nomhicca#3;

     |SI swEncontrada = 1;
          #nomcot38#4 = nCampoHis;
          aAlfa1 = nCampoHis;
          |QBF aAlfa1;
          aAlfa1 = ("000" + aAlfa1) % -103;
          aAlfa1 = "|C" + aAlfa1 + "NOMBRE";
          #nomcot38#5 = #nomhicca#aAlfa1#;

          #nomcot38#6 = #nomhicca#nCampoHis#;
          #nomcot38#7 = #nomcot08#nCampo#;
          #nomcot38#8 = "Historico";
          |GRABA 020#nomcot38.n;
          |LIBERA #nomcot38;
     |SINO;
          #nomcot38#4 = 255;
          #nomcot38#5 = "NO ENCONTRADO";
          #nomcot38#8 = "Historico";
          |SI #nomcot38#3 = 0;
               |GRABA 020#nomcot38.n;
               |LIBERA #nomcot38;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO ConversionHis;
     |SI nCampo ] 0; |Y nCampo [ 65;
          nCampoHis = nCampo;
     |FINSI;
     |SI nCampo ] 66; |Y nCampo [ 75;
          nCampoHis = nCampo + 39;
     |FINSI;
     |SI nCampo ] 76; |Y nCampo [ 78;
          nCampoHis = nCampo + 41;
     |FINSI;
     |SI nCampo ] 79;
          nCampoHis = nCampo + 42
     |FINSI;
|FPRC;

|PROCESO nomhicca;
     swEncontrada = 0;

     #labempre#0 = #nomhicca#0;
     |LEE 000#labempre.=;

     #labtraba#0 = #nomhicca#0;
     #labtraba#1 = #nomhicca#1;
     |LEE 000#labtraba.=;

     |SI #labtraba#42 = "H";
          |ACABA;
     |FINSI;
     |SI #labtraba#247 = 0138;
          |ACABA;
     |FINSI;
     |SI #labempre#67 ! "S";
          |ACABA;
     |FINSI;
     |SI #labtraba#42 = "A"; |O #labtraba#42 = "E";
          |ACABA;
     |FINSI;

     #nomcot08#0 = #nomhicca#0;
     #nomcot08#1 = #nomhicca#1;
     #nomcot08#2 = #nomhicca#2;
     #nomcot08#3 = #nomhicca#3;
     |LEE 000#nomcot08.=;
     |SI FSalida = 0; |Y #nomcot08#184 = #0#6;
          swEncontrada = 1;
          |PARA nCampo = 0; |SI nCampo [ 198; |HACIENDO nCampo = nCampo + 1;
               |HAZ ConversionHis;
               swSigue = 1;
               |SI #labtraba#45 = 421; |O #labtraba#45 = 422;
                    |SI nCampo = 39; swSigue = 0; |FINSI;
                    |SI nCampo = 40; swSigue = 0; |FINSI;
               |FINSI;
               |SI nCampo = 123;
                    swSigue = 0;
               |FINSI;
               |SI nCampo = 81; |O nCampo = 191;
                    swSigue = 0;
               |FINSI;
               |SI nCampo = 43;
                    #nomhicca#nCampoHis# = #nomhicca#nCampoHis# + #nomhicca#233;
               |FINSI;
               |SI nCampo = 63;
                    #nomhicca#nCampoHis# = #nomhicca#nCampoHis# + #nomcalca#123
               |FINSI;
               |SI nCampo = 12; |Y #nomhicca#43 = 0;
                    swSigue = 0;
               |FINSI;
               |SI swSigue = 1;
                    |SI #nomhicca#nCampoHis# ! #nomcot08#nCampo#;
                         nNume1 = #nomhicca#nCampoHis#;
                         nNume2 = #nomcot08#nCampo#;
                         nDiferencia = nNume1 - nNume2;
                         |SI -0.05 [ nDiferencia; |Y nDiferencia [ 0.05;
                              || centimos
                              swSigue = 0;
                              || considero que no hay diferencia
                         |SINO;
                              swSigue = 1;
                         |FINSI;
                    |SINO;
                         swSigue = 0;
                    |FINSI;
               |FINSI;
               |SI swSigue = 1;
                    |HAZ GrabaDifHis;
               |FINSI;
          |SIGUE;
     |SINO;
          |HAZ GrabaDifHis;
     |FINSI;
|FPRC;

|DEFBUCLE nomhicca;
     #nomhicca#1;
     ;
     #0#0, #0#2, nAny, #0#7, #0#4;
     #0#1, #0#3, nAny, #0#7, #0#5;
     ;
     NULL, nomhicca;
|FIN;

|PROCESO GrabaDif;
     |DDEFECTO #nomcot38;
     #nomcot38#0 = #nomcalca#0;
     #nomcot38#1 = #nomcalca#1;
     #nomcot38#2 = #nomcalca#2;
     #nomcot38#3 = #nomcalca#3;

     |SI swEncontrada = 1;
          #nomcot38#4 = nCampo;
          aAlfa1 = nCampo;
          |QBF aAlfa1;
          aAlfa1 = ("000" + aAlfa1) % -103;
          aAlfa1 = "|C" + aAlfa1 + "NOMBRE";
          #nomcot38#5 = #nomcalca#aAlfa1#;

          #nomcot38#6 = #nomcalca#nCampo#;
          #nomcot38#7 = #nomcot08#nCampo#;
          #nomcot38#8 = "Mensual";
          |GRABA 020#nomcot38.n;
          |LIBERA #nomcot38;
     |SINO;
          #nomcot38#4 = 255;
          #nomcot38#5 = "NO ENCONTRADO";
          #nomcot38#8 = "Mensual";
          |SI #nomcot38#3 = 0;
               |GRABA 020#nomcot38.n;
               |LIBERA #nomcot38;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO nomcalca;
     |SI #nomcalca#145 = "S";
          || nomina bloqueada, ignorar

          |ACABA;
     |FINSI;

     swEncontrada = 0;

     #labempre#0 = #nomcalca#0;
     |LEE 000#labempre.=;

     #labtraba#0 = #nomcalca#0;
     #labtraba#1 = #nomcalca#1;
     |LEE 000#labtraba.=;

     |SI #labtraba#42 = "H";
          |ACABA;
     |FINSI;
     |SI #labtraba#247 = 0138;
          |ACABA;
     |FINSI;
     |SI #labempre#67 ! "S";
          |ACABA;
     |FINSI;
     |SI #labtraba#42 = "A"; |O #labtraba#42 = "E";
          |ACABA;
     |FINSI;

     #nomcot08#0 = #nomcalca#0;
     #nomcot08#1 = #nomcalca#1;
     #nomcot08#2 = #nomcalca#2;
     #nomcot08#3 = #nomcalca#3;
     |LEE 000#nomcot08.=;
     |SI FSalida = 0;
          swEncontrada = 1;
          |PARA nCampo = 0; |SI nCampo [ 198; |HACIENDO nCampo = nCampo + 1;
               swSigue = 1;
               |SI #labtraba#45 = 421; |O #labtraba#45 = 422;
                    |SI nCampo = 39; swSigue = 0; |FINSI;
                    |SI nCampo = 40; swSigue = 0; |FINSI;
               |FINSI;
               |SI nCampo = 123;
                    swSigue = 0;
               |FINSI;
               |SI nCampo = 81; |O nCampo = 191;
                    swSigue = 0;
               |FINSI;
               |SI nCampo = 43;
                    #nomcalca#nCampo# = #nomcalca#nCampo# + #nomcalca#191;
               |FINSI;
               |SI nCampo = 63;
                    #nomcalca#nCampo# = #nomcalca#nCampo# + #nomcalca#81;
               |FINSI;
               |SI nCampo = 12; |Y #nomcalca#43 = 0;
                    swSigue = 0;
               |FINSI;
               |SI swSigue = 1;
                    |SI #nomcalca#nCampo# ! #nomcot08#nCampo#;
                         nNume1 = #nomcalca#nCampo#;
                         nNume2 = #nomcot08#nCampo#;
                         nDiferencia = nNume1 - nNume2;
                         |SI -0.05 [ nDiferencia; |Y nDiferencia [ 0.05;
                              || centimos
                              swSigue = 0;
                              || considero que no hay diferencia
                         |SINO;
                              swSigue = 1;
                         |FINSI;
                    |SINO;
                         swSigue = 0;
                    |FINSI;
               |FINSI;
               |SI swSigue = 1;
                    |HAZ GrabaDif;
               |FINSI;
          |SIGUE;
     |SINO;
          |HAZ GrabaDif;
     |FINSI;
|FPRC;

|DEFBUCLE nomcalca;
     #nomcalca#1;
     ;
     #0#0, #0#2, #0#7, #0#4;
     #0#1, #0#3, #0#7, #0#5;
     ;
     NULL, nomcalca;
|FIN;

|PROGRAMA;
     |CLS;

     |PINPA #0#0;

     |ABRE #0;
     |DDEFECTO #0;
     |LEE 000#0p;
     |SI FSalida = 0;
          |BORRA 020#0a;
     |FINSI;

     |PINDA #0#0;

     |ENDATOS #1#0;
     |SI FSalida = 0;
          aAlfa = Usuario; |QBT aAlfa; aAlfa = "tm_nomcot38_" + aAlfa;
          |NOME_DAT #nomcot38#aAlfa#;
          |DELINDEX #nomcot38;
          |ABRE #nomcot08;
          |ABRE #nomcalca;
          |ABRE #nomcot38;
          |ABRE #labempre;
          |ABRE #labtraba;

          |BUCLE nomcalca;
          nAny = #0#6 - 2000;
          ||BUCLE nomhicca;             || tarda mucho, mirar

          ||CONSULTA_CLAVE #1#nomcot34;
          |HAZ Informe;

          |CIERRA #labtraba;
          |CIERRA #labempre;
          |CIERRA #nomcalca;
          |CIERRA #nomcot08;
          |DELINDEX #nomcot38;
          |CIERRA #nomcot38;
     |FINSI;


     |GRABA 000#0n;
     |CIERRA #0;
|FPRO;
