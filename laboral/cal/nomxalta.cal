|FICHEROS;
     nomxalta;
     labtraba;
     labempre;
|FIN;

|VARIABLES;
     nOk                 = 0;
     nEmp                = 0;
     nOpcion             = 0;
     nPasada             = 0;
     nDEmpre             = 0;
     nHEmpre             = 0;
     desde               = 0;
     hasta               = 0;
     mirar               = 0;
     fecsys              = @;
     swbonif             = 0;
     mesnum              = 0;
     anyonum             = 0;

     p_mes               = "";
     mes                 = "";
     anyo                = "";
     alfa1               = "";
     aAlfa               = "";
     aEmp                = "";
     aNomEmp             = "";

     {-1}aMenu           = "";
         aMenu1          = "2400";
         aMenu2          = "1";
         aMenu3          = "Archivo documental activado. Elija opci¢n: ";
         aMenu4          = "IAB";
         aMenu5          = "Imprimir";
         aMenu6          = "Archivar";
         aMenu7          = "Ambos";

     &fecbonif           = "";
     &eaVengoDe          = "";
     &eaPeriodos         = "";
     &enDesdeMes         = 0;
     &enAny              = 0;
     &nArchi             = 0;
     &swErrorDSArchi     = 0;
|FIN;

|PROCESO desmenuza;
     mes  = alfa1 % 402;
     anyo = alfa1 % 704;
     mesnum  = mes;
     anyonum = anyo;
     mirar = (anyonum * 100) + mesnum;
|FPRC;

|PROCESO imprimir;
     swbonif = 0;
     fecbonif = "";

     |SI #labtraba#36 ! "..........";  |Y #labtraba#36 ! "          ";
         alfa1 = #labtraba#36;
         |HAZ desmenuza;
         |SI desde [ mirar; |Y mirar [ hasta;
             fecbonif = #labtraba#36;
             swbonif  = 1;
         |FINSI;
     |FINSI;

     |SI swbonif = 0;
         |ACABA;
     |FINSI;

     |SI nPasada = 0;
         nOk = 1;
         |ERROR10;
         |ACABA;
     |FINSI;

     #labempre#0 = #labtraba#0;
     |LEE 000#labempre.=;
     |SI FSalida ! 0;  |DDEFECTO #labempre;  |FINSI;

     |PRINT;
|FPRC;

|PROCESO inicia;
     desde = (#nomxalta#5 * 100) + #nomxalta#4;
     hasta = (#nomxalta#8 * 100) + #nomxalta#7;
|FPRC;

|DEFBUCLE nomxalta;
     #labtraba#1;
     ;
     nDEmpre, #nomxalta#2;
     nHEmpre, #nomxalta#3;
     ;
     inicia, imprimir;
|FIN;

|PROCESO Papel;
     nOk     = 0;
     nPasada = 0;  |BUCLE nomxalta;
     |SI nOk = 0;
         |ACABA;
     |FINSI;

     Impresora = "CrystalReport";
     |IMPRE -1;
     |SI FSalida ! 0;
         |MENAV "    Proceso cancelado ...";
         |ACABA;
     |FINSI;

     |INFOR "nomxalta";
     |SI FSalida ! 0;
         |FINIMP;
         |ACABA;
     |FINSI;

     nPasada = 1;  |BUCLE nomxalta;

     |PIEINF;
     |FININF;
     |FINIMP;
|FPRC;

|PROCESO Mensaje;
     aEmp = "                                             ";

     aAlfa   = ("00000" + #labempre#0) % -105;
     aNomEmp = #labempre#2;
     aNomEmp = aNomEmp % 126;
     aEmp = "Empresa: " + aAlfa + " - " + aNomEmp;
     aEmp = " " + aEmp + " ";

     |CUADRO 1217 1619;
     |ATRI R;
     |PINTA 1318, "           - Procesando archivo de altas -          ";
     |PINTA 1418, "                                                    ";
     |PINTA 1518, "                                                    ";
     |PINTA 1518, aEmp;
     |ATRI 0;
|FPRC;

|PROCESO labempre;
     nDEmpre = #labempre#0;
     nHEmpre = #labempre#0;

     desde   = 0;
     hasta   = 0;
     nOk     = 0;
     nPasada = 0;  |BUCLE nomxalta;
     |SI nOk = 0;
         |ACABA;
     |FINSI;

     |HAZ Mensaje;
     |HAZ IMPREArchi;

     nPasada = 1;  |BUCLE nomxalta;

     |PIEINF;
     |FININF;

     |SI swErrorDSArchi = 0;
         |HAZ Archivando;
     |FINSI;
|FPRC;

|DEFBUCLE labempre;
     #labempre#1;
     ;
     #nomxalta#0;
     #nomxalta#1;
     ;
     NULL, labempre;
|FIN;

|PROCESO Archivado;
     eaPeriodos = ("00" + #nomxalta#4) % -102;
     eaPeriodos = eaPeriodos + "/" + #nomxalta#5;
     eaPeriodos = eaPeriodos + " a  " + (("00" + #nomxalta#7) % -102);
     eaPeriodos = eaPeriodos + "/" + #nomxalta#8;

     enDesdeMes = #nomxalta#4;
     enAny      = #nomxalta#5;

     |BUCLE labempre;
|FPRC;

|PROCESO impre; |TIPO 3;
     |ABRET #labempre;

     nDEmpre = #nomxalta#0;
     nHEmpre = #nomxalta#1;

     eaVengoDe = "nomxalta";
     |HAZ MiraSiArchiva;
     |SI nArchi = 0;
         |HAZ Papel;

         |CIERRAT #labempre;
         |ACABA;
     |FINSI;

     |MENU aMenu;
     nOpcion = FSalida;
     |SI nOpcion = 1;  |HAZ Papel;      |FINSI;
     |SI nOpcion = 2;  |HAZ Archivado;  |FINSI;
     |SI nOpcion = 3;  |HAZ Papel;      |FINSI;
     |SI nOpcion = 3;  |HAZ Archivado;  |FINSI;

     |CIERRAT #labempre;
|FPRC;

|PROCESO defecto; |TIPO 7; ||calcula el periodo por defecto segun fecha sistema
     fecsys = ~;
     alfa1 = fecsys;
     alfa1 = alfa1 % -104;
     #nomxalta#Campo# = alfa1;
     |PINTA #nomxalta#Campo#;
|FPRC;

|PROCESO pintames; |TIPO 0;
     |SI #nomxalta#Campo# =  1;  p_mes = "ENERO     ";  |FINSI;
     |SI #nomxalta#Campo# =  2;  p_mes = "FEBRERO   ";  |FINSI;
     |SI #nomxalta#Campo# =  3;  p_mes = "MARZO     ";  |FINSI;
     |SI #nomxalta#Campo# =  4;  p_mes = "ABRIL     ";  |FINSI;
     |SI #nomxalta#Campo# =  5;  p_mes = "MAYO      ";  |FINSI;
     |SI #nomxalta#Campo# =  6;  p_mes = "JUNIO     ";  |FINSI;
     |SI #nomxalta#Campo# =  7;  p_mes = "JULIO     ";  |FINSI;
     |SI #nomxalta#Campo# =  8;  p_mes = "AGOSTO    ";  |FINSI;
     |SI #nomxalta#Campo# =  9;  p_mes = "SEPTIEMBRE";  |FINSI;
     |SI #nomxalta#Campo# = 10;  p_mes = "OCTUBRE   ";  |FINSI;
     |SI #nomxalta#Campo# = 11;  p_mes = "NOVIEMBRE ";  |FINSI;
     |SI #nomxalta#Campo# = 12;  p_mes = "DICIEMBRE ";  |FINSI;
     |PINTA 1422, p_mes;
|FPRC;

|PROCESO mirames; |TIPO 7; ||calcula el mes por defecto segun fecha sistema
     fecsys = ~;
     alfa1 = fecsys;
     alfa1 = alfa1 % 402;
     #nomxalta#Campo# = alfa1;
     |PINTA #nomxalta#Campo#;
|FPRC;

|PROCESO masuno; |TIPO 0;   || mes desde mayor que mes hasta y a¤o igual
     |SI #nomxalta#4 > #nomxalta#7;|Y #nomxalta#5 = #nomxalta#8;
         |MENAV "    ATENCION!!! Error de entrada ...";
         |ERROR;
     |FINSI;
|FPRC;

|PROCESO masdos; |TIPO 0;   || a¤o desde mayor que a¤o hasta
     |SI #nomxalta#5 > #nomxalta#8;
         |MENAV "    ATENCION!!! Error de entrada ...";
         |ERROR;
     |FINSI;
|FPRC;
