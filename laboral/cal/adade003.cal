|FICHEROS;
     adade003 #0;
     adade004;
     labempre;
     labcentr;
     labtraba;
     nomtrali;
     nomcatli;
|FIN;

|VARIABLES;
     aValor = "";
     aAlfa = "";
     nLinea = 0;
     &nMes = 0;
     &nAny = 0;
     &aMes = "";
     aDirBase = "";
     aBase = "";
     aOrigen = "";
     aDestino = "";
     aDestinoTrab = "";
     aIPRemoto = "";
     nValor = 0;
  {1}aVersion = "";
     aPath = "";
     nLongitud = 0;
     aLongitud = "";
     nPos = 0;
     nCaracter = 0;
     aCaracter = "";

     nValorCon = 0;
     nConcepto = 0;
     nCampo = 0;
     swSigue = 0;
|FIN;

|PROCESO ExtraePath;
     aPath     = aVersion1;
     aLongitud = aPath % 0;
     nLongitud = aLongitud;
     |PARA nPos = 1; |SI nPos [ nLongitud; |HACIENDO nPos = nPos + 1;
           nCaracter = (nPos * 100) + 1;
           aCaracter = aPath % nCaracter;
           |SI aCaracter = ",";
               nPos = nLongitud - nPos;
               nPos = nPos + 100;
               nPos = (-1) * nPos;
               aPath = aPath % nPos;
               |SAL;
          |FINSI;
     |SIGUE;

     aPath = aPath - "[";
     aPath = aPath - "]";
     |QBF aPath;
|FPRC;

|PROCESO BuscaConcepto;
     #nomcatli#0 = #labtraba#87;
     #nomcatli#1 = #labtraba#17;
     #nomcatli#2 = nConcepto;
     |LEE 000#nomcatli.=;
     |SI FSalida = 0;
          nValorCon = #nomcatli#4;
     |FINSI;

     #nomtrali#0 = #labtraba#0;
     #nomtrali#1 = #labtraba#1;
     #nomtrali#2 = nConcepto;
     |LEE 000#nomtrali.=;
     |SI FSalida = 0;
          nValorCon = #nomtrali#4;
     |FINSI;
|FPRC;

|PROCESO labtraba;
     swSigue = 0;

     #adade004#0 = #labtraba#87;
     |LEE 000#adade004.=;
     |SI FSalida = 0;
          swSigue = 1;
     |FINSI;

     |SI #labtraba#42 = "C"; |O #labtraba#42 = "F";
          |SI #0#9 = "C"; |O #0#9 = "T";
               swSigue = 1;
          |FINSI;
     |FINSI;

     |SI #labtraba#42 = "E";
          |SI #0#9 = "A"; |O #0#9 = "T";
               swSigue = 1;
          |FINSI;
     |FINSI;

     |SI swSigue = 0;
          |ACABA;
     |FINSI;

     nLinea = nLinea + 1;
/*
     aValor = #labtraba#0;
     |QBF aValor;
     aValor = ("00000" + aValor) % -105;
     aAlfa = "A" + nLinea + "," + aValor; |ALFACALLDLL "agixl97.dll","poke_dato", aAlfa;

     aValor = #labtraba#1;
     |QBF aValor;
     aValor = ("00000" + aValor) % -105;
     aAlfa = "B" + nLinea + "," + aValor; |ALFACALLDLL "agixl97.dll","poke_dato", aAlfa;
*/
     aValor = #labtraba#7;
     |QBF aValor;
     aAlfa = "C" + nLinea + "," + aValor; |ALFACALLDLL "agixl97.dll","poke_dato", aAlfa;

     aValor = #labtraba#2;
     |QBF aValor;
     aAlfa = "D" + nLinea + "," + aValor; |ALFACALLDLL "agixl97.dll","poke_dato", aAlfa;

     |SI #labtraba#42 ! "E";
          |ACABA;
     |FINSI;

     nValor = 0;

     |PARA nCampo = 1; |SI nCampo [ 15; |HACIENDO nCampo = nCampo + 1;
          nValorCon = 0;
          nConcepto = #adade004#nCampo#;
          |HAZ BuscaConcepto;
          nValor = nValor + nValorCon;
     |SIGUE;

     aAlfa = "AM" + nLinea + "," + nValor; |ALFACALLDLL "agixl97.dll","poke_dato", aAlfa;
|FPRC;

|DEFBUCLE labtraba;
     #labtraba#8;
     77;
     "A", #0#0, #0#4, #0#2;
     "A", #0#1, #0#5, #0#3;
     ;
     NULL, labtraba;
|FIN;

|PROCESO PintaMes; |TIPO 14;
     nMes = #0#6;
     nAny = #0#7;
     |HAZ MesLetra_plb;
     |ATRI I;
     |PINTA 1946, aMes;
|FPRC;

|PROCESO ElInforme;
     |DBASE aDirBase;    |QBF aDirBase;

     |CARGADLL "agixl97.dll";
     |SI FSalida < 0;
          |MENAV "0000No se puede usar la DLL de Excel.";
          |ACABA;
     |FINSI;

     |DBASE aBase;  |QBF aBase;
     aOrigen = aBase + "documentos/nominas_agrarios_citricos.xlsx";
     aDestinoTrab = "C:\DOCUMENTOS";         |RMKDIR aDestino;
     aDestino = aDestinoTrab + "\trabajo.xlsx";

     aIPRemoto = "";
     |IP_REMOTO aIPRemoto;  |QBT aIPRemoto;
     |SI aIPRemoto ! "";
          |ENVIA_FICHERO aOrigen, aDestino;
     |SINO;
          |COPIA_FICHERO aOrigen, aDestino;
     |FINSI;

     |ALFACALLDLL "agixl97.dll", "carga_book", aDestino;
     |ALFACALLDLL "agixl97.dll", "selecciona_hoja", "Hoja1";

     nLinea = 1;
     aValor = "RELACION MENSUAL NOMINAS AGRARIOS-CITRICOS";
     aAlfa = #0#7;
     |QBF aMes;
     aValor = aValor + " - " + aMes + " " + aAlfa;
     aAlfa = "A" + nLinea + "," + aValor; |ALFACALLDLL "agixl97.dll","poke_dato", aAlfa;

     nLinea = 3;
     |BUCLE labtraba;

     aAlfa = #0#7;
     |QBF aAlfa;
     aAlfa = aDestinoTrab + "\nominas_" + aMes + "_" + aAlfa + ".xlsx";
     |RFBORRA aAlfa;

     |ALFACALLDLL "agixl97.dll", "fichero_destino", aAlfa;

     |ALFACALLDLL "agixl97.dll", "fin_book", "salva";
     |DESCARGADLL "agixl97.dll";

     aDestino   = aDestinoTrab + "\trabajo.xlsx";
     |RFBORRA aDestino;

     |MENAV "    Se ha generado la hoja Excel en C:\DOCUMENTOS";
|FPRC;

|PROGRAMA;
     |CLS;
     |CABEZA "Generacion de hoja excel mensual";

     |ABRE #0;
     |LEE 001#0p;
     |SI FSalida ! 0;
          |DDEFECTO #0;
          |GRABA 020#0n;
     |FINSI;
     |LEE 101#0=;

     |PINPA #0#0;
     |PINDA #0#0;
     |ENDATOS #1#0;
     |SI FSalida = 0;
          |GRABA 020#0a;

          |DDEFECTO #adade004;
          |ABRE #adade004;

          |ABRE #labtraba;
          |ABRE #nomtrali;
          |ABRE #nomcatli;

          |HAZ ElInforme;

          |CIERRA #nomcatli;
          |CIERRA #nomtrali;
          |CIERRA #labtraba;
          |CIERRA #adade004;
     |FINSI;

     |LIBERA #0;
     |CIERRA #0;
|FPRO;
