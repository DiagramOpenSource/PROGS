|FICHEROS;
     silcon17 #0;
     silcon18;
     nomhicca;
     nomcalca;
     labtraba;
     labparma;
     labparhi;
     copcalca
     tempmant@ #200;
     labcentr;
     nomcenes;
     labempre;
     nomcaldl;
     nomcontr;
|FIN;

|VARIABLES;
     &nContador = 0;
     nCampo = 0;
     &nEmpExt = 0;
     &nTraExt = 0;
     &fFecExt = @;
     &fFecExtAlta = @;
     &aTipExt = "";
     &aCCC = "";
     aParam = "";
     i = 0;
     nume0 = 0;
     nume1 = 0;
     nume2 = 0;
     nume3 = 0;
     nume4 = 0;
     nBaseHE = 0;
     alfa1 = "";
     alfa2 = "";
     alfa3 = "";
     alfa4 = "";
     aAlfa = "";
     aAlfa1 = "";
     aAlfa2 = "";
     &aAccExt = "";
     aCambios = "";
     swNoGrabes = 0;
     nNume = 0;
     nMensaje = 0;
     &aDias = "";
     nDiasCot = 0;
     nBaseCot = 0;
     nBuscaMeses = 0;
     aEstoyEn = "";
     FEstado = 0;
     &nBaseRegul = 0;
     &aFecRecExt = "";
     &aRecExt = "";
     &promE_AUTO = 0;
     &nDiasCotExt = 0;
     nParte = 0;
     fFechaActual = @;
     nParteActual = 0;
     &nBaseHoras = 0;
     prom_ac2 = 0;
     nAny = 0;
     nBaseMat = 0;

 {-1}menu = "";
     menu1 = "2400";
     menu2 = "1";
     menu3 = "Opciones:";
     menu4 = "IFES";
     menu5 = " Imprimir ";
     menu6 = " Grabar Mov. FDI ";
     menu7 = " Enviar Online ";
     menu8 = " Salir ";  || no imprimo, no guardo

     swYaExiste = 0;

     nAnyXX = 0;
     nDiasNatAlta = 0;
     fech1 = @;
     fech2 = @;
     fech3 = @;
     fech4 = @;
     fech5 = @;
     fech6 = @;
     &nMes = 0;
     &nTopemes = 0;
     aNif = "";
     &swParteOnline = 0;
|FIN;

/******************** PROCESOS DESDE LA PANTALLA ****************************/

|PROCESO Previo; |TIPO 7;
     |HAZ AltaBaja;
|FPRC;

|PROCESO AltaBaja;
     |C_M #0#24, 1, "N";
     |C_V #0#24, 1, "N";
     |C_M #0#25, 1, "N";
     |C_V #0#25, 1, "N";
     |C_M #0#26, 1, "N";
     |C_V #0#26, 1, "N";
     |C_M #0#27, 1, "N";
     |C_V #0#27, 1, "N";
     |C_M #0#28, 1, "N";
     |C_V #0#28, 1, "N";
     |C_M #0#29, 1, "N";
     |C_V #0#29, 1, "N";
     |PINTA 2112, "                                     ";
     |PINTA 2212, "                                                             ";
     |SI #0#3 = "A";
          |C_M #0#22, 1, "S";
          |C_V #0#22, 1, "S";
          |C_M #0#23, 1, "S";
          |C_V #0#23, 1, "S";

          |C_M #0#5, 1, "N";
          |C_M #0#8, 1, "N";
          |C_M #0#9, 1, "N";
          |C_M #0#10, 1, "N";
          |C_V #0#5, 1, "N";
          |C_V #0#8, 1, "N";
          |C_V #0#9, 1, "N";
          |C_V #0#10, 1, "N";
          |BLANCO 14111572;

          |C_M #0#14, 1, "N";
          |C_M #0#15, 1, "N";
          |C_M #0#16, 1, "N";
          |C_M #0#17, 1, "N";
          |C_V #0#14, 1, "N";
          |C_V #0#15, 1, "N";
          |C_V #0#16, 1, "N";
          |C_V #0#17, 1, "N";
          |BLANCO 17112272;

          |PINTA 1244, "Fecha del Alta:";
          |PINTA 1344, "Causa del Alta:";
          |PINTA #0#22;
          |PINTA #0#23;
     |FINSI;
     |SI #0#3 = "B";
          |C_M #0#22, 1, "N";
          |C_V #0#22, 1, "N";
          |C_M #0#23, 1, "N";
          |C_V #0#23, 1, "N";
          |PINTA 1244, "                           ";
          |PINTA 1344, "                           ";

          |PINTA 1412, "Recaída ........:                                   ";
          |PINTA 1512, "Días prob. baja.:                                   ";
          |ATRI I;
          |PINTA 1712, "- Contratos a tiempo parcial/fijos discontinuos -   ";
          |ATRI 0;
          |PINTA 1812, "Suma bases cotización:          Suma días cotizados: ";
          |ATRI I;
          |PINTA 1912, "- Resto de contratos - ";
          |ATRI 0;
          |PINTA 2012, "Base de cotización ..:          Días cotizados mes :";

          |C_M #0#5, 1, "S";
          ||C_M #0#8, 1, "S";
          |C_M #0#9, 1, "S";
          ||C_M #0#10, 1, "S";
          |C_V #0#5, 1, "S";
          ||C_V #0#8, 1, "S";
          |C_V #0#9, 1, "S";
          ||C_V #0#10, 1, "S";
          |PINTA #0#5;
          ||PINTA #0#8;
          |PINTA #0#9;
          ||PINTA #0#10;

          |C_M #0#14, 1, "S";
          |C_M #0#15, 1, "S";
          |C_M #0#16, 1, "S";
          |C_M #0#17, 1, "S";
          |C_V #0#14, 1, "S";
          |C_V #0#15, 1, "S";
          |C_V #0#16, 1, "S";
          |C_V #0#17, 1, "S";
          |PINTA #0#14;
          |PINTA #0#15;
          |PINTA #0#16;
          |PINTA #0#17;
     |FINSI;
     |SI #0#3 = "C";
          |C_M #0#4, 1, "N";
          |C_M #0#5, 1, "N";
          |C_M #0#8, 1, "N";
          |C_M #0#9, 1, "N";
          |C_M #0#10, 1, "N";

          |C_V #0#5, 1, "N";
          |C_V #0#8, 1, "N";
          |C_V #0#9, 1, "N";
          |C_V #0#10, 1, "N";
          |BLANCO 14111572;

          |C_M #0#14, 1, "N";
          |C_M #0#15, 1, "N";
          |C_M #0#16, 1, "N";
          |C_M #0#17, 1, "N";
          |C_V #0#14, 1, "N";
          |C_V #0#15, 1, "N";
          |C_V #0#16, 1, "N";
          |C_V #0#17, 1, "N";
          |BLANCO 17112272;

          |PINTA 1712, "Fecha Parte Conf:"
          ||PINTA 1812, "Fecha Cambio Ent:"
          |PINTA 1744, "Nímero Parte..:"
          ||PINTA 1844, "Nueva Entidad.:"

          |C_P #0#26, 1, 1730;
          |C_V #0#26, 1, "S";
          |C_M #0#26, 1, "S";
          |PINTA #0#26;
          |C_P #0#27, 1, 1760;
          |C_V #0#27, 1, "S";
          |C_M #0#27, 1, "S";
          |PINTA #0#27;
          /*
          |C_P #0#28, 1, 1830;
          |C_V #0#28, 1, "S";
          |C_M #0#28, 1, "S";
          |PINTA #0#28;
          |C_P #0#29, 1, 1860;
          |C_V #0#29, 1, "S";
          |C_M #0#29, 1, "S";
          |PINTA #0#29;
          */
     |FINSI;
|FPRC;

|PROCESO Contingencia;
     |SI #0#3 = "B";
          |SI #0#4 = "3"; |O #0#4 = "4"; |O #0#4 = "5";
               |ATRI I;
               |PINTA 2112, "- Otras Cotizaciones A¤o Anterior -"
               |ATRI 0;
               |PINTA 2212, "Horas Extras ........:          Otros Conceptos ...:         ";
               |C_M #0#24, 1, "S";
               |C_V #0#24, 1, "S";
               |PINTA #0#24;
               |C_M #0#25, 1, "S";
               |C_V #0#25, 1, "S";
               |PINTA #0#25;

               |C_M #0#23, 1, "S";
               |C_V #0#23, 1, "S";
               |PINTA 1244, "Fecha AT/EP ..:";
               |PINTA #0#23;
          |SINO;
               |C_M #0#24, 1, "N";
               |C_V #0#24, 1, "N";
               |C_M #0#25, 1, "N";
               |C_V #0#25, 1, "N";
               |PINTA 2112, "                                     ";
               |PINTA 2212, "                                                             ";

               |C_M #0#23, 1, "N";
               |C_V #0#23, 1, "N";
               |PINTA 1244, "                           ";
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO atep; |TIPO 7;
     |MENSA "     Indique la fecha de Alta";
     |SI #0#3 = "B";
          |SI #0#4 = "3"; |O #0#4 = "4"; |O #0#4 = "5";
               |MENSA "     Indique la fecha para AT/EP";
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO PintaCont;
     |ATRI R;
     |SI #0#4 = 1;
          |PINTA 1333, "Enf. Com.";
     |FINSI;
     |SI #0#4 = 2;
          |PINTA 1333, "Acc.NoLab";
     |FINSI;
     |SI #0#4 = 3;
          |PINTA 1333, "Acc. Lab.";
     |FINSI;
     |SI #0#4 = 4;
          |PINTA 1333, "Enf.Prof.";
     |FINSI;
     |SI #0#4 = 5;
          |PINTA 1333, "Obs.Enf.P";
     |FINSI;
     |ATRI 0;
|FPRC;

|PROCESO PintaCont_7; |TIPO 7;
     |HAZ PintaCont;
|FPRC;

|PROCESO Restaura;
     |POPV;
|FPRC;

|PROCESO ayuda1; |TIPO 7;
     |PUSHV 14442380;
     |ATRI R;
     |PINTA 1444, "      -  Elija opción  -      ";
     |PINTA 1544, " 1. Enfermedad Común          ";
     |PINTA 1644, " 2. Accidente no Laboral      ";
     |PINTA 1744, " 3. Accidente de Trabajo      ";
     |PINTA 1844, " 4. Enfermedad Profesional    ";
     |PINTA 1944, " 5. Periodos de observación   ";
     |PINTA 2044, "    de enfermedad profesional ";
     |ATRI 0;
|FPRC;

|PROCESO DiasNatAlta;
     nDiasNatAlta = 0;

     alfa1 = "01";
     alfa2 = #200#2; alfa2 = "00" + alfa2; alfa2 = alfa2 % -102;
     nume3 = #200#66 + 2000;
     alfa3 = nume3;
     alfa4 = alfa1 + "." + alfa2 + "." + alfa3;
     fech1 = alfa4;      || PRIMER DIA DEL MES

     nMes = #200#2;
     nAny = #200#66 + 2000;
     |HAZ topemes_plb;

     alfa1 = nTopemes; alfa1 = "00" + alfa1; alfa1 = alfa1 % -102;
     alfa2 = #200#2; alfa2 = "00" + alfa2; alfa2 = alfa2 % -102;
     nume3 = #200#66 + 2000;
     alfa3 = nume3;
     alfa4 = alfa1 + "." + alfa2 + "." + alfa3;
     fech2 = alfa4;      || ULTIMO DIA DEL MES

     alfa1 = #200#4;
     fech3 = alfa1;      || DIA ALTA TRABAJADOR

     alfa1 = #200#5;
     |QBF alfa1;
     |SI alfa1 ! ""; |Y alfa1 ! "..........";
          fech4 = alfa1;      || DIA BAJA TRABAJADOR
     |SINO;
          fech4 = fech2;
     |FINSI;

     |SI fech3 ] fech1;
          fech5 = fech3;
     |SINO;
          fech5 = fech1;
     |FINSI;

     |SI fech4 [ fech2;
          fech6 = fech4;
     |SINO;
          fech6 = fech2;
     |FINSI;

     nume1 = fech5;
     nume2 = fech6;

     nDiasNatAlta = nume2 - nume1 + 1;
|FPRC;

|PROCESO labtraba;
     #200#0 = #labtraba#0;
     #200#1 = #labtraba#1;
     #200#66 = nAnyXX;     || a¤o
     #200#2 = nMes;     || mes
     #200#3 = 0;         || tipo
     |LEE 000#200=;
     |SI FSalida = 0;
          nBaseHE = nBaseHE + #200#41 + #200#42;
                                             || pongo la de comunes por seguir
          nBaseCot = nBaseCot + #200#39;     || y despues sumo la parte de horas
          |HAZ DiasNatAlta;
          |SI #labtraba#42 = "X";
               nDiasCot = nDiasCot + nDiasNatAlta;
          |SINO;
               nDiasCot = nDiasCot + nDiasNatAlta
          |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE labtraba;
     #labtraba#5;
     ;
     nEmpExt, aNif;
     nEmpExt, aNif;
     ;
     NULL, labtraba;
|FIN;

|PROCESO suma;
     aNif = #labtraba#7;

     |SALVA_FICHA #labtraba;

     nBaseHE = 0;
     nBaseCot = 0;
     nDiasCot = 0;
     nMes = nume1;
     nAnyXX = nume4;
     |PARA i = 1; |SI i [ nBuscaMeses; |HACIENDO i = i + 1;

          |BUCLE labtraba;

          |SI nMes < 12;
               nMes = nMes + 1;
          |SINO;
               nMes = 1;
               nAnyXX = nAnyXX + 1;
          |FINSI;
     |SIGUE;

     |REPON_FICHA #labtraba;
|FPRC;

|PROCESO nomcaldl;
     |SI #nomcaldl#5 = 041000; |O #nomcaldl#5 = 051000; |O #nomcaldl#5 = 061000;
          nBaseMat = nBaseMat + #nomcaldl#10;
     |FINSI;
|FPRC;

|DEFBUCLE nomcaldl;
     #nomcaldl#1;
     ;
     #0#0, #0#1, nAny, nume2, 0, 041000;
     #0#0, #0#1, nAny, nume2, 0, 062999;
     ;
     NULL, nomcaldl;
|FIN;

|PROCESO lineal;
     nBaseMat = 0;
     nAny = nume3 + 2000;
     |BUCLE nomcaldl;
|FPRC;

|PROCESO BuscaBases;
     |ABRE #200;
     #200#0 = #0#0;
     #200#1 = #0#1;
     #200#2 = nume2;
     #200#3 = 0;
     |SI aEstoyEn = "H";
          #200#66 = nume3;
     |FINSI;
     |LEE 000#200=;
     |SI FSalida = 0;
          |SI #labtraba#42 ! "T"; |Y #labtraba#42 ! "X";
/*
               || esto no es correcto ya que si tiene horas extras esta teniendo
               || en cuenta la base de horas extras junto con la base de contin-
               || gencias, por lo que se esta duplicando, lo dejo para todos que
               || lo coja del #200#39, tiquet 3006.699
               |SI aTipExt = "A";
                    #0#16 = #200#40;
               |SINO;
                    #0#16 = #200#39;
               |FINSI;
*/
               #0#16 = #200#39;
               #0#14 = #200#8;
               |SI #200#65 ! 0;
                    || busco en el nomcaldl, que ahi tendre las bases
                    || como son de verdad, ajustadas a 30 dias en caso de mensuales

                    |HAZ lineal;
                    #0#16 = #0#16 + nBaseMat;

                    || #0#16 = #0#16 + (#200#65 * #200#35);
               |FINSI;
          |SINO;
               nume1 = nume2 - 2;  || mes desde (3 meses anteriores)
               nume4 = nume3;  || a¤o desde (3 meses anteriores)
               |SI nume1 < 1;
                    nume1 = 12 + nume1;
                    nume4 = nume4 - 1;
               |FINSI;
               nBuscaMeses = 3;
               |HAZ suma;
               #0#17 = nBaseCot;
               #0#15 = nDiasCot;
          |FINSI;
     |SINO;
          FEstado = FSalida;
          |CIERRA #200;
          |ACABA;
     |FINSI;

     nume1 = nume2 + 1;  || mes desde (12 meses anteriores)
     nume4 = nume3 - 1;  || a¤o desde (12 meses anteriores)
     |SI nume1 = 13;
          nume1 = 1;
          nume4 = nume4 + 1;
     |FINSI;
     nBuscaMeses = 12;

     |HAZ suma;
     |SI #0#3 = "B";
          |SI #0#4 = "3"; |O #0#4 = "4"; |O #0#5 = "5";
               #0#24 = nBaseHE;
          |FINSI;
     |SINO;
          #0#24 = 0;
     |FINSI;
     |SI #labtraba#42 ! "T"; |Y #labtraba#42 ! "X";
          |PINTA #0#14;
          |PINTA #0#16;
     |SINO;
          |PINTA #0#15;
          |PINTA #0#17;
     |FINSI;
     |PINTA #0#24;
     |CIERRA #200;
|FPRC;

|PROCESO LeeBases; |TIPO 7;
     |SI aParam = "MODIF";
          |ACABA;
     |FINSI;
     |SI aParam = "NOMVARCA"; |Y promE_AUTO ! 0;
          |SI #labtraba#42 ! "T"; |Y #labtraba#42 ! "X";
               #0#16 = promE_AUTO * nDiasCotExt;
               #0#14 = nDiasCotExt;
          |SINO;
               #0#17 = promE_AUTO * nDiasCotExt;
               #0#15 = nDiasCotExt;
          |FINSI;
          |ACABA;
     |FINSI;
     |SI aRecExt ! "S";
          alfa1 = #0#7;            || fecha accidente
          alfa2 = alfa1 % 402;     || mes accidente
          alfa3 = alfa1 % 704;     || a¤o accidente
          nume2 = alfa2;
          nume3 = alfa3;
          nume2 = nume2 - 1;
     |SINO;
          alfa1 = aFecRecExt
          alfa2 = alfa1 % 402;
          alfa3 = alfa1 % -104;
          nume2 = alfa2;      || si es recaida no tengo que buscar la base
                              || del mes anterior, sino la del propio mes
          nume3 = alfa3;      || de le enfermedad inicial

          nume2 = nume2 - 1;  || bueno pues segun nos dice Dany tengo que buscar
                              || la del mes anterior a la recaida, claro
                              || la verdad es que es mas logico
     |FINSI;
     |SI nume2 = 0;           || si es enero, el mes anterior es Diciembre
          nume2 = 12;         || del a¤o anterior
          nume3 = nume3 - 1;
     |FINSI;
     nume3 = nume3 - 2000;    || en nume3 el a¤o

     |DBASE aAlfa; |QBF aAlfa; aAlfa = aAlfa + "def/nomhicca.mas";
     |CARGA_DEF aAlfa, tempmant@;
     |SI FSalida = 0;
          aEstoyEn = "H";
          |HAZ BuscaBases;
     |SINO;
          aAlfa = "    ATENCION: Error en CARGA_DEF: " + aAlfa;
          |MENAV aAlfa;
     |FINSI;

     |DESCARGA_DEF #tempmant@;

     |SI FEstado ! 0;    || no he encontrado nada en Hco, busco mensual
          |DBASE aAlfa; |QBF aAlfa; aAlfa = aAlfa + "def/nomcalca.mas";
          |CARGA_DEF aAlfa, tempmant@;
          |SI FSalida = 0;
               aEstoyEn = "M";
               |HAZ BuscaBases;
          |SINO;
               aAlfa = "    ATENCION: Error en CARGA_DEF: " + aAlfa;
               |MENAV aAlfa;
          |FINSI;

          |DESCARGA_DEF #tempmant@;
     |FINSI;
|FPRC;

|PROCESO BuscaDupli_F;
     |SI #silcon17#3 ! "C";
          |ACABA;
     |FINSI;

     |SI #0#26 = fFechaActual;     || debo permitir volver a poner la misma
          |ACABA;                  || fecha del parte que esoy editando
     |FINSI;

     |SELECT #5#silcon18;
     #silcon18#0 = #silcon17#0;
     #silcon18#1 = #silcon17#1;
     #silcon18#7 = #silcon17#7;
     #silcon18#3 = #silcon17#3;
     #silcon18#26 = #silcon17#26;
     |LEE 000#silcon18.=;
     |SI FSalida = 0;
          aAlfa1 = #silcon18#27;
          aAlfa = "    ATENCION: ya existe un parte de confirmación en esta fecha:";
          aAlfa = aAlfa + " parte nímero " + aAlfa1;
          |MENAV aAlfa;
          aAlfa1 = #silcon18#2; aAlfa1 = "000000" + aAlfa1; aAlfa1 = aAlfa1 % -106;
          |SI #silcon18#20 = "-";
               aAlfa2 = "PENDIENTES";
          |SINO;
               aAlfa2 = "ENVIADOS";
          |FINSI;
          aAlfa = "    Consulte los movimientos FDI " + aAlfa2 + ". ";
          aAlfa = aAlfa + "Linea " + aAlfa1;
          |MENAV aAlfa;
          |ERROR;
     |FINSI;

     |SELECT #1#silcon18;
|FPRC;

|PROCESO BuscaDupli_C;
     |SI #silcon17#3 ! "C";
          |ACABA;
     |FINSI;

     |SI #0#27 = nParteActual;     || debo permitir poner el mismo numero de
          |ACABA;                  || parte que el de el que estoy editando
     |FINSI;

     |SELECT #4#silcon18;
     #silcon18#0 = #silcon17#0;
     #silcon18#1 = #silcon17#1;
     #silcon18#7 = #silcon17#7;
     #silcon18#3 = #silcon17#3;
     #silcon18#27 = #silcon17#27;
     |LEE 000#silcon18.=;
     |SI FSalida = 0;
          aAlfa = #silcon18#27;
          aAlfa = "    ATENCION: el parte de confirmación nímero " + aAlfa;
          aAlfa = aAlfa + " ya está creado para esta IT";
          |MENAV aAlfa;
          aAlfa1 = #silcon18#2; aAlfa1 = "000000" + aAlfa1; aAlfa1 = aAlfa1 % -106;
          |SI #silcon18#20 = "-";
               aAlfa2 = "PENDIENTES";
          |SINO;
               aAlfa2 = "ENVIADOS";
          |FINSI;
          aAlfa = "    Consulte los movimientos FDI " + aAlfa2 + ". ";
          aAlfa = aAlfa + "Linea " + aAlfa1;
          |MENAV aAlfa;
          |ERROR;
     |FINSI;

     |SELECT #1#silcon18;
|FPRC;

|PROCESO BuscaUltimo_C;
     |SI aParam = "MODIF"; |O #silcon17#3 ! "C";
          |ACABA;
     |FINSI;
     |SELECT #4#silcon18;
     FSalida = 0;
     |PARA nParte = 1; |SI FSalida = 0; |HACIENDO nParte = nParte + 1;
          #silcon18#0 = #silcon17#0;
          #silcon18#1 = #silcon17#1;
          #silcon18#7 = #silcon17#7;
          #silcon18#3 = #silcon17#3;
          #silcon18#27 = nParte;
          |LEE 000#silcon18.=;
     |SIGUE;
     #0#27 = nParte - 1;
     |PINTA #0#27;

     |SELECT #1#silcon18;
|FPRC;

|PROCESO BuscaDupli;
     |SI aParam = "MODIF"; |O #silcon17#3 = "C"; |O aParam = "NOMVARCA";
          |ACABA;
     |FINSI;
     |SELECT #2#silcon18;
     #silcon18#0 = #silcon17#0;
     #silcon18#1 = #silcon17#1;
     #silcon18#7 = #silcon17#7;
     #silcon18#3 = #silcon17#3;
     |LEE 000#silcon18.=
     |SI FSalida = 0;
          aAlfa = "    ATENCION: ya se ha creado un parte en esta fecha ";
          aAlfa = aAlfa + "para la acción '" + #silcon17#3 + "'.";
          |MENAV aAlfa;
          aAlfa1 = #silcon18#2; aAlfa1 = "000000" + aAlfa1; aAlfa1 = aAlfa1 % -106;
          |SI #silcon18#20 = "-";
               aAlfa2 = "PENDIENTES";
          |SINO;
               aAlfa2 = "ENVIADOS";
          |FINSI;
          aAlfa = "    Consulte los movimientos FDI " + aAlfa2 + ". ";
          aAlfa = aAlfa + "Linea " + aAlfa1;
          |MENAV aAlfa;
          |ERROR;
     |FINSI;
     |SELECT #1#silcon18;
|FPRC;

|PROCESO BuscaBaja;
     |SI aParam = "MODIF"; |O #0#3 ! "C";
          |ACABA;
     |FINSI;
     |SELECT #2#silcon18;
     #silcon18#0 = #silcon17#0;
     #silcon18#1 = #silcon17#1;
     #silcon18#7 = #silcon17#7;
     #silcon18#3 = "B";
     |LEE 000#silcon18.=
     |SI FSalida ! 0;
          aAlfa = "    ATENCION: no se ha encontrado el parte de baja asociado ";
          aAlfa = aAlfa + "en esta fecha. Por favor, seleccione uno:";
          |MENAV aAlfa;
          FSalida = 13;
          |HAZ Consulta;
          |POSICIONATE #0#7;
     |SINO;
          #0#4 = #silcon18#4;
          #0#11 = #silcon18#11;
          #0#12 = #silcon18#12;
          #0#13 = #silcon18#13;
          |PINTA #0#4;
          |HAZ PintaCont;
          |PINTA #0#11;
          |PINTA #0#12;
          |PINTA #0#13;
          ||POSICIONATE #0#26;
     |FINSI;
     |SELECT #1#silcon18;
|FPRC;

|PROCESO Min;
     #silcon18#0 = #0#0;
     #silcon18#1 = #0#1;
     #silcon18#7 = "01.01.1999";
     #silcon18#3 = "B";
|FPRC;

|PROCESO Max;
     #silcon18#0 = #0#0;
     #silcon18#1 = #0#1;
     #silcon18#7 = "31.12.2999";
     #silcon18#3 = "B";
|FPRC;

|PROCESO Consulta;
     |SI FSalida = 13;
          |ABRE #silcon18;
          |CONSULTA_F_CLAVE #2#silcon18, Min, Max;
          |SI FSalida = 0;
               #0#7 = #silcon18#7;
               |PINTA #0Campo;
          |SINO;
               |ERROR;
          |FINSI;
          |CIERRA #silcon18;
     |FINSI;
|FPRC;

/******************** FIN PROCESOS DESDE LA PANTALLA ************************/


|PROCESO contador;
     |SELECT #3#silcon18;
     |LEE 000#silcon18.u;
     |SI FSalida = 0;
          nContador = #silcon18#2;
          nContador = nContador + 1;
     |SINO;
          nContador = 1;
     |FINSI;
|FPRC;

|PROCESO FechaAltaoATEP;
     #silcon18#23 = 0;
     |SI #silcon17#3 = "A";
          #silcon18#23 = #silcon17#23;
     |FINSI;
     |SI #silcon17#3 = "B";
          |SI #silcon17#4 = "3"; |O #silcon17#4 = "4"; |O #silcon17#4 = "5";
               #silcon18#23 = #silcon17#23;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO CargaModif;
     |SELECT #2#silcon18;
     #silcon18#0 = nEmpExt;
     #silcon18#1 = nTraExt;
     #silcon18#7 = fFecExt;
     #silcon18#3 = aAccExt;
     |LEE 000#silcon18.=;
     |PARA nCampo = 3; |SI nCampo [ 17; |HACIENDO nCampo = nCampo + 1;
          #silcon17#nCampo# = #silcon18#nCampo#;
     |SIGUE;
     #silcon17#0 = #silcon18#0;
     #silcon17#1 = #silcon18#1;
     |PARA nCampo = 22; |SI nCampo [ 31; |HACIENDO nCampo = nCampo + 1;
          #silcon17#nCampo# = #silcon18#nCampo#;
     |SIGUE;
|FPRC;


|PROCESO CambioA;
     aAlfa = Contenido;
     |SI aAlfa ! #0Campo;
          aAlfa = Campo; aAlfa = "000" + aAlfa; aAlfa = aAlfa % -103;
          aAlfa = "|C" + aAlfa + "NOMBRE"; aAlfa = #silcon17#aAlfa#;
          |QBF aAlfa;
          aCambios = aCambios + ", " + aAlfa;
     |FINSI;
|FPRC;

|PROCESO CambioN;
     nNume = Contenido;
     |SI nNume ! #0Campo;
          aAlfa = Campo; aAlfa = "000" + aAlfa; aAlfa = aAlfa % -103;
          aAlfa = "|C" + aAlfa + "NOMBRE"; aAlfa = #silcon17#aAlfa#;
          |QBF aAlfa;
          aCambios = aCambios + ", " + aAlfa;
     |FINSI;
|FPRC;

|PROCESO mensajes;
     |SI nMensaje = 1;
          aAlfa = "    ATENCION. Ya se ha enviado un parte de accidente por el ";
          aAlfa = aAlfa + "sistema Delt@.";
          |MENAV aAlfa;
          aAlfa ="    Deberá dejar el parte como PENDIENTE para poder modificarlo"
          aAlfa1 = #labparma#0; aAlfa1 = "000000" + aAlfa1; aAlfa1 = aAlfa1 % -106;
          aAlfa = "Consulte movimientos Delta PENDIENTES: linea " + aAlfa1;
          |MENAV aAlfa;
     |FINSI;
     |SI nMensaje = 2;
          aCambios = aCambios - ", ";
          aAlfa = "    Campos comunes modificados: ";
          aAlfa = aAlfa + aCambios;
          |MENAV aAlfa;
     |FINSI;
     |SI nMensaje = 3;
          aAlfa = "    ATENCION: Parte no encontrado";
          |MENAV aAlfa;
     |FINSI;
     |SI nMensaje = 4;
          aAlfa = "    Modificado el parte de accidente asociado en Delt@. ";
          aAlfa1 = #labparma#0; aAlfa1 = "000000" + aAlfa1; aAlfa1 = aAlfa1 % -106;
          aAlfa = aAlfa + "Movimientos Delt@ PENDIENTES: linea " + aAlfa1;
          |MENAV aAlfa;
     |FINSI;
|FPRC;

|PROCESO comprobaciones;
     swNoGrabes = 0;
     |ABRE #labparma;
     |ABRE #labparhi;
     #labparma#1 = #silcon17#0;    || empresa
     #labparma#2 = #silcon17#1;    || trabajador
     #labparma#3 = #silcon17#7;    || fecha de baja
     |LEE 000#labparma.=;
     |SI FSalida = 0;              || el parte de accidente existe
          |SI #labparma#6 = "*";    || y encima esta ya emitido
               |QBF aCambios;
               |SI aCambios ! "";
                    nMensaje = 1;
                    |HAZ mensajes;
                    nMensaje = 2;
                    |HAZ mensajes;
                    swNoGrabes = 1;
               |FINSI;
          |SINO;
               |SI aCambios ! "";
                    #labparhi#1 = #silcon17#0;    || empresa
                    #labparhi#2 = #silcon17#1;    || trabajador
                    #labparhi#15 = #silcon17#7;    || fecha de baja
                    |LEE 101#labparhi.=;
                    |SI FSalida = 0;
                         |SI #silcon17#5 = "S";        || recaida S/N
                              #labparhi#40 = "R";
                         |SINO;
                              #labparhi#40 = "A";
                         |FINSI;
                         #labparhi#37 = #silcon17#14;  || dias cotizados mes
                         #labparhi#36 = #silcon17#16;  || base cotizacion mes anterior
                         #labparhi#38 = #silcon17#24;  || base cotizacion horas extras
                         #labparhi#39 = #silcon17#25;  || base cotizacion otros
                         |GRABA 020#labparhi.a;
                         nMensaje = 4;
                         |HAZ mensajes;
                         nMensaje = 2;
                         |HAZ mensajes;
                    |SINO;
                         nMensaje = 3;
                         |HAZ mensajes;
                         nMensaje = 2;
                         |HAZ mensajes;
                    |FINSI;
               |FINSI;
          |FINSI;
     |FINSI;

     |CIERRA #labparma;
     |CIERRA #labparhi;
|FPRC;

|PROCESO GrabaModif;
     |HAZ comprobaciones;
     |SI swNoGrabes = 0;
          |PARA nCampo = 3; |SI nCampo [ 17; |HACIENDO nCampo = nCampo + 1;
               #silcon18#nCampo# = #silcon17#nCampo#;
          |SIGUE;
          |PARA nCampo = 22; |SI nCampo [ 31; |HACIENDO nCampo = nCampo + 1;
               #silcon18#nCampo# = #silcon17#nCampo#;
          |SIGUE;
          |HAZ FechaAltaoATEP;
          |GRABA 020#silcon18.a;
          |LIBERA #silcon18;
     |FINSI;
|FPRC;

|PROCESO calproac;
     nume1 = 0;      || acumulador de nro. de meses
     nume2 = 0;      || acumulador de bases cotizadas
     prom_ac2 = 0;   || promedio accid.

     |ABRE #labempre;
     #labempre#0 = #labtraba#0;
     |LEE 000#labempre.=;
     |CIERRA #labempre;

     |SI #labempre#40 = "S";
          prom_ac2 = (nBaseHE / nDiasCot) ! 2;
     |FINSI;
|FPRC;

|PROCESO VariablesImp;
     |SI #labtraba#42 = "X"; |O #labtraba#42 = "T";
          |SI aTipExt = "M"; |O aTipExt = "P"; |O aTipExt = "R";
               aDias = "doce";
          |SINO;
               aDias = "tres";
          |FINSI;
     |FINSI;
     || nBaseRegul = #labtraba#95;
     |SI #labtraba#42 = "X"; |O #labtraba#42 = "T";
          nBaseRegul = (#0#17 / #0#15) ! 2;
     |SINO;
          nBaseRegul = (#0#16 / #0#14) ! 2;
     |FINSI;
     |SI aTipExt = "A";
          |HAZ calproac;
          nBaseRegul = nBaseRegul + prom_ac2;
     |FINSI;
     |SI aAccExt = "A";
          aAccExt = "ALTA";
     |SINO;
          aAccExt = "BAJA";
     |FINSI;

     |ABRE #labcentr;
     #labcentr#0 = #labtraba#0;
     #labcentr#1 = #labtraba#77;
     |LEE 000#labcentr.=;
     |SI FSalida = 0;
          aCCC = #labcentr#10;
     |FINSI;
     |CIERRA #labcentr;

     nBaseHoras = #silcon17#24;

     |SI #labtraba#45 = "421";
          |ABRE #nomcenes;
          #nomcenes#0 = #labtraba#0;    || empresa
          #nomcenes#13 = 1;             || codigo centro (01 por defecto)
          #nomcenes#1 = 87;

          |LEE 000#nomcenes.=;
          |SI FSalida = 0;
               aCCC = #nomcenes#10;
          |FINSI;
          |CIERRA #nomcenes;
     |FINSI;
|FPRC;

|PROCESO ModifBases;
     |SI #0Campo ! Contenido; |Y aParam = "NOMVARCA"; |Y aAccExt = "B";
          #0#15 = 0;          || inicializo bases
          #0#16 = 0;
          #0#17 = 0;
          #0#24 = 0;
          #0#25 = 0;
          |PINTA #0#15;
          |PINTA #0#16;
          |PINTA #0#17;
          |PINTA #0#24;
          |PINTA #0#25;
          |HAZ LeeBases;
     |FINSI;
|FPRC;

|PROCESO GrabaParte;
     |SI swYaExiste = 0;
          || primero cojo el contador que me tocaria,
          || para crear el parte nuevo
          |HAZ contador;

          |SELECT #1#silcon18;
          |PARA nCampo = 0; |SI nCampo [ 17; |HACIENDO nCampo = nCampo + 1;
               #silcon18#nCampo# = #silcon17#nCampo#;
          |SIGUE;
          #silcon18#2= nContador;
          #silcon18#6 = ~;
          #silcon18#18 = "";
          #silcon18#19 = "";
          #silcon18#20 = "-";
          #silcon18#21 = " ";
          |PARA nCampo = 22; |SI nCampo [ 31; |HACIENDO nCampo = nCampo + 1;
               #silcon18#nCampo# = #silcon17#nCampo#;
          |SIGUE;
          |HAZ FechaAltaoATEP;
          |GRABA 020#silcon18.n;
          |LIBERA #silcon18;

          aAlfa1 = nContador;
          |QBF aAlfa1;
          aAlfa1 = ("000000" + aAlfa1) % -106;
          aAlfa = "    Se ha creado el parte de IT con el número de ";
          aAlfa = aAlfa + "movimiento " + aAlfa1;
          |MENAV aAlfa;
          swYaExiste = 1;
     |SINO;
          || estoy regrabando un parte, desde variaciones
          || o entrando desde la pantalla directamente

          |SI #silcon18#20 = "*";
               aAlfa = "    ATENCION. El parta ya ha sido enviado. ";
               aAlfa = aAlfa + "Si se han modificado los datos, no se actualizará";
               |MENAV aAlfa;
               |ACABA;
          |FINSI;

          |SELECT #1#silcon18;

          #silcon18#20 = "-";  || es el contador, me quedo lo que tenia
          #silcon18#2 = nContador;  || es el contador, me quedo lo que tenia
          |LEE 101#silcon18.=;

          #silcon18#0 = #silcon17#0;
          #silcon18#1 = #silcon17#1;
          #silcon18#2 = nContador;  || es el contador, me quedo lo que tenia
          |PARA nCampo = 3; |SI nCampo [ 17; |HACIENDO nCampo = nCampo + 1;
               |SI nCampo ! 6;
                    || el #6 es la fecha de creacion del parte
                    #silcon18#nCampo# = #silcon17#nCampo#;
               |FINSI;
          |SIGUE;
          |PARA nCampo = 22; |SI nCampo [ 31; |HACIENDO nCampo = nCampo + 1;
               #silcon18#nCampo# = #silcon17#nCampo#;
          |SIGUE;
          |HAZ FechaAltaoATEP;
          |GRABA 020#silcon18.a;
          |LIBERA #silcon18;

          aAlfa1 = nContador;
          |QBF aAlfa1;
          aAlfa1 = ("000000" + aAlfa1) % -106;
          aAlfa = "    Actualizado el parte de IT con el número de ";
          aAlfa = aAlfa + "movimiento " + aAlfa1;
          |MENAV aAlfa;
          swYaExiste = 1;
     |FINSI;
|FPRC;

|PROCESO Movimiento;
     |HAZ GrabaParte;
|FPRC;

|PROCESO Informe;
     |HAZ VariablesImp;
     |IMPRE 0;
     |SI FSalida = 0;
          |INFOR "silcon17";
          |PRINT;
          |PIEINF;
          |FININF;
     |FINSI;
     |FINIMP;
|FPRC;

|PROCESO ParteNuevo;
     #silcon17#0 = nEmpExt;
     #silcon17#1 = nTraExt;
     #silcon17#7 = fFecExt;
     #silcon17#3 = aAccExt;
     |SI aTipExt = "E"; |O aTipExt = "M"; |O aTipExt = "P"; |O aTipExt = "R";
          #silcon17#4 = 1;
     |FINSI;
     |SI aTipExt = "A";
          #silcon17#4 = 3;
     |FINSI;
     |SI aAccExt = "A";
          #silcon17#23 = fFecExtAlta;
     |FINSI;
     |HAZ LeeBases;
|FPRC;

|PROCESO CompruebaParte;
     || busco si el parte que voy a crear ya existia

     |DDEFECTO #silcon18;
     |SELECT #4#silcon18;
     #silcon18#0 = nEmpExt;
     #silcon18#1 = nTraExt;
     #silcon18#7 = fFecExt;
     #silcon18#3 = aAccExt;
     #silcon18#27 = 00;
     |LEE 000#silcon18.=;
     |SI FSalida = 0;
          || si el parte existe, presento los datos del parte que ya existia
          swYaExiste = 1;

          || me guardo el contador para luego:
          nContador = #silcon18#2;

          || presento el resto de campos:
          |PARA nCampo = 0; |SI nCampo [ 31; |HACIENDO nCampo = nCampo + 1;
               |SI nCampo ! 18; |Y nCampo ! 19; |Y nCampo ! 20; |Y nCampo ! 21;
                    #silcon17#nCampo# = #silcon18#nCampo#;
               |FINSI;
          |SIGUE;
     |FINSI;
|FPRC;

|PROGRAMA;
     aParam = PARAMETRO;
     |QBF aParam;
     |CLS;
     |PINPA #0#0;
     |ABRE #silcon18;
     |SI aParam = "MODIF";
          |HAZ CargaModif;
          fFechaActual = #0#26;
          nParteActual = #0#27;
          |PINDA #0#0;
          |CAMPO_MODIFICA #0#0, 1, "N";
          |CAMPO_MODIFICA #0#1, 1, "N";
          |CAMPO_MODIFICA #0#7, 1, "N";
          |CAMPO_MODIFICA #0#3, 1, "N";
          |ENDATOS #1#0;
          |SI FSalida = 0;
               |HAZ GrabaModif;
               |CONFI "    S¿Desea enviar el movimiento Online?"
               |SI FSalida = 0;
                    nContador = #silcon18#2;
                    |HAZ ConexionConectaSS;
               |FINSI;
          |FINSI;
     |FINSI;

     |SI aParam = "";
          |PINDA #0#0;
          |ENDATOS #1#0;
          |SI FSalida = 0;
               swYaExiste = 0;
               |HAZ GrabaParte;
               |CONFI "    S¿Desea enviar el movimiento Online?"
               |SI FSalida = 0;
                    |HAZ ConexionConectaSS;
               |FINSI;
          |FINSI;
     |FINSI;

     |SI aParam = "NOMVARCA";
          |ABRE #labtraba;
          #labtraba#0 = nEmpExt;
          #labtraba#1 = nTraExt;
          |LEE 000#labtraba.=;
          |CIERRA #labtraba;
          |SI aAccExt = "A";
               |ATRI R;
               |PINTA 1055, " PARTE DE ALTA ";
               |ATRI 0;
               |HAZ PintaCont;
          |FINSI;
          |SI aAccExt = "B";
               |ATRI R;
               |PINTA 1055, " PARTE DE BAJA ";
               |ATRI 0;
               |HAZ PintaCont;
          |FINSI;

          swYaExiste = 0;
          |HAZ CompruebaParte;

          |SI swYaExiste = 0;
               |HAZ ParteNuevo;
          |FINSI;
          |HAZ AltaBaja;

          |SI swYaExiste = 1;
               aAlfa1 = #silcon18#2;
               |QBF aAlfa1;
               aAlfa1 = ("000000" + aAlfa1) % -106;

               |SI #silcon18#20 = "*";
                    |ATRI S;
                    aAlfa = "MOVIMIENTO ENVIADO: " + aAlfa1;
                    |PINTA 1147, aAlfa;
                    |ATRI 0;
               |SINO;
                    |ATRI S;
                    aAlfa = "MOVIMIENTO PENDIENTE: " + aAlfa1;
                    |PINTA 1145, aAlfa;
                    |ATRI 0;
               |FINSI;
          |FINSI;

          |CAMPO_MODIFICA #0#0, 1, "N";
          |CAMPO_MODIFICA #0#1, 1, "N";
          |CAMPO_MODIFICA #0#3, 1, "N";

          |PINDA #0#0;
          |ENDATOS #1#0;

          |SI FSalida = 0;
               |PARA; |SI; |HACIENDO;
                    |MENU menu;
                    |SI FSalida = 0; |O FSalida = 4
                         |SAL;
                    |FINSI;
                    |SI FSalida = 1;
                         |HAZ Informe;
                    |FINSI;
                    |SI FSalida = 2;
                         |HAZ Movimiento;
                    |FINSI;
                    |SI FSalida = 3;
                         |HAZ Movimiento;
                         |HAZ ConexionConectaSS;
                    |FINSI;
               |SIGUE;
          |FINSI;
     |FINSI;
     |CIERRA #silcon18;
|FPRO;
