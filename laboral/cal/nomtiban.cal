|FICHEROS;
     nomtiban #0;
     labtraba #2;         || trabajadores
     nomdator #11;        || bancos soporte magnetico
     nomrevca #12;        || transfer.cabs.
     nomrevli #13;        || transfer.lins.
     nompagli;
     nomcontr;
|FIN;

|VARIABLES;
    seleccio = 0;
    swpri = 0;
    p_mes = "";
    fecsys = @;
    fecalf = "";
    alfa1 = "";
    alfa2 = "";
    alfa3 = "";
    alfa4 = "";
    alfa5 = "";
    alfa6 = "";
    alfa7 = "";
    alfa8 = "";
    alfa9 = "";
    traba = "";
    nume1 = 0;
    nume2 = 0;
    nume3 = 0;
    nume4 = 0;
    fech1 = @;
    fech2 = @;
    fech3 = @;
    fech4 = @;
    empresa = "";
    fiche = "";
    canal = 0;
    espacio = " ";
    longitud = "";
    treintaiseis = "";
    importes = 0;
    registro = 0;
    regis_total = 0;
    contenido = "";
    secuenci = "";
    unida = "";
    sistem = "";
    dire = "";
    fichero1 = "";
    fichero2 = "";
    fichero3="";
    dreg = 0;
    hreg = 0;
    demp = 0;
    hemp = 0;
    para1 = 0;
    para2 = 0;
    aEl0306 = "";
    aEl0606 = "";
    aEl0806 = "";
    aMonDisco = "";
    &eaMoneda = "";
    &enCanti = 0;
    aIP = "";
     aDesdeReg = "";
     aHastaReg = "";
     aAlfa = "";
     aAlfa1 = "";
     aAlfa2 = "";
     &swModulo = 0;
     aTipo = "";
     aUsuario = "";
     aDato = "";

     nCanal = 0;
     aRelleno = "";
     aRellenoAlfa = " ";
     aRellenoCero = "0";
     nLongitud = 0;
     aTipoIngreso = "";
     nNume = 0;
     aRuta = "";

     aAlfaRar     = "";
     aAlfaRar1    = "";
     aAlfaRar2    = "";
     aLongRar     = "";

     CalcRar      = 0;
     nContRar     = 0;
     nContRar1    = 0;
     CalcRar1     = 0;
     nLongRar     = 0;
     nTecla       = 0;
     aCadena = "";

     nTTotal     = 0;
     nTRegistros = 0;
     nOpe        = 0;
     nTXML       = 0;
     nBueno      = 0;
     &eaOrigen   = "";
     &eaDestino  = "";
     &eaNombre1  = "";
     &eaFichero  = "";
     &eaPain     = "";
     &eaTag      = "";
     &eaVal      = "";
     &eaGrupo    = "";
     nSwCab      = 0;

     fHoy = @;
     aPais = "";
     aNif = "";
     aSufijo = "";
     aId = "";
     nPersona = 0;
     aCar = "";
     aLon = "";
     nLon = 0;
     aDeci = "";
     aHora = "";

     eaNombre    = "";
     eaDireccion = "";
     eaPoblacion = "";
     eaProvincia = "";
     eaPais      = "";
     eaCodIBAN   = "";
     eaCodBIC    = "";
     eaNif       = "";
     eaCP        = "";
|FIN;
____________________________________/ CALCULOS DE PANTALLA
|PROCESO Ruta;
     |SI FSalida = 10;
          aAlfa = "D"; |BROWSE aAlfa;
          aAlfa = aAlfa % 0299; |QBF aAlfa;
          |SI aAlfa ! "";
               #0#5 = aAlfa; |PINTA #0#5;
          |FINSI;
          |ERROR;
     |FINSI;
|FPRC;

|PROCESO mayus; |TIPO 0;
    #0Campo = #0Campo > #0Campo;
|PINTA #0Campo;
|FPRC;

|PROCESO AntesFichero; |TIPO 7;
   || nTXML = 0; |SI #0#8 ] "01.01.2016"; nTXML = 1; |FINSI;
      nTXML = 1;
      |SI nTXML = 1;
          |SI #0#2 = "D";
              aAlfa1 = ("00000" + #0#12) % -105;
          |SINO;
              aAlfa1 = ("00000" + #0#18) % -105;
          |FINSI;
          aAlfa1 = "n34" + aAlfa1 + ".xml";
          #0#15 = aAlfa1; |PINTA #0#15;
      |FINSI;
|FPRC;

|PROCESO no_vacio; |TIPO 0;
    alfa1 = #0#15;
   |QBF alfa1;
   |SI alfa1 = "";
       |AVISO;
       |ERROR;
   |FINSI;
|FPRC;

|PROCESO seleccion; |TIPO 0;
|SI #0#2 = "D";
        alfa1 = "S";
        alfa2 = "N";
|SINO;
        alfa1 = "N";
        alfa2 = "S";
|FINSI;
|C_M #0#0, 1, alfa1;
|C_M #0#1, 1, alfa1;
|C_M #0#12, 1, alfa1;
||C_M #0#16, 1, alfa1;

|C_M #0#18, 1, alfa2;
|C_M #0#19, 1, alfa2;
|C_M #0#20, 1, alfa2;
|C_M #0#21, 1, alfa2;
|C_M #0#22, 1, alfa2;
|C_M #0#23, 1, alfa2;
|C_M #0#24, 1, alfa2;
|C_M #0#25, 1, alfa2;
|C_M #0#26, 1, alfa2;
|C_M #0#27, 1, alfa2;
|C_M #0#28, 1, alfa2;
|C_M #0#29, 1, alfa2;
|C_M #0#30, 1, alfa2;
|C_M #0#31, 1, alfa2;
|C_M #0#32, 1, alfa2;
|C_M #0#33, 1, alfa2;
|C_M #0#34, 1, alfa2;
|C_M #0#35, 1, alfa2;
|C_M #0#36, 1, alfa2;
|C_M #0#37, 1, alfa2;
|FPRC;

|PROCESO nomdator;
     #0#9 = #nomdator#0;
     #0#10 = #nomdator#1;
     |PINTA #0#9;
     |PINTA #0#10;
     |ERROR10;
|FPRC;

|DEFBUCLE nomdator;
     #nomdator#1;
     13;
     01, #0#12;
     09, #0#12;
     ;
     NULL, nomdator;
|FIN;

|PROCESO ProponOrd; |TIPO 7;
     swModulo = 0;
     |HAZ BuscaModulo;
     |SI swModulo = 2805; |O swModulo = 1;
          |BUCLE nomdator;
     |FINSI;
|FPRC;

|PROCESO CompOrd;
     swModulo = 0;
     |HAZ BuscaModulo;
     |SI swModulo = 2805; |O swModulo = 1;
          |ABRE #nomdator;
          #nomdator#0 = #0#9;
          |LEE 000#nomdator.=;
          |SI FSalida = 0;
               |SI #nomdator#13 ! #0#12;
                    aAlfa = "    ATENCION: El ordenante no esta asignado a la empresa ";
                    aAlfa = aAlfa + "seleccionada";
                    |MENAV aAlfa;
               |FINSI;
          |FINSI;
          |CIERRA #nomdator;
     |FINSI;
|FPRC;

|PROCESO NormaOrdenante;
     || Esto en verdad es para usar la norma 43 o la norma 43-1, pero no se
     || usa ya que lo que se hacia de toda la vida es correcto, no hace
     || falta diferenciar. El campo en la pantalla del nomdator esta
     || quitado y se deja por defecto a "2" para que lo haga como toda la vida
     || aunque de las dos maneras lo haria bien

     |SI #11#16 = 2;
          alfa1 = #11#2;             || codigo ordenante
     |SINO;
          aDato = #11#2;
          |QBF aDato;
          alfa1 = " " + aDato;
     |FINSI;
|FPRC;

|PROCESO NormaBeneficiario;          || datos del beneficiario
     || Esto en verdad es para usar la norma 43 o la norma 43-1, pero no se
     || usa ya que lo que se hacia de toda la vida es correcto, no hace
     || falta diferenciar. El campo en la pantalla del nomdator esta
     || quitado y se deja por defecto a "2" para que lo haga como toda la vida
     || aunque de las dos maneras lo haria bien

     alfa2 = #13#4 % 112;      || dni
     |QBF alfa2;
     |SI #11#16 = 2;
          alfa2 = ("000000000000" + alfa2) % -112;
     |SINO;
          aDato = alfa2;
          aDato = aDato + "            ";
          aDato = aDato % 112;
          alfa2 = aDato;
     |FINSI;
|FPRC;

____________________________________/ GRABACION SECUENCIAL
|PROCESO grabalin;
     contenido = contenido + longitud;
     contenido = contenido % 173;
     || contenido = contenido + ".";             || total = 2 + 72
     contenido = contenido + " " + &13 + &10;             || total = 2 + 72
     |FGRABA contenido;
     regis_total = regis_total + 1;
|FPRC;

|PROCESO Formato;
     aRelleno = "";
     |SI aTipo = "N";
          aRelleno = aRellenoCero * nLongitud;
     |FINSI;
     |SI aTipo = "A";
          aRelleno = aRellenoAlfa * nLongitud;
     |FINSI;
     |SI aTipo = "F";
          aRelleno = aRellenoAlfa * nLongitud;
          aDato = (aDato % -104) + (aDato % 402) + (aDato % 102);
     |FINSI;
     |QBF aDato;
     |SI aTipo = "N";
          nLongitud = -1 * (100 + nLongitud);
          aDato = (aRelleno + aDato) % nLongitud;
     |SINO;
          nLongitud = (100 + nLongitud);
          aDato = (aDato + aRelleno) % nLongitud;
     |FINSI;
|FPRC;

|PROCESO QuitaRarosIBAN;
     aLongRar = aCadena % 0; nLongRar = aLongRar; aAlfaRar2 = "";
     |PARA nContRar1 = 1; |SI nContRar1 [ nLongRar; |HACIENDO nContRar1 = nContRar1 + 1;
          CalcRar1 = (nContRar1 * 100) + 1; aAlfaRar1  = aCadena % CalcRar1;
          |SI aAlfaRar1 = " ";                     |VETE Sigue1; |FINSI;
          |SI aAlfaRar1 ] "A"; |Y aAlfaRar1 [ "Z"; |VETE Sigue1; |FINSI;
          |SI aAlfaRar1 ] "a"; |Y aAlfaRar1 [ "z"; |VETE Sigue1; |FINSI;
          |SI aAlfaRar1 ] "0"; |Y aAlfaRar1 [ "9"; |VETE Sigue1; |FINSI;

          |SI aAlfaRar1 = "/"; |O aAlfaRar1 = "-"; |O aAlfaRar1 = "?";
          |O aAlfaRar1 = ":"; |O aAlfaRar1 = "("; |O aAlfaRar1 = ")";
          |O aAlfaRar1 = "."; |O aAlfaRar1 = ","; |O aAlfaRar1 = "+";
          |O aAlfaRar1 = " ";
               |VETE Sigue1;
          |FINSI;

          |SI aAlfaRar1 = " ";  aAlfaRar1 = "a";   |VETE Sigue1;|FINSI;
          |SI aAlfaRar1 = "‚";  aAlfaRar1 = "e";   |VETE Sigue1;|FINSI;
          |SI aAlfaRar1 = "¡";  aAlfaRar1 = "i";   |VETE Sigue1;|FINSI;
          |SI aAlfaRar1 = "¢";  aAlfaRar1 = "o";   |VETE Sigue1;|FINSI;
          |SI aAlfaRar1 = "£";  aAlfaRar1 = "u";   |VETE Sigue1;|FINSI;
          |SI aAlfaRar1 = "¥";  aAlfaRar1 = "N";   |VETE Sigue1;|FINSI;
          |SI aAlfaRar1 = "¤";  aAlfaRar1 = "n";   |VETE Sigue1;|FINSI;

          aAlfaRar = " ";
          aAlfaRar1 = " ";

          |ET Sigue1;
          aAlfaRar2 = aAlfaRar2 + aAlfaRar1;
     |SIGUE;
     aCadena = aAlfaRar2;
|FPRC;

|PROCESO grabaDato;
     |HAZ Formato;

     aCadena = aDato;
     |HAZ QuitaRarosIBAN;
     aDato = aCadena;

     |XGRABA nCanal, aDato;
|FPRC;

|PROCESO ordenante;
     aAlfa = #0#15;
     |QBF aAlfa;
     aAlfa1 = #0#5;
     |QBF aAlfa1;
     aAlfa2 = aAlfa1 % -101;
     |SI aAlfa2 ! "\";
          aAlfa1 = aAlfa1 + "\";
     |FINSI;
     aRuta = aAlfa1;
     aAlfa = aAlfa1 + aAlfa;

     |XABRE "CBW", aAlfa, nCanal;
     |SI FSalida ] 0;
          || Campo 1: codigo de registro
          aDato = "01";
          nLongitud = 2; aTipo = "N"; |HAZ grabaDato;

          || Campo 2: codigo de operacion
          aDato = "ORD";
          nLongitud = 3; aTipo = "A"; |HAZ grabaDato;

          || Campo 3: version del cuaderno
          aDato = "34145";
          nLongitud = 5; aTipo = "N"; |HAZ grabaDato;

          || Campo 4: numero de dato
          aDato = "001";
          nLongitud = 3; aTipo = "N"; |HAZ grabaDato;

          || Campo 5: identificacion del ordenante (NIF)
          aDato = #11#2;
          nLongitud = 9; aTipo = "A"; |HAZ grabaDato;

          || Campo 6: identificacion del ordenante (sufijo)
          aDato = #11#23;
          nLongitud = 3; aTipo = "A"; |HAZ grabaDato;

          || Campo 7: Fecha de creacion del fichero
          fecsys = ~;
          aDato = fecsys;
          nLongitud = 8; aTipo = "F"; |HAZ grabaDato;

          || Campo 8: fecha de ejecucion o emision de las ordenes
          aDato = #0#8;
          nLongitud = 8; aTipo = "F"; |HAZ grabaDato;

          || Campo 9: tipo de identificacion de la cuenta del ordenante
          aDato = "A";
          nLongitud = 1; aTipo = "A"; |HAZ grabaDato;

          || Campo 10: codigo de cuenta (IBAN)
          aDato = #11#21;
          nLongitud = 34; aTipo = "A"; |HAZ grabaDato;

          || Campo 11: detalle del cargo
          aDato = #11#15;
          nLongitud = 1; aTipo = "N"; |HAZ grabaDato;

          || Campo 12: nombre del ordenante
          aDato = #11#3;
          nLongitud = 70; aTipo = "A"; |HAZ grabaDato;

          || Campo 13: domicilio del ordenante (tipo via, nombre via, numero, piso)
          aDato = #11#8;
          nLongitud = 50; aTipo = "A"; |HAZ grabaDato;

          || Campo 14: domicilio del ordenante (codigo posta y localidad)
          aAlfa1 = #11#17;    |QBF aAlfa1;   aAlfa1 = ("00" + aAlfa1) % -102;
          aAlfa2 = #11#18;    |QBF aAlfa2;   aAlfa2 = ("000" + aAlfa2) % -103;
          aDato = aAlfa1 + aAlfa2 + #11#9;
          nLongitud = 50; aTipo = "A"; |HAZ grabaDato;

          || Campo 15: domicilio del ordenante (provincia)
          aDato = #11#19;
          nLongitud = 40; aTipo = "A"; |HAZ grabaDato;

          || Campo 16: pais del ordenante (codigo alfa)
          aDato = #11#20;
          nLongitud = 2; aTipo = "A"; |HAZ grabaDato;

          || Campo 17: Libre de 311 (primeros 99)
          aDato = "";
          nLongitud = 99; aTipo = "A"; |HAZ grabaDato;

          || Campo 17: Libre de 311 (siguientes 99)
          aDato = "";
          nLongitud = 99; aTipo = "A"; |HAZ grabaDato;

          || Campo 17: Libre de 311 (siguientes 99)
          aDato = "";
          nLongitud = 99; aTipo = "A"; |HAZ grabaDato;

          || Campo 17: Libre de 311 (ultimos 14)
          aDato = "";
          nLongitud = 14; aTipo = "A"; |HAZ grabaDato;

          aAlfa = &13 + &10;
          |XGRABA nCanal, aAlfa;
          regis_total = regis_total + 1;
     |FINSI;
|FPRC;

|PROCESO cabecera;
          || Campo 1: codigo de registro
          aDato = "02";
          nLongitud = 2; aTipo = "N"; |HAZ grabaDato;

          || Campo 2: codigo de operacion
          aDato = "SCT";
          nLongitud = 3; aTipo = "A"; |HAZ grabaDato;

          || Campo 3: version del cuaderno
          aDato = "34145";
          nLongitud = 5; aTipo = "N"; |HAZ grabaDato;

          || Campo 4: identificacion del ordenante (NIF)
          aDato = #11#2;
          nLongitud = 9; aTipo = "A"; |HAZ grabaDato;

          || Campo 5: identificacion del ordenante (sufijo)
          aDato = #11#23;
          nLongitud = 3; aTipo = "A"; |HAZ grabaDato;

          || Campo 6: Libre de 578 (primeros 99)
          aDato = "";
          nLongitud = 99; aTipo = "A"; |HAZ grabaDato;

          || Campo 6: Libre de 578 (siguientes 99)
          aDato = "";
          nLongitud = 99; aTipo = "A"; |HAZ grabaDato;

          || Campo 6: Libre de 578 (siguientes 99)
          aDato = "";
          nLongitud = 99; aTipo = "A"; |HAZ grabaDato;

          || Campo 6: Libre de 578 (siguientes 99)
          aDato = "";
          nLongitud = 99; aTipo = "A"; |HAZ grabaDato;

          || Campo 6: Libre de 578 (siguientes 99)
          aDato = "";
          nLongitud = 99; aTipo = "A"; |HAZ grabaDato;

          || Campo 6: Libre de 578 (ultimos 83)
          aDato = "";
          nLongitud = 83; aTipo = "A"; |HAZ grabaDato;

          aAlfa = &13 + &10;
          |XGRABA nCanal, aAlfa;
          regis_total = regis_total + 1;
|FPRC;

|PROCESO total;
     importes = importes + #13#6;   || total importes
     registro = registro + 1;       || total registros '002'
|FPRC;

|PROCESO beneficiario;
          aTipoIngreso = #13#7 % 103;

          || Campo 1: codigo de registro
          aDato = "03";
          nLongitud = 2; aTipo = "N"; |HAZ grabaDato;

          || Campo 2: codigo de operacion
          aDato = "SCT";
          nLongitud = 3; aTipo = "A"; |HAZ grabaDato;

          || Campo 3: version del cuaderno
          aDato = "34145";
          nLongitud = 5; aTipo = "N"; |HAZ grabaDato;

          || Campo 4: numero de dato
          aDato = "002";
          nLongitud = 3; aTipo = "N"; |HAZ grabaDato;

          || Campo 5: referencia del ordenante (NIF ordenante + registro + nro.orden)
          aDato = #11#2; |QBF aDato;
          aAlfa1 = #13#1; |QBF aAlfa1; aAlfa1 = ("000000" + aAlfa1) % -106;
          aAlfa2 = #13#2; |QBF aAlfa2; aAlfa2 = ("00000" + aAlfa2) % -105;
          aDato = aDato + "-" + aAlfa1 + "-" + aAlfa2;
          nLongitud = 35; aTipo = "A"; |HAZ grabaDato;

          || Campo 6: tipo de identificador cuenta del beneficiario (IBAN = A)
          aDato = "A";
          nLongitud = 1; aTipo = "A"; |HAZ grabaDato;

          || Campo 7: cuenta del beneficiario (IBAN)
          aDato = #13#9;
          nLongitud = 34; aTipo = "A"; |HAZ grabaDato;

          || Campo 8: Importe de la transferencia
          enCanti = #13#6;
          |HAZ ConverMoneda;
          #13#6 = enCanti;
          alfa4 = #13#6;  alfa4 = "00000000000" + alfa4;  alfa4 = alfa4 % -111;
          aDato = alfa4;
          nLongitud = 11; aTipo = "0"; |HAZ grabaDato;

          |HAZ total;

          || Campo 9: Clave de gastos = 3 (gastos compartidos)
          aDato = 3;
          nLongitud = 1; aTipo = "N"; |HAZ grabaDato;

          || Campo 10: BIC de la entidad del beneficiario
          aDato = "";
          nLongitud = 11; aTipo = "A"; |HAZ grabaDato;

          || Campo 11: Nombre del beneficiario
          aDato = #13#8;
          nLongitud = 70; aTipo = "A"; |HAZ grabaDato;

          #labtraba#0 = #13#0;
          #labtraba#1 = #13#3;
          |LEE 000#labtraba.=;

          || Campo 12: Direccion del beneficiario
          traba = #2#3;   |QBF traba;  alfa8 = traba + " " + #2#4 + " " + #2#5;
          aDato = alfa8;
          |SI aTipoIngreso = "EMB"; aDato = ""; |FINSI;
          nLongitud = 50; aTipo = "A"; |HAZ grabaDato;

          || Campo 13: Direccion del beneficiario
          aAlfa1 = #labtraba#8;    |QBF aAlfa1;   aAlfa1 = ("00" + aAlfa1) % -102;
          aAlfa2 = #labtraba#9;    |QBF aAlfa2;   aAlfa2 = ("000" + aAlfa2) % -103;
          aDato = aAlfa1 + aAlfa2 + #labtraba#6;
          |SI aTipoIngreso = "EMB"; aDato = ""; |FINSI;
          nLongitud = 50; aTipo = "A"; |HAZ grabaDato;

          || Campo 14: Direccion del beneficiario
          aDato = #labtraba#10;
          |SI aTipoIngreso = "EMB"; aDato = ""; |FINSI;
          nLongitud = 40; aTipo = "A"; |HAZ grabaDato;

          || Campo 15: Pais
          aDato = "ES ";
          nLongitud = 2; aTipo = "A"; |HAZ grabaDato;

          || Campo 16: Concepto (longitud 140, primeros 99)
          aDato = #13#7;
          nLongitud = 99; aTipo = "A"; |HAZ grabaDato;

          || Campo 16: Concepto (ultimos 41)
          aDato = "";
          nLongitud = 41; aTipo = "A"; |HAZ grabaDato;

          || Campo 17: identificacion de la transaccion (reservado)
          aDato = "";
          nLongitud = 35; aTipo = "A"; |HAZ grabaDato;

          || Campo 18: tipo de transferencia: nomina (codigo "SALA")
          aDato = "SALA";
          ||SI aTipoIngreso = "EMB"; aDato = ""; |FINSI;
          nLongitud = 4; aTipo = "A"; |HAZ grabaDato;

          || Campo 19: proposito de la transferencia: nomina (codigo "SALA")
          aDato = "SALA";
          ||SI aTipoIngreso = "EMB"; aDato = ""; |FINSI;
          nLongitud = 4; aTipo = "A"; |HAZ grabaDato;

          || Campo 20: libre
          aDato = "";
          nLongitud = 99; aTipo = "A"; |HAZ grabaDato;

          aAlfa = &13 + &10;
          |XGRABA nCanal, aAlfa;
          regis_total = regis_total + 1;
|FPRC;

|PROCESO totales;
          || Campo 1: codigo de registro
          aDato = "04";
          nLongitud = 2; aTipo = "N"; |HAZ grabaDato;

          || Campo 2: codigo de operacion
          aDato = "SCT";
          nLongitud = 3; aTipo = "A"; |HAZ grabaDato;

          || Campo 3: total de importes
          aDato = importes;
          nLongitud = 17; aTipo = "N"; |HAZ grabaDato;

          || Campo 4: numero de registros de transferencias a beneficiario
          aDato = registro;
          nLongitud = 8; aTipo = "N"; |HAZ grabaDato;

          || Campo 5: total de registros
          regis_total = regis_total + 1;
          nNume = regis_total - 1; || quito uno, el 02 del principio parece
                                   || que no se cuenta
          aDato = nNume;
          nLongitud = 10; aTipo = "N"; |HAZ grabaDato;

          || Campo 6: Libre de 560 (primeros 99)
          aDato = "";
          nLongitud = 99; aTipo = "A"; |HAZ grabaDato;

          || Campo 6: Libre de 560 (siguientes 99)
          aDato = "";
          nLongitud = 99; aTipo = "A"; |HAZ grabaDato;

          || Campo 6: Libre de 560 (siguientes 99)
          aDato = "";
          nLongitud = 99; aTipo = "A"; |HAZ grabaDato;

          || Campo 6: Libre de 560 (siguientes 99)
          aDato = "";
          nLongitud = 99; aTipo = "A"; |HAZ grabaDato;

          || Campo 6: Libre de 560 (siguientes 99)
          aDato = "";
          nLongitud = 99; aTipo = "A"; |HAZ grabaDato;

          || Campo 6: Libre de 560 (ultimos 65)
          aDato = "";
          nLongitud = 65; aTipo = "A"; |HAZ grabaDato;

          aAlfa = &13 + &10;
          |XGRABA nCanal, aAlfa;
|FPRC;

|PROCESO totales_general;
          || Campo 1: codigo de registro
          aDato = "99";
          nLongitud = 2; aTipo = "N"; |HAZ grabaDato;

          || Campo 2: codigo de operacion
          aDato = "ORD";
          nLongitud = 3; aTipo = "A"; |HAZ grabaDato;

          || Campo 3: total de importes
          aDato = importes;
          nLongitud = 17; aTipo = "N"; |HAZ grabaDato;

          || Campo 4: numero de registros
          aDato = registro;
          nLongitud = 8; aTipo = "N"; |HAZ grabaDato;

          || Campo 5: total de registros
          regis_total = regis_total + 1;
          aDato = regis_total;
          nLongitud = 10; aTipo = "N"; |HAZ grabaDato;

          || Campo 6: Libre de 560 (primeros 99)
          aDato = "";
          nLongitud = 99; aTipo = "A"; |HAZ grabaDato;

          || Campo 6: Libre de 560 (siguientes 99)
          aDato = "";
          nLongitud = 99; aTipo = "A"; |HAZ grabaDato;

          || Campo 6: Libre de 560 (siguientes 99)
          aDato = "";
          nLongitud = 99; aTipo = "A"; |HAZ grabaDato;

          || Campo 6: Libre de 560 (siguientes 99)
          aDato = "";
          nLongitud = 99; aTipo = "A"; |HAZ grabaDato;

          || Campo 6: Libre de 560 (siguientes 99)
          aDato = "";
          nLongitud = 99; aTipo = "A"; |HAZ grabaDato;

          || Campo 6: Libre de 560 (ultimos 65)
          aDato = "";
          nLongitud = 65; aTipo = "A"; |HAZ grabaDato;

          aAlfa = &13 + &10;
          |XGRABA nCanal, aAlfa;

          |XCIERRA nCanal;
|FPRC;

|| ///////////////////////////////////////////// XML
|| .... Varios calculos
|PROCESO NumDeci;
     nNume = (nNume * 100) ! 0;
     aAlfa = nNume;
     aLon = aAlfa % A0; nLon = aLon;
     aDeci = aAlfa % -102;
     nLon = nLon + 100 - 2;
     aAlfa = (aAlfa % nLon) + "." + aDeci;
|FPRC;

|PROCESO FormaFecha;
     eaVal = (eaVal % 704) + "-" + (eaVal % 402) + "-" + (eaVal % 102);
|FPRC;

|PROCESO HoraActual;
     fHoy = ~;
     |HORA aAlfa;
     aAlfa = (fHoy % 704) + "-" + (fHoy % 402) + "-" + (fHoy % 102) + "T" + aAlfa;
|FPRC;

|PROCESO ObtenId;
     |PARA FSalida = 1; |SI FSalida ! 0; |HACIENDO; aNif = aNif - "/"; |SIGUE;
     |PARA FSalida = 1; |SI FSalida ! 0; |HACIENDO; aNif = aNif - "-"; |SIGUE;
     |PARA FSalida = 1; |SI FSalida ! 0; |HACIENDO; aNif = aNif - "?"; |SIGUE;
     |PARA FSalida = 1; |SI FSalida ! 0; |HACIENDO; aNif = aNif - ":"; |SIGUE;
     |PARA FSalida = 1; |SI FSalida ! 0; |HACIENDO; aNif = aNif - "("; |SIGUE;
     |PARA FSalida = 1; |SI FSalida ! 0; |HACIENDO; aNif = aNif - ")"; |SIGUE;
     |PARA FSalida = 1; |SI FSalida ! 0; |HACIENDO; aNif = aNif - "."; |SIGUE;
     |PARA FSalida = 1; |SI FSalida ! 0; |HACIENDO; aNif = aNif - ","; |SIGUE;
     |PARA FSalida = 1; |SI FSalida ! 0; |HACIENDO; aNif = aNif - "'"; |SIGUE;
     |PARA FSalida = 1; |SI FSalida ! 0; |HACIENDO; aNif = aNif - "+"; |SIGUE;
     |PARA FSalida = 1; |SI FSalida ! 0; |HACIENDO; aNif = aNif - &34; |SIGUE;

     aAlfa = aNif + aPais + "00";

     aAlfa = aAlfa > aAlfa;
     aCar = ""; aCadena = "";
     |PARA; |SI aAlfa ! ""; |HACIENDO;
          aCar = aAlfa % 0101; aAlfa = aAlfa % 0299;
          |SI aCar ] "0"; |Y aCar [ "9";
               aCadena = aCadena + aCar;
          |SINO;
               |SI aCar ] "A"; |Y aCar [ "Z"; |Y aCar ! "Ñ";
                    nNume = &aCar;
                    nNume = nNume - 55;
                    aCar = nNume;
                    aCadena = aCadena + aCar;
               |FINSI;
          |FINSI;
     |SIGUE;
     aAlfa = aCadena;
     |HAZ Mod_9710;
     aId = aPais + aAlfa + aSufijo + aNif;
|FPRC;

|PROCESO Mod_9710;
     aAlfa2 = aAlfa % 0101;
     |PARA; |SI aAlfa2 = "0"; |HACIENDO;
          aAlfa = aAlfa % 0299; aAlfa2 = aAlfa % 0101;
     |SIGUE;

     aAlfa1 = "";
     |PARA; |SI aAlfa ! ""; |HACIENDO;
          aAlfa2 = aAlfa % 0; nNume = aAlfa2;
          |SI nNume > 5;
               aAlfa2 = aAlfa % 0105;
               aAlfa = aAlfa % 0699;
          |SINO;
               aAlfa2 = aAlfa;
               aAlfa = "";
          |FINSI;
          aAlfa1 = aAlfa1 + aAlfa2;
          nNume = aAlfa1;
          nNume = nNume / 97;
          aAlfa2 = nNume;
          aAlfa2 = aAlfa2 - ".";
          |SI FSalida ! 0;
               nNume = FSalida + 98;
               aAlfa2 = aAlfa2 % nNume;
               aAlfa2 = "0." + aAlfa2;
               nNume = aAlfa2;
          |SINO;
               nNume = 0;
          |FINSI;
          nNume = (nNume * 97) ! 0;
          aAlfa1 = nNume;
     |SIGUE;
     nNume  = aAlfa1;
     nNume = 98 - nNume;
     aAlfa = ("00" + nNume) % -102;
||nNume  = 98 - (nNume - (((nNume / 97) ! 0) * 97)); || MOD 97-10  ISO 7604
|FPRC;

|| ....

|PROCESO leedatostrabaja;
 || .............. Lee la ficha del trabajador

 eaNombre    = "";
 eaDireccion = "";
 eaPoblacion = "";
 eaProvincia = "";
 eaPais      = "";
 eaCodIBAN   = "";
 eaCodBIC    = "";

 #labtraba#0 = #13#0;
 #labtraba#1 = #13#3;
 |LEE 000#labtraba.=;
 |SI FSalida ! 0; |DDEFECTO #labtraba; |FINSI;

 eaNif     = #13#4;
 eaNombre  = #13#8;
 eaCodIBAN = #13#9;

 aAlfa     = #labtraba#3; |QBF aAlfa;
 aAlfa1    = #labtraba#4; |QBF aAlfa1;
 aAlfa2    = #labtraba#5; |QBF aAlfa2;
 |SI aAlfa ! "";
     |SI aAlfa1 ! "";
         aAlfa = aAlfa + ", " + aAlfa1 + " " + aAlfa2;
     |SINO;
         |SI aAlfa2 ! "";
             aAlfa = aAlfa + ", " + aAlfa2;
         |FINSI;
     |FINSI;
 |FINSI;
 eaDireccion  = aAlfa;
 eaPoblacion  = #labtraba#6;
 |SI #labtraba#8 ! 0; |Y #labtraba#9 ! 0;
     eaCP     = ("00" + #labtraba#8) % -102 + ("000" + #labtraba#9) % -103;
 |SINO;
     eaCP     = "";
 |FINSI;
 eaProvincia  = #labtraba#10;
 eaPais       = "ES";
 eaCodBIC     = #13#10;
 || eaCodBIC     = #labtraba#155;
 || eaPaisBan    = #labtraba#153;
|FPRC;
|| ....

|PROCESO BenefiXML;    ||Transferecnia Sepa
     |QBF eaCodBIC;
     |QBF eaCodIBAN;
     |QBF eaDireccion;
     |QBF eaPoblacion;
     |QBF eaProvincia;

     eaGrupo = "S";
     eaTag = "CdtTrfTxInf";|HAZ XmlTag;

     eaGrupo = "S";
     eaTag = "PmtId";|HAZ XmlTag;

||2.30
     |QBF eaNif;
     eaVal = eaNif + (fHoy % 102) + (fHoy % 402) + (fHoy % 704);
     eaVal = eaVal + (aHora % 102) + (aHora % 402) + (aHora % 702);
     eaVal = eaVal + (("000000" + #13#1) % -106) + (("00000" + #13#2) % -105);
     eaTag = "EndToEndId"; |HAZ XmlTag;

     eaGrupo = "N"; |HAZ XmlTag;

     eaGrupo = "S";
     eaTag = "Amt";|HAZ XmlTag;

     nNume = #13#6; |HAZ NumDeci; eaVal = aAlfa;
     eaTag = "InstdAmt Ccy=" + &34 + "EUR" + &34; |HAZ XmlTag;

     eaGrupo = "N"; |HAZ XmlTag;

||2.77
     |SI eaCodBIC ! "";
          eaGrupo = "S";
          eaTag = "CdtrAgt";|HAZ XmlTag;

          eaGrupo = "S";
          eaTag = "FinInstnId";|HAZ XmlTag;

          eaVal = eaCodBIC;
          eaTag = "BIC"; |HAZ XmlTag;

          eaGrupo = "N"; |HAZ XmlTag;
          eaGrupo = "N"; |HAZ XmlTag;
     |FINSI;

||2.79
     eaGrupo = "S";
     eaTag = "Cdtr";|HAZ XmlTag;

     eaVal = eaNombre; |QBF eaVal;
     |SI eaVal ! "";
          eaTag = "Nm"; |HAZ XmlTag;
     |FINSI;

     eaGrupo = "S";
     eaTag = "PstlAdr";|HAZ XmlTag;

     eaVal = eaPais; |QBF eaPais;
     |SI eaPais = ""; eaVal = "ES"; |FINSI;
     eaTag = "Ctry"; |HAZ XmlTag;

     eaVal = eaDireccion; |QBF eaVal;
     |SI eaVal ! "";
          eaTag = "AdrLine"; |HAZ XmlTag;
     |FINSI;

     |SI eaPoblacion = "";
          |SI eaProvincia ! "";
               eaVal = eaProvincia;
               eaTag = "AdrLine"; |HAZ XmlTag;
          |FINSI;
     |SINO;
          |SI eaProvincia ! "";
               eaVal = eaCP + eaPoblacion + ", " + eaProvincia;
               eaTag = "AdrLine"; |HAZ XmlTag;
          |SINO;
               eaVal = eaCP + eaPoblacion;
               eaTag = "AdrLine"; |HAZ XmlTag;
          |FINSI;
     |FINSI;

     eaGrupo = "N"; |HAZ XmlTag;
     eaGrupo = "N"; |HAZ XmlTag;

     eaGrupo = "S";
     eaTag = "CdtrAcct";|HAZ XmlTag;

     eaGrupo = "S";
     eaTag = "Id";|HAZ XmlTag;

     eaVal = eaCodIBAN;
     eaTag = "IBAN"; |HAZ XmlTag;

     eaGrupo = "N"; |HAZ XmlTag;
     eaGrupo = "N"; |HAZ XmlTag;

     eaGrupo = "S";
     eaTag = "RmtInf";|HAZ XmlTag;

     eaVal = #13#7;  || concepto
     eaTag = "Ustrd"; |HAZ XmlTag;

     eaGrupo = "N"; |HAZ XmlTag;
     eaGrupo = "N"; |HAZ XmlTag;
|FPRC;

|PROCESO InfoPago;
     nSwCab = 1;

     eaGrupo = "S";
     eaTag = "PmtInf";|HAZ XmlTag;

     fHoy = ~;
     |HORA aHora;
     aAlfa = #11#2 % 109;
     eaVal = (fHoy % 102) + (fHoy % 402) + (fHoy % 704);
     eaVal = eaVal + (aHora % 102) + (aHora % 402) + (aHora % 702);
     eaVal = eaVal + aAlfa + "001";
     eaTag = "PmtInfId"; |HAZ XmlTag;

     eaVal = "TRF";
     eaTag = "PmtMtd"; |HAZ XmlTag;

     eaVal = "true";
     |SI #11#15 = "1"; eaVal = "false"; |FINSI;
     eaTag = "BtchBookg"; |HAZ XmlTag; ||aqui

     eaVal = nTRegistros;
     eaTag = "NbOfTxs"; |HAZ XmlTag;

     nNume = nTTotal; |HAZ NumDeci;
     eaVal = aAlfa;
     eaTag = "CtrlSum"; |HAZ XmlTag;

||----
     eaGrupo = "S";
     eaTag   = "PmtTpInf"; |HAZ XmlTag;

     eaGrupo = "S";
     eaTag   = "SvcLvl"; |HAZ XmlTag;

     eaVal   = "SEPA";
     eaTag   = "Cd"; |HAZ XmlTag;

     eaGrupo = "N"; |HAZ XmlTag;

|| migue nuevo
     |SI #0#39 = "S";
          eaGrupo = "S";
          eaTag   = "LclInstrm"; |HAZ XmlTag;

          eaVal   = "SDCL";
          eaTag   = "Cd"; |HAZ XmlTag;

          eaGrupo = "N"; |HAZ XmlTag;
     |FINSI;
|| migue hasta aqui
     eaGrupo = "S";
     eaTag   = "CtgyPurp"; |HAZ XmlTag;

     eaVal   = "SALA";
     eaTag   = "Cd"; |HAZ XmlTag;

     eaGrupo = "N"; |HAZ XmlTag;

     eaGrupo = "N"; |HAZ XmlTag;

||----
     eaVal = #0#8; |HAZ FormaFecha;
     eaTag = "ReqdExctnDt"; |HAZ XmlTag;

     eaGrupo = "S";
     eaTag = "Dbtr"; |HAZ XmlTag;

     eaVal = #11#3; |QBF eaVal;
     |SI eaVal ! "";
          eaTag = "Nm"; |HAZ XmlTag;
     |FINSI;

     eaGrupo = "S";
     eaTag = "PstlAdr"; |HAZ XmlTag;

     eaVal = "ES";
     eaTag = "Ctry"; |HAZ XmlTag;

     eaVal = #11#8; |QBF eaVal;
     |SI eaVal ! "";
          eaTag = "AdrLine"; |HAZ XmlTag;
     |FINSI;

     aAlfa1 = #11#17;  |QBF aAlfa1;   aAlfa1 = ("00" + aAlfa1) % -102;
     aAlfa2 = #11#18;  |QBF aAlfa2;   aAlfa2 = ("000" + aAlfa2) % -103;
     |SI #11#17 ! 0; |Y #11#18 ! 0;
          eaVal = aAlfa1 + aAlfa2 + #11#9;
     |SINO;
          eaVal =  #11#9;
     |FINSI;
     |QBF eaVal;
     |SI eaVal ! "";
          eaTag = "AdrLine"; |HAZ XmlTag;
     |FINSI;

     eaGrupo = "N"; |HAZ XmlTag;
     eaGrupo = "N"; |HAZ XmlTag;
||2.20  ????
     eaGrupo = "S";
     eaTag = "DbtrAcct"; |HAZ XmlTag;

     eaGrupo = "S";
     eaTag = "Id"; |HAZ XmlTag;

     aAlfa = #11#21; |QBF aAlfa;
     |SI aAlfa ! "";
          eaVal = aAlfa;
          eaTag = "IBAN"; |HAZ XmlTag;
     |SINO;
          eaGrupo = "S";
          eaTag = "Othr"; |HAZ XmlTag;

          aPais = "ES";
          aNif = #11#2 % 109;
          aSufijo = #11#23; |QBF aSufijo; aSufijo = ("000" + aSufijo) % -103;
          |HAZ ObtenId;
          eaVal = aId;
          eaTag = "Id"; |HAZ XmlTag;

          eaGrupo = "N"; |HAZ XmlTag;
     |FINSI;
     eaGrupo = "N"; |HAZ XmlTag;
     eaGrupo = "N"; |HAZ XmlTag;

     eaGrupo = "S";
     eaTag = "DbtrAgt"; |HAZ XmlTag;

     eaGrupo = "S";
     eaTag = "FinInstnId"; |HAZ XmlTag;

     eaVal = #11#22; |QBF eaVal;
     |SI eaVal ! "";
          eaTag = "BIC"; |HAZ XmlTag;
     |SINO;
          eaGrupo = "S";
          eaTag = "Othr"; |HAZ XmlTag;

          eaVal = "NOTPROVIDED";
          eaTag = "Id"; |HAZ XmlTag;

          eaGrupo = "N"; |HAZ XmlTag;
     |FINSI;

     eaGrupo = "N"; |HAZ XmlTag;
     eaGrupo = "N"; |HAZ XmlTag;
|FPRC;

|PROCESO ProcesaCabeXML;
     eaGrupo = "S";
     eaTag = "CstmrCdtTrfInitn";|HAZ XmlTag;

     eaGrupo = "S";
     eaTag = "GrpHdr"; |HAZ XmlTag;

     eaVal = ("000000" + #12#1) % -106;
     eaTag = "MsgId"; |HAZ XmlTag;

     |HAZ HoraActual; eaVal = aAlfa;
     eaTag = "CreDtTm"; |HAZ XmlTag;

     eaVal = nTRegistros;
     eaTag = "NbOfTxs"; |HAZ XmlTag;

     nNume = nTTotal; |HAZ NumDeci;
     eaVal = aAlfa;
     eaTag = "CtrlSum"; |HAZ XmlTag;

     eaGrupo = "S";
     eaTag = "InitgPty"; |HAZ XmlTag;

     eaVal = #11#3; |QBF eaVal;
     |SI eaVal ! "";
          eaTag = "Nm"; |HAZ XmlTag;
     |FINSI;

     aAlfa = #11#2; |QBF aAlfa; aAlfa = aAlfa % 0101;
     |SI aAlfa ] "A"; |Y aAlfa [ "W";
          nPersona = 1; || Persona juridica
     |SINO;
          nPersona = 2; || Persona fisica
     |FINSI;

     eaGrupo = "S";
     eaTag = "Id"; |HAZ XmlTag;

     |SI nPersona = 1;
          eaGrupo = "S";
          eaTag = "OrgId"; |HAZ XmlTag;

          eaGrupo = "S";
          eaTag = "Othr"; |HAZ XmlTag;

          aPais = "ES";
          aNif = #11#2 % 109;
          aSufijo = #11#23; |QBF aSufijo; aSufijo = ("000" + aSufijo) % -103;
          eaVal = aNif + aSufijo;
          eaTag = "Id"; |HAZ XmlTag;

          eaGrupo = "N"; |HAZ XmlTag;
          eaGrupo = "N"; |HAZ XmlTag;
     |SINO;
          eaGrupo = "S";
          eaTag = "PrvtId"; |HAZ XmlTag;

          eaGrupo = "S";
          eaTag = "Othr"; |HAZ XmlTag;

          aPais = "ES";
          aNif = #11#2 % 109;
          aSufijo = #11#23; |QBF aSufijo; aSufijo = ("000" + aSufijo) % -103;
          eaVal = aNif + aSufijo;
          eaTag = "Id"; |HAZ XmlTag;

          eaGrupo = "N"; |HAZ XmlTag;
          eaGrupo = "N"; |HAZ XmlTag;
     |FINSI;

     eaGrupo = "N"; |HAZ XmlTag;
     eaGrupo = "N"; |HAZ XmlTag;
     eaGrupo = "N"; |HAZ XmlTag;
|FPRC;

|PROCESO OrdenaXML;
      nBueno = 0;
      |DFICE aAlfa;
      |SI #0#2 = "D";
          aAlfa1 = ("00000" + #0#12) % -105;
      |SINO;
          aAlfa1 = ("00000" + #0#18) % -105;
      |FINSI;
      aAlfa1 = "n34" + aAlfa1 + ".xml";
      eaOrigen = aAlfa;
      eaNombre1 = aAlfa1;
      eaFichero = aAlfa + aAlfa1;

      aAlfa = #0#15; |QBF aAlfa;
      aAlfa1 = #0#5;
      |QBF aAlfa1;
      aAlfa2 = aAlfa1 % -101;
      |SI aAlfa2 ! "\";
           aAlfa1 = aAlfa1 + "\";
      |FINSI;
      aRuta = aAlfa1;
      aAlfa = aAlfa1 + aAlfa;
      eaDestino = aAlfa;

      eaPain = "001.001.03";
      |HAZ XmlIni;
      |SI FSalida ] 0;
          nBueno = 1;
          |HAZ ProcesaCabeXML;
      |FINSI;
|FPRC;
|| ///////////////////////////////////////////////////////////////////////
____________________________________/ LECTURA Y CALCULO
|PROCESO proceso;
     #12#0 = #13#0;
     #12#1 = #13#1;
     |LEE 101#12=;
     |SI FSalida ! 0;    |ACABA;   |FINSI;

     #12#5 = "S";
     |GRABA 020#12.a;
     |LIBERA #12;

     |HAZ leedatostrabaja;

     |SI #13#6 > 0;
          |HAZ ordena;
          #0#13 = #2#0;  |PINTA #0#13;
          #0#14 = #2#1;  |PINTA #0#14;

          seleccio = 1;
          |SI swpri = 0;
              |SI nTXML = 0
                  |HAZ ordenante;
                  |HAZ cabecera;
              |SINO;
                  |HAZ OrdenaXML;
                  |SI nBueno = 0;
                      |ERROR10;
                      |ACABA;
                  |FINSI;
              |FINSI;
              swpri = 1;
          |FINSI;
          |SI nTXML = 0
              |HAZ beneficiario;
          |SINO;
              |SI nSwCab = 0; |HAZ InfoPago; |FINSI;
              |HAZ BenefiXML;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO ordena;   || LEE EL TRABAJADOR
|DDEFECTO #2;
    #2#0 = #13#0;
    #2#1 = #13#3;
|LEE 000#2=;
|FPRC;

|PROCESO lee_todo;
|DDEFECTO #11;
|ABRE #11;          || lee el banco transferencia
    #11#0 = #0#9;
|LEE 000#11=;
|CIERRA #11;
|FPRC;

|DEFBUCLE leyendo2;
 #13#3;
 ;
 "            ", #12#0, #12#1,      1;
 "zzzzzzzzzzzz", #12#0, #12#1, 999999;
 ;
 NULL, ordena, NULL, NULL, NULL, proceso;
 #2#0, #2#77, #2#1;
|FIN;

|DEFBUCLE leyendo;
 #13#3;
 ;
 "            ", #12#0, #12#1,      1;
 "zzzzzzzzzzzz", #12#0, #12#1, 999999;
 ;
 NULL, proceso;
|FIN;

|PROCESO cabeceraReg;
     |SI nOpe = 1;
         |SI #12#5 = "S";
             |SI #nomcontr#42 = "S";
                 aAlfa1 = #12#0; aAlfa1 = "00000" + aAlfa1; aAlfa1 = aAlfa1 % -105;
                 aAlfa2 = #12#1; aAlfa2 = "000000" + aAlfa2; aAlfa2 = aAlfa2 % -106;

                 aAlfa = "    ATENCION: Ya se ha generado el fichero correspondiente a la transfe- rencia ";
                 aAlfa = aAlfa + aAlfa2 + " de la empresa ";
                 aAlfa = aAlfa + aAlfa1;
                 |MENAV aAlfa;
                 aAlfa = "    N¿Desea volver a crear la transferencia de este registro?";
                 |CONFI aAlfa;
                 |SI FSalida = 0;
                     #12#5 = "N";
                     |GRABA 020#12a;
                     FSalida = 0;
                 |FINSI;
             |SINO;
                  FSalida = 0;
             |FINSI;
             |SI FSalida = 0;
                 nTRegistros = nTRegistros + #12#3;
                 nTTotal     = nTTotal + #12#2;
             |FINSI;
         |SINO;
              nTRegistros = nTRegistros + #12#3;
              nTTotal     = nTTotal + #12#2;
         |FINSI;
     |FINSI;

     |SI nOpe = 0;
         |SI #12#5 = "S";
             |SI #nomcontr#42 = "N";
                 |SI #0#3 = 1;   |BUCLE leyendo;     |FINSI;
                 |SI #0#3 = 2;   |BUCLE leyendo2;    |FINSI;
             |FINSI;
         |SINO;
             |SI #0#3 = 1;   |BUCLE leyendo;     |FINSI;
             |SI #0#3 = 2;   |BUCLE leyendo2;    |FINSI;
         |FINSI;
     |FINSI;

     |LIBERA #12;
|FPRC;

|DEFBUCLE cabeceraReg;
     #12#1;
     ;
     demp, dreg;
     demp, hreg;
     be;
     NULL, cabeceraReg;
|FIN;

|PROCESO MarcaPagado;
     #nompagli#8 = #0#8;
     #nompagli#11 = "S";
     #nompagli#14 = #0#9;
     |GRABA 020#nompagli.a;
|FPRC;

|DEFBUCLE MarcaPagado;
     #nompagli#2;
     ;
     demp, aDesdeReg, 1900, 01, 0, " ", 00001;
     hemp, aHastaReg, 2999, 12, 9, "z", 99999;
     be;
     NULL, MarcaPagado;
|FIN;

|PROCESO tipo3; |TIPO 3;
     secuenci = #0#15;
     |QBF secuenci;
     |HAZ lee_todo;

     || nTXML = 0; |SI #0#8 ] "01.01.2016"; nTXML = 1; |FINSI;
     || nTXML = 0; |SI #0#39 = "X"; nTXML = 1; |FINSI;
     nTXML = 1;

     |ABRE #nomcontr; |LEE 000#nomcontr.p; |CIERRA #nomcontr;

     |ABRE #2;
     |ABRE #12;

     nTRegistros = 0;
     nTTotal     = 0;

     |SI #0#2 = "D";
          nOpe = 1;
          dreg = #0#0;     hreg = #0#1;
          demp = #0#12;    hemp = #0#12;
          |BUCLE cabeceraReg;
          nOpe = 0;
          dreg = #0#0;     hreg = #0#1;
          demp = #0#12;    hemp = #0#12;
          |BUCLE cabeceraReg;
     |FINSI;
     |SI #0#2 = "S";
          |PARA para1 = 18; |SI para1 [ 27; |HACIENDO para1 = para1 + 1;
               para2 = para1 + 10;
               nOpe = 1;
               dreg = #0para2;   hreg = dreg;
               demp = #0para1;   hemp = demp;
               |BUCLE cabeceraReg;
          |SIGUE;
          |PARA para1 = 18; |SI para1 [ 27; |HACIENDO para1 = para1 + 1;
               para2 = para1 + 10;
               nOpe = 0;
               dreg = #0para2;   hreg = dreg;
               demp = #0para1;   hemp = demp;
               |BUCLE cabeceraReg;
          |SIGUE;
     |FINSI;
     |CIERRA #2;
     |CIERRA #12;
     |SI seleccio = 0;
         |MENAV "    Seleccion vacia ...";
     |SINO;
          aDesdeReg = dreg;
          aDesdeReg = ("000000" + aDesdeReg) % -106;
          aHastaReg = hreg;
          aHastaReg = ("000000" + aHastaReg) % -106;
          |BUCLE MarcaPagado;
          |SI nTXML = 0;
              |HAZ totales;
              |HAZ totales_general;
          |SINO;
              |HAZ XmlFin;
          |FINSI;
          |HAZ final2;
     |FINSI;
|FPRC;

|PROCESO final2;         || copia a disco
     |SI nTXML = 0;
         nume1 = importes;
         nume1 = importes / 100;
     |SINO;
         nume1    = nTTotal;
         registro = nTRegistros;
     |FINSI;
     alfa1 = nume1;     alfa1 = "          " + alfa1;  alfa1 = alfa1 % -110;
     alfa2 = registro;  alfa2 = "          " + alfa2;  alfa2 = alfa2 % -110;
     alfa1 = " Importe total transferencias (euros): " + alfa1 + " ";
     alfa2 = " N£mero de perceptores ..............: " + alfa2 + " ";

     |HAZ aviso1;

     |SI nTXML = 1;
         |IP_REMOTO aIP;
         |SI aIP ! "";
             |ENVIA_FICHERO eaFichero, eaDestino;
         |SINO;
             |COPIA_FICHERO eaFichero, eaDestino;
         |FINSI;
         || falta copiar
     |FINSI;
     aAlfa = #0#15;
     |QBF aAlfa;
     aAlfa =  "    El fichero " + aAlfa + " se ha creado en la ";
     aAlfa = aAlfa + "carpeta " + aRuta;
     |MENAV aAlfa;
|FPRC;

|PROCESO AvisoPago; |TIPO 7;
                || 3 4         5         6         7
                || 8901234567890123456789012345678901234567890
     |PUSHV 12372380;
     |ATRI R;
     |PINTA 1338, " --------------- OPCIONES ---------------- ";
     |PINTA 1438, " (S) las transferencias  se liquidan a los ";
     |PINTA 1538, "     beneficiarios EL MISMO DIA  de la Fe- ";
     |PINTA 1638, "     cha de  Env¡o, previo acuerdo  con el ";
     |PINTA 1738, "     banco emisor.                         ";
     |PINTA 1838, " (N) Es la opci¢n por defecto y la  que se ";
     |PINTA 1938, "     ven¡a usando hasta la introducci¢n de ";
     |PINTA 2038, "     este par metro.Normalmente las trans- ";
     |PINTA 2138, "     ferencias se liquidan el d¡a siguien- ";
     |PINTA 2238, "     te a la fecha de env¡o solicitada.    ";
     |PINTA 2338, " ----------------------------------------- ";
     |ATRI 0;
|FPRC;

|PROCESO RestauraTrans;
     |POPV;
|FPRC;

|PROCESO aviso1;         || desde calculo 'final2'
|PUSHV 05012380;
|HAZ color_f;
|CUADRO 09 20 12 71;
||            1234567890123456789012345678901234567890
|HAZ color_c;
|PINTA 1021, alfa1;
|PINTA 1121, alfa2;
|ATRI 0;
|AVISO;
|FPRC;

|PROCESO color_c;        || activa el color caracter en avisos
|COLOR 14, 0;
|ATRI 0;
|FPRC;

|PROCESO color_f;        || activa el color graficos en avisos
|COLOR 4, 0;
|ATRI 0;
|FPRC;

|PROCESO LaEntrada; |TIPO 8;
     aUsuario = Usuario % 810;
     #0#6 = aUsuario;
     |LEE 000#0=;
     |SI FSalida ! 0;
          |DDEFECTO #0;
          #0#6 = aUsuario;
          |GRABA 020#0b;
     |FINSI;

     aAlfa = #0#5;
     |QBF aAlfa;
     |SI aAlfa = " :\";
          #0#5 = "C:\";
          |PINTA #0#5;
     |FINSI;
|FPRC;

|PROGRAMA;
     |CLS;
     |CABEZA "Creacion Soporte Magnetico";
     |ABRE #0;
     |HAZ LaEntrada;
     |PINPA #0#0;
     |PINDA #0#0;
     |ENDATOS #1#0;
     |SI FSalida = 0;
          |GRABA 020#0a;
          |HAZ tipo3
     |FINSI;
     |CIERRA #0;
|FPRO;
