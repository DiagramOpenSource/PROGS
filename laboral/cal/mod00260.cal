|FICHEROS;
     labtraba;

     gomdetjr;

     mod00243; || cabecera
     mod00244; || lineas

     mod00242; || tipos y precio hora general
     mod00249; || precio hora por trabajador

     nomfical;
|FIN;

|VARIABLES;
     aParam = "";
     nEmp = 0;
     nTra = 0;
     nAny = 0;
     nMes = 0;
     nCampo = 0;
     aAlfa1 = "";
     aAlfa2 = "";
     aAlfa3 = "";
     aFecha = "";
     fFecha = @;
     fFechaBus = @;
     swHayHoras = 0;
     swHayHorasTrab = 0;
     aTipoHora = "";
     nPrecio = 0;
     nNroHoras = 0;
     nPrecioFes = 0;

     fDiaLet = @;
     aDiaLet = "";
     nDiaSem = 0;
     nCampoF = 0;
     nCampoF1 = 0;
     nCampoF2 = 0;
     nCampoF3 = 0;

     swFestivo = 0;
     aAlfa = "";
     nFechaNume = 0;
     nNume1 = 0;
     nNume2 = 0;
     nNume3 = 0;
     swSigue = 0;
     nCalendario = 0;

     nDia = 0;

|FIN;

|PROCESO Calendario;
     fDiaLet = fFecha % 1;
     aDiaLet = fDiaLet;
     aDiaLet = aDiaLet % 1103;
     |SI aDiaLet = "Sab"; |O aDiaLet = "Dom";
          swFestivo = 1;
     |FINSI;

     aFecha = fFecha;
     aAlfa = (aFecha % 402) + (aFecha % 102);
     nFechaNume = aAlfa;

     |PARA nCampoF = 9; |SI nCampoF [ 96; |HACIENDO nCampoF = nCampoF + 3;
          nCampoF1 = nCampoF;
          nCampoF2 = nCampoF + 1;
          nCampoF3 = nCampoF + 2;

          aAlfa1 = #nomfical#nCampoF1#; |QBF aAlfa1;
          aAlfa2 = #nomfical#nCampoF2#; |QBF aAlfa2;
          nNume3 = #nomfical#nCampoF3#;
          |SI aAlfa1 ! ""; |Y aAlfa2 ! ""; |Y nNume3 = 1;
               aAlfa1 = (aAlfa1 % 302) + (aAlfa1 % 102);
               nNume1 = aAlfa1;
               aAlfa2 = (aAlfa2 % 302) + (aAlfa2 % 102);
               nNume2 = aAlfa2;
               |SI nNume1 [ nFechaNume; |Y nFechaNume [ nNume2;
                    swFestivo = 1;
                    |SAL;
               |FINSI;
          |FINSI;
     |SIGUE;

     |SI swFestivo = 0;
          |PARA nCampoF = 99; |SI nCampoF [ 157; |HACIENDO nCampoF = nCampoF + 2;
               nCampoF1 = nCampoF;
               nCampoF3 = nCampoF + 1;

               aAlfa1 = #nomfical#nCampoF1#; |QBF aAlfa1;
               nNume3 = #nomfical#nCampoF3#;
               |SI aAlfa1 ! ""; |Y nNume3 = 1;
                    aAlfa1 = (aAlfa1 % 302) + (aAlfa1 % 102);
                    nNume1 = aAlfa1;
                    |SI nNume1 = nFechaNume;
                         swFestivo = 1;
                         |SAL;
                    |FINSI;
               |FINSI;
          |SIGUE;
     |FINSI;
|FPRC;

|PROCESO BuscaHorasTrab;
     swHayHorasTrab = 1;
     aTipoHora = #mod00249#2;
     nPrecio = #mod00249#3;
     nPrecioFes = #mod00249#4;
     nNroHoras = #mod00249#5;
     |ERROR10;
|FPRC;

|DEFBUCLE BuscaHorasTrab;
     #mod00249#1;
     ;
     nEmp, nTra, " ";
     nEmp, nTra, "z";
     ;
     NULL, BuscaHorasTrab;
|FIN;

|PROCESO BorraHoras;
     |BORRA 020#mod00244.a;
     |LIBERA #mod00244;
|FPRC;

|DEFBUCLE BorraHoras;
     #mod00244#1;
     ;
     nEmp, nTra, fFecha, " ";
     nEmp, nTra, fFecha, "z";
     be;
     NULL, BorraHoras;
|FIN;

|PROCESO BuscaHoras;
     swHayHoras = 1;
|FPRC;

|DEFBUCLE BuscaHoras;
     #mod00244#1;
     ;
     nEmp, nTra, fFecha, " ";
     nEmp, nTra, fFecha, "z";
     ;
     NULL, BuscaHoras;
|FIN;

|PROCESO GrabaDia;
     #mod00244#0 = nEmp;
     #mod00244#1 = nTra;
     #mod00244#2 = fFecha;
     swHayHorasTrab = 0;
     |BUCLE BuscaHorasTrab;
     |SI swHayHorasTrab = 1;
          #mod00244#3 = aTipoHora;

          #mod00242#0 = aTipoHora;
          |LEE 000#mod00242.=;
          |SI FSalida = 0;
               #mod00244#4 = #mod00242#1;
          |FINSI;

          |SI nNroHoras = 0;
               #mod00244#5 = #mod00242#4;
          |SINO;
               #mod00244#5 = nNroHoras;
          |FINSI;

          swFestivo = 0;
          |HAZ Calendario;

          |SI swFestivo = 0;
               #mod00244#6 = nPrecio;
               |SI nPrecio = 0;
                    #mod00242#0 = aTipoHora;
                    |LEE 000#mod00242.=;
                    |SI FSalida = 0;
                         #mod00244#6 = #mod00242#2;
                    |FINSI;
               |FINSI;
          |SINO;
               #mod00244#6 = nPrecioFes;
               |SI nPrecioFes = 0;
                    #mod00242#0 = aTipoHora;
                    |LEE 000#mod00242.=;
                    |SI FSalida = 0;
                         #mod00244#6 = #mod00242#5;
                    |FINSI;
               |FINSI;
          |FINSI;
     |FINSI;
     |GRABA 020#mod00244.n;
     |LIBERA #mod00244;
|FPRC;

|PROCESO Pase;
     || lineas
     |ABRE #gomdetjr;
     |ABRE #mod00242;
     |ABRE #mod00243;
     |ABRE #mod00244;
     |ABRE #mod00249;
     |ABRE #nomfical;
     |ABRE #labtraba;


     swSigue = 1;
     #labtraba#0 = nEmp;
     #labtraba#1 = nTra;
     |LEE 000#labtraba.=;
     |SI FSalida = 0;
          nCalendario = #labtraba#156;
          |SI nCalendario = 00;
               |MENAV "    ATENCION. El trabajador no tiene asignado calendario laboral";
               swSigue = 0;
          |SINO;
               #nomfical#0 = nCalendario;
               aAlfa = nAny;
               #nomfical#1 = aAlfa;
               |LEE 000#nomfical.=;
               |SI FSalida ! 0;
                    aAlfa = nCalendario; |QBF aAlfa; aAlfa = ("00" + aAlfa) % -102;
                    aAlfa = "    ATENCION. No se ha encontrado el calendario laboral " + aAlfa;
                    |MENAV aAlfa;
                    swSigue = 0;
               |FINSI;
          |FINSI;
     |FINSI;

     |SI swSigue = 1;
          || cabecera
          #mod00243#0 = nEmp;
          #mod00243#1 = nTra;
          |LEE 000#mod00243.=;
          |SI FSalida ! 0;
               #mod00243#0 = nEmp;
               #mod00243#1 = nTra;
               |GRABA 020#mod00243.n;
               |LIBERA #mod00243;
          |FINSI;


          #gomdetjr#0 = nEmp;
          #gomdetjr#1 = nTra;
          #gomdetjr#2 = nAny;
          #gomdetjr#3 = nMes;
          |LEE 000#gomdetjr.=;
          |SI FSalida = 0;
               |PARA nDia = 1; |SI nDia [ 31; |HACIENDO nDia = nDia + 1;
                    nCampo = nDia + 3;
                    aAlfa1 = nDia; |QBF aAlfa1; aAlfa1 = ("00" + aAlfa1) % -102;
                    aAlfa2 = nMes; |QBF aAlfa2; aAlfa2 = ("00" + aAlfa2) % -102;
                    aAlfa3 = nAny;
                    aFecha = aAlfa1 + "." + aAlfa2 + "." + aAlfa3;
                    fFecha = aFecha;

                    |DDEFECTO #mod00244;
                    |SI #gomdetjr#nCampo# = "X";
                         swHayHoras = 0;
                         |BUCLE BuscaHoras;
                         |SI swHayHoras = 0;
                              || si hay algo en ese dia lo respeto
                              |HAZ GrabaDia;
                         |FINSI;
                    |SINO;
                         || si el dia no esta marcado como trabajado
                         || borro las horas
                         |BUCLE BorraHoras;
                    |FINSI;
               |SIGUE;
          |FINSI;
     |FINSI;

     |CIERRA #mod00243;
     |CIERRA #labtraba;
     |CIERRA #mod00249;
     |CIERRA #mod00244;
     |CIERRA #mod00242;
     |CIERRA #gomdetjr;
     |CIERRA #nomfical;
|FPRC;

|PROGRAMA;
     aParam = PARAMETRO;
     aAlfa = aParam % 105;
     nEmp = aAlfa;
     aAlfa = aParam % 610;
     nTra = aAlfa;
     aAlfa = aParam % 1104;
     nAny = aAlfa;
     aAlfa = aParam % 1502;
     nMes = aAlfa;


     |HAZ Pase;
|FPRO;
