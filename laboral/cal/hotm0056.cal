|FICHEROS;
     hotm0056 #0;
     labtraba;
     nomtrcal;
     hotm0007;
     hotm0010;
     hotm0011;
     hotm0057 #100;      || tmp movimientos
     hotm0058 #101;      || tmp movimientos intragrupos
     nomcalca;
     nomhicca;

     hotm0060;           || Equivalencia Diagram-Evology Empresas
     hotm0061;           || Equivalencia Diagram-Evology Centros
     hotm0062;           || Equivalencia Diagram-Evology Areas
|FIN;

|VARIABLES;
     nCentro = 0;
     aCentro = "";
     aArea = "";
     aSeccion = "";
     nAntCentro = 0;
     aAntCentro = "";
     aAntArea = "";
     aAntSeccion = "";
     aContrato = "";
     aAntContrato = "";
     nMes = 0;
     nAny = 0;
     aAny = "";
     nEmp = 0;
     nTra = 0;
     nNroTra = 0;
     nDia = 0;
     aDia = "";
     nPesoDia = 0;
     aPesoDia = "";
     nPuntero = 0;
     nCalendario = 0;
     aCalendario = "";
     nJornadas = 0;
     aFechaAlta = "";
     aMesAlta = "";
     nMesAlta = 0;
     aAnyAlta = "";
     nAnyAlta = 0;
     nAltasReales = 0;
     aFechaBaja = "";
     aMesBaja = "";
     nMesBaja = 0;
     aAnyBaja = "";
     nAnyBaja = 0;
     nBajasReales = 0;
     aPeriodo = "";
     aLinea = "";
     aAlfa = "";
     nAltasIntra = 0;
     nBajasIntra = 0;
     nTotalJor = 0;
     aTotalJor = "";
     nTotalTrab = 0;
     nCanal = 0;
     aDNITrab = "";
     aAntDNITrab = "";
     aAltasReales = "";
     aBajasReales = "";
     aPath = "";
     fFecha = @;
     aFecha = "";
     aMes = "";
     swElPrimero = 0;
     nAntEmp = 0;
     nAntTra = 0;
     nDato = 0;
     aDato = "";
     swStop = 0;
     FEstado = 0;
     &aDeCen = "";
     &aACen = "";
     &aDeEmp = "";
     &aAEmp = "";
     swSigue = 0;

     aMesAltaTra = "";
     nMesAltaTra = 0;
     aAnyAltaTra = "";
     nAnyAltaTra = 0;
     swAltaReal = 0;
     aMesBajaTra = "";
     nMesBajaTra = 0;
     aAnyBajaTra = "";
     nAnyBajaTra = 0;
     swBajaReal = 0;
     nEmpresa = 0;
     nAntEmpresa = 0;
     swAltaMes = 0;
     swBajaMes = 0;
     nAntTrab = 0;
     nTrab = 0;
|FIN;

/*
|PROCESO BuscaCalendario;          || para buscar dias laborables, pero no lo
     #labtraba#0 = nEmp;           || voy a usar, voy a coger los dias que
     #labtraba#1 = nTra;           || me vengan en el #hotm0010#12 de momento
     |LEE 000#labtraba.=;
     nCalendario = #labtraba#156;

     #nomtrcal#0 = nCalendario;
     #nomtrcal#1 = aAny;
     |LEE #nomtrcal.=;
     |SI FSalida = 0;
          aCalendario = #nomtrcal#nMes#;
          |PARA nDia = 1; |SI nDia [ 31; |HACIENDO nDia = nDia + 1;
               nPuntero = (nDia * 100) + 1;
               aPesoDia = aCalendario % nPuntero;
               nPesoDia = aPesoDia;
               nLaborables = nLaborables + nPesoDia;
          |SIGUE;
          nLaborables = 31 - nLaborables;
     |FINSI;
|FPRC;
*/

|PROCESO imprime;
     swSigue = 0;
     |SI #0#1 [ #100#3; |Y #100#3 [ #0#3;
          swSigue = 1;
     |SINO;
          |SI #0#1 [ #100#12; |Y #100#12 [ #0#3;
               swSigue = 1;
          |FINSI;
     |FINSI;
     |SI swSigue = 0;
          |ACABA;
     |FINSI;
     |SI #100#12 > 0; |O #100#16 ! "          "; |O #100#17 ! "          ";
          |SI #100#12 = 0;
               aDeCen = "";
          |SINO;
               aDeCen = #100#12;
               aDeCen = ("00" + aDeCen) % -102;
          |FINSI;
          |SI #100#13 = 0;
               aACen = "";
          |SINO;
               aACen = #100#13;
               aACen = ("00" + aACen) % -102;
          |FINSI;
          |SI #100#18 = 0;
               aDeEmp = "";
          |SINO;
               aDeEmp = #100#18;
               aDeEmp = ("00000" + aDeEmp) % -105;
          |FINSI;
          |SI #100#19 = 0;
               aAEmp = "";
          |SINO;
               aAEmp = #100#19;
               aAEmp = ("00000" + aAEmp) % -105;
          |FINSI;
          |PRINT;
     |FINSI;
|FPRC;

|DEFBUCLE comprobacion;
     #100#1;
     ;
     00, "  ", "  ", "   ", "            ", 001, "01.01.1900";
     99, "zz", "zz", "zzz", "zzzzzzzzzzzz", 999, "31.12.2999";
     ;
     NULL, imprime;
|FIN;

|PROCESO ElInforme;
     |CONFI "     Desea imprimir informe de comprobacion de movimientos?";
     |SI FSalida = 0;
          |IMPRE 0;
          |SI FSalida = 0;
               |INFOR "hotm0056";
               |BUCLE comprobacion;
               |PIEINF;
               |FININF;
          |FINSI;
          |FINIMP;
     |FINSI;
|FPRC;

|PROCESO Defecto; |TIPO 7;
     fFecha = ~;
     aFecha = fFecha;
     aMes = aFecha % 402;
     aAny = aFecha % -104;
     nMes = aMes;
     nAny = aAny;
     #0#5 = nMes;
     |PINTA #0#6;
     #0#6 = nAny;
     |PINTA #0#7;
|FPRC;

|PROCESO LaCabecera;
     aLinea = "";

     aAlfa = "CE";
     aLinea = aLinea + aAlfa + "#";
     aAlfa = "AR";
     aLinea = aLinea + aAlfa + "#";
     aAlfa = "SE";
     aLinea = aLinea + aAlfa + "#";
     aAlfa = "CON";
     aLinea = aLinea + aAlfa + "#";
     aAlfa = "PERIOD";
     aLinea = aLinea + aAlfa + "#";
     aAlfa = "PLA";
     aLinea = aLinea + aAlfa + "#";
     aAlfa = "ALT";
     aLinea = aLinea + aAlfa + "#";
     aAlfa = "BAJ";
     aLinea = aLinea + aAlfa + "#";
     aAlfa = "ALI";
     aLinea = aLinea + aAlfa + "#";
     aAlfa = "BAI";
     aLinea = aLinea + aAlfa + "#";
     aAlfa = "JOR";
     aLinea = aLinea + aAlfa;

     aLinea = aLinea + &13 + &10;
     |XGRABA nCanal, aLinea;
|FPRC;

|PROCESO LaLinea;
     aLinea = "";

     nDato = nAntCentro;
     #hotm0061#0 = nDato;
     |LEE 000#hotm0061.=;
     |SI FSalida = 0;
          nDato = #hotm0061#1;
     |FINSI;
     aAlfa = nDato;
     aAlfa = "00" + aAlfa;    aAlfa = aAlfa % -102;
     aLinea = aLinea + aAlfa + "#";

     aDato = aAntArea;
     #hotm0062#0 = aDato;
     |LEE 000#hotm0062.=;
     |SI FSalida = 0;
          aDato = #hotm0062#1;
     |FINSI;
     aLinea = aLinea + aDato + "#";

     aLinea = aLinea + aAntSeccion + "#";

     aLinea = aLinea + aAntContrato + "#";

     aPeriodo = #0#5;
     aPeriodo = aAny + ("00" + aPeriodo) % -102;
     aLinea = aLinea + aPeriodo + "#";

     aAlfa = nTotalTrab;
     aAlfa = ("000" + aAlfa) % -103;
     aLinea = aLinea + aAlfa + "#";

     aAlfa = nAltasReales;
     aAlfa = ("000" + aAlfa) % -103;
     aLinea = aLinea + aAlfa + "#";

     aAlfa = nBajasReales;
     aAlfa = ("000" + aAlfa) % -103;
     aLinea = aLinea + aAlfa + "#";

     aAlfa = nAltasIntra;
     aAlfa = ("000" + aAlfa) % -103;
     aLinea = aLinea + aAlfa + "#";

     aAlfa = nBajasIntra;
     aAlfa = ("000" + aAlfa) % -103;
     aLinea = aLinea + aAlfa + "#";

     aAlfa = nTotalJor;
     aAlfa = ("000" + aAlfa) % -103;
     aLinea = aLinea + aAlfa;

     aLinea = aLinea + &13 + &10;
     |XGRABA nCanal, aLinea;
|FPRC;

|PROCESO ElUltimo;
     #101#0 = nAntCentro;
     #101#1 = aAntArea;
     #101#2 = aAntSeccion;
     |LEE 000#101=;
     |SI FSalida = 0;
          nAltasIntra = #101#3;
          nBajasIntra = #101#4;
     |FINSI;

     |HAZ LaLinea;
|FPRC;

|PROCESO BuscaBajaReal;
     |SI #labtraba#0 ! #100#1; |O #labtraba#1 ! #100#2;
          || no es el mismo que estoy buscando, candidato a baja Real
          aMesAltaTra = #labtraba#36 % 402;
          nMesAltaTra = aMesAltaTra;
          aAnyAltaTra = #labtraba#36 % -104;
          nAnyAltaTra = aAnyAltaTra;
          |SI nMesAltaTra = #0#5; |Y nAnyAltaTra = #0#6;
               swBajaReal = 0;
          |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE BuscaBajaReal;
     #labtraba#2;
     ;
     aDNITrab;
     aDNITrab;
     ;
     NULL, BuscaBajaReal;
|FIN;

|PROCESO BuscaAltaReal;
     |SI #labtraba#0 ! #100#1; |O #labtraba#1 ! #100#2;
          || no es el mismo que estoy buscando, candidato a alta Real
          aMesBajaTra = #labtraba#37 % 402;
          nMesBajaTra = aMesBajaTra;
          aAnyBajaTra = #labtraba#37 % -104;
          nAnyBajaTra = aAnyBajaTra;
          |SI nMesBajaTra = #0#5; |Y nAnyBajaTra = #0#6;
               swAltaReal = 0;
          |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE BuscaAltaReal;
     #labtraba#2;
     ;
     aDNITrab;
     aDNITrab;
     ;
     NULL, BuscaAltaReal;
|FIN;

|PROCESO CuentaTrab;
     aDNITrab = #100#0;
     |QBF aDNITrab;

     nCentro = #100#3;
     aArea = #100#4;
     aSeccion = #100#5;
     aContrato = #100#9;
     |SI nAntCentro = 0; |Y aAntArea = ""; |Y aAntContrato = ""; |Y aAntSeccion = "";
          swElPrimero = 1;
          aAntSeccion = aSeccion;
          aAntArea = aArea;
          nAntCentro = nCentro;
          aAntContrato = aContrato;
     |SINO;
          swElPrimero = 0;
     |FINSI;
     |QBF aArea;
     |QBF aSeccion;
     |QBF aContrato;

     |SI aContrato ! aAntContrato; |O aSeccion ! aAntSeccion;
     |O aArea ! aAntArea; |O nCentro ! nAntCentro;
          |HAZ LaLinea;

          nTotalTrab = 0;
          nTotalJor = 0;
          nAltasReales = 0;
          nBajasReales = 0;
          nAltasIntra = 0;
          nBajasIntra = 0;
     |FINSI;

          #101#0 = nCentro;
          #101#1 = aArea;
          #101#2 = aSeccion;
          |LEE 000#101=;
          |SI FSalida = 0;
               nAltasIntra = #101#3;
               nBajasIntra = #101#4;
          |FINSI;

          aFechaAlta = #100#10;
          |QBF aFechaAlta;
          |SI aFechaAlta ! "";
               aMesAlta = aFechaAlta % 402;
               nMesAlta = aMesAlta;
               aAnyAlta = aFechaAlta % -104;
               nAnyAlta = aAnyAlta;
               |SI nMesAlta = #0#5; |Y nAnyAlta = #0#6;
                    swAltaReal = 1;
                    |BUCLE BuscaAltaReal;
                    |SI swAltaReal = 1;
                         nAltasReales = nAltasReales + 1;
                         #100#16 = aFechaAlta;
                         |GRABA 020#100a;
                    |FINSI;
               |FINSI;
          |FINSI;

          aFechaBaja = #100#11;
          |QBF aFechaBaja;
          |SI aFechaBaja ! "";
               aMesBaja = aFechaBaja % 402;
               nMesBaja = aMesBaja;
               aAnyBaja = aFechaBaja % -104;
               nAnyBaja = aAnyBaja;
               |SI nMesBaja = #0#5; |Y nAnyBaja = #0#6;
                    swBajaReal = 1;
                    |BUCLE BuscaBajaReal;
                    |SI swBajaReal = 1;
                         nBajasReales = nBajasReales + 1;
                         #100#17 = aFechaBaja;
                         |GRABA 020#100a;
                    |FINSI;
               |FINSI;
          |FINSI;

          nTotalTrab = nTotalTrab + 1;
          nTotalJor = nTotalJor + #100#6;
     ||FINSI;

     aAntDNITrab = aDNITrab;
     nAntCentro = nCentro;
     aAntArea = aArea;
     aAntSeccion = aSeccion;
     aAntContrato = aContrato;
|FPRC;

|DEFBUCLE CuentaTrab;
     #100#1;
     ;
     #0#1, #0#2, "  ", "   ", "            ", 001, "01.01.1900";
     #0#3, #0#4, "zz", "zzz", "zzzzzzzzzzzz", 999, "31.12.2999";
     ;
     NULL, CuentaTrab;
|FIN;

|PROCESO Generacion;
     nCentro = 0;
     aArea = "";
     aSeccion = "";
     nAntCentro = 0;
     aAntArea = "";
     aAntSeccion = "";
     nTotalTrab = 0;
     nTotalJor = 0;
     aAltasReales = 0;
     aBajasReales = 0
     aPath = #0#7;
     |QBF aPath;

     |XABRE "CBW", aPath, nCanal;

     |ABRE #hotm0060;
     |ABRE #hotm0061;
     |ABRE #hotm0062;

     |HAZ LaCabecera;
     |BUCLE CuentaTrab;
     |HAZ ElUltimo;

     |CIERRA #hotm0062;
     |CIERRA #hotm0061;
     |CIERRA #hotm0060;

     |XCIERRA nCanal;
|FPRC;

|PROCESO Intragrupos;
     aDNITrab = #100#0;
     |QBF aDNITrab;
     nEmpresa = #100#1;
     nTrab = #100#2;
     nCentro = #100#3;
     aArea = #100#4;
     aSeccion = #100#5;

     |SI aDNITrab = aAntDNITrab;   || Dos trabajadores en el mismo periodo

          swAltaMes = 0;
          aFechaAlta = #100#10;
          |QBF aFechaAlta;
          |SI aFechaAlta ! "";
               aMesAlta = aFechaAlta % 402;
               nMesAlta = aMesAlta;
               aAnyAlta = aFechaAlta % -104;
               nAnyAlta = aAnyAlta;
               |SI nMesAlta = #0#5; |Y nAnyAlta = #0#6;
                    swAltaMes = 1;
               |FINSI;
          |FINSI;

          swBajaMes = 0;
          aFechaBaja = #100#11;
          |QBF aFechaBaja;
          |SI aFechaBaja ! "";
               aMesBaja = aFechaBaja % 402;
               nMesBaja = aMesBaja;
               aAnyBaja = aFechaBaja % -104;
               nAnyBaja = aAnyBaja;
               |SI nMesBaja = #0#5; |Y nAnyBaja = #0#6;
                    swBajaMes = 1;
               |FINSI;
          |FINSI;


          || SI swAltaMes ! 1; |O swBajaMes ! 1;
          || SI swAltaMes ! 1;
               || ACABA;        || tiene que se alta y baja en este periodo para
          || FINSI;             || considerarse movimiento intragrupo


          || SI nEmpresa ! nAntEmpresa;   || Estoy en dos empresas distintas
          |SI swAltaMes = 1; |Y nTrab ! nAntTrab;
               |SI nCentro ! nAntCentro;     || Ademas en Centros Distintos
               |O aArea ! aAntArea;          || Grabo temporal:
                    |DDEFECTO #101;
                    #101#0 = nCentro;        || Centro
                    #101#1 = aArea;          || Area
                    |LEE 101#101=;
                    |SI FSalida ! 0;         || el Alta
                         #101#2 = aSeccion;       || Seccion
                         #101#3 = 1;
                         |GRABA 020#101n;
                    |SINO;
                         #101#2 = aSeccion;       || Seccion
                         #101#3 = #101#3 + 1;
                         |GRABA 020#101a;
                    |FINSI;

                    |DDEFECTO #101;
                    #101#0 = nAntCentro;        || Centro
                    #101#1 = aAntArea;          || Area
                    |LEE 101#101=;
                    |SI FSalida ! 0;            || la Baja
                         #101#4 = 1;
                         #101#2 = aAntSeccion;       || Seccion
                         |GRABA 020#101n;
                    |SINO;
                         #101#4 = #101#4+ 1;
                         #101#2 = aAntSeccion;       || Seccion
                         |GRABA 020#101a
                    |FINSI;

                    #100#12 = nAntCentro;
                    #100#13 = nCentro;
                    #100#14 = aAntArea;
                    #100#15 = aArea;
                    #100#18 = nAntEmpresa;
                    #100#19 = nEmpresa;
                    |GRABA 020#100a;
               |FINSI;
          |FINSI;
     |FINSI;

     aAntDNITrab = aDNITrab;
     nAntCentro = nCentro;
     nAntEmpresa = nEmpresa;
     nAntTrab = nTrab;
     aAntArea = aArea;
     aAntSeccion = aSeccion;
|FPRC;

|DEFBUCLE Intragrupos;
     #100#2;
     ;
     "            ", "01.01.1900", 001;
     "zzzzzzzzzzzz", "31.12.2999", 999;
     be;
     NULL, Intragrupos;
|FIN;

|PROCESO Compruebamelon;
     #nomhicca#0 = #hotm0010#0;
     #nomhicca#1 = #hotm0010#1;
     #nomhicca#2 = #0#5;
     #nomhicca#3 = 0;
     #nomhicca#66 = #0#6 - 2000;
     |LEE 000#nomhicca.=;
     |SI FSalida ! 0;
          #nomcalca#0 = #hotm0010#0;
          #nomcalca#1 = #hotm0010#1;
          #nomcalca#2 = #0#5;
          #nomcalca#3 = 0;
          |LEE 000#nomcalca.=;
          |SI FSalida ! 0;
               swStop = 1;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO GrabaTmp;
     swStop = 0;
     |HAZ Compruebamelon;
     |SI swStop = 1;
          |ACABA;
     |FINSI;

     #labtraba#0 = #hotm0010#0;
     #labtraba#1 = #hotm0010#1;
     |LEE 000#labtraba.=;
     |SI FSalida = 0;
          #100#3 = #hotm0010#5;         || Centro
          #100#4 = #hotm0010#7;         || Area
          #100#5 = #hotm0010#9;         || Seccion
          #100#0 = #labtraba#7;         || DNI
          #100#7 = #hotm0010#4;         || Linea hotm0010
          #100#10 = #labtraba#36;       || Fecha Alta
          |LEE 000#100=;
          FEstado = FSalida;

          #100#1 = #hotm0010#0;         || Empresa
          #100#2 = #hotm0010#1;         || Trabajador
          |SI FEstado ! 0;
               #100#6 = + #hotm0010#12;        || Dias Trabajados
          |SINO;
               #100#6 = #100#6 + #hotm0010#12;        || Dias Trabajados
          |FINSI;
          #100#8 = #hotm0010#11;        || Coeficiente
          #100#9 = #labtraba#45;        || Contrato
          aAlfa = #labtraba#36;        || Fecha Alta

          ||SI #100#1 ! nAntEmp; |O #100#2 ! nAntTra;
               |SI aAlfa = "..........";     aAlfa = "";    |FINSI;
               |QBF aAlfa;
               #100#10 = aAlfa;

               aAlfa = #labtraba#37;        || Fecha Baja
               |SI aAlfa = "..........";     aAlfa = "";    |FINSI;
               |QBF aAlfa;
               #100#11 = aAlfa;
          ||FINSI;

          |SI FEstado ! 0;
               |GRABA 000#100n;
          |SINO;
               |GRABA 000#100a;
          |FINSI;

          nAntEmp = #100#1;
          nAntTra = #100#2;
     |FINSI;
|FPRC;

|DEFBUCLE GrabaTmp;
     #hotm0010#1;
     ;
     00001, 00001, aAny, nMes, 001;
     99999, 99999, aAny, nMes, 999;
     ;
     NULL, GrabaTmp;
|FIN;

/*
|DEFBUCLE GrabaTmp;
     #hotm0010#1;
     5, 7, 9;
     00001, 00001, aAny, nMes, 001, nCentro, aArea, "  ";
     99999, 99999, aAny, nMes, 999, nCentro, aArea, "zz";
     ;
     NULL, GrabaTmp;
|FIN;
*/

|PROCESO hotm0007;
     nNroTra = 0;
     nJornadas = 0;
     nCentro = #hotm0007#0;
     aArea = #hotm0007#1;
     || aSeccion = #hotm0008#2;
     nMes = #0#5;
     nAny = #0#6;
     aAny = nAny;

     |BUCLE GrabaTmp;
     |BUCLE Intragrupos;
     |HAZ Generacion;
|FPRC;

/*
|DEFBUCLE hotm0007;
     #hotm0007#1;
     ;
     01, "  ";
     99, "zz";
     ;
     NULL, hotm0007;
|FIN;
*/

|PROCESO tipo3;
     |ABRE #labtraba;
     |ABRE #nomtrcal;
     |ABRE #nomcalca;
     |ABRE #nomhicca;

     aAlfa = Usuario; |QBF aAlfa; aAlfa = "h" + aAlfa;
     |NOME_DAT #100 aAlfa;
     |DELINDEX #100;
     |ABRE #100;

     aAlfa = Usuario; |QBF aAlfa; aAlfa = "i" + aAlfa;
     |NOME_DAT #101 aAlfa;
     |DELINDEX #101;
     |ABRE #101;

     |HAZ hotm0007;           || Centros/Areas
     |HAZ ElInforme;          || Informe de Comprobacion
     |CIERRA #101;
     |CIERRA #100;
     |CIERRA #nomtrcal;
     |CIERRA #labtraba;
     |CIERRA #nomcalca;
     |CIERRA #nomhicca;
|FPRC;

|PROGRAMA;
     |CLS;
     |ABRE #0;
     #0#0 = 1;
     |LEE 101#0=;
     |SI FSalida ! 0;
          |GRABA 020#0.n;
     |FINSI;

     |PINPA #0#0;
     |PINDA #0#0;
     |ENDATOS #1#0;
     |SI FSalida = 0;
          |GRABA 020#0.a;
          |HAZ tipo3;
     |FINSI;
     |CIERRA #0;
|FPRO;
