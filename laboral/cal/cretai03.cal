|FICHEROS;
     cretai03 #0;
     cretam03;
     cretam04;
     cretam22;
     cretam23;
     cretam24;
     cretat03 #100;
     cretat06 #101;

     cretam16;
     cretam17;
|FIN;

|VARIABLES;
     &nError = 0;
     nCampo = 0;
     nCampo1 = 0;
     nCampo2 = 0;
     FEstado = 0;
     aAlfa = "";
     aAntCCC = "";

     fFecha = @;
     aFecha = "";
     nMes = 0;
     nAny = 0;
     aReferencia = "";
     nNume = 0;
     nTramos = 0;
     swBucle = 0;
     swHayBase = 0;
|FIN;

|PROCESO Defectos;
     fFecha = ~;
     aFecha = fFecha;
     aAlfa = aFecha % -104;
     nAny = aAlfa;

     aAlfa = aFecha % 402;
     nMes = aAlfa;
     nMes = nMes - 1;
     |SI nMes = 0;
          nMes = 12;
          nAny = nAny - 1;
     |FINSI;

     #0#2 = nAny;
     #0#3 = nMes;
     |PINTA #0#2;
     |PINTA #0#3;
|FPRC;

|PROCESO ElIMPRE;
     Impresora = "CrystalReport";
     |IMPRE -1;
     |SI FSalida = 0;
          |INFOR "cretai03";
     |FINSI;
|FPRC;

|PROCESO ElFINIMP;
     |PIEINF;
     |FININF;

     |FINIMP;
|FPRC;

|PROCESO ImprimeErrores;
     |SI swBucle = 2;
          #cretat06#0 = #cretat03#5;
          |LEE 000#cretat06.=;
          |SI FSalida = 0;
               || este trabajador tenia otro tipo de errores, de lo que grabo
               || en el cretat06, asi que no lo saco otra vez
               |ACABA;
          |FINSI;
     |FINSI;

     |SI nTramos = 0;
          |HAZ ElIMPRE;
     |FINSI;
     |PRINT;
     nTramos = nTramos + 1;
|FPRC;

|DEFBUCLE cretat03_01;
     #cretat03#1;
     14;
     00001, 2014, 01, 00, "               ", "            ", 01, 01;
     99999, 2099, 12, 20, "zzzzzzzzzzzzzzz", "zzzzzzzzzzzz", 31, 01;
     ;
     NULL, ImprimeErrores;
|FIN;

|DEFBUCLE ImprimeErrores;
     #cretat03#1;
     ;
     00001, 2014, 01, 00, "               ", #cretat06#0, 01;
     99999, 2099, 12, 20, "zzzzzzzzzzzzzzz", #cretat06#0, 31;
     ;
     NULL, ImprimeErrores;
|FIN;

|PROCESO cretat06;
     |BUCLE ImprimeErrores;
|FPRC;

|DEFBUCLE cretat06;
     #cretat06#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, cretat06;
|FIN;

|PROCESO ImprimeTodos;
     |SI nTramos = 0;
          |HAZ ElIMPRE;
     |FINSI;
     |PRINT;
     nTramos = nTramos + 1;
|FPRC;

|DEFBUCLE cretat03;
     #cretat03#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, ImprimeTodos;
|FIN;

|PROCESO Peculiaridades;
     nNume = #cretam24#nCampo1#;
     |SI nNume ! 0;
          #cretam16#0 = #cretam24#nCampo1#;
          |LEE 000#cretam16.=;
          |SI FSalida = 0;
               #cretat03#nCampo2# = #cretam24#nCampo1#;
               nCampo2 = nCampo2 + 1;
               #cretat03#nCampo2# = #cretam16#1;
          |FINSI;

          nCampo1 = nCampo1 + 1;
          nCampo2 = nCampo2 + 1;
          #cretam17#0 = #cretam24#nCampo1#;
          |LEE 000#cretam17.=;
          |SI FSalida = 0;
               #cretat03#nCampo2# = #cretam24#nCampo2#;
               nCampo2 = nCampo2 + 1;
               #cretat03#nCampo2# = #cretam17#1;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO cretam24;
     |DDEFECTO #cretat03;

     #cretat03#0 = #cretam24#0;
     #cretat03#1 = #0#2;
     #cretat03#2 = #0#3;
     #cretat03#3 = 00;
     #cretat03#4 = #cretam24#4;
     #cretat03#5 = #cretam24#5;

     #cretat03#6 = #cretam24#6;
     |LEE 101#cretat03.=;
     FEstado = FSalida;

     #cretat03#7 = #cretam23#6;
     #cretat03#8 = #cretam23#9;
     #cretat03#9 = #cretam23#8;

     #cretat03#12 = #cretam24#8;
     #cretat03#13 = #cretam24#9;

     #cretat03#16 = #cretam23#11;
     |SI #cretat03#16 = 0;
          #cretat03#8 = "TRABAJADOR NO ENCONTRADO";
          #cretat03#40 = 91;
          #cretat03#41 = "No se ha encontrado el NAF del trabajador entre los trabajadores de la empresa";
     |FINSI;

     #cretat03#17 = #cretam23#7;
     #cretat03#18 = #cretam23#10;

     nCampo1 = 74; nCampo2 = 20; |HAZ Peculiaridades;
     nCampo1 = 76; nCampo2 = 24; |HAZ Peculiaridades;
     nCampo1 = 78; nCampo2 = 28; |HAZ Peculiaridades;
     nCampo1 = 80; nCampo2 = 32; |HAZ Peculiaridades;
     nCampo1 = 82; nCampo2 = 36; |HAZ Peculiaridades;

     |SI #cretat03#10 = #cretat03#12; |Y #cretat03#11 = #cretat03#13;
          #cretat03#14 = 00;
          #cretat03#15 = "Tramo correcto";
     |FINSI;

     |SI #cretat03#10 = #cretat03#12; |Y #cretat03#11 ! #cretat03#13;
          #cretat03#14 = 02;
          #cretat03#15 = "ERROR: Tramo encontrado, pero con distinta fecha de fin";
     |FINSI;

     |SI #cretat03#10 ! "01.01.0000"; |Y #cretat03#11 ! "01.01.0000"
          |SI #cretat03#12 = "01.01.0000"; |Y #cretat03#13 = "01.01.0000";
               #cretat03#14 = 03;
               #cretat03#15 = "ERROR: Tramo no encontrado en el fichero de trabajadores y tramos";
          |FINSI;
     |FINSI;

     |SI #cretat03#12 ! "01.01.0000"; |Y #cretat03#13 ! "01.01.0000"
          |SI #cretat03#10 = "01.01.0000"; |Y #cretat03#11 = "01.01.0000";
               |PARA nCampo = 0; |SI nCampo [ 6; |HACIENDO nCampo = nCampo + 1;
                    #cretam04#nCampo# = #cretat03#nCampo#;
               |SIGUE;
               |LEE 000#cretam04.=;
               |SI FSalida = 0;
                    || puede ser un tramo con bases a cero, que aparezca en el fichero
                    || pero que no se haya grabado por tener base a cero, en este
                    || caso no hay que dar error
                    #cretat03#10 = #cretam04#8;
                    #cretat03#11 = #cretam04#9;
               |SINO;
                    #cretat03#14 = 04;
                    #cretat03#15 = "ERROR: Tramo no encontrado en los datos de la liquidaci¢n calculada";
               |FINSI;
          |FINSI;
     |FINSI;

     |SI FEstado = 0;
          |GRABA 020#cretat03.a;
     |SINO;
          |GRABA 020#cretat03.n;
     |FINSI;

     || esto lo hago porque si un trabajador tiene error en un tramo, quiero
     || que en el informe salgan TODOS los tramos del trabajador, no solo
     || el que tiene el error, lo que hago es guardarme un fichero con los
     || NAF que tienen algun error para despues imprimir haciendo un bucle sobre
     || este fichero (cretat06) si pido solo errores

     |SI #cretat03#14 ! 00; |O #cretat03#40 ! 00;
          #cretat06#0 = #cretat03#5;
          |LEE 101#cretat06.=;
          |SI FSalida ! 0;
               |GRABA 020#cretat06.n;
               |LIBERA #cretat06;
          |FINSI;
     |FINSI;

     |LIBERA #cretat03;
|FPRC;

|DEFBUCLE cretam24;
     #cretam24#1;
     ;
     #cretam23#0, #cretam23#1, #cretam23#2, #cretam23#3, #cretam23#4, #cretam23#5, 01, "        ";
     #cretam23#0, #cretam23#1, #cretam23#2, #cretam23#3, #cretam23#4, #cretam23#5, 99, "zzzzzzzz";
     ;
     NULL, cretam24;
|FIN;

|PROCESO cretam23;
     |BUCLE cretam24;
|FPRC;

|DEFBUCLE cretam23;
     #cretam23#1;
     ;
     00000, #0#2, #0#3, 00, aAntCCC, "            ", aReferencia;
     99999, #0#2, #0#3, 00, aAntCCC, "zzzzzzzzzzzz", aReferencia;
     ;
     NULL, cretam23;
|FIN;

|PROCESO cretam04;
     || compruebo que exista alguna base que no este a cero

     swHayBase = 0;
     |PARA nCampo = 31; |SI nCampo [ 49; |HACIENDO nCampo = nCampo + 2;
          |SI #cretam04#nCampo# ! 0;
               swHayBase = 1;
               |SAL;
          |FINSI;
     |SIGUE;

     |SI swHayBase = 0; |Y #cretam04#27 = "ALT";
          |ACABA;
     |FINSI;
     |SI #cretam04#27 = "HUE"; |O #cretam04#27 = "INA";
          |ACABA;
     |FINSI;

     |DDEFECTO #cretat03;

     #cretat03#0 = #cretam03#0;
     #cretat03#1 = #0#2;
     #cretat03#2 = #0#3;
     #cretat03#3 = 00;
     #cretat03#4 = #cretam03#4;
     #cretat03#5 = #cretam03#5;

     #cretat03#6 = #cretam04#6;
     |LEE 101#cretat03.=;
     FEstado = FSalida;

     #cretat03#7 = #cretam03#6;
     #cretat03#8 = #cretam03#9;
     #cretat03#9 = #cretam03#8;

     #cretat03#10 = #cretam04#8;
     #cretat03#11 = #cretam04#9;

     #cretat03#14 = 01;
     #cretat03#15 = "ERROR: Tramo no encontrado en fichero de trabajadores y tramos";

     #cretat03#16 = #cretam03#11;
     #cretat03#17 = #cretam03#7;
     #cretat03#18 = #cretam03#10;

     |SI FEstado = 0;
          |GRABA 020#cretat03.a;
     |SINO;
          |GRABA 020#cretat03.n;
     |FINSI;

     |LIBERA #cretat03;
|FPRC;

|DEFBUCLE cretam04;
     #cretam04#1;
     ;
     #cretam03#0, #cretam03#1, #cretam03#2, #cretam03#3, #cretam03#4, #cretam03#5, 01;
     #cretam03#0, #cretam03#1, #cretam03#2, #cretam03#3, #cretam03#4, #cretam03#5, 99;
     ;
     NULL, cretam04;
|FIN;

|PROCESO cretam22;
     aReferencia = #cretam22#13;
|FPRC;

|DEFBUCLE cretam22;
     #cretam22#1;
     ;
     00000, #0#2, #0#3, 00, aAntCCC, "        ";
     99999, #0#2, #0#3, 00, aAntCCC, "zzzzzzzz";
     ;
     NULL, cretam22;
|FIN;

|PROCESO cretam03;
     |SI aAntCCC ! #cretam03#4;
          || primero busco cual es la ultima respuesta recibida de este CCC
          aReferencia = "";
          |BUCLE cretam22;

          || ahora ya puedo buscar la respuesta que lei (la ultima)
          |BUCLE cretam23;
     |FINSI;

     |BUCLE cretam04;

     aAntCCC = #cretam03#4;
|FPRC;

|DEFBUCLE cretam03;
     #cretam03#1;
     ;
     #0#0, #0#2, #0#3, 00, "               ", "            ";
     #0#1, #0#2, #0#3, 00, "zzzzzzzzzzzzzzz", "zzzzzzzzzzzz";
     ;
     NULL, cretam03;
|FIN;

|PROCESO Compara;
     |BUCLE cretam03;

     || primero busco cual es la ultima respuesta recibida de este CCC
     aReferencia = "";
     |BUCLE cretam22;
     |BUCLE cretam23;
|FPRC;

|PROGRAMA;
     |CLS;
     |PINPA #0#0;

     |ABRE #0;
     |DDEFECTO #0;
     |LEE 000#0p;
     |SI FSalida = 0;
          |BORRA 020#0a;
     |SINO;
          |HAZ Defectos;
     |FINSI;

     |PINDA #0#0;

     |ENDATOS #1#0;
     |SI FSalida = 0;
          aAlfa = Usuario; |QBT aAlfa; aAlfa = ("c" + aAlfa) % 108;
          |NOME_DAT #100 aAlfa;
          |DELINDEX #cretat03;

          aAlfa = Usuario; |QBT aAlfa; aAlfa = ("d" + aAlfa) % 108;
          |NOME_DAT #101 aAlfa;
          |DELINDEX #cretat06;

          |ABRE #cretat03;
          |ABRE #cretat06;
          |ABRE #cretam03;
          |ABRE #cretam04;
          |ABRE #cretam23;
          |ABRE #cretam24;
          |ABRE #cretam16;
          |ABRE #cretam17;

          |HAZ Compara;

          |CIERRA #cretam17;
          |CIERRA #cretam16;
          |CIERRA #cretam24;
          |CIERRA #cretam23;
          |CIERRA #cretam04;
          |CIERRA #cretam03;

          |LEE 000#100.p;
          |SI FSalida = 0;
               ||CONSULTA_CLAVE #1#100;

               || IMPRESION
               |SI #0#4 = "T";
                    |BUCLE cretat03;
               |FINSI;
               |SI #0#4 = "E";
                    ||CONSULTA_CLAVE #1#cretat06;

                    swBucle = 1;
                    |BUCLE cretat06;
                    || ahora imprimo los que tienen el error 01, que no se han
                    || encontrado (estos no lo podia meter en el cretat06)

                    swBucle = 2;
                    |BUCLE cretat03_01;
               |FINSI;

               |SI nTramos ! 0;
                    |HAZ ElFINIMP;
               |SINO;
                    aAlfa = "    No se han encontrado errores en la conciliación ";
                    aAlfa = aAlfa + "de los tramos";
                    |MENAV aAlfa;
               |FINSI;
          |SINO;
               aAlfa = "    No se han encontrado errores en la conciliación ";
               aAlfa = aAlfa + "de los tramos";
               |MENAV aAlfa;
          |FINSI;

          |CIERRA #cretat03;
          |DELINDEX #cretat03;

          |CIERRA #cretat06;
          |DELINDEX #cretat06;
     |FINSI;

     |GRABA 000#0n;
     |CIERRA #0;
|FPRO;
