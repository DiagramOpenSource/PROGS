|FICHEROS;
     nomfical #0;
     nomtrcal #1;
     labmunic;
     nomfiest;
     :integral/dsam0101 #101;
|FIN;

|VARIABLES;
     dianum = 0;
     mesnum = 0;
     swbien = 0;
     topemes = 0;
     nume1 = 0;
     diades = 0;
     mesdes = 0;
     diahas = 0;
     meshas = 0;
     resto = 0;
     bisiesto = 0;
     swentra = 0;
     semana1 = 0;
     campo = 0;
     desde = 0;
     hasta = 0;
     pesos = 0;
     mesact = 0;
     topemes1 = 0;
     topemes2 = 0;
     mid = 0;
     diafij = 0;
     mesfij = 0;
     mid_1 = 0;
     mid_2 = 0;
     errorfec1 = "     ATENCION !!! Fecha erronea / Formato (DDMM) ...";
     errorfec2 = "     ATENCION !!! Error de entrada ...";
     fecha  = @;
     alfa1  = "";
     alfa2  = "";
     alfa3  = "";
     mesalf = "";   || calculo lib_0
     clave  = "";

     swExiste = 0;
     aCodPobla = "";
     aAlfa = "";
     aCodPostal = "";
     nAut = 0;
     aFecha = "";
     nCampoF = 0;
     nPro = 0;
     aPro = "";
     nPob = 0;
     aPob = "";
     aAbre = "";
     nAct = 0;
     nCampo = 0;
     nCampo2 = 0;
     nCampo3 = 0;
     nCampoD = 0;
     nCampoH = 0;
     nCampoP = 0;
     nHandle = 0;

     enBoton = 0;
     nOpcion = 0;
     swBotonSiNo = 0;
     aAlfa1 = "";

     swSigue = 0;
     nModo = 0;
     swNoMensa = 0;
     aDesdeMuni = "";
     aHastaMuni = "";
     aCodMuni = "";
|FIN;


|| *************************************************************************
|| ****** calculos desde el mantenimiento **********************************
|| *************************************************************************


|PROCESO BuscaPob;
     swExiste = 1;
     |ERROR10;
|FPRC;

|DEFBUCLE BuscaPob;
     #labmunic#3;
     ;
     aCodPobla, "00000";
     aCodPobla, "99999";
     ;
     NULL, BuscaPob;
|FIN;

|PROCESO Poblacion;
     swExiste = 0;
     aCodPobla = #0#159;
     aCodPobla = "00000" + aCodPobla;
     aCodPobla = aCodPobla % -105;

     |BUCLE BuscaPob;
     |SI swExiste  = 1; |Y #0#159 ! 0;
          #0#159 = #labmunic#1;
          |PINTA #0#159;
          || #0#160 = #labmunic#4;
          || PINTA #0#160;
          aAlfa = #labmunic#3; |QBF aAlfa;
          aAlfa1 = #labmunic#2; |QBF aAlfa1;
          #0#161 = aAlfa + " (" + aAlfa1 + ")";
          |PINTA #0#161;
     |SINO;
          |SI #0#159 ! 0;
               aAlfa = "    Municipio no encontrado en el fichero general";
               aAlfa = aAlfa + " de Municipios.";
               |SI swNoMensa = 0;
                    |MENAV aAlfa;
                    |ERROR;
               |FINSI;
          |SINO;
               #0#161 = "No asignado a municipio";
               |PINTA #0#161;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO PintaPob; |TIPO 13;
     || para los que ya lo tuvieran introducidos
     aAlfa = #0#161;
     |QBF aAlfa;
     |SI #0#159 ! 0; |Y aAlfa = "";
          swNoMensa = 1;
          |HAZ Poblacion;
          swNoMensa = 0;
     |FINSI;
|FPRC;

|PROCESO BuscaCodPostal;
     swExiste = 1;
     |ERROR10;
|FPRC;

|DEFBUCLE BuscaCodPostal;
     #labmunic#1;
     ;
     aCodPostal, aDesdeMuni;
     aCodPostal, aHastaMuni;
     ;
     NULL, BuscaCodPostal;
|FIN;

|PROCESO CodigoPostal;
     swExiste = 0;
     aCodPostal = #0#160;
     aCodPostal = "00000" + aCodPostal;
     aCodPostal = aCodPostal % -105;

     aCodMuni = #0#159;
     aCodMuni = "00000" + aCodMuni;
     aCodMuni = aCodMuni % -105;

     |SI #0#159 = 0;
          aDesdeMuni = "00000";
          aHastaMuni = "99999";
     |SINO;
          |SI #0Campo = Contenido;
               aDesdeMuni = aCodMuni;
               aHastaMuni = aCodMuni;
          |SINO;
               aDesdeMuni = "00000";
               aHastaMuni = "99999";
          |FINSI;
     |FINSI;

     |BUCLE BuscaCodPostal;

     |SI swExiste = 1; |Y #0#160 ! 0;
          #0#159 = #labmunic#1;
          |PINTA #0#159;
          #0#160 = #labmunic#4;
          |PINTA #0#160;
          aAlfa = #labmunic#3; |QBF aAlfa;
          aAlfa1 = #labmunic#2; |QBF aAlfa1;
          #0#161 = aAlfa + " (" + aAlfa1 + ")";
          |PINTA #0#161;
     |SINO;
          |SI #0#160 ! 0
               aAlfa = "    No se ha encontrado un municipio para este Cod.";
               aAlfa = aAlfa + " Postal en la tabla de Municipios.";
               |MENAV aAlfa;
               |ERROR;
          |SINO;
               |SI #0#159 = 0
                    #0#161 = "No asignado a municipio";
                    |PINTA #0#161;
               |FINSI;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO minimo;
     #labmunic#4 = "00000";
     #labmunic#1 = "00000";
|FPRC;

|PROCESO maximo;
     #labmunic#4 = "99999";
     #labmunic#1 = "99999";
|FPRC;

|PROCESO Municipios; |TIPO 66;
     |ABRE #labmunic;
     |CONSULTA_F_CLAVE #1#labmunic, minimo, maximo;
     |SI FSalida = 0;
          #0#159 = #labmunic#1;
          |PINTA #0#159;
          #0#160 = #labmunic#4;
          |PINTA #0#160;
          aAlfa = #labmunic#3; |QBF aAlfa;
          aAlfa1 = #labmunic#2; |QBF aAlfa1;
          #0#161 = aAlfa + " (" + aAlfa1 + ")";
          |PINTA #0#161;
          |ERROR;
     |FINSI;
     |CIERRA #labmunic;
|FPRC;

|PROCESO MunicipiosF5;
     |SI FSalida = 13;
          |HAZ Municipios;
     |FINSI;
|FPRC;

/***** PROCESOS PARA BUSQUEDA DE FIESTAS PARA RELLENADO DE CALENDARIOS ******/

|PROCESO ayuda1; |TIPO 7;
     |PUSHV 05301280;
     |ATRI R;
     ||             4         5         6         7
     ||            90123456789012345768901234567890123456
     |PINTA 0639, "Introduzca el  codigo de Poblacion  al";
     |PINTA 0739, "que corresponda  el calendario o pulse";
     |PINTA 0839, "<F5> para consultar por Codigo Postal.";
     |PINTA 0939, "Con 00000 (por defecto) no se asignara";
     |PINTA 1039, "este calendario a ningun municipio.   ";
     |ATRI 0;
|FPRC;

|PROCESO Restaura;
     |POPV;
|FPRC;

|PROCESO BuscaPob2;
     swExiste   = 1;
     aCodPostal = #labmunic#4;
     aPro = aCodPostal % 102;
     aPob = aCodPostal % -103;

     |DBASS aAbre;  |QBF aAbre;
     aAbre = aAbre + "integral/def/dsam0101.mas";
     |XABRE "E", aAbre, nHandle;
     |SI FSalida < 0;  |ACABA;  |FINSI;

     nPro = aPro;
     nPob = aPob;
     #101#0 = nPro;
     #101#1 = nPob;
     |LEE 000#101=;
     |SI FSalida = 0;
          nAut = #101#4;
          |ERROR10;
     |FINSI;
|FPRC;

|DEFBUCLE BuscaPob2;
     #labmunic#3;
     ;
     aCodPobla, "00000";
     aCodPobla, "99999";
     ;
     NULL, BuscaPob2;
|FIN;

|PROCESO Fiestas;
     aFecha = #nomfiest#8;
     aFecha = aFecha - ".";

     #nomfical#nCampoD# = aFecha;
     #nomfical#nCampoH# = aFecha;
     #nomfical#nCampoP# = 1;

     |PINTA #nomfical#nCampoD#;
     |PINTA #nomfical#nCampoH#;
     |PINTA #nomfical#nCampoP#;

     nCampoD = nCampoD + 3;
     nCampoH = nCampoH + 3;
     nCampoP = nCampoP + 3;
|FPRC;

|DEFBUCLE Fiestas;
     #nomfiest#1;
     ;
     nAut, nPro, nPob, nAct, "    ";
     nAut, nPro, nPob, nAct, "zzzz";
     ;
     NULL, Fiestas;
|FIN;

|PROCESO PonFiestas;
     |ABRE #101;
     |PARA nCampo = 9; |SI nCampo [ 96; |HACIENDO nCampo = nCampo + 3;
          nCampo2 = nCampo + 1;
          nCampo3 = nCampo + 2;
          #nomfical#nCampo# = "    ";
          #nomfical#nCampo2# = "    ";
          #nomfical#nCampo3# = 0;
     |SIGUE;
     |PARA nCampo = 99; |SI nCampo [ 158; |HACIENDO nCampo = nCampo + 2;
          nCampo2 = nCampo + 1;
          #nomfical#nCampo# = "    ";
          #nomfical#nCampo2# = 0;
     |SIGUE;
     nCampoD = 9;
     nCampoH = 10;
     nCampoP = 11;

     nAut = 0; || busco fiestas nacionales
     nPro = 0;
     nPob = 0;
     nAct = 0;
     |BUCLE Fiestas;

     aCodPobla = #0#159;
     aCodPobla = "00000" + aCodPobla;
     aCodPobla = aCodPobla % -105;
     |BUCLE BuscaPob2;   || para buscar la comunidad autonoma

     nPro = 0; || busco fiestas autonomicas
     nPob = 0;
     nAct = 0;
     |BUCLE Fiestas;

/*
     aPro = aCodPobla % 102;  || busco fiestas provinciales
     nAut = 0;
     nPro = aPro;
     nPob = 0;
     nAct = 0;
     |BUCLE Fiestas;
*/

     nAut = 0;      || busco locales
     nPro = 0;
     nPob = aCodPobla;
     nAct = 0;
     |BUCLE Fiestas;

     |CIERRA #101;
|FPRC;

|PROCESO Cambia;
     swSigue = 0;
     nModo = (FEntrada / 100) ! 0;
     aAlfa = Contenido;
     |SI #0Campo ! aAlfa; |Y #0Campo ! 0;
          swSigue = 1;
     |FINSI;
     |SI nModo = 1;
          swSigue = 1;
     |FINSI;
     |SI swSigue = 1;
          aAlfa = "     Desea rellenar este  calendario segun los dias no ";
          aAlfa = aAlfa + "laborales del Manteniento de Dias Festivos?";
          |CONFI aAlfa;
          |SI FSalida = 0;
               |HAZ PonFiestas;
          |FINSI;
     |FINSI;
|FPRC;

/****************************************************************************/



|PROCESO fechbien; |TIPO 0;   || chequea la fecha
    alfa1 = #0Campo % 102;
    alfa2 = #0Campo % 302;
    dianum = alfa1;
    mesnum = alfa2;
    swbien = 0;
|SI mesnum ] 1; |O mesnum [ 12;
    |HAZ lib_0;
    |SI dianum [ topemes;
            swbien = 1;
    |FINSI;
|FINSI;
|SI swbien = 0;
    |MENAV errorfec1;
    |ERROR;
|FINSI;
|FPRC;

|PROCESO desdefec; |TIPO 0;   || chequea si el desde es mayor al hasta
    nume1 = Campo - 1;
    alfa1 = #0nume1 % 102;    || separa el desde
    alfa2 = #0nume1 % 302;
    diades = alfa1;
    mesdes = alfa2;
    alfa1 = #0Campo % 102;    || separa el hasta
    alfa2 = #0Campo % 302;
    diahas = alfa1;
    meshas = alfa2;
    swbien = 0;
|SI mesdes < meshas;
        swbien = 1;
|FINSI;
|SI mesdes = meshas; |Y diades [ diahas;
        swbien = 1;
|FINSI;
|SI swbien = 0;
    |MENAV errorfec2;
    |ERROR;
|FINSI;
|FPRC;

|PROCESO anyo; |TIPO 0;       || chequea si el a¤o es bisiesto
    nume1 = #0Campo;
|SI nume1 ! 0;|Y nume1 > 1980;
        resto = nume1 $ 4;
    |SI resto = 0;
            bisiesto = 1;
    |SINO;
            bisiesto = 0;
    |FINSI;
|SINO;
    |ERROR;
|FINSI;
|FPRC;

|PROCESO graba; |TIPO 3;      || construye calendario en fichero de trabajo
|SI FSalida ! 7;  || CTRL-C
    |ABRE #1;
        #1#00 = #0#0;   || codigo calendario
        #1#13 = #0#1;   || anyo calendario
    |LEE 140#1=;
    |SI FSalida = 0;   |BORRA 020#1c;   |FINSI;
    |DDEFECTO #1;
        #1#00 = #0#0;   || codigo calendario
        #1#13 = #0#1;   || anyo calendario
    |HAZ firstdia;
    |ATRI P;
    |PINTA 2415, "Confeccionando calendario, espere ...";
    |ATRI 0;
    |HAZ semana;
    |HAZ fechas;
    |HAZ diasfi;
    |GRABA 020#1n;
    |CIERRA #1;
    |BLIN 24;
|FINSI;
|FPRC;

|PROCESO borra; |TIPO 2;  || borra registro en el calendario de trabajo
    swentra = FEntrada / 100;
    swentra = swentra ! 0;
|SI swentra = 3;
    |ABRE #1;
        #1#00 = #0#0;
        #1#13 = #0#1;
    |LEE 140#1=;
    |SI FSalida = 0;
        |BORRA 020#1c;
    |FINSI;
    |CIERRA #1;
|FINSI;
|FPRC;

||****************************************************************************
||******* rutinas ************************************************************
||****************************************************************************

|PROCESO lib_0;        || convierte a literal mes en numero y halla ultimo dia
|SI mesnum =  1;  topemes = 31;             |FINSI;
|SI mesnum =  2;  topemes = 28 + bisiesto;  |FINSI;
|SI mesnum =  3;  topemes = 31;             |FINSI;
|SI mesnum =  4;  topemes = 30;             |FINSI;
|SI mesnum =  5;  topemes = 31;             |FINSI;
|SI mesnum =  6;  topemes = 30;             |FINSI;
|SI mesnum =  7;  topemes = 31;             |FINSI;
|SI mesnum =  8;  topemes = 31;             |FINSI;
|SI mesnum =  9;  topemes = 30;             |FINSI;
|SI mesnum = 10;  topemes = 31;             |FINSI;
|SI mesnum = 11;  topemes = 30;             |FINSI;
|SI mesnum = 12;  topemes = 31;             |FINSI;
|FPRC;

|PROCESO firstdia;           || calcula el dia de la semana del 01.01.XXXX
    alfa1 = "01.01." + #0#1;
    fecha = alfa1;
    fecha = fecha % 1;
    alfa1 = fecha % 11;
|QBF alfa1;
|SI alfa1 = "Lunes";     semana1 = 2;   |FINSI;
|SI alfa1 = "Martes";    semana1 = 3;   |FINSI;
|SI alfa1 = "Miercoles"; semana1 = 4;   |FINSI;
|SI alfa1 = "Jueves";    semana1 = 5;   |FINSI;
|SI alfa1 = "Viernes";   semana1 = 6;   |FINSI;
|SI alfa1 = "Sabado";    semana1 = 7;   |FINSI;
|SI alfa1 = "Domingo";   semana1 = 8;   |FINSI;
|FPRC;


||****************************************************************************
||******* crean el calendario en el fichero de trabajo ***********************
||****************************************************************************

|PROCESO semana;      || calcula los pesos segun dia semana
|PARA mesnum = 1; |SI mesnum [ 12; |HACIENDO mesnum = mesnum + 1;
    |HAZ lib_0;
        alfa1 = "";
    |PARA dianum = 1; |SI dianum [ topemes; |HACIENDO dianum = dianum + 1;
            clave = #0semana1;     || carga en 'clave' el peso correspondiente
            alfa1 = alfa1 + clave; || recarga 'alfa1' con el peso
            semana1 = semana1 + 1; || pasa el dia de la semana
        |SI semana1 = 9;
                semana1 = 2;
        |FINSI;
    |SIGUE;
        #1mesnum = alfa1;
|SIGUE;
|FPRC;


|PROCESO fechas;      || calcula los pesos segun margenes de fechas
|PARA campo = 9; |SI campo [ 96; |HACIENDO campo = campo + 3;
    |SI #0campo ! "    ";
            desde = campo;
            hasta = campo + 1;
            pesos = campo + 2;

            alfa1 = #0desde % 102;    || separa el desde
            alfa2 = #0desde % 302;
            diades = alfa1;
            mesdes = alfa2;

            alfa1 = #0hasta % 102;    || separa el hasta
            alfa2 = #0hasta % 302;
            diahas = alfa1;
            meshas = alfa2;

        |PARA mesact = mesdes; |SI mesact [ meshas; |HACIENDO mesact=mesact+1;
                alfa1 = #1mesact;         || carga la variable mes del fichero
                alfa2 = "";               || limpia la variable
                mesnum = mesact;
            |HAZ lib_0;                   || calcula el tope del mes

            |SI mesact=mesdes; topemes1=diades; |SINO; topemes1=1;       |FINSI;
            |SI mesact=meshas; topemes2=diahas; |SINO; topemes2=topemes; |FINSI;

            |PARA mid = 1; |SI mid [ topemes; |HACIENDO mid = mid + 1;
                    nume1 = (mid * 100) + 1;
                    clave = alfa1 % nume1;
                |SI mid ] topemes1; |Y mid [ topemes2;
                        clave = #0pesos;
                |FINSI;
                    alfa2 = alfa2 + clave;
            |SIGUE;
                #1mesact = alfa2;          || descarga la variable mes al fichero
        |SIGUE;

    |FINSI;
|SIGUE;
|FPRC;

|PROCESO diasfi;      || calcula los pesos segun dias fijos
|PARA campo = 99; |SI campo [ 157; |HACIENDO campo = campo + 2;
    |SI #0campo ! "    ";

            pesos = campo + 1;

            alfa1 = #0campo % 102;    || separa el dia fijo
            alfa2 = #0campo % 302;
            diafij = alfa1;
            mesfij = alfa2;

            mesnum = mesfij;
        |HAZ lib_0;

            alfa1 = #1mesfij;
            alfa2 = "";
            alfa3 = "";
            mid_1 = 0;
            mid_2 = 0;
        |SI diafij ! 1;
                mid_1 = 99 + diafij;
                alfa2 = alfa1 % mid_1;
        |FINSI;
        |SI diafij ! topemes;
                mid_2 = (diafij + 1);
                alfa3 = alfa1 % mid_2;
        |FINSI;
            alfa1 = alfa2 + #0pesos + alfa3;
            #1mesfij = alfa1;
    |FINSI;
|SIGUE;
|FPRC;

|PROCESO ElComentario;
     alfa1 = #0#0;
     alfa1 = alfa1 + #0#1;
     alfa1 = "000000" + alfa1;
     alfa1 = alfa1 % -107;
     alfa1 = "nomficom.tab;" + alfa1;
     |SUB_EJECUTA alfa1;
|FPRC;

|PROCESO Tipo20; |TIPO 20;
     |SI FSalida = "OPCION";
          FSalida = "Observaciones";
     |SINO;
          alfa1 = #0#1;
          |QBF alfa1;
          |SI alfa1 = "";
               |MENAV "    No hay ningun Calendario seleccionado";
          |SINO;
               |HAZ ElComentario;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Boton; |TIPO 8;
     |CONTROL_SIMPLE 0, "BOTON,Observaciones", 0762, 0775, ElComentario;
     enBoton = FSalida;
     |ESTADO_CONTROL enBoton, "ENABLE";
     swBotonSiNo = 1;
|FPRC;

|PROCESO FinBoton; |TIPO 9;
     |FIN_CONTROL_WINDOWS enBoton;
     |PULSATECLA;
     swBotonSiNo = 0;
|FPRC;
