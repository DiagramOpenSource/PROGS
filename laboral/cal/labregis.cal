|FICHEROS;
    labregis #0;
    labempre #2;
    labtraba #3;
    labhisto;
|FIN;

|VARIABLES;
    trabajo = 0;
    jj = 0;
    conta = 0;
    mod = 0;
    xfecreal = @;
    xfecnume = 0;
    xfecalfa = "";
    xfectope = "";
    xdiaalfa = "";
    xmesalfa = "";
    xanyalfa = "";
    xdianume = 0;
    xmesnume = 0;
    xanynume = 0;
    xtopemes = 0;
    xdiashas = 0;
    xdiastop = 0;
    xmod   = 0;
    xini = "";
    xfin = "";
    xmes = 0;
    xdia = 0;
    xvarcampo = 0;
    xcheck = 0;
    mensa1 = "0000NO EXISTE EMPRESA !!";
    mensa2 = "0000NO EXISTE TRABAJADOR !!";
    mensa9 = "0000ATENCION !! Duracion sobrepasa 48 meses !!";
    men1 = "0000ATENCION Fecha erronea";
    dia1 = "";
    mes1 = "";
    any1 = "";
    dia = 0;
    mes = 0;
    any = 0;
    tope = 0;
    sw = 0;
    fec = "";
    blanco = "          ";
    puntos = "..........";
    prodefec = @;
    {-1}menu = "";
       menu1 = "2415";
       menu2 = "1";
       menu3 = "Elija modo alta: ";
       menu4 = "AM";
       menu5 = " Automatico ";
       menu6 = " Manual ";
    nume1 = 0;
    nume2 = 0;
    nume3 = 0;
    nume4 = 0;
    aAlfa = "";
    aAlfa1 = "";
    nModo = 0;

     enBoton = 0;
     &enEmp = 0;
     &enTra = 0;
     &eaNif = "";
     &eaFechaAlta = "";
     &enRegistro = 0;
     &eaIdContrato = "";
     &eaQueEs            = "";
|FIN;

|PROCESO alta; |TIPO 7;
    trabajo = FEntrada / 100;
    trabajo = trabajo ! 0;
|SI trabajo = 1;
    |MENU menu;
        jj = FSalida;
    |SI jj = 1;
        |ABRE #0;
        |LEE 040#0u;
        |SI FSalida ! 0;
                conta = 1;
        |SINO;
                conta = #0#0 + 1;
        |FINSI;
        |DDEFECTO #0;
        |CIERRA #0;
    |FINSI;
    |SI jj = 2;
            conta = 1;
    |FINSI;
    |SI jj ! 1; |Y jj ! 2;
            conta = 1;
        |PTEC 807;
    |FINSI;
        #0#0 = conta;
    |PINTA #0#0;
|FINSI;
|FPRC;

|PROCESO BorraHisto; |TIPO 1;
     |SI FSalida = 0;
          |SI trabajo = 1; |O trabajo = 3;
               |ABRE #labhisto;
               #labhisto#100 = #labregis#0;
               |LEE 101#labhisto.=;
               |SI FSalida = 0;
                    |BORRA 020#labhisto.a;
                    |CIERRA #labhisto;
               |FINSI;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO fechacon; |TIPO 0;
        fec = #0Campo; sw = 0;
|SI fec ! blanco; |Y fec ! puntos; |Y fec ! Aindefinido;
  dia1 = fec % A102;
  mes1 = fec % A402;
  any1 = fec % A704;
  dia = dia1; mes = mes1; any = any1;
  |SI dia ! 0; |Y mes ! 0; |Y any ! 0;
     |SI mes [ 12; |Y mes ] 1;
       |SI mes = 1; |O mes = 3; |O mes = 5; |O mes = 7; |O mes = 8; |O mes = 10; |O mes = 12;
           tope = 31;
       |SINO;
           |SI mes = 2;
               mod = any $ 4;
               |SI mod = 0;
                   tope = 29;
               |SINO;
                   tope = 28;
               |FINSI;
            |SINO;
               tope = 30;
            |FINSI;
        |FINSI;
        |SI dia [ tope;
            sw = 1;
        |FINSI;
     |FINSI;
  |FINSI;
|SINO;
  sw = 1;
|FINSI;
|SI sw = 0;
    |MENAV men1;
    |ERROR;
|FINSI;
|FPRC;

|PROCESO leeempre;|TIPO 0;    || CALCULO PARA ASEGURAR EFECTIVIDAD
|ABRET #2;                    || DE LA RELACION CON EL FICHERO DE
        #2#0 = #0#1;          || EMPRESAS
|LEE 040#2=;
|SI FSalida ! 0;
    |MENAV mensa1;
        #0#1 = 0;
    |ERROR;
|FINSI;
|CIERRAT #2;
|FPRC;

|PROCESO leetraba;|TIPO 0;    || CALCULO PARA ASEGURAR EFECTIVIDAD
     |ABRE #3;                    || EN LA RELACION CON EL FICHERO DE
     #3#0 = #0#1;              || TRABAJADORES
     #3#1 = #0#2;
     |LEE 000#3=;
     |SI FSalida ! 0;
          |MENAV mensa2;
          #0#2 = 0;
          |ERROR;
     |FINSI;
     |CIERRA #3;
|FPRC;

|PROCESO duracion; |TIPO 0;                 || CALCULA DURACION DE CONTRATOS
|HAZ seleccio;
|SI xcheck ! 0; |Y xini ! blanco; |Y xini ! puntos;
        nume1 = xmes / 1000;            || meses a sumar en formato suma
        nume2 = (xdia - 1) + nume1;     || fecha a sumar en formato suma
        xfecreal = xini;
        xfecreal = xfecreal + nume2;
    |SI xvarcampo = 7;|O xvarcampo=9;|O xvarcampo=10;
            #0#8 = xfecreal;
        |PINTA #0#8;                        || pinta fecha final
    |SINO;                                  || segun prorrogas o contratos
            #0#20 = xfecreal;
        |PINTA #0#20;
    |FINSI;
        nume2 = (-1) + (4 / 1000000);  || suma 4 a¤os para saber el tope
        xfecreal = #0#7;
        xfecreal = xfecreal + nume2;
        nume1 = xfecreal;   || dias hasta (tope)
    |SI xvarcampo = 7;
            xfecreal = #0#8;
    |SINO;
            xfecreal = #0#20;
    |FINSI;
        nume2 = xfecreal;   || dias hasta
    |SI nume2 > nume1;|Y FSalida ! 2;  || evita el segundo mensaje
        |MENAV mensa9;      || chequea los 4 a¤os
    |FINSI;
|FINSI;
|FPRC;

|PROCESO seleccio;
    xvarcampo = Campo;
|SI xvarcampo = 7;|O xvarcampo=9;|O xvarcampo=10;
    xini = #0#7;
    xmes = #0#9;
    xdia = #0#10;
|SINO;
    xini = #0#19;
    xmes = #0#16;
    xdia = #0#17;
|FINSI;
    xcheck = xini + xdia;
    |SI #0#9 = 0; |Y #0#10 = 0;
        xcheck = 0;
        #0#8 = "";
        |PINTA #0#8;
    |FINSI;
|FPRC;

|PROCESO acumu; |TIPO 0;
     #0#18 = #0#9 + #0#16;
     #0#22 = #0#10 + #0#17;
     |SI #0#22 ] 30;
          nume1 = (#0#22 / 30) ! 0;
          nume2 = #0#22 $ 30;
          #0#18 = #0#18 + nume1;
          #0#22 = #0#22 - (nume1 * 30);
     |FINSI;
     |PINTA #0#18;
     |PINTA #0#22;
|FPRC;

|PROCESO fecdefec; |TIPO 7;
|SI #0#15 = 1;
    prodefec = #0#8;
|SINO;
    prodefec = #0#20;
|FINSI;
    prodefec = prodefec + 1;
    #0#19 = prodefec;
|PINTA #0#19;
|FPRC;

|PROCESO recarga; |TIPO 3;
|SI #0#07!"..........";|Y #0#07!"          ";
        #0#23 = #0#07;
|SINO;
        #0#23 = "00.00.0000";
|FINSI;
|SI #0#08!"..........";|Y #0#08!"          ";
        #0#24 = #0#08;
|SINO;
        #0#24 = "00.00.0000";
|FINSI;
|SI #0#19!"..........";|Y #0#19!"          ";
        #0#25 = #0#19;
|SINO;
        #0#25 = "00.00.0000";
|FINSI;
|SI #0#20!"..........";|Y #0#20!"          ";
        #0#26 = #0#20;
|SINO;
        #0#26 = "00.00.0000";
|FINSI;
|FPRC;

|PROCESO actua_trab; |TIPO 0;
|ABRE #3;
    #3#0 = #0#1;
    #3#1 = #0#2;
|LEE 101#3=;
|SI FSalida = 0;
        #3#36 = #0#7;
    |GRABA 020#3a;
|FINSI;
|LIBERA #3;
|CIERRA #3;
|FPRC;

|PROCESO RellenaContrato; |TIPO 6;
     aAlfa1 = #0#4;
     |QBF aAlfa1;
     aAlfa1 = ("000" + aAlfa1) % -103;
     #0#4 = aAlfa1;
     |PINTA #0#4;
|FPRC;

|PROCESO BuscaID;
     |SI nModo ! 4;
          |MENAV "    Opción disponible sólo en modo consulta";
          |ACABA;
     |FINSI;

     enEmp = #0#1;
     enTra = #0#2;
     |HAZ leetraba;
     eaNif = #labtraba#7;     |QBF eaNif;
     eaFechaAlta = #0#7;      |QBF eaFechaAlta;
     |SI eaFechaAlta ! "";
          eaFechaAlta = (eaFechaAlta % 102) + "/" + (eaFechaAlta % 402) + "/" + (eaFechaAlta % -104);
     |FINSI;
     enRegistro = #0#0;
     eaQueEs    = "C";
     |SUB_EJECUTA "labnct09";
     |QBF eaIdContrato;
     |SI eaIdContrato ! "";
          |MENAV "    Se ha recogido correctamente el ID del contrato";
          #0#14 = eaIdContrato;
          |PINTA #0#14;
     |SINO;
          |MENAV "    No se ha conseguido obtener el ID del contrato";
     |FINSI;
|FPRC;

|PROCESO BotonID; |TIPO 8;
     |MODULO aAlfa;
     |QBF aAlfa;
     |SI aAlfa ! "labregis";
          |ACABA;
     |FINSI;

     |CONTROL_SIMPLE 0, "BOTON,Obtener ID/Huella", 0970, 1077, BuscaID;
     enBoton = FSalida;
     |ESTADO_CONTROL enBoton, "ENABLE";
|FPRC;

|PROCESO FinBotonID; |TIPO 9;
     |MODULO aAlfa;
     |QBF aAlfa;
     |SI aAlfa ! "labregis";
          |ACABA;
     |FINSI;

     |FIN_CONTROL_WINDOWS enBoton;
     |PULSATECLA;
|FPRC;

|PROCESO Modo; |TIPO 14;
     nModo = (FEntrada / 100) ! 0;
|FPRC;
