|FICHEROS;
     aomopaex #0; ||Este Def.
     aomentra #1; ||Horas.
     aomobras #2; ||Maestro Obras.
     gomanth@ #3; ||Expedientes.
     gomantl@ #4; ||Lineas de Expedientes.
     aomopatm #5; ||Temporal.
     gopre00@ #6; ||Desglose Presupuesto.
     goparam@ #7; ||Parametros Obra.
     aomopat2 #8; ||Temporal.
|FIN;

|VARIABLES;
     aPath  = "";
     aPatf  = "";
     aDef   = "";
     aDef1  = "";
     aDef2  = "";
     aDef3  = "";
     aDef4  = "";
     aDef5  = "";
     aDef6  = "";
     aDef7  = "";
     aDef8  = "";
     aDef9  = "";
     aDef10 = "";
     swErr  = 0;
     aSer   = "";
     aEmpre = "";
     nThora = 0;
     nHora  = 0;
     nSal   = 0;
|FIN;

|PROCESO aomopatm3;
     nHora = #1#5 % #5#5;
     |SI nHora ! 0;
          |DDEFECTO #8;
          #8#0 = #1#3; ||Cod.Trabajador.
          #8#1 = #1#0; ||Fecha.
          #8#2 = #5#0; ||Serie Presupuesto.
          #8#3 = #5#1; ||Numero Presupuesto.
          #8#4 = #5#2; ||Capitulo.
          #8#5 = #5#3; ||Partida.
          |LEE 101#8=;
          nSal = FSalida;
          #8#6 = #8#6 + nHora; ||Horas aSignadas.
          |SI nSal = 0; |GRABA 020#8a; |FINSI;
          |SI nSal ! 0; |GRABA 020#8n; |FINSI;
          |LIBERA #8;
     |FINSI;
|FPRC;

|DEFBUCLE aomopatm3;
     #5#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL,aomopatm3;
|FIN;

|PROCESO aomentra;
     |BUCLE aomopatm3;
|FPRC;

|DEFBUCLE aomentra;
     #1#1;
     ;
     #0#1,#0#3,#0#5,#0#7,00,01;
     #0#2,#0#4,#0#6,#0#7,99,99;
     ;
     NULL,aomentra;
|FIN;

|PROCESO aomopatm2;
     #5#5 = (#5#4 * 100) / nThora;
     |GRABA 020#5a;
|FPRC;

|DEFBUCLE aomopatm2;
     #5#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL,aomopatm2;
|FIN;

|PROCESO aomopatm;
     nThora = nThora + #5#4;
|FPRC;

|DEFBUCLE aomopatm;
     #5#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL,aomopatm;
|FIN;

|PROCESO gopre004;
     |SI #7#6 ! #6#14; |ACABA; |FINSI;
     #5#0 = #6#0;
     #5#1 = #6#1;
     #5#2 = #6#2;
     #5#3 = #6#3;
     |LEE 101#5=;
     |SI FSalida ! 0;
           #5#4 = #6#8;
           #5#5 = 0;
           #5#6 = 0;
           |GRABA 020#5n;
     |FINSI;
     |LIBERA #5;
|FPRC;

|DEFBUCLE gopre004;
     #6#1005;
     ;
     #4#3,#4#4,"                    ","                    ",  1;
     #4#3,#4#4,"zzzzzzzzzzzzzzzzzzzz","zzzzzzzzzzzzzzzzzzzz",999;
     ;
     NULL,gopre004;
|FIN;

|PROCESO gomantl1;
     |BUCLE gopre004;
|FPRC;

|DEFBUCLE gomantl1;
     #4#1003;
     ;
     #3#0,#3#1,001;
     #3#0,#3#1,999;
     ;
     NULL,gomantl1;
|FIN;

|PROGRAMA;
     |CLS;
     |ABRE #0;
     #0#0 = 1;
     |LEE 101#0=;
     |SI FSalida ! 0;
          |DDEFECTO #0;
          |GRABA 020#0b;
     |FINSI;
     |PINPA #0#0;
     |PINDA #0#0;
     |ENDATOS #1#0;
     |SI FSalida = 0;
          |GRABA 020#0a;
          |HAZ CargaDef;
          |SI swErr = 1;
               |HAZ DesCargaDef;
          |SINO;
               |ABRE #2;
               #2#0 = #0#7;
               |LEE 000#2=;
               |SI FSalida = 0;
                    aSer   = #2#2;
                    aEmpre = #2#3;
                    |QBF aEmpre;
                    aEmpre = ("00000" + aEmpre) % -105;
                    aPatf = aPatf + aEmpre + "/";
               |FINSI;
               |CIERRA #2;
               |PATH_DAT #3 aPatf;
               |PATH_DAT #4 aPatf;
               |PATH_DAT #6 aPatf;
               |PATH_DAT #7 aPatf;
               |ABRE #3;
               |SELECT #2#3;
               #3#28 = aSer;
               |LEE 000#3=;
               |SI FSalida = 0;
                    |DELINDEX #5;
                    |DELINDEX #8;
                    |ABRE #5;
                    |ABRE #7;
                    |DDEFECTO #7;
                    #7#0 = "A";
                    |LEE 000#7=;
                    |CIERRA #7;
                    |BUCLE gomantl1;
                    |CIERRA #5;
                    |BUCLE aomopatm;
                    |BUCLE aomopatm2;
                    |ABRE #8;
                    |BUCLE aomentra;
                    |CIERRA #8;
                    |HAZ segundaparte;
               |FINSI;
               |CIERRA #3;
          |FINSI;
     |FINSI;
     |LIBERA #0;
     |CIERRA #0;
     |HAZ DesCargaDef;
|FPRO;

|PROCESO CargaDef;
     |DBASS aPath;
     |QBF aPath;
     aPath = aPath + "dscomer9/";
     aPatf = aPath + "fich/";

     aDef1 = aPath + "def/" + "gomantho";
     aDef  = aDef1;
     |CARGA_DEF aDef, gomanth@;
     |SI FSalida ! 0;
          swErr = 1;
          aDef1 = "";
          |ACABA;
     |FINSI;

     aDef2 = aPath + "def/" + "gomantl1";
     aDef  = aDef2;
     |CARGA_DEF aDef, gomantl@;
     |SI FSalida ! 0;
          swErr = 1;
          aDef2 = "";
          |ACABA;
     |FINSI;

     aDef3 = aPath + "def/" + "gopre004";
     aDef  = aDef3;
     |CARGA_DEF aDef, gopre00@;
     |SI FSalida ! 0;
          swErr = 1;
          aDef3 = "";
          |ACABA;
     |FINSI;

     aDef4 = aPath + "def/" + "goparame";
     aDef  = aDef4;
     |CARGA_DEF aDef, goparam@;
     |SI FSalida ! 0;
          swErr = 1;
          aDef4 = "";
          |ACABA;
     |FINSI;

|FPRC;

|PROCESO DesCargaDef;
     |SI aDef4  ! ""; |DESCARGA_DEF #goparam@; |FINSI;
     |SI aDef3  ! ""; |DESCARGA_DEF #gopre00@; |FINSI;
     |SI aDef2  ! ""; |DESCARGA_DEF #gomantl@; |FINSI;
     |SI aDef1  ! ""; |DESCARGA_DEF #gomanth@; |FINSI;
|FPRC;
