|FICHEROS;
     nominfho #0;
     nomcal03;
     nomcal04;

     labempre;
     labcentr;
     labtraba;
     nomcenes;
     nomcontr;
|FIN;

|VARIABLES;
     nCampo        = 0;
     topemes       = 0;
     nMes          = 0;
     nActivoNuevo  = 0;
     nOpcion       = 0;
     nPasada       = 0;
     nHay          = 0;

     aAlfa         = "";
     aPathTrab     = "";
     aFicPDF       = "";
     aFecha        = "";
     aMes          = "";
     aAny          = "";

     fFecha        = @;
     fFechaRef     = @;
     fIniMes       = @;
     fFinMes       = @;
     fFechaIniMes  = @;
     fFechaFinMes  = @;
     fFechaAlta    = @;
     fFechaBaja    = @;

 {-1} aMenu        = "";
      aMenu1       = "2400";
      aMenu2       = "1";
      aMenu3       = "";
      aMenu4       = "IABC";
      aMenu5       = "Imprimir ";
      aMenu6       = "Archivar ";
      aMenu7       = "amBos    ";
      aMenu8       = "Cancelar ";

     &aMesLetra       = "";
     &aFechaIniMes    = "";
     &aFechaFinMes    = "";
     &eaVengoDe       = "";
     &nArchi          = 0;
     &eaUnidadBasico  = "";
     &eaOrig          = "";
     &eaObservaciones = "";
     &enSwDesde9      = 0;
     &enEmp           = 0;
     &enTra           = 0;
     &enMes           = 0;
     &enAny           = 0;
     &enSwMeteGrupoSubTipo = 0;

     nNume = 0;
|FIN;

|PROCESO Defectos; |TIPO 7;
     fFecha = ~;
     aAlfa = fFecha;
     aAlfa = aAlfa % -104;
     #nominfho#4 = aAlfa;
     |PINTA #nominfho#4;

     aAlfa = fFecha;
     aAlfa = aAlfa % 402;
     #nominfho#5 = aAlfa;
     #nominfho#6 = aAlfa;
     |PINTA #nominfho#5;
     |PINTA #nominfho#6;
|FPRC;

|PROCESO ultidia;
     topemes = 31;
     |SI nMes = 4; |O  nMes = 6; |O  nMes = 9; |O  nMes = 11;
          topemes = 30;
     |FINSI;
     |SI nMes = 2;
          topemes = 28;
          nNume = #0#4 $ 4;
          |SI nNume = 0;
               topemes = 29;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO MesHasta;
     #nominfho#6 = #nominfho#5;
     |PINTA #nominfho#6;
|FPRC;

|PROCESO Informe;
     #labempre#0 = #nomcal04#0;
     |LEE 000#labempre.=;

     #labtraba#0 = #nomcal04#0;
     #labtraba#1 = #nomcal04#1;
     |LEE 000#labtraba.=;

     |SI #labtraba#77 < #nominfho#10;  |O #labtraba#77 > #nominfho#11;
         |ACABA;
     |FINSI;

     |SI #0#9 = "C";
          |SI #labtraba#42 = "X"; |O #labtraba#42 = "T"; |O #labtraba#42 = "P";
               |ACABA;
          |FINSI;
     |FINSI;
     |SI #0#9 = "P";
          |SI #labtraba#42 ! "X"; |Y #labtraba#42 ! "T"; |Y #labtraba#42 ! "P";
               |ACABA;
          |FINSI;
     |FINSI;

     #labcentr#0 = #nomcal04#0;
     #labcentr#1 = #labtraba#77;
     |LEE 000#labcentr.=;

     || comprueba fechas de alta y baja respecto al preriodo que estoy imprimiendo

     nMes = #nomcal04#3;
     aMes = nMes;
     aMes = ("00" + aMes) % -102;
     aAny = #nomcal04#2;

     aFecha = "01" + "." + aMes + "." + aAny;
     fIniMes = aFecha;

     |HAZ ultidia;
     aFecha = topemes;
     aFecha = ("00" + aFecha) % -102;
     aFecha = aFecha + "." + aMes + "." + aAny;
     fFinMes = aFecha;

     aAlfa = #labtraba#36;
     fFechaAlta = aAlfa;
     aAlfa = #labtraba#37;
     |QBF aAlfa;
     |SI aAlfa = ""; |O aAlfa = "..........";
          aAlfa = "31.12.2999";
     |FINSI;
     fFechaBaja = aAlfa;

     |SI fFechaAlta [ fFinMes; |Y fFechaBaja ] fIniMes;
          || adelante
     |SINO;
          || el trabajador esta en el Registro de Horas, aunque despues
          || resulta que la fecha de baja es anterior al periodo que se pide
          || la baja seria posterior al grabado del registro
          || no lo imprimo
          |ACABA;
     |FINSI;

     |SI #labtraba#45 = "421";
          #nomcenes#0 = #labtraba#0;
          #nomcenes#13 = #labtraba#77;
          #nomcenes#1 = 87;
          |LEE 000#nomcenes.=;
          |SI FSalida = 0;
               |PARA nCampo = 2; |SI nCampo [ 11; |HACIENDO nCampo = nCampo + 1;
                    #labcentr#Campo# = #nomcenes#Campo#;
               |SIGUE;
          |FINSI;
     |FINSI;

     |PRINT;
|FPRC;

|DEFBUCLE Informe;
     #nomcal04#1;
     ;
     #nominfho#0, #nominfho#2, #nominfho#4, #nominfho#5, 01;
     #nominfho#1, #nominfho#3, #nominfho#4, #nominfho#6, 31;
     ;
     NULL, Informe;
|FIN;

|PROCESO PorPantalla;
     Impresora = "CrystalReport";
     |IMPRE -1;
     |INFOR "nominfho";

     |BUCLE Informe;

     |PIEINF;
     |FININF;
     |FINIMP;
|FPRC;

|PROCESO nomcal04A;
     #labempre#0 = #nomcal04#0;
     |LEE 000#labempre.=;

     #labtraba#0 = #nomcal04#0;
     #labtraba#1 = #nomcal04#1;
     |LEE 000#labtraba.=;

     |SI #labtraba#77 < #nominfho#10;  |O #labtraba#77 > #nominfho#11;
         |ACABA;
     |FINSI;

     #labcentr#0 = #nomcal04#0;
     #labcentr#1 = #labtraba#77;
     |LEE 000#labcentr.=;

     |SI #0#9 = "C";
          |SI #labtraba#42 = "X"; |O #labtraba#42 = "T"; |O #labtraba#42 = "P";
               |ACABA;
          |FINSI;
     |FINSI;
     |SI #0#9 = "P";
          |SI #labtraba#42 ! "X"; |Y #labtraba#42 ! "T"; |Y #labtraba#42 ! "P";
               |ACABA;
          |FINSI;
     |FINSI;

     |SI nPasada = 0;
         nHay = 1;
         |ERROR10;
         |ACABA;
     |FINSI;

     || comprueba fechas de alta y baja respecto al preriodo que estoy imprimiendo

     nMes = #nomcal04#3;
     aMes = nMes;
     aMes = ("00" + aMes) % -102;
     aAny = #nomcal04#2;

     aFecha = "01" + "." + aMes + "." + aAny;
     fIniMes = aFecha;

     |HAZ ultidia;
     aFecha = topemes;
     aFecha = ("00" + aFecha) % -102;
     aFecha = aFecha + "." + aMes + "." + aAny;
     fFinMes = aFecha;

     aAlfa = #labtraba#36;
     fFechaAlta = aAlfa;
     aAlfa = #labtraba#37;
     |QBF aAlfa;
     |SI aAlfa = ""; |O aAlfa = "..........";
          aAlfa = "31.12.2999";
     |FINSI;
     fFechaBaja = aAlfa;

     |SI fFechaAlta [ fFinMes; |Y fFechaBaja ] fIniMes;
          || adelante
     |SINO;
          || el trabajador esta en el Registro de Horas, aunque despues
          || resulta que la fecha de baja es anterior al periodo que se pide
          || la baja seria posterior al grabado del registro
          || no lo imprimo
          |ACABA;
     |FINSI;

     |SI #labtraba#45 = "421";
          #nomcenes#0 = #labtraba#0;
          #nomcenes#13 = #labtraba#77;
          #nomcenes#1 = 87;
          |LEE 000#nomcenes.=;
          |SI FSalida = 0;
               |PARA nCampo = 2; |SI nCampo [ 11; |HACIENDO nCampo = nCampo + 1;
                    #labcentr#Campo# = #nomcenes#Campo#;
               |SIGUE;
          |FINSI;
     |FINSI;

     |PRINT;
|FPRC;

|DEFBUCLE nomcal04A;
     #nomcal04#1;
     ;
     #nomcal03#0, #nomcal03#1, #nomcal03#2, #nomcal03#3, 01;
     #nomcal03#0, #nomcal03#1, #nomcal03#2, #nomcal03#3, 31;
     ;
     NULL, nomcal04A;
|FIN;

|PROCESO nomcal03;


     aFicPDF = aPathTrab + "Horas" + (("00000" + #nomcal03#0) % -105);
     aFicPDF = aFicPDF +  (("00000" + #nomcal03#1) % -105);
     aFicPDF = aFicPDF +  ".pdf";

     nHay    = 0;
     nPasada = 0;
     |BUCLE nomcal04A;

     |SI nHay = 0;  |ACABA;  |FINSI;

     Impresora = "CrystalReport;" + aFicPDF;
     |IMPRE -1;
     |INFOR "nominfho";

     nPasada = 1;
     |BUCLE nomcal04A;

     |PIEINF;
     |FININF;
     |FINIMP;

     aAlfa = aFicPDF;
     |XABRE "CE", aAlfa, 0;
     |SI FSalida < 0;  |ACABA;  |FINSI;

     eaVengoDe       = "nominfho";
     enEmp           = #nomcal03#0;
     enTra           = #nomcal03#1;
     enMes           = #nomcal03#3;
     enAny           = #nomcal03#2;
     eaOrig          = aFicPDF;
     eaObservaciones = "Registro de horas";

     |HAZ Archivando;

     |PULSATECLA;
|FPRC;

|DEFBUCLE nomcal03;
     #nomcal03#1;
     ;
     #nominfho#0, #nominfho#2, #nominfho#4, #nominfho#5;
     #nominfho#1, #nominfho#3, #nominfho#4, #nominfho#6;
     ;
     NULL, nomcal03;
|FIN;

|PROCESO PonUbica;
     |SI eaUnidadBasico = "";  |HAZ LeeUnidadBasico;  |FINSI;

     aPathTrab = eaUnidadBasico  + "\DOCUMENTOS";               |RMKDIR aPathTrab;
     aPathTrab = aPathTrab + "\LABORAL";                        |RMKDIR aPathTrab;
     aPathTrab = aPathTrab + "\" + Usuario; |QBF aPathTrab;     |RMKDIR aPathTrab;
     aPathTrab = aPathTrab + "\";
|FPRC;

|PROCESO PorArchivo;
     enSwDesde9           = 1;
     enSwMeteGrupoSubTipo = 0;
     |HAZ CtrlPedirGrupoDsArchi;

     |HAZ PonUbica;
     |BUCLE nomcal03;

     aAlfa = "    Los documentos se han archivado en ";
     aAlfa = aAlfa + "la Gestión del Archivo Documental (GAD).";
     |MENAV aAlfa;
|FPRC;

|PROCESO Tipo12;  |TIPO 12;
     |SI FSalida = 7;  |ACABA;  |FINSI;

     |CONFI "0000S¿ES CORRECTO?";
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     nOpcion = 1;
     |SI nArchi = 1;
         |MENU aMenu;
         nOpcion = FSalida;
     |FINSI;

     |ABRE #labempre;
     |ABRE #labcentr;
     |ABRE #labtraba;
     |ABRE #nomcenes;

     |SI nOpcion = 1;
         |HAZ PorPantalla;
     |FINSI;

     |SI nOpcion = 2;
         |HAZ PorArchivo;
     |FINSI;

     |SI nOpcion = 3;
         |HAZ PorPantalla;
         |HAZ PorArchivo;
     |FINSI;

     |CIERRA #nomcenes;
     |CIERRA #labtraba;
     |CIERRA #labcentr;
     |CIERRA #labempre;
|FPRC;

|PROGRAMA;
     nActivoNuevo = 1;

     |SI nActivoNuevo = 1;
         eaVengoDe  = "nominfho";
         nArchi     = 0;
         |HAZ MiraSiArchiva;
     |FINSI;

     |CLS;

     |ABRE #0;
     |LEE 001#0p;
     |SI FSalida ! 0;
          |DDEFECTO #0;
          |HAZ Defectos;
          |GRABA 020#0n;
     |FINSI;
     |LEE 101#0=;

     |PINPA #0#nominfho;
     |PINDA #0#nominfho;

     |ABRE #nomcontr; |LEE 000#nomcontr.=; |CIERRA #nomcontr;
     |SI #nomcontr#75 = "S";
          |CAMPO_MODIFICA #0#9, 1, "S";
          |CAMPO_VISUAL #0#9, 1, "S";
          |PINTA 1924, "³ Tipo cont.:               ³";
          |PINTA #0#9;
     |SINO;
          |CAMPO_MODIFICA #0#9, 1, "N";
          |CAMPO_VISUAL #0#9, 1, "N";
          |PINTA 1924, "³                           ³";
          #0#9 = "P";
     |FINSI;

     |ENDATOS #1#nominfho;

     |GRABA 020#0a;
     |LIBERA #0;
     |CIERRA #0;
|FPRO;
