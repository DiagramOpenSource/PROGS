|FICHEROS;
     labnct15 #0;
     labnct10;
     labnct19 #100,MANTE;
     nomtmp06 #101;
     labnct16;

     labempre;
     labtraba;
     nomvarca;
     nomcalli;
     nomhicli;
|FIN;

|VARIABLES;
     nOpcionSS = 0;
     aParam = "";
     nFecha = 0;
     aFecha = "";
     fFecha = @;
     aFechaDesde = "";
     fFechaDesde = @;
     aFechaHasta = "";
     fFechaHasta = @;
     aAlfa = "";

     &swSigue = 0;
     seleccio = 0;
     swRegistrado = 0;
     nOperacion = 0;
     &eaOpcion = "";
     &eaVengoDe = "";
     aMarca = "";

     nOpcion = 0;
 {-1}aMenu  = "";
     aMenu1 = "2400";
     aMenu2 = "1";
     aMenu3 = "";
     aMenu4 = "REC";
     aMenu5 = " Revisar ";
     aMenu6 = " Enviar seleccionados";
     aMenu7 = " Cancelar ";

     &swConectaSS = 0;

     nMes = 0;
     nNume = 0;
     nDiasVac = 0;
     fFechaVac = @;
     fFechaMov = @;
     aFechaMov = "";
     nCausa = 0;
     nDesdeMes = 0;
     nHastaMes = 0;

     &swPortal = 0;
     &eaMensaPortal = "";
|FIN;

|PROCESO Vacaciones;

|FPRC;

|PROCESO MarcaSi; |TIPO 0;
     #labnct19#6 = "*";

     |PINTA #labnct19#6;
     |GRABA 020#labnct19.a;
|FPRC;

|PROCESO Marcalo; |TIPO 0;
     |SI #labnct19#6 = "*";
         #labnct19#6 = " ";
     |SINO;
         #labnct19#6 = "*";
     |FINSI;

     |PINTA #labnct19#6;
     |GRABA 020#labnct19.a;
     |LIBERA #labnct19;
     |ERROR;
|FPRC;

|PROCESO MarcaDesmarca;
     #labnct19#6 = aMarca;
     |GRABA 020#labnct19.a;
     |LIBERA #labnct19;
|FPRC;

|DEFBUCLE MarcaDesmarca;
     #labnct19#1;
     ;
     INICIO;
     FINAL;
     be;
     NULL, MarcaDesmarca;
|FIN;

|PROCESO MiraMarca; |TIPO 11;
     |SI FSalida = 14; || Marca todo
          |SALVA_FICHA #labnct19;
          |SI #labnct19#6 = "*";
               aMarca = " ";
          |SINO;
               aMarca = "*";
          |FINSI;
          |BUCLE MarcaDesmarca;
          |REPON_FICHA #labnct19;
          ||PTEC 808;
          |PONCONTROL 23, "si";
     |FINSI;

     |SI FSalida = 13;
          |HAZ Marcalo;
     |FINSI;
|FPRC;

|PROCESO Defectos;
     fFecha = ~;
     nFecha = fFecha;
     nFecha = nFecha - 14;
     fFecha = nFecha;
     aFecha = fFecha;
     #0#0 = aFecha;

     fFecha = ~;
     nFecha = fFecha;
     nFecha = nFecha + 14;
     fFecha = nFecha;
     aFecha = fFecha;
     #0#1 = aFecha;
|FPRC;

|PROCESO Tipo; |TIPO 14;
     |ATRI S;
     |SI #0#2 = "A";
          |PINTA 1928, " ALTA de trabajadores";
     |SINO;
          |PINTA 1928, " BAJA de trabajadores";
     |FINSI;
     |ATRI 0;
|FPRC;

|PROCESO sel_min;
     #100#6 = " ";
     #100#0 = 00001;
     #100#1 = 00001;
|FPRC;

|PROCESO sel_max;
     #100#6 = "*";
     #100#0 = 99999;
     #100#1 = 99999;
|FPRC;

|PROCESO Busca;
     |SI #0#2 = #labnct16#4;
          swRegistrado = 1;
     |FINSI;
|FPRC;

|DEFBUCLE BuscaRegistro;
     #labnct16#1;
     ;
     #labtraba#0, #labtraba#1, "01.01.2018", "000000", 3408;
     #labtraba#0, #labtraba#1, "31.12.2999", "999999", 3409;
     ;
     NULL, Busca;
|FIN;

|PROCESO Registro;
     swRegistrado = 0;
     |BUCLE BuscaRegistro;
|FPRC;

|PROCESO labnct19;
     |SI #labnct19#6 ! "*";
          |ACABA;
     |FINSI;

     #nomtmp06#0 = #labnct19#0;
     #nomtmp06#1 = #labnct19#1;
     #nomtmp06#2 = #labnct19#3;
     #nomtmp06#4 = "*";
     #nomtmp06#6 = #labnct19#4;
     #nomtmp06#7 = #labnct19#7;
     #nomtmp06#8 = #labnct19#8;
     #nomtmp06#9 = #labnct19#9;
     |GRABA 020#nomtmp06.n;
     |LIBERA #nomtmp06;
|FPRC;

|DEFBUCLE labnct19;
     #labnct19#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, labnct19;
|FIN;

|PROCESO GrabaTmp2;
     |BUCLE labnct19;
|FPRC;

|PROCESO BuscaVac;
     |ABRE #nomcalli;
     |ABRE #nomhicli;

     #nomcalli#0 = #labtraba#0;
     #nomcalli#1 = #labtraba#1;
     #nomcalli#2 = nMes;
     #nomcalli#3 = 2;
     #nomcalli#4 = 125;
     |LEE 000#nomcalli.=;
     |SI FSalida = 0;
          nNume = #nomcalli#11;
          nNume = (nNume + 0.49) ! 0;
          nDiasVac = nNume;

          fFecha = aFechaMov;
          nFecha = fFecha;
          nFecha = nFecha + nDiasVac;
          fFechaVac = nFecha;
     |FINSI;

     #nomhicli#0 = #labtraba#0;
     #nomhicli#1 = #labtraba#1;
     #nomhicli#2 = nMes;
     #nomhicli#3 = 2;
     #nomhicli#4 = 125;

     aAlfa = aFechaMov % -104;
     nNume = aAlfa;
     nNume = nNume - 2000;
     #nomhicli#5 = nNume;
     |LEE 000#nomhicli.=;
     |SI FSalida = 0;
          nNume = #nomhicli#11;
          nNume = (nNume + 0.49) ! 0;
          nDiasVac = nNume;

          fFecha = aFechaMov;
          nFecha = fFecha;
          nFecha = nFecha + nDiasVac;
          fFechaVac = nFecha;
     |FINSI;

     |CIERRA #nomhicli;
     |CIERRA #nomcalli;
|FPRC;

|PROCESO labtraba;

     || este sera el proceso del pase
     swSigue = 1;        || por defecto SI

     #labempre#0 = #labtraba#0;
     |LEE 000#labempre.=;

     aFechaMov = "";
     nCausa = 00;
     nDiasVac = 0;
     fFechaVac = "01.01.0000";

     |SI #0#2 = "A";
          || fecha de alta
          aFechaMov = #labtraba#36;
     |FINSI;
     |SI #0#2 = "B";
          || fecha y causa de baja
          aAlfa = #labtraba#37;
          |QBF aAlfa;
          |SI aAlfa ! ""; |Y aAlfa ! "..........";
               aFechaMov = aAlfa;
          |FINSI;
          aAlfa = #labtraba#184;
          aAlfa = aAlfa % 102;
          |QBF aAlfa;
          |SI aAlfa ! "";
               nNume = aAlfa;
               |SI nNume ! 0;
                    nCausa = aAlfa;
               |FINSI;
          |FINSI;

          || causa y fecha de baja en variaciones
          || y dias de vacaciones en finiquito

          |ABRE #nomvarca;
          #nomvarca#0 = #labtraba#0;
          #nomvarca#1 = #labtraba#1;
          aAlfa = #0#0; aAlfa = aAlfa % 402; nDesdeMes = aAlfa;
          aAlfa = #0#1; aAlfa = aAlfa % 402; nHastaMes = aAlfa;
          |SI nHastaMes < nDesdeMes;
               nHastaMes = nHastaMes + 12;
          |FINSI;
          |PARA nMes = nDesdeMes; |SI nMes [ nHastaMes; |HACIENDO nMes = nMes + 1;
               |SI nHastaMes ] 13;
                    nMes = nHastaMes - 12;
               |FINSI;
               #nomvarca#2 = nMes;
               |LEE 000#nomvarca.=;
               |SI FSalida = 0;
                    aAlfa = #nomvarca#16;
                    |QBF aAlfa;
                    |SI aAlfa ! ""; |Y aAlfa ! "..........";
                         aFechaMov = aAlfa;
                    |FINSI;
                    nNume = #nomvarca#46;
                    |SI nNume ! 0;
                         nCausa = nNume;
                    |FINSI;
               |FINSI;

               |HAZ BuscaVac;
          |SIGUE;
          |CIERRA #nomvarca;
     |FINSI;

     fFechaMov = aFechaMov;

     fFechaDesde = #0#0;
     fFechaHasta = #0#1;
     |SI fFechaDesde [ fFechaMov; |Y fFechaMov [ fFechaHasta;
          || entra
     |SINO;
          swSigue = 0;
     |FINSI;

     || los que tengan 00 si, aunque igual da error

     |SI nCausa = 08; |O nCausa = 09; |O nCausa = 10;
     |O nCausa = 11; |O nCausa = 12;
          swSigue = 0;
     |FINSI;

     || aqui se seleccionaran los trabajadores y grabare el temporal

     |SI swSigue = 1;
          seleccio = 1;

          |DDEFECTO #100;
          #100#0 = #labtraba#0;
          #100#1 = #labtraba#1;
          #100#2 = #labempre#2;
          #100#3 = #labtraba#2;
          #100#4 = aFechaMov;
          #100#5 = #labtraba#45;
          |SI #0#2 = "B";
               |SI nCausa = 98;
                    nCausa = 99;
               |FINSI;
               #100#7 = nCausa;
               |SI nDiasVac ! 0;
                    #100#8 = nDiasVac;
                    #100#9 = fFechaVac;
               |FINSI;

               |SI #100#7 = 00;
               |O #100#7 = 08; |O #100#7 = 09; |O #100#7 = 10;
               |O #100#7 = 11; |O #100#7 = 12;
                    #100#6 = " ";
               |FINSI;
          |FINSI;

          swRegistrado = 0;
          |HAZ Registro;

          |SI swRegistrado = 1;
               |SI #0#8 = "S";
                    |GRABA 020#100n;
               |FINSI;
          |SINO;
               |GRABA 020#100n;
          |FINSI;

          |LIBERA #100;
     |FINSI;
|FPRC;

|DEFBUCLE labtraba;
     #labtraba#1;
     ;
     #0#3, #0#5;
     #0#4, #0#6;
     ;
     NULL, labtraba;
|FIN;

|PROCESO lectura;             || INICIO DEL CALCULO
     |ABRE #labempre;
     |ABRE #labtraba;

     |BUCLE labtraba;

     |CIERRA #labtraba;
     |CIERRA #labempre;
|FPRC;

|PROCESO Parametros;
     |SI #0#2 = "A";
          |ATRI R;
          |PINTA 0830, "ALTAS";
          |ATRI 0;

          |PINTA 1239, "alta";
          |PINTA 1439, "alta";
     |FINSI;

     |SI #0#2 = "B";
          |ATRI R;
          |PINTA 0830, "BAJAS";
          |ATRI 0;

          |PINTA 1239, "baja";
          |PINTA 1439, "baja";
     |FINSI;
|FPRC;

|PROCESO EnvioOnline;
          aAlfa = Usuario;         |QBF aAlfa;
          aAlfa = "tm_nomtmp06_" + aAlfa;
          |NOME_DAT #nomtmp06#aAlfa#;
          |DELINDEX #nomtmp06;
          |ABRE #nomtmp06;

          |HAZ GrabaTmp2;
          |HAZ BajaOnline;

          |DELINDEX #nomtmp06;
          |CIERRA #nomtmp06
|FPRC;

|PROCESO PintaPantalla;
     |CLS;
     |PINPA #0#labnct19;
     |SI eaOpcion = "A";
          |ATRI R;
          |PINTA 0532, "altas";

          |ATRI R;
          |PINTA 0766, "alta";

          |ATRI 0;

          |CAMPO_MODIFICA #labnct19#7, 1, "N";
          |CAMPO_MODIFICA #labnct19#8, 1, "N";
          |CAMPO_MODIFICA #labnct19#9, 1, "N";
          |CAMPO_VISUAL #labnct19#7, 1, "N";
          |CAMPO_VISUAL #labnct19#8, 1, "N";
          |CAMPO_VISUAL #labnct19#9, 1, "N";
          |PINTA 2105, "³                                                                      ³";
          |PINTA 2205, "³                                                                      ³";
     |FINSI;
     |SI eaOpcion = "B";
          |ATRI R;
          |PINTA 0532, "bajas";

          |ATRI R;
          |PINTA 0766, "baja";

          |ATRI 0;
     |FINSI;
|FPRC;

|PROGRAMA;
     aParam = PARAMETRO;
     swPortal = 0;
     |HAZ Portal;
     |SI swPortal = 0;
          |MENAV eaMensaPortal;
          |ACABA;
     |FINSI;

     |SUB_EJECUTA "labnct00";
     |PULSATECLA;
     |PULSATECLA;
     |PULSATECLA;
     |PULSATECLA;
     |PULSATECLA;
     |SI swSigue = 0;
          |ACABA;
     |SINO;
          |SI swSigue = 2;
                 swConectaSS = 1;
          |FINSI;
     |FINSI;

     aAlfa = Usuario;         |QBF aAlfa;
     aAlfa = "tm_labnct19_" + aAlfa;
     |NOME_DAT #100 aAlfa;
     |DELINDEX #100;

     |CLS;
     |CABEZA "Altas/Bajas Masivas de Trabajadores Online";
     |HAZ Defectos;
     |PINPA #0#0;

     ||HAZ Parametros;


     |ABRE #0;
     |DDEFECTO #0;
     |LEE 000#0p;
     |SI FSalida = 0;
          |BORRA 020#0a;
     |FINSI;

     |PINDA #0#0;
     |SI #0#0 = "          "; |O #0#1 = "          ";
          |HAZ Defectos;
          |PINDA #0#0;
     |FINSI;

     |SI aParam = "A"; |O aParam = "B";
          #0#2 = aParam;
          |PINTA #0#2;
          |CAMPO_MODIFICA #0#2, 1, "N";
     |FINSI;

     |ENDATOS #1#0;

     |SI FSalida = 0;
          |ABRE #100;
          |HAZ lectura;
          |CIERRA #100;

          || si se va a presentar un lineal con la seleccion este es el momento

          |SI seleccio = 0;
               |ATRI P;

               |SI #0#2 = "A";
                    |MENAV "    No se han encontrado altas de trabajadores en las fechas seleccionadas";
               |FINSI;
               |SI #0#2 = "B";
                    |MENAV "    No se han encontrado bajas de trabajadores en las fechas seleccionadas";
               |FINSI;

               |ATRI 0;
          |SINO;
               eaOpcion = #0#2;
               nOpcion = 1;
               |PARA; |SI nOpcion = 1; |HACIENDO;
                    |HAZ PintaPantalla;
                    |SI #0#2 = "A";
                         |ENTLINEAL #1#100, -1, 4, 22, 0, sel_min, sel_max;
                    |FINSI;
                    |SI #0#2 = "B";
                         |ENTLINEAL #1#100, -1, 2, 20, 0, sel_min, sel_max;
                    |FINSI;
                    |MENU aMenu;
                    nOpcion = FSalida;
                    |SI nOpcion = 1;
                         || vuelvo al lineal
                    |FINSI;
                    |SI nOpcion = 2;
                         |HAZ EnvioOnline;
                    |FINSI;
                    |SI nOpcion = 3;
                         || se saldra
                    |FINSI;
               |SIGUE;
          |FINSI;
     |FINSI;

     |DELINDEX #100;
     |CIERRA #100;

     |GRABA 000#0n;
     |CIERRA #0;
|FPRO;
