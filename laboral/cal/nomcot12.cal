|FICHEROS;
     nomcot01;
     nomcot02;
     nomcot09;

     nomcot22;
     nomcot32; || auxiliar

     labtraba;
     labempre;
     labcentr;
     nomcenes;
     nomconca;
|FIN;

|VARIABLES;
     &nEmp = 0;
     &nTra = 0;
     &nElAny = 0;
     &nElMes = 0;
     &aCCCReg = "";
     &aNaf = "";
     &enTipo = 0;
     aRegimen = "";

     &fFechaIniPer = @;
     &fFechaFinPer = @;

     FEstado = 0;
     nCampo = 0;
     aFecha = "";
     fFecha = @;
     nFecha = 0;
     aDia = ""; aMes = ""; aAny = "";

     aAlfa = "";
     swDistintos = 0;
     aAnterior = "";
     aNuevo = "";
     aDescMotivo = "";
     aMotivo1 = ""; aMotivo2 = ""; aMotivo3 = ""; aMotivo4 = ""; aMotivo5 = "";
     aDescMotivo1 = ""; aDescMotivo2 = ""; aDescMotivo3 = ""; aDescMotivo4 = "";
     aDescMotivo5 = "";
     swTramo = 0;
     nUltimoDiaGrabado = 0;

     nDiasAlta = 0;
     nDiasIT = 0;
     nDiasERE = 0;

     &nAny = 0;
     &nMes = 0;
     &nTopemes = 0;
     aTipoCot = "";
     nGrupoCot = 0;
     nDiaIni = 0;
     nDiaFin = 0;
     aSituacion = "";
     swSigue = 0;
     nNume = 0;
     nNume1 = 0;
     nAjuste = 0;
     nUltimoTramoIT = 0;
     swUltimoTramoIT_TP = 0;
     nUltimoTramoAlta = 0;

     nEmpOri = 0;
     nTraOri = 0;
     nSitDiaAnt = 0;
     nDiaIT = 0;
     &eaFichTramos = "";
     &enSwPasada09 = 0;

     nCuantosCEC = 0;
     swITUltimo = 0;
     nDesdeTra = 0;
     nHastaTra = 0;
     swPasada = 0;
     &nDiasITCEC = 0;
     &nDiasAltaCEC = 0;
     &swCandidatoCEC = 0;
     nAntTra = 0;
     nDiasITCECTot = 0;

     &prom_enf = 0;
     &prom_acc = 0;
     nDiasCotMes = 0;
     nDiasCotAcum = 0;

     nNroCCCs = 0;
     aAntCCC = "";
     nAntEmp = 0;
     nAntDia = 0;
     nAntCodTra = 0;
     &swFijoDisc = 0;

     nClave0 = 0; aClave0 = "";
     nClave1 = 0; aClave1 = "";
     nClave2 = 0; aClave2 = "";
     nClave3 = 0; aClave3 = "";
     nClave4 = 0; aClave4 = "";
     nClave5 = 0; aClave5 = "";
     nClave6 = 0; aClave6 = "";
     nCodigoTrab = 0;
     &runnom = "";
     swParcial = 0;
     swEnf = 0;
     swAcc = 0;
     swMatPat = 0;
     swMatPatTP = 0;
     nGuardaEmp = 0;
     nGuardaTra = 0;
     swERE = 0;
     swERETP = 0;
     &nDiasERETC_CEC = 0;
     &nDiasERETP_CEC = 0;
     swETP_ALT = 0;
     swETP_IT = 0;

     aAlfa1 = "";
     aAlfa2 = "";
     &nHorasCEC = 0;
|FIN;

|PROCESO HorasTP;
     |SI #labtraba#42 = "X"; |O #labtraba#42 = "T";
     |O #labtraba#42 = "P";
     ||O #labtraba#42 = "I";  || los "I" NO
          nNume = #labtraba#47;
          |SI nNume = 0;
               nNume = (#labtraba#234 / 7) ! 2;
          |FINSI;
     |SINO;
          nNume = (#nomconca#162 / 7);
     |FINSI;
     |SI #labtraba#42 = "C"; |O #labtraba#42 = "F";
          |SI #labtraba#47 ! 0;
               nNume = #labtraba#47;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO nomcot02_ajuste;
     #labtraba#0 = #nomcot02#0;
     #labtraba#1 = #nomcot02#7;
     |LEE 000#labtraba.=;

     #nomcot02#22 = #nomcot02#22 - nAjuste
     || de nuevo las horas tramo en los TP

     |SI aTipoCot = "X"; |O aTipoCot = "T";
     |O aTipoCot = "P";
     |O swFijoDisc = 1;
          |SI nUltimoTramoIT ! 0; || en tiempo parcial no hay ajuste en IT
          |Y nDiasERE ! nDiasIT;        || salvo que haya ERE, que entonces si hay ajuste
                                        || si todos los dias de IT son por ERE
                                        || (no hay IT+ERE)
               |SI #nomcot02#27 = "ETP";
                    || ERE parcial, en trabajador a tiempo parcial
                    |HAZ HorasTP;
                    #nomcot02#24 = (#nomcot02#22 * ((100 - #nomcot02#88) / 100) * nNume) ! 0;
               |SINO;
                    || ERE completo, no hay horas
                    #nomcot02#24 = 0;
               |FINSI;
          |SINO;
               |HAZ HorasTP;
               |SI #nomcot02#27 = "ETP";
                    #nomcot02#24 = (#nomcot02#22 * ((100 - #nomcot02#88) / 100) * nNume) ! 0;
               |SINO;
                    |SI #nomcot02#27 = "ERE";
                         #nomcot02#24 = 0;   || ERE completo
                    |SINO;
                         #nomcot02#24 = (#nomcot02#22 * nNume) ! 0;
                    |FINSI;
               |FINSI;
               |SI aTipoCot = "P"; |O aTipoCot = "I";
                    || esto ... puf, una barbaridad pero para irregulares, sobra
                    || ya veremos
                    |SI #labtraba#46 ! 0;
                         #nomcot02#24 = (#labtraba#46 * nNume) ! 0;
                    |FINSI;
               |FINSI;
          |FINSI;
     |SINO;
          |SI #nomcot02#27 = "ETP";
          |Y #nomcot02#98 = "ALT";      || en ETP+IT no saldr n horas
               || si hay ajuste, hay que ajustar tambien las horas del ERE a TP
               || trabajadas, que antes habia grabado
               |HAZ HorasTP;
               #nomcot02#24 = (#nomcot02#22 * ((100 - #nomcot02#88) / 100) * nNume) ! 0;
          |FINSI;
     |FINSI;

     swParcial = 0;
     nNume = #labtraba#45;
     |SI nNume ] 200; |Y nNume [ 299; swParcial = 1; |FINSI;
     |SI nNume ] 500; |Y nNume [ 599; swParcial = 1; |FINSI;

     |SI swParcial = 0;
     |Y #labtraba#237 = "S";  || pluriempleo, X-M, contrato 100,
     |Y #labtraba#129 ! "R";  || si no es Mat/pat TP parte de alta
                              || que entonces si que puede ser
     |Y #nomcot02#27 ! "ETP"; || ojo, en ETP si que van las horas en la parte
                              || de alta del tramo, aunque sea Tiempo Completo
          #nomcot02#24 = 0;
     |FINSI;

     #nomcot02#72 = 1;   || marco el tramo donde hago el ajuste

     |GRABA 020#nomcot02.a;
     |LIBERA #nomcot02;
|FPRC;

|DEFBUCLE nomcot02_ajuste;
     #nomcot02#1;
     ;
     nEmp, nElAny, nElMes, enTipo, aCCCReg, aNaf, nClave6, 00001;
     nEmp, nElAny, nElMes, enTipo, aCCCReg, aNaf, nClave6, 99999;
     ;
     NULL, nomcot02_ajuste;
|FIN;

|PROCESO FijoDiscHoras;
     swFijoDisc = 0;

     nNume = #labtraba#45;
     |SI nNume ] 300; |Y nNume [ 399;
          swFijoDisc = 1;
     |FINSI;
|FPRC;

|PROCESO Ajustes;
     nAjuste = 0;
     swSigue = 0;

     || primera condicion: mes que no sea de 30 dias
     |SI nElMes = 4; |O nElMes = 6; |O nElMes = 9; |O nElMes = 11;
          || no hay ajuste
     |SINO;
          swSigue = swSigue + 1;
          |SI nElMes = 2;
               nNume = nElAny $ 4;
               |SI nNume = 0;
                    nAjuste = -1;
               |SINO;
                    nAjuste = -2;
               |FINSI;
          |SINO;
               nAjuste = 1;
          |FINSI;
     |FINSI;

     || segunda condicion: alta todo el mes
     |SI nDiasAlta = nTopemes;
          swSigue = swSigue + 1;
     |FINSI;

     || tercera condicion: tipo de cotizacion
     |SI aTipoCot ! "D"; |Y aTipoCot ! "T";
     |Y aTipoCot ! "E";
     |Y aTipoCot ! "C"; |Y aTipoCot ! "F";
          swSigue = swSigue + 1;
     |FINSI;
     |SI aTipoCot = "C"; |O aTipoCot = "F";
          |SI #labtraba#33 ] 08;
               swSigue = swSigue - 1;
          |FINSI;
     |FINSI;
     |SI aTipoCot = "X"; |Y nDiasIT ! 0;
          swSigue = swSigue - 1;
          || ojo, no se hace esto en caso de que los dias de IT sean por ERE
          || todos ellos
          |SI nDiasERE ! 0;
          |Y nDiasERE = nDiasIT;
               swSigue = swSigue + 1;
          |FINSI;
     |FINSI;
     |SI swFijoDisc = 1; |Y nDiasIT ! 0;
          swSigue = swSigue - 1;
     |FINSI;

     |SI swSigue = 3; |Y nAjuste ! 0;
          nGuardaEmp = #labtraba#0;     || porque en el bucle pueden cambiar
          nGuardaTra = #labtraba#1;     || me quedo de donde venia

          |SI nUltimoTramoIT ! 0;
               nClave6 = nUltimoTramoIT;
          |SINO;
               nClave6 = nUltimoTramoAlta;
          |FINSI;

          |BUCLE nomcot02_ajuste;

          #labtraba#0 = nGuardaEmp;     || recupero a los valores anteriores
          #labtraba#1 = nGuardaTra;     || al bucle
          |LEE 000#labtraba.=;
     |FINSI;
|FPRC;

|PROCESO BuscaIT;
     |SI #nomcot01#9 ! 0; aSituacion = "ENF"; |FINSI;
     |SI #nomcot01#10 ! 0; aSituacion = "ACC"; |FINSI;
     |SI #nomcot01#11 ! 0; aSituacion = "MAT"; |FINSI;
     |SI #nomcot01#12 ! 0; aSituacion = "PAT"; |FINSI;
     |SI #nomcot01#13 ! 0; aSituacion = "RIE"; |FINSI;
     |SI #nomcot01#14 ! 0; aSituacion = "CON"; |FINSI;
     |SI #nomcot01#15 ! 0; aSituacion = "ERE"; |FINSI;
     ||SI #nomcot01#16 ! 0; aSituacion = "ABS"; |FINSI;
     |SI #nomcot01#17 ! 0; aSituacion = "HUE"; |FINSI;
     |SI #nomcot01#18 ! 0; aSituacion = "ASR"; |FINSI;
     |SI #nomcot01#19 ! 0; aSituacion = "INA"; |FINSI;

     |SI aSituacion = "MAT"; |Y #nomcot01#21 ! 0;
          aSituacion = "MTP";
     |FINSI;
     |SI aSituacion = "PAT"; |Y #nomcot01#21 ! 0;
          aSituacion = "PTP";
     |FINSI;
     |SI aSituacion = "ERE"; |Y #nomcot01#21 ! 0;
          aSituacion = "ETP";
     |FINSI;
     |SI aSituacion = "HUE"; |Y #nomcot01#21 ! 0;
          aSituacion = "HTP";
     |FINSI;
     nDiaIT = #nomcot01#30;

     |ERROR10;
|FPRC;

|DEFBUCLE BuscaIT;
     #nomcot01#1;
     ;
     nEmp, nElAny, nElMes, enTipo, aCCCReg, aNaf, nDiaIni;
     nEmp, nElAny, nElMes, enTipo, aCCCReg, aNaf, nDiaFin;
     ;
     NULL, BuscaIT;
|FIN;

|PROCESO nomcot02;
     |SI swPasada = 2;
          || solo para actualizar los tramos con los dias cotizables mes
          || y el numero de dias naturales del mes
          || y con el acumulado de dias cotizables que se lleva en cada tramo
          #nomcot02#95 = nDiasCotMes;
          #nomcot02#96 = nTopemes;

          nDiasCotAcum = nDiasCotAcum + #nomcot02#22;
          #nomcot02#97 = nDiasCotAcum;

          swFijoDisc = 0;
          |SI #labtraba#42 = "M";
               nNume = #labtraba#45;
               |SI nNume ] 300; |Y nNume [ 399;
                    swFijoDisc = 1;
               |FINSI;
          |FINSI;

          || hacemos otra cosa mas en esta pasada: el indicador 51M
          swSigue = 0;
          |SI #labtraba#33 ] 8;
               swSigue = swSigue + 1;
          |FINSI;
          |SI #labtraba#42 ! "T"; |Y #labtraba#42 ! "D"; |Y #labtraba#42 ! "A";
          |Y #labtraba#42 ! "E"; |Y #labtraba#42 ! "C"; |Y #labtraba#42 ! "F";
               swSigue = swSigue + 1;
          |SINO;
               |SI swFijoDisc = 1;
                    swSigue = swSigue + 1;
               |SINO;
                    |SI #labtraba#42 = "D"; |Y #labtraba#64 = "M";
                         swSigue = swSigue + 1;
                    |FINSI;
               |FINSI;
          |FINSI;
          |SI #labtraba#45 ! "421"; |Y #labtraba#45 ! "422";
               swSigue = swSigue + 1;
          |FINSI;
          |SI nTopemes ! 30; |Y nDiasAlta = nTopemes;
               swSigue = swSigue + 1;
          |FINSI;
          |SI #labtraba#42 = "X"; |O swFijoDisc = 1;
               |SI nDiasIT ! 0;
                    |SI nDiasERE ! 0;
                    |Y nDiasERE = nDiasIT;
                         || ojo, en caso de que los dias de IT sean TODOS por
                         || ERE, si que cotizo como los mensuales
                         swSigue = swSigue + 1;
                    |SINO;
                         || si no, si es por otra causa de IT, cotizo como
                         || diario, por 31 dias
                    |FINSI;
               |SINO;
                    swSigue = swSigue + 1;
               |FINSI;
          |SINO;
               swSigue = swSigue + 1;
          |FINSI;
          |SI #nomcot02#27 = "HUE"; |O #nomcot02#27 = "ASR";
               || nunca en huelga ni ASR
               swSigue = swSigue - 1;
          |FINSI;
          |SI swSigue = 5;
               #nomcot02#25 = "S";
               |SI #nomcot02#50 = 51; |O #nomcot02#52 = 51;
                    || trabajadores que ya lo tienen grabado, no hace falta otra vez
                    || esto es porque tienen dos codigos de trabajador
                    |ACABA;
               |FINSI;
               |SI #nomcot02#50 = 00;
                    #nomcot02#50 = 51;
                    #nomcot02#51 = "M";
               |SINO;
                    |SI #nomcot02#52 = 00;
                         #nomcot02#52 = 51;
                         #nomcot02#53 = "M";
                    |FINSI;
               |FINSI;
          |FINSI;

          |GRABA 020#nomcot02.a;
          |LIBERA #nomcot02;

          |ACABA;
     |FINSI;

     #labtraba#0 = #nomcot02#0;
     #labtraba#1 = #nomcot02#7;
     |LEE 000#labtraba.=;

     swParcial = 0;
     nNume = #labtraba#45;
     |SI nNume ] 200; |Y nNume [ 299; swParcial = 1; |FINSI;
     |SI nNume ] 500; |Y nNume [ 599; swParcial = 1; |FINSI;

     aTipoCot = #labtraba#42;

     |SI #labtraba#45 = 421; |O #labtraba#45 = 422;
          aTipoCot = "M";
     |FINSI;

     nGrupoCot = #labtraba#33;
     |SI aTipoCot = "D"; |Y #labtraba#64 = "M";
          aTipoCot = "M";
     |FINSI;

     || esto es para el caso de MAT o PAT a TP
     || en estos casos estoy conviertiendo en TP a un trabajador que en verdad
     || no lo es, eso me puede hacer cotizar si soy mensual en un mes de 31
     || dias por dias naturales cuando no deberia

     |SI aTipoCot = "X"; |Y swParcial = 0;
          |SI #labtraba#129 = "T"; |O #labtraba#129 = "R";
               aTipoCot = "M";
          |FINSI;
     |FINSI;

     aAlfa = #nomcot02#8;
     aAlfa = aAlfa % 102;
     nDiaIni = aAlfa;
     #nomcot02#20 = nDiaIni;    || dia inicio tramo

     aAlfa = #nomcot02#9;
     aAlfa = aAlfa % 102;
     nDiaFin = aAlfa;
     #nomcot02#21 = nDiaFin;    || dia fin tramo
     #nomcot02#22 = nDiaFin - nDiaIni + 1;   || dias naturales tramo

     |SI #labtraba#45 ! 421; |Y #labtraba#45 ! 422;
          |SI aTipoCot = "M"; |O aTipoCot = "X";
               |SI nGrupoCot ] 08;
                    #nomcot02#23 = "S";
               |FINSI;
          |FINSI;
     |FINSI;

     |HAZ FijoDiscHoras;

     || horas tramo en los TP
     |SI aTipoCot = "X"; |O aTipoCot = "T";
     |O aTipoCot = "P";
     ||O aTipoCot = "I";
     |O swFijoDisc = 1;
          |SI aTipoCot = "X"; |O aTipoCot = "T";
          |O aTipoCot = "P";
               nNume = #labtraba#47;
               |SI nNume = 0;
                    nNume = (#labtraba#234 / 7) ! 2;
               |FINSI;
          |SINO;
               nNume = (#nomconca#162 / 7);
          |FINSI;
          |SI aTipoCot = "C"; |O aTipoCot = "F";
               |SI #labtraba#47 ! 0;
                    nNume = #labtraba#47;
               |FINSI;
          |FINSI;
          #nomcot02#24 = (#nomcot02#22 * nNume) ! 0;
          |SI aTipoCot = "P";
               || esto ... puf, una barbaridad pero para irregulares, sobra
               || ya veremos
               |SI #labtraba#46 ! 0;
                    #nomcot02#24 = (#labtraba#46 * nNume) ! 0;
               |FINSI;
          |FINSI;
          |SI #nomcot02#24 = 0;
               #nomcot02#24 = 1;
          |FINSI;
     |FINSI;

     |SI swParcial = 0; |Y #labtraba#237 = "S";
          || pluriempleo, X-M, contrato 100,
          #nomcot02#24 = 0;
     |FINSI;

     || contrato
     #nomcot02#73 = #labtraba#45;

     || Situacion del tramo: ALT o si no, la IT que sea
     aSituacion = "";
     nDiaIT = 0;
     |BUCLE BuscaIT;
     |SI aSituacion = "";
          aSituacion = "ALT";
     |FINSI;
     |SI #labtraba#129 = "R";
          |SI aSituacion ! "ETP";
               aSituacion = "ALT";
          |FINSI;
     |FINSI;
     |SI #labtraba#129 = "T";
          |SI aSituacion = "MAT"; aSituacion = "MTP"; |FINSI;
          |SI aSituacion = "PAT"; aSituacion = "PTP"; |FINSI;
          |SI aSituacion = "ETP"; |Y #nomcot01#11 = 1;
               aSituacion = "MTP";
          |FINSI;
          |SI aSituacion = "ETP"; |Y #nomcot01#12 = 1;
               aSituacion = "PTP";
          |FINSI;
     |FINSI;
     #nomcot02#27 = aSituacion;

     |SI aSituacion = "ALT"; |Y #labtraba#129 = "R";
          |HAZ HorasTP;
          #nomcot02#24 = (#nomcot02#22 * nNume) ! 0;
     |FINSI;

     |SI aSituacion = "ETP";
          #nomcot02#88 = #nomcot01#21;
          |SI #labtraba#129 = "R";
               || en la parte trabajada, estoy parte en ERE a TP
               #nomcot02#88 = #nomcot01#31;   || me guard‚ aqui el % ERE TP
          |FINSI;
          #nomcot02#98 = "ALT";
          |SI #labtraba#42 = "X"; |O #labtraba#42 = "T";
          |O #labtraba#42 = "P";
          ||O #labtraba#42 = "I";
               nNume = #labtraba#47;
               |SI nNume = 0;
                    nNume = (#labtraba#234 / 7) ! 2;
               |FINSI;
          |SINO;
               nNume = (#nomconca#162 / 7);
          |FINSI;
          #nomcot02#24 = (#nomcot02#22 * ((100 - #nomcot02#88) / 100) * nNume) ! 0;
          nNume1 = #nomcot01#9 + #nomcot01#10 + #nomcot01#11;
          nNume1 = nNume1 + #nomcot01#12 + #nomcot01#13 + #nomcot01#14;
          |SI nNume1 ! 0;
               |SI #nomcot01#9 ! 0; #nomcot02#98 = "ENF"; |FINSI;
               |SI #nomcot01#10 ! 0; #nomcot02#98 = "ACC"; |FINSI;
               |SI #nomcot01#11 ! 0;
                    #nomcot02#98 = "MAT";
                    |SI #labtraba#129 = "R"; #nomcot02#98 = "ALT"; |FINSI;
                    |SI #labtraba#129 = "T"; #nomcot02#98 = "MAT"; |FINSI;
               |FINSI;
               |SI #nomcot01#12 ! 0;
                    #nomcot02#98 = "PAT";
                    |SI #labtraba#129 = "R"; #nomcot02#98 = "ALT"; |FINSI;
                    |SI #labtraba#129 = "T"; #nomcot02#98 = "PAT"; |FINSI;
               |FINSI;
               |SI #nomcot01#13 ! 0; #nomcot02#98 = "RIE"; |FINSI;
               |SI #nomcot01#14 ! 0; #nomcot02#98 = "CON"; |FINSI;
               |SI #nomcot02#98 ! "ALT";
                    #nomcot02#89 = #nomcot01#9;
                    #nomcot02#28 = nDiaIT;
                    #nomcot02#24 = 0;
                    #nomcot02#100 = 100 - #nomcot02#88;
               |FINSI;
          |FINSI;
     |FINSI;

     |SI aSituacion = "HTP";
          #nomcot02#87 = #nomcot01#21;
          |SI #labtraba#42 = "X"; |O #labtraba#42 = "T";
          |O #labtraba#42 = "P";
          ||O #labtraba#42 = "I";
               nNume = #labtraba#47;
               |SI nNume = 0;
                    nNume = (#labtraba#234 / 7) ! 2;
               |FINSI;
          |SINO;
               nNume = (#nomconca#162 / 7);
          |FINSI;
          #nomcot02#24 = (#nomcot02#22 * ((100 - #nomcot02#87) / 100) * nNume) ! 0;
     |FINSI;

     |SI aSituacion ! "INA";
     |Y aSituacion ! "PTP";   || en PTP y MTP, no cuento este dia, si no
     |Y aSituacion ! "MTP";   || lo cuento dos veces
          nDiasCotMes = nDiasCotMes + #nomcot02#22;
     |FINSI;

     |SI aSituacion = "ALT"; |O aSituacion = "ASR";
          |SI aSituacion = "ASR";
               |SI nUltimoTramoAlta = 0;
                    nUltimoTramoAlta = #nomcot02#6;
               |FINSI;
          |SINO;
               nUltimoTramoAlta = #nomcot02#6;
          |FINSI;
     |FINSI;

     |SI aSituacion = "ENF"; |O aSituacion = "ACC"; |O aSituacion = "MAT";
     |O aSituacion = "PAT"; |O aSituacion = "RIE"; |O aSituacion = "CON";
     |O aSituacion = "ERE";
     |O aSituacion = "MTP"; |O aSituacion = "PTP"; |O aSituacion = "ETP";
     |O aSituacion = "INA";
          nDiasIT = nDiasIT + #nomcot02#22;  || dias IT ajustables
          |SI aSituacion ! "ETP";
               #nomcot02#24 = 0;           || en IT nada de horas, si en ETP
          |FINSI;
          |SI aSituacion = "ERE"; |O aSituacion = "ETP";
               || ojo, me guardo aqui los dias en ERE, comprobado que en caso de
               || ERE, en los trabajadores mensuales a TP, NO hay que tratarlos
               || como diarios (como hacemos cuando estan en enf, acc, mat, pat ... )
               || hay que hacer el ajuste "normal" como si NO fueran a TP
               || tiquet 593.1345

               nDiasERE = nDiasERE + #nomcot02#22;
          |FINSI;

          |SI aSituacion = "MTP"; |O aSituacion = "PTP"; |O aSituacion = "ETP";
               || si hay ya otro tramo en IT, prefiero el otro a uno con algo
               || de esto a TP
               |SI nUltimoTramoIT = 0;
                    || si no existia otro tramo en IT, este es el ultimo
                    nUltimoTramoIT = #nomcot02#6;
                    || me guardo si el tramo en que voy a ajustar es a TP
                    swUltimoTramoIT_TP = 1;
               |SINO;
                    || si s¡ exist¡a, entonces me quedo con el anterior para
                    || ajustar en ese, SALVO que ese tambien fuera TP, en ese
                    || caso paso a ajustar en el que estoy ahora
                    |SI swUltimoTramoIT_TP = 1;
                         nUltimoTramoIT = #nomcot02#6;
                         || me guardo si el tramo en que voy a ajustar es a TP
                         swUltimoTramoIT_TP = 1;
                    |FINSI;
               |FINSI;
          |SINO;
               nUltimoTramoIT = #nomcot02#6;
          |FINSI;
     |FINSI;
     |SI aSituacion = "HUE";
          #nomcot02#24 = 0;    || en HUE si es un trab. a TP, no van las horas
     |FINSI;
     |SI aSituacion = "ENF";
          #nomcot02#89 = #nomcot01#9;
          #nomcot02#28 = nDiaIT;
     |FINSI;
     |SI aSituacion = "MTP"; |O aSituacion = "PTP";
          #nomcot02#75 = #nomcot01#7;
          #nomcot02#78 = #nomcot01#26;
     |FINSI;
     |SI aSituacion = "ACC"; |Y #nomcot01#10 = 9;
          || solo si es igual a 9 y por lo que sea no tiene que pagar prestaciones
          || en accidente (pago directo por ser jub. parcial por ejemplo)
          #nomcot02#89 = #nomcot01#10;
     |FINSI;

     |GRABA 020#nomcot02.a;
     |LIBERA #nomcot02;
|FPRC;

|DEFBUCLE nomcot02;
     #nomcot02#1;
     ;
     || nEmp, nElAny, nElMes, enTipo, "               ", aNaf, 01, 00001;
     || nEmp, nElAny, nElMes, enTipo, "zzzzzzzzzzzzzzz", aNaf, 31, 99999;
     || nEmp, nElAny, nElMes, enTipo, aCCCReg, aNaf, 01;
     || nEmp, nElAny, nElMes, enTipo, aCCCReg, aNaf, 31;
     nEmp, nElAny, nElMes, enTipo, aCCCReg, aNaf, 01, 00001;
     nEmp, nElAny, nElMes, enTipo, aCCCReg, aNaf, 31, 99999;
     be;
     NULL, nomcot02;
|FIN;

|PROCESO CompletaTramos;
     nUltimoTramoAlta = 0;
     nUltimoTramoIT = 0;
     swUltimoTramoIT_TP = 0;
     nDiasIT = 0;
     nDiasERE = 0;
     nDiasCotMes = 0;
     nDiasCotAcum = 0;
     aTipoCot = "";

     nAny = nElAny;
     nMes = nElMes;
     |HAZ topemes_plb;

     |HAZ FijoDiscHoras;

     swPasada = 1;
     |BUCLE nomcot02;    || busco los dias de IT por los tramos

     |SI nNroCCCs = 1;
          || mas de un CCC no hay que ajustar nada
          |HAZ Ajustes;
     |FINSI;

     |SI swSigue = 3; |Y nAjuste ! 0;
          nDiasCotMes = nDiasCotMes - nAjuste;
     |FINSI;

     swPasada = 2;
     |BUCLE nomcot02;    || actualizo tramos con dias cotizables mes

     || hay por poner lo del 51 M
|FPRC;

|PROCESO nomcot02_cierra;
     aDia = #nomcot32#6; |QBF aDia; aDia = ("00" + aDia) % -102;
     aMes = #nomcot32#2; |QBF aMes; aMes = ("00" + aMes) % -102;
     aAny = #nomcot32#1;
     aFecha = aDia + "." + aMes + "." + aAny;
     #nomcot02#9 = aFecha;
     nAntCodTra = #nomcot02#7;
     |GRABA 020#nomcot02.a;
     |LIBERA #nomcot02;
|FPRC;

|DEFBUCLE nomcot02_cierra;
     #nomcot02#1;
     ;
     nClave0, nClave1, nClave2, nClave3, aClave4, aClave5, nUltimoDiaGrabado, 00001;
     nClave0, nClave1, nClave2, nClave3, aClave4, aClave5, nUltimoDiaGrabado, 99999;
     be;
     NULL, nomcot02_cierra;
|FIN;

|PROCESO CierraPeriodo;
     || en el nomcot32 esta el ultimo dia leido

     nClave0 = #nomcot32#0;
     nClave1 = #nomcot32#1;
     nClave2 = #nomcot32#2;
     nClave3 = #nomcot32#3;
     aClave4 = #nomcot32#4;
     aClave5 = #nomcot32#5;

     |BUCLE nomcot02_cierra;
/*
     || tengo que cambiar esto por el bucle, para mas de un codigo

     |DDEFECTO #nomcot02;
     |PARA nCampo = 0; |SI nCampo [ 5; |HACIENDO nCampo = nCampo + 1;
          #nomcot02#nCampo# = #nomcot32#nCampo#;
     |SIGUE;
     #nomcot02#6 = nUltimoDiaGrabado;
     |LEE 101#nomcot02.=;
     |SI FSalida = 0;
          aDia = #nomcot32#6; |QBF aDia; aDia = ("00" + aDia) % -102;
          aMes = #nomcot32#2; |QBF aMes; aMes = ("00" + aMes) % -102;
          aAny = #nomcot32#1;
          aFecha = aDia + "." + aMes + "." + aAny;
          #nomcot02#9 = aFecha;
          |GRABA 020#nomcot02.a;
          |LIBERA #nomcot02;
     |FINSI;
*/
|FPRC;

|PROCESO Motivos;
     swDistintos = 1;

     |SI aNuevo = "0";
          aDescMotivo = "Final";
     |FINSI;
     |SI aNuevo = "1";
          aDescMotivo = "Inicio";
     |FINSI;
     |SI #nomcot22#1 = "ENF";
          |SI aNuevo = "1";
               aDescMotivo = "Inicio";
          |FINSI;
          |SI aNuevo = "2";
               aDescMotivo = "2do. Tramo";
          |FINSI;
          |SI aNuevo = "3";
               aDescMotivo = "3er. Tramo";
          |FINSI;
     |FINSI;
     |SI #nomcot22#1 = "BON";
          |SI aNuevo = "";
               aDescMotivo = aAnterior + " Final";
          |SINO;
               aAlfa = aNuevo % -101;
               |SI aAlfa = "T";
                    aDescMotivo = aNuevo + " 2do.T";
               |SINO;
                    aDescMotivo = aNuevo + " Inicio";
               |FINSI;
          |FINSI;
     |FINSI;
     |SI #nomcot22#1 = "SIT";
          aDescMotivo = #nomcot01#29;
     |FINSI;

     |SI #nomcot22#1 = "POR";
          aDescMotivo = #nomcot01#21;
     |FINSI;
     |SI aMotivo1 = "";
          aMotivo1 = #nomcot22#1;
          aDescMotivo1 = aDescMotivo;
     |SINO;
          |SI aMotivo2 = "";
               aMotivo2 = #nomcot22#1;
               aDescMotivo2 = aDescMotivo;
          |SINO;
               |SI aMotivo3 = "";
                    aMotivo3 = #nomcot22#1;
                    aDescMotivo3 = aDescMotivo;
               |SINO;
                    |SI aMotivo4 = "";
                         aMotivo4 = #nomcot22#1;
                         aDescMotivo4 = aDescMotivo;
                    |SINO;
                         |SI aMotivo5 = "";
                              aMotivo5 = #nomcot22#1;
                              aDescMotivo5 = aDescMotivo;
                         |FINSI;
                    |FINSI;
               |FINSI;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Comprueba;
     nCampo = #nomcot22#0;

     |SI #nomcot01#nCampo# ! #nomcot32#nCampo#;
          |SI #nomcot32#16 = 1; |Y #nomcot22#1 = "POR";
               || si el cambio de debido a un nuevo porcentaje de jornada
               || derivado de un absentismo, no hay que hacer nuevo tramo
               || ya que el absentismo no genera nuevo tramo
               |ACABA;
          |FINSI;

          aAnterior = #nomcot32#nCampo#;
          |QBF aAnterior;
          aNuevo = #nomcot01#nCampo#;
          |QBF aNuevo;

          |HAZ Motivos;
     |FINSI;
|FPRC;

|DEFBUCLE Comprueba;
     #nomcot22#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, Comprueba;
|FIN;

|PROCESO GrabaTramoTrab;
     || valores de la clave, igual que los del nomcot02
     |DDEFECTO #nomcot02;
     |PARA nCampo = 0; |SI nCampo [ 6; |HACIENDO nCampo = nCampo + 1;
          #nomcot02#nCampo# = #nomcot01#nCampo#;
     |SIGUE;
     #nomcot02#7 = nCodigoTrab;
     |LEE 101#nomcot02.=;
     FEstado = FSalida;
     aDia = #nomcot02#6; |QBF aDia; aDia = ("00" + aDia) % -102;
     aMes = #nomcot02#2; |QBF aMes; aMes = ("00" + aMes) % -102;
     aAny = #nomcot02#1;
     aFecha = aDia + "." + aMes + "." + aAny;
     fFecha = aFecha;
     #nomcot02#8 = fFecha;

     #nomcot02#29 = #labtraba#2;
     #nomcot02#70 = fFechaIniPer;
     #nomcot02#71 = fFechaFinPer;

     || descripciones para saber por que se crea tramo
     |SI swTramo = 0;
          #nomcot02#10 = "INI"; #nomcot02#11 = "Inicio Per";
     |FINSI;
     |SI swTramo = 1;
          || lo grabo por inicio de tramo por algun cambio de "algo"
          #nomcot02#10 = aMotivo1; #nomcot02#11 = aDescMotivo1;
          #nomcot02#12 = aMotivo2; #nomcot02#13 = aDescMotivo2;
          #nomcot02#14 = aMotivo3; #nomcot02#15 = aDescMotivo3;
          #nomcot02#16 = aMotivo4; #nomcot02#17 = aDescMotivo4;
          #nomcot02#18 = aMotivo5; #nomcot02#18 = aDescMotivo5;
     |FINSI;

     || bonificaciones
     || me guardo la bonificacion que exista en este tramo

     #nomcot02#90 = #nomcot01#22;
     #nomcot02#91 = #nomcot01#23;
     #nomcot02#92 = #nomcot01#24;
     #nomcot02#93 = #nomcot01#25;

     #labtraba#0 = #nomcot02#0;    || vuelvo a leer labtraba, para comprobar si
     #labtraba#1 = #nomcot02#7;    || de verdad existe la bonificacion, puede
     |LEE 000#labtraba.=;          || que aunque no tenga bonif, la grabe, en casos
                                   || de maternidad a TP
                                   || mira tiquet 2404.1120

     |SI #nomcot01#21 ! 0;         || IT a TP
     |Y #nomcot01#26 ! 0;          || efectivamente, dos trab.
                                   || hago la comprobacion

          |SI #nomcot02#90 ! "    ";
               nNume = #nomcot02#90;
               |SI nNume ! #labtraba#39;     || si no es la misma bonif asumo
                    #nomcot02#90 = "";       || que no hay bonif
               |FINSI;
          |FINSI;
     |FINSI;
     |SI FEstado = 0;
          |GRABA 020#nomcot02.a;
          |LIBERA #nomcot02;
     |SINO;
          |GRABA 020#nomcot02.n;
          |LIBERA #nomcot02;
     |FINSI;
     nUltimoDiaGrabado = #nomcot02#6;   || me hara falta para saber que
                                        || tramo es el final para cerrarlo

     |SI swTramo = 1;
          || es un tramo que no es el primero, uno que ha habido que
          || crear por algun motivo, en este caso cierro el tramo anterior
          || con el dia antes de la fecha fin de este

          || primero vuelvo a situarme en el tramo actual
          |PARA nCampo = 0; |SI nCampo [ 6; |HACIENDO nCampo = nCampo + 1;
               #nomcot02#nCampo# = #nomcot02#nCampo#;
          |SIGUE;
          |LEE 101#nomcot02.=;

          || cierro el tramo anterior, lo leo primero
          || tengo que hacer bucle, por si hubiera mas de uno por
          || MTP o PTP

          #nomcot02#0 = nAntEmp;
          #nomcot02#4 = aAntCCC;
          #nomcot02#6 = nAntDia;
          #nomcot02#7 = nAntCodTra;
          |LEE 101#nomcot02.=;
          ||LEE 101#nomcot02.a;
          |SI FSalida = 0;
               nFecha = fFecha;
               nFecha = nFecha - 1;
               fFecha = nFecha;
               #nomcot02#9 = fFecha;
               |GRABA 020#nomcot02.a;
               |LIBERA #nomcot02;

               || se supone que esto no tiene que dar ningun problema
               || cuidado si lo da, viene por el tiquet 3430.1456

               || tengo otro trabajador por MTP o PTP en el mismo tramo,
               || tengo que cerrar los dos

               || esto era para que solo lo hiciera en MTP o PTP, pero tiene
               || que hacerlo en cualquier caso en que haya dos codigos
               || de alta en el mismo periodo
               ||SI #nomcot32#11 ! 0; |O #nomcot32#12 ! 0;
                    ||SI #nomcot32#21 ! 0; |Y #nomcot32#26 ! 0;
                    |SI #nomcot32#26 ! 0;
                         || el que tengo que cerrar es uno de los dos
                         || o el #7 o el #26
                         |SI #nomcot32#26 ! nAntCodTra;
                              #nomcot02#7 = #nomcot32#26;
                              |LEE 101#nomcot02.=;
                              |SI FSalida = 0;
                                   #nomcot02#9 = fFecha;
                                   |GRABA 020#nomcot02.a;
                                   |LIBERA #nomcot02;
                              |FINSI;
                         |FINSI;
                         |SI #nomcot32#7 ! nAntCodTra;
                              #nomcot02#7 = #nomcot32#7;
                              |LEE 101#nomcot02.=;
                              |SI FSalida = 0;
                                   #nomcot02#9 = fFecha;
                                   |GRABA 020#nomcot02.a;
                                   |LIBERA #nomcot02;
                              |FINSI;
                         |FINSI;
                    |FINSI;
               ||FINSI;
          |FINSI;
     |FINSI;

     || aqui me quedo con el ultimo trab con el que he abierto un tramo
     nAntCodTra = nCodigoTrab;
|FPRC;

|PROCESO GrabaTramo;
     || si hay que cerrar el tramo anterior, me quedo con los valores que
     || tenia la clave del tramo en curso, para despues buscarlo, ya que
     || no puedo usar el LEE nomcot02.a
     || me falta el CCC anterior, pero eso lo tengo del aAntCCC de los dias

     nAntEmp = #nomcot02#0;
     /*
     nAntCodTra = #nomcot02#7;
     |SI swTramo = 0;        || indicador tramo de inicio
          || en el tramo inicial no esta cargado aun el nomcot02
          nAntCodTra = #nomcot01#7;
     |FINSI;
     */
     nAntDia = nUltimoDiaGrabado;

     || migue: cambio de sistema para mas de un codigo en mismo tramo

     nCodigoTrab = #nomcot01#7;
     |HAZ GrabaTramoTrab;

     nCodigoTrab = #nomcot01#26;
     |SI nCodigoTrab ! 00000; |HAZ GrabaTramoTrab; |FINSI;

     nCodigoTrab = #nomcot01#27;
     |SI nCodigoTrab ! 00000; |HAZ GrabaTramoTrab; |FINSI;

     nCodigoTrab = #nomcot01#28;
     |SI nCodigoTrab ! 00000; |HAZ GrabaTramoTrab; |FINSI;
|FPRC;


|PROCESO LeeDias;
     || esto para contar los CCCs que hay, si hay mas de uno, nunca ajustare
     aAlfa = #nomcot01#4;
     |QBF aAlfa;
     |SI aAntCCC ! aAlfa;
          nNroCCCs = nNroCCCs + 1;
     |FINSI;

     || en primer lugar, si me viene con situacion 0 es porque este dia no
     || estoy de alta, entonces si el dia anterior la sit. era distinta de 0
     || cierro el periodo (el anterior fue el ultimo dia de alta del trabajador)
     || esto es para bajas a mitad de mes

     || si el anterior tambien fue 0, sigo buscando, o bien estoy recorriendo
     || el mes hasta el final y ya he sido baja un dia anterior (.. 1111000 ...)
     || o bien aun no he sido alta (000011111 ...)

     |SI #nomcot01#8 = 0;
          |SI nSitDiaAnt ! 0;
               |HAZ CierraPeriodo;
          |FINSI;

          aAntCCC = #nomcot01#4;   || esto tambien esta al final y no lo hacia
          |QBF aAntCCC;            || aqui, antes del ACABA, no ha dado error
                                   || pero lo pongo que es lo normal 13.01.2020
          nSitDiaAnt = #nomcot01#8;
          |ACABA;

          || acabo aqui quedandome con el nSitDiaAnt para el siguiente dia que
          || lea, ojo, no me quedo con todos los valores en el nomcot32 (imagino
          || que no hace falta)
     |SINO;
          || tengo un dia que soy alta

          nDiasAlta = nDiasAlta + 1;

          |SI nSitDiaAnt = 0;
               || el dia anterior estaba a cero, es el primer dia del periodo
               || de esta ficha, por tanto grabo el tramo de inicio

               swTramo = 0;        || indicador tramo de inicio
               |HAZ GrabaTramo;
          |SINO;
               || segundo dia y siguientes, veamos primero si la situacion del
               || dia es igual a la del dia anterior
               || comparamos los campos que si cambian, hay que cerrar tramo y
               || generar otro

               swDistintos = 0;
               aMotivo1 = ""; aMotivo2 = ""; aMotivo3 = ""; aMotivo4 = "";
               aMotivo5 = "";
               aDescMotivo1 = ""; aDescMotivo2 = ""; aDescMotivo3 = "";
               aDescMotivo4 = ""; aDescMotivo5 = "";
               |BUCLE Comprueba;

               swTramo = 1;        || indicador resto de tramos (= 0 si es el
                                   || primer tramo de la fich)
               |SI swDistintos = 1;
                    || grabo tramo si en el BUCLE Comprueba he detectado alguna
                    || variacion de situacion

                    |HAZ GrabaTramo;
               |FINSI;
          |FINSI;
     |FINSI;

     || esto siempre, para todos los dias, en este temporal me quedo los
     || valores del nomcot01 actual para compararlo con el siguiente que lea

     |PARA nCampo = 0; |SI nCampo [ 30; |HACIENDO nCampo = nCampo + 1;
          #nomcot32#nCampo# = #nomcot01#nCampo#;
     |SIGUE;
     nSitDiaAnt = #nomcot01#8;

     || valores necesarios para la clave del dia anterior, por si
     || un cambio de CCC y hay que cerrar el tramo
     aAntCCC = #nomcot01#4;
     |QBF aAntCCC;
|FPRC;

|DEFBUCLE LeeDias;
     #nomcot01#1;
     ;
     nEmp, nElAny, nElMes, enTipo, aCCCReg, aNaf, 01;
     nEmp, nElAny, nElMes, enTipo, aCCCReg, aNaf, 31;
     || nEmp, nElAny, nElMes, enTipo, "               ", aNaf, 01;
     || nEmp, nElAny, nElMes, enTipo, "zzzzzzzzzzzzzzz", aNaf, 31;
     || aNaf, enTipo, nElAny, nElMes, 01, "               ", nEmp;
     || aNaf, enTipo, nElAny, nElMes, 31, "zzzzzzzzzzzzzzz", nEmp;
     ;
     NULL, LeeDias;
|FIN;

/*
|DEFBUCLE LeeDias;
     #nomcot01#2;
     ;
     || nEmp, nElAny, nElMes, enTipo, aCCCReg, aNaf, 01;
     || nEmp, nElAny, nElMes, enTipo, aCCCReg, aNaf, 31;
     || nEmp, nElAny, nElMes, enTipo, "               ", aNaf, 01;
     || nEmp, nElAny, nElMes, enTipo, "zzzzzzzzzzzzzzz", aNaf, 31;
     aNaf, enTipo, nElAny, nElMes, 01, "               ", nEmp;
     aNaf, enTipo, nElAny, nElMes, 31, "zzzzzzzzzzzzzzz", nEmp;
     ;
     NULL, LeeDias;
|FIN;
*/

/*
|PROCESO BorraPrevioTra;
     |BORRA 020#nomcot02.a;
     |LIBERA #nomcot02;
|FPRC;

|DEFBUCLE BorraPrevioTra;
     #nomcot02#3;
     ;
     nEmp, nElAny, nElMes, enTipo, aCCCReg, aNaf, nTraOri, 01;
     nEmp, nElAny, nElMes, enTipo, aCCCReg, aNaf, nTraOri, 99;
     be;
     NULL, BorraPrevioTra;
|FIN;
*/

|PROCESO DiasCEC;
     |SI swPasada = 1;
          |SI #nomcot02#27 = "ALT";
               nDiasAltaCEC = nDiasAltaCEC + #nomcot02#22;
          |SINO;
               |SI #nomcot02#27 = "ETP";
                    nNume = #nomcot02#22 * (#nomcot02#88 / 100);
                    nDiasERETP_CEC = nDiasERETP_CEC + nNume;
                    |SI #nomcot02#98 = "ALT";
                         nNume = #nomcot02#22 * ((100 - #nomcot02#88) / 100);
                         nDiasAltaCEC = nDiasAltaCEC + nNume;
                         nNume = #nomcot02#22 * (#nomcot02#88 / 100);
                         nDiasITCEC = nDiasITCEC + nNume;
                    |SINO;
                         nDiasITCEC = nDiasITCEC + #nomcot02#22;
                    |FINSI;
               |SINO;
                    nDiasITCEC = nDiasITCEC + #nomcot02#22;
                    |SI #nomcot02#27 = "ERE";
                         nDiasERETC_CEC = nDiasERETC_CEC + #nomcot02#22;
                    |FINSI;
               |FINSI;
          |FINSI;
          nHorasCEC = nHorasCEC + #nomcot02#24;
     |FINSI;
     |SI swPasada = 2;
          |SI nAntTra ! #nomcot02#7;
               nCuantosCEC = nCuantosCEC + 1;
          |FINSI;
          |SI #nomcot02#27 ! "ALT";
               nDiasITCECTot = nDiasITCECTot + #nomcot02#22;
          |FINSI;
          |SI #nomcot02#27 = "ERE";
               swERE = 1;
          |FINSI;
          |SI #nomcot02#27 = "ETP";
               swERETP = 1;
          |FINSI;
          |SI #nomcot02#27 = "ENF";
               swEnf = 1;
          |FINSI;
          |SI #nomcot02#27 = "ACC";
               swAcc = 1;
          |FINSI;
          |SI #nomcot02#27 = "PAT"; |O #nomcot02#27 = "MAT";
               swMatPat = 1;
          |FINSI;
          |SI #nomcot02#27 = "PTP"; |O #nomcot02#27 = "MTP";
               swMatPatTP = 1;
          |FINSI;
          |SI #nomcot02#27 = "ETP"; |Y #nomcot02#98 = "ALT";
               swETP_ALT = 1;
          |FINSI;
          |SI #nomcot02#27 = "ETP";
               |SI #nomcot02#98 = "ENF"; |O #nomcot02#98 = "ACC";
               |O #nomcot02#98 = "MAT"; |O #nomcot02#98 = "PAT";
                    swETP_IT = 1;
               |FINSI;
          |FINSI;
          nAntTra = #nomcot02#7;
     |FINSI;
     |SI swPasada = 3;
          |SI #nomcot02#27 ! "ALT";
               swITUltimo = 1;
               || ojo
               || si en el mismo mes hay MAT + MAT TP o PAT + PAT TP
               || se ajusta en el segmento MAT o PAT
               |SI swMatPatTP = 1;
                    |SI #nomcot02#27 = "MTP"; |O #nomcot02#27 = "PTP";
                         swITUltimo = 0;
                         || CandidatoCEC se activara
                    |FINSI;
               |FINSI;

               || otro caso: si en el mismo mes una ficha en  ALT y otra
               || en ALT+ERE, la nomina no esta ajustando en la ultima, lo
               || cual deberia hacer, lo soluciono aqui
               |SI swERE = 1;
                    swITUltimo = 0;
                    || Se activa CandidatoCEC tambien
               |FINSI;

               |SI swETP_ALT = 1; |Y swETP_IT = 1;
                    swITUltimo = 0;
                    || Se activa CandidatoCEC tambien
               |FINSI;
          |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE DiasCEC;
     #nomcot02#3;
     ;
     nEmp, nElAny, nElMes, enTipo, aCCCReg, aNaf, nDesdeTra, 01;
     nEmp, nElAny, nElMes, enTipo, aCCCReg, aNaf, nHastaTra, 99;
     ;
     NULL, DiasCEC;
|FIN;

|PROCESO Contadores;
     swCandidatoCEC = 0;
     || primera pasada: cuento los dias cotizables de alta e IT del trabajador
     || que estoy calculando
     nDiasAltaCEC = 0;
     nDiasITCEC = 0;
     nDiasERETC_CEC = 0;
     nDiasERETP_CEC = 0;
     nDesdeTra = nTraOri;
     nHastaTra = nTraOri;
     swPasada = 1;
     |BUCLE DiasCEC;

     || segunda pasada: cuento cuantas fichas hay
     nCuantosCEC = 0;
     nDesdeTra = 00001;
     nHastaTra = 99999;
     nDiasITCECTot = 0;
     nAntTra = 0;
     swPasada = 2;
     swEnf = 0;
     swAcc = 0;
     swMatPat = 0;
     swMatPatTP = 0;
     swERE = 0;
     swERETP = 0;
     swETP_ALT = 0;
     swETP_IT = 0;
     |BUCLE DiasCEC;

     || tercera pasada: si tengo mas de una ficha y ademas tengo IT
     || miro si la IT esta en el ultimo
     |SI nCuantosCEC > 1; |Y nDiasITCECTot ! 0;
          swITUltimo = 0;
          swPasada = 3;
          nDesdeTra = nAntTra;
          nHastaTra = nAntTra;
          |BUCLE DiasCEC;
     |FINSI;
     |SI swETP_ALT = 1; |Y swETP_IT = 1;
          swCandidatoCEC = 1;
     |FINSI;
     |SI swERE = 1; |O swERETP = 1;
          || 26.05.2020
          || despues de muchos cambios, esto sigue saliendo mal, asi que
          || de una vez, que los dias vengan de aqui, de moemento en caso
          || de ERE+IT tambien
          |SI swEnf = 1; |O swAcc = 1;
               swCandidatoCEC = 1;
          |FINSI;
     |FINSI;
     |SI swERE = 1; |Y swERETP = 1;
          || lo mismo
          swCandidatoCEC = 1;
     |FINSI;
     |SI nCuantosCEC > 1; |Y nDiasITCECTot ! 0; |Y swITUltimo = 0;
          |SI #labtraba#42 ! "D"; |Y #labtraba#42 ! "T";
               swCandidatoCEC = 1;
          |FINSI;
          |SI #labtraba#42 = "C"; |O #labtraba#42 = "F";
               |SI #labtraba#33 ] 08;
                    swCandidatoCEC = 0;
               |FINSI;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO BuscaCCC_12;
     |SI #labtraba#42 = "H"; |O #labtraba#247 = 0138;
          |ACABA;
     |FINSI;

     aCCCReg = "";
     nEmp = #labtraba#0;
     nTra = #labtraba#1;
     aAlfa = #labtraba#247;
     |QBF aAlfa;
     aAlfa = ("0000" + aAlfa) % -104;
     aRegimen = aAlfa;

     |ABRE #labcentr;
     #labcentr#0 = #labtraba#0;
     #labcentr#1 = #labtraba#77;
     |LEE 000#labcentr.=;
     |CIERRA #labcentr;

     |SI #labtraba#45 = 421; |O #labtraba#45 = 422;
          |ABRE #nomcenes;
          #nomcenes#0 = nEmp;
          #nomcenes#13 = #labtraba#77;
          #nomcenes#1 = 87;
          |LEE 000#nomcenes.=;
          |SI FSalida = 0;
               aCCCReg = #nomcenes#10;
          |SINO;
               aCCCReg = #labcentr#10;
          |FINSI;
          |CIERRA #nomcenes;
     |SINO;
          aCCCReg = #labcentr#10;
     |FINSI;
     |QBF aCCCReg;
     |SI aCCCReg = "";
          aCCCReg = #labempre#13;
          |QBF aCCCReg;
     |FINSI;
     aCCCReg = aRegimen + aCCCReg;

     aNaf = #labtraba#75;
     |QBF aNaf;
     aAlfa1 = aNaf % 102;
     aAlfa2 = aNaf % 310;
     |QBF aAlfa2;
     aAlfa2 = ("0000000000" + aAlfa2) % -110;
     aNaf = aAlfa1 + aAlfa2;
|FPRC;

|PROCESO Tramos;
     |SI runnom = "nomcalfi";
          |ACABA;
     |FINSI;
     |SI enSwPasada09 = 1;
          || esto lo hago aqui porque me hara falta para los dias de la nomina
          || en ciertos casos, y si estoy en la pasada 09 y no sigo, no tendre
          || estos valores
          nEmpOri = #labtraba#0;
          nTraOri = #labtraba#1;
          |HAZ Contadores;

          || estoy en la pasada en que calculo las nominas del nomcot09
          || es decir las que he ido guardando en el tmp que tengo que
          || calcular siempre por ser de un NIF

          || no tengo que hacer distribucion porque ya la he hecho en la
          || primera ficha calculada
          |ACABA;
     |FINSI;
/*
     || esto ya no, lo de antes

     || lo primero es comprobar que esta ficha no tengo grabado nada en el
     || nomcot09. En ese me voy grabando los trabajadores de los que he ido
     || creando tramos y si los calculo (porque estoy en el calculo de ellos
     || mismos) o no (porque he creado tramos porque eran parte de otra ficha)

     |NOME_DAT #nomcot09#eaFichTramos#;
     |SALVA_FICHA #nomcot09;
     |ABRE #nomcot09;
     #nomcot09#0 = nEmpOri;
     #nomcot09#1 = nTraOri;
     |LEE 101#nomcot09.=;
     |SI FSalida = 0;
          |CIERRA #nomcot09;
          |REPON_FICHA #nomcot09;
          |ACABA;
     |FINSI;
     |CIERRA #nomcot09;
     |REPON_FICHA #nomcot09;
*/
     || si ha pasado la prueba, continuo creando los tramos

     |ABRE #nomcot01;
     |ABRE #nomcot02;
     |ABRE #nomcot32;

     nEmpOri = #labtraba#0;
     nTraOri = #labtraba#1;

     ||BUCLE BorraPrevioTra;

     |SELECT #1#nomcot02;

     |DDEFECTO #nomcot32;

     || leo del nomcot01 que tiene la distribucion de dias
     nDiasAlta = 0;
     nSitDiaAnt = 0;
     nNroCCCs = 0;
     aAntCCC = "";
     nEmp = #labtraba#0;

     |HAZ BuscaCCC_12;
     |BUCLE LeeDias;
     |SI nDiasAlta ! 0;
          |HAZ CierraPeriodo;

          || ahora voy a rellenar el resto de campos que me haran falta
          || para pasar despues al cretam04

          |HAZ BuscaCCC_12;
          |HAZ CompletaTramos;
     |FINSI;

     #labtraba#0 = nEmpOri;
     #labtraba#1 = nTraOri;
     |LEE 000#labtraba.=;

     nHorasCEC = 0;
     |HAZ Contadores;

     |CIERRA #nomcot32;
     |CIERRA #nomcot02;
     |CIERRA #nomcot01;
|FPRC;
