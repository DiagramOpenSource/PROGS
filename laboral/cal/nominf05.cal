|FICHEROS;
     nominf05 #0;
     nomhicca;

     labempre;
     labcentr;
     labtraba;
     nomcnahi;
     nomcnama;
     nomocuma;
     nomepigr;
     nominft5 #100;

     nombofli;

     nombofnu;
     nombofn1;
     nomconst;
|FIN;

|VARIABLES;
     aAlfa = "";
     aFecha = "";
     fFecha = @;

     nPorIT = 0;
     nPorIMS = 0;
     nPorIT_it = 0;
     nPorIMS_it = 0;

     nBaseIT = 0;
     nBaseMat = 0;
     nBaseAlta = 0;

     nCuotaAlta = 0;
     nCuotaIT = 0;
     nCuotaIMS = 0;
     nCuota_it = 0;
     nCuotaIT_it = 0;
     nCuotaIMS_it = 0;

     nDesdeA¤o = 0;
     nHastaA¤o = 0;
     aAny = "";
     aMes = "";
     nDesdeMes = 0;
     nHastaMes = 0;
     aImporte = "";
     nBaseERE = 0;
     nContador = 0;
     FEstado = 0;
     aAlfa1 = "";

     nCuotaTotal = 0;
     nTotalTrab = 0;
     nPorc = 0;
     nMedia = 0;
     aInformeSN = "";
     aParam = "";
     swNuevo = 0;
     nCreditoEmpNue = 0;
     nCreditoCenNue = 0;
     nBonifCons = 0;
     nEmpAnt = 0;
     nNume = 0;
     alfa1 = "";
     alfa2 = "";
     alfa3 = "";
     alfa4 = "";
     nPorFP = 0;
     swAgrario = 0;
     nAgrarios = 0;
     nEmp      = 0;

     &nAny      = 0;
     &nMes      = 0;
     &nImporte  = 0;
     &nCredito  = 0;
     &fCFecha   = @;
     &aCError   = "";
     &nCConst   = 0;
     &nCuota    = 0;
     &nBase     = 0;
     &enCredFor = 0;
     &enCredCon = 0;
|FIN;

|PROCESO Defectos; |TIPO 8;
     fFecha = ~;
     aFecha = fFecha;
     aAny = aFecha % -104;
     nAny = aAny;
     #0#2 = nAny;
     |PINTA #0#2;

     aMes = aFecha % 402;
     nMes = aMes;
     #0#3 = aMes;
     |PINTA #0#3;
|FPRC;

|PROCESO nombofli;
     nBonifCons = nBonifCons + #nombofli#3;
|FPRC;

|DEFBUCLE nombofli;
     #nombofli#1;
     ;
     #100#0, #0#2,   01, 001, " ", "B";
     #100#0, #0#2, #0#3, 999, "F", "B";
     ;
     NULL, nombofli;
|FIN;

|PROCESO Consumido;
     |SI #100#0 = nEmpAnt;
          |ACABA;
     |FINSI;

     nBonifCons = 0;
     |BUCLE nombofli;
     #100#13 = nBonifCons;
     |GRABA 020#100a;         || lo grabo en el primer registro que encuentre
     |LIBERA #100;

     nEmpAnt = #100#0;
|FPRC;

|DEFBUCLE Consumido;
     #100#1;
     ;
     INICIO;
     FINAL;
     be;
     NULL, Consumido;
|FIN;

|PROCESO Impresion;
     |PRINT;
|FPRC;

|DEFBUCLE Impresion;
     #100#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, Impresion;
|FIN;

|PROCESO PintaImporte;
/*
     nImporte = #0#4;
     aImporte = nImporte;
     |QBF aImporte;
     aAlfa = aImporte % -301;
     |SI aAlfa ! ".";
          aAlfa = aImporte % -201;
          |SI aAlfa ! ".";
               aImporte = aImporte + ".00";
               aImporte = ("       " + aImporte) % -107;
          |FINSI;
     |FINSI;

     |ATRI S;
     |PINTA 2055, aImporte;
     |ATRI 0;
*/
|FPRC;

|PROCESO Epigrafes;
     #nomepigr#0 = #labtraba#32;
     |LEE 000#nomepigr.=;
     nPorIT = #nomepigr#1;
     nPorIMS = #nomepigr#2;

     #nomepigr#0 = 126;
     |LEE 000#nomepigr.=;
     nPorIT_it = #nomepigr#1;
     nPorIMS_it = #nomepigr#2;
|FPRC;

|PROCESO CNAE_Ocup;
     aAlfa = #labtraba#204;
     |QBF aAlfa;
     |SI aAlfa ! "";
          #nomocuma#6 = fFecha;
          #nomocuma#0 = aAlfa;
          |LEE 000#nomocuma.=;
          |SI FSalida = 0;
               nPorIT = #nomocuma#2;
               nPorIMS = #nomocuma#3;
          |FINSI;
     |SINO;
          aAlfa = #labcentr#22;
          |QBF aAlfa;
          |SI aAlfa ! "";          || CNAE por Centro
               |SI 2008 [ nAny; |Y nAny [ 2009;
                    #nomcnahi#2 = nAny;
                    #nomcnahi#0 = #nomhicca#0;
                    #nomcnahi#4 = #labtraba#77;
                    |LEE 000#nomcnahi.=;
                    |SI FSalida = 0;
                         aAlfa = #nomcnahi#3;
                    |FINSI;
               |FINSI;
          |SINO;
               aAlfa = #labempre#128;
               |SI 2008 [ nAny; |Y nAny [ 2009;
                    #nomcnahi#2 = nAny;
                    #nomcnahi#0 = #nomhicca#0;
                    #nomcnahi#4 = 00;
                    |LEE 000#nomcnahi.=;
                    |SI FSalida = 0;
                         aAlfa = #nomcnahi#3;
                    |FINSI;
               |FINSI;
          |FINSI;

          |SI aAlfa ! "";
               #nomcnama#6 = fFecha;
               #nomcnama#0 = aAlfa;
               |LEE 000#nomcnama.=;
               |SI FSalida = 0;
                    nPorIT = #nomcnama#2;
                    nPorIMS = #nomcnama#3;
               |FINSI;
          |FINSI;
     |FINSI;

     |SI 2007 [ nAny; |Y aAny [ 2009;
          #nomocuma#6 = fFecha;
          #nomocuma#0 = "c";
          |LEE 000#nomocuma.=;
          |SI FSalida = 0;
               nPorIT_it = #nomocuma#2;
               nPorIMS_it = #nomocuma#3;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO CenNuevo;
     |DDEFECTO #100;
     |SELECT #1#100;
     #100#0 = #nombofn1#0;
     #100#1 = "XXXXXXXXXXXX";
     #100#2 = #nombofn1#1;   || codigo en caso de centro nuevo

     #labempre#0 = #nombofn1#0;
     |LEE 000#labempre.=;

     #100#3 = #labempre#2;

     #labcentr#0 = #nombofn1#0;
     #labcentr#1 = #nombofn1#1;
     |LEE 000#labcentr.=;

     #100#4 = #labcentr#2;

     #100#5 = #0#2;
     #100#6 = 99;
     #100#7 = 0;
     #100#8 = 0;
     #100#9 = 0;
     #100#10 = 0;

     #100#11 = #nombofn1#3;         || numero de trabajadores nuevos
     #100#12 = #100#11 * 65;

     nCreditoCenNue = #100#12;

     |GRABA 020#100n;

     |LIBERA #100;
|FPRC;

|PROCESO EmpNueva;
     |DDEFECTO #100;
     |SELECT #1#100;
     #100#0 = #nombofnu#0;
     #100#1 = "XXXXXXXXXXXX";
     #100#2 = 00000;

     #labempre#0 = #nombofnu#0;
     |LEE 000#labempre.=;

     #100#3 = #labempre#2;

     #100#4 = "";

     #100#5 = #0#2;
     #100#6 = 99;
     #100#7 = 0;
     #100#8 = 0;
     #100#9 = 0;
     #100#10 = 0;

     #100#11 = #nombofnu#3;         || numero de trabajadores nuevos
     |SI #100#11 [ 5;
          #100#12 = 420;
     |SINO;
          #100#12 = #100#11 * 65;
     |FINSI;

     nCreditoEmpNue = #100#12;

     |GRABA 020#100n;

     |LIBERA #100;
|FPRC;

|DEFBUCLE nombofn1;
     #nombofn1#1;
     2;
     #0#0, 001, #0#2;
     #0#1, 999, #0#2;
     ;
     NULL, CenNuevo;
|FIN;

|DEFBUCLE nombofnu;
     #nombofnu#1;
     2;
     #0#0, #0#2;
     #0#1, #0#2;
     ;
     NULL, EmpNueva;
|FIN;

|PROCESO constante;
     alfa1 = "01";
     alfa2 = #nomhicca#2;  alfa2 = "00" + alfa2;    alfa2 = alfa2 % -102;
     nNume = #nomhicca#66 + 2000;
     alfa3 = nNume;
     alfa4 = alfa1 + "." + alfa2 + "." + alfa3;

     fCFecha = alfa4;         || primer dia de mes de calculo
     aCError = "";            || inicializo errores
     |HAZ LeeCons;
|FPRC;

|PROCESO nomhicca;
     #labempre#0 = #nomhicca#0;
     |LEE 000#labempre.=;

     #labtraba#0 = #nomhicca#0;
     #labtraba#1 = #nomhicca#1;
     |LEE 000#labtraba.=;

     |SI #labtraba#42 = "H";
          |ACABA;
     |FINSI;
     |SI #labtraba#42 = "A"; |O #labtraba#42 = "E"; |O #labtraba#42 = "e";
          |SI #0#2 < 2012;    || agrarios no cotizan a FP hasta 2012
               |ACABA;        || pero a partir de 2012 tienen derecho al
          |FINSI;             || minimo de 420 euros
     |FINSI;

     #labcentr#0 = #labtraba#0;
     #labcentr#1 = #labtraba#77;
     |LEE 000#labcentr.=;

     || Bases

     nBaseERE = 0;
     nBaseIT = #nomhicca#62;
     nBaseMat = (#nomhicca#35 * #nomhicca#65);
     nBaseAlta = #nomhicca#40 - (nBaseIT - nBaseMat);

     |SI #nomhicca#119 ! 0;
          |SI #nomhicca#170 ! 0;
               nBaseERE = #nomhicca#170;
          |SINO;
               nBaseERE = #labtraba#242 * #nomhicca#119;
          |FINSI;
     |FINSI;

     nBase = #nomhicca#40 + nBaseMat + nBaseERE;

     |DDEFECTO #nomconst;
     nCuota = 0;
     nPorFP = 0;
     nCConst = #labtraba#248;
     |HAZ constante;
     nPorFP = (#nomconst#16 + #nomconst#17);

     swAgrario = 0;
     |SI nPorFP = 0;
          |SI #labtraba#42 ! "A"; |Y #labtraba#42 ! "E"; |Y #labtraba#42 ! "e";
               |ACABA;
          |FINSI;
     |FINSI;
     |SI #labtraba#42 = "A"; |O #labtraba#42 = "E"; |O #labtraba#42 = "e";
          swAgrario = 1;
          || agrarios a partir de 2012;
          |SI #nomhicca#66 ] 12;
               nCuota = nBase % nPorFP;
          |SINO;
               ||ACABA;
               || si pido para el 2013 se va a buscar desde dic 2011 hasta
               || nov 2012, en 2011 aun no se cotizaba a FP los agrarios
               || pero quito el ACABA para que cuente los trabajadores
               || y aparezca la empresa para el minimo en 2012
          |FINSI;
     |SINO;
          nCuota = nBase % nPorFP;
     |FINSI;

     nMes = #nomhicca#2;

     |SELECT #2#100;
     #100#0 = #labtraba#0;
     #100#1 = #labtraba#7;
     #100#5 = #nomhicca#66 + 2000;
     #100#6 = #nomhicca#2;
     |LEE 101#100=;
     |SI FSalida = 0;
          nContador = 0;
     |SINO;
          nContador = 1;
     |FINSI;

     |DDEFECTO #100;
     |SELECT #1#100;
     #100#0 = #labtraba#0;
     #100#1 = #labtraba#7;
     #100#2 = #labtraba#1;
     #100#5 = #nomhicca#66 + 2000;
     #100#6 = #nomhicca#2;
     |LEE 101#100=;
     FEstado = FSalida;

     #100#3 = #labempre#2;
     #100#4 = #labtraba#2;
     #100#7 = #nomhicca#3;
     #100#8 = #100#8 + nBase;
     #100#9 = #100#9 + nCuota;
     #100#10 = #100#10 + nContador;
     #100#14 = swAgrario;

     nCuotaTotal = nCuotaTotal + (nCuota ! 2);
     nTotalTrab = nTotalTrab + nContador;
     nAgrarios = nAgrarios + swAgrario;

     |SI FEstado = 0;
          |GRABA 020#100a;
     |SINO;
          |GRABA 020#100n;
     |FINSI;

     |LIBERA #100;
|FPRC;

|DEFBUCLE nomhicca;
     #nomhicca#1;
     ;
     #0#0, 00001, nDesdeA¤o, nDesdeMes, 0;
     #0#1, 99999, nHastaA¤o, nHastaMes, 0;
     ;
     NULL, nomhicca;
|FIN;

|PROCESO Principal;
     |ABRE #nomhicca;
     |ABRE #labempre;
     |ABRE #labcentr;
     |ABRE #labtraba;
     |ABRE #nomcnahi;
     |ABRE #nomcnama;
     |ABRE #nomocuma;
     |ABRE #nomepigr;
     |ABRE #nombofnu;
     aAlfa1 = Usuario; |QBT aAlfa1;    aAlfa1 = ("g" + aAlfa1) % 108;
     |NOME_DAT #100 aAlfa1;
     |DELINDEX #100;
     |ABRE #100;

     nAgrarios = 0;

     nDesdeA¤o = #0#2 - 2000 - 2;
     nHastaA¤o = #0#2 - 2000 - 2;
     nDesdeMes = 12;
     nHastaMes = 12;
     |BUCLE nomhicca;

     nDesdeA¤o = #0#2 - 2000 - 1;
     nHastaA¤o = #0#2 - 2000 - 1;
     nDesdeMes = 01;
     nHastaMes = 11;
     |BUCLE nomhicca;

     swNuevo = 1;
     |BUCLE nombofnu;         || empresas de nueva creacion
     swNuevo = 2;
     |BUCLE nombofn1;         || centros de nueva creacion

     |CIERRA #100;
     |CIERRA #nombofnu;
     |CIERRA #nomepigr;
     |CIERRA #nomocuma;
     |CIERRA #nomcnama;
     |CIERRA #nomcnahi;
     |CIERRA #labtraba;
     |CIERRA #labcentr;
     |CIERRA #labempre;
     |CIERRA #nomhicca;
|FPRC;

|PROCESO SacaCredito;
     nMedia = nTotalTrab / 12;
     nMedia = nMedia ! 0;

     |SI nMedia < 1; |Y nTotalTrab ! 0;
         nMedia = 1;
     |FINSI;

     |SI nMedia ] 0; |Y nMedia [ 5;
         nPorc = 100;
     |FINSI;

     |SI nMedia ] 6; |Y nMedia [ 9;
         nPorc = 100;
     |FINSI;

     |SI nMedia ] 10; |Y nMedia [ 49;
         nPorc = 75;
     |FINSI;

     |SI nMedia ] 50; |Y nMedia [ 249;
         nPorc = 60;
     |FINSI;

     |SI nMedia ] 250;
         nPorc = 50;
     |FINSI;

     nCredito = nCuotaTotal % nPorc;
     nCredito = nCredito ! 2;
     |SI nMedia [ 5; |Y nCredito < 420; |Y nTotalTrab > 0;
         nCredito = 420;
     |FINSI;

     |SI #0#2 = 2012; |Y nAgrarios > 0;
         nCredito = 420;
     |FINSI;

     nCredito = nCredito + nCreditoEmpNue + nCreditoCenNue;
|FPRC;

|PROCESO AcumEmpresa;
     nCredito       = 0;
     nCreditoEmpNue = 0;
     nCreditoCenNue = 0;
     nTotalTrab = 0;
     nCuotaTotal = 0;
     #0#0 = nEmp;
     #0#1 = nEmp;
     |HAZ Principal;
     |BUCLE Consumido;
     |HAZ SacaCredito;

     enCredFor = enCredFor + nCredito;
     enCredCon = enCredCon + nBonifCons;
|FPRC;

|PROGRAMA;
     aParam = PARAMETRO;  |QBF aParam;
     aAlfa = aParam - "ESTADISTICA";
     |SI FSalida ! 0;
         #0#2      = aAlfa;
         #0#3      = 12;
         enCredFor = 0;
         enCredCon = 0;

         |ABRET #labempre;
         |PARA nEmp = 1;  |SI nEmp [ 99999;  |HACIENDO nEmp = nEmp + 1;
          nCredito = 0;
               nBonifCons = 0;
               #labempre#0 = nEmp;
               |LEE 000#labempre.=;
               |SI FSalida = 0;
                   |HAZ AcumEmpresa;
               |FINSI;
         |SIGUE;
         |CIERRAT #labempre;
         |ACABA;
     |FINSI;

     |SI aParam = "";
          |CLS;
          |PINPA #0#0;
          |PINDA #0#0;
          |HAZ PintaImporte;
          |HAZ Defectos;
          |ENDATOS #1#0;
          |SI FSalida = 0;
               Impresora = "CrystalReport";
               |IMPRE -1;
               |INFOR "nominf05";

               |HAZ Principal;
               |BUCLE Consumido;
               |BUCLE Impresion;

               |PIEINF;
               |FININF;
               |FINIMP;
          |FINSI;
     |SINO;
          aAlfa = aParam % 105;
          #0#0 = aAlfa;
          #0#1 = aAlfa;
          aAlfa = aParam % 604;
          #0#2 = aAlfa;
          aAlfa = aParam % 1001;
          aInformeSN = aAlfa;
          #0#3 = 12;
          |HAZ Principal;

          |SI aInformeSN = "S";
               Impresora = "CrystalReport";
               |IMPRE -1;
               |INFOR "nominf05";

               |BUCLE Consumido;
               |BUCLE Impresion;

               |PIEINF;
               |FININF;
               |FINIMP;
          |SINO;
               |HAZ SacaCredito;
          |FINSI;
     |FINSI;
|FPRO;
