|FICHEROS;
     labempre;
     labtraba;
     labcentr;

     labere22;
     labere23;
     labere24;
     labere25;

     labtmper;
     labtmpet;
     nomconca;
     labcontr;
|FIN;

|VARIABLES;
     &enEmpresa = 0;
     &enNroERE = 0;
     &aRegimen = "";
     nNume = 0;
     &aFirma = "";
     aAlfa = "";
     aAlfa1 = "";
     aAlfa2 = "";

     alfa1 = "";
     fecalf = "";
     dia = 0;
     mes = 0;
     any = 0;
     swfuera = 0;

     aBase = "";
     aHojaExcel = "";
     aOrigen = "";
     aDestino = "";
     aDestinoTrab = "";
     aIPRemoto = "";
     aResultado = "";
     aCol = "";
     nLinea = 0;
     aValor = "";
     swCabecera = 0;
     nLineaTra = 0;
     swTP = 0;

     &eaNom = "";
     &eaApel1 = "";
     &eaApel2 = "";
     &aCadena = "";

     aCCC = "";
     nCCCs = 0;
     &nTrabEmp = 0;
     &nTrabCen = 0;
     nAny = 0;
     nMes = 0;
     nDia = 0;

     &eaFormato = "";
     &nOpcion = 0;

     aIdentificacion = "";
     aNombreHoja = "";
     fFecha = @;
     aFecha = "";
     aProvincia = "";
     fDesdeFecha = @;
     fHastaFecha = @;
     swSigue = 0;
     aIP = "";
|FIN;

/****************************************************************************/
/********************   CONTADOR DE TRABAJADORES   **************************/
/****************************************************************************/

|PROCESO desglosa_fec;
     alfa1 = fecalf %  102;   dia = alfa1;
     alfa1 = fecalf %  402;   mes = alfa1;
     alfa1 = fecalf % -104;   any = alfa1;
     |SI alfa1 = "....";
          dia = 0;
          mes = 0;
          any = 0;
     |FINSI;
|FPRC;

|PROCESO Cuenta;
     |SI #labtraba#42 = "H";
          |ACABA;
     |FINSI;

     swfuera = 0;

     fecalf = #labtraba#36;                           || fecha de alta
     |HAZ desglosa_fec;
     |SI any > nAny;|O any = 0;
          swfuera = 1;
     |SINO;
          |SI mes > nMes; |Y any = nAny;
               swfuera = 1;
          |SINO;
               |SI dia > nDia; |Y mes = nMes; |Y any = nAny;
                    swfuera = 1;
               |FINSI;
          |FINSI;
     |FINSI;

     |SI swfuera = 0;
          fecalf = #labtraba#37;                       || fecha de baja
          |HAZ desglosa_fec;
          |SI any < nAny; |Y any ! 0;
               swfuera = 1;
          |SINO;
               |SI mes < nMes; |Y any = nAny; |Y mes ! 0;
                    swfuera = 1;
               |SINO;
                    |SI dia < nDia; |Y mes = nMes; |Y any = nAny; |Y dia ! 0;
                         swfuera = 1;
                    |FINSI;
               |FINSI;
          |FINSI;
     |FINSI;

     |SI swfuera = 0;
          |DDEFECTO #labtmpet;
          #labtmpet#0 = #labtraba#0;
          #labtmpet#1 = 00;
          |LEE 101#labtmpet.=;
          |SI FSalida ! 0
               #labtmpet#2 = 1;
               |GRABA 020#labtmpet.n;
               |LIBERA #labtmpet;
          |SINO;
               #labtmpet#2 = #labtmpet#2 + 1;
               |GRABA 020#labtmpet.a;
               |LIBERA #labtmpet;
          |FINSI;

          |DDEFECTO #labtmpet;
          #labtmpet#0 = #labtraba#0;
          #labtmpet#1 = #labtraba#77;
          |LEE 101#labtmpet.=;
          |SI FSalida ! 0
               #labtmpet#2 = 1;
               |GRABA 020#labtmpet.n;
               |LIBERA #labtmpet;
          |SINO;
               #labtmpet#2 = #labtmpet#2 + 1;
               |GRABA 020#labtmpet.a;
               |LIBERA #labtmpet;
          |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE Cuenta;
     #labtraba#1;
     ;
     enEmpresa, 00001;
     enEmpresa, 99999;
     ;
     NULL, Cuenta;
|FIN;

|PROCESO CuentaTrab;
     aAlfa = #labere22#3 % 102;
     nDia = aAlfa;
     aAlfa = #labere22#3 % 402;
     nMes = aAlfa;
     aAlfa = #labere22#3 % -104;
     nAny = aAlfa;

     |BUCLE Cuenta;
|FPRC;

|PROCESO PonValor;
     aAlfa = aCol + nLinea + "," + aValor;
     |ALFACALLDLL "agixl97.dll","poke_dato", aAlfa;
|FPRC;

|PROCESO FinPrest;
     |SI #labere23#7 = "  ";
          |ACABA;
     |FINSI;

     aAlfa = #labere23#22;
     |QBF aAlfa;
     |SI aAlfa = ""
          |ACABA;
     |FINSI;

     aAlfa = #labere23#22;
     fFecha = aAlfa;
     aAlfa = #labere25#0;
     fDesdeFecha = aAlfa;
     aAlfa = #labere25#1;
     fHastaFecha = aAlfa;

     |SI fDesdeFecha [ fFecha; |Y fFecha [ fHastaFecha;
          || adelante
     |SINO;
          |ACABA;
     |FINSI;

     #labtraba#0 = #labere23#0;
     #labtraba#1 = #labere23#2;
     |LEE 000#labtraba.=;

     |SI swCabecera = 0;
          aProvincia = #labtmper#0;
          |QBF aProvincia;

          nLinea = 3;
          aCol = "B";
/*
          |ABRE #labcontr; |LEE 000#labcontr.p; |CIERRA #labcontr;
          aAlfa = #labcontr#68;
          |QBF aAlfa;
          |SI aAlfa ! "";
               aIdentificacion = aAlfa;
          |SINO;
               aIdentificacion = #labempre#2;
          |FINSI;
*/
          aIdentificacion = #labempre#2;
          aValor = aIdentificacion;
          |HAZ PonValor;

          aIdentificacion = aIdentificacion - "," - "," - "," - "." - "." - ".";
          aIdentificacion = aIdentificacion - "'" - "'" - "'" - "-" - "-" - "-";
          aCadena = aIdentificacion;
          |HAZ QuitaRaros;
          aIdentificacion = aCadena;

          nLinea = 4;
          aCol = "B";
/*
          aAlfa = #labcontr#71;
          |QBF aAlfa;
          |SI aAlfa ! "";
               aValor = aAlfa;
          |SINO;
               aValor = #labere22#8;
          |FINSI;
*/
          aValor = #labere22#8;
          |HAZ PonValor;

          nLinea = 5;
          aCol = "B";
/*
          aAlfa = #labcontr#73;
          |QBF aAlfa;
          |SI aAlfa ! "";
               aValor = aAlfa;
          |SINO;
               aValor = #labere22#9;
          |FINSI;
*/
          aValor = #labere22#9;
          |HAZ PonValor;

          swCabecera = 1;

          nLinea = 8;
     |FINSI;

     nLinea = nLinea + 1;

     aCol = "A";
     aCCC = #labere23#20;
     |QBF aCCC;
     aValor = aCCC;
     |HAZ PonValor;

     aCol = "B";
     aAlfa = #labere23#6;
     |QBF aAlfa;
     aAlfa = ("000000000" + aAlfa) % -109;
     aValor = aAlfa;
     |HAZ PonValor;

     aCol = "C";
     aValor = #labere23#22;
     aAlfa = aValor % 102;
     nNume = aAlfa;
     |SI nNume [ 12;
          aValor = (aValor % 402) + "-" + (aValor % 102) + "-" + (aValor % -102);
     |SINO;
          aValor = (aValor % 102) + "-" + (aValor % 402) + "-" + (aValor % -102);
     |FINSI;
     |HAZ PonValor;
|FPRC;

|PROCESO SolicitudPrest;
     |SI #labere23#7 = "  ";
          |ACABA;
     |FINSI;

     #labtraba#0 = #labere23#0;
     #labtraba#1 = #labere23#2;
     |LEE 000#labtraba.=;

     |SI swCabecera = 0;
          nLinea = 3;
          aCol = "B"; aValor = #labere23#16; |HAZ PonValor;
          aCol = "I"; aValor = #labere22#2; |HAZ PonValor;

          nLinea = nLinea + 1;
          aCol = "B"; aValor = #labempre#10; |HAZ PonValor;

          |DDEFECTO #labtmpet;
          #labtmpet#0 = #labtraba#0;
          #labtmpet#1 = 00;
          |LEE 000#labtmpet.=;
          aValor = #labtmpet#2;
          aCol = "I"; aValor = aValor; |HAZ PonValor;

          nLinea = nLinea + 1;
          aCCC = #labere23#20;
          |QBF aCCC;
          aCol = "B"; aValor = aCCC; |HAZ PonValor;
          nNume = #labtraba#247;
          aAlfa = nNume; |QBF aAlfa; aAlfa = ("0000" + aAlfa) % -104;
          aValor = aAlfa + #labempre#13;
          aCol = "I"; aValor = aValor; |HAZ PonValor;

          nLinea = nLinea + 1;
          aValor = #labere22#10; |QBF aValor;
          |SI aValor = ".........................";
               aValor = #labere22#1;
               aValor = ("000000" + aValor) % -106;
          |FINSI;
          aCol = "B"; aValor = aValor; |HAZ PonValor;
          nNume = #labere22#6;
          |SI nNume = 1; aValor = "NACIONAL"; |FINSI;
          |SI nNume = 2; aValor = "AUTONOMICO"; |FINSI;
          |SI nNume = 3; aValor = "PROVINCIAL"; |FINSI;
          aCol = "I"; aValor = aValor; |HAZ PonValor;

          nLinea = nLinea + 2;
          aCol = "B"; aValor = #labempre#15; |HAZ PonValor;
          nLinea = nLinea + 1;
          aCol = "B"; aValor = #labempre#17; |HAZ PonValor;
          nLinea = nLinea + 1;
          aCol = "B"; aValor = #labere22#8; |HAZ PonValor;
          nLinea = nLinea + 1;
          aCol = "B"; aValor = #labere22#9; |HAZ PonValor;

          swCabecera = 1;
     |FINSI;

     nLineaTra = nLineaTra + 1;
     nLinea = nLineaTra;

     aCol = "A"; aValor = #labere23#6; |HAZ PonValor;

     aCadena = #labere23#3;
     |HAZ SeparaNom_plb;
     aCol = "B"; aValor = eaNom; |HAZ PonValor;
     aCol = "C"; aValor = eaApel1; |HAZ PonValor;
     aCol = "D"; aValor = eaApel2; |HAZ PonValor;
     aCol = "E"; aValor = #labtraba#244; |HAZ PonValor;
     aAlfa1 = #labtraba#8; |QBF aAlfa1; aAlfa1 = ("00" + aAlfa1) % -102;
     aAlfa2 = #labtraba#9; |QBF aAlfa2; aAlfa2 = ("000" + aAlfa2) % -103;
     aCol = "F"; aValor = aAlfa1 + aAlfa2; |HAZ PonValor;
     aValor = #labere23#19 % 104;
     |QBF aValor;
     |SI aValor ! ""; |Y aValor ! "ES";
          aCol = "G"; aValor = #labere23#19 % 104; |HAZ PonValor;
     |FINSI;
     aCol = "H"; aValor = #labere23#19 % 520; |HAZ PonValor;
     |SI #labere23#13 = "R";
          aValor = "REDUC. JORN";
     |SINO;
          aValor = "SUSPENSION";
     |FINSI;
     aCol = "I"; aValor = aValor; |HAZ PonValor;
     aValor = #labere23#14;
     aAlfa = aValor % 102;
     nNume = aAlfa;
     |SI nNume [ 12;
          aValor = (aValor % 402) + "-" + (aValor % 102) + "-" + (aValor % -102);
     |SINO;
          aValor = (aValor % 102) + "-" + (aValor % 402) + "-" + (aValor % -102);
     |FINSI;
     aCol = "J"; aValor = aValor; |HAZ PonValor;

     aValor = #labere23#15;
     |QBF aValor;
     |SI aValor ! "";
          aAlfa = aValor % 102;
          nNume = aAlfa;
          |SI nNume [ 12;
               aValor = (aValor % 402) + "-" + (aValor % 102) + "-" + (aValor % -102);
          |SINO;
               aValor = (aValor % 102) + "-" + (aValor % 402) + "-" + (aValor % -102);
          |FINSI;
          aCol = "K"; aValor = aValor; |HAZ PonValor;
     |FINSI;

     swTP = 0;
     |SI 200 [ #labtraba#45; |Y #labtraba#45 [ 299; swTP = 1; |FINSI;
     |SI 500 [ #labtraba#45; |Y #labtraba#45 [ 599; swTP = 1; |FINSI;

     |SI swTP = 1; |Y #labtraba#234 ! 0;
          #nomconca#0 = #labtraba#87;
          |LEE 000#nomconca.=;

          |SI #nomconca#162 = 0;
               #nomconca#162 = 40;
          |FINSI;
          nNume = #labtraba#234 / #nomconca#162;
          || nNume = nNume * 100;
          aValor = nNume;

          aCol = "L"; aValor = nNume; |HAZ PonValor;
     |FINSI;
     |SI #labere23#17 ! 0;
          nNume = #labere23#17 / 100;
          aCol = "M"; aValor = nNume; |HAZ PonValor;
     |FINSI;

     |SI #labere23#18 ! 0;
          aCol = "N"; aValor = #labere23#18; |HAZ PonValor;
     |FINSI;
|FPRC;

|PROCESO labere23_2;
     |SI nOpcion = 2;
          || solicitud de prestaciones
          |HAZ SolicitudPrest;
     |FINSI;
     |SI nOpcion = 3;
          || baja en prestaciones (fin del ERTE)
          |HAZ FinPrest;
     |FINSI;
|FPRC;

|DEFBUCLE labere23_2_prov;
     #labere23#3;
     ;
     #labtmper#0, enEmpresa, enNroERE, 00001;
     #labtmper#0, enEmpresa, enNroERE, 99999;
     ;
     NULL, labere23_2;
|FIN;

|DEFBUCLE labere23_2_ccc;
     #labere23#2;
     ;
     #labtmper#0, enEmpresa, enNroERE, 00001;
     #labtmper#0, enEmpresa, enNroERE, 99999;
     ;
     NULL, labere23_2;
|FIN;


|PROCESO labtmper;
     || un excel por CCC

     |SI nOpcion = 2;
          aHojaExcel = "plantilla_eres_covid19";
          aNombreHoja = "Plantilla";
     |FINSI;
     |SI nOpcion = 3;
          aHojaExcel = "plantilla_baja_ertes";
          aNombreHoja = "Listado de bajas";
     |FINSI;

     |DBASE aBase;  |QBF aBase;
     aHojaExcel = aHojaExcel + eaFormato;
     aOrigen = aBase + "documentos/" + aHojaExcel;
     aDestinoTrab = "C:\DOCUMENTOS\LABORAL\";
     |RMKDIR aDestinoTrab;
     aDestino = aDestinoTrab + aHojaExcel;

     aIPRemoto = "";
     |IP_REMOTO aIPRemoto;  |QBT aIPRemoto;
     |SI aIPRemoto ! "";
          |ENVIA_FICHERO aOrigen, aDestino;
     |SINO;
          |COPIA_FICHERO aOrigen, aDestino;
     |FINSI;

     aOrigen = aDestinoTrab + aHojaExcel;
     aDestino = aDestinoTrab + "trabajo" + eaFormato;
     |RFBORRA aDestino;
     |RCOPIA_FICHERO aOrigen, aDestino;

     |CARGADLL "agixl97.dll";
     |SI FSalida < 0;
          |MENAV "0000No se puede usar la DLL de Excel.";
          |ACABA;
     |FINSI;

     |ALFACALLDLL "agixl97.dll", "carga_book", aDestino;

     aAlfa = aDestinoTrab + aHojaExcel;
     |RFBORRA aAlfa;

     |ALFACALLDLL "agixl97.dll", "selecciona_hoja", aNombreHoja;

     swCabecera = 0;
     nLineaTra = 15;
     |SI nOpcion = 2;
          |BUCLE labere23_2_ccc;
     |FINSI;
     |SI nOpcion = 3;
          |BUCLE labere23_2_prov;
     |FINSI;

     |SI nOpcion = 2;
          aHojaExcel = aCCC + eaFormato;
     |FINSI;
     |SI nOpcion = 3;
          fFecha = ~;
          aFecha = fFecha;
          aFecha = (aFecha % 102) + (aFecha % 402) + (aFecha % -102);

          |QBT aIdentificacion;
          aIdentificacion = (aIdentificacion % 112);
          |SI nCCCs > 1;
               aIdentificacion = (aIdentificacion % 110) + aProvincia;
               |QBT aIdentificacion;
          |FINSI;

          aHojaExcel = "B" + aFecha + aIdentificacion + eaFormato;
     |FINSI;

     aAlfa = aDestinoTrab + aHojaExcel;
     |RFBORRA aAlfa;                    || por si estuviera

     |ALFACALLDLL "agixl97.dll", "fichero_destino", aAlfa;

     |ALFACALLDLL "agixl97.dll", "fin_book", "salva";

     aAlfa = aDestinoTrab + "trabajo" + eaFormato;
     |RFBORRA aAlfa;

     aResultado = aAlfa;

     |DESCARGADLL "agixl97.dll";

     |SI nOpcion = 3;
          || todo esto por la B mayuscula

          aOrigen = aDestinoTrab + aHojaExcel;
          aDestino = aDestinoTrab + "copia_" + aHojaExcel;

          |RCOPIA_FICHERO aOrigen, aDestino;
          |RFBORRA aOrigen;
          |RCOPIA_FICHERO aDestino, aOrigen;
          |RFBORRA aDestino;
     |FINSI;

     aAlfa = aDestinoTrab + aHojaExcel;
     aAlfa = "cmd " + &34 + "/c START " + aAlfa + &34;
     |RSYSTEM aAlfa;
|FPRC;

|DEFBUCLE labtmper;
     #labtmper#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, labtmper;
|FIN;

|PROCESO labere23_ccc;
     aAlfa = #labere23#23;
     |QBF aAlfa;
     |SI aAlfa = "";
          #labere23#23 = #labere23#20 % 502;
          |GRABA 020#labere23.a;
          |LIBERA #labere23;
     |FINSI;

     |SI #labere23#7 = "  ";
          |ACABA;
     |FINSI;

     #labtmper#0 = #labere23#20;
     |SI nOpcion = 3;
          #labtmper#0 = #labere23#20 % 502;
     |FINSI;
     |LEE 101#labtmper.=;
     |SI FSalida ! 0;
          |GRABA 020#labtmper.n;
          nCCCs = nCCCs + 1;
     |FINSI;
     |LIBERA #labtmper;
|FPRC;

|DEFBUCLE labere23_ccc;
     #labere23#1;
     ;
     enEmpresa, enNroERE, 00001;
     enEmpresa, enNroERE, 99999;
     be;
     NULL, labere23_ccc;
|FIN;

|PROCESO Seleccion2;
     swSigue = 0;
     |SI nOpcion = 3;
          |PUSHV 05012380;
          #labere25#0 = #labere22#11;
          #labere25#1 = #labere22#11;
          |PINPA #0#labere25;
          |PINDA #0#labere25;
          |ENDATOS #1#labere25;
          |SI FSalida = 0;
               swSigue = 1;
          |FINSI;
     |SINO;
          swSigue = 1;
     |FINSI;

     |SI swSigue = 0;
          |POPV;
          |ACABA;
     |FINSI;

     aAlfa = Usuario; |QBT aAlfa; aAlfa = "tm_labtmper_" + aAlfa;
     |NOME_DAT #labtmper#aAlfa#;
     |DELINDEX #labtmper;
     |ABRE #labtmper;

     aAlfa = Usuario; |QBT aAlfa; aAlfa = "tm_labtmpet_" + aAlfa;
     |NOME_DAT #labtmpet#aAlfa#;
     |DELINDEX #labtmpet;
     |ABRE #labtmpet;

     |ABRE #labempre;
     |ABRE #labtraba;
     |ABRE #labcentr;
     |ABRE #nomconca;
     |ABRE #labere22;

     #labere22#0 = enEmpresa;
     #labere22#1 = enNroERE;
     |LEE 000#labere22.=;

     #labempre#0 = #labere23#0;
     |LEE 000#labempre.=;

     |HAZ CuentaTrab;

     || lo primero ver los CCC que hay, sera mejor despues
     nCCCs = 0;
     |BUCLE labere23_ccc;
     |SI nCCCs > 1;
          |SI nOpcion = 2;
               aAlfa = "    ATENCION. Existen trabajadores en más de un CCC. ";
               aAlfa = aAlfa + "Se emitirá una hoja Excel por cada CCC.";
          |FINSI;
          |SI nOpcion = 3;
               aAlfa = "    ATENCION. Existen centros en distintas provincias. ";
               aAlfa = aAlfa + "Se emitirá una hoja Excel por cada provincia.";
          |FINSI;
          |MENAV aAlfa;
     |FINSI;

     || ahora hago el bucle agrupando por CCC

     |BUCLE labtmper;

     |CIERRA #labtmpet;
     |DELINDEX #labtmpet;

     |CIERRA #labtmper;
     |DELINDEX #labtmper;

     |CIERRA #labere22;
     |CIERRA #nomconca;
     |CIERRA #labcentr;
     |CIERRA #labtraba;
     |CIERRA #labempre;

     |POPV;
|FPRC;

/****************************************************************************/
/**********   COMUNICACION A LOS TRABAJADORES *******************************/
/****************************************************************************/

|PROCESO labere23_1;
     |SI #labere23#7 = "  ";
          |ACABA;
     |FINSI;

     #labempre#0 = #labere23#0;
     |LEE 000#labempre.=;

     #labtraba#0 = #labere23#0;
     #labtraba#1 = #labere23#2;
     |LEE 000#labtraba.=;

     #labcentr#0 = #labtraba#0;
     #labcentr#1 = #labtraba#77;
     |LEE 000#labcentr.=;

     nNume = #labtraba#247;
     aRegimen = nNume;

     |DDEFECTO #labtmpet;
     #labtmpet#0 = #labtraba#0;
     #labtmpet#1 = 00;
     |LEE 000#labtmpet.=;
     nTrabEmp = #labtmpet#2;

     |DDEFECTO #labtmpet;
     #labtmpet#0 = #labtraba#0;
     #labtmpet#1 = #labtraba#77;
     |LEE 000#labtmpet.=;
     nTrabCen = #labtmpet#2;

     |PRINT;
|FPRC;

|DEFBUCLE labere23_1;
     #labere23#1;
     ;
     enEmpresa, enNroERE, 00001;
     enEmpresa, enNroERE, 99999;
     ;
     NULL, labere23_1;
|FIN;

|PROCESO Seleccion1;
     |PUSHV 05012380;
     |PINPA #0#labere24;
     |PINDA #0#labere24;
     |ENDATOS #1#labere24;

     |SI FSalida = 0;
          aFirma = "";
          aAlfa = #labere24#0; |QBF aAlfa; aFirma = "En " + aAlfa + ", a ";
          aAlfa = #labere24#1; |QBF aAlfa; aFirma = aFirma + aAlfa + " de ";
          aAlfa = #labere24#2; |QBF aAlfa; aFirma = aFirma + aAlfa + " de ";
          aAlfa = #labere24#3; |QBF aAlfa; aFirma = aFirma + aAlfa;

          |ABRE #labempre;
          |ABRE #labtraba;
          |ABRE #labcentr;
          |ABRE #labere22;

          aAlfa = Usuario; |QBT aAlfa; aAlfa = "tm_labtmpet_" + aAlfa;
          |NOME_DAT #labtmpet#aAlfa#;
          |DELINDEX #labtmpet;
          |ABRE #labtmpet;

          Impresora = "CrystalReport";
          |IMPRE -1;
          |INFOR "labere22";

          #labere22#0 = enEmpresa;
          #labere22#1 = enNroERE;
          |LEE 000#labere22.=;

          |HAZ CuentaTrab;

          |BUCLE labere23_1;

          |PIEINF;
          |FININF;
          |FINIMP;

          |CIERRA #labtmpet;
          |DELINDEX #labtmpet;

          |CIERRA #labere22;
          |CIERRA #labcentr;
          |CIERRA #labtraba;
          |CIERRA #labempre;
     |FINSI;
     |POPV;
|FPRC;
