|FICHEROS;
     hotm0043;      || selector
     hotm0044;      || tmp. informe
     nomvarca;      || variaciones cabs.
     labtraba;      || trabajadores
     hotm0013;      || planilla lins.
     hotm0011;      || centros
     hotm0007;      || areas
     hotm0008;      || secciones
     labempre;      || empresas
|FIN;

|VARIABLES;
     aAlfa1 = "";
     aAlfa2 = "";
     aAlfa3 = "";
     aAlfa4 = "";
     nNume1 = 0;
     nNume2 = 0;
     nNume3 = 0;
     nNume4 = 0;
     fFech1 = @;
     fFech2 = @;
     fFech3 = @;
     fFech4 = @;
     nPara1 = 0;
     nPara2 = 0;
     nPara3 = 0;
     nPara4 = 0;
     nModo = 0;
     nSeleccio = 0;

     nMes = 0;
     nSiVale = 0;
     nCto = 0;
     aAre = "";
     aSec = "";
     nAntCto = 0;
     aAntAre = "";
     aAntSec = "";
     &enSwLinea = 0;
     &enCuan4 = 0;
     &enCuan3 = 0;
     &enCuan2 = 0;
|FIN;
_____________________________________________________/
|PROCESO Selecc; |TIPO 0;
     |VARIABLES;
          i = 0;
     |FIN;

     |SI #hotm0043#6 = "R";
          |PARA i =  7;|SI i [ 12; |HACIENDO i = i + 1;
               |CAMPO_MODIFICA #hotm0043#i#, 1, "S";
          |SIGUE;
          |PARA i = 13;|SI i [ 42; |HACIENDO i = i + 1;
               |CAMPO_MODIFICA #hotm0043#i#, 1, "N";
          |SIGUE;
     |SINO;
          |PARA i =  7;|SI i [ 12; |HACIENDO i = i + 1;
               |CAMPO_MODIFICA #hotm0043#i#, 1, "N";
          |SIGUE;
          |PARA i = 13;|SI i [ 42; |HACIENDO i = i + 1;
               |CAMPO_MODIFICA #hotm0043#i#, 1, "S";
          |SIGUE;
     |FINSI;
|FPRC;
_____________________________________________________/
|PROCESO hotm0008;       || lee secciones
     |DDEFECTO #hotm0008;
     #hotm0008#0 = nAntCto;
     #hotm0008#1 = aAntAre;
     #hotm0008#2 = aAntSec;
     |LEE 000#hotm0008.=;

     enSwLinea = 4; |PRINT;

     aAntSec = #hotm0044#3;
     enCuan4 = 0;
|FPRC;

|PROCESO hotm0007;       || lee areas
     |DDEFECTO #hotm0007;
     #hotm0007#0 = nAntCto;
     #hotm0007#1 = aAntAre;
     |LEE 000#hotm0007.=;

     enSwLinea = 3; |PRINT;

     aAntAre = #hotm0044#2;
     enCuan3 = 0;
|FPRC;

|PROCESO hotm0011;       || lee centros
     |DDEFECTO #hotm0011;
     #hotm0011#0 = nAntCto;
     |LEE 000#hotm0011.=;

     enSwLinea = 2; |PRINT;

     nAntCto = #hotm0044#1;
     enCuan2 = 0;
|FPRC;

|PROCESO hotm0044;
     |DDEFECTO #labempre;
     #labempre#0 = #hotm0044#0;
     |LEE 000#labempre.=;

     |DDEFECTO #labtraba;
     #labtraba#0 = #hotm0044#0;
     #labtraba#1 = #hotm0044#4;
     |LEE 000#labtraba.=;

     |DDEFECTO #nomvarca;
     #nomvarca#0 = #labtraba#0;
     #nomvarca#1 = #labtraba#1;
     #nomvarca#2 = nMes;
     #nomvarca#3 = 0;
     |LEE 000#nomvarca.=;

     |SI nAntCto = 0;         || se usa como switch para la ruptura
          nAntCto = #hotm0044#1;
          aAntAre = #hotm0044#2;
          aAntSec = #hotm0044#3;
     |FINSI;

     |SI nAntCto ! #hotm0044#1;
          |HAZ hotm0008;
          |HAZ hotm0007;
          |HAZ hotm0011;
     |SINO;
          |SI aAntAre ! #hotm0044#2;
               |HAZ hotm0008;
               |HAZ hotm0007;
          |SINO;
               |SI aAntSec ! #hotm0044#3;
                    |HAZ hotm0008;
               |FINSI;
          |FINSI;
     |FINSI;

     enCuan2 = enCuan2 + 1;
     enCuan3 = enCuan3 + 1;
     enCuan4 = enCuan4 + 1;

     enSwLinea = 0; |PRINT;
|FPRC;

|DEFBUCLE hotm0044;
     #hotm0044#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, hotm0044;
|FIN;

|PROCESO hotm0013;
     nSiVale = 0;
     nCto = #hotm0013#3;
     aAre = #hotm0013#5;
     aSec = #hotm0013#7;
     |ERROR10;
|FPRC;

|DEFBUCLE hotm0013;
     #hotm0013#2;
     ;
     #labtraba#0, #labtraba#1, #hotm0043#7, #hotm0043#9,  #hotm0043#11;
     #labtraba#0, #labtraba#1, #hotm0043#8, #hotm0043#10, #hotm0043#12;
     ;
     NULL, hotm0013;
|FIN;

|PROCESO AcotacionPlanilla;
     nSiVale = 1;

     |SI #hotm0043#6 = "R";
          |BUCLE hotm0013;
     |FINSI;
     |SI #hotm0043#6 = "P";
          |PARA nPara1 = 13; |SI nPara1 [ 40; |HACIENDO nPara1 = nPara1 + 3;
               #hotm0043#7 = #hotm0043#nPara1#;   || desde centro
               #hotm0043#8 = #hotm0043#7;         || hasta centro
               nPara2 = nPara1 + 1;
               #hotm0043#9 = #hotm0043#nPara2#;   || desde area
               #hotm0043#10 = #hotm0043#9;        || hasta area
               nPara3 = nPara1 + 2;
               #hotm0043#11 = #hotm0043#nPara3#;  || desde seccion
               #hotm0043#12 = #hotm0043#11;       || hasta seccion
               |BUCLE hotm0013;
               |SI nSiVale = 0;
                    |SAL;
               |FINSI;
          |SIGUE;
     |FINSI;
|FPRC;

|PROCESO labtraba;
     nSiVale = 0;        || con cero es valido, con uno no es valido

     |SI #labtraba#48 = "S";                 || finiquito S/N
          #nomvarca#0 = #labtraba#0;
          #nomvarca#1 = #labtraba#1;
          #nomvarca#2 = nMes;
          #nomvarca#3 = 0;
          |LEE 000#nomvarca.=;
          |SI FSalida = 0;
               fFech1 = #nomvarca#16;
               |SI fFech1 ] #hotm0043#5;     || fecha baja posterior
                    nSiVale = 0;   || no hace falta pero asi no hay que
               |SINO;              ||/comprobar si viene vacia
                    nSiVale = 1;
               |FINSI;
          |SINO;
               nSiVale = 1;
          |FINSI;
     |FINSI;

     |SI nSiVale = 0;
          |HAZ AcotacionPlanilla;
          |SI nSiVale = 0;
               |DDEFECTO #hotm0044;
               #hotm0044#0 = #labtraba#0;
               #hotm0044#1 = nCto;
               #hotm0044#2 = aAre;
               #hotm0044#3 = aSec;
               #hotm0044#4 = #labtraba#1;
               |GRABA 020#hotm0044.n;
               |SI FSalida = 0;
                    nSeleccio = 1;
               |FINSI;
          |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE labtraba;
     #labtraba#1;
     44;
     #hotm0043#0,     1, "A";
     #hotm0043#1, 99999, "A";
     ;
     NULL, labtraba;
|FIN;

|PROCESO Tipo3; |TIPO 3;
     |IMPRE 0;
     |SI FSalida = 0;
          |INFOR "hotm0043";
          |SI FSalida = 0;

               aAlfa1 = #hotm0043#5;
               aAlfa1 = aAlfa1 % 402;
               nMes = aAlfa1;           || para las variaciones

               aAlfa1 = Usuario;   |QBF aAlfa1;
               |NOME_DAT #hotm0044#aAlfa1#;
               |DELINDEX #hotm0044;

               |ABRE #hotm0044;
               |ABRE #nomvarca;
               |BUCLE labtraba;
               |CIERRA #hotm0044;

               |ABRE #hotm0007;
               |ABRE #hotm0008;
               |ABRE #hotm0011;
               |ABRE #labtraba;
               |BUCLE hotm0044;
               |SI nSeleccio = 0;
                    |MENAV "0000Seleccion vacia.";
               |SINO;
                    |HAZ hotm0008;
                    |HAZ hotm0007;
                    |HAZ hotm0011;
                    |PIEINF;
               |FINSI;
               |CIERRA #labtraba;
               |CIERRA #hotm0007;
               |CIERRA #hotm0008;
               |CIERRA #hotm0011;
               |CIERRA #nomvarca;

          |SINO;
               |MENAV "0000Informe [hotm0043] inaccesible o inexistente.";
          |FINSI;
          |FININF;
     |SINO;
          |MENAV "0000Proceso abortado.";
     |FINSI;
     |FINIMP;
|FPRC;
