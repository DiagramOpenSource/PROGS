      *****   CALCULO DE NOMINAS  (FINIQUITO !!!- 1¦ PARTE) *********
      *****   ACTUALIZA Y SE ENGANCHA AL PROGRAMA DE CALCULO ********

|FICHEROS;
    nomcalf1 #00;      || pantalla calcular sin actualizar
    labempre #01;      || empresas
    labtraba #02;      || trabajadores
    nomvarca #07;      || variaciones cabs.
    nomconca #09;      || cabeceras convenios
    nomcalca #11;      || registro calculos cabeceras
    nomcalli #12;      || registro calculos lineas
    nomhicca #39;      || historico cabs.
    nomvart3 #40;      || variaciones t.parcial (ctos.irregulares)
    nomgruec;          || grupos de empresas Cabs.
    nomgruel;          || grupos de empresas Lins.
|FIN;

|VARIABLES;
    &runnom = "nomcalf1";
    lmn = 0;
    mevoy = 0;
    mesnum = 0;
    anynum = 0;
    topemes = 0;
    mesact = 0;
    anyact = 0;
    nume1 = 0;
    nume2 = 0;
    nume3 = 0;
    fech1 = @;
    fech2 = @;
    fech3 = @;
    fech4 = @;
    swlimite = 0;
    swfuera = 0;
    tt = 0;
    campo = 0;
    num_enfer = 0;
    num_accid = 0;
    dia_enfer = 0;
    dia_accid = 0;
    num_veces = 0;
    mes_d1 = 0;
    mes_d2 = 0;
    mes_d3 = 0;
    mes_d4 = 0;
    mes_h1 = 0;
    mes_h2 = 0;
    mes_h3 = 0;
    mes_h4 = 0;
    x = 0;
    desde = 0;
    hasta = 0;
    resta = 0;
    dia_d = 0;
    dia_h = 0;
    mesn = 0;
    anyn = 0;
    dia_2 = 0;
    dia_3 = 0;
    dia_4 = 0;
    cl = 0;
    descl = 0;
    hascl = 0;
    topemes1 = 0;
    topemes2 = 0;
    mid = 0;
    nume0 = 0;
    pageta = 0;
    aver = 0;
    paga = 0;
    swdesde = 0;
    swhasta = 0;
    desde_p = 0;
    b_dif = 0;
    a_bases = 0;
    d_dto = 0;
    hasta_p = 0;
    confecc = 0;
    dias = 0;
    desde_i = 0;
    hasta_i = 0;
    dedia_i = 0;
    hadia_i = 0;
    dedia_p = 0;
    hadia_p = 0;
    tipos = 0;
    estevale = 0;
    ha2 = 0;
    pdia_0 = 0;
    pdia_1 = 0;
    pdia_2 = 0;
    pdia_3 = 0;
    pdia_4 = 0;
    pdia_6 = 0;
    pdia_a = 0;
    pdia_b = 0;
    pdia_c = 0;
    pdia_d = 0;
    pdia_e = 0;
    pdia_f = 0;
    dvalo_209 = 0;
    vari_desde = 0;
    vari_hasta = 0;
    de1 = 0;
    ha1 = 0;
    de2 = 0;
    calcdto = 0;
    diasefe = 0;
    perio = 0;
    swpro = 0;
    t_prorrata = 0;
    numer = 0;
    bisiesto = 0;
    mas = 0;
    rompe = 0;
    des1 = 0;
    des2 = 0;
    has1 = 0;
    has2 = 0;
    des = 0;
    has = 0;
    act = 0;
    pp = 0;
    pepulti = 0;
    nume6 = 0;
    nume7 = 0;
    concep1 = 0;
    concep2 = 0;
    desc = 0;
    hasc = 0;
    actc = 0;
    duni1_999 = 0;
    duni2_999 = 0;
    dvalo_999 = 0;
    product = 0;
    divide = 0;
    &dia = "";
    &mes = "";
    &anyo = "";
    &fecha = "";
    &clave = "";
    &fecsys = @;        || variable de trabajo para fecha sistema
    &fvenci = "";       || fecha vencimiento antiguedad
    &fecalf = "";       || calculo 'desglosa'
    &fecbaj = "";       || calculo 'baja'
    &any = "";          || calculo 'desglosa'
    &alfa = "";
    &alfa1 = "";        || variable de trabajo
    &alfa2 = "";        || variable de trabajo
    &alfa3 = "";        || variable de trabajo
    &alfa4 = "";        || variable de trabajo
    &p_mes = "";        || variable de trabajo para pintar mes literal
    &cabecera = 0;      || switch para grabar o no cabeceras de calculos
    &seleccio = 0;      || switch para mensaje 'Seleccion vacia' o no
    &convenio = 0;      || switch de error de existencia o no de linea convenio
    &concepto = 0;      || variable de trabajo del campo 'concepto'
    &trabamen = "";     || variable de trabajo para construir mensaje
    &mespinta = "";     || variable de trabajo para pintar el mes
    &mensaje0 = "    Seleccion vacia ...";
    &mensaje1 = "ATENCION, PROCESO DETENIDO !!! Convenio ";
    &mensaje2 = ", INEXISTENTE en fichero ";
    &meslabo = "";      || mes del calendario laboral
    &graba05 = "";
    &graba08 = "";
    &graba09 = "";      || unidades alfanumericas
    &graba10 = "";      || valor alfanumerico
    &tipo1 = "";        || mes que cumple a¤o (actual)
    &tipo2 = "";        || mes que cumple a¤o (siguiente)
    &tipo3 = "";        || trimestre que cumple a¤o (actual)
    &tipo4 = "";        || trimestre que cumple a¤o (siguiente)
    &tipo5 = "";        || semestre que cumple a¤o (actual)
    &tipo6 = "";        || semestre que cumple a¤o (siguiente)
    &tipo7 = "";        || a¤o que cumple a¤o (actual)
    &tipo8 = "";        || a¤o que cumple a¤o (siguiente)
    &swcorre = 0;
    &mes_act = 0;   || mes actualizacion
    &any_act = 0;   || any actualizacion
    &guarda = 0;  || dias tope mes
    &guaact = 0;
    &conser = 0;  || dias laborables del mes
    &histori = "";
    &swmarca = "";
    &labtraba = 0;
    &nomcalf1 = 0;
    &salte_1 = 0;
    &salte_2 = 0;
    &nor_mensu = 0;
    &nor_diari = 0;
    t_fic = "nomcalli";
    t_haz = 0;
    para1 = 0;
    para2 = 0;
    para3 = 0;
    para4 = 0;
    sw_mater = 0;
    m_dias = 0;
    div_prom = 0;
    no_promac = 0;
    pr_enf = 0;
    pr_acc = 0;
    tpz = 0;
    es_irregul = 0;
    hany = 0;
    hmes = 0;
    hbase = 0;
    henfe = 0;
    hdias = 0;
    hhor1 = 0;
    hhor2 = 0;
    &EURODB = 0;
    swActiva = 0;
    num_veces_e = 0;
    num_veces_m = 0;
    d_acum = 0;
    swPorGrupos = 0;
    nGrupo = 0;
    nCampoG = 0;
    pror_p = 0;
     aAlfa1 = "";
     aAlfa2 = "";
     &nElMes = 0;
     &nElAny = 0;
     &nElMesIRPF = 0;
     &nElAnyIRPF = 0;
     &eaVengoDe = "nomcalf1";
     FEstado = 0;
     aAlfa = "";
     &enAct89 = 0;
     &eaAct60 = "";
     &enAct92 = 0;
|FIN;

|INCLUYE nomcal_1;
|INCLUYE nomcal_4;

||****************************************************************************
|| COMO SI FUERA UN -INCLUYE nomcal_3 (pero solo de 'chequea' e 'historico')
||****************************************************************************

|PROCESO chequea;  || chequea el trabajador este en periodo de alta
    swfuera = 0;
|SI #2#42 ! "A"; |Y #2#42 ! "B"; |Y #2#42 ! "E";
        fecalf = #2#36;   || fecha de alta
    |HAZ desglosa;
    |SI any > #0#9;|O any = 0;
            swfuera = 1;
    |SINO;
        |SI mes > #0#8; |Y any = #0#9;
                swfuera = 1;
        |FINSI;
    |FINSI;
    |SI swfuera = 0;
            fecalf = #2#37;  || fecha de baja
        |HAZ desglosa;
        |SI any < #0#9; |Y any ! 0;
                swfuera = 1;
        |SINO;
            |SI mes < #0#8; |Y any = #0#9; |Y mes ! 0;
                    swfuera = 1;
            |FINSI;
        |FINSI;
    |FINSI;
|SINO;
        swfuera = 1;
|FINSI;
|FPRC;

|PROCESO historico;
|ABRE #39;
    #39#0 = #2#0;   || empresa
    #39#1 = #2#1;   || trabajador
    nume1 = #0#9;   nume1 = nume1 $ 100;
    #39#66= nume1;  || a¤o
    #39#2 = #0#8;   || mes
    #39#3 = 0;      || tipo
|LEE 000#39=;
|SI FSalida = 0;
        alfa1 = "    Debera desactualizar a [" + #39#0 + "." + #39#1 + "." + #39#66 + "." + #39#2 + "." + #39#3 + "] para poder calcularlo ...";
    |MENAV alfa1;
        swfuera = 1;
|FINSI;
|CIERRA #39;
|FPRC;

||****************************************************************************
||                  LLAMADAS DESDE EL MANTENIMIENTO
||****************************************************************************

|PROCESO seleccion; |TIPO 0;
|SI #0#21 = 0;
    |PARA para1 = 0; |SI para1 [ 7; |HACIENDO para1 = para1 + 1;
        |CAMPO_MODIFICA #0para1, 1, "S";
    |SIGUE;
    |PARA para1 = 22; |SI para1 [ 62; |HACIENDO para1 = para1 + 1;
        |CAMPO_MODIFICA #0para1, 1, "N";
    |SIGUE;
    |PARA nume1 = 21; |SI nume1 [ 62; |HACIENDO nume1 = nume1 + 1;
            #0nume1 = 0;
    |SIGUE;
    |PINDA #0#0;
|SINO;
    |PARA para1 = 0; |SI para1 [ 7; |HACIENDO para1 = para1 + 1;
        |CAMPO_MODIFICA #0para1, 1, "N";
    |SIGUE;
    |PARA para1 = 22; |SI para1 [ 62; |HACIENDO para1 = para1 + 1;
        |CAMPO_MODIFICA #0para1, 1, "S";
    |SIGUE;
|FINSI;
|FPRC;

|PROCESO pintames; |TIPO 0;
|SI #0Campo =  1;  p_mes = "ENERO     ";  |FINSI;
|SI #0Campo =  2;  p_mes = "FEBRERO   ";  |FINSI;
|SI #0Campo =  3;  p_mes = "MARZO     ";  |FINSI;
|SI #0Campo =  4;  p_mes = "ABRIL     ";  |FINSI;
|SI #0Campo =  5;  p_mes = "MAYO      ";  |FINSI;
|SI #0Campo =  6;  p_mes = "JUNIO     ";  |FINSI;
|SI #0Campo =  7;  p_mes = "JULIO     ";  |FINSI;
|SI #0Campo =  8;  p_mes = "AGOSTO    ";  |FINSI;
|SI #0Campo =  9;  p_mes = "SEPTIEMBRE";  |FINSI;
|SI #0Campo = 10;  p_mes = "OCTUBRE   ";  |FINSI;
|SI #0Campo = 11;  p_mes = "NOVIEMBRE ";  |FINSI;
|SI #0Campo = 12;  p_mes = "DICIEMBRE ";  |FINSI;
|ATRI I;
|PINTA 0748, p_mes;
|ATRI 0;
|FPRC;

|PROCESO mirames; |TIPO 7; ||calcula el mes por defecto segun fecha sistema
    fecsys = ~;
    alfa1 = fecsys;
    alfa1 = alfa1 % 402;
    #0Campo = alfa1;
    lmn = 0;
    #0Campo = #0Campo + 1;
|SI #0Campo = 13;  #0Campo = 1;  lmn = 1; |FINSI;
|PINTA #0Campo;
|HAZ pintames;
|FPRC;

|PROCESO defecto; |TIPO 7; ||calcula el a¤o por defecto segun fecha sistema
    fecsys = ~;
    alfa1 = fecsys;
    alfa1 = alfa1 % -104;
    #0Campo = alfa1;
    #0Campo = #0Campo + lmn;
|PINTA #0Campo;
|FPRC;

|PROCESO lib_0; |TIPO 0;   || halla ultimo dia natural del mes
    mesnum = #0#8;
    anynum = #0#9;
|HAZ ulti_dia;
    guarda = topemes;      || cambia de variable para no perderlo
|FPRC;

||****************************************************************************
||                    PROCESO Y LECTURA DE TRABAJADORES
||****************************************************************************

|PROCESO proceso;
|HAZ chequea;
|HAZ activasino;
|SI swfuera = 0; |Y swActiva = 1;
    |HAZ historico;
    |SI swfuera = 0;
        |HAZ pintalo;
            #9#0 = #2#87;
        |LEE 000#9=;
        |SI FSalida = 0;
                cabecera = 0;
            |HAZ actualiz;
            |SI swcorre = 1;
                    seleccio = 1;
                |HAZ carga;
                |CIERRAT #9;
                |CIERRAT #11;
                |CIERRAT #12;
                nElMes = #0#8;
                nElAny = #0#9;
                |SUB_EJECUTA "nomcalf2.run";
                |ABRET #09;
                |ABRET #11;
                |ABRET #12;
            |FINSI;
        |SINO;
            |ATRI P;
                trabamen = #2#87;
                trabamen = "    " + mensaje1 + trabamen + mensaje2;
            |MENAV trabamen;
            |ATRI 0;
        |FINSI;
    |FINSI;
|FINSI;
|FPRC;

|DEFBUCLE nomcalf10;
 #2#1;
 87, 17, 77, 44;
 #0#0, #0#2, #0#4, #0#6, #0#71, "A";
 #0#1, #0#3, #0#5, #0#7, #0#72, "A";
 e;
 NULL, proceso;
|FIN;

|DEFBUCLE nomcalcg;
     #2#1;
     44;
     #nomgruel#1, 00001, "A";
     #nomgruel#1, 99999, "A";
     ;
     NULL, proceso;
|FIN;

|PROCESO CalGrupo;       || para cada una hace el proceso de siemrpe
     |BUCLE nomcalcg;
|FPRC;

|DEFBUCLE Grupo;         || lee empresas del grupo
     #nomgruel#1;
     ;
     nGrupo, 00001;
     nGrupo, 99999;
     ;
     NULL, CalGrupo;
|FIN;

|PROCESO PorGrupos;
     swPorGrupos = 1;
     |PARA nCampoG = 63; |SI nCampoG [ 70; |HACIENDO nCampoG = nCampoG + 1;
          nGrupo = #0nCampoG;
          |BUCLE Grupo;
     |SIGUE;
|FPRC;

|PROCESO rellena;      || rellena de ceros campos numericos ('tipos')
    alfa1 = nume1;  alfa1 = "00" + nume1;  alfa1 = alfa1 % -102;
    alfa2 = nume2;
    nume1 = mesact;
    nume2 = anyact;

    alfa1 = alfa1 + "." + alfa2;
|FPRC;

|PROCESO tipos; |TIPO 0;      || construye fechas vencimiento para antiguedad
     mesact = nElMes;
     anyact = nElAny;
     nume1 = mesact;
     nume2 = anyact;
     |HAZ rellena;
     tipo1 = alfa1;                 || mes actual

     nume1 = nume1 - 1;
     |SI nume1 = 0;
          nume1 = 12;
          nume2 = nume2 - 1;
     |FINSI;
     |HAZ rellena;
     tipo2 = alfa1;                 || mes siguiente

     |SI mesact [  3;|Y mesact ] 1;  nume1 =  3;  |FINSI;
     |SI mesact [  6;|Y mesact ] 3;  nume1 =  6;  |FINSI;
     |SI mesact [  9;|Y mesact ] 6;  nume1 =  9;  |FINSI;
     |SI mesact [ 12;|Y mesact ] 9;  nume1 = 12;  |FINSI;
     |HAZ rellena;
     tipo3 = alfa1;                 || trimestre actual

     |SI mesact [  3;|Y mesact ] 1;  nume1 =  12;  nume2 = nume2 - 1;  |FINSI;
     |SI mesact [  6;|Y mesact ] 3;  nume1 =   3;                      |FINSI;
     |SI mesact [  9;|Y mesact ] 6;  nume1 =   6;                      |FINSI;
     |SI mesact [ 12;|Y mesact ] 9;  nume1 =   9;                      |FINSI;
     |HAZ rellena;
     tipo4 = alfa1;                 || trimestre siguiente

     |SI mesact [  6;|Y mesact ] 1;  nume1 =  6;  |FINSI;
     |SI mesact [ 12;|Y mesact ] 6;  nume1 = 12;  |FINSI;
     |HAZ rellena;
     tipo5 = alfa1;                  || semestre actual

     |SI mesact [  6;|Y mesact ] 1;  nume1 = 12;  nume2 = nume2 - 1;  |FINSI;
     |SI mesact [ 12;|Y mesact ] 6;  nume1 =  6;                      |FINSI;
     |HAZ rellena;
     tipo6 = alfa1;                  || semestre siguiente

     nume1 = 12;
     |HAZ rellena;
     tipo7 = alfa1;                  || a¤o actual

     nume1 = 12;
     nume2 = nume2 - 1;
     |HAZ rellena;
     tipo8 = alfa1;                  || a¤o siguiente
|FPRC;


|PROGRAMA;            || INICIO DEL PROCESO
|HAZ LeeEURODB;
|CLS;
|CABEZA "Calc.Actualizando";
|PINPA #0#0;
|ABRE #0;
|DDEFECTO #0;
|LEE 001#0p;
|SI FSalida = 0;  |BORRA 020#0a;  |FINSI;
|CIERRA #0;
|PINDA #0#0;
|ENDATOS #1#0;
|SI FSalida = 0;
    |ABRET #09;
    |ABRET #11;
    |ABRET #12;
    |DIMENSIONA 255, labtraba, 0;
    |DIMENSIONA  15, nomcalf1, 0;
    |HAZ lib_0;
    nElMes = #0#8;
    nElAny = #0#9;
    |HAZ ValoresITIMS;
    |HAZ tipos;

    |SI #0#21 = 0;
        |SI #0#63 = 0;
            |BUCLE nomcalf10;
        |SINO;
            |HAZ PorGrupos;
        |FINSI;
    |SINO;
        |HAZ salteada;
    |FINSI;
    |SI seleccio = 0;
        |ATRI P;
        |MENAV mensaje0;
        |ATRI 0;
    |FINSI;
    |CIERRAT #09;
    |CIERRAT #11;
    |CIERRAT #12;
    |FINDIM labtraba;
    |FINDIM nomcalf1;

      nElMesIRPF = #0#8;
      nElAnyIRPF = #0#9;
     |SUB_EJECUTA "nom400ca";
|FINSI;
|ABRE #0;
|GRABA 040#0n;
|CIERRA #0;
|FPRO;

|PROCESO carga;
|PARA nume1 = 0;|SI nume1 [ 248; |HACIENDO nume1 = nume1 + 1;
        alfa1 = #2nume1;
    |AL_BUF labtraba, nume1, alfa1;
|SIGUE;
|PARA nume1 = 0;|SI nume1 [ 10; |HACIENDO nume1 = nume1 + 1;
        alfa1 = #0nume1;
    |AL_BUF nomcalf1, nume1, alfa1;
|SIGUE;
|FPRC;

||****************************************************************************
||               RUTINAS DEL PROCESO Y LECTURA DE TRABAJADORES
||****************************************************************************

|PROCESO desglosa;     || DESGLOSA EN DIA, MES Y ANYO UNA FECHA DD.MM.AAAA
    alfa1 = fecalf %  102;   dia = alfa1;
    alfa1 = fecalf %  402;   mes = alfa1;
    alfa1 = fecalf % -104;   any = alfa1;
|SI alfa1 = "....";
        dia = 0;
        mes = 0;
        any = 0;
|FINSI;
|FPRC;

|PROCESO pintalo;
|PINTA 1972, #2#0;
|PINTA 2072, #2#1;
|FPRC;

|| *************************************************************************
||            ACTUALIZA SIN GRABAR !!! LOS CAMPOS DEL TRABAJADOR:
||    ACUM. DIAS ENFERMEDAD, DIAS PAGAS, CALCULO DE PROMEDIOS, ACUM. IRPF
|| *************************************************************************

|PROCESO actual;
    swcorre = 1;
|HAZ leetodo;
|HAZ empieza1;
                    || PANTALLA DE ENFEMEDAD Y ACCIDENTES TRABAJADOR
|HAZ si_mater;           || maternidad (1995)

    num_enfer = 0;  num_accid = 0;
    dia_enfer = 0;  dia_accid = 0; num_veces = 0;
    num_veces_e = 0; num_veces_m = 0; d_acum = 0; nume1 = 0;

    mes_d1 = #11#19; mes_d2 = #11#20;  || Desde mesde
    mes_d3 = #11#21; mes_d4 = #11#22;

    mes_h1 = #11#23; mes_h2 = #11#24;  || Hasta meses
    mes_h3 = #11#25; mes_h4 = #11#26;

|SI #11#23 = 0; mes_h1 = #11#7; |FINSI;
|SI #11#24 = 0; mes_h2 = #11#7; |FINSI;
|SI #11#25 = 0; mes_h3 = #11#7; |FINSI;
|SI #11#26 = 0; mes_h4 = #11#7; |FINSI;

|SI #11#19 = 0; mes_d1 = 1; |FINSI;
|SI #11#20 = 0; mes_d2 = 1; |FINSI;
|SI #11#21 = 0; mes_d3 = 1; |FINSI;
|SI #11#22 = 0; mes_d4 = 1; |FINSI;

                        || ENFERMEDADES

|SI #11#27 = "E";|O #11#27 = "M"; num_enfer = num_enfer + 1; |FINSI;
|SI #11#28 = "E";|O #11#28 = "M"; num_enfer = num_enfer + 1; |FINSI;
|SI #11#29 = "E";|O #11#29 = "M"; num_enfer = num_enfer + 1; |FINSI;
|SI #11#30 = "E";|O #11#30 = "M"; num_enfer = num_enfer + 1; |FINSI;

    num_veces = num_enfer;
|SI num_veces > 1;
    |PARA x = 27;|SI num_veces ] 1;|HACIENDO x = x + 1;

        |SI x = 27; desde = 19; hasta = 23; resta = mes_h1 - mes_d1; |FINSI;
        |SI x = 28; desde = 20; hasta = 24; resta = mes_h2 - mes_d2; |FINSI;
        |SI x = 29; desde = 21; hasta = 25; resta = mes_h3 - mes_d3; |FINSI;
        |SI x = 30; desde = 22; hasta = 26; resta = mes_h4 - mes_d4; |FINSI;

        |SI #11x = "E";|O #11x = "M";
                num_veces = num_veces - 1;
                |SI #1x = "E"; num_veces_e = num_veces_e + 1; |FINSI;
                |SI #1x = "M"; num_veces_m = num_veces_m + 1; |FINSI;
        |FINSI;
        |SI num_veces = 0;
            |SI #11x = "E";|O #11x = "M";
                    clave = #11x;
                    dia_d = #11desde;
                    dia_h = #11hasta;
                    |SI #1x = "E";
                        |SI num_veces_e > 1; d_acum = resta + 1; |FINSI;
                    |FINSI;
                    |SI #1x = "M";
                        |SI num_veces_m > 1; d_acum = resta + 1; |FINSI;
                    |FINSI;
            |FINSI;
        |FINSI;
    |SIGUE;
|SINO;
    |SI num_veces = 1;
        |PARA x = 27;|SI x [ 30;|HACIENDO x = x + 1;
            |SI x = 27; desde = 19; hasta = 23; |FINSI;
            |SI x = 28; desde = 20; hasta = 24; |FINSI;
            |SI x = 29; desde = 21; hasta = 25; |FINSI;
            |SI x = 30; desde = 22; hasta = 26; |FINSI;
            |SI #11x = "E";|O #11x = "M";
                    clave = #11x;
                    dia_d = #11desde;
                    dia_h = #11hasta;
            |FINSI;
        |SIGUE;
    |FINSI;
|FINSI;

    div_prom = #11#8;                                  || resto (dias alta)

|SI #2#42 = "E";                                       || eventuales agrarios
     div_prom = #11#11;                                || (jornadas reales)
|FINSI;

|SI #2#45 = "087";|O #2#45 = "097";|O #2#45 = "085";   || aprend./format.
        div_prom = 30;                                 || (30 a pelo)
|FINSI;

    no_promac = 0;       || switch para no tocar el promedio de AT

|SI dia_d = 0;|Y dia_h = 0;|Y #2#89 ! 0;     || sigue de EC del mes anterior
        #2#88 = #2#88 + #11#8;     || dias a¤o
        #2#89 = #2#89 + #11#8;     || dias altas
        #2#90 = #2#90 + #11#8;     || dias enfer.anual
    |SI #2#95 = 0;  #2#95 = pr_enf;  |FINSI;
        no_promac = 1;
|FINSI;

|SI num_enfer = 0;|Y #2#89 = 0;    || ni alta ni baja de EC este mes
        #2#60 = "";
        #2#90 = #2#88;
    |HAZ prom_enf;
|FINSI;

|SI dia_d ! 0;|Y dia_h = 0;        || baja de EC este mes
        #2#60 = clave;
    |SI #2#60 = "E";
            |SI num_veces_e > 1;
                nume1 = d_acum;
            |SINO;
                nume1 = #11#31;
            |FINSI;
            #2#88 = #2#88 + #11#31;
            ||#2#89 = #11#31;
            #2#89 = nume1;
            #2#90 = #2#90 + #11#31;
    |FINSI;
    |SI #2#60 = "M";
            |SI num_veces_m > 1;
                nume1 = d_acum;
            |SINO;
                nume1 = #11#65;
            |FINSI;
            #2#88 = #2#88 + #11#65;
            ||#2#89 = #11#65;
            #2#89 = nume1;
            #2#89 = #11#65;
            #2#90 = #2#90 + #11#65;
    |FINSI;
    |SI #2#95 = 0;  #2#95 = pr_enf;  |FINSI;
        no_promac = 1;
|FINSI;

|SI dia_d = 0;|Y dia_h ! 0;        || alta de EC este mes
        #2#60 = "";
        #2#88 = #2#88 + #11#31 + #11#65;
        #2#89 = 0;
        #2#90 = #2#90 + #11#31 + #11#65;
    |HAZ prom_enf;
|FINSI;

|SI dia_d ! 0;|Y dia_h ! 0;        || baja y alta de EC este mes
        #2#60 = "";
        #2#88 = #2#88 + #11#31 + #11#65;
        #2#89 = 0;
        #2#90 = #2#90 + #11#31 + #11#65;
    |HAZ prom_enf;
|FINSI;

                        || ACCIDENTES

|SI #11#27 = "A"; num_accid = num_accid + 1; |FINSI;
|SI #11#28 = "A"; num_accid = num_accid + 1; |FINSI;
|SI #11#29 = "A"; num_accid = num_accid + 1; |FINSI;
|SI #11#30 = "A"; num_accid = num_accid + 1; |FINSI;

    num_veces = num_accid;
|SI num_veces > 1;
    |PARA x = 27;|SI num_veces ] 1;|HACIENDO x = x + 1;
        |SI x = 27; desde = 19; hasta = 23; resta = mes_h1 - mes_d1;|FINSI;
        |SI x = 28; desde = 20; hasta = 24; resta = mes_h2 - mes_d2;|FINSI;
        |SI x = 29; desde = 21; hasta = 25; resta = mes_h3 - mes_d3;|FINSI;
        |SI x = 30; desde = 22; hasta = 26; resta = mes_h4 - mes_d4;|FINSI;

        |SI #11x = "A";
                num_veces = num_veces - 1;
                dia_accid = dia_accid + resta + 1;
        |FINSI;
        |SI num_veces = 0;
            |SI #11x = "A";
                    dia_d = #11desde;
                    dia_h = #11hasta;
                    dia_accid = dia_accid - resta - 1;
            |FINSI;
        |FINSI;
    |SIGUE;
|SINO;
    |SI num_veces = 1;
        |PARA x = 27;|SI x [ 30;|HACIENDO x = x + 1;
            |SI x = 27; desde = 19; hasta = 23; |FINSI;
            |SI x = 28; desde = 20; hasta = 24; |FINSI;
            |SI x = 29; desde = 21; hasta = 25; |FINSI;
            |SI x = 30; desde = 22; hasta = 26; |FINSI;
                |SI #11x = "A";
                        dia_d = #11desde;
                        dia_h = #11hasta;
                |FINSI;
        |SIGUE;
    |FINSI;
|FINSI;

|SI dia_d = 0;|Y dia_h = 0;|Y #2#92 ! 0;     || sigue de AT del mes anterior
        #2#91 = #2#91 + #11#8;     || dias a¤o
        #2#92 = #2#92 + #11#8;     || dias alta
    |SI #2#96 = 0;  #2#96 = pr_acc;  |FINSI;
|FINSI;

|SI num_accid = 0;|Y #2#92 = 0;    || ni alta ni baja de AT este mes
    |HAZ prom_acc;
|FINSI;

|SI dia_d ! 0;|Y dia_h = 0;        || baja de AT este mes
        #2#91 = #2#91 + #11#32;
        #2#92 = #11#32 - dia_accid;
    |SI #2#96 = 0;  #2#96 = pr_acc;  |FINSI;
|FINSI;

|SI dia_d = 0;|Y dia_h ! 0;        || alta de AT este mes
        #2#91 = #2#91 + #11#32;
        #2#92 = 0;
    |HAZ prom_acc;
|FINSI;

|SI dia_d ! 0;|Y dia_h ! 0;        || baja y alta de AT este mes
        #2#91 = #2#91 + #11#32;
        #2#92 = 0;
    |HAZ prom_acc;
|FINSI;
                        || RESTO DE CAMPOS

    #2#36 = #11#4;          || fecha de alta
|SI #11#5 ! ".........."; |Y #11#5 ! "          ";
        #2#37 = #11#5;          || fecha de baja (solo si es algo !!!)
|FINSI;
    #2#34 = #11#6;          || fecha antiguedad
    alfa1 = #2#37;
    alfa = alfa1 % 402;
    alfa1 = alfa1 % -102;
    mesn = alfa;
    anyn = alfa1;
|SI mesn = #11#2; |Y anyn = any_act;   || situacion (baja)
        #2#44 = "B";
|FINSI;
    #2#93 = #2#93 + #11#33; || Dias de huelga
    #2#94 = #2#94 + #11#34; || Dias de absentismo
    #2#243= #2#243 +#11#78; || Dias de desempleo

|SI #11#2 = 12;
        #2#88 = 0; #2#91 = 0; #2#93 = 0; #2#94 = 0; #2#243 = 0;
    |SI #2#60 = " ";
            #2#90 = 0;
    |FINSI;
|FINSI;

    #2#148 = "A";
|SI #2#42 ! "T";|Y #2#42 ! "X";
        #2#46 = #11#10;            || en los t.p. no se actualiza
|FINSI;

|SI #2#239 = "D";   #2#240 = #2#240 + #11#8; |FINSI;   || dias antiguedad

             || **************** TERCERA PANTALLA DEL TRABAJADOR
|HAZ prorrata;

     enAct89 = #2#89;
     eaAct60 = #2#60;
     enAct92 = #2#92;
|FPRC;

|PROCESO si_mater;
     sw_mater = 0;
     |SI #11#65 ! 0;|Y #0#9 ] "1995";

     m_dias = #11#65;
     |SI #2#60 = "M";     m_dias = m_dias + #2#89; |FINSI;

        nume1 = 0;
    |SI #11#27 = "M";    nume1 = #11#23; |FINSI;
    |SI #11#28 = "M";    nume1 = #11#24; |FINSI;
    |SI #11#29 = "M";    nume1 = #11#25; |FINSI;
    |SI #11#30 = "M";    nume1 = #11#26; |FINSI;
    |SI nume1 = 0;       nume1 = #11#7;  |FINSI;

        alfa1 = nume1;       alfa1 = "00" + alfa1;    alfa1 = alfa1 % -102;
        alfa2 = #0#8;        alfa2 = "00" + alfa2;    alfa2 = alfa2 % -102;
        alfa3 = alfa1 + "." + alfa2 + "." + #0#9;
        fech1 = alfa3;            nume1 = fech1;
        fech2 = "01.01.1995";     nume2 = fech2;
        nume3 = nume1 - nume2 + 1;

    |SI m_dias [ nume3; sw_mater = 1;  |FINSI;
    |SI #0#8 ] 6;   sw_mater = 1;  |FINSI;
|FINSI;
|FPRC;

|PROCESO resta_mes;
    hmes = hmes - 1;
|SI hmes = 0;
        hmes = 12;
        hany = hany - 1;
    |SI hany < 0;
            hany = 99;
    |FINSI;
|FINSI;
|FPRC;

|PROCESO busca_acc;
|HAZ resta_mes;
|DDEFECTO #39;
    #39#0  = #11#0;  || empresa
    #39#1  = #11#1;  || trabajador
    #39#2  = hmes;   || mes
    #39#3  = #11#3;  || tipo
    #39#66 = hany;   || a¤o
|LEE 000#39=;
|SI #39#65 ! 0;
        henfe = henfe + #39#62;
|FINSI;
|SI #1#40 ! "S";
        hhor1 = 0;
        hhor2 = 0;
|SINO;
        hhor1 = hhor1 + #39#41;
        hhor2 = hhor2 + #39#42;
|FINSI;
    hbase = hbase + #39#39;
    hdias = hdias + #39#8;
|FPRC;

|PROCESO prom_tp_acc;
|SI #2#42 = "T";|O #2#42 = "X";|O #2#42 = "P";
        hmes = #11#2;
        nume1 = #0#9;   nume1 = nume1 $ 100;
        hany = nume1;
    |ABRE #39;
    |HAZ busca_acc;
    |HAZ busca_acc;
    |CIERRA #39;
|FINSI;
|FPRC;

|PROCESO busca_enf;
|HAZ resta_mes;
|DDEFECTO #39;
    #39#0  = #11#0;  || empresa
    #39#1  = #11#1;  || trabajador
    #39#2  = hmes;   || mes
    #39#3  = #11#3;  || tipo
    #39#66 = hany;   || a¤o
|LEE 000#39=;
|SI #39#65 ! 0;
        henfe = henfe + #39#62;
|FINSI;
    hbase = hbase + #39#39;
    hdias = hdias + #39#8;
|FPRC;

|PROCESO prom_tp_enf;
|SI #2#42 = "T";|O #2#42 = "X";|O #2#42 = "P";
        hmes = #11#2;
        nume1 = #0#9;   nume1 = nume1 $ 100;
        hany = nume1;
    |ABRE #39;
    |HAZ busca_enf;
    |HAZ busca_enf;
    |CIERRA #39;
|FINSI;
|FPRC;

|PROCESO prom_enf;
|SI #2#42 ! "A";|Y #2#42 ! "B";|Y #2#42 ! "E";    || distingue a los agrarios
    |SI sw_mater ! 0;
            henfe = #11#62;   || si tiene maternidad la base de cotizacion
    |SINO;                    ||/es la suma de la de maternidad + la normal
            henfe = 0;
    |FINSI;
        hbase = #11#39;
        hdias = div_prom;
    |HAZ prom_tp_enf;         || busca los 2 ultimos meses para los t.parcial
        #2#95 = (hbase + henfe) / hdias;
|SINO;
    |SI div_prom ! 0;
            #2#95 = (#11#37 + #11#38) / div_prom; || total remuneracion / dias alta
    |FINSI;
|FINSI;
|FPRC;

|PROCESO prom_acc;
|SI no_promac = 0;
    |SI #2#42 ! "A";|Y #2#42 ! "B";|Y #2#42 ! "E";  || distingue los agrarios
        |SI sw_mater ! 0;
                henfe = #11#62; || si tiene maternidad la base de cotizacion
        |SINO;                  ||/es la suma de la de maternidad + la normal
                henfe = 0;
        |FINSI;
        |SI #1#40 ! "S";
                hhor1 = 0;
                hhor2 = 0;
        |SINO;
                hhor1 = #11#41;
                hhor2 = #11#42;
        |FINSI;
            hbase = #11#40;
            hdias = div_prom;
        |HAZ prom_tp_acc;     || busca los 2 ultimos meses para los t.parcial
            #2#96 = ((hbase + henfe) - (hhor1 + hhor2)) / hdias;
    |SINO;                                        || agrarios SI
        |SI div_prom ! 0;
                #2#96 = (#11#37 + #11#38) / div_prom;  || total remuner./dias alta
        |FINSI;
    |FINSI;
|SINO;
    |SI #2#96 = 0;  #2#96 = pr_acc;  |FINSI;      || esta en ILT de Enf.Comun
|FINSI;                                           ||/(no se toca)
|FPRC;

|DEFBUCLE nomcalf11;
 #11#1;
 ;
 #2#0, #2#1, mes_act, 0;
 #2#0, #2#1, mes_act, 0;
 ;
 NULL, actual;
|FIN;

|PROCESO actualiz;
    mes_act = #0#8 - 1;
    any_act = #0#9;
|SI mes_act = 0;
        mes_act = 12;
        any_act = any_act - 1;
|FINSI;
    mesnum = mes_act;
    anynum = any_act;
|HAZ ulti_dia;
    guaact = topemes;

    swcorre = 0;
|ABRE #1;
|ABRE #7;
|CIERRAT #11;
|BUCLE nomcalf11;        || bucle de calculos cabs.
|ABRET #11;
|CIERRA #1;
|CIERRA #7;
|FPRC;

|| *************************************************************************
||              RUTINAS DE LA ACTUALIZACION SIN GRABAR
|| *************************************************************************

|PROCESO leetodo;
|DDEFECTO #1;
    #1#0 = #2#0;
|LEE 020#1=;             || empresa

    pr_enf = 0;
    pr_acc = 0;
    #7#0 = #11#0;   || Codigo empresa
    #7#1 = #11#1;   || Codigo trabajador
    #7#2 = #11#2;   || Mes
    #7#3 = #11#3;   || Tipo
|LEE 000#7=;
|SI FSalida = 0;
        pr_enf = #7#17;       || promedio informado mediante variaciones
        pr_acc = #7#18;
|FINSI;
|FPRC;

|PROCESO empieza1;      || CARGA EL HISTORIAL DE ILT DEL TRABAJADOR
    dia_2 = guaact;
    dia_3 = 30;
|SI #2#42 = "D"; |O #2#42 = "T";
        dia_3 = guaact;
|FINSI;
    dia_4 = dia_3;
    histori = "AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA";
    swmarca = "B";
|HAZ altas;
|HAZ bajas;
|PARA cl = 27; |SI cl [ 30; |HACIENDO cl = cl + 1;
    |SI #11cl ! " ";
        |SI #11cl = "E";                 swmarca = "I";  |FINSI;
        |SI #11cl = "M";                 swmarca = "M";  |FINSI;
        |SI #11cl = "A";                 swmarca = "J";  |FINSI;
        |SI #11cl = "D";                 swmarca = "B";  |FINSI;
        |SI #11cl = "H";|Y #11#76 = "S"; swmarca = "B";  |FINSI;
        |SI #11cl = "F";|Y #11#77 = "S"; swmarca = "K";  |FINSI;
            descl = cl - 8;     hascl = cl - 4;
            topemes1 = #11descl; topemes2 = #11hascl;
        |HAZ calendar;
    |FINSI;
|SIGUE;
|FPRC;

|PROCESO altas;
    fecalf = #2#36;
|HAZ desglosa;
|SI mes = #11#2; |Y any = any_act;     || si es alta este mes
        topemes1 = 0;
        topemes2 = dia - 1;
    |SI topemes2 ! 0;  |HAZ calendar;  |FINSI;  || por si es alta el dia 1
|FINSI;
|FPRC;

|PROCESO bajas;
    fecalf = #2#37;
|HAZ desglosa;
|SI mes = #11#2; |Y any = any_act;     || si es baja este mes
        topemes1 = dia + 1;
        topemes2 = 0;
    |SI topemes1 [ guaact;  |HAZ calendar;  |FINSI;  || por si es final de mes
|FINSI;
|FPRC;

|PROCESO calendar;
|SI topemes1 = 0;  topemes1 =      1;  |FINSI;
|SI topemes2 = 0;  topemes2 = guaact;  |FINSI;
|PARA mid = topemes1; |SI mid [ topemes2; |HACIENDO mid = mid + 1;
        alfa1 = histori % 0;  nume0 = alfa1;         || longitud
        nume1 = (100 + (mid - 1));                   || parte izquierda
        alfa1 = histori % nume1;
        nume2 = (100 + (nume0 - mid)) * (-1);        || parte derecha
        alfa2 = histori % nume2;
        histori = alfa1 + swmarca + alfa2;           || une
|SIGUE;
|FPRC;

|| *************************************************************************
||              SUBRUTINAS DE LA ACTUALIZACION SIN GRABAR (BUCLELIN)
|| *************************************************************************

|PROCESO prorrata;   || ****** TERCERA PANTALLA DEL TRABAJADOR
    tpz = #11#8;
    es_irregul = 0;
|SI #2#42 = "I";|O #2#42 = "P";
        es_irregul = 1;
|FINSI;

    #12#0 = #11#0;
    #12#1 = #11#1;
    #12#2 = mes_act;
    #12#3 = 0;
|PARA pageta = 201; |SI pageta [ 208; |HACIENDO pageta = pageta + 1;
        #12#4 = pageta;
    |LEE 040#12=;
        aver = #12#10;
    |SI FSalida = 0;|Y aver ! 0;
        |HAZ empieza2;
        |HAZ busca;                          || carga dias periodo y 'swpro'
        |SI #9paga = "N";                    || si es normal o porcentual
            |HAZ deshas;
            |SI swdesde = 1;|O swhasta = 1;  || para descontar los dias ILT
                |HAZ diasilt;
                |HAZ descuen;
            |FINSI;
            |HAZ normal;
        |SINO;
            |HAZ porcen;
        |FINSI;
    |FINSI;
|SIGUE;
|FPRC;

***************************************************************************
  SUBRUTINAS DEL BUCLELIN 'prorrata'
***************************************************************************

|PROCESO empieza2;      || CARGA VARIABLES PARA LA PAGA ESPECIFICA
|SI #12#4 = 201; desde_p = 4;  b_dif = 65; a_bases = 55; d_dto = 50; |FINSI;
|SI #12#4 = 202; desde_p = 5;  b_dif = 66; a_bases = 56; d_dto = 51; |FINSI;
|SI #12#4 = 203; desde_p = 6;  b_dif = 67; a_bases = 57; d_dto = 52; |FINSI;
|SI #12#4 = 204; desde_p = 7;  b_dif = 68; a_bases = 58; d_dto = 53; |FINSI;
|SI #12#4 = 205; desde_p = 8;  b_dif = 69; a_bases = 59; d_dto = 54; |FINSI;
|SI #12#4 = 206; desde_p = 9;  b_dif = 84; a_bases = 81; d_dto = 78; |FINSI;
|SI #12#4 = 207; desde_p = 10; b_dif = 85; a_bases = 82; d_dto = 79; |FINSI;
|SI #12#4 = 208; desde_p = 11; b_dif = 86; a_bases = 83; d_dto = 80; |FINSI;
    hasta_p = desde_p + 8;  || hasta mes prorratas
    confecc = hasta_p + 8;  || mes confeccion
    paga    = confecc + 8;  || tipo de paga
    dias    = paga + 8;     || dias paga
    desde_i = desde_p + 88; || desde mes ILT
    hasta_i = desde_i + 8;  || hasta mes ILT
    dedia_i = hasta_i + 24; || desde dia ILT
    hadia_i = dedia_i + 8;  || hasta dia ILT
    dedia_p = hadia_i - 24; || desde dia prorrata
    hadia_p = dedia_p + 8;  || hasta dia prorrata
    tipos   = hadia_p - 88; || tipo de paga
    estevale = 0;
    ha2 = -1;
    pdia_0 = 0; pdia_1 = 0; pdia_2 = 0; pdia_3 = 0; pdia_4 = 0; pdia_6 = 0;
    pdia_a = 0; pdia_b = 0; pdia_c = 0; pdia_d = 0; pdia_e = 0; pdia_f = 0;
    dvalo_209 = #12#10;

    pror_p = pageta - 200 + 112;   || prorrateo paga S/N (trabajador)
    |SI #labtraba#pror_p# = "S"; || xxx
         #9confecc = 99;
    |FINSI;
|FPRC;

|PROCESO deshas;  || CARGA LOS DESDE/HASTA ILT PARA CONTAR LOS DIAS DE DTO.
    swdesde = 0;
    swhasta = 0;
|SI #9desde_i < #9hasta_i;
        vari_desde = #9desde_i;
        vari_hasta = #9hasta_i;
|FINSI;
|SI #9desde_i > #9hasta_i;
        vari_desde =  1;
        vari_hasta = 12;
|FINSI;
|SI #9desde_i = #9hasta_i;|Y #9dedia_i > #9hadia_i;
        vari_desde =  1;
        vari_hasta = 12;
|FINSI;
|SI #9desde_i = #9hasta_i;|Y #9dedia_i [ #9hadia_i;
        vari_desde = #9desde_i;
        vari_hasta = #9hasta_i;
|FINSI;
|SI #11#2 ] #9desde_i;  |Y #11#2 [ vari_hasta;   swdesde = 1;  |FINSI;
|SI #11#2 ] vari_desde; |Y #11#2 [ #9hasta_i;    swhasta = 1;  |FINSI;
|FPRC;

|PROCESO diasilt;
|SI #11#2 = #9desde_i;|Y #9dedia_i !      1;  estevale = 1;  |FINSI;
|SI #11#2 = #9hasta_i;|Y #9hadia_i ! guaact;  estevale = 1;  |FINSI;
|SI estevale = 1;
    |HAZ recuento;
|SINO;
    |HAZ diaspaga;
    |HAZ diasotro;
|FINSI;
|FPRC;

|PROCESO recuento;
    de1 = 0;  ha1 = -1;  de2 = 0;  ha2 = -1;
|SI #11#2 = #9desde_i;     de1 = #9dedia_i;  ha1 =    guaact;  |FINSI;
|SI #11#2 = #9hasta_i;     de1 =         1;  ha1 = #9hadia_i;  |FINSI;
|SI #9desde_i = #9hasta_i;|Y #9dedia_i [ #9hadia_i;
        de1 = #9dedia_i;   ha1 = #9hadia_i;
|FINSI;
|SI #9desde_i = #9hasta_i;|Y #9dedia_i > #9hadia_i;
        de1 = #9dedia_i;   ha1 =    guaact;
        de2 =         1;   ha2 = #9hadia_i;
|FINSI;
|PARA mid = de1; |SI mid [ ha1; |HACIENDO mid = mid + 1;
        nume1 = (100 * mid) + 1;
        alfa1 = histori % nume1;
    |SI                mid [ dia_3;  pdia_a = pdia_a + 1;  |FINSI;  || alta
    |SI alfa1 = "I";                 pdia_b = pdia_b + 1;  |FINSI;  || enf.
    |SI alfa1 = "B";|Y mid [ dia_3;  pdia_c = pdia_c + 1;  |FINSI;  || baja
    |SI alfa1 = "J";                 pdia_d = pdia_d + 1;  |FINSI;  || accid.
    |SI alfa1 = "K";                 pdia_e = pdia_e + 1;  |FINSI;  || abs.
    |SI alfa1 = "M";                 pdia_f = pdia_f + 1;  |FINSI;  || mat.
|SIGUE;
    pdia_a = pdia_a - pdia_c;
|PARA mid = de2; |SI mid [ ha2; |HACIENDO mid = mid + 1;
        nume1 = (100 * mid) + 1;
        alfa1 = histori % nume1;
    |SI                mid [ dia_3;  pdia_0 = pdia_0 + 1;  |FINSI;  || alta
    |SI alfa1 = "I";                 pdia_1 = pdia_1 + 1;  |FINSI;  || enf.
    |SI alfa1 = "B";|Y mid [ dia_3;  pdia_2 = pdia_2 + 1;  |FINSI;  || baja
    |SI alfa1 = "J";                 pdia_3 = pdia_3 + 1;  |FINSI;  || accid.
    |SI alfa1 = "K";                 pdia_4 = pdia_4 + 1;  |FINSI;  || abs.
    |SI alfa1 = "M";                 pdia_6 = pdia_6 + 1;  |FINSI;  || mat.
|SIGUE;
    pdia_0 = pdia_0 - pdia_2;
|SI ha2 = -1;
    |HAZ diasotro;
|FINSI;
|FPRC;

|PROCESO diaspaga;
    pdia_a = #11#08;
    pdia_b = #11#31;
    pdia_c = dia_3  - #11#08;
    pdia_d = #11#32;
    pdia_e = #11#34;
    pdia_f = #11#65;

|SI #11#77 = "S";  pdia_e = 0;  |FINSI; || como ya los ha restado de la paga
                                        ||/no lo actualiza

|SI #11#27 = "f";|O #11#28 = "f";|O #11#29 = "f";|O #11#30 = "f";
        pdia_e = 0;           || si es absentismo sin dto. no lo actualiza
|FINSI;
|FPRC;

|PROCESO diasotro;
    pdia_0 = pdia_a;
    pdia_1 = pdia_b;
    pdia_2 = pdia_c;
    pdia_3 = pdia_d;
    pdia_4 = pdia_e;
    pdia_6 = pdia_f;
|FPRC;

|PROCESO descuen;   || DESCUENTA DIAS ILT
|SI ha2 ! -1;
        calcdto = #2d_dto;
    |SI #1#26 = "S";   calcdto = calcdto + pdia_1;  |FINSI; || enf.
    |SI #1#27 = "S";   calcdto = calcdto + pdia_3;  |FINSI; || acc.
    |SI #1#44 = "S";   calcdto = calcdto + pdia_4;  |FINSI; || abs.
    |SI #1#43 = "S";   calcdto = calcdto + pdia_6;  |FINSI; || mat.
        #2d_dto = 0;
|FINSI;
|SI #1#26 = "S";   #2d_dto = #2d_dto + pdia_b;  |FINSI;     || enf.
|SI #1#27 = "S";   #2d_dto = #2d_dto + pdia_d;  |FINSI;     || acc.
|SI #1#44 = "S";   #2d_dto = #2d_dto + pdia_e;  |FINSI;     || abs.
|SI #1#43 = "S";   #2d_dto = #2d_dto + pdia_f;  |FINSI;     || mat.
|SI ha2 = -1;
        calcdto = #2d_dto;
|FINSI;
|FPRC;

|PROCESO normal;   || PROCESO PAGAS NORMALES
|SI #11#2 = #9hasta_p;      || si finaliza el prorrateo
    |SI #9hasta_p = #9confecc;  || si es mes de confeccion
            #2b_dif = 0;          || cierra acumulados
        |SI ha2 = -1;
                #2a_bases = 0;
                #2d_dto = 0;
        |SINO;
                #2a_bases = pdia_a;  || el '#2d_dto' ya viene cargado
        |FINSI;
    |SINO;                        || si no es mes de confeccion
        |SI #9#140 = "N";  |HAZ calpror;  |FINSI;
            #2a_bases = #2a_bases + pdia_0;
            diasefe = #2a_bases - calcdto;
        |SI diasefe < 0;  diasefe = 0;  |FINSI;
            #2b_dif = ( (diasefe*dvalo_209) /perio) *#9dias;
        |SI ha2 = -1;
                #2a_bases = 0;
                #2d_dto =   0;
        |SINO;
                #2a_bases = pdia_a;  || el '#2d_dto' ya viene cargado
        |FINSI;
    |FINSI;
|SINO;                      || si se sigue prorrateando
    |SI swpro = 1;                           || si se ha prorrateado
            #2a_bases = #2a_bases + pdia_a;  || acumula dias
    |FINSI;
    |SI #11#2 = #9confecc;              || si es mes de confeccion
            #2b_dif = 0;                || descarga bases diferidas
    |FINSI;
|FINSI;
|FPRC;

|PROCESO porcen;    || PROCESO PAGAS PORCENTUALES
    t_prorrata = #12#9 * #12#10;
    t_prorrata = t_prorrata ! EURODB;
    #2d_dto =   0;
|SI #11#2 = #9hasta_p;      || si finaliza el prorrateo
    |SI #9hasta_p = #9confecc;  || si es mes de confeccion
            #2b_dif = 0;          || cierra acumulados
            #2a_bases = 0;
    |SINO;                                     || si no es mes de confeccion
            #2b_dif = #2a_bases + t_prorrata;  || carga en bases diferidas
            #2a_bases = 0;
    |FINSI;
|SINO;                      || si se sigue prorrateando
    |SI swpro = 1;                                || si se ha prorrateado
            #2a_bases = #2a_bases + t_prorrata;   || acumula ptas.
    |FINSI;
    |SI #11#2 = #9confecc;                   || si es mes de confeccion
            #2b_dif = 0;                     || descarga bases diferidas
    |FINSI;
|FINSI;
|FPRC;

|PROCESO ulti_dia;  || halla el ultimo dia del mes
    numer = anynum $ 4;
|SI numer = 0;
        bisiesto = 1;
|SINO;
        bisiesto = 0;
|FINSI;
|SI mesnum =  1;  topemes = 31;            |FINSI;
|SI mesnum =  2;  topemes = 28 + bisiesto; |FINSI;
|SI mesnum =  3;  topemes = 31;            |FINSI;
|SI mesnum =  4;  topemes = 30;            |FINSI;
|SI mesnum =  5;  topemes = 31;            |FINSI;
|SI mesnum =  6;  topemes = 30;            |FINSI;
|SI mesnum =  7;  topemes = 31;            |FINSI;
|SI mesnum =  8;  topemes = 31;            |FINSI;
|SI mesnum =  9;  topemes = 30;            |FINSI;
|SI mesnum = 10;  topemes = 31;            |FINSI;
|SI mesnum = 11;  topemes = 30;            |FINSI;
|SI mesnum = 12;  topemes = 31;            |FINSI;
|FPRC;

|PROCESO busca;  || BUSCA SI HAY PRORRATA EN LA PAGA ACTUAL Y VA CONTANDO DIAS
    perio = 0;
    mas = 0;
    rompe = 0;
    swpro = 0;
|SI #9hasta_p = #9desde_p; |Y #9dedia_p > #9hadia_p;  rompe = 1;  |FINSI;
|SI #9hasta_p < #9desde_p; |O rompe = 1;
        nume1 = any_act - 1;
        des1  = #9desde_p;   des2 =         1;
        has1  =        12;   has2 = #9hasta_p;
    |SI #9desde_p [ #11#2;                || si ya ha empezado el ejer. actual
            mas = 1;                      || ejemplo: desde=4, hasta=3, actual=4
    |FINSI;                               || empieza a contar en este a¤o !!!
|SINO;
        nume1 = any_act;
        des1 = #9desde_p;   des2 =  0;
        has1 = #9hasta_p;   has2 = -1;
|FINSI;

    nume1 = nume1 + mas;
    nume2 = any_act + mas;

    des = (nume1 * 100) + #9desde_p;  || mes desde
    has = (nume2 * 100) + #9hasta_p;  || mes hasta
    act = (any_act * 100) + #11#2;      || mes actual

|SI act ] des; |Y act [ has;
        swpro = 1;
|FINSI;

|PARA pp = des1; |SI pp [ has1; |HACIENDO pp = pp + 1;
        anynum = nume1;
    |HAZ suma;
|SIGUE;
|PARA pp = des2; |SI pp [ has2; |HACIENDO pp = pp + 1;
        anynum = nume2;
    |HAZ suma;
|SIGUE;

    mesnum = #9hasta_p;  anynum = nume2;      |HAZ ulti_dia;

|SI #2#42="D";|O #2#42="T";  pepulti=topemes;  |SINO;  pepulti=30;  |FINSI;

|SI #9dedia_p !       0; nume6=#9dedia_p-      1; |SINO;  nume6 = 0; |FINSI;
|SI #9hadia_p ! topemes; nume7=pepulti-#9hadia_p; |SINO;  nume7 = 0; |FINSI;

    perio = perio - (nume6 + nume7);

|FPRC;

|PROCESO suma;
|SI #2#42 = "D";|O #2#42 = "T";
        mesnum = pp;
    |HAZ ulti_dia;
|SINO;
        topemes = 30;        || si es mensual cuenta por 30 los meses
|FINSI;
    perio = perio + topemes;
|FPRC;

|PROCESO calpror;
    dvalo_209 = 0;                  || inicializa acumulador
    paga      = #12#4 - 200;        || numero de paga
|HAZ buclet;
    dvalo_209 = dvalo_209 ! EURODB; ||estaba redon. a 2, cambio por eurodb
|FPRC;

|PROCESO buclet;
    concep1 = 51 + paga;      || calcula el 1er. campo de conceptos
    concep2 = concep1 + 32;   || calcula el ultimo campo de concep.
|PARA campo = concep1; |SI campo [ concep2; |HACIENDO campo=campo+8;
    |SI #9campo ! 0;
            desc = #9campo;  hasc = #9campo;
        |SI #9campo = 100;  desc = 101;      hasc = 199;     |FINSI;
        |SI #9campo = 400;  desc = 401;      hasc = 420;     |FINSI;
        |PARA actc = desc; |SI actc [ hasc; |HACIENDO actc=actc+1;
                concepto = actc;    || bucle para los 100 y 400
            |HAZ leeconcep;         || lee y acumula conceptos paga
        |SIGUE;
    |FINSI;
|SIGUE;
|FPRC;

|PROCESO leeconcep;  || lee y acumula concepto para paga
    #12#4 = concepto;
|LEE 000#12=;
|SI FSalida = 0;
        duni1_999 = #12#09;
        duni2_999 = #12#11;
        dvalo_999 = #12#10;
    |SI #12#05 = "F"; |Y duni1_999 = 0; || si es calculo 'F' carga las unidades
            duni1_999 = 1;             || con la unidad !!!
    |FINSI;
    |HAZ cal_valo;
|FINSI;
|FPRC;

|PROCESO cal_valo;   || CALCULA EL VALOR ACUMULADO DE LOS 5 CONCEPTOS POR PAGA
    product = duni1_999 * dvalo_999;
    product = product ! EURODB;
|SI #9tipos = "N";   || si es tipo 'N' o 'P'
    |SI #12#05="D";                divide=dia_2;     |FINSI; || dias mes
    |SI #12#05="M";                divide=dia_4;     |FINSI; || dias cotiz.
    |SI #12#05="L";|O #12#05="K";  divide=duni1_999; |FINSI; || dias lab./fes.
    |SI #12#05="V";|O #12#05="F";|O #12#05="A";  divide=   30; |FINSI; ||fijos
    |SI es_irregul = 1;|Y tpz ! 0;
        |DDEFECTO #40;
        |ABRE #40;                 || si se carga con los dias de alta
            #40#0 = #2#87;         ||/se divide por los mismos
            #40#1 = #12#4;
        |LEE 000#40=;
        |SI #40#3 = "S";     divide = tpz;  |FINSI;
        |CIERRA #40;
    |FINSI;
|SINO;
        divide = 100;
|FINSI;
    dvalo_209 = dvalo_209 + (product / divide);  || entran todos los conceptos
|FPRC;

|PROCESO activasino;
     swActiva = 1;
     |ABRE #1;
     #1#0 = #2#0;
     |LEE 000#1=;
     |SI #1#50 = "S";
          swActiva = 0;  || empresa no activa
     |FINSI;
     |CIERRA #1;
|FPRC;
