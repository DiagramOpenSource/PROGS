|FICHEROS;
     nomajusc #0;
     labtraba;
     nomajusl #100,MANTE;
     labempre;
|FIN;

|VARIABLES;
     fFechaNac = @;
     nFechaNac = 0;
     aFechaNac = "";
     fFechaAnt = @;
     nFechaAnt = 0;
     aFechaAnt = "";
     aAnyoNac = "";
     nAnyoNac = 0;
     aMesNac = "";
     nMesNac = 0;
     aAnyoAnt = "";
     nAnyoAnt = 0;
     aMesAnt = "";
     nMesAnt = 0;
     nMesRef = 0;
     nAnyoRef = 0;

     nAnyos = 0;
     nMeses = 0;
     nEdad = 0;

     swGraba = 0;
     {-1}menu = "";
     menu1 = "2400";
     menu2 = "1";
     menu3 = "Elegir Opcion:";
     menu4 = "RAC";
     menu5 = "Revisar";
     menu6 = "Actualizar";
     menu7 = "Cancelar";
     aAlfa1 = "";
     nOpcion = 0;
     aDesdeEstado = "";
     aHastaEstado = "";
|FIN;


|PROCESO graba;
     |DDEFECTO #100;
     #100#0 = #labtraba#0;    || Codigo empresa
     #100#1 = #labtraba#1;    || Codigo trabajador
     #100#2 = #labtraba#2;    || nombre trabajador
     #100#3 = #labtraba#61;   || ajuste negativo S/N
     #100#4 = #labtraba#97;   || tipo ajuste
     #100#5 = #labtraba#98;   || importe
     |GRABA 020#100n;
|FPRC;

|DEFBUCLE labtraba_2;
     #labtraba#3;
     0, 1, 97, 44;
     " ", #nomajusc#0, 00001, #nomajusc#2, aDesdeEstado;
     "z", #nomajusc#0, 99999, #nomajusc#3, aHastaEstado;
     ;
     NULL, graba;
|FIN;

|DEFBUCLE labtraba;
     #labtraba#1;
     97, 44;
     #nomajusc#0, 00001, #nomajusc#2, aDesdeEstado;
     #nomajusc#0, 99999, #nomajusc#3, aHastaEstado;
     ;
     NULL, graba;
|FIN;

|PROCESO actualiza2;
     #labtraba#0 = #nomajusl#0;
     #labtraba#1 = #nomajusl#1;
     |LEE 101#labtraba.=
     |SI FSalida = 0;
          #labtraba#61 = #100#3;   || ajuste negativo S/N
          #labtraba#97 = #100#4;   || tipo ajuste
          #labtraba#98 = #100#5;   || importe
          #labtraba#246 = #100#6;   || ajuste anticipos S/N
          |GRABA 020#labtraba.a;
     |SINO;
          aAlfa1 = "0000Trabajador [" + #labtraba#0 + "." + #labtraba#1 + "] inaccesible.";
          |MENAV aAlfa1;
     |FINSI;
     |LIBERA #labtraba;
|FPRC;

|DEFBUCLE actualiza_labtraba;
     #nomajusl#1;
     ;
     00001,00001;
     99999,99999;
     ;
     NULL,actualiza2;
|FIN;

|PROCESO actualiza;
     |MENAV "0000Se actualizara la ficha del trabajador para los trabajadores listados";
     |ABRE #labtraba;
     |BUCLE actualiza_labtraba;
     |CIERRA #labtraba;
|FPRC;

|PROCESO imprime_linea;
     |PRINT;
|FPRC;

|DEFBUCLE impresion_2;
     #nomajusl#2;
     ;
     INICIO;
     FINAL;
     ;
     NULL,imprime_linea;
|FIN;

|DEFBUCLE impresion;
     #nomajusl#1;
     ;
     00001,00001;
     99999,99999;
     ;
     NULL,imprime_linea;
|FIN;

|PROCESO imprime;
     |IMPRE 0;
     |SI FSalida = 0;
          |INFOR "nomajusc";
          |SI #0#5 = 1;
               |BUCLE impresion;
          |SINO;
               |BUCLE impresion_2;
          |FINSI;
          |PRINT;
          |PIEINF;
          |FININF;
     |SINO;
          |MENAV "0000Proceso abortado.";
     |FINSI;
     |FINIMP;
|FPRC;

|PROCESO min_2;
     #nomajusl#2 = " ";
|FPRC;

|PROCESO max_2;
     #nomajusl#2 = "z";
|FPRC;

|PROCESO min;
     #nomajusl#0 = 1;
     #nomajusl#1 = 1;
|FPRC;

|PROCESO max;
     #nomajusl#0 = 99999;
     #nomajusl#1 = 99999;
|FPRC;

|PROCESO Tipo3; |TIPO 3;
     aAlfa1 = Usuario;  |QBF aAlfa1;    aAlfa1 = "b" + aAlfa1;
     |NOME_DAT #100 aAlfa1;
     |DELINDEX #100;
     |ABRE #100;
     |BUCLE labtraba;
     |CIERRA #100;

     |PARA; |SI; |HACIENDO;
          |SI #0#5 = 1;
               |ENTLINEAL #1#nomajusl,2,2,22,1,min,max;
          |SINO;
               |ENTLINEAL #2#nomajusl,1,2,22,1,min_2,max_2;
          |FINSI;
          |MENU menu;
          nOpcion = FSalida;
          |SI nOpcion = 2;     |HAZ actualiza;     |SAL;|FINSI;
          |SI nOpcion = 3;     |SAL;                    |FINSI;
          ||SI nOpcion = 4;     |HAZ imprime;            |FINSI;
          menu2 = nOpcion;
     |SIGUE;
|FPRC;

|PROCESO ayuda3; |TIPO 7;
     |SI FSalida = 5;|O FSalida = 4;    |ACABA;   |FINSI;

     |PUSHV 05171256;
     |CUADRO 11 22 12 56;
     |ATRI R;
     ||            7890123456789012345678901234567890123456
     |PINTA 0517, " -----  TIPOS DE AJUSTE POSIBLES ------ ";
     |PINTA 0617, " (0) Sin ajuste                         ";
     |PINTA 0717, " (1) A brutos   (no cotiza -407-)       ";
     |PINTA 0817, " (2) A brutos   (cotiza -120-)          ";
     |PINTA 0917, " (3) A liquidos (no cotiza -407-)       ";
     |PINTA 1017, " (4) A liquidos (cotiza -120-)          ";
     |PINTA 1117, " (5) A brutos   (no cot./no IRPF -411-) ";
     |PINTA 1217, " (6) A liquidos (no cot./no IRPF -411-) ";
     |ATRI 0;
|FPRC;

|PROCESO solo_liquido; |TIPO 7;
     |SI #100#4 ! 3;|Y #100#4 ! 4;|Y #100#4 ! 6;
          #100#6 = "N";
          |PINTA #100#6;
          |CAMPO_MODIFICA #100#6, 1, "N";
     |SINO;
          |CAMPO_MODIFICA #100#6, 1, "S";
     |FINSI;
|FPRC;

|PROCESO no_ajuste; |TIPO 0;
     |SI #100#4 = 0;
          aAlfa1 = "N";
     |SINO;
          aAlfa1 = "S";
     |FINSI;
     |CAMPO_MODIFICA #100#3, 1, aAlfa1;
     #100#5 = 0;
     |CAMPO_MODIFICA #100#5, 1, aAlfa1;
     |POPV;
|FPRC;

|PROCESO Estado;
     |SI #0#4 = "S";
          aDesdeEstado = "A";
          aHastaEstado = "Z";
     |SINO;
          aDesdeEstado = "A";
          aHastaEstado = "A";
     |FINSI;
|FPRC;
