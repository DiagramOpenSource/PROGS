|FICHEROS;
     labconfo #0;
     labempre;
     labtraba;
     labregis;
     labacue2;

     labconf1;
     labconf2;

     labcontr;
     labnct10;
|FIN;

|VARIABLES;
     aFechaDesde = "";
     aFechaHasta = "";
     aFechaAlta = "";

     fFechaDesde = @;
     fFechaHasta = @;
     fFechaAlta = @;

     &nContratos = 0;
     &aEmpresa = "";
     &aAsesoria = "";

     swImpresion = 0;
     aPassword = "";
     &nAcuNet = 0;
     &aNetContrata = "";
     aParam = "";

     fFecha = @;
     nMesesAtras = 0;
     aDia = "";
     nDia = 0;
     nResta = 0;
     swNoSigas = 0;
     aFecha = "";
     nFecha = 0;
     aMes = "";
     aAny = "";
     nMes = 0;
     nAny = 0;
     nCuantosAnys = 0;
     nCuantosMeses = 0;
     nResultados = 0;

     aIPDsCorreo = "";
     aSMTP = "";
     aDirOrigen = "";
     eaDirDest = "";
     eaAsunto = "";
     eaTexto = "";
     eaFichero = "";

     nCampo = 0;
     aAlfa = "";
     aDestino = "";
     aIP = "";
     &aCliente = "";
|FIN;

|PROCESO ElPRINT;
     |SI nResultados = 0;
          |SI aParam = "AUTO";
               Impresora = "CrystalReport;C:\DOCUMENTOS\informe.pdf"
          |SINO;
               Impresora = "CrystalReport";
          |FINSI;
          |IMPRE -1

          |INFOR "labconfo";

          |PRINT;
     |SINO;
          |PRINT;
     |FINSI;
     nResultados = nResultados + 1;
|FPRC;

|PROCESO labregis;
     aFechaDesde = #0#4;
     aFechaHasta = #0#5;

     aFechaAlta = #labregis#7;

     fFechaDesde = aFechaDesde;
     fFechaHasta = aFechaHasta;
     fFechaAlta = aFechaAlta;

     |SI fFechaDesde [ fFechaAlta; |Y fFechaAlta [ fFechaHasta;
          |SI swImpresion = 0;
               nContratos = nContratos + 1;

               |DDEFECTO #labacue2;
               #labacue2#0 = #labregis#1;
               #labacue2#1 = #labregis#2;
               |LEE 000#labacue2.=;
               |SI FSalida = 0;
                    aAlfa = #labacue2#23;
                    |QBF aAlfa;
                    |SI aAlfa = "1820126"; |O aAlfa = "1800020126";
                         nAcuNet = nAcuNet + 1;
                    |FINSI;
               |FINSI;
          |FINSI;

          |SI swImpresion = 1;
               |DDEFECTO #labempre;
               #labempre#0 = #labregis#1;
               |LEE 000#labempre.=;

               |DDEFECTO #labtraba;
               #labtraba#0 = #labregis#1;
               #labtraba#1 = #labregis#2;
               |LEE 000#labtraba.=;

               aEmpresa = "";
               ||SI Impresora = "CrystalReport";
                    aEmpresa = #labempre#0; |QBF aEmpresa; aEmpresa = ("00000" + aEmpresa) % -105;
                    aEmpresa = aEmpresa + " - " + #labempre#2;
               ||FINSI;

               |DDEFECTO #labacue2;
               aNetContrata = "NO";
               #labacue2#0 = #labtraba#0;
               #labacue2#1 = #labtraba#1;
               |LEE 000#labacue2.=;
               |SI FSalida = 0;
                    aAlfa = #labacue2#23; |QBF aAlfa;
                    |SI aAlfa = "1820126"; |O aAlfa = "1800020126";
                         aNetContrata = "SI";
                    |FINSI;
               |FINSI;

               |HAZ ElPRINT;
         |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE labregis;
     #labregis#1;
     1, 2, 4;
     000001, #0#0, #0#2, "421";
     999999, #0#1, #0#3, "421";
     ;
     NULL, labregis;
|FIN;

|PROCESO Parametros;
     #0#0 = 00001;
     #0#1 = 99999;
     #0#2 = 00001;
     #0#3 = 99999;

     fFecha = ~;
     #0#5 = fFecha;

     |SI #labconf1#3 = 00;
          |ABRE #labconf2;
          |LEE 000#labconf2.u;
          |SI FSalida = 0;
               #0#4 = #labconf2#0;
          |SINO;
               #0#4 = "01.01.2000";
          |FINSI;
          |CIERRA #labconf2;
     |SINO;
          nMesesAtras = #labconf1#3;
          aFecha = fFecha;
          aMes = aFecha % 402;
          aAny = aFecha % -104;
          nMes = aMes;
          nAny = aAny;
          nCuantosAnys = ((nMesesAtras / 12) - 0.49) ! 0;
          nAny = nAny - nCuantosAnys;
          nCuantosMeses = nMesesAtras - (nCuantosAnys * 12);
          nMes = nMes - nCuantosMeses;
          |SI nMes < 0;
               nAny = nAny - 1
               nMes = 12 + nMes;
          |FINSI;
          aMes = nMes;
          aMes = ("00" + aMes) % -102;
          aAny = nAny;
          aFecha = "01." + aMes + "." + aAny;
          fFecha = aFecha;
          #0#4 = fFecha;
     |FINSI;
|FPRC;

|PROCESO Correo;
     |DBASE aDestino;
     aDestino = aDestino + "tmp";
     |MKDIR aDestino;
     aDestino = aDestino + "/informe.pdf";
     eaFichero = "C:\DOCUMENTOS\informe.pdf";

     |IP_REMOTO aIP;
     |SI aIP ! "";
          |RECIBE_FICHERO eaFichero, aDestino;
     |SINO;
          |COPIA_FICHERO eaFichero, aDestino;
     |FINSI;

     |RFBORRA eaFichero;

     aIPDsCorreo = "217.13.92.143";
     aSMTP = "asmtp.diagram.es";
     aDirOrigen = "T7OKBke4VZuT8b:laboral@avisos.diagram.es:laboral@avisos.diagram.es";
     eaFichero = aDestino;

     aAlfa = #labconf1#4; |QBF aAlfa;
     |SI aAlfa ! "";
          eaDirDest = eaDirDest + aAlfa;
     |FINSI;
     |PARA nCampo = 5; |SI nCampo [ 8; |HACIENDO nCampo = nCampo + 1;
          aAlfa = #labconf1#nCampo#; |QBF aAlfa;
          |SI aAlfa ! ""; aAlfa = ";" + aAlfa; |FINSI;
          eaDirDest = eaDirDest + aAlfa;
     |SIGUE;

     eaAsunto = "Estado del servidor - " + aCliente;
     eaTexto = "Estado Servidor - " + aCliente;

     |DSCORREO_ENVIA aIPDsCorreo, aSMTP, aDirOrigen, eaDirDest, eaAsunto, eaTexto, eaFichero;

     |FBORRA eaFichero;
|FPRC;

|PROCESO Comprobacion;
     |SI #labconf1#1 = "N";
          swNoSigas = 1;
     |FINSI;

     fFecha = ~;
     aFecha = fFecha;
     aDia = aFecha % 102;
     nDia = aDia;
     |SI nDia ! #labconf1#2;
          swNoSigas = 1;
     |FINSI;

     |ABRE #labconf2;
     |LEE 000#labconf2.u;
     |SI FSalida = 0;
          fFecha = ~;
          |SI fFecha [ #labconf2#0;
               swNoSigas = 1;
          |FINSI;
     |FINSI;
     |CIERRA #labconf2;

     |ABRE #labnct10;
     |SELECT #2#labnct10;
     |LEE 000#labnct10.u;
     |SI FSalida = 0;
          |SI #labnct10#4 [ #0#4; |O #labnct10#4 ] #0#5;
               swNoSigas = 1;
          |FINSI;
     |SINO;
          swNoSigas = 1;
     |FINSI;
     |CIERRA #labnct10;
|FPRC;

|PROGRAMA;
     aParam = PARAMETRO;
     |QBF aParam;

     eaFichero = "C:\DOCUMENTOS\informe.pdf";
     |RFBORRA eaFichero;

     |SI aParam = "AUTO";
          |ABRE #labconf1;
          |ABRE #labconf2;

          |LEE 000#labconf1.=;
          |SI FSalida = 0;
               swNoSigas = 0;

               |HAZ Parametros;

               |HAZ Comprobacion;

               |SI swNoSigas = 1;
                    |CIERRA #labconf2;
                    |CIERRA #labconf1;

                    |ACABA;
               |FINSI;
          |SINO;
               |CIERRA #labconf1;
               |CIERRA #labconf2;
               |ACABA;
          |FINSI;

          |CIERRA #labconf1;
          |CIERRA #labconf2;

          FSalida = 0;
     |SINO;
          |CLS;
          |CUADRO 1218 1565;
          |PINTA 1320, "Introduzca contrase¤a:";
          E_inf = "";
          E_sup = "20";
          aPassword = 1342 ? -1;
          |SI aPassword ! "<MYPASWD>            ";
               |MENAV "     Permiso denegado.";
               |ACABA;
          |FINSI;

          |PINPA #0#0;
          |PINDA #0#0;
          |ENDATOS #1#0;
     |FINSI;
     |SI FSalida = 0;
          |ABRE #labempre;
          |ABRE #labtraba;
          |ABRE #labacue2;
          |ABRE #labcontr; |LEE 000#labcontr.p; |CIERRA #labcontr;

          aCliente = #labcontr#68;
          |QBF aCliente;
          |SI aCliente = "";
               |DBASS aCliente;
               |QBF aCliente;
          |FINSI;

          swImpresion = 0;
          |BUCLE labregis;

          swImpresion = 1;
          |BUCLE labregis;

          |CIERRA #labacue2;
          |CIERRA #labtraba;
          |CIERRA #labempre;

          fFecha = ~;
          |ABRE #labconf2;
          #labconf2#0 = fFecha;
          |LEE 101#labconf2.=;
          |SI FSalida ! 0;
               #labconf2#0 = fFecha;
               |GRABA 020#labconf2.n;
          |FINSI;
          |CIERRA #labconf2;

          |SI aParam ! "AUTO";
               |SI nContratos = 0;
                    |MENAV "    No se ha encontrato ningún contrato de formación en las fechas solicitadas";
               |FINSI;
          |FINSI;

          |SI nResultados ! 0;
               |PIEINF;
               |FININF;
               |FINIMP;

               || FALTA ENVIARLO POR CORREO Y LUEGO BORRARLO

               |SI aParam = "AUTO";
                    |HAZ Correo;
               |FINSI;
          |FINSI;
     |FINSI;
|FPRO;
