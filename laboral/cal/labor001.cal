|FICHEROS;
     labor001 #0;
     labor008,MANTE;
     labempre;
     labtraba #500;

     :/basica/def/dsficher #800;

     deforige@ #150;
     defdesti@ #151;
     gecom019@ #100;
     gecom011@ #101;
     gecom014@ #104;

     cargadef@  #600;
     &afichdes@ #900;
     &refesql@  #901;
|FIN;

|VARIABLES;
     &eaDirDest  = "";
     &eaAsunto   = "";
     &eaTexto    = "";
     &eaFichero  = "";

     aAlfa = "";
     aAlfa1 = "";
     aAlfa2 = "";
     aEmpresa = "";
     aRutaApl = "";
     aRutaDef = "";
     &aRutaFich = "";
     &aRutaFichEmp = "";
     aFichero = "";
     nCampo = 0;
     nNroCampos = 0;
     aTablaTemporal = "";
     aOrigen = "";
     aDestino = "";
     aGrupo = "";
     nGrupo = 0;
     FEstado = 0;
     nEmpresa = 0;
     nCamposClave = 0;
     aMensa = "";
     nModo = 0;

     nOpcion = 0;
     {-1}menu = "";
     menu1 = "2400";
     menu2 = "1";
     menu3 = "Opciones:";
     menu4 = "ARC";
     menu5 = "Aceptar seleccion";
     menu6 = "Revisar seleccion";
     menu7 = "Salir al menu";
     aHora = "";

     aFichSql = "";
     aDef = "";
     aSelect = "";
     aFrom = "";
     aInto = "";
     aWhere = "";
     nCampoCli = 0;
     nCampoDef = 0;
     &eaEjec = "";
     aBorrar = "";
     nTCampo = 0;
     aQueQuiero = "";
     aCampo = "";
     aAplica = "";
     aDDef = "";
     aHDef = "";
     aRutaFichBas = "";
     aRestauraIni = "";
     aDsini1      = "";
     aDsini2      = "";

     nFichero = 0;
     nFicheros = 0;
     &aParam = "";
     swSigue = 0;
     &nEmpExt = 0;
     nDesdeEmpresa = 0;
     nHastaEmpresa = 0;
     &eaVengoDe = "";
     aParam2 = "";
     nHandle = 0;
     nSize = 0;
     swExpress = 0;
     swOmite = 0;
|FIN;

|PROCESO Aviso;
     |SI aParam2 = "AUTO";
          |ACABA;
     |FINSI;
     |SI aParam = "NORMAL";
          |PUSHV 11201760;
          |BLANCO 11201760;
          |CUADRO 11201760;
          ||            2       3         4         5
          ||            2345678901234567890123456789012345678
          |PINTA 1222, "            - ATENCION -             ";
          |PINTA 1322, "Este proceso sincroniza  los datos de";
          |PINTA 1422, "las empresas  seleccionadas  desde la";
          |PINTA 1522, "la carpeta general (fich) a la carpe-";
          |PINTA 1622, "      ta propia de cada empresa.     ";
     |FINSI;
     |SI aParam = "PATRONES";
          |PUSHV 11201762;
          |BLANCO 11201762;
          |CUADRO 11201762;
          ||            2       3         4         5
          ||            234567890123456789012345678901234567890
          |PINTA 1222, "             - ATENCION -              ";
          |PINTA 1322, "Este proceso actualiza los datos de los";
          |PINTA 1422, "los ficheros que no dependen del codigo";
          |PINTA 1522, "de empresa  desde  la  carpeta  general";
          |PINTA 1622, "  a la carpeta propia de cada empresa. ";
     |FINSI;
     |SI aParam = "INVERSA";
          |PUSHV 11201762;
          |BLANCO 11201762;
          |CUADRO 11201762;
          ||            2       3         4         5
          ||            234567890123456789012345678901234567890
          |PINTA 1222, "             - ATENCION -              ";
          |PINTA 1322, "Este proceso actualiza los datos de las";
          |PINTA 1422, "  empresas seleccionadas a la carpeta  ";
          |PINTA 1522, "           general de datos.           ";
          |PINTA 1622, "                                       ";
     |FINSI;
     |PAUSA;
     |POPV;
|FPRC;

|PROCESO Mensa; |TIPO 7;
     ||MENSA "    Pulse < F5 > para activar o desactivar la copia de la empresa seleccionada";
     |MENSA "    < F5 > activa o desactiva copia de la empresa seleccionada; < F6 > Rapida";
|FPRC;

|PROCESO Baja8;
     #labor008#0 = #labor001#1;
     #labor008#1 = "";
|FPRC;

|PROCESO Alta8;
     #labor008#0 = #labor001#1;
     #labor008#1 = "z" * 10;
|FPRC;


|PROCESO Activa; |TIPO 0;
     |SI FSalida = 10;
         |PUSHV 0501 2380;
         |BLANCO 0501 2380;

         |PINPA #0#labor008;
         |PINTA 0502, #labor001#1;
         |PINTA 0508, #labor001#2;

         |ENTLINEAL #1#labor008, -2, 1, 21, 0, Baja8, Alta8;

         |POPV;
         |ACABA;
     |FINSI;

     |SI FSalida = 13;
          |SI #0#3 = "S";
               #0#3 = "N";
               |GRABA 020#0a;
          |SINO;
               #0#3 = "S";
               |GRABA 020#0a;
          |FINSI;
          |LIBERA #0;
          |PONCONTROL 23, "si";
     |SINO;
          nModo = (FEntrada / 100) ! 0;
          |SI nModo = 1;
               |ERROR;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO CargaRutas;
     aEmpresa = #0#4;
     |QBF aEmpresa;
     aEmpresa = ("00000" + aEmpresa) % -105;

     |DBASE aRutaDef;
     |QBF aRutaDef;
     aRutaDef = aRutaDef + "def/";      || ruta def actual, para CARGA_DEF

     aRutaDef = aRutaDef + aFichero;    || con fichero, para CARGA_DEF

     |DFICE aRutaFich;                  || ruta fich actual

     aRutaFichEmp = aRutaFich + aEmpresa + "/"; || ruta fich empresa, para PATH_DAT
|FPRC;
|| xxxxxxxxxxx

|PROCESO BorradoPrevio;
     aFichSql = ("sql" + Usuario + "zzzzzzzz") % 108;
     aOrigen  = aDef;
     aDestino = aRutaDef + "dsborsql.mas";
     |COPIA_FICHERO aOrigen, aDestino;

     |CARGA_DEF aDestino, refesql@;
     |SI FSalida = 0;
          |PATH_DAT #901 aRutaFichEmp;       || origen: el "por empresa"
          |NOME_DAT #901 aFichSql;           || que es el que quiero borrar

          aSelect = "SELECT * ";
          aFrom   = "FROM "  + "dsborsql ";
          aInto   = "INTO "  + aFichSql + ".def ";
          aWhere  = "WHERE " + nCampoCli + " == " + #0#1;
          eaEjec  =  aSelect + aFrom + aInto + aWhere;
          |SUB_EJECUTA_NP "labor002;BORRADO";

          |PATH_DAT #901 aRutaFich;     || origen: esta en el fich
          |ABRET #900;
          |ABRET #901;
          |LEE 000#901p;
          |PARA;  |SI FSalida = 0;  |HACIENDO;
               |PARA nCampoDef = 0;  |SI nCampoDef [ nTCampo;  |HACIENDO nCampoDef = nCampoDef + 1;
                    #900nCampoDef = #901nCampoDef;
               |SIGUE;
               #900nCampoCli = #0#1;

               |LEE 000#900=;
               |SI FSalida = 0;
                    |BORRA 020#900a;
               |FINSI;

               |LIBERA #900;

               |LEE 000#901s;
          |SIGUE;
          |CIERRAT #900;
          |CIERRAT #901;

          |DELINDEX #901;

          |DESCARGA_DEF #refesql@;
     |FINSI;

     || aDestino = aPathDefSQL + "dsborsql.mas";
     || aBorrar  = aPathDefSQL + aFichSql  + ".mas";  |FBORRA aBorrar;
     aBorrar  = aRutaDef + aFichSql  + ".mas";  |FBORRA aBorrar;
     || aBorrar  = aPathDefSQL + "dsborsql.mas";      |FBORRA aBorrar;
     aBorrar  = aRutaDef + "dsborsql.mas";      |FBORRA aBorrar;

     || aOrigen  = aPathDefSQL + "dsborsq1.mas";
     || aDestino = aPathDefSQL + "dsborsql.mas";
     aOrigen  = aRutaDef + "dsborsq1.mas";
     aDestino = aRutaDef + "dsborsql.mas";

     |COPIA_FICHERO aOrigen, aDestino;
|FPRC;

|PROCESO PorSQL;
     |HAZ BorradoPrevio;

     aFichSql = ("sql" + Usuario + "zzzzzzzz") % 108;
     aOrigen  = aDef;
     || aDestino = aPathDefSQL + "dsborsql.mas";
     aDestino = aRutaDef + "dsborsql.mas";
     |COPIA_FICHERO aOrigen, aDestino;

     || aPathFich = aPathFicOrigen;

     |CARGA_DEF aDestino, refesql@;
     |SI FSalida = 0;
          |SI aParam = "INVERSA";
               |PATH_DAT #901 aRutaFichEmp;       || origen: el "por empresa"
          |SINO;
               |PATH_DAT #901 aRutaFich;          || origen: el "general"
          |FINSI;
          |NOME_DAT #901 aFichSql;


         aSelect = "SELECT * ";
         aFrom   = "FROM "  + "dsborsql ";
         aInto   = "INTO "  + aFichSql + ".def ";
         aWhere  = "WHERE " + nCampoCli + " == " + #0#1;
         eaEjec  =  aSelect + aFrom + aInto + aWhere;
         |SUB_EJECUTA_NP "labor002;ACHILIPUM";

|| xxxxxxxxxxxx

         |PATH_DAT #901 aRutaFich;          || origen: el "general"
         |ABRET #900;
         |ABRET #901;
         |LEE 000#901p;
         |PARA;  |SI FSalida = 0;  |HACIENDO;
              |PARA nCampoDef = 0;  |SI nCampoDef [ nTCampo;  |HACIENDO nCampoDef = nCampoDef + 1;
                    #900nCampoDef = #901nCampoDef;
              |SIGUE;
              |GRABA 020#900n;
              |LIBERA #900;

              |LEE 000#901s;
          |SIGUE;
          |CIERRAT #900;
          |CIERRAT #901;

          |DELINDEX #901;

          |DESCARGA_DEF #refesql@;
     |FINSI;

     aBorrar  = aRutaDef + aFichSql  + ".mas";  |FBORRA aBorrar;
     aBorrar  = aRutaDef + "dsborsql.mas";      |FBORRA aBorrar;

     aOrigen  = aRutaDef + "dsborsq1.mas";
     aDestino = aRutaDef + "dsborsql.mas";

     |COPIA_FICHERO aOrigen, aDestino;

     |PULSATECLA;
|FPRC;

|PROCESO TrataFichero;
     || aDef = aPathDefFichero + aFichero;
     aAlfa = aFichero % 108;

     aDef = aRutaDef + aFichero;
     |CARGA_DEF aDef, afichdes@;
     |SI FSalida ! 0;
         |ACABA;
     |FINSI;

     aQueQuiero = "|NC";
     aCampo     = #900#aQueQuiero#;
     nTCampo    = aCampo;
     nTCampo    = nTCampo - 1;

     ||PATH_DAT #900 aPathFicDestino;
     |SI aParam = "INVERSA";
          |PATH_DAT #900 aRutaFich;          || destino: el "general"
     |SINO;
          |PATH_DAT #900 aRutaFichEmp;       || destino: el "por empresa"
     |FINSI;
     |HAZ PorSQL;

     |DESCARGA_DEF #afichdes@;
|FPRC;

|PROCESO PintaProgreso;
     nFichero = nFichero + 1;
     |SI aParam2 = "AUTO";
          |ACABA;
     |FINSI;
     aAlfa1 = nFichero;
     aAlfa2 = nFicheros;

     aAlfa = #800#1;
     |PINTA 1540, aAlfa;
     aAlfa = "             ";
     |PINTA 1640, aAlfa;

     |SI aParam ! "PATRONES"; |Y aParam ! "INVERSA";
          aAlfa = aAlfa1 + " de " + aAlfa2;
          |PINTA 1640, aAlfa;
     |FINSI;
|FPRC;

|PROCESO ProcedeAlFichero;
     aFichero    = #800#1;   |QBF aFichero;
     nCampoCli   = #800#4;

     swOmite = 0;

     aAlfa = aFichero % 108;

     |SI aAlfa = "labnct10"; |O aAlfa = "nomatcon"; |O aAlfa = "nomcal03";
     |O aAlfa = "nomcal04"; |O aAlfa = "nomcaldc"; |O aAlfa = "nomcaldl";
     |O aAlfa = "nomgirca"; |O aAlfa = "nomgirli"; |O aAlfa = "nomgirhi";
     |O aAlfa = "nomir012"; |O aAlfa = "nomir013"; |O aAlfa = "nomir015";
     |O aAlfa = "nomlogca"; |O aAlfa = "nomlogli"; |O aAlfa = "nomcalca";
     |O aAlfa = "nomcalli"; |O aAlfa = "nomhicca"; |O aAlfa = "nomhicli";
          swOmite = 1;
     |FINSI;

     |SI swOmite = 0;
          |HAZ TrataFichero;

          |HAZ PintaProgreso;

          |SI #800#27 = "S";
              nCampoCli   = #800#28;
              |HAZ TrataFichero;
          |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE dsficher;
     #800#1;
     ;
     aAplica, aDDef;
     aAplica, aHDef;
     ;
     NULL, ProcedeAlFichero;
|FIN;

|PROCESO ConEmpresa;
     || aPathDefFichero = aPathDestino + "laboral/def/";
     || aPathFicOrigen  = aPathOrigen  + "laboral/fich/";
     || aPathFicDestino = aPathDestino + "laboral/fich/";

     aAplica = "laboral";

     aDDef = "";
     aHDef = "zzzzzzzzzzzz";

     |BUCLE dsficher;
|FPRC;

|PROCESO Purga;
     aAlfa = aFichero % 101;
     |SI aAlfa = "."; |O aAlfa = "0"; |O aAlfa = "1"; |O aAlfa = "2"; |O aAlfa = "3";
     |O aAlfa = "4"; |O aAlfa = "5"; |O aAlfa = "6"; |O aAlfa = "7"; |O aAlfa = "8";
     |O aAlfa = "9"; |O aAlfa = "E"; |O aAlfa = "T"; |O aAlfa = "_";
     |O aAlfa = "U"; |O aAlfa = "X";
          swSigue = 0;
     |FINSI;

     aAlfa = aFichero % 102;
     |SI aAlfa = "fo"; |O aAlfa = "f0"; |O aAlfa = "f1"; |O aAlfa = "fe";
     |O aAlfa = "g0"; |O aAlfa = "h1"; |O aAlfa = "i1";
     |O aAlfa = "l1"; |O aAlfa = "li";
     |O aAlfa = "n0"; |O aAlfa = "n1"; |O aAlfa = "nh"; |O aAlfa = "rr";
     |O aAlfa = "s0"; |O aAlfa = "s1"; |O aAlfa = "t0"; |O aAlfa = "t1";
     |O aAlfa = "tc"; |O aAlfa = "tl"; |O aAlfa = "tm"; |O aAlfa = "u0";
     |O aAlfa = "u1"; |O aAlfa = "x0"; |O aAlfa = "x1"; |O aAlfa = "y0";
     |O aAlfa = "y1"; |O aAlfa = "tf";
     |O aAlfa = "b0"; |O aAlfa = "b1";
     |O aAlfa = "c0"; |O aAlfa = "c1"; |O aAlfa = "c3"; |O aAlfa = "c4";
     |O aAlfa = "d0"; |O aAlfa = "d1"; |O aAlfa = "e0"; |O aAlfa = "e1";
     |O aAlfa = "m0"; |O aAlfa = "m1";
     |O aAlfa = "w0"; |O aAlfa = "w1"; |O aAlfa = "w3";
     |O aAlfa = "EP"; |O aAlfa = "ep";
          swSigue = 0;
     |FINSI;

     aAlfa = aFichero % 106;
     |SI aAlfa = "nomgir";    || gestor de incidencias IRPF
          |ACABA;             || no necesario y tarda una barbaridad
     |FINSI;
     |SI aFichero = "silcon02"; |O aFichero = "silcon03";
     |O aFichero = "pdfcontr";
          swSigue = 0;
     |FINSI;

     |SI aFichero = "labcontr";
          |DBASS aAlfa; |QBF aAlfa;
          aAlfa1 = #0#4; |QBF aAlfa1; aAlfa1 = ("00000" + aAlfa1) % -105;
          aAlfa = aAlfa + "laboral/fich/" + aAlfa1 + "/" + aFichero + ".dat";
          |XABRE "B", aAlfa, nHandle;
          |SI FSalida = 0;
               |XPOSICION nHandle, nSize, 2;
               |SI nSize ! 0;
                    swSigue = 0;
               |FINSI;
          |FINSI;
          |XCIERRA nHandle;
     |FINSI;
|FPRC;

|PROCESO CopiaSinEmpresa2;
     #800#0 = "laboral ";
     #800#1 = aFichero + ".mas";
     |LEE 000#800=;
     |SI FSalida = 0;  |ACABA;  |FINSI;

     swSigue = 1;
     |HAZ Purga;

     |SI swSigue = 0; |ACABA; |FINSI;

     |HAZ PintaProgreso;

     aOrigen = aRutaFich + aFichero + ".dat";
     aDestino = aRutaFichEmp + aFichero + ".dat";
     |COPIA_FICHERO aOrigen, aDestino;

     aOrigen = aRutaFich + aFichero + ".ddx";
     aDestino = aRutaFichEmp + aFichero + ".ddx";
     |COPIA_FICHERO aOrigen, aDestino;

     aOrigen = aRutaFich + aFichero + ".ixx";
     aDestino = aRutaFichEmp + aFichero + ".ixx";
     |COPIA_FICHERO aOrigen, aDestino;
|FPRC;

|PROCESO ElDir;
     aFichero = aRutaFich + "*.dat";
     |_PDIR aFichero;
     |PARA; |SI FSalida = 0; |HACIENDO;
          aFichero = aFichero - ".dat";
          aFichero = aFichero % -108

          |HAZ CopiaSinEmpresa2;

          |_SDIR aFichero;
     |SIGUE;
|FPRC;

|PROCESO SinEmpresa;
     || aDirEmpresa = "";
     aAplica     = "laboral";
     |HAZ ElDir;
|FPRC;

|PROCESO ElContador;
     nFicheros = 0;
     aFichero = aRutaFich + "*.dat";
     |_PDIR aFichero;
     |PARA; |SI FSalida = 0; |HACIENDO;
          swSigue = 1;
          |HAZ Purga;
          |SI swSigue = 1;
               nFicheros = nFicheros + 1;
          |FINSI;
          |_SDIR aFichero;
     |SIGUE;
|FPRC;

|PROCESO CargaRutas2;
     aEmpresa = #0#4;
     |QBF aEmpresa;
     aEmpresa = ("00000" + aEmpresa) % -105;

     |DBASE aRutaDef;
     |QBF aRutaDef;
     aRutaDef = aRutaDef + "def/";      || ruta def actual, para CARGA_DEF

     || aRutaDef = aRutaDef + aFichero;    || con fichero, para CARGA_DEF

     |DFICE aRutaFich;                  || ruta fich actual

     aRutaFichEmp = aRutaFich + aEmpresa + "/"; || ruta fich empresa, para PATH_DAT

     |DBASS aRutaFichBas;
     |QBF aRutaFichBas;
     aRutaFichBas = aRutaFichBas + "basica/fich/"
|FPRC;

|PROCESO gecom014_001;
     eaDirDest = #gecom014@#6;
     |HAZ SendMail;
|FPRC;

|DEFBUCLE gecom014_001;
     #104#1003#;
     ;
     #labor001#0, 001, "          ";
     #labor001#0, 999, "zzzzzzzzzz";
     ;
     NULL, gecom014_001;
|FIN;

|PROCESO Email;
     || Emite correo electronico al usuario de que la empresa ha sido
     ||    sincronizada

     aAlfa = (("000000" + #labor001#1) % -106);
     eaTexto = &13 + &10 + "El administrador ha actualizado los datos de la empresa (" + aAlfa + ") " + #labor001#2; |QBF eaTexto;
     eaTexto = eaTexto + &13 + &10 + &13 + &10;
     eaTexto = eaTexto + " Actualizacion con fecha " + #labor001#6 + " a las " + #labor001#7 + &13 + &10;

     eaAsunto  = "Actualizada empresa (" + aAlfa + ") " + #labor001#2; |QBF eaAsunto;
     eaFichero = "";
     eaDirDest = "";

     |DBASS aAlfa; |QBF aAlfa;
     aAlfa = aAlfa % -109;
     |SI aAlfa = "dsprofes/";       || estoy en produccion, sin duda
        aAlfa = "/u/dsmedida/gecochek/def/gecom014.mas";
     |SINO;
        |DBASS aAlfa; |QBF aAlfa;
        aAlfa = aAlfa + "gecochek/def/gecom014.mas";
     |FINSI;

     |CARGA_DEF aAlfa, gecom014@;
     |SI FSalida = 0;
        |DBASS aAlfa; |QBF aAlfa;
        aAlfa = aAlfa % -109;
        |SI aAlfa = "dsprofes/";       || estoy en produccion, sin duda
           aAlfa = "/u/dsmedida/gecochek/fich/";
        |SINO;
           |DBASS aAlfa; |QBF aAlfa;
           aAlfa = aAlfa + "/gecochek/fich/";
        |FINSI;

        |PATH_DAT #gecom014@#aAlfa#;
        |ABRE #gecom014@;
        |BUCLE gecom014_001;
        |CIERRA #gecom014@;
        |DESCARGA_DEF #gecom014@;
     |FINSI;
|FPRC;

|PROCESO Express; |TIPO 0;
     |SI FSalida = 14;
          swExpress = 1;

          |HAZ CargaRutas2;
          nFichero = 0;
          nFicheros = 5;

          |PATH_DAT #800 aRutaFichBas;
          |ABRE #800;    || el dsficher

          nEmpresa = #labor001#1;

          |PUSHV 11281754;
          |BLANCO 11281754;
          |CUADRO 11281754;
          ||            23         4         5
          ||            9012345678901234567890123
          |ATRI R;
          |PINTA 1229, "  SINCRONIZACION RAPIDA  ";
          |ATRI I;
          |PINTA 1429, " Empresa.:               ";
          |PINTA 1529, " Fichero.:               ";
          |PINTA 1629, " Progreso:               ";
          |ATRI 0;

          aAlfa = #0#1;
          aAlfa = ("00000" + aAlfa) % -105;
          |PINTA 1440, aAlfa;

          #800#0 = "laboral ";
          #800#1 = "labempre.mas";
          |LEE 000#800=;
          |HAZ ProcedeAlFichero;

          #800#0 = "laboral ";
          #800#1 = "labtraba.mas";
          |LEE 000#800=;
          |HAZ ProcedeAlFichero;

          #800#0 = "laboral ";
          #800#1 = "labregis.mas";
          |LEE 000#800=;
          |HAZ ProcedeAlFichero;

          #800#0 = "laboral ";
          #800#1 = "labhisto.mas";
          |LEE 000#800=;
          |HAZ ProcedeAlFichero;

          #800#0 = "laboral ";
          #800#1 = "nomir003.mas";
          |LEE 000#800=;
          |HAZ ProcedeAlFichero;

          #800#0 = "laboral ";
          #800#1 = "nomir001.mas";
          |LEE 000#800=;
          |HAZ ProcedeAlFichero;

          |CIERRA #800;

          |POPV;

          swExpress = 0;
     |FINSI;
|FPRC;

|PROCESO Sincroniza;
     |HAZ CargaRutas2;

     |PATH_DAT #800 aRutaFichBas;
     |ABRE #800;    || el dsficher

     nEmpresa = #labor001#1;

     |SI aParam2 ! "AUTO";
          |PUSHV 11281754;
          |BLANCO 11281754;
          |CUADRO 11281754;
          ||            23         4         5
          ||            9012345678901234567890123
          |ATRI R;
          |PINTA 1229, "      PROCESANDO ...     ";
          |ATRI I;
          |PINTA 1429, " Empresa.:               ";
          |PINTA 1529, " Fichero.:               ";
          |PINTA 1629, " Progreso:               ";
          |ATRI 0;

          aAlfa = #0#1;
          aAlfa = ("00000" + aAlfa) % -105;
          |PINTA 1440, aAlfa;
     |FINSI;

     nFichero = 0;
     nFicheros = 0;
     |HAZ ElContador;

     |SI aParam = "NORMAL"; |O aParam = "PATRONES";
          |HAZ SinEmpresa;
     |FINSI;

     |SI aParam = "NORMAL"; |O aParam = "INVERSA";
          |HAZ ConEmpresa;
     |FINSI;

     |SI nEmpExt ! 0;        || vengo de fuera
          aAplica = "laboral";

          aDDef = "labtraba.mas"; aHDef = "labtraba.mas";
          |BUCLE dsficher;

          aDDef = "labregis.mas"; aHDef = "labregis.mas";
          |BUCLE dsficher;

          aFichero = "labhisto";
          |HAZ CopiaSinEmpresa2;
     |FINSI;

     |CIERRA #800;

     |POPV;

     #0#6 = ~;
     |HORA aHora;
     #0#7 = aHora;
     #0#8 = (Usuario % 810);
     |SI nEmpExt = 0;
          |GRABA 020#0a;
          |LIBERA #0;

          |HAZ Email;
     |FINSI;
|FPRC;

|DEFBUCLE labor001;
     #labor001#1;
     3;
     00000, nDesdeEmpresa, "S";
     99999, nHastaEmpresa, "S";
     be;
     NULL, Sincroniza;
|FIN;

|PROCESO Sincronizacion;
     |SI nEmpExt = 0;
          nDesdeEmpresa = 00001;
          nHastaEmpresa = 99999;
     |SINO;
          nDesdeEmpresa = nEmpExt;
          nHastaEmpresa = nEmpExt;
     |FINSI;

     |BUCLE labor001;

     |SI nEmpExt = 0; |Y aParam2 ! "AUTO";
          |MENAV "    Se ha realizado la sincronización de las empresas seleccionadas";
     |FINSI;
|FPRC;

|PROCESO superior;
     #0#0 = 99999;
     #0#1 = 99999;
|FPRC;

|PROCESO inferior;
     #0#0 = 00000;
     #0#1 = 00001;
|FPRC;

|PROCESO graba;
     aGrupo = aRutaApl % -201;
     nGrupo  = aGrupo;
     |SI nGrupo = 0;
         nGrupo = 1;
     |FINSI;
     aAlfa = aRutaApl % -109;

     |SI aAlfa = "dsprofes/";       || estoy en produccion, sin duda
          aGrupo = "1";
          nGrupo = 1
     |FINSI;

     #0#0 = #100#0;
     #0#1 = #100#1;
     |LEE 101#0=;
     FEstado = FSalida;

     #0#0 = #100#0;      || cliente
     #0#1 = #100#1;      || empresa laboral
     #0#2 = #100#2;      || nombre empresa
     #0#3 = "S";         || activado Si de momento

     #101#0 = #0#0;      || cliente
     |LEE 000#101=;
     |SI FSalida = 0;
          aAlfa = aGrupo + (("00" + #101#1) % -102) + "01";  || carpeta cliente (del fich)
          #0#4 = aAlfa;
          #0#5 = #101#2;    || nombre cliente gecochek
     |SINO;
          || error;
     |FINSI;

     |SI FEstado = 0;
          |GRABA 020#0a;
     |SINO;
          |GRABA 020#0n;
     |FINSI;
     |LIBERA #0;
|FPRC;

|DEFBUCLE empresas;
     #100#1002;
     ;
     nDesdeEmpresa, 00001;
     nHastaEmpresa, 99999;
     ;
     NULL, graba;
|FIN;

|PROCESO CargaEmpresas;
     |ABRE #0;
     |DBASS aRutaApl;
     |QBF aRutaApl;

     aAlfa = aRutaApl % -109;
     |SI aAlfa = "dsprofes/";       || estoy en produccion, sin duda
          aRutaDef = "/u/dsmedida/gecochek/def/gecom019";      || ruta def actual, para CARGA_DEF
          aRutaFich = "/u/dsmedida/gecochek/fich/";      || ruta def actual, para CARGA_DEF
     |SINO;
          |DBASS aRutaApl;
          aRutaDef = aRutaApl + "/gecochek/def/gecom019";      || ruta def actual, para CARGA_DEF
          aRutaFich = aRutaApl + "/gecochek/fich/";      || ruta def actual, para CARGA_DEF
     |FINSI;
     |CARGA_DEF aRutaDef, gecom019@;
     |PATH_DAT #100 aRutaFich;

     |SI aAlfa = "dsprofes/";       || estoy en produccion, sin duda
          aRutaDef = "/u/dsmedida/gecochek/def/gecom011";      || ruta def actual, para CARGA_DEF
          aRutaFich = "/u/dsmedida/gecochek/fich/";      || ruta def actual, para CARGA_DEF
     |SINO;
          |DBASS aRutaApl;
          aRutaDef = aRutaApl + "/gecochek/def/gecom011";      || ruta def actual, para CARGA_DEF
          aRutaFich = aRutaApl + "/gecochek/fich/";      || ruta def actual, para CARGA_DEF
     |FINSI;
     |CARGA_DEF aRutaDef, gecom011@;
     |PATH_DAT #101 aRutaFich;

     |ABRE #101;
     |SI nEmpExt = 0;
          nDesdeEmpresa = 00001;
          nHastaEmpresa = 99999;
     |SINO;
          nDesdeEmpresa = nEmpExt;
          nHastaEmpresa = nEmpExt;
     |FINSI;

     |BUCLE empresas;
     |CIERRA #101;

     |DESCARGA_DEF #gecom019@;
     |DESCARGA_DEF #gecom011@;
     |CIERRA #0;
|FPRC;

|PROCESO Desactiva;
     #0#3 = "N";
     |GRABA 020#0a;
     |LIBERA #0;
|FPRC;

|DEFBUCLE Desactiva;
     #0#1;
     ;
     INICIO;
     FINAL;
     be;
     NULL, Desactiva;
|FIN;

|PROCESO BuscaCliente;
     |SI #0#0 = 11;
          |ACABA;
     |FINSI;
     |HAZ Express;
|FPRC;

|DEFBUCLE BuscaCliente;
     #0#1;
     ;
     00001, nEmpresa;
     99999, nEmpresa;
     ;
     NULL, BuscaCliente;
|FIN;

|PROGRAMA;
     aParam = PARAMETRO;
     |QBF aParam;

     aParam2 = PARAMETRO;
     aParam2 = aParam2 % 804;
     |QBF aParam2;
     |SI aParam2 = "AUTO";
          aParam = aParam - aParam2;
     |FINSI;
     |SI aParam2 = "EXPR";
          aParam = aParam - aParam2;
          swExpress = 1;
          FSalida = 14;

          nEmpresa = aParam;
          |BUCLE BuscaCliente;
     |FINSI;

     |BUCLE Desactiva;   || inicialmente las pongo todas a "N" de lo que estaba
                         || grabado de la ultima vez, cuando despues compruebe
                         || que estan en el gecochek las activare
     |HAZ CargaEmpresas;

     |SI aParam2 = "AUTO";         || sincronizacion de una sola empresa
          |HAZ Sincronizacion;     || que me viene en el parametro
          |ACABA;
     |SINO;
          |SI aParam = "AUTO";
               |SI aParam2 = ""; |O aParam2 = "EXPR";
                    aParam = "NORMAL";
                    aParam2 = "AUTO";    || sincronizacion de TODAS las empresas
                    |HAZ Sincronizacion; || aParam = AUTO y aParam2 = "";
                                         || aunque pongo aParam2 = AUTO para que
                                         || no aparezcan mensajes
                    |ACABA;
               |FINSI;
          |FINSI;
     |FINSI;

     |SI nEmpExt = 0;
          |PARA; |SI; |HACIENDO;
               |ENTLINEAL #1#0, -1, 4, 20, 1, inferior, superior;
               |MENU menu;
               nOpcion = FSalida;
               |SI nOpcion = 1;
                    |HAZ Aviso;
                    aAlfa = "0000N¿Desea continuar con el proceso?";
                    |CONFI aAlfa;
                    |SI FSalida = 0;
                         |HAZ Sincronizacion;
                         nOpcion = 3;
                    |SINO;
                         nOpcion = 2;
                    |FINSI;
               |FINSI;
               |SI nOpcion = 2;
               |FINSI;
               |SI nOpcion = 3;
                    |SAL;
               |FINSI;
          |SIGUE;
     |SINO;
          |HAZ Sincronizacion;
     |FINSI;
|FPRO;

|PROCESO Tipo80;  |TIPO 80;
     || Desactiva el logger

     aDsini2 = "";
     aDsini1 = "LOGGER";
     |VALOR_INI aDsini1,aDsini2;
     aRestauraIni = aDsini2; |QBF aRestauraIni;
     |SI aRestauraIni ! "";
          aDsini2 = "";       || VACIO = DESACTIVADO
          aDsini1 = aDsini1 + "=" + aDsini2;
          |VALOR_INI aDsini1,aDsini2;
     |FINSI;
|FPRC;

|PROCESO Tipo90;  |TIPO 90;
     || Activa el logger

     |SI aRestauraIni ! "";
         aDsini2 = aRestauraIni;
         aDsini1 = "LOGGER="+ aDsini2;
         |VALOR_INI aDsini1,aDsini2;
     |FINSI;
|FPRC;
