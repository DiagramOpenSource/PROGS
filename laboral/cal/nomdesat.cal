|FICHEROS;
     nomdesat #0;
     nomhicca #1;    || historico cabs.
     nomhicli #2;    || historico lins.
     labtraba #5;    || trabajadores
     nomtraac #7;    || variaciones cabs. (atrasos trab.)
     nomtraal #8;    || variaciones lins. (atrasos trab.)
     labbasec #9;    || bases cabs.
     labbasel #10;   || bases lins.
     labempre #12;   || empresas

     cretam01;
     cretam02;
     cretam03;
     cretam04;
|FIN;

|VARIABLES;
    anyo_irpf = 0;
    anyo_hist = 0;
    anyo_otro = 0;
    ejea = 0;
    curso = 0;
    seleccio = 0;
    swfuera = 0;
    mes_act = 0;
    campo = 0;
    linea = 0;
    borra = 0;
    mes = 0;
    any = 0;
    dia = 0;
    nume1 = 0;
    tp = 0;
    p_mes = "";
    fecsys = @;
    alfa = "";
    alfa1 = "";
    diab = "";
    mesb = "";
    anyb = "";
    fecha = "";
    clave = "";
    fecalf = "";

     swExiste = 0;
     aNaf = "";
     aCCC = "";
     aAlfa1 = "";
     aAlfa2 = "";
     nAnyHis = 0;
|FIN;
____________________________________/ CALCULOS DE PANTALLA
|PROCESO SinMes; |TIPO 7;
     |SI #0#5 ! #0#10;
          |C_M #0#8, 1, "N";
     |SINO;
          |C_M #0#8, 1, "S";
     |FINSI;
|FPRC;

|PROCESO defecto; |TIPO 7;
    fecsys = ~;
    alfa = fecsys;
    alfa = alfa % -102;
    #0Campo = alfa;
|PINTA #0Campo;
|FPRC;

|PROCESO mes; |TIPO 7;
    fecsys = ~;
    alfa = fecsys;
    alfa = alfa % 402;
    #0Campo = alfa;
|PINTA #0Campo;
|FPRC;

|PROCESO pintames; |TIPO 0;
|SI #0Campo =  1;  p_mes = "ENERO     ";  |FINSI;
|SI #0Campo =  2;  p_mes = "FEBRERO   ";  |FINSI;
|SI #0Campo =  3;  p_mes = "MARZO     ";  |FINSI;
|SI #0Campo =  4;  p_mes = "ABRIL     ";  |FINSI;
|SI #0Campo =  5;  p_mes = "MAYO      ";  |FINSI;
|SI #0Campo =  6;  p_mes = "JUNIO     ";  |FINSI;
|SI #0Campo =  7;  p_mes = "JULIO     ";  |FINSI;
|SI #0Campo =  8;  p_mes = "AGOSTO    ";  |FINSI;
|SI #0Campo =  9;  p_mes = "SEPTIEMBRE";  |FINSI;
|SI #0Campo = 10;  p_mes = "OCTUBRE   ";  |FINSI;
|SI #0Campo = 11;  p_mes = "NOVIEMBRE ";  |FINSI;
|SI #0Campo = 12;  p_mes = "DICIEMBRE ";  |FINSI;
|SI Campo = 4;  |PINTA 1139, p_mes;  |FINSI;
|SI Campo = 7;  |PINTA 1161, p_mes;  |FINSI;
|FPRC;

|PROCESO Historico; |TIPO 7;
     #0#11 = #0#5;
     |PINTA #0#11;
|FPRC;

|PROCESO Cotizacion; |TIPO 7;
     #0#12 = #0#11;
     |PINTA #0#12;
|FPRC;

*************************************************************************
          INICIA EL PROCESO DE LECTURA Y DESACTUALIZACION
*************************************************************************

|PROCESO actual;
|ATRI I;
|PINTA 1928, #1#0;
|PINTA 1949, #1#1;
|ATRI 0;

|HAZ traba1;
|SI swfuera = 1;
        alfa1 = "    El trabajador [" + #1#0 + "-" + #1#1 + "] no es alta en el mes de calculo ...";
    |MENAV alfa1;
|FINSI;
|SI swfuera = 0;
        seleccio = 1;
    |HAZ bases1;
    |HAZ varia1;
|FINSI;
|FPRC;

|DEFBUCLE nomdesat0;
 #1#1;
 ;
 #0#0, #0#2, #0#11, curso, #0#9;
 #0#1, #0#3, #0#11, curso, #0#9;
 ;
 NULL, actual;
|FIN;

|PROCESO tipo3; |TIPO 3;
|SI #0#10>80;  anyo_irpf = #0#10+1900; |SINO; anyo_irpf = #0#10+2000; |FINSI;
|SI #0#5>80;   anyo_hist = #0#5+1900;  |SINO; anyo_hist = #0#5+2000;  |FINSI;
|SI #0#12>80;  anyo_otro = #0#12+1900; |SINO; anyo_otro = #0#12+2000; |FINSI;

|ABRE #5;
|ABRE #7;
|ABRET #8;
|ABRE #9;
|ABRET #10;
|ABRE #12;
|PARA curso = #0#7; |SI curso ] #0#4; |HACIENDO curso = curso - 1;
        alfa1 = curso;   alfa1 = "0" + curso;   alfa1 = alfa1 % -102;
    |PINTA 1963, alfa1;
    |BUCLE nomdesat0;
    |SI seleccio = 0;
        |MENAV "    Seleccion vacia ...";
    |FINSI;
|SIGUE;
|CIERRA #5;
|CIERRA #7;
|CIERRAT #8;
|CIERRA #9;
|CIERRAT #10;
|CIERRA #12;
|FPRC;

*************************************************************************
          DESACTUALIZA LOS TRABAJADORES
*************************************************************************

|PROCESO traba1;
    swfuera = 2;

    #5#0 = #1#0;    || Codigo empresa
    #5#1 = #1#1;    || Codigo trabajador
|LEE 121#5=;
|SI FSalida = 0;
    ||HAZ chequea;

    || no tengo que chequear si el trabajador esta de alta o no
    || si he calculado y actualizado los atrasos de un trabajador que estaba
    || de baja en el mes en que lo actualizo es porque lo he hecho intenciona-
    || damente, pueden haberme dado de baja en 05/2007 y luego calcularme
    || atrasos del 2007 que me actualicn en 2008, si luego los quiero
    || desactualizar debe permitirmelo 28.02.2008

     swfuera = 0;
    |SI swfuera = 0;
        |HAZ traba2;
    |FINSI;
|FINSI;
|LIBERA #5;
|FPRC;

******************************************************************************
      RUTINAS DESACTUALIZACION TRABAJADOR
******************************************************************************

|PROCESO traba2;
|DDEFECTO #12;
    #12#0 = #5#0;
|LEE 000#12=;

|GRABA 020#5a;
|FPRC;

*************************************************************************
          DESACTUALIZA LAS BASES
*************************************************************************

|PROCESO bases1;
    #9#0 = #1#0;
    #9#1 = #1#1;
|LEE 000#9=;
|SI FSalida = 0;
        linea = 0;
        borra = 0;
    |HAZ fechabas;
    |BUCLELIN 2bases2#9;
    |SI borra = 1;
            #10#0 = #1#0;
            #10#1 = #1#1;
            #10#2 = linea;
        |LEE 121#10=;
        |SI #10#7 = #1#39; |Y #10#8 = #1#40;
            |BORRA 020#10a;
            |SI linea = 1;
                |BORRA 020#9a;  || si solo hay una linea, borra la cabecera
            |FINSI;
        |SINO;
                #10#7 = #10#7 - #1#39;  || resta base con.com.
                #10#8 = #10#8 - #1#40;  || resta base d+f+p
                #10#9 = "";             || quita lo de observaciones
            |GRABA 020#10a;
        |FINSI;
        |LIBERA #10;
    |FINSI;
|FINSI;
|FPRC;

*************************************************************************
          RUTINAS DESACTUALIZACION BASES
*************************************************************************

|PROCESO fechabas;
    fecalf = #1#5;
|HAZ desglosa;
|SI mes = #1#2; |Y any = anyo_otro;
        diab = dia;
|SINO;
     |SI #1#2 = 2;|Y #1#7 = 28;
          |SI anyo_otro = 2000;|O anyo_otro = 2004;|O anyo_otro = 2008;
                  diab = 29;
          |SINO;
                  diab = #1#7;
          |FINSI;
     |SINO;
            diab = #1#7;
     |FINSI;
|FINSI;
                  diab = "0" + diab;  diab = diab % -102;
    mesb = #1#2;  mesb = "0" + #1#2;  mesb = mesb % -102;
    anyb = anyo_otro;
    fecha = diab + "." + mesb + "." + anyb;
|FPRC;

|PROCESO bases2;
|SI fecha = #10#3;
        borra = 1;
        linea = #10#2;
|FINSI;
|FPRC;

***************************************************************************
       CREACION VARIACIONES ATRASOS
***************************************************************************

|PROCESO varia1;
    #7#0 = #1#0;
    #7#1 = #1#1;
|LEE 000#7=;
|SI FSalida = 0; |Y #0#6 = "S";
    |BUCLELIN 1NULL#7;
    |BORRA 020#7a;
|FINSI;
|HAZ varia3;
|FPRC;

/****************************************************************************/
/*****    PROCESOS PARA BORRAR LA LIQUIDACION CRETA SI ESTA CREADA **********/
/****************************************************************************/

|PROCESO cretam02_busca;
     swExiste = 1;
|FPRC;

|DEFBUCLE cretam02_busca;
     #cretam02#1;
     ;
     #nomhicca#0, nAnyHis, #nomhicca#2, #0#9, "               ";
     #nomhicca#0, nAnyHis, #nomhicca#2, #0#9, "zzzzzzzzzzzzzzz";
     be;
     NULL, cretam02_busca;
|FIN;

|PROCESO cretam01;
     || ahora busco si queda algun CCC en esta empresa con el tipo 5 y si no queda,
     || me cargo el registro tipo 5 del cretam01 esta empresa

     #cretam01#0 = #nomhicca#0;
     #cretam01#1 = nAnyHis;
     #cretam01#2 = #nomhicca#2;

     swExiste = 0;
     |BUCLE cretam02_busca;
     |SI swExiste = 0;
          #cretam01#3 = #0#9;
          |LEE 101#cretam01.=;
          |SI FSalida = 0;
               |BORRA 020#cretam01.a;
               |LIBERA #cretam01;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO cretam03_busca;
     swExiste = 1;
|FPRC;

|DEFBUCLE cretam03_busca;
     #cretam03#1;
     ;
     #nomhicca#0, nAnyHis, #nomhicca#2, #0#9, aCCC, "            ";
     #nomhicca#0, nAnyHis, #nomhicca#2, #0#9, aCCC, "zzzzzzzzzzzz";
     be;
     NULL, cretam03_busca;
|FIN;

|PROCESO cretam02;
     || me cargo el registro tipo que estoy desactualizando del cretam02 este CCC

     #cretam02#0 = #nomhicca#0;
     #cretam02#1 = nAnyHis;
     #cretam02#2 = #nomhicca#2;
     #cretam02#4 = aCCC;

     swExiste = 0;
     |BUCLE cretam03_busca;
     |SI swExiste = 0;
          #cretam02#3 = #0#9;
          |LEE 101#cretam02.=;
          |SI FSalida = 0;
               |BORRA 020#cretam02.a;
               |LIBERA #cretam02;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO cretam03;
     #cretam03#0 = #nomhicca#0;
     #cretam03#1 = nAnyHis;
     #cretam03#2 = #nomhicca#2;
     #cretam03#3 = #0#9;
     #cretam03#4 = aCCC;
     #cretam03#5 = aNaf;
     |LEE 101#cretam03.=;
     |SI FSalida = 0;
          |BORRA 020#cretam03.a;
     |FINSI;
|FPRC;

|PROCESO cretam04;
     swExiste = 1;
     aCCC = #cretam04#4;

     |BORRA 020#cretam04.a;
|FPRC;

|DEFBUCLE cretam04;
     #cretam04#1;
     ;
     #nomhicca#0, nAnyHis, #nomhicca#2, #0#9, "               ", aNaf, 01;
     #nomhicca#0, nAnyHis, #nomhicca#2, #0#9, "zzzzzzzzzzzzzzz", aNaf, 99;
     be;
     NULL, cretam04;
|FIN;

|PROCESO creta;
     |ABRE #cretam01;
     |ABRE #cretam02;
     |ABRE #cretam03;
     |ABRE #cretam04;

     nAnyHis = #nomhicca#66 + 2000;

     aNaf = #labtraba#75;
     |QBF aNaf;
     aAlfa1 = aNaf % 102;
     aAlfa2 = aNaf % 310;
     |QBF aAlfa2;
     aAlfa2 = ("0000000000" + aAlfa2) % -110;
     aNaf = aAlfa1 + aAlfa2;

     swExiste = 0;
     |BUCLE cretam04;
     |SI swExiste = 1;
          |HAZ cretam03;
          |HAZ cretam02;
          |HAZ cretam01;
     |FINSI;

     |CIERRA #cretam04;
     |CIERRA #cretam03;
     |CIERRA #cretam02;
     |CIERRA #cretam01;
|FPRC;

/****************************************************************************/

/****************************************************************************/
/*       RUTINAS CREACION VARIACIONES ATRASOS                               */
/****************************************************************************/

|PROCESO varia3;
     |SI #0#6 = "S"; |Y curso = #0#4;
         |DDEFECTO #7;
             #7#00 = #1#0;  || empresa
             #7#01 = #1#1;  || trabajador
         |GRABA 020#7n;
     |FINSI;

     |BUCLELIN 0varia4#1;

     |BORRA 020#1a;

     |HAZ creta;
|FPRC;

|PROCESO varia4;
|SI #0#6 = "S"; |Y curso = #0#4;
    |SI #2#04 < 201; |O #2#04 > 208;
        |SI #2#04 < 501; |O #2#04 > 599;
                #8#0 = #1#00;  || empresa
                #8#1 = #1#01;  || trabajador
                #8#2 = #2#04;  || concepto
                nume1 = #2#10;
            |SI nume1 ] 0;
                    #8#3 =   "+";  || signo positivo
            |SINO;
                    #8#3 =   "-";  || signo negativo
            |FINSI;
                #8#4 = #2#10;  || valor
            |GRABA 020#8n;
        |FINSI;
    |FINSI;
|FINSI;

|BORRA 020#2a;
|FPRC;

***************************************************************************
       SUBRUTINAS VARIAS
***************************************************************************

|PROCESO chequea;  || chequea el trabajador este en periodo de alta
    swfuera = 0;
    fecalf = #5#36;   || fecha de alta
|HAZ desglosa;
|SI any > anyo_hist;
        swfuera = 1;
|SINO;
    |SI mes > curso; |Y any = anyo_hist;
            swfuera = 1;
    |FINSI;
|FINSI;
|SI swfuera = 0;
        fecalf = #5#37;  || fecha de baja
    |HAZ desglosa;
    |SI any < anyo_hist; |Y any ! 0;
            swfuera = 1;
    |SINO;
        |SI mes < curso; |Y any = anyo_hist; |Y mes ! 0;
                swfuera = 1;
        |FINSI;
    |FINSI;
|FINSI;
|FPRC;

|PROCESO desglosa;     || DESGLOSA EN DIA, MES Y ANYO UNA FECHA DD.MM.AAAA
    alfa1 = fecalf %  102;   dia = alfa1;
    alfa1 = fecalf %  402;   mes = alfa1;
    alfa1 = fecalf % -104;   any = alfa1;
|SI alfa1 = "....";
        dia = 0;
        mes = 0;
        any = 0;
|FINSI;
|FPRC;
