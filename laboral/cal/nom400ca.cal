|FICHEROS;
     nom400ca;
     nomcalca;
     nom400eu;
     nomgruel;
     copcalcu@ #200;      || copia del def del calculo de nominas (normal o agrario)
     nomimpr1;      || lineas del registro de pagas extras emitidas
     labtraba;
     nomir003;
|FIN;

|VARIABLES;
     &nElMesIRPF = 0;
     &nElAnyIRPF = 0;
     &eaVengoDe = "";
     aDef = "";
     FCargaDef = 0;
     aAlfa = "";
     nDesdeEmp = 0;
     nHastaEmp = 0;
     nDesdeTra = 0;
     nHastaTra = 0;
     FEstado = 0;
     nNume = 0;
     nTipo = 0;
     nGrupo = 0;
     swPorGrupos = 0;
     nCampoG = 0;
     salte_1 = 0;
     salte_2 = 0;
     &swFiniq = 0;
     &nEmp = 0;
     &nTra = 0;
     swPagaYaEmi = 0;
     nReduccion = 0;
     &swCalPagas = "N";
     aNif = "";
     nOtraReducc = 0;
     aFecha1 = "";
     aFecha2 = "";
     fFecha1 = @;
     fFecha2 = @;
     swSigue = 0;
     nLimite = 0;
     nTotalNom = 0;
     nMeQuedan = 0;
     nBase1 = 0;
     nBase2 = 0;
     nBase3 = 0;
     nReduccNom = 0;
|FIN;

|PROCESO nom400eu;
     #nom400eu#0 = #labtraba#0;
     #nom400eu#1 = #labtraba#1;
     #nom400eu#2 = 2008;
     #nom400eu#3 = 6;
     |LEE 000#nom400eu.=;
     |SI FSalida = 0;
          |SI #nomcalca#0 ! #labtraba#0; |O #nomcalca#1 ! #labtraba#1;
               swSigue = 0;

               || esto apara asegurarme de que existe su ficha en el nomcalca
               |SALVA_FICHA #nomcalca;
               #nomcalca#0 = #nom400eu#0;
               #nomcalca#1 = #nom400eu#1;
               #nomcalca#2 = 6;
               #nomcalca#3 = 0;
               |LEE 000#nomcalca.=;
               |SI FSalida = 0;
                    swSigue = 1;
               |FINSI;
               |REPON_FICHA #nomcalca;

               |SI swSigue = 1;
                    aFecha1 = #nomcalca#4;
                    fFecha1 = aFecha1;
                    aFecha2 = #labtraba#34;
                    fFecha2 = aFecha2;
                    |SI fFecha1 > fFecha2;
                         nOtraReducc = nOtraReducc + #nom400eu#10 + #nom400eu#13 + #nom400eu#21;
                         nOtraReducc = nOtraReducc + #nom400eu#27 + #nom400eu#33;
                    |FINSI;
               |FINSI;
          |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE labtraba;
     #labtraba#2;
     ;
     aNif;
     aNif;
     ;
     NULL, nom400eu;
|FIN;

|PROCESO BuscaOtro;
     #labtraba#0 = #nomcalca#0;
     #labtraba#1 = #nomcalca#1;
     |LEE 000#labtraba.=;
     aNif = #labtraba#7;

     nOtraReducc = 0;
     |BUCLE labtraba;
|FPRC;

|PROCESO Limpia;
     |SI nTipo = 0;
          || base 1 nomina
          #nom400eu#4 = 0; #nom400eu#5 = 0; #nom400eu#6 = 0;
          #nom400eu#10 = 0; #nom400eu#11 = 0; #nom400eu#12 = 0;

          || base 2 nomina
          #nom400eu#24 = 0; #nom400eu#25 = 0; #nom400eu#26 = 0;
          #nom400eu#27 = 0; #nom400eu#28 = 0; #nom400eu#29 = 0;

          || base 3 nomina
          #nom400eu#30 = 0; #nom400eu#31 = 0; #nom400eu#32 = 0;
          #nom400eu#33 = 0; #nom400eu#34 = 0; #nom400eu#35 = 0;

          || base paga
          |SI swPagaYaEmi = 0; |O swCalPagas = "S";
               #nom400eu#7 = 0; #nom400eu#8 = 0; #nom400eu#9 = 0;
               #nom400eu#13 = 0; #nom400eu#14 = 0; #nom400eu#15 = 0;
          |FINSI;
     |FINSI;
     |SI nTipo = 2;
          || base finiquito
          #nom400eu#18 = 0; #nom400eu#19 = 0; #nom400eu#20 = 0;
          #nom400eu#21 = 0; #nom400eu#22 = 0; #nom400eu#23 = 0;
     |FINSI;
|FPRC;

|PROCESO Los400;
     swPagaYaEmi = 0;

     #nomimpr1#0 = #nomcalca#0;
     #nomimpr1#1 = #nomcalca#1;
     #nomimpr1#2 = 06;
     #nomimpr1#3 = 0;
     #nomimpr1#4 = 2008;
     |LEE 000#nomimpr1.=;
     |SI FSalida = 0;
          || ATENCION: la paga ya ha sido emitida, no vario el porcentaje
          || de paga

          swPagaYaEmi = 1;
     |FINSI;

     |HAZ BuscaOtro;
     nReduccion = 200 - nOtraReducc;
     |SI nReduccion < 0;
          nReduccion = 0;
     |FINSI;

     #labtraba#0 = #nomcalca#0;
     #labtraba#1 = #nomcalca#1;
     |LEE 000#labtraba.=;

     #nomir003#0 = #labtraba#7;
     #nomir003#40 = 2008;
     #nomir003#41 = #labtraba#0;
     |LEE 000#nomir003.=;

     nLimite = 0;
     /*
     |SI #nomir003#5 = 2;
          nLimite = 2;
     |FINSI;
     |SI #nomir003#5 = 3;
          nLimite = 15;
     |FINSI;
     */
     nLimite = -1;

     |DDEFECTO #nom400eu;
     #nom400eu#0 = #nomcalca#0;         || Empresa
     #nom400eu#1 = #nomcalca#1;         || Trabajador
     #nom400eu#2 = nElAnyIRPF;          || A¤o
     #nom400eu#3 = nElMesIRPF;          || Mes
     |LEE 101#nom400eu.=;
     FEstado = FSalida;

     |SI FSalida = 0; |Y #labtraba#186 = "N";
          |BORRA 020#nom400eu.a;
          |LIBERA #nom400eu;
     |FINSI;
     |SI #labtraba#186 = "N";
          |ACABA;
     |FINSI;
     |SI FEstado = 0;
          |HAZ Limpia;
     |FINSI;

     nTotalNom = 0;
     nMeQuedan = 0;
     nBase1 = 0;
     nBase2 = 0;
     nBase3 = 0;
     |SI nTipo = 0;
          #nom400eu#4 = #nomcalca#14;        || Base IRPF 1 Nomina
          #nom400eu#5 = #nomcalca#13;        || % retencion IRPF 1 nomina
          #nom400eu#6 = #nomcalca#15;        || cuota retencion 1 nomina

          #nom400eu#24 = #nomcalca#69;        || Base IRPF 2 Nomina
          #nom400eu#25 = #nomcalca#67;        || % retencion IRPF 2 nomina
          #nom400eu#26 = #nomcalca#71;        || cuota retencion 2 nomina

          #nom400eu#30 = #nomcalca#70;        || Base IRPF 3 Nomina
          #nom400eu#31 = #nomcalca#68;        || % retencion IRPF 3 nomina
          #nom400eu#32 = #nomcalca#72;        || cuota retencion 3 nomina

          |SI swPagaYaEmi = 0; |O swCalPagas = "S";
               #nom400eu#7 = #nomcalca#16;        || Base IRPF Paga
               #nom400eu#8 = #nomcalca#87;        || % retencion IRPF Paga
               #nom400eu#9 = #nomcalca#17;        || cuota retencion Paga

               nTotalNom = #nomcalca#15 + #nomcalca#71 + #nomcalca#72;
               nNume = nTotalNom + #nomcalca#17;

               |SI nNume ] nReduccion;
                    nReduccNom = nTotalNom / nNume * nReduccion;
                    nMeQuedan = nReduccNom;

                    #nom400eu#10 = #nomcalca#15 * nReduccNom / nTotalNom;
                    #nom400eu#27 = #nomcalca#71 * nReduccNom / nTotalNom;
                    #nom400eu#33 = #nomcalca#72 * nReduccNom / nTotalNom;

                    nMeQuedan = nReduccNom - (#nom400eu#10 + #nom400eu#27 + #nom400eu#33);
                    |SI nMeQuedan ! 0; |Y #nomcalca#72 > 0;
                         #nom400eu#33 = #nom400eu#33 + nMeQuedan;
                         nMeQuedan = 0;
                    |FINSI;
                    |SI nMeQuedan ! 0; |Y #nomcalca#71 > 0;
                         #nom400eu#27 = #nom400eu#27 + nMeQuedan;
                         nMeQuedan = 0;
                    |FINSI;
                    |SI nMeQuedan ! 0; |Y #nomcalca#15 > 0;
                         #nom400eu#10 = #nom400eu#10 + nMeQuedan;
                         nMeQuedan = 0;
                    |FINSI;

                    #nom400eu#13 = nReduccion - nReduccNom;            || reduccion Paga
               |SINO;
                    #nom400eu#10 = #nomcalca#15;        || reduccion Base 1
                    #nom400eu#27 = #nomcalca#71;        || reduccion Base 2
                    #nom400eu#33 = #nomcalca#72;        || reduccion Base 3

                    #nom400eu#13 = #nomcalca#17;       || reduccion Paga
               |FINSI;
          |SINO;
               nReduccion = nReduccion - #nom400eu#13;

               nTotalNom = #nomcalca#15 + #nomcalca#71 + #nomcalca#72;
               nNume = nTotalNom;

               |SI nNume ] nReduccion;
                    || #nom400eu#10 = #nomcalca#15 / nNume * nReduccion;    || reduccion Nomina
                    || #nom400eu#13 = nReduccion - #nom400eu#10;            || reduccion Paga
                    nReduccNom = nTotalNom / nNume * nReduccion;
                    nMeQuedan = nReduccNom;

                    #nom400eu#10 = #nomcalca#15 * nReduccNom / nTotalNom;
                    #nom400eu#27 = #nomcalca#71 * nReduccNom / nTotalNom;
                    #nom400eu#33 = #nomcalca#72 * nReduccNom / nTotalNom;

                    nMeQuedan = nReduccNom - (#nom400eu#10 + #nom400eu#27 + #nom400eu#33);
                    |SI nMeQuedan ! 0; |Y #nomcalca#72 > 0;
                         #nom400eu#33 = #nom400eu#33 + nMeQuedan;
                         nMeQuedan = 0;
                    |FINSI;
                    |SI nMeQuedan ! 0; |Y #nomcalca#71 > 0;
                         #nom400eu#27 = #nom400eu#27 + nMeQuedan;
                         nMeQuedan = 0;
                    |FINSI;
                    |SI nMeQuedan ! 0; |Y #nomcalca#15 > 0;
                         #nom400eu#10 = #nom400eu#10 + nMeQuedan;
                         nMeQuedan = 0;
                    |FINSI;
               |SINO;
                    #nom400eu#10 = #nomcalca#15;        || reduccion Base 1
                    #nom400eu#27 = #nomcalca#71;        || reduccion Base 2
                    #nom400eu#33 = #nomcalca#72;        || reduccion Base 3
               |FINSI;
          |FINSI;

          #nom400eu#12 = #nomcalca#15 - #nom400eu#10;   || cuota final 1
          #nom400eu#11 = (#nom400eu#12 / #nomcalca#14 * 100) ! 2;  || % ret. final 1

          #nom400eu#29 = #nomcalca#71 - #nom400eu#27;   || cuota final 2
          #nom400eu#28 = (#nom400eu#29 / #nomcalca#69 * 100) ! 2;  || % ret. final 2

          #nom400eu#35 = #nomcalca#72 - #nom400eu#33;   || cuota final 3
          #nom400eu#34 = (#nom400eu#35 / #nomcalca#70 * 100) ! 2;  || % ret. final 3

          |SI swPagaYaEmi = 0; |O swCalPagas = "S";
               #nom400eu#15 = #nomcalca#17 - #nom400eu#13;  || cuota final Paga
               #nom400eu#14 = (#nom400eu#15 / #nomcalca#16 * 100) ! 2; || % ret. final paga
          |FINSI;

          || APLICACION DE LIMITES PARA TIPO DE CONTRATO 2 o 3
          |SI #nom400eu#11 < nLimite;
               |SI nLimite > #nom400eu#5;
                    nLimite = #nom400eu#5;
               |FINSI;
               #nom400eu#11 = nLimite;
               #nom400eu#12 = (#nom400eu#4 % nLimite) ! 2;
               #nom400eu#10 = #nom400eu#6 - #nom400eu#12;
          |FINSI;
          |SI #nom400eu#14 < nLimite;
               |SI nLimite > #nom400eu#8;
                    nLimite = #nom400eu#8;
               |FINSI;
               #nom400eu#14 = nLimite;
               #nom400eu#15 = (#nom400eu#7 % nLimite) ! 2;
               #nom400eu#13 = #nom400eu#9 - #nom400eu#15;
          |FINSI;
     |SINO;
          nReduccion = nReduccion - #nom400eu#10 - #nom400eu#13;
          nReduccion = nReduccion - #nom400eu#27 - #nom400eu#33;
          #nom400eu#18 = #nomcalca#14;        || Base IRPF Finiquito
          #nom400eu#19 = #nomcalca#13;        || % retencion IRPF finiquito
          #nom400eu#20 = #nomcalca#15;        || cuota retencion finiquito
          nNume = #nomcalca#15;
          |SI #nomcalca#15 ] nReduccion;
               #nom400eu#21 = nReduccion;    || reduccion finiquito
          |SINO;
               #nom400eu#21 = #nomcalca#15;     || reduccion finiquito
          |FINSI;

          #nom400eu#23 = #nomcalca#15 - #nom400eu#21;   || cuota final finiquito
          #nom400eu#22 = (#nom400eu#23 / #nomcalca#14 * 100) ! 2;  || % ret. final Nomina

          || APLICACION DE LIMITES PARA TIPO DE CONTRATO 2 o 3
          |SI #nom400eu#22 < nLimite;
               |SI nLimite > #nom400eu#19;
                    nLimite = #nom400eu#19;
               |FINSI;
               #nom400eu#22 = nLimite;
               #nom400eu#23 = (#nom400eu#18 % nLimite) ! 2;
               #nom400eu#21 = #nom400eu#20 - #nom400eu#23;
          |FINSI;
     |FINSI;

     |SI FEstado = 0;
          |GRABA 020#nom400eu.a;
     |SINO;
          |GRABA 020#nom400eu.n;
     |FINSI;
     |LIBERA #nom400eu;


     |SI nTipo = 0
          #nomcalca#13 = #nom400eu#11;  || nueva retencion IRPF 1 nomina
          #nomcalca#15 = #nom400eu#12;  || nueva cuota IRPF 1 nomina

          #nomcalca#67 = #nom400eu#28;  || nueva retencion IRPF 2 nomina
          #nomcalca#71 = #nom400eu#29;  || nueva cuota IRPF 2 nomina

          #nomcalca#68 = #nom400eu#34;  || nueva retencion IRPF 3 nomina
          #nomcalca#72 = #nom400eu#35;  || nueva cuota IRPF 3 nomina

          |SI swPagaYaEmi = 0; |O swCalPagas = "S";
               #nomcalca#87 = #nom400eu#14;  || nueva retencion IRPF pagas
               #nomcalca#17 = #nom400eu#15;  || nueva cuota IRPF pagas
          |FINSI;

          #nomcalca#75 = #nomcalca#75 - #nom400eu#6 + #nom400eu#12;
          #nomcalca#75 = #nomcalca#75 - #nom400eu#26 + #nom400eu#29;
          #nomcalca#75 = #nomcalca#75 - #nom400eu#32 + #nom400eu#35;

          #nomcalca#109 = #nomcalca#109 + #nom400eu#6 - #nom400eu#12;
          #nomcalca#109 = #nomcalca#109 + #nom400eu#26 - #nom400eu#29;
          #nomcalca#109 = #nomcalca#109 + #nom400eu#32 - #nom400eu#35;
          #nomcalca#111 = #nomcalca#111 + #nom400eu#9 - #nom400eu#15;

          #nomcalca#113 = #nomcalca#109 + #nomcalca#111;
     |SINO;
          #nomcalca#13 = #nom400eu#22;  || nueva retencion IRPF nomina
          #nomcalca#15 = #nom400eu#23;  || nueva cuota IRPF nomina

          #nomcalca#75 = #nomcalca#75 - #nom400eu#20 + #nom400eu#23;

          #nomcalca#109 = #nomcalca#109 + #nom400eu#20 - #nom400eu#23;
          #nomcalca#113 = #nomcalca#109 + #nomcalca#111;
     |FINSI;

     |GRABA 020#nomcalca.a;
     |LIBERA #nomcalca;
|FPRC;

|DEFBUCLE nomcalcg;
     #nomcalca#1;
     ;
     #nomgruel#1, 00001, nElMesIRPF, nTipo;
     #nomgruel#1, 99999, nElMesIRPF, nTipo;
     be;
     NULL, Los400;
|FIN;

|PROCESO CalGrupo;       || para cada una hace el proceso de siemrpe
     |BUCLE nomcalcg;
|FPRC;

|DEFBUCLE Grupo;         || lee empresas del grupo
     #nomgruel#1;
     ;
     nGrupo, 00001;
     nGrupo, 99999;
     ;
     NULL, CalGrupo;
|FIN;

|PROCESO PorGrupos;
     swPorGrupos = 1;
     |PARA nCampoG = 63; |SI nCampoG [ 70; |HACIENDO nCampoG = nCampoG + 1;
          nGrupo = #copcalcu@#nCampoG#;
          |BUCLE Grupo;
     |SIGUE;
|FPRC;

|DEFBUCLE DesdeHasta;
     #nomcalca#1;
     ;
     nDesdeEmp, nDesdeTra, nElMesIRPF, nTipo;
     nHastaEmp, nHastaTra, nElMesIRPF, nTipo;
     be;
     NULL, Los400;
|FIN;

|PROCESO Salteada;
     |PARA salte_1 = 21; |SI salte_1 [ 41; |HACIENDO salte_1 = salte_1 + 1;
          salte_2 = salte_1 + 21;
          #nomcalca#0 = #copcalcu@#salte_1#;
          #nomcalca#1 = #copcalcu@#salte_2#;
          #nomcalca#2 = nElMesIRPF;
          #nomcalca#3 = 0;
          |LEE 000#nomcalca.=;
          |SI FSalida = 0;
               |HAZ Los400;
          |FINSI;
     |SIGUE;
|FPRC;

|PROGRAMA;
     |SI nElMesIRPF ! 6; |O nElAnyIRPF ! 2008;
          |ACABA;
     |FINSI;

     |DDEF aDef;
     aDef = aDef + eaVengoDe;
     |CARGA_DEF aDef, copcalcu@;
     FCargaDef = FSalida;

     aAlfa = Usuario;         |QBF aAlfa; aAlfa = "n" +  aAlfa;
     |NOME_DAT #copcalcu@#aAlfa#;

     |SI FCargaDef ! 0;
          |ACABA;
     |FINSI;

     |ABRE #labtraba;
     |ABRE #nomcalca;
     |ABRE #nom400eu;
     |ABRE #nomimpr1;
     |ABRE #copcalcu@;
     |ABRE #nomir003;

     nTipo = 0;
     |LEE 000#copcalcu@.p;
     |SI FSalida = 0; |O swFiniq = 1;
          |SI swFiniq = 1;
               #copcalcu@#0 = nEmp;
               #copcalcu@#1 = nEmp;
               #copcalcu@#2 = nTra;
               #copcalcu@#3 = nTra;
               #copcalcu@#8 = nElMesIRPF;
               #copcalcu@#9 = nElAnyIRPF;
               #copcalcu@#21 = 0;
               nTipo = 2;
          |FINSI;
          |SI #copcalcu@#21 = 0; |Y #copcalcu@#63 = 0;
               nDesdeEmp = #copcalcu@#0;
               nHastaEmp = #copcalcu@#1;
               nDesdeTra = #copcalcu@#2;
               nHastaTra = #copcalcu@#3;
               |BUCLE DesdeHasta;
          |SINO;
               |SI #copcalcu@#63 = 0;
                    |HAZ Salteada;
               |SINO;
                    |HAZ PorGrupos;
               |FINSI;
          |FINSI;
     |FINSI;

     |CIERRA #nomimpr1;
     |CIERRA #copcalcu@;
     |CIERRA #nomir003;
     |CIERRA #nomcalca;
     |CIERRA #nom400eu;
     |CIERRA #labtraba;

     |SI FCargaDef = 0;
          |DESCARGA_DEF #copcalcu@;
     |FINSI;
|FPRO;
