|FICHEROS;
     nomz0016 #0;

     nomtmp13 #10,MANTE;
     labtraba #2;
     labpadre;
     silcon06 #20;
     silcon13 #21;
|FIN;

|VARIABLES;
   aAlfa  = "";
   aAlfa1  = "";
   aMarca = "";
   nCalc  = 0;
   nOpcion = 0;
   nNumTrab = 0;

{-1}Menu  = "";
    Menu1 = "2400";
    Menu2 = "1";
    Menu3 = "";
    Menu4 = "RPC";
    Menu5 = " Revisar ";
    Menu6 = " Preparar remesa";
    Menu7 = " Cancelar ";

     aFechaBaja = "";
     fFechaBaja = @;
     nFechaBaja = 0;
     fFecha = @;

     aFechaLim = "";
     fFechaLim = @;
     nFechaLim = 0;
     swSigue = 0;
     red_ultimo = 0;

     nEmp = 0;
     nTra = 0;
|FIN;

|PROCESO Mensaje; |TIPO 7;
     |MENSA "    <INTRO> Marcar/Desmarcar, <F5> Todos, <F6> Ninguno";
|FPRC;

|PROCESO MarcaDesmarca;
     #nomtmp13#6 = aMarca; |GRABA 020#nomtmp13.a;
|FPRC;

|DEFBUCLE MarcaDesmarca;
     #nomtmp13#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, MarcaDesmarca;
|FIN;

|PROCESO Marcalo; |TIPO 0;
     |SI FSalida = 0;
          |SI #nomtmp13#6 = "**";
               #nomtmp13#6 = "  ";
          |SINO;
               #nomtmp13#6 = "**";
          |FINSI;

          |PINTA #nomtmp13#6;
          |GRABA 020#nomtmp13.a;
     |FINSI;
     |SI FSalida = 13; || Marca todo
          nEmp = #nomtmp13#0;
          nTra = #nomtmp13#1;

          aMarca = "**"; |BUCLE MarcaDesmarca;

          #nomtmp13#0 = nEmp;
          #nomtmp13#1 = nTra;
          |LEE 000#nomtmp13.=;

          |PONCONTROL 23, "si";
     |FINSI;

     |SI FSalida = 14; || Desmarca todo
          nEmp = #nomtmp13#0;
          nTra = #nomtmp13#1;

          aMarca = "  "; |BUCLE MarcaDesmarca;

          #nomtmp13#0 = nEmp;
          #nomtmp13#1 = nTra;
          |LEE 000#nomtmp13.=;

          |PONCONTROL 23, "si";
     |FINSI;
|FPRC;

|RUTINA inf04;
   #nomtmp13#0 = 00001
   #nomtmp13#1 = 00001
|FRUT;

|RUTINA sup04;
   #nomtmp13#0 = 99999;
   #nomtmp13#1 = 99999;
|FRUT;

|PROCESO borralo1;
     |BORRA 020#21a;
     |LIBERA #21;
|FPRC;

|DEFBUCLE borralo;
     #21#2;
     14;
     #2#0, #2#1, 0;
     #2#0, #2#1, 0;
     be;
     NULL, borralo1;
|FIN;

|PROCESO el_red;
          #labtraba#0 = #nomtmp13#0;
          #labtraba#1 = #nomtmp13#1;

          |LEE 000#labtraba.=;
/*
          |DDEFECTO #labpadre;
          |ABRE #labpadre;
          #labpadre#0 = #labtraba#0;
          #labpadre#1 = #labtraba#1;
          |LEE 000#labpadre.=;
          |CIERRA #labpadre;
*/
          |ABRE #21;
          |LEE 000#21u;
          |SI FSalida ! 0;
               red_ultimo = 1;
          |SINO;
               red_ultimo = #21#0 + 1;
          |FINSI;
          |CIERRA #21;

          |BUCLE borralo;

          |ABRE #21;
          |DDEFECTO #21;
          #21#0 = red_ultimo;
          |GRABA 020#21b;

          #21#1 = #nomtmp13#0;          || empresa
          #21#2 = #nomtmp13#1;          || trabajador
          #21#3 = "MG";
          fFecha = ~;
          aAlfa = fFecha;
          aAlfa = "01" + (aAlfa % -108);
          #21#13 = aAlfa;

          |GRABA 020#21a;
          |LIBERA #21;
          |CIERRA #21;

          nNumTrab = nNumTrab + 1;
|FPRC;

|DEFBUCLE el_red;
     #nomtmp13#1;
     6;
     00001, 00001, "**";
     99999, 99999, "**";
     ;
     NULL, el_red;
|FIN;

|PROCESO Lote;
     |ABRE #labtraba;

     |BUCLE el_red;

     |CIERRA #labtraba;

     |SI nNumTrab > 0;
          aAlfa1 = nNumTrab;
          aAlfa = "    Se ha preparado la remesa de comunicación de indicador ";
          aAlfa = aAlfa + "de Cot. MENSUAL para " + aAlfa1 + " trabajadores."
          |MENAV aAlfa;

          aAlfa = "    Puede crear el lote desde la opción - Creación Lote ";
          aAlfa = aAlfa + "AFI - para el Sistema RED";
          |MENAV aAlfa;
     |FINSI;
|FPRC;

|PROCESO Trabajadores;
     aFechaBaja = #labtraba#37;
     fFechaBaja = aFechaBaja;
     nFechaBaja = fFechaBaja;
     |QBF aFechaBaja;

     aFechaLim = "31.12.2017";
     fFechaLim = aFechaLim;
     nFechaLim = fFechaLim;

     |SI aFechaBaja ! ""; |Y aFechaBaja ! "..........";
          |SI nFechaBaja [ nFechaLim;
               |ACABA;
          |FINSI;
     |FINSI;

     swSigue = 0;

     |SI #labtraba#42 = "M"; |O #labtraba#42 = "X";
          |SI #labtraba#33 ] 08;
               swSigue = 1;
          |FINSI;
     |FINSI;

     #20#0 = #labtraba#0;
     |LEE 000#20=;
     |SI FSalida ! 0;
          swSigue = 0;
     |FINSI;

     |SI swSigue = 1;
          |DDEFECTO #nomtmp13;
          #nomtmp13#0 = #labtraba#0;
          #nomtmp13#1 = #labtraba#1;
          #nomtmp13#2 = #labtraba#2;
          #nomtmp13#3 = #labtraba#45;
          #nomtmp13#4 = #labtraba#33;
          #nomtmp13#5 = #labtraba#42;
          #nomtmp13#6 = "**";
          |GRABA 020#nomtmp13.n;
          nNumTrab = nNumTrab + 1;

          |LIBERA #nomtmp13;
     |FINSI;
|FPRC;

|DEFBUCLE Trabajadores;
     #labtraba#1;
     44;
     #nomz0016#0, #nomz0016#2, "A";
     #nomz0016#1, #nomz0016#3, "A";
     ;
     NULL, Trabajadores;
|FIN;


|PROGRAMA;
     |CLS;
     |CABEZA " ";

     |PINPA #0#0; |PINDA #0#0;
     |ENDATOS #1#0;
     |SI FSalida = 0;
          aAlfa = "05" + Usuario; |NOME_DAT #nomtmp13#aAlfa#;
          |DELINDEX #nomtmp13;
          |ABRE #nomtmp13;
          nNumTrab = 0;
          |ABRE #20;
          |BUCLE Trabajadores;
          |ET OtraVez;
          |ENTLINEAL #1#nomtmp13, -1, 4, 21, 1, inf04, sup04;
          |SI nNumTrab = 0;
              |CIERRA #20;
              |CIERRA #nomtmp13;
              |DELINDEX #nomtmp13;
              |ACABA;
          |FINSI;

      |ET OtroMenu;
      |MENU Menu;
      nOpcion = FSalida;
      |SI nOpcion = 1; |VETE OtraVez;   |FINSI;
      |SI nOpcion = 2;
           nNumTrab = 0;
           |HAZ Lote;
      |FINSI;
      |SI nOpcion = 3;
         aAlfa = "    S" + &191 + " Está seguro de salir sin aplicar los cambios ?";
         |CONFI aAlfa;
         |SI FSalida ! 0; |VETE OtraVez; |FINSI;
      |FINSI;
      |SI nOpcion < 1; |VETE OtroMenu; |FINSI;
      |CIERRA #nomtmp13;   |DELINDEX #nomtmp13;
      |CIERRA #20;
     |FINSI;
|FPRO;
