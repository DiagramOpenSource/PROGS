|FICHEROS;
    gomentra #0;
    gomentr1 #1;    || temporal introduccion 903
    labtraba #2;    || trabajadores
    nomconli #3;    || convenios lins.
    nomcatli #4;    || categorias lins.
    nomtrali #5;    || trabaj. lins.
    nomvarli #6;    || variaciones lins.
    nomvarca #7;    || variaciones cabs.
    gomentr2 #8;    || temporal introduccion resto ctos.
    nomconca #9;    || convenios cabs.
    labempre #10;   || empresas
    labcentr;
    gomdetjr #11;   || detalle de dias de realizacion de jornadas
    nomtrcal #12;   || calendario laboral
    nomcontr #13;   || control aplicacion

    silcon06 #20;   || gestion empresas AGIRED
    silcon13 #21;   || movimientos AFI AGIRED
    labpadre;       || datos auxiliares (nombre padre y madre)

     gomdetj1,MANTE;
|FIN;

|VARIABLES;
    alfa1 = "";
    alfa2 = "";
    alfa3 = "";
    alfa4 = "";
    nume1 = 0;
    nume2 = 0;
    nume3 = 0;
    nume4 = 0;
    para1 = 0;
    para2 = 0;
    para3 = 0;
    para4 = 0;
    fech1 = @;
    fech2 = @;
    fech3 = @;
    fech4 = @;
    FEstado = 0;
    seleccio = 0;
    p_mes = "";

    guia = 0;
    jorna = 0;
    jornaIT = 0;
    v_101 = 0;
    cto_bus = 0;
    swcheck = 0;
    ant_jor = 0;
    d_cla = "";
    h_cla = "";
    no_vale = 0;
    swActiva = 0;

    bisiesto = 0;
    numer = 0;
    topemes = 0;
    nPara1 = 0;
    nPara2 = 0;
    nPara3 = 0;
    nCampo = 0;
    nTotal = 0;
    nDia = 0;
    nMes = 0;
    aAnyo = "";
    nNume1 = 0;
    aAlfa1 = "";
    aAlfa2 = "";
    aCalendario = "";
    mesnum = 0;
    swYaexiste = 0;

    red_ultimo = 0;
    aJornada = "";
    aAlfa3 = "";
     swEsAlta = 0;
     aFechaAlta = "";
     aMesAlta = "";
     aAnyAlta = "";
     nMesAlta = 0;
     nAnyAlta = 0;

     nPos = 0;
     aPos1 = "";
     aPos2 = "";
     nNume = 0;
     aCalendario1 = "";
     aCalendario2 = "";
     swHayOtro = 0;
     nEmpresa = 0;
     aAlfa = "";
     aCCC = "";
     aCCCOtro = "";
     nTotalIT = 0;
     nJorAlta = 0;
     nJorIT = 0;
     swInactividad = 0;
     nTecla = 0;
     nInac = 0;
     nDiasIT = 0;
     swCitrico = 0;
     swCargaInicial = 0;
     nUltimoDia = 0;
|FIN;

|INCLUYE i_tempo;
____________________________________/ PANTALLA
|PROCESO AnyoDefecto; |TIPO 7; || CALCULA EL ANYO DE DEFECTO PARA RELACIONAR CON
    fech1 = ~;                 || PARA RELACIONAR CON EL MANTENIMIENTO DE DIAS EN
    alfa1 = fech1;             || QUE SE REALIZARON LAS JORNADAS
    alfa1 = alfa1 % -104;
    #0Campo = alfa1;
    |PINTA #0Campo;
|FPRC;

|PROCESO mirames; |TIPO 7; ||CALCULA EL MES POR DEFECTO SEGUN FECHA SISTEMA
    fech1 = ~;
    alfa1 = fech1;
    alfa1 = alfa1 % 402;
    #0Campo = alfa1;
|PINTA #0Campo;
|FPRC;

|PROCESO pintames; |TIPO 0;      || PINTA EL MES LITERAL
|SI #0Campo =  1;  p_mes = "ENERO     ";  |FINSI;
|SI #0Campo =  2;  p_mes = "FEBRERO   ";  |FINSI;
|SI #0Campo =  3;  p_mes = "MARZO     ";  |FINSI;
|SI #0Campo =  4;  p_mes = "ABRIL     ";  |FINSI;
|SI #0Campo =  5;  p_mes = "MAYO      ";  |FINSI;
|SI #0Campo =  6;  p_mes = "JUNIO     ";  |FINSI;
|SI #0Campo =  7;  p_mes = "JULIO     ";  |FINSI;
|SI #0Campo =  8;  p_mes = "AGOSTO    ";  |FINSI;
|SI #0Campo =  9;  p_mes = "SEPTIEMBRE";  |FINSI;
|SI #0Campo = 10;  p_mes = "OCTUBRE   ";  |FINSI;
|SI #0Campo = 11;  p_mes = "NOVIEMBRE ";  |FINSI;
|SI #0Campo = 12;  p_mes = "DICIEMBRE ";  |FINSI;
|PINTA 742, p_mes;

|FPRC;

|PROCESO DetalleMes;
     |SI FEstado ! 13; |O #nomcontr#35 ! "M";
          |ACABA;
     |FINSI;
     |ABRE #11;
     |DDEFECTO #11;
     |PUSHV 05012380;
     #11#0 = #gomentr1#1;
     #11#1 = #gomentr1#2;
     #11#2 = #gomentra#5;
     #11#3 = #gomentra#4;
     |LEE 101#11=;
     FEstado = FSalida;
     |SI FSalida ! 0;
          |GRABA 020#11n;
     |FINSI;
     |PINPA #0#11;
     |LEE 101#11=;
     |PINDA #0#11;
     |ENDATOS #2#11;
     |SI FSalida = 0;
          |GRABA 020#11a;
     |FINSI;
     |POPV;
     |CIERRA #11;
|FPRC;

|PROCESO tipo5; |TIPO 5;
     #gomentr1#14 = "S";
     |LINEAS_BI_ATRI #gomentr1#14, 0, I, -1;
|FPRC;

|PROCESO ultimo_dia;  || halla el ultimo dia del mes
     bisiesto = 0;
     numer = #0#5 $ 4;
     |SI numer = 0;
          bisiesto = 1;
     |FINSI;
     topemes = 31;
     mesnum = #0#4;
     |SI mesnum= 2;
          topemes = 28 + bisiesto;
     |FINSI;
     |SI mesnum= 4;|O mesnum= 6;|O mesnum= 9;|O mesnum=11;
          topemes = 30;
     |FINSI;
|FPRC;

|PROCESO cambia;
     |SI #nomvarca#12 = " ";|Y #nomvarca#13 = " ";|Y #nomvarca#14 = " ";|Y #nomvarca#15 = " ";
         |SI #labtraba#89 ! 0;  #nomvarca#12 = #labtraba#60;  |FINSI;
         |SI #labtraba#92 ! 0;  #nomvarca#12 = "A";    |FINSI;
     |FINSI;

     |PARA nPara1 = 4; |SI nPara1 [ 7; |HACIENDO nPara1 = nPara1 + 1;
          nPara2 = nPara1 + 4;
          nPara3 = nPara1 + 8;
          |SI #nomvarca#nPara3# ! " ";
               |SI #nomvarca#nPara1# = 0; #nomvarca#nPara1# = 1; |FINSI;
               |SI #nomvarca#nPara2# = 0; #nomvarca#nPara2# = topemes; |FINSI;
          |FINSI;
     |SIGUE;
|FPRC;

|PROCESO LimiteJor;
     |SI #labtraba#247 ! 613; |Y #labtraba#247 ! 163;
     || solo agrarios, citricos por ejemplo NO
          |ACABA;
     |FINSI;

     |SI #labtraba#42 = "C"; |O #labtraba#42 = "F";
          |ACABA;
     |FINSI;

     nume1 = #gomentr1#4 + #gomentr1#13;
     alfa1 = #gomentr1#1; alfa1 = "00000" + alfa1; alfa1 = alfa1 % -105;
     alfa2 = #gomentr1#2; alfa2 = "00000" + alfa2; alfa2 = alfa2 % -105;
     |SI #0#5 = 2009;
          alfa3 = "2009";
          alfa4 = "24";
          |SI nume1 > 24;
               aAlfa = "    ATENCION: el trabajador " + alfa1 + "." + alfa2;
               aAlfa = aAlfa + " supera el límite máximo de " + alfa4  + " jornadas cotizables para ";
               aAlfa = aAlfa + alfa3;
               |MENAV aAlfa;
               |ERROR;
          |FINSI;
     |FINSI;
     |SI #0#5 ] 2010;
          || en los convenios con conceptos 101 que sean D1 o M1 no tienen
          || que limitar las jornadas a 23 en la introduccion ya que se cobra
          || segun las jornadas introducidas y se puede cobrar lo que se quiera
          || siempre que luego la nomina limite a 23

          |ABRE #nomconli;
          #nomconli#0 = #labtraba#87;
          #nomconli#2 = 101;
          |LEE 000#nomconli.=;
          |SI FSalida = 0;
               |SI #nomconli#3 = "D"; |O #nomconli#3 = "M";
                    |SI #nomconli#4 = 1;
                         |CIERRA #nomconli;
                         |ACABA;
                    |FINSI;
               |FINSI;
          |FINSI;
          |CIERRA #nomconli;

          |SI #0#5 [ 2011;
               alfa3 = #0#5;
               alfa4 = "23";
               |SI nume1 > 23;
                    aAlfa = "    ATENCION: el trabajador " + alfa1 + "." + alfa2;
                    aAlfa = aAlfa + " supera el límite máximo de " + alfa4  + " jornadas cotizables para ";
                    aAlfa = aAlfa + alfa3;
                    |MENAV aAlfa;
                    |ERROR;
               |FINSI;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Automatico;
     FEstado = FSalida;

     nJorAlta = 0;
     nJorIT = 0;

     |ABRE #nomcontr;
     |LEE 000#nomcontr.p;
     |CIERRA #nomcontr;

     |SI #nomcontr#35 ! "A";
          |ACABA;
     |FINSI;

     |SI swCargaInicial = 0;
          || en la carga inicial no, que ya estoy en eun bucle sobre el labtraba
          || solo si estoy en la linea editando trabajadores

          |ABRE #labtraba;                   || busco en el labtraba para ver si
          #labtraba#0 = #gomentr1#1;         || estoy arrastrando alguna IT y
          #labtraba#1 = #gomentr1#2;         || para coger el calendario DEL
          |LEE 000#labtraba.=;               || TRABAJADOR
          |CIERRA #labtraba;

          |SI #labtraba#42 = "C"; |O #labtraba#42 = "F";
               swCitrico = 1;
          |SINO;
               swCitrico = 0;
          |FINSI;
     |FINSI;

     |HAZ LimiteJor;

     |ABRE #gomdetjr;
     |DDEFECTO #gomdetjr;
     #gomdetjr#0 = #gomentr1#1;
     #gomdetjr#1 = #gomentr1#2;
     #gomdetjr#2 = #gomentra#5;
     #gomdetjr#3 = #gomentra#4;
     |LEE 101#gomdetjr.=;
     |SI FSalida = 0;
          |PARA nCampo = 4; |SI nCampo [ 34; |HACIENDO nCampo = nCampo + 1;
               |SI #gomdetjr#nCampo# = "X";
                    nJorAlta = nJorAlta + 1;
               |FINSI;
               |SI #gomdetjr#nCampo# = "I";
                    nJorIT = nJorIT + 1;
               |FINSI;
          |SIGUE;
          |SI nJorAlta = #1#4; |Y nJorIT = #1#13;
               |LIBERA #gomdetjr;
               |ACABA;
               || tenia ya jornadas introducidas y coinciden con las de alta
               || e IT del lineal, puede ser que el usuario haya variado
               || los dias, asi que no las borro, lo dejo tal cual este
          |SINO;
               |PARA nCampo = 4; |SI nCampo [ 34; |HACIENDO nCampo = nCampo + 1;
                    #gomdetjr#nCampo# = " ";
               |SIGUE;
          |FINSI;
          swYaexiste = 1;
     |SINO;
          swYaexiste = 0;
     |FINSI;

     alfa1 = #labtraba#36;              || comprobamos fecha de alta
     alfa2 = alfa1 % 102;               || dia
     alfa3 = alfa1 % 402;               || mes
     alfa4 = alfa1 % -104;              || a¤o

     nume2 = alfa2;
     nume3 = alfa3;
     nume4 = alfa4;

     |SI nume3 = #gomentra#4; |Y nume4 = #gomentra#5;
          nume2 = nume2 + 3;
          |PARA nCampo = 4; |SI nCampo < nume2; |HACIENDO nCampo = nCampo + 1;
               #gomdetjr#nCampo# = "x"; || dia no disponible
                                        || por fechas de alta/baja
          |SIGUE;
     |FINSI;

     ||  busco fecha de baja en el nomvarca
     alfa1 = "";
     |ABRE #nomvarca;
     #nomvarca#0 = #gomentr1#1;
     #nomvarca#1 = #gomentr1#2;
     #nomvarca#2 = #gomentra#4;
     #nomvarca#3 = 0;
     |LEE 000#nomvarca.=;
     |SI FSalida = 0;
          alfa1 = #nomvarca#16;
     |FINSI;
     |CIERRA #nomvarca;

     |SI alfa1 = ".........."; alfa1 = ""; |FINSI;
     |QBF alfa1;
     |SI alfa1 = "";     || no tengo fecha baja en nomvarca, busco en labtraba
          alfa1 = #labtraba#37;
     |FINSI;
     |SI alfa1 = ".........."; alfa1 = ""; |FINSI;
     |QBF alfa1;
     |SI alfa1 ! "";     || hay alguna fecha de baja: nomvarca o labtraba
          alfa2 = alfa1 % 102;               || dia
          alfa3 = alfa1 % 402;               || mes
          alfa4 = alfa1 % -104;              || a¤o

          nume2 = alfa2;
          nume3 = alfa3;
          nume4 = alfa4;

          |SI nume3 = #gomentra#4; |Y nume4 = #gomentra#5;
               nume2 = nume2 + 4;
               |PARA nCampo = nume2; |SI nCampo < 35; |HACIENDO nCampo = nCampo + 1;
                    #gomdetjr#nCampo# = "x"; || dia no disponible
                                             || por fechas de alta/baja
               |SIGUE;
          |FINSI;
     |FINSI;

     |ABRE #nomtrcal;
     aAnyo = #0#5;
     #nomtrcal#0 = #labtraba#156;       || defecto calendario laboral
     #nomtrcal#13 = aAnyo;
     |LEE 000#nomtrcal.=;
     nMes = #0#4;
     aCalendario = #nomtrcal#nMes#;
     |CIERRA #nomtrcal;
     |HAZ ultimo_dia;
     |PARA nDia = 1; |SI nDia [ topemes; |HACIENDO nDia = nDia + 1;
          nCampo = nDia + 3;
          nNume1 = (nDia * 100) + 1;
          aAlfa1 = aCalendario % nNume1;
          |SI aAlfa1 = 1;
               |SI #gomdetjr#nCampo# ! "x";
                    || si es igual a "x" ya lo habia marcado como no disponible
                    || debido a fechas de alta/baja, por tanto no ha lugar a que
                    || se marque como dia festivo

                    #gomdetjr#nCampo# = "y"; || y minuscula: dia no disponible
                                             || por dia no laborable calendario
               |FINSI;
          |FINSI;
     |SIGUE;

     |ABRE #nomvarca;
     #nomvarca#0 = #gomentr1#1;
     #nomvarca#1 = #gomentr1#2;
     #nomvarca#2 = #gomentra#4;
     #nomvarca#3 = 0;
     |LEE 000#nomvarca.=;
     |HAZ cambia;   || controla las bajas tipo "del dia 1 al dia 0"
     |SI FSalida = 0;
          |PARA nPara1 = 4; |SI nPara1 [ 7; |HACIENDO nPara1 = nPara1 + 1;
               |SI #nomvarca#nPara1# ! 0;
                    nPara2 = nPara1 + 4;
                    |PARA nDia = #nomvarca#nPara1#; |SI nDia [ #nomvarca#nPara2#; |HACIENDO nDia = nDia + 1;
                         nCampo = nDia + 3;

                         |SI swCitrico = 0;
                              |SI #gomdetjr#nCampo# ! "x"; |Y #gomdetjr#nCampo# ! "y";
                                                            || si es "x" no es alta y
                                                            || si es "y" no es laborable
                                   #gomdetjr#nCampo# = "i"; || CANDIDATO JORNADA IT
                              |FINSI;
                         |FINSI;
                         |SI swCitrico = 1;
                              |SI #gomdetjr#nCampo# ! "x";
                                   || en citricos no puedo usar este dia si es "x" (no alta)
                                   || si es no laborable si que lo puedo usar, que ya que
                                   || cuento las ITs coo DIAS no como jornadas
                                   #gomdetjr#nCampo# = "i"; || CANDIDATO DIA IT
                              |FINSI;
                         |FINSI;
                    |SIGUE;
               |FINSI;
          |SIGUE;
     |FINSI;
     |CIERRA #nomvarca;

     nTotal = 0;
     nTotalIT = 0;
     |PARA nDia = 1; |SI nDia [ topemes; |HACIENDO nDia = nDia + 1;
          nCampo = nDia + 3;
          |SI #gomdetjr#nCampo# = "x"; |O #gomdetjr#nCampo# = "y";
          |O #gomdetjr#nCampo# = "i";
               || #gomdetjr#nCampo# = " ";
               || despues veo si tiene IT asignable a este dia,
               || y despues si no, se quedara en blanco para el AFI
          |SINO;
               |SI nTotal ! #gomentr1#4;
                    #gomdetjr#nCampo# = "X";
                    nTotal = nTotal + 1;
               |FINSI;
          |FINSI;
          |SI #gomdetjr#nCampo# = "i";
               |SI nTotalIT ! #gomentr1#13;
                    #gomdetjr#nCampo# = "I";
                    nTotalIT = nTotalIT + 1;
               |FINSI;
          |FINSI;
     |SIGUE;

     || segundo repaso: ya estan asignadas las jornadas en alta y las en IT
     || cuando tengo dias en IT, ahora faltan las de IT si no he metido antes
     || los dias por variaciones (lo logico)

     |SI nTotalIT < #gomentr1#13;
          |PARA nDia = 1; |SI nDia [ topemes; |HACIENDO nDia = nDia + 1;
               nCampo = nDia + 3;
               |SI nTotalIT ! #gomentr1#13;
                    |SI swCitrico = 0;
                         |SI #gomdetjr#nCampo# = "i"; |O #gomdetjr#nCampo# = " ";
                              || dia disponible para jornada en IT;
                              #gomdetjr#nCampo# = "I";
                              nTotalIT = nTotalIT + 1;
                         |FINSI;
                    |FINSI;
                    |SI swCitrico = 1;
                         |SI #gomdetjr#nCampo# = "i"; |O #gomdetjr#nCampo# = " ";
                         |O #gomdetjr#nCampo# = "y";
                              || dia disponible para jornada en IT;
                              #gomdetjr#nCampo# = "I";
                              nTotalIT = nTotalIT + 1;
                         |FINSI;
                    |FINSI;
               |FINSI;
          |SIGUE;
     |FINSI;

     || ultimo repaso: solo me quedo con "X" o "I"

     |PARA nDia = 1; |SI nDia [ topemes; |HACIENDO nDia = nDia + 1;
          nCampo = nDia + 3;
          |SI #gomdetjr#nCampo# ! "X"; |Y #gomdetjr#nCampo# ! "I";
               |SI swCitrico = 0;
                    #gomdetjr#nCampo# = " ";
               |FINSI;
               |SI swCitrico = 1;
                    |SI #gomdetjr#nCampo# = "x"; |O #gomdetjr#nCampo# = "y";
                         |SI #gomdetjr#nCampo# = "x";
                              #gomdetjr#nCampo# = " ";
                         |FINSI;
                         |SI #gomdetjr#nCampo# = "y";
                              |SI #nomcontr#71 = "S";
                                   #gomdetjr#nCampo# = "D";
                              |SINO;
                                   #gomdetjr#nCampo# = " ";
                              |FINSI;
                         |FINSI;
                    |SINO;
                         #gomdetjr#nCampo# = "D";
                    |FINSI;
               |FINSI;
          |FINSI;
     |SIGUE;

     |SI nTotal < #gomentr1#4; |Y swCargaInicial = 0;
          aAlfa2 = #gomentr1#4;
          aAlfa1 = "    ATENCION. El número de Jornadas especificado (" + aAlfa2;
          aAlfa1 = aAlfa1 + ") supera el número de días laborables disponibles (";
          aAlfa2 = nTotal;
          aAlfa1 = aAlfa1 + aAlfa2 + ")";
          |MENAV aAlfa1;
     |FINSI;

     |SI swYaexiste = 1;
          |GRABA 020#gomdetjr.a;
     |SINO;
          |GRABA 020#gomdetjr.n;
     |FINSI;
     |CIERRA #gomdetjr;
|FPRC;
____________________________________/ CARGA DE TRABAJADORES
|PROCESO lee_convenio1;
    no_vale = 0;

|SI #3#2 ] 201;|Y #3#2 [ 299;      no_vale = 1;   |FINSI;
|SI #3#2 = 102;|Y #9#2 > 1;        no_vale = 1;   |FINSI;
|SI #3#2 = 103;|Y #9#3 > 1;        no_vale = 1;   |FINSI;
|SI #3#3 ! "F";|Y #3#3 ! "V";      no_vale = 1;   |FINSI;

|SI no_vale = 0;
        #8#2 = #3#2;
        #8#3 = #3#6;
    |GRABA 020#8n;
|FINSI;
|FPRC;

|DEFBUCLE lee_convenio;
 #3#1;
 ;
 #1#10, 101;
 #1#10, 499;
 ;
 NULL, lee_convenio1;
|FIN;

|PROCESO busca_var1;
|SI cto_bus = 101;  v_101 = #6#6;  |FINSI;
|SI cto_bus = 903;  jorna = #6#5;  |FINSI;
|SI cto_bus = 922;  jornaIT = #6#5;  |FINSI;
|FPRC;

|DEFBUCLE busca_var;
 #6#1;
 ;
 #2#0, #2#1, #0#4, 0, cto_bus;
 #2#0, #2#1, #0#4, 9, cto_bus;
 e;
 NULL, busca_var1;
|FIN;

|PROCESO busca_tra1;
|SI cto_bus = 101;  v_101 = #5#4;  |FINSI;
|SI cto_bus = 903;  jorna = #5#3;  |FINSI;
|SI cto_bus = 922;  jornaIT = #5#3;  |FINSI;
|FPRC;

|DEFBUCLE busca_tra;
 #5#1;
 ;
 #2#0, #2#1, cto_bus;
 #2#0, #2#1, cto_bus;
 e;
 NULL, busca_tra1;
|FIN;

|PROCESO busca_cat1;
|SI cto_bus = 101;  v_101 = #4#4;  |FINSI;
|SI cto_bus = 903;  jorna = #4#3;  |FINSI;
|SI cto_bus = 922;  jornaIT = #4#3;  |FINSI;
|FPRC;

|DEFBUCLE busca_cat;
 #4#1;
 ;
 #2#87, #2#17, cto_bus;
 #2#87, #2#17, cto_bus;
 e;
 NULL, busca_cat1;
|FIN;

|PROCESO busqueda;
|BUCLE busca_cat;
|BUCLE busca_tra;
|BUCLE busca_var;
|FPRC;

|PROCESO esaltasino;
     swEsAlta = 1;
     aFechaAlta = #2#36;
     aMesAlta = aFechaAlta % 402;
     aAnyAlta = aFechaAlta % 704;
     nMesAlta = aMesAlta;
     nAnyAlta = aAnyAlta;
     |SI #0#5 < aAnyAlta;
          swEsAlta = 0;
     |FINSI;
     |SI #0#5 = aAnyAlta; |Y #0#4 < nMesAlta;
          swEsAlta = 0;
     |FINSI;
|FPRC;

|PROCESO LeeCalendario;
     nInac = 0;
     |ABRE #gomdetjr;
     #gomdetjr#0 = #1#1;
     #gomdetjr#1 = #1#2;
     #gomdetjr#2 = #0#5;
     #gomdetjr#3 = #0#4;
     |LEE 000#gomdetjr.=;
     |SI FSalida = 0;
          |PARA nCampo = 4; |SI nCampo [ 34; |HACIENDO nCampo = nCampo + 1;
               |SI #labtraba#42 = "C"; |O #labtraba#42 = "F";
               || citricos
                    |SI #gomdetjr#nCampo# = "D";
                         nInac = nInac + 1;
                    |FINSI;
               |FINSI;
          |SIGUE;
     |FINSI;
|FPRC;

|PROCESO FechaBaja;
     ||  busco fecha de baja en el nomvarca
     alfa1 = "";
     |ABRE #nomvarca;
     #nomvarca#0 = #labtraba#0;
     #nomvarca#1 = #labtraba#1;
     #nomvarca#2 = #gomentra#4;
     #nomvarca#3 = 0;
     |LEE 000#nomvarca.=;
     |SI FSalida = 0;
          alfa1 = #nomvarca#16;
     |FINSI;
     |CIERRA #nomvarca;

     |SI alfa1 = ".........."; alfa1 = ""; |FINSI;
     |QBF alfa1;
     |SI alfa1 = "";     || no tengo fecha baja en nomvarca, busco en labtraba
          alfa1 = #labtraba#37;
     |FINSI;
     |SI alfa1 = ".........."; alfa1 = ""; |FINSI;
     |QBF alfa1;
     |SI alfa1 ! "";     || hay alguna fecha de baja: nomvarca o labtraba
          alfa2 = alfa1 % 102;               || dia
          alfa3 = alfa1 % 402;               || mes
          alfa4 = alfa1 % -104;              || a¤o

          nume2 = alfa2;
          nume3 = alfa3;
          nume4 = alfa4;

          |SI nume3 = #gomentra#4; |Y nume4 = #gomentra#5;
               nUltimoDia = nume2;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO buscaIT;
     |HAZ ultimo_dia;
     nUltimoDia = topemes;
     |HAZ FechaBaja;
     nDiasIT = 0;

     |ABRE #nomvarca;
     #nomvarca#0 = #labtraba#0;
     #nomvarca#1 = #labtraba#1;
     #nomvarca#2 = #gomentra#4;
     #nomvarca#3 = 0;
     |LEE 000#nomvarca.=;
     |SI FSalida = 0;
          |PARA nPara1 = 4; |SI nPara1 [ 7; |HACIENDO nPara1 = nPara1 + 1;
               nPara2 = nPara1 + 4;
               nPara3 = nPara1 + 8;
               |SI #nomvarca#nPara3# ! " "; |Y #nomvarca#nPara3# ! "V";
                    |SI #nomvarca#nPara1# = 0; #nomvarca#nPara1# = 1; |FINSI;
                    |SI #nomvarca#nPara2# = 0; #nomvarca#nPara2# = nUltimoDia; |FINSI;
                    nDiasIT = nDiasIT + (#nomvarca#nPara2# - #nomvarca#nPara1# + 1);
               |FINSI;
          |SIGUE;
     |FINSI;
     |CIERRA #nomvarca;
|FPRC

|PROCESO lee_traba1;
     |SI d_cla = "C";
          |SI #2#42 = "E";
               |ACABA;
          |FINSI;
     |FINSI;
     |HAZ activasino;
     |HAZ esaltasino;
     |SI swActiva = 1; |Y swEsAlta = 1;
          swCitrico = 0;
          |SI #2#42 = "C"; |O #2#42 = "F";
               swCitrico = 1;
          |FINSI;

          jorna = 0;
          jornaIT = 0;
          cto_bus = 101;  |HAZ busqueda;
          cto_bus = 903;  |HAZ busqueda;
          cto_bus = 922;  |HAZ busqueda;
          |SI swCitrico = 1;
               |HAZ buscaIT;
               |SI nDiasIT ! jornaIT;
                    jornaIT = nDiasIT;
               |FINSI;
          |FINSI;
          |DDEFECTO #1;
          guia = guia + 1;
          #1#0 = guia;          || nro. linea
          #1#1 = #2#0;          || cod. empresa
          #1#2 = #2#1;          || cod. trabajador
          |SI #2#42 = "e";
               #1#9 = "E";         || clave dias salario
          |SINO;
               #1#9 = #2#42;         || clave dias salario
          |FINSI;
          |SI #1#9 = "E";  #1#3 = "EVEN";  |FINSI;          || tipo
          |SI swCitrico = 1; #1#3 = "CITR"; |FINSI;
          #1#4 = jorna;         || jornadas a computar
          #1#13 = jornaIT;         || jornadas IT a computar
          #1#8 = v_101;         || valor concepto 101
          |SI #1#9 = "E";  #1#5 = #1#4 * #1#8;  |FINSI;
          |SI #1#9 ! "E";  #1#5 = 30   * #1#8;  |FINSI;     || importe a computar
          #1#6 = 0;             || diferencia
          #1#7 = 0;             || concepto
          #1#10 = #2#87;        || convenio
          #1#11 = #2#17;        || categoria
          |SI swCitrico = 1;
               swCargaInicial = 1;
               |HAZ Automatico;
               swCargaInicial = 0;
               |HAZ LeeCalendario;
               |SI nInac ! 0;
                    #1#14 = "S";
               |FINSI;
               #1#15 = #gomdetjr#35;
          |SINO;
               #1#15 = #1#4 + #1#13;
          |FINSI;
          |GRABA 020#1n;
          |SI FSalida = 0;
               |DDEFECTO #9;
               |ABRE #9;
               #9#0 = #1#10;
               |LEE 000#9=;
               |CIERRA #9;

               |DDEFECTO #8;
               #8#0 = #1#1;     || empresa
               #8#1 = #1#2;     || trabajador
               #8#4 = #1#4;     || jornadas 903 (por defecto)

               |SI #1#9 ! "E";
                    |BUCLE lee_convenio;  || en los eventuales no hay carga automatica
               |FINSI;
          |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE lee_traba;
 #2#4;
 44;
 d_cla, #0#0, #0#2, "A";
 h_cla, #0#1, #0#3, "A";
 ;
 NULL, lee_traba1;
|FIN;

|PROCESO carga_red;
     |SI #1#4 > 0; |O #1#13 > 0; |O #1#14 > 0;
          |HAZ el_red;
     |FINSI;
|FPRC;

|DEFBUCLE carga_red;
     #1#1;
     ;
     INICIO;
     FINAL;
     e;
     NULL, carga_red;
|FIN;

|PROCESO tipo3;
|ATRI P;
|MENSA "    Cargando Temporal ...";
|ATRI 0;
|HAZ nom_usu;
    alfa1 = "1" + (nom_tmp % 2);   |NOME_DAT #1 alfa1; |DELINDEX #1;
    alfa1 = "2" + (nom_tmp % 2);   |NOME_DAT #8 alfa1; |DELINDEX #8;
|ABRE #1;
|ABRE #8;
     |SI #0#7 = "A"; |O #0#7 = "T";
          d_cla = "E";    h_cla = "E";   |BUCLE lee_traba;
     |FINSI;
     |SI #0#7 = "C"; |O #0#7 = "T";
          d_cla = "C";    h_cla = "F";   |BUCLE lee_traba;
     |FINSI;
|CIERRA #1;
|CIERRA #8;
|BLIN 4;
|ENTLINEAL #1#1, 1, 3, 22, 0, min, max;
|CONFI "    SRealizar la Carga en Variaciones (S/N): ";
|SI FSalida = 0;
    |HAZ cargalo;
|FINSI;
|CONFI "    SRealizar la carga del Lote AFI (Accion MJR) (S/N): ";
|SI FSalida = 0;
    |BUCLE carga_red;
|FINSI;
|DELINDEX #1;
|DELINDEX #8;
|FPRC;

|PROCESO min;
    #1#0 = 1;
|FPRC;

|PROCESO max;
    #1#0 = guia;
|FPRC;
____________________________________/ CALCULOS DE LOS ENT LINEAL
|PROCESO tipo11; |TIPO 11;
     |SI FSalida = 13;
          |PUSHV 0501 2380;
          |DDEFECTO #2;
          |ABRE #2;
          #2#0 = #1#1;
          #2#1 = #1#2;
          |LEE 000#2];
          |PINPA #0#2;
          |PINDA #0#2;
          |CONSULTA #2#2;
          |CIERRA #2;
          |POPV;
     |FINSI;
|FPRC;

|PROCESO fijos; |TIPO 0;
|SI #1#9 ! "E";
        alfa1 = "N";
|SINO;
        alfa1 = "S";
|FINSI;
|CAMPO_MODIFICA #1#5, 1, alfa1;
|CAMPO_MODIFICA #1#7, 1, alfa1;
|FPRC;

|PROCESO difer; |TIPO 0;
|SI #1#9 = "E";
        #1#6 = #1#5 - (#1#8 * #1#4);    || halla la diferencia
    |PINTA #1#6;
    |SI #1#6 = 0;
            alfa1 = "N";
    |SINO;
            alfa1 = "S";
    |FINSI;
    |CAMPO_MODIFICA #1#7, 1, alfa1;
|FINSI;
|FPRC;

|PROCESO conce; |TIPO 0;
|SI #1#7 ! 0;
    |ABRE #3;
        #3#0 = #1#10;
        #3#2 = #1#7;
    |LEE 000#3=;
    |SI FSalida ! 0;
        |MENAV "    ATENCION!!! Concepto inexistente en Convenio ...";
        |ERROR;
    |SINO;
        |HAZ checcon4;
        |SI swcheck = 0;
            |MENAV "    ATENCION!!! Concepto fuera de rango ...";
            |ERROR;
        |SINO;
                #1#12 = #3#6;      || descripcion
            |PINTA #1#12;
        |FINSI;
    |FINSI;
    |CIERRA #3;
|FINSI;
|FPRC;

|PROCESO checcon4; |TIPO 0;     || chequea rangos de concepto
    swcheck = 0;
|SI #3#2 ] 101; |Y #3#2 [ 199; swcheck = 1; |FINSI;
|SI #3#2 ] 301; |Y #3#2 [ 310; swcheck = 1; |FINSI;
|SI #3#2 ] 401; |Y #3#2 [ 420; swcheck = 1; |FINSI;
|SI #3#2 ] 901; |Y #3#2 [ 999; swcheck = 1; |FINSI;
|FPRC;

|PROCESO antes; |TIPO 7;
    ant_jor = #1#4;
|FPRC;

|PROCESO impor; |TIPO 0;
|SI ant_jor ! #1#4;|Y #1#9 = "E";       || solo eventuales
        #1#5 = #1#4 * #1#8;
    |PINTA #1#5;
|FINSI;
|FPRC;

|PROCESO menos;
    #8#0 = #1#1;
    #8#1 = #1#2;
    #8#2 = 1;
|FPRC;

|PROCESO mas;
    #8#0 = #1#1;
    #8#1 = #1#2;
    #8#2 = 999;
|FPRC;

|PROCESO resto_ctos; |TIPO 1;
     |SI #0#6 = "S";
          |PUSHV 0501 2380;
          |PINPA #0#8;
          |ENTLINEAL #1#8, 3, 3, 20, 0, menos, mas;
          |POPV;
     |FINSI;
|FPRC;

|PROCESO conce2; |TIPO 0;
    |ABRE #3;
        #3#0 = #1#10;
        #3#2 = #8#2;
    |LEE 000#3=;
    |SI FSalida ! 0;
        |MENAV "    ATENCION!!! Concepto inexistente en Convenio ...";
        |ERROR;
    |SINO;
        |HAZ checcon4;
        |SI swcheck = 0;
            |MENAV "    ATENCION!!! Concepto fuera de rango ...";
            |ERROR;
        |SINO;
                #8#3 = #3#6;      || descripcion
            |PINTA #8#3;
        |FINSI;
    |FINSI;
    |CIERRA #3;
|FPRC;
____________________________________/ CARGA DE VARIACIONES
|PROCESO temporal2_1;
     |SI #8#4 ! 0;
          |DDEFECTO #7;
          #7#0 = #8#0;    || empresa
          #7#1 = #8#1;    || trabajador
          #7#2 = #0#4;    || mes
          #7#3 = 0;       || tipo
          |LEE 000#7=;
          |SI FSalida ! 0;  |GRABA 020#7n;  |FINSI;

          |DDEFECTO #6;
          #6#0 = #8#0;
          #6#1 = #8#1;
          #6#2 = #0#4;
          #6#3 = 0;
          #6#4 = #8#2;
          |LEE 101#6=;
          FEstado = FSalida;
          #6#5 = #8#4;         || jornadas
          #6#6 = "";           || valor
          |SI FEstado = 0;  |GRABA 020#6a;  |FINSI;
          |SI FEstado ! 0;  |GRABA 020#6n;  |FINSI;
          |LIBERA #6;
     |FINSI;
|FPRC;

|DEFBUCLE temporal2;
 #8#1;
 ;
     1,     1,   1;
 99999, 99999, 999;
 e;
 NULL, temporal2_1;
|FIN;

|PROCESO temporal1_1;
     |DDEFECTO #7;
     #7#0 = #1#1;    || empresa
     #7#1 = #1#2;    || trabajador
     #7#2 = #0#4;    || mes
     #7#3 = 0;       || tipo
     |LEE 000#7=;
     |SI FSalida ! 0;  |GRABA 020#7n;  |FINSI;

     |SI #1#4 ! 0;
          |DDEFECTO #6;
          #6#0 = #1#1;
          #6#1 = #1#2;
          #6#2 = #0#4;
          #6#3 = 0;
          #6#4 = 903;
          |LEE 101#6=;
          FEstado = FSalida;
          aAlfa = #1#4;
          |QBF aAlfa;
          aAlfa = ("          " + aAlfa) % -110;
          #6#5 = aAlfa;        || jornadas
          #6#6 = "Jornadas";   || valor
          |SI FEstado = 0;  |GRABA 020#6a;  |FINSI;
          |SI FEstado ! 0;  |GRABA 020#6n;  |FINSI;
          |LIBERA #6;
     |FINSI;

     |SI #1#13 ! 0;
          |DDEFECTO #6;
          #6#0 = #1#1;
          #6#1 = #1#2;
          #6#2 = #0#4;
          #6#3 = 0;
          #6#4 = 922;
          |LEE 101#6=;
          FEstado = FSalida;
          aAlfa = #1#13;
          |QBF aAlfa;
          aAlfa = ("          " + aAlfa) % -110;
          #6#5 = aAlfa;           || jornadas
          #6#6 = "Jornadas IT";   || valor
          |SI FEstado = 0;  |GRABA 020#6a;  |FINSI;
          |SI FEstado ! 0;  |GRABA 020#6n;  |FINSI;
          |LIBERA #6;
     |FINSI;

     |SI #1#7 ! 0;|Y #1#6 ! 0;          || concepto receptor
          |DDEFECTO #6;
          #6#0 = #1#1;
          #6#1 = #1#2;
          #6#2 = #0#4;
          #6#3 = 0;
          #6#4 = #1#7;
          |LEE 101#6=;
          FEstado = FSalida;
          #6#5 = 1;             || unidades
          #6#6 = #1#6;          || valor (diferencia)
          |SI FEstado = 0;  |GRABA 020#6a;  |FINSI;
          |SI FEstado ! 0;  |GRABA 020#6n;  |FINSI;
          |LIBERA #6;
     |FINSI;
|FPRC;

|DEFBUCLE temporal1;
 #1#1;
 ;
    1;
 9999;
 e;
 NULL, temporal1_1;
|FIN;

|PROCESO cargalo;
     |ATRI P;
     |MENSA "    Cargando Variaciones ...";
     |ATRI 0;
     |ABRE #7;
     |ABRE #6;
     |BUCLE temporal1;
     |SI #0#6 = "S";
          |BUCLE temporal2;
     |FINSI;
     |CIERRA #7;
     |CIERRA #6;
     |BLIN 4;
|FPRC;

|PROCESO activasino;
     swActiva = 1;
     |ABRE #10;
     #10#0 = #2#0;
     |LEE 000#10=;
     |SI #10#50 = "S";
          swActiva = 0;  || empresa no activa
     |FINSI;
     |CIERRA #10;
|FPRC;

|PROCESO borralo1;
     |SI #21#40 ! #0#5; |O #21#41 ! #0#4;
          |LIBERA #21;
          |ACABA;
     |FINSI;

     |BORRA 020#21a;
     |LIBERA #21;
|FPRC;

|DEFBUCLE borralo;
     #21#2;
     3, 14;
     #2#0, #2#1, "MJR", 0;
     #2#0, #2#1, "MJR", 0;
     be;
     NULL, borralo1;
|FIN;

|PROCESO silcon13;
     #labtraba#0 = #silcon13#1;         || si el trabajador que encuentro
     #labtraba#1 = #silcon13#2;         || pertenece a otro CCC no tengo que
     |LEE 000#labtraba.=;               || fusionar los dos calendarios, en
                                        || ese caso tienen que salir los dos
     #labcentr#0 = #labtraba#0;
     #labcentr#1 = #labtraba#77;
     |LEE 000#labcentr.=;
     aCCCOtro = #labcentr#10;
     |QBF aCCCOtro;
     |SI aCCCOtro = "";
          #labempre#0 = #labtraba#0;
          |LEE 000#labempre.=;
          aCCCOtro = #labempre#13;
          |QBF aCCCOtro;
     |FINSI;

     |SI aCCC ! aCCCOtro;
          |ACABA;
     |FINSI;

     |SI #21#40 ! #0#5; |O #21#41 ! #0#4;
          |ACABA;
     |FINSI;

     aAlfa1 = #21#42;
     |QBF aAlfa1;
     aAlfa2 = aCalendario;
     |QBF aAlfa2;

     |PARA nDia = 1; |SI nDia [ 31; |HACIENDO nDia = nDia + 1;
          nPos = (nDia * 100) + 1;
          aPos1 = aAlfa1 % nPos;
          aPos2 = aAlfa2 % nPos;
          nNume = ((nDia - 1) + 100);
          aCalendario1 = aCalendario % nNume;
          nNume = ((nDia + 1) * 100) + 31;
          aCalendario2 = aCalendario % nNume;
          |SI aPos1 = "S"; |O aPos2 = "S";
               aCalendario = aCalendario1 + "S" + aCalendario2;
          |SINO;
               aCalendario = aCalendario1 + "N" + aCalendario2;
          |FINSI;
     |SIGUE;
     swHayOtro = 1;
     #21#42 = aCalendario;
     |GRABA 020#silcon13.a;
     |LIBERA #silcon13;
|FPRC;

|DEFBUCLE silcon13;
     #21#4;
     ;
     00000, #labtraba#75, 000001;
     00000, #labtraba#75, 999999;
     be;
     NULL, silcon13;
|FIN;

|PROCESO el_red;
     |ABRE #20;
     #20#0 = #gomentr1#1;
     |LEE 000#20=;
     |SI FSalida = 0;
          |ABRE #labempre;
          |ABRE #labcentr;

          |ABRE #labtraba;
          #labtraba#0 = #gomentr1#1;
          #labtraba#1 = #gomentr1#2;
          |LEE 000#labtraba.=;
/*
          |DDEFECTO #labpadre;
          |ABRE #labpadre;
          #labpadre#0 = #labtraba#1;
          #labpadre#1 = #labtraba#2;
          |LEE 000#labpadre.=;
          |CIERRA #labpadre;
*/
          |ABRE #21;
          |LEE 000#21u;
          |SI FSalida ! 0;
               red_ultimo = 1;
          |SINO;
               red_ultimo = #21#0 + 1;
          |FINSI;
          |CIERRA #21;

          |BUCLE borralo;

          |ABRE #21;
          |DDEFECTO #21;
          #21#0 = red_ultimo;
          |GRABA 020#21b;

          #21#1 = #gomentr1#1;          || empresa
          #21#2 = #gomentr1#2;          || trabajador
          #21#3 = "MJR";                || accion:comunicacion jornadas reales

          #21#5 = "";           || mod.duracion 2104/84
          |SI #2#45 ] "014";|Y #2#45 [ "017";   #21#5 = #2#45; |FINSI;
          #21#6 = " ";         || calif. per. horas
          #21#7 = 0;      || nro. horas contrato (A)
          #21#8 = 0;      || nro. horas empresa/convenio (B)
          #21#12 = 0;
          #21#13 = "";     || fecha incidencia
          #21#14 = 0;         || nro. de lote
          #21#15 = 0;   || nro. minutos contrato (A)
          #21#16 = 0;   || nro. minutos empresa/convenio (B)
          || #21#17 = #labpadre#2;    || nombre padre
          || #21#18 = #labpadre#3;    || nombre madre
          #21#17 = "";             || nombre padre
          #21#18 = "";             || nombre madre
          #21#19 = #labtraba#244;  || telefono
          #21#20 = "0";                           || desempleado: no inscrito
          #21#21 = "N";
          #21#22 = "N";
          #21#23 = "N";
          #21#24 = 0;                        || grado minusvalia
          #21#25 = "0";
          #21#30 = "N";
          #21#31 = "N";
          #21#37 = " ";
          #21#38 = "0";
          #21#40 = #0#5;     || A¤o Jornadas Reales
          #21#41 = #0#4;     || Mes Jornadas Reales
          |HAZ dias_mes;
          |SI #labtraba#42 = "C"; |O #labtraba#42 = "F";
               |SI swInactividad = 0;
                    |BORRA 020#21a;
                    |LIBERA #21;
                    |CIERRA #21;
                    |CIERRA #20;
                    |ACABA;
               |FINSI;
          |FINSI;
          #21#42 = aCalendario;
          #21#12 = 0;                  || Coef. T.P. no admisible en agrarios
          #21#56 = #labtraba#75;
          nEmpresa = #21#1;
          |SI #labtraba#42 = "C"; |O #labtraba#42 = "F";
               #21#65 = "32";
          |FINSI;

          #labcentr#0 = #labtraba#0;
          #labcentr#1 = #labtraba#77;
          |LEE 000#labcentr.=;
          aCCC = #labcentr#10;
          |QBF aCCC;
          |SI aCCC = "";
               #labempre#0 = #labtraba#0;
               |LEE 000#labempre.=;
               aCCC = #labempre#13;
               |QBF aCCC;
          |FINSI;

          |SALVA_DATOS #21;
          |SALVA_FICHA #21;
          |SALVA_FICHA #labtraba;
          swHayOtro = 0;
          |BUCLE silcon13;
          |SELECT #1#silcon13;
          |REPON_FICHA #labtraba;
          |REPON_FICHA #21;
          |REPON_DATOS #21;
          |SI swHayOtro = 1;
/*
               || esto ya lo hago antes

               aCalendario1 = #21#42;
               |QBF aCalendario1;
               aCalendario2 = aCalendario;
               |QBF aCalendario2;

               aCalendario = "";
               |PARA nDia = 1; |SI nDia [ 31; |HACIENDO nDia = nDia + 1;
                    nNume1 = (nDia * 100) + 1;
                    aAlfa1 = aCalendario1 % nNume1;
                    aAlfa2 = aCalendario2 % nNume1;
                    |SI aAlfa1 ! "N"; |O aAlfa2 ! "N";
                         |SI aAlfa1 ! "N";
                              aCalendario = aCalendario + aAlfa1;
                         |SINO;
                              aCalendario = aCalendario + aAlfa2;
                         |FINSI;
                    |SINO;
                         aCalendario = aCalendario + "N";
                    |FINSI;
               |SIGUE;

               #21#42 = aCalendario;
*/
               |BORRA 020#21a;
               |LIBERA #21;
               |CIERRA #21;
               |CIERRA #20;
               |ACABA;
          |FINSI;

          |GRABA 020#21a;
          |LIBERA #21;
          |CIERRA #21;

          |CIERRA #labcentr;
          |CIERRA #labempre;
     |FINSI;
     |CIERRA #20;
|FPRC;

|PROCESO dias_mes;
     swInactividad = 0;

     |ABRE #gomdetjr;
     #gomdetjr#0 = #1#1;
     #gomdetjr#1 = #1#2;
     #gomdetjr#2 = #0#5;
     #gomdetjr#3 = #0#4;
     |LEE 000#gomdetjr.=;
     aCalendario = "";
     |SI FSalida = 0;
          |PARA nCampo = 4; |SI nCampo [ 34; |HACIENDO nCampo = nCampo + 1;
               |SI #labtraba#42 = "C"; |O #labtraba#42 = "F";
               || citricos
                    |SI #gomdetjr#nCampo# = "D";
                         aJornada = "S";
                         swInactividad = 1;
                    |SINO;
                         aJornada = " ";
                    |FINSI;
               |SINO;
                    |SI #gomdetjr#nCampo# = "X";
                         aJornada = "S";
                    |SINO;
                         |SI #gomdetjr#nCampo# = "I";
                              aJornada = "P";
                         |SINO;
                              aJornada = "N";
                         |FINSI;
                    |FINSI;
               |FINSI;
               aCalendario = aCalendario + aJornada;
          |SIGUE;
     |SINO;
          aCalendario = "NNNNNNNNNNNNNNNNNNNNNNNNNNNNNNN";
          aAlfa1 = #labtraba#0;
          aAlfa1 = "00000" + aAlfa1;
          aAlfa1 = aAlfa1 % -105;
          aAlfa2 = #labtraba#1;
          aAlfa2 = "00000" + aAlfa2;
          aAlfa2 = aAlfa2 % -105;
          aAlfa3 = "    ATENCION. Trabajador " + aAlfa1 + "." + aAlfa2 + " ";
          aAlfa3 = aAlfa3 + "no tiene informado calendario con detalle de jornadas";
          |MENAV aAlfa3;
     |FINSI;
     |CIERRA #gomdetjr;
|FPRC;

|PROCESO Calendario;
     |SI nTecla = 2;
          |ACABA;
     |FINSI;
     |PUSHV 11352380;
     |ABRE #gomdetj1;
     #gomdetj1#0 = #gomentr1#1;
     #gomdetj1#1 = #gomentr1#2;
     #gomdetj1#2 = #gomentra#5;
     #gomdetj1#3 = #gomentra#4;
     |LEE 101#gomdetj1.=;
     |PINPA #0#gomdetj1;
     |SI #gomentr1#3 = "EVEN";
          |PINTA 2242, "                        ";
          |PINTA 2342, "ÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ";
     |FINSI;
     |PINDA #0#gomdetj1;
     |ENDATOS #2#gomdetj1;
     |SI FSalida = 0;
          |GRABA 020#gomdetj1.a;
     |FINSI;
     |LIBERA #gomdetj1;
     |CIERRA #gomdetj1;
     |POPV;
     |PINTA #1#15;
|FPRC;

|PROCESO Tecla;
     nTecla = FSalida;
|FPRC;

|PROGRAMA;
     |CLS;
     |PINPA #0#0;

     |ABRE #0;
     |DDEFECTO #0;
     |LEE 000#0p;
     |SI FSalida = 0;
          |BORRA 020#0a;
     |FINSI;

     |PINDA #0#0;

     |ENDATOS #1#0;
     |SI FSalida = 0;
          |HAZ tipo3;
     |FINSI;

     |GRABA 000#0n;
     |CIERRA #0;
|FPRO;
