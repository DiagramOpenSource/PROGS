|FICHEROS;
    nomvart1 #0;         || seleccion
    labtraba #2;         || trabajadores
    nomvart2 #10;        || variaciones tiempos parcial
    nomvarca #11;        || variaciones cabs.
    nomvarli #12;        || variaciones lins.
    nomvart3 #13;        || ctos. variables
    nomtpsem #14;        || semana tiempo parcial
    nomvart4 #15;        || editar ILT
    nomtrcal #20;        || calendario laboral

    labempre #42;        || empresas    (promedio maternidad)
    nomhicca #43;        || historico cabs.  (promedio maternidad)
|FIN;

|VARIABLES;
    alfa1 = "";
    alfa2 = "";
    alfa3 = "";
    alfa4 = "";
    nume1 = 0;
    nume2 = 0;
    nume3 = 0;
    nume4 = 0;
    para1 = 0;
    para2 = 0;
    para3 = 0;
    para4 = 0;
    fech1 = @;
    fech2 = @;
    fech3 = @;
    fech4 = @;
    elsw1 = 0;
    elsw2 = 0;
    elsw3 = 0;
    elsw4 = 0;
    FEstado = 0;
    seleccio = 0;
    modo = 0;
    sw_rup = 0;

    p_mes = "";
    el_ultimo = 0;
    concepto = 0;
    unidades = "";
    valor = "";
    clave = "";

    inkey = 0;
    topemes = 0;
    fbase = 0;
    bisiesto = 0;
    dia_semana = "";
    las_horas = "";
    tot_horas = 0;
    f_graba = 0;
    f_semana = 0;
    sw_dia = "";
    horas_ilt = 0;
    sw_ilt = 0;
    guarda_fentr = "";
    d_enf = 0;
    d_acc = 0;
    d_hue = 0;
    d_abs = 0;
    d_abc = 0;
    d_mat = 0;
    d_vac = 0;
    d_des = 0;
    comprueba = "";
    ilt_d = 0;
    ilt_c = "";
    calendar = "";
    fiesta = "";
    col = 0;
    fil = 0;
    pos = 0;
    dia = 0;
    mes = 0;
    any = 0;
    dalta = 0;
    dilt = 0;
    gtope = 0;

    fecalf = "";
    hbase = 0;
    henfe = 0;
    hdias = 0;
    hhor1 = 0;
    hhor2 = 0;
    hmes = 0;
    hany = 0;
    hnat = 0;
    swActiva = 0;
|FIN;
____________________________________/ CALCULOS DE PANTALLA
|PROCESO mirames; |TIPO 7;
    fech1 = ~;
    alfa1 = fech1;
    alfa1 = alfa1 % 402;
    #0Campo = alfa1;
|PINTA #0Campo;

|HAZ pintames;
|FPRC;

|PROCESO pintames; |TIPO 0;
|HAZ lite_mes;
    gtope = topemes;
|ATRI I;  |PINTA 0844, p_mes; |ATRI 0;
|FPRC;

|PROCESO lite_mes;
    bisiesto = #0#5 $ 4;
|SI bisiesto = 0;
        bisiesto = 1;
|SINO;
        bisiesto = 0;
|FINSI;

|SI #0#4 =  1;  p_mes = "ENERO     ";  topemes = 31;  |FINSI;
|SI #0#4 =  2;  p_mes = "FEBRERO   ";  topemes = 28+bisiesto;  |FINSI;
|SI #0#4 =  3;  p_mes = "MARZO     ";  topemes = 31;  |FINSI;
|SI #0#4 =  4;  p_mes = "ABRIL     ";  topemes = 30;  |FINSI;
|SI #0#4 =  5;  p_mes = "MAYO      ";  topemes = 31;  |FINSI;
|SI #0#4 =  6;  p_mes = "JUNIO     ";  topemes = 30;  |FINSI;
|SI #0#4 =  7;  p_mes = "JULIO     ";  topemes = 31;  |FINSI;
|SI #0#4 =  8;  p_mes = "AGOSTO    ";  topemes = 31;  |FINSI;
|SI #0#4 =  9;  p_mes = "SEPTIEMBRE";  topemes = 30;  |FINSI;
|SI #0#4 = 10;  p_mes = "OCTUBRE   ";  topemes = 31;  |FINSI;
|SI #0#4 = 11;  p_mes = "NOVIEMBRE ";  topemes = 30;  |FINSI;
|SI #0#4 = 12;  p_mes = "DICIEMBRE ";  topemes = 31;  |FINSI;
|FPRC;

|PROCESO defecto; |TIPO 7;
    fech1 = ~;
    alfa1 = fech1;
    alfa1 = alfa1 % -104;
    #0Campo = alfa1;
|PINTA #0Campo;
|FPRC;
____________________________________/ ACTUALIZACION EN VARIACIONES GRAL.
|PROCESO el_valor;
    valor = "            ";
|SI concepto = 901;   valor = "H.Sb.TC2/1  ";  |FINSI;
|SI concepto = 902;   valor = "Indemi.Año  ";  |FINSI;
|SI concepto = 904;   valor = "Vacaci.Año  ";  |FINSI;
|SI concepto = 905;   valor = "H.Mes Cot.  ";  |FINSI;
|SI concepto = 909;   valor = "H.Max.Sem.  ";  |FINSI;
|SI concepto = 910;   valor = "H.Sem.Tra.  ";  |FINSI;
|SI concepto = 911;   valor = "H.Max.Mes   ";  |FINSI;
|SI concepto = 912;   valor = "H.Mes.Tra.  ";  |FINSI;
|SI concepto = 914;   valor = "Dias Alta   ";  |FINSI;
|SI concepto = 917;   valor = "Dias TC2    ";  |FINSI;
|SI concepto > 917;   valor = "INDEFINIDO  ";  |FINSI;
|FPRC;

|PROCESO lineas;
|DDEFECTO #12;
    #12#0 = #10#1;       || empresa
    #12#1 = #10#2;       || trabajador
    #12#2 = #0#4;        || mes
    #12#3 = 0;           || tipo
    #12#4 = concepto;    || concepto
|LEE 101#12=;
    FEstado = FSalida;

|QBF unidades;
    #12#5 = ("          " + unidades) % -110;     || unidades

|HAZ el_valor;
    #12#6 = valor;                                || valor

|SI FEstado = 0;    |GRABA 020#12a;     |FINSI;
|SI FEstado ! 0;    |GRABA 020#12n;     |FINSI;
|LIBERA #12;
|FPRC;

|PROCESO lee_con_variab1;
    concepto = #13#1;    |HAZ lineas;   || unidades conceptos variables
|FPRC;

|DEFBUCLE lee_con_variab;
 #13#1;
 3;
 #2#87, 101, "S";
 #2#87, 999, "S";
 e;
 NULL, lee_con_variab1;
|FIN;

|PROCESO trabajador;
    #2#0 = #10#1;        || empresa
    #2#1 = #10#2;        || trabajador
|LEE 101#2=;
|SI FSalida = 0;
        #2#46 = #10#16;            || dias contrato
        #2#47 = #10#15 / #10#16;   || horas TC2 diarias
    |GRABA 020#2a;
|FINSI;
|LIBERA #2;
|FPRC;

|PROCESO periodo_ilt2;
|SI ilt_d ! 0;
    |SI para1 [ 15;
            #11para1 = ilt_c;
            para2 = para1 - 8;     #11para2 = dalta;
            para2 = para1 - 4;     #11para2 = ilt_d - 1 + dalta;

            para1 = para1 + 1;
        |SI ilt_c = "M";|Y #2#42 = "P";
            |HAZ prom_tp_enf;
                #11#17 = (hbase + henfe) / hdias;
            |HAZ prom_tp_acc;
                #11#18 = ((hbase + henfe) - (hhor1 + hhor2)) / hdias;
        |FINSI;
    |FINSI;
|FINSI;
|FPRC;

|PROCESO periodo_ilt;
|PDEFECTO #11, 4, 16;

    #15#0 = #0#5;   || a¤o
    #15#1 = #0#4;   || mes
    #15#2 = #10#1;  || empresa
    #15#3 = #10#2;  || trabajador
|LEE 000#15=;
|SI FSalida = 0;
        d_enf = 0;  d_acc = 0;  d_hue = 0;  d_abs = 0;
        d_mat = 0;  d_vac = 0;  d_des = 0;  d_abc = 0;
        comprueba = "EAHFfMVD";
    |PARA para1 = 35; |SI para1 [ 65; |HACIENDO para1 = para1 + 1;
        |SI #15para1 = "E";   d_enf = d_enf + 1;  |FINSI;
        |SI #15para1 = "A";   d_acc = d_acc + 1;  |FINSI;
        |SI #15para1 = "H";   d_hue = d_hue + 1;  |FINSI;
        |SI #15para1 = "F";   d_abs = d_abs + 1;  |FINSI;
        |SI #15para1 = "f";   d_abc = d_abc + 1;  |FINSI;
        |SI #15para1 = "M";   d_mat = d_mat + 1;  |FINSI;
        |SI #15para1 = "V";   d_vac = d_vac + 1;  |FINSI;
        |SI #15para1 = "D";   d_des = d_des + 1;  |FINSI;
            comprueba = comprueba - #15para1;
    |SIGUE;
        alfa1 = comprueba % 0;     nume1 = alfa1;
    |SI nume1 < 3;
            alfa1 = "    [" + #15#2 + "-" + #15#3 + "]: informadas mas de 4 incidencias ...";
        |MENAV alfa1;
    |FINSI;
        para1 = 12;
        ilt_c = "E";  ilt_d = d_enf;   |HAZ periodo_ilt2;
        ilt_c = "A";  ilt_d = d_acc;   |HAZ periodo_ilt2;
        ilt_c = "H";  ilt_d = d_hue;   |HAZ periodo_ilt2;
        ilt_c = "F";  ilt_d = d_abs;   |HAZ periodo_ilt2;
        ilt_c = "f";  ilt_d = d_abc;   |HAZ periodo_ilt2;
        ilt_c = "M";  ilt_d = d_mat;   |HAZ periodo_ilt2;
        ilt_c = "V";  ilt_d = d_vac;   |HAZ periodo_ilt2;
        ilt_c = "D";  ilt_d = d_des;   |HAZ periodo_ilt2;
|FINSI;
|FPRC;

|PROCESO cabecera;
|DDEFECTO #11;
    #11#0 = #10#1;       || empresa
    #11#1 = #10#2;       || trabajador
    #11#2 = #0#4;        || mes
    #11#3 = 0;           || tipo
|LEE 101#11=;
    FEstado = FSalida;

|HAZ lee_traba;
|HAZ dia_alta;
|HAZ periodo_ilt;

|SI FEstado = 0;    |GRABA 020#11a;     |FINSI;
|SI FEstado ! 0;    |GRABA 020#11n;     |FINSI;
|LIBERA #11;
|FPRC;

|PROCESO lee_edicion1;
|HAZ cabecera;

|SI #10#3 = "P";
    |SI #10#5 = "S";
            concepto = 909;  unidades = #10#7;  |HAZ lineas; || conv.semana
            concepto = 910;  unidades = #10#6;  |HAZ lineas; || trab.semana
    |FINSI;
    |SI #10#5 = "M";
            concepto = 911;  unidades = #10#7;  |HAZ lineas; || conv.mes
            concepto = 912;  unidades = #10#6;  |HAZ lineas; || trab.mes
    |FINSI;
|FINSI;

|SI #10#3 = "P";
        concepto = 905;  unidades = #10#6;  |HAZ lineas; || h.trabaj.mes
|FINSI;
|SI #10#3 = "I";
        concepto = 905;  unidades = #10#17; |HAZ lineas; || dias.trabaj.mes
|FINSI;

    unidades = #10#16;
    concepto = 917; |HAZ lineas;   || dias TC2

    unidades = #10#17;
    concepto = 914; |HAZ lineas;   || dias alta

|HAZ trabajador;

    unidades = #10#4;              || dias cobro
|BUCLE lee_con_variab;
|FPRC;

|DEFBUCLE lee_edicion;
 #10#1;
 ;
 #0#5, #0#4,     1;
 #0#5, #0#4, 99999;
 e;
 NULL, lee_edicion1;
|FIN;

|PROCESO actualiza;
|CONFI "    NActualizar en Variaciones Generales (S/N): ";
|SI FSalida = 0;
    |MENSA "    ...Actualizando en Variaciones General...";
    |ABRE #2;
    |ABRE #11;
    |ABRE #12;
    |ABRE #15;
    |BUCLE lee_edicion;
    |CIERRA #2;
    |CIERRA #11;
    |CIERRA #12;
    |CIERRA #15;
    |BLIN 4;
|FINSI;
|FPRC;
____________________________________/ EDICION
|PROCESO min;
    #10#13 = #0#5;       || a¤o
    #10#14 = #0#4;       || mes
    #10#0  = 1;          || guia
|FPRC;

|PROCESO max;
    #10#13 = #0#5;       || a¤o
    #10#14 = #0#4;       || mes
    #10#0  = el_ultimo;  || guia
|FPRC;

|PROCESO edita;
|ENTLINEAL #1#10, 3, 3, 20, 0, min, max;
|FPRC;
____________________________________/ LECTURA TRABAJADORES
|PROCESO lee_traba1;
|DDEFECTO #10;
|SELECT #2#10;
    #10#1  = #2#0;   || empresa
    #10#2  = #2#1;   || trabajador
    #10#13 = #0#5;   || a¤o
    #10#14 = #0#4;   || mes
|LEE 000#10=;
|SI FSalida ! 0;
    |HAZ activasino;
    |SI swActiva = 1;
        |SELECT #1#10;
            el_ultimo = el_ultimo + 1;
            #10#0  = el_ultimo;        || guia
            #10#1  = #2#0;             || empresa
            #10#2  = #2#1;             || trabajador
            #10#3  = #2#42;            || clave dias salario
            #10#4  = #2#46;            || dias cobro = dias contrato
            #10#5  = "M";              || semana/meses
        |SI #10#3 = "I";
                #10#6  = 0;                 || horas trabajadas
                #10#7  = 160;               || horas convenio
                #10#8  = 1;                 || proporcion
        |FINSI;
        |SI #10#3 = "P";
                #10#6  = #2#46 * #2#47;     || horas trabajadas
                #10#7  = 160;               || horas convenio
                #10#8  = #10#6 / #10#7;     || proporcion
        |FINSI;
            #10#9  = #2#2;             || nombre
            #10#10 = #2#87;            || convenio
            #10#11 = #2#17;            || categoria
            #10#12 = #2#45;            || contrato
            #10#13 = #0#5;             || a¤o
            #10#14 = #0#4;             || mes
            #10#15 = #2#46 * #2#47;    || horas TC2
            #10#16 = #10#4;            || dias TC2 = dias cobro
            #10#17 = #10#4;            || dias alta = dias cobro
        |HAZ recalculo;

            #15#0 = #0#5;    || a¤o
            #15#1 = #0#4;    || mes
            #15#2 = #10#1;   || empresa
            #15#3 = #10#2;   || trabajador
        |BORRA 000#15c;      || Introduccion ILT
    |FINSI;
|FINSI;
|FPRC;

|PROCESO lee_traba0;
    el_ultimo = 0;

    #10#13 = #0#5;       || a¤o
    #10#14 = #0#4;       || mes
    #10#0  = 99999;      || guia
|LEE 000#10];
|SI FSalida = 0;
    |LEE 000#10a;
|SINO;
    |LEE 000#10u;
|FINSI;
|SI FSalida = 0;|Y #10#13 = #0#5;|Y #10#14 = #0#4;
        el_ultimo = #10#0;
|FINSI;
|FPRC;

|DEFBUCLE lee_traba;
 #2#4;
 44;
 clave,     1,     1, "A";
 clave, 99999, 99999, "A";
 ;
 NULL, lee_traba1;
|FIN;

|PROCESO recoge;
|| xxxxxxxxx
|MENSA "    ...Procesando Trabajadores...";
|ABRE #10;
|ABRE #15;
    f_graba = 1;
|HAZ lee_traba0;
    clave = "I";    |BUCLE lee_traba;
    clave = "P";    |BUCLE lee_traba;
|CIERRA #10;
|CIERRA #15;
|BLIN 4;
|FPRC;
____________________________________/ CALCULO PRINCIPAL
|PROCESO tipo3; |TIPO 3;
|HAZ recoge;                  || lee los trabajadores
|HAZ edita;                   || edita los trabajadores
|HAZ actualiza;               || actualiza en variaciones general
|FPRC;

____________________________________________________________________________
____________________________________/ CALCULOS DESDE LINEAS
|PROCESO mayus; |TIPO 0;
    #10Campo = #10Campo > #10Campo;
|PINTA #10Campo;

|SI modo = 1;
    |SI #10Campo = "M"; #10#7 = 160;   |FINSI;
    |SI #10Campo = "S"; #10#7 =  40;   |FINSI;
    |PINTA #10#7;
|FINSI;
|FPRC;

|PROCESO propor; |TIPO 0;
    nume1 = Contenido;
|SI nume1 ! #10Campo;
    |SI #10#6 = 0;
            #10#8 = 1;
    |SINO;
            #10#8 = #10#6 / #10#7;
    |FINSI;
    |PINTA #10#8;
|FINSI;
|FPRC;

|PROCESO tiempo; |TIPO 0;
|DDEFECTO #2;
|ABRE #2;
    #2#0 = #10#1;
    #2#1 = #10#2;
|LEE 000#2=;
|SI #2#42 ! "X";|Y #2#42 ! "T";
    |MENAV "    ATENCION!!! El Trabajador no es a Tiempo Parcial ...";
    |ERROR;
|FINSI;
|CIERRA #2;
|FPRC;

|PROCESO tipo11; |TIPO 11;
    inkey = FSalida;
|SI inkey ] 9;|Y inkey [ 20;
    |SI inkey = 9;
        |HAZ edita_ilt;       || edicion de ILT (F1)
    |SINO;
        |AVISO;
    |FINSI;
    |ERROR;
|FINSI;
|FPRC;

|PROCESO noalta; |TIPO 0;
    modo = (FEntrada / 100) ! 0;
|SI modo = 1;
    |MENAV "    Alta no permitida ...";
    |ERROR;
|FINSI;
|FPRC;

|PROCESO tipo2; |TIPO 2;
|SI #10#3 = "I";
    |CAMPO_MODIFICA #10#5,  1, "N";
    |CAMPO_MODIFICA #10#6,  1, "N";
    |CAMPO_MODIFICA #10#7,  1, "N";
|SINO;
    |CAMPO_MODIFICA #10#5,  1, "S";
    |CAMPO_MODIFICA #10#6,  1, "S";
    |CAMPO_MODIFICA #10#7,  1, "S";
|FINSI;
|FPRC;
____________________________________/ RECALCULO SEGUN SEMANA
|PROCESO lee_traba;
|DDEFECTO #2;
    #2#0 = #10#1;
    #2#1 = #10#2;
|LEE 000#2=;
|FPRC;

|PROCESO dia_alta;
    alfa1 = #2#36 %  102;   dia = alfa1;
    alfa1 = #2#36 %  402;   mes = alfa1;
    alfa1 = #2#36 % -104;   any = alfa1;
|SI mes = #0#4;|Y any = #0#5;
        dalta = dia;
|SINO;
        dalta = 1;
|FINSI;

    alfa1 = #2#37 %  102;   dia = alfa1;
    alfa1 = #2#37 %  402;   mes = alfa1;
    alfa1 = #2#37 % -104;   any = alfa1;
|SI mes = #0#4;|Y any = #0#5;
        topemes = dia;
|SINO;
        topemes = gtope;
|FINSI;
|FPRC;

|PROCESO saca_horas;
    las_horas = 0;

    nume1 = (para1 * 100) + 1;
    fiesta = calendar % nume1;     || dia festivo o laboral s/calendario
|SI fiesta = "0";
    |SI dia_semana = "Lunes";     las_horas = #14#2;  |FINSI;
    |SI dia_semana = "Martes";    las_horas = #14#3;  |FINSI;
    |SI dia_semana = "Miercoles"; las_horas = #14#4;  |FINSI;
    |SI dia_semana = "Jueves";    las_horas = #14#5;  |FINSI;
    |SI dia_semana = "Viernes";   las_horas = #14#6;  |FINSI;
    |SI dia_semana = "Sabado";    las_horas = #14#7;  |FINSI;
    |SI dia_semana = "Domingo";   las_horas = #14#8;  |FINSI;

    |QBT las_horas;
    |SI las_horas = "T";
            las_horas = #14#9;         || tiempo completo
    |FINSI;
|FINSI;
|FPRC;

|PROCESO lee_calendario;
|DDEFECTO #20;
|ABRE #20;
    #20#0 = #2#156;
    #20#13 = #0#5;
|LEE 000#20=;            || calendario
    nume1 = #0#4;
    calendar = #20nume1;
|CIERRA #20;
|FPRC;

|PROCESO recalculo;
|MENSA "2405Calculando ...";
|ABRE #14;
    #14#0 = #10#1;
    #14#1 = #10#2;
|LEE 000#14=;
|SI FSalida = 0;
        tot_horas = 0;                       || horas totales del mes

        alfa1 = #0#4;   alfa1 = ("00" + alfa1) % -102;
        alfa1 = "01." + alfa1 + "." + #0#5;
        fech1 = alfa1;                       || primer dia del mes

        fbase = fech1;
        fbase = fbase - 1;                   || ultimo dia del mes anterior

    |HAZ lee_calendario;                     || carga calendario laboral
    |HAZ dia_alta;                           || comprueba si es alta

    |PARA para1 = dalta; |SI para1 [ topemes; |HACIENDO para1 = para1 + 1;
            nume1 = fbase + para1;
            fech1 = nume1;
            fech1 = fech1 % 1;
            dia_semana = fech1;
            dia_semana = dia_semana % 11;
        |HAZ saca_horas;
            nume1 = las_horas;
            tot_horas = tot_horas + nume1;
    |SIGUE;

        #10#6 = tot_horas;         || horas trabajadas
        #10#15= #10#6;             || horas TC2
        #10#4 = #10#6 / #14#9;     || dias cobro
        #10#7 = #10#6;             || horas convenio
    |SI #10#3 = "I";     #10#8  = 1;              |FINSI;
    |SI #10#3 = "P";     #10#8  = #10#6 / #10#7;  |FINSI;   || proporcion
        #10#16 = #10#4;            || dias TC2 = dias cobro
        #10#17 = #10#4;            || dias alta = dias cobro

    |SI f_graba = 0;     |GRABA 020#10a;     |FINSI;
    |SI f_graba ! 0;     |GRABA 020#10n;     |FINSI;
    |SI FSalida = 0;|Y f_graba = 0;
        |PINTA #10#6;
        |PINTA #10#7;
        |PINTA #10#8;
        |PINTA #10#4;
        |PINTA #10#15;
        |PINTA #10#16;
    |FINSI;
|SINO;
    |SI f_graba ! 0;
        |GRABA 020#10n;
    |FINSI;
|FINSI;
|CIERRA #14;
|BLIN 24;
|FPRC;

____________________________________/ EDICION DE ILT
|PROCESO actualiza_linea;
    #10#6 = #10#6 - horas_ilt;          || horas trabajadas
    #10#15 = #10#15 - horas_ilt;        || horas TC2
    #10#7 = #10#6;                      || horas convenio
    #10#4 = #10#6 / #14#9;              || dias cobro
|SI #10#3 = "I";     #10#8  = 1;              |FINSI;
|SI #10#3 = "P";     #10#8  = #10#6 / #10#7;  |FINSI;
    ||#10#16 = ;                        || dias TC2 (NO SE TOCAN)
    ||#10#17 = ;                        || dias alta (NO SE TOCAN)
|GRABA 020#10a;

|PINTA #10#6;
|PINTA #10#7;
|PINTA #10#8;
|PINTA #10#4;
|PINTA #10#15;
|PINTA #10#16;
|FPRC;

|PROCESO quita_ilt;
|PARA para1 = 4; |SI para1 [ 34; |HACIENDO para1 = para1 + 1;
        para2 = para1 + 31;
        alfa1 = #15para2;
    |QBT alfa1;
    |SI alfa1 ! "";
            nume1 = ((para1 - 3)* 100) + 1;
            fiesta = calendar % nume1;       || dia festivo o laboral
        |SI fiesta = "0";
            |PARA para3 = 2; |SI para3 [ 8; |HACIENDO para3 = para3 + 1;
                    sw_ilt = 0;
                |SI #15para1 = "L";|Y para3 = 2; sw_ilt = 1;    |FINSI;
                |SI #15para1 = "M";|Y para3 = 3; sw_ilt = 1;    |FINSI;
                |SI #15para1 = "X";|Y para3 = 4; sw_ilt = 1;    |FINSI;
                |SI #15para1 = "J";|Y para3 = 5; sw_ilt = 1;    |FINSI;
                |SI #15para1 = "V";|Y para3 = 6; sw_ilt = 1;    |FINSI;
                |SI #15para1 = "S";|Y para3 = 7; sw_ilt = 1;    |FINSI;
                |SI #15para1 = "D";|Y para3 = 8; sw_ilt = 1;    |FINSI;
                |SI sw_ilt = 1;
                        alfa1 = #14para3;
                    |QBT alfa1;
                    |SI alfa1 = "T";
                            horas_ilt = horas_ilt + #14#9;
                    |SINO;
                            nume1 = alfa1;
                            horas_ilt = horas_ilt + nume1;
                    |FINSI;
                |FINSI;
            |SIGUE;
        |FINSI;
    |FINSI;
|SIGUE;
|FPRC;

|PROCESO carga_semana;
    alfa1 = #0#4;   alfa1 = ("00" + alfa1) % -102;
    alfa1 = "01." + alfa1 + "." + #0#5;
    fech1 = alfa1;                      || primer dia del mes

    fbase = fech1;
    fbase = fbase - 1;                  || ultimo dia del mes anterior

|PARA para1 = 1; |SI para1 [ topemes; |HACIENDO para1 = para1 + 1;
        nume1 = fbase + para1;
        fech1 = nume1;
        fech1 = fech1 % 1;
        dia_semana = fech1;
        dia_semana = dia_semana % 11;
        para2 = para1 + 3;
        dilt = para1 + 34;
    |SI dia_semana = "Miercoles";
            #15para2 = "X";
    |SINO;
            #15para2 = dia_semana % 101;
    |FINSI;

    |CAMPO_MODIFICA #15dilt, 1, "N";
    |CAMPO_ATRIBUTO #15para2, D, 0, 0;
    |SI #15para2 = "L";  para3 = 2;     |FINSI;
    |SI #15para2 = "M";  para3 = 3;     |FINSI;
    |SI #15para2 = "X";  para3 = 4;     |FINSI;
    |SI #15para2 = "J";  para3 = 5;     |FINSI;
    |SI #15para2 = "V";  para3 = 6;     |FINSI;
    |SI #15para2 = "S";  para3 = 7;     |FINSI;
    |SI #15para2 = "D";  para3 = 8;     |FINSI;
        alfa1 = #14para3;
    |QBT alfa1;
    |SI alfa1 = "T"; alfa1 = #14#9; |FINSI;
            nume1 = alfa1;
    |SI nume1 ! 0;|Y para1 ] dalta;
        |CAMPO_MODIFICA #15dilt, 1, "S";
        |CAMPO_ATRIBUTO #15para2, R, 0, 0;
    |FINSI;

        nume1 = (para1 * 100) + 1;
        fiesta = calendar % nume1;      || dia festivo o laboral s/calendario
    |SI fiesta = "1";
            col = 0;
        |SI para1 [  7;|Y col = 0;  col = 16;  fil = para1 + 10; |FINSI;
        |SI para1 [ 14;|Y col = 0;  col = 25;  fil = para1 + 3;  |FINSI;
        |SI para1 [ 21;|Y col = 0;  col = 34;  fil = para1 - 4;  |FINSI;
        |SI para1 [ 28;|Y col = 0;  col = 43;  fil = para1 - 11; |FINSI;
        |SI para1 [ 31;|Y col = 0;  col = 52;  fil = para1 - 18; |FINSI;
            alfa1 = para1;    alfa1 = ("00" + alfa1) % -102;
            pos = (fil * 100) + col;
        |ATRI S;
        |PINTA pos, alfa1;
        |ATRI 0;
        |CAMPO_MODIFICA #15dilt, 1, "N";
    |FINSI;
|SIGUE;
|PARA para1 = topemes + 1; |SI para1 [ 31; |HACIENDO para1 = para1 + 1;
        dilt = para1 + 34;
    |CAMPO_MODIFICA #15dilt, 1, "N";
|SIGUE;
|FPRC;

|PROCESO lee_semana;
|DDEFECTO #14;
|ABRE #14;
    #14#0 = #10#1;
    #14#1 = #10#2;
|LEE 000#14=;
    f_semana = FSalida;
|CIERRA #14;
|FPRC;

|PROCESO edita_ilt;
|HAZ lee_semana;
|SI f_semana = 0;
        horas_ilt = 0;
    |PUSHV 0813 2362;
    |PINPA #0#15;
    |DDEFECTO #15;
    |ABRE #15;
        #15#0 = #0#5;   || a¤o
        #15#1 = #0#4;   || mes
        #15#2 = #10#1;  || empresa
        #15#3 = #10#2;  || trabajador
    |LEE 101#15=;
        FEstado = FSalida;
    |ABRE #2;
    |HAZ lee_traba;
    |CIERRA #2;
    |HAZ lee_calendario;
    |HAZ dia_alta;
    |HAZ carga_semana;
    |PINDA #0#15;
        guarda_fentr = FEntrada;
    |ENDATOS #1#15;
    |SI FSalida = 0;
        |SI FEstado ! 0;    |GRABA 020#15n;     |FINSI;
        |SI FEstado = 0;    |GRABA 020#15a;     |FINSI;
        |SI FSalida = 0;
            |HAZ quita_ilt;
        |FINSI;
    |FINSI;
    |POPV;
        FEntrada = guarda_fentr;

        f_graba = 0;
    |HAZ recalculo;
    |SI horas_ilt ! 0;
        |HAZ actualiza_linea;
    |FINSI;

    |LIBERA #15;
    |CIERRA #15;
|SINO;
    |MENAV "     'Maestros/Complementarios I/Semanas T.Parcial' SIN configurar ...";
|FINSI;
|FPRC;


____________________________________/ CALCULO DE LOS PROMEDIOS DE MATERNIDAD
|PROCESO resta_mes;
    hmes = hmes - 1;
|SI hmes = 0;
        hmes = 12;
        hany = hany - 1;
    |SI hany < 0;
            hany = 99;
    |FINSI;
|FINSI;

    nume1 = hany $ 4;
|SI nume1 = 0;   nume1 = 1;   |SINO;    nume1 = 0;     |FINSI;
|SI hmes = 1;  hnat = 31;     |FINSI;
|SI hmes = 2;  hnat = 28 + nume1;  |FINSI;
|SI hmes = 3;  hnat = 31;     |FINSI;
|SI hmes = 4;  hnat = 30;     |FINSI;
|SI hmes = 5;  hnat = 31;     |FINSI;
|SI hmes = 6;  hnat = 30;     |FINSI;
|SI hmes = 7;  hnat = 31;     |FINSI;
|SI hmes = 8;  hnat = 31;     |FINSI;
|SI hmes = 9;  hnat = 30;     |FINSI;
|SI hmes = 10; hnat = 31;     |FINSI;
|SI hmes = 11; hnat = 30;     |FINSI;
|SI hmes = 12; hnat = 31;     |FINSI;

    fecalf = #2#36;
    alfa1 = fecalf %  102;   dia = alfa1;
    alfa1 = fecalf %  402;   mes = alfa1;
    alfa1 = fecalf % -102;   any = alfa1;
|SI alfa1 = "..";|O alfa1 = "  ";
        dia = 0;
        mes = 0;
        any = 0;
|FINSI;

|SI mes = hmes;|Y any = hany;
        hnat = hnat - dia + 1;
|FINSI;
|FPRC;

|PROCESO busca_acc;
|HAZ resta_mes;
|DDEFECTO #43;
    #43#0  = #11#0;  || empresa
    #43#1  = #11#1;  || trabajador
    #43#2  = hmes;   || mes
    #43#3  = #11#3;  || tipo
    #43#66 = hany;   || a¤o
|LEE 000#43=;
|SI #43#65 ! 0;
        henfe = henfe + #43#62;
|FINSI;
|SI #42#40 ! "S";
        hhor1 = 0;
        hhor2 = 0;
|SINO;
        hhor1 = hhor1 + #43#41;
        hhor2 = hhor2 + #43#42;
|FINSI;
    hbase = hbase + #43#39;
    hdias = hdias + hnat;          || dias naturales!!!
|FPRC;

|PROCESO prom_tp_acc;
    hmes = #11#2;
    fech1 = ~; alfa1 = fech1; alfa1 = alfa1 % -102;    || a¤o del sistema
    hany = alfa1;

|ABRE #42;
    #42#0 = #11#0;
|LEE 000#42=;
|CIERRA #42;

|ABRE #43;
|HAZ busca_acc;
|HAZ busca_acc;
|HAZ busca_acc;
|CIERRA #43;
|FPRC;

|PROCESO busca_enf;
|HAZ resta_mes;
|DDEFECTO #43;
    #43#0  = #11#0;  || empresa
    #43#1  = #11#1;  || trabajador
    #43#2  = hmes;   || mes
    #43#3  = #11#3;  || tipo
    #43#66 = hany;   || a¤o
|LEE 000#43=;
|SI #43#65 ! 0;
        henfe = henfe + #43#62;
|FINSI;
    hbase = hbase + #43#39;
    hdias = hdias + hnat;          || dias naturales!!!
|FPRC;

|PROCESO prom_tp_enf;
    hmes = #11#2;
    fech1 = ~; alfa1 = fech1; alfa1 = alfa1 % -102;    || a¤o del sistema
    hany = alfa1;
|ABRE #43;
    hbase = 0;
    hdias = 0;
    henfe = 0;
|HAZ busca_enf;
|HAZ busca_enf;
|HAZ busca_enf;
|CIERRA #43;
|FPRC;

|PROCESO activasino;
     swActiva = 1;
     |ABRE #42;
     #42#0 = #2#0;
     |LEE 000#42=;
     |SI #42#50 = "S";
          swActiva = 0;  || empresa no activa
     |FINSI;
     |CIERRA #42;
|FPRC;
