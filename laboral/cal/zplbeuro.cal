|FICHEROS;
     refer00@ #0;
     :/basica/def/zmonedas #500;
|FIN;

|VARIABLES;
     aPrograma    = "";
     aDef         = "";
     aQueQuiero   = "";
     aNombreCampo = "";
     aFichero     = "";

     nCampo       = 0;
     nTCampo      = 0;
     nSwHay       = 0;

     &eaMoneda    = "";
     &enCanti     = 0;
     &aMonedaRec  = "";
     &enSwTrata   = 0;

     &EURODB      = 0;

     {-1}aMoneda   = "";
         aMoneda1  = "LEER";
         aMoneda2  = "";
|FIN;

|PROCESO PintaMoneda;  |TIPO 7;
     aPrograma = Control;  |QBF aPrograma;

     |DBASE aDef;  |QBF aDef;
     aDef   = aDef + "def/" + aPrograma + ".mas";
     |CARGA_DEF aDef, refer00@;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     aQueQuiero = "|NC";
     aFichero   = #0#aQueQuiero#;
     nTCampo    = aFichero;
     nTCampo    = nTCampo - 1;
     nSwHay     = 0;
     |PARA nCampo = 0;  |SI nCampo [ nTCampo;  |HACIENDO nCampo = nCampo + 1;
           aQueQuiero   = "|C" + (("000" + nCampo) % -103) + "NOMBRE";
           aNombreCampo = (#0#aQueQuiero# + (" " * 20)) % 120;
           |QBF aNombreCampo;
           |SI aNombreCampo = "***MONEDA***";
               nSwHay = 1;
               |SAL;
           |FINSI;
     |SIGUE;

     |SI nSwHay = 0;
         |DESCARGA_DEF #refer00@;
         |ACABA;
     |FINSI;

     IaMoneda = aMoneda1<-;
     |ESPECIFICA "MODO_VER_EURO", aMoneda;

     |SI EURODB ! 0;
         |SI aMoneda2 = "0";
             #0nCampo = "EUROS";
         |SINO;
             #0nCampo = "PESETAS";
         |FINSI;
     |SINO;
         |SI aMoneda2 = "0";
             #0nCampo = "PESETAS";
         |SINO;
             #0nCampo = "EUROS";
         |FINSI;
     |FINSI;

     |PINTA #0nCampo;
     eaMoneda = #0nCampo;

     |DESCARGA_DEF #refer00@;
|FPRC;

|PROCESO ConverMoneda;
     enCanti = enCanti ! EURODB;
     |SI EURODB ! 0;
         |SI eaMoneda = "PESETAS";
             enCanti = ((enCanti * 166.386) ! 0);
         |SINO;
             |SI enSwTrata = 0;  enCanti = enCanti * 100;  |FINSI;
         |FINSI;
     |SINO;
         |SI eaMoneda = "EUROS  ";
             enCanti = ((enCanti / 166.386) ! 2) * 100;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO ConverMoneda2;
     |SI EURODB ! 0;
         |SI eaMoneda = "PESETAS";
             aMonedaRec = "EUROS  ";
         |ACABA;
         |FINSI;
         |SI eaMoneda = "EUROS  ";
             aMonedaRec = "PTAS.";
             enCanti = ((enCanti * 166.386) ! 0);
         |FINSI;
     |SINO;
         |SI eaMoneda = "PESETAS";
             aMonedaRec = "EUROS  ";
             enCanti = ((enCanti / 166.386) ! 2);
         |ACABA;
         |FINSI;
         |SI eaMoneda = "EUROS  ";
             aMonedaRec = "PTAS.";
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO LeeEURODB;
     |ABRE #500;
     |LEE 040#500p;
     |SI FSalida ! 0;  |DDEFECTO #500;  |FINSI;
     |CIERRA #500;

     EURODB = 0;
     |SI #500#5 = "E";  EURODB = 2;  |FINSI;
|FPRC;
