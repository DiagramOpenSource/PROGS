||Este PLB se utiliza para enviar documentos al DSARCHI.

||Para enviar los documentos desde cualquier aplicacion seguiremos estos pasos.

||IniEnvioDocDSArchi
||Para cada documento
     || Enviardocumento (DocFisicoDSArchi). Controlando si existe.
     || Enviar uno a uno los campos a rellenar del registro documental (MeteCampoDSArchi)
||FinEnvioDocDSArchi

|| En dsarchi, consultar el documento "reg_docs.txt", para ver la estructura
|| del dsarm005 (Registro de Documentos).

|FICHEROS;
     referen@  #500;

     dsarm009@ #509;          || Cache Usuarios

     dsarm001@ #501;          || Parametros

     dsarm072@ #502;          || Carpetas archivado documentos laboral
     dsarm002@;
     dsarm003@;
     dsarm004@;
     agitab99@;

     nomrecib;
     nomtrnom;
     nomareci;
|FIN;

|VARIABLES;
     nSwPideEstructura = 0;
     aIntePara = "";
     nHandle = 0;
     aLinea = "";

     &enGrupoTres = 0;
     &enSubGTres = 0;
     &enTipoTres = 0;
     &enSwMeteGrupoSubTipo = 0;

     &enSwDesde9 = 0;              ||Sirve para indicar si lanzamos el proceso desde 9 o desde X.

     &enSwCDefOk = 0;              ||Nos indica si el CARGA_DEF ha sido Correcto
     &eaDocumentoArchi = "";       ||Ubicacion completa del documento
     &eaNomDocArchi = "";          ||Nombre del documento (con extension)

     &enSwExisteDoc = 0;           ||Indica si existe fisicamente el documento
                                   ||y si tengo permisos de lectura.

     &enNumCampoArchi = 0;         ||Numero de campo del Registro
     &eaValorArchi = "";           ||Valor a guardar en el Registro.

     &enNumeroRegistro = 0;        ||Numero de registro en el temporal (AUTO)

     &enSwUbicacionDoc = "";       ||Indica si los documentos esta en el
                                   ||"C"liente o en el "S"ervidor.
                                   ||Se carga en el Inicio del Proceso

     &eaNomTemporal = "";          ||Nombre temporal que se usa para los datos a DSARCHI
     &enSwErrorRecDoc = 0;         ||No la devuelve el DSARCHI indicado si todo correcto o ERROR

     aBass = "";
     aMas = "";
     nSwEstaDsArchi = 0;
     aMensaje = "";
     aUnidad = "";
     aDef = "";
     aUsuario = "";
     aPathDatos = "";
     aOrigen = "";
     aDestino = "";
     aIPRemoto = "";
     aEjecuta = "";

     aPathTmpUsu = "";
     aUsuTmp = "";
     aFicTar = "";

     aBorra = "";
     aExtension = "";
     aSinExtension = "";
     aNombre = "";
     aAux = "";

     aBaseArchi = "";


     ||Aleatorio
     rnd        = 0;
     r_semilla  = 0;
     r_vuelta   = 0;
     r_bucle    = 0;
     r_hora     = "";
     r_beta     = "";
     r_punto    = "";
     r_pi       = 3.141592;
     r_producto = 0.000000;
     &enSwNomina = 0;
     &enSwNominaEmpCen = 0;

     &nTipo = 0;
     &eaSwEnvioCorreo = "";
     &nDesdeMes = 0;
     &nHastaMes = 0;

     &eaVengoDe = "";
     &nModelo = 0;
     &swEsProrroga = 0;
     &swPagasSN = 0;
     &enSwFiniq = 0;
     nTipoAnt = 0;
     nContador = 0;
     nCod      = 0;
     nCli      = 0;
     nDoc      = 0;

     aDes   = "";
     aCar   = "";
     aAlfa  = "";
     aAlfa1 = "";
     aAlfa2 = "";
     aAlfa3 = "";
     aAlfa4 = "";
     aAlfa5 = "";
     aAlfa6 = "";
     aFic   = "";

     &swNoArchives = 0;
     &enTipo       = 0;
     &enAny = 0;
     &enMes = 0;
|FIN;

|PROCESO ExisteDocFisico;
     aAux = eaDocumentoArchi;
     |SI enSwUbicacionDoc = "S";
          |XABRE "E", aAux, 0;
     |SINO;
          |XABRE "CE", aAux, 0;
     |FINSI;
     |SI FSalida ] 0;
          enSwExisteDoc = 1;
     |SINO;
          enSwExisteDoc = 0;
     |FINSI;
|FPRC;

|PROCESO EstaDsArchi;
     |SI enSwUbicacionDoc = "";
         |IP_REMOTO aIPRemoto;  |QBT aIPRemoto;
         |SI aIPRemoto ! "";
              enSwUbicacionDoc = "S";  || lo guardo en el servidor
         |SINO;
              enSwUbicacionDoc = "C";  || lo guardo en local
         |FINSI;
     |FINSI;

     |DBASS aBass;
     |QBF aBass;
     aMas = aBass + "dsarchi/def/dsarm001.mas";
     |XABRE "E", aMas, 0;
     |SI enSwUbicacionDoc = "S";
          |XABRE "E", aMas, 0;
     |SINO;
          |XABRE "CE", aMas, 0;
     |FINSI;
     |SI FSalida ] 0;
          nSwEstaDsArchi = 1;
     |SINO;
          nSwEstaDsArchi = 0;
     |FINSI;
|FPRC;

|PROCESO DimeUnidad;
     ||OUT   aUnidad

     aDef = aBass + "dsarchi/def/dsarm009.mas";
     |CARGA_DEF aDef, dsarm009@;
     |SI FSalida ! 0;
          |ACABA;
     |FINSI;

     aPathDatos = aBass + "dsarchi/fich/";
     |PATH_DAT #509 aPathDatos;

     |ABRE #dsarm009@;
     aUsuario = Usuario % 810;
     |QBF aUsuario;

     |DDEFECTO #dsarm009@;
     #dsarm009@#0 = aUsuario;
     |LEE 000#dsarm009@.=;
     |SI FSalida ! 0;
          aUnidad = "C";
     |SINO;
          aUnidad = #dsarm009@#1;
          |QBF aUnidad;
          |SI aUnidad = ""; aUnidad = "C"; |FINSI;
     |FINSI;
     |CIERRA #dsarm009@;

     |DESCARGA_DEF #dsarm009@;
|FPRC;

|PROCESO LeeDescripcion;
     |SI #dsarm072@#3 ! 000;
         #dsarm002@#0 = #dsarm072@#3;
         |LEE 000#dsarm002@.=;
         |SI FSalida = 0;
             #dsarm072@#6 = #dsarm002@#1;
         |FINSI;

         |SI #dsarm072@#4 ! 000;
             #dsarm003@#0 = #dsarm072@#3;
             #dsarm003@#1 = #dsarm072@#4;
             |LEE 000#dsarm003@.=;
             |SI FSalida = 0;
                 #dsarm072@#7 = #dsarm003@#2;
             |FINSI;

             |SI #dsarm072@#5 ! 000;
                 #dsarm004@#0 = #dsarm072@#3;
                 #dsarm004@#1 = #dsarm072@#4;
                 #dsarm004@#2 = #dsarm072@#5;
                 |LEE 000#dsarm004@.=;
                 |SI FSalida = 0;
                     #dsarm072@#8 = #dsarm004@#3;
                 |FINSI;
             |FINSI;

         |FINSI;
     |FINSI;
|FPRC;

|PROCESO ControlaModelo;
     |DBASS aAlfa; |QBF aAlfa;
     aAlfa = aAlfa + "basica/def/agitab99.mas";
     |CARGA_DEF aAlfa, agitab99@;

     |DBASS aAlfa; |QBF aAlfa;
     aAlfa = aAlfa + "basica/fich/";
     |PATH_DAT #agitab99@#aAlfa#;

     |ABRE #agitab99@;
     |DDEFECTO #agitab99@;
     #agitab99@#0 = #dsarm072@#0;
     |LEE 000#agitab99@.=;
     |SI FSalida ! 0;
         |DDEFECTO #agitab99@;
         #agitab99@#0 = #dsarm072@#0;

         |SI #agitab99@#0 = 990;
             #agitab99@#1 = "RECIBOS DE NOMINA";
             #agitab99@#2 = "RECIBOS SALARIALES DE TRABAJADORES";
             #agitab99@#3 = "";
             #agitab99@#5 = "M";
         |FINSI;

         |SI #agitab99@#0 = 991;
             #agitab99@#1 = "SEGURIDAD SOCIAL ONLINE (ALTA,BAJA,ETC.)";
             #agitab99@#2 = "Documentos que generan los procesos de Seguridad Social Online, como";
             #agitab99@#3 = "altas, cambios de contrato, bajas etc. Para DSArchi.";
             #agitab99@#5 = "M";
         |FINSI;

         |SI #agitab99@#0 = 992;
             #agitab99@#1 = "CONTRATOS";
             #agitab99@#2 = "Documentos asociados a contratación generados por NetContrat@ (o por";
             #agitab99@#3 = "Contrat@)";
             #agitab99@#5 = "M";
         |FINSI;

         |SI #agitab99@#0 = 993;
             #agitab99@#1 = "CERTIFICADOS";
             #agitab99@#2 = "Documentos asociados a certificados de empresa";
             #agitab99@#3 = "(huella generada por NetContrat@ o el propio certificado)";
             #agitab99@#5 = "M";
         |FINSI;

         |SI #agitab99@#0 = 994;
             #agitab99@#1 = "RESUMENES LABORALES";
             #agitab99@#2 = "";
             #agitab99@#3 = "";
             #agitab99@#5 = "M";
         |FINSI;

         |SI #agitab99@#0 = 995;
             #agitab99@#1 = "REGISTRO HORAS TIEMPO PARCIAL";
             #agitab99@#2 = "";
             #agitab99@#3 = "";
             #agitab99@#5 = "M";
         |FINSI;

         |SI #agitab99@#0 = 996;
             #agitab99@#1 = "RNT";
             #agitab99@#2 = "RELACION NOMINAL DE TRABAJADORES";
             #agitab99@#3 = "";
             #agitab99@#5 = "M";
         |FINSI;

         |SI #agitab99@#0 = 997;
             #agitab99@#1 = "RLC";
             #agitab99@#2 = "RELACION LIQUIDACION DE COTIZACIONES";
             #agitab99@#3 = "";
             #agitab99@#5 = "M";
         |FINSI;

         |SI #agitab99@#0 = 998;
             #agitab99@#1 = "FLC";
             #agitab99@#2 = "FUNDACION LABORAL DE LA CONSTRUCCION";
             #agitab99@#3 = "";
             #agitab99@#5 = "M";
         |FINSI;

         |SI #agitab99@#0 = 999;
             #agitab99@#1 = "INF";
             #agitab99@#2 = "INFORMES LABORALES";
             #agitab99@#3 = "";
             #agitab99@#5 = "M";
         |FINSI;

         |GRABA 020#agitab99@.n;
         |LIBERA #agitab99@;
     |FINSI;
     |CIERRA #agitab99@;

     |DESCARGA_DEF #agitab99@;
|FPRC;


|PROCESO GrabaModelo;
     aAlfa1 = aAlfa % 103;
     aAlfa2 = aAlfa % 502;
     aAlfa3 = aAlfa % 1240;
     aAlfa4 = aAlfa % 5203;
     aAlfa5 = aAlfa % 5503;
     aAlfa6 = aAlfa % 5803;

     #dsarm072@#0 = aAlfa1;
     #dsarm072@#1 = aAlfa2;
     |LEE 000#dsarm072@.=;
     |SI FSalida = 0;  |ACABA;  |FINSI;

     |DDEFECTO #dsarm072@;
     #dsarm072@#0 = aAlfa1;
     #dsarm072@#1 = aAlfa2;
     #dsarm072@#2 = aAlfa3;
     #dsarm072@#3 = aAlfa4;
     #dsarm072@#4 = aAlfa5;
     #dsarm072@#5 = aAlfa6;
     #dsarm072@#9 = "S";
     |HAZ LeeDescripcion;

     |GRABA 020#dsarm072@.n;
     |LIBERA #dsarm072@;

     |HAZ ControlaModelo;
|FPRC;

|PROCESO GrabaFijos;
            ||          1         2         3         4
            || 1234567890123456789012345678901234567890
     aAlfa  = "990/00     NOMINA                                  001001001"; |HAZ GrabaModelo;
     aAlfa  = "990/01     NOMINA (PAGAS)                          001001002"; |HAZ GrabaModelo;
     aAlfa  = "990/02     NOMINA (FINIQUITOS)                     001001003"; |HAZ GrabaModelo;
     aAlfa  = "990/05     NOMINA (ATRASOS)                        001001004"; |HAZ GrabaModelo;
     aAlfa  = "991/00     AFILIACION ALTAS                        001004001"; |HAZ GrabaModelo;
     aAlfa  = "991/01     AFILIACION BAJAS                        001004002"; |HAZ GrabaModelo;
     aAlfa  = "991/02     AFILIACION VARIACIONES                  001004003"; |HAZ GrabaModelo;
     aAlfa  = "991/03     AFILIACION CAMBIO CONTRATO              001004003"; |HAZ GrabaModelo;
     aAlfa  = "991/04     AFILIACION ALTAS IDC                    001004001"; |HAZ GrabaModelo;
     aAlfa  = "991/05     AFILIACION BAJAS IDC                    001004002"; |HAZ GrabaModelo;
     aAlfa  = "992/00     CONTRATO (HUELLA)                       001002002"; |HAZ GrabaModelo;
     aAlfa  = "992/01     CONTRATO                                001002001"; |HAZ GrabaModelo;
     aAlfa  = "992/02     PRORROGA                                001002003"; |HAZ GrabaModelo;
     aAlfa  = "993/00     CERTIFICADOS (HUELLA)                   001006002"; |HAZ GrabaModelo;
     aAlfa  = "993/01     CERTIFICADO EMPRESA                     001006002"; |HAZ GrabaModelo;
     aAlfa  = "994/00     RESUMENES                               001007001"; |HAZ GrabaModelo;
     aAlfa  = "995/00     HORAS TIEMPO PARCIAL                    001008001"; |HAZ GrabaModelo;
     aAlfa  = "996/00     RNT                                     001003001"; |HAZ GrabaModelo;
     aAlfa  = "996/02     RNT (FINIQUITOS)                        001003001"; |HAZ GrabaModelo;
     aAlfa  = "996/05     RNT (ATRASOS)                           001003001"; |HAZ GrabaModelo;
     aAlfa  = "996/90     RNT (COMPLEMENTARIOS)                   001003001"; |HAZ GrabaModelo;
     aAlfa  = "997/00     RLC                                     001003002"; |HAZ GrabaModelo;
     aAlfa  = "997/02     RLC (FINIQUITOS)                        001003002"; |HAZ GrabaModelo;
     aAlfa  = "997/05     RLC (ATRASOS)                           001003002"; |HAZ GrabaModelo;
     aAlfa  = "997/90     RLC (COMPLEMENTARIOS)                   001003002"; |HAZ GrabaModelo;
     aAlfa  = "998/00     FLC                                     001003003"; |HAZ GrabaModelo;
     aAlfa  = "999/00     RELACION DE TRANSFERENCIAS              001008002"; |HAZ GrabaModelo;
     aAlfa  = "999/01     INFORME DE ALTAS                        001008003"; |HAZ GrabaModelo;
     aAlfa  = "999/02     INFORME DE BAJAS                        001008004"; |HAZ GrabaModelo;
     aAlfa  = "999/03     INFORME DE ABSENTISMOS                  001008005"; |HAZ GrabaModelo;
     aAlfa  = "999/04     INFORME DE VENCIMIENTOS                 001008006"; |HAZ GrabaModelo;
     aAlfa  = "999/05     INFORME COMPARATIVO PERSONAL ASALARIADO 001008007"; |HAZ GrabaModelo;
|FPRC;

|PROCESO Alta4;
     #dsarm004@#0 = 1;
     #dsarm004@#1 = 8;
     #dsarm004@#2 = nCod;
     |LEE 000#dsarm004@.=;
     |SI FSalida = 0;  |ACABA;  |FINSI;

     |DDEFECTO #dsarm004@;
     #dsarm004@#0  = 1;
     #dsarm004@#1  = 8;
     #dsarm004@#2  = nCod;
     #dsarm004@#3  = aDes;
     #dsarm004@#4  = aCar;
     #dsarm004@#20 = "S";
     #dsarm004@#22 = "S";

     |GRABA 020#dsarm004@.n;
     |LIBERA #dsarm004@;
|FPRC;

|PROCESO CarpetaNuevasLaboral;
     #dsarm003@#0 = 1;
     #dsarm003@#1 = 8;
     |LEE 101#dsarm003@.=;
     |SI FSalida ! 0;
         |DDEFECTO #dsarm003@;

         #dsarm003@#0 = 1;
         #dsarm003@#1 = 8;
         #dsarm003@#2 = "INFORMES LABORALES";
         #dsarm003@#3 = "INFORMES";
         #dsarm003@#6 = "INFORMES LABORALES";

         |GRABA 020#dsarm003@.b;
     |FINSI;

     aAlfa = #dsarm003@#2 - "INFORME HORAS TIEMPO";
     |SI FSalida ! 0;
         #dsarm003@#2 = "INFORMES LABORALES";
         #dsarm003@#6 = "INFORMES LABORALES";
     |FINSI;

     |GRABA 020#dsarm003@.a;
     |LIBERA #dsarm003@;

     aAlfa = #dsarm003@#2; |QBF aAlfa;
     |SI aAlfa = "INFORMES LABORALES";
         nCod = 1;
         aDes = "INFORME HORAS TIEMPO PARCIAL";
         aCar = "INFORMEHORAS";
         |HAZ Alta4;

         nCod = 2;
         aDes = "RELACION DE TRANSFERENCIAS";
         aCar = "TRANSFERENCIAS";
         |HAZ Alta4;

         nCod = 3;
         aDes = "INFORME DE ALTAS";
         aCar = "INFORMEALTAS";
         |HAZ Alta4;

         nCod = 4;
         aDes = "INFORME DE BAJAS";
         aCar = "INFORMEBAJAS";
         |HAZ Alta4;

         nCod = 5;
         aDes = "INFORME DE ABSENTISMO";
         aCar = "INFORMEABSENTISMO";
         |HAZ Alta4;

         nCod = 6;
         aDes = "INFORME DE VENCIMIENTOS";
         aCar = "INFORMEVENCIMIENTOS";
         |HAZ Alta4;

         nCod = 7;
         aDes = "INFORME COMPARATIVO PERSONAL ASALARIADO";
         aCar = "INFORMECOMPARATIVO";
         |HAZ Alta4;
     |FINSI;
|FPRC;

|PROCESO CreaDefectos;
     |ABRE #dsarm002@;
     |ABRE #dsarm003@;
     |ABRE #dsarm004@;
     |ABRE #dsarm072@;

     |HAZ CarpetaNuevasLaboral;
     |HAZ GrabaFijos;

     |CIERRA #dsarm072@;
     |CIERRA #dsarm004@;
     |CIERRA #dsarm003@;
     |CIERRA #dsarm002@;
|FPRC;

|PROCESO TipoModelo;
     |SI eaVengoDe = "nomrecib";
          nModelo = 990;
          nTipo = #nomrecib#10;    || el tipo me viene del calculo
                                   || 0 ¢ 2 para finiquitos
          |SI #nomrecib#68 = "P";
               nTipo = 1;
          |FINSI;
          |SI #nomrecib#68 = "F";
               nTipo = 2;
          |FINSI;
          |SI #nomrecib#67 = "A";
               nTipo = 5;
               nTipoAnt = 5;
          |FINSI;
          aAlfa = PARAMETRO % 101
          |SI aAlfa = "T";
               nTipo = 5;
               nTipoAnt = #nomrecib#16;
          |FINSI;
     |FINSI;

     |SI eaVengoDe = "nomareci";
          nModelo = 990;
          nTipo = #nomareci#10;
          nTipoAnt = nTipo;
          nTipo = 5;
     |FINSI;

     |SI eaVengoDe = "labimpre"; |O eaVengoDe = "contrato";
          nModelo = 992;           || modelo 992 para contratos
          |SI eaVengoDe = "contrato";
               nTipo = 1;
               |SI swEsProrroga = 1;
                    nTipo = 2;
               |FINSI;
          |SINO;
               nTipo = 0;
          |FINSI;
     |FINSI;

     |SI eaVengoDe = "labcexml"; |O eaVengoDe = "labimpce";
          nModelo = 993;           || modelo 993 para certificados
          nTipo   = 0;
     |FINSI;

     |SI eaVengoDe = "nomyrenm";   || resumen de nominas
          nModelo = 994;
          |SI swPagasSN = 1;
               nTipo = 01;
               |SI enSwFiniq = 1;
                    nTipo = 04;
               |FINSI;
          |FINSI;
          |SI swPagasSN = 2;
               nTipo = 03;
          |FINSI;
     |FINSI;

     |SI eaVengoDe = "nomyrpac";   || resumen de pagas
          nModelo = 994;
          nTipo = 02;
     |FINSI;


     |SI eaVengoDe = "nomirenm";   || resumen de nominas areas y secciones
          nModelo = 994;
          |SI swPagasSN = 1;
               nTipo = 11;
               |SI enSwFiniq = 1;
                    nTipo = 14;
               |FINSI;
          |FINSI;
          |SI swPagasSN = 2;
               nTipo = 13;
          |FINSI;
     |FINSI;

     |SI eaVengoDe = "nomirpas";   || resumen de pagas areas y secciones
          nTipo = 12;
     |FINSI;

     |SI eaVengoDe = "labcerti";
          nModelo = 993;
          nTipo   = 1;
     |FINSI;

     |SI eaVengoDe = "nominfho";   || Informe de horas realizadas
          nModelo = 995;
          nTipo   = 0;
     |FINSI;

     |SI eaVengoDe = "nomytran";   || Informe transferencias bancarias
     |O eaVengoDe = "nomytras";
          nModelo = 999;
          nTipo   = 0;
     |FINSI;

     |SI eaVengoDe = "nomyyflc";   || FLC
          nModelo = 998;
          nTipo   = 0;
     |FINSI;

     |SI eaVengoDe = "cretai05";   || RNT
          nModelo = 996;
          nTipo   = enTipo;
     |FINSI;

     |SI eaVengoDe = "cretai07";   || RLC
          nModelo = 997;
          nTipo   = enTipo;
     |FINSI;

     |SI nModelo = 994;            || resumen de nominas
          nTipoAnt = nTipo;
          nTipo = 000;
     |FINSI;

     |SI eaVengoDe = "nomxalta";  nModelo = 999;  nTipo   = 1;  |FINSI;
     |SI eaVengoDe = "nominfde";  nModelo = 999;  nTipo   = 2;  |FINSI;
     |SI eaVengoDe = "nombajas";  nModelo = 999;  nTipo   = 3;  |FINSI;
     |SI eaVengoDe = "nombaja2";  nModelo = 999;  nTipo   = 3;  |FINSI;
     |SI eaVengoDe = "nomvenc2";  nModelo = 999;  nTipo   = 4;  |FINSI;
     |SI eaVengoDe = "labinfco";  nModelo = 999;  nTipo   = 5;  |FINSI;
|FPRC;

|PROCESO CompruebaModelo;
     |DBASS aBass;
     aDef = aBass + "dsarchi/def/dsarm072.mas";
     |CARGA_DEF aDef, dsarm072@;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     aDef = aBass + "dsarchi/def/dsarm002.mas";
     |CARGA_DEF aDef, dsarm002@;
     |SI FSalida ! 0;
         |DESCARGA_DEF #dsarm072@;
         |ACABA;
     |FINSI;

     aDef = aBass + "dsarchi/def/dsarm003.mas";
     |CARGA_DEF aDef, dsarm003@;
     |SI FSalida ! 0;
         |DESCARGA_DEF #dsarm002@;
         |DESCARGA_DEF #dsarm072@;
         |ACABA;
     |FINSI;

     aDef = aBass + "dsarchi/def/dsarm004.mas";
     |CARGA_DEF aDef, dsarm004@;
     |SI FSalida ! 0;
         |DESCARGA_DEF #dsarm003@;
         |DESCARGA_DEF #dsarm002@;
         |DESCARGA_DEF #dsarm072@;
         |ACABA;
     |FINSI;

     aPathDatos = aBass + "dsarchi/fich/";
     |PATH_DAT #dsarm072@#aPathDatos#;
     |PATH_DAT #dsarm002@#aPathDatos#;
     |PATH_DAT #dsarm003@#aPathDatos#;
     |PATH_DAT #dsarm004@#aPathDatos#;

     |HAZ CreaDefectos;
     swNoArchives = 0;

     |ABRE #dsarm002@;
     |ABRE #dsarm003@;
     |ABRE #dsarm004@;

     |ABRE #dsarm072@;
     |DDEFECTO #dsarm072@;
     #dsarm072@#0 = nModelo;
     #dsarm072@#1 = nTipo;
     |LEE 000#dsarm072@.=;
     |SI FSalida = 0;
         enGrupoTres = #dsarm072@#3;
         enSubGTres  = #dsarm072@#4;
         enTipoTres  = #dsarm072@#5;

         |SI #dsarm072@#9 = "S";
             nSwPideEstructura = 1;
         |SINO;
             nSwPideEstructura = 0;
            enSwMeteGrupoSubTipo = 1;
         |FINSI;
     |SINO;
          || SI EL MODELO NO EXISTE, NO SE ARCHIVA NADA
          nSwPideEstructura = 0;
          swNoArchives = 1;
     |FINSI;

     |CIERRA #dsarm002@;
     |CIERRA #dsarm003@;
     |CIERRA #dsarm004@;
     |CIERRA #dsarm072@;

     |DESCARGA_DEF #dsarm004@;
     |DESCARGA_DEF #dsarm003@;
     |DESCARGA_DEF #dsarm002@;
     |DESCARGA_DEF #dsarm072@;

     |SI nModelo = 990; |Y nTipo = 5;
          nTipo = nTipoAnt;
     |FINSI;

     |SI nModelo = 994;
          nTipo = nTipoAnt;
     |FINSI;
|FPRC;

|PROCESO CtrlPedirGrupoDsArchi;
     |SI eaVengoDe = "nomenvio";
          nSwPideEstructura = 0;
          swNoArchives = 0;
          |ACABA;
     |FINSI;

     nSwEstaDsArchi = 0;
     |HAZ EstaDsArchi;

     nContador = nContador + 1;
     |HAZ TipoModelo;

     swNoArchives = 0;
     |HAZ CompruebaModelo;

     |SI nSwEstaDsArchi ! 0; |Y swNoArchives = 0;
          |SI nModelo = 990; |O nModelo = 994;  |O nModelo = 999;
           |O nModelo = 996; |O nModelo = 997;  |O nModelo = 998;
               |SI nContador > 1;
                    || estoy en resumenes o nomina y ya tengo el grupo
                    || subgrupo o tipo porque lo he pedido en la primera

                    enSwMeteGrupoSubTipo = 1;

                    |ACABA;
               |FINSI;
          |SINO;
               || continua
          |FINSI;
          enGrupoTres = 0;
          enSubGTres = 0;
          enTipoTres = 0;

          ||CARGA_DEF

          |DBASS aBass;
          aDef = aBass + "dsarchi/def/dsarm001.mas";
          |CARGA_DEF aDef, dsarm001@;
          |SI FSalida ! 0;
               |ACABA;
          |FINSI;

          aPathDatos = aBass + "dsarchi/fich/";
          |PATH_DAT #501 aPathDatos;

          |ABRE #dsarm001@;
          |DDEFECTO #dsarm001@;
          |LEE 000#dsarm001@.=;
          |SI #dsarm001@#123 = "S";
               nSwPideEstructura = 1;
          |SINO;
               nSwPideEstructura = 0;
          |FINSI;

          |CIERRA #dsarm001@;
          |DESCARGA_DEF #dsarm001@;

          || la variable nSwPideEstructura ya no se usa de manera global
          || se usa lo que se ponga para cada documento

          |HAZ TipoModelo;
          |HAZ CompruebaModelo;

          |SI nSwPideEstructura = 1;
               |SI enSwDesde9 = 0;
                    aEjecuta = ":dsarchi/dsarz003;DAMETRE";
               |SINO;
                    |DBASS aBass;
                    |QBF aBass;
                    aIntePara = aBass + "dsarchi/intepara/" + Usuario + "parametro.txt";
                    |FBORRA aIntePara;

                    |XABRE "WB", aIntePara, nHandle;
                    aAlfa = (("000" + enGrupoTres) % -103);
                    aAlfa = aAlfa + (("000" + enSubGTres) % -103);
                    aAlfa = aAlfa + (("000" + enTipoTres) % -103);
                    aAlfa = aAlfa + &13 + &10;
                    |XGRABA nHandle, aAlfa;
                    |XCIERRA nHandle;

                    aEjecuta = "|:dsarchi/dsarz003;DAMETRE" + Usuario + "parametro.txt";
               |FINSI;

               |SUB_EJECUTA_NP aEjecuta;

               |PULSATECLA;
               |PULSATECLA;
               |PULSATECLA;
               |PULSATECLA;
               |PULSATECLA;

               |SI enSwDesde9 = 1;
                    |XABRE "U", aIntePara, nHandle;
                    |SI FSalida ] 0;
                         |XLEE nHandle, 250, aLinea;
                         |SI FSalida ] 0;
                              |QBF aLinea;

                              aAux = aLinea % 103;
                              enGrupoTres = aAux;
                              aAux = aLinea % 403;
                              enSubGTres = aAux;
                              aAux = aLinea % 703;
                              enTipoTres = aAux;
                         |FINSI;
                    |FINSI;
                    |XCIERRA nHandle;
                    |FBORRA aIntePara;
               |FINSI;
               |SI enGrupoTres = 0; |Y enSubGTres = 0; |Y enTipoTres = 0;
                   swNoArchives = 1;
               |FINSI;

               |SI enGrupoTres ! 0;
                    enSwMeteGrupoSubTipo = 1;
               |FINSI;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO IniEnvioDocDSArchi;
     enSwCDefOk = 0;

     nSwEstaDsArchi = 0;
     |HAZ EstaDsArchi;
     |SI nSwEstaDsArchi = 0;
          aMensaje = "0000Atenci¢n: Modulo DSARCHI no instalado.";
          |MENAV aMensaje;
          |ACABA;
     |FINSI;

     |DBASS aBass;  |QBF aBass;

     aUsuTmp = Usuario;
     |QBF aUsuTmp;
     aUsuTmp = "rd" + aUsuTmp;

     |SI enSwUbicacionDoc = "S";
          aBaseArchi = aBass + "dsarchi/";
          aPathTmpUsu = aBaseArchi + "rec_doc/";
          |MKDIR aPathTmpUsu;
     |SINO;
          aUnidad = "C";
          |HAZ DimeUnidad;

          aPathTmpUsu = aUnidad + ":\DOCUMENTOS\";
          |RMKDIR aPathTmpUsu;
          aPathTmpUsu = aPathTmpUsu + "DSARCHI\";
          |RMKDIR aPathTmpUsu;

          aUsuTmp = Usuario;
          |QBF aUsuTmp;
          aUsuTmp = "rd" + aUsuTmp;
          aPathTmpUsu = aPathTmpUsu + aUsuTmp + "\";

          ||Borro todo lo que haya en esta carpeta
          ||pero el RRMDIR, solo borra si la carpeta esta vacia.
          ||necesito un R_PDIR
          aBorra = aPathTmpUsu + "*.*";
          |R_PDIR aBorra;
          |PARA; |SI FSalida = 0; |HACIENDO;
               |RFBORRA aBorra;
               |R_SDIR aBorra;
          |SIGUE;

          |RRMDIR aPathTmpUsu;

          |RMKDIR aPathTmpUsu;
     |FINSI;

     aDef = aBass + "dsarchi/def/dsarm005.mas";
     |CARGA_DEF aDef, referen@;
     |SI FSalida ! 0;
          enSwCDefOk = 0;
          aMensaje = "0000Problemas al cargar el fichero: " + aDef;
          |MENAV aMensaje;
          |ACABA;
     |SINO;
          enSwCDefOk = 1;
     |FINSI;

     aPathDatos = aBass + "dsarchi/fich/";
     |PATH_DAT #500 aPathDatos;
     |NOME_DAT #500 aUsuTmp;

     |ABRE #500;  |CIERRA #500;  |DELINDEX #500;

     |ABRE #500;
|FPRC;

|PROCESO FinEnvioDocDSArchi;
     nSwEstaDsArchi = 0;
     |HAZ EstaDsArchi;
     |SI nSwEstaDsArchi = 0;
          aMensaje = "0000Atenci¢n: Modulo DSARCHI no instalado.";
          |MENAV aMensaje;
          |ACABA;
     |FINSI;

     |SI enNumeroRegistro ! 0;
          |GRABA 020#500.a;
          |LIBERA #500;
     |FINSI;

     nCli = #500#8;
     nDoc = #500#0;

     |CIERRA #500;

     |SI enSwCDefOk = 1;
          |DESCARGA_DEF #referen@;
     |FINSI;

     |SI enSwCDefOk = 0;
         |DBASS aBass;  |QBF aBass;

         aDef = aBass + "dsarchi/def/dsarw120.mas";
         |CARGA_DEF aDef, referen@;
         |SI FSalida = 0;
             aPathDatos = aBass + "dsarchi/fich/";
             |PATH_DAT #referen@#aPathDatos#;

             aFic = "dsarw120" + Usuario;
             |NOME_DAT #referen@#aFic#;

             |ABRE #referen@;
             #referen@#0 = nCli;
             #referen@#1 = nDoc;
             |LEE 000#referen@.=;
             |SI FSalida ! 0;
                 #referen@#0 = nCli;
                 #referen@#1 = nDoc;
                 |GRABA 000#referen@.n;
                 |LIBERA #referen@;
             |FINSI;
             |CIERRA #referen@;
             |DESCARGA_DEF #referen@;
         |FINSI;
     |FINSI;

     enSwDesde9 = 1;

     |SI enSwUbicacionDoc = "S";
          eaNomTemporal = aUsuTmp;
          aEjecuta = ":dsarchi/dsarz032";
          |SUB_EJECUTA_NP aEjecuta;

          |PULSATECLA;
          |PULSATECLA;
          |PULSATECLA;
          |PULSATECLA;
          |PULSATECLA;

          |SI enSwErrorRecDoc = 1;
               aMensaje = "0000Ha ocurrido un error al archivar los documentos en DSARCHI. Revise los logs [rec_doc.log]";
               |MENAV aMensaje;
               |ACABA;
          |FINSI;
     |SINO;
          |DBASS aBass;
          |QBF aBass;
          aUnidad = "C";
          |HAZ DimeUnidad;

          aPathTmpUsu = aUnidad + ":\DOCUMENTOS\";
          |RMKDIR aPathTmpUsu;
          aPathTmpUsu = aPathTmpUsu + "DSARCHI\";
          |RMKDIR aPathTmpUsu;

          aUsuTmp = Usuario;
          |QBF aUsuTmp;
          aUsuTmp = "rd" + aUsuTmp;
          aPathTmpUsu = aPathTmpUsu + aUsuTmp + "\";

          ||Comprimir
          aFicTar = aPathTmpUsu + aUsuTmp + ".tar" + " 000*.*";
          |RATAR aFicTar, aPathTmpUsu;
          |SI FSalida = 0;
               aFicTar = aPathTmpUsu + aUsuTmp + ".tar";
               |RCOMPRIME aFicTar;
               |SI FSalida = 0;
                    aOrigen = aPathTmpUsu + aUsuTmp + ".tgz";
                    aDestino = aBass + "dsarchi/rec_doc/" + aUsuTmp + ".tgz";

                    aIPRemoto = "";
                    |IP_REMOTO aIPRemoto;  |QBT aIPRemoto;
                    |SI aIPRemoto ! "";
                         |RECIBE_FICHERO aOrigen, aDestino;
                    |SINO;
                         |COPIA_FICHERO aOrigen, aDestino;
                    |FINSI;

                    eaNomTemporal = aUsuTmp;
                    aEjecuta = ":dsarchi/dsarz032";
                    |SUB_EJECUTA_NP aEjecuta;
                    |PULSATECLA;
                    |PULSATECLA;
                    |PULSATECLA;
                    |PULSATECLA;
                    |PULSATECLA;

                    |SI enSwErrorRecDoc = 1;
                         aMensaje = "0000Ha ocurrido un error al archivar los documentos en DSARCHI. Revise los logs [rec_doc.log]";
                         |MENAV aMensaje;
                         |ACABA;
                    |FINSI;
               |FINSI;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO DocFisicoDSArchi;
     nSwEstaDsArchi = 0;
     |HAZ EstaDsArchi;
     |SI nSwEstaDsArchi = 0;
          |ACABA;
     |FINSI;

     |DBASS aBass;
     |QBF aBass;

     |SI enNumeroRegistro ! 0;
          |GRABA 020#500.a;
          |LIBERA #500;
     |FINSI;

     |DDEFECTO #500;

     |LEE 000#500.u;
     |SI FSalida = 0;
          enNumeroRegistro = #500#0 + 1;
     |SINO;
          |HAZ Random;  rnd = rnd $ 999999; rnd = rnd + 1;
          enNumeroRegistro = rnd;
     |FINSI;

     |DDEFECTO #500;
     #500#0 = enNumeroRegistro;
     |LEE 101#500.=;
     |SI FSalida ! 0; |GRABA 020#500.b; |FINSI;

     aExtension = eaNomDocArchi % -104;
     aSinExtension = (("000000000") + enNumeroRegistro) % -109;
     aNombre = aSinExtension + aExtension;
     #500#9 = aNombre;

     aOrigen = eaDocumentoArchi;
     aDestino = aPathTmpUsu + aSinExtension + aExtension;

     |SI enSwUbicacionDoc = "S";
          |COPIA_FICHERO aOrigen, aDestino;
     |SINO;
          |RCOPIA_FICHERO aOrigen, aDestino;
     |FINSI;

     #500#93 = eaNomDocArchi;
|FPRC;

|PROCESO MeteCampoDSArchi;
     |QBF eaValorArchi;
     #500#enNumCampoArchi# = eaValorArchi;
|FPRC;

||-------------------- Proceso que genera aleatorios (variable 'rnd')-------
|| El numero aleatorio se devuelve en la variable 'rnd'
|| Ej: obtener un numero comprendido entre 1 y 100:
||  |HAZ Random;  rnd = rnd $  100; rnd = rnd + 1;  --->   1 ] rnd [ 100

|PROCESO Random;
     r_vuelta = r_vuelta + 1;
     |SI r_semilla [ 0; |O r_vuelta > 200;
          |HORA r_hora;
          r_beta = r_hora % 702;
          r_semilla = r_beta;
          r_vuelta = 0;
     |FINSI;
     r_producto = r_semilla / r_pi;
     r_beta = r_producto;
     |PARA r_bucle = 101;|SI r_bucle < 1000;|HACIENDO r_bucle = r_bucle + 100;
          r_punto = r_beta % r_bucle;
          |SI r_punto = "."; |SAL; |FINSI;
     |SIGUE;
     r_bucle = r_bucle + 104;
     r_beta = r_beta % r_bucle;
     r_semilla = r_beta;
     rnd = r_semilla;
|FPRC;
