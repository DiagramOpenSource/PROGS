|FICHEROS;
    aomagico #0;
    labempre #1;    || empresas
    aomcoste #2;    || costes

    :/contagen/def/camandia #20;   || diarios CONTAGEN
    :/contagen/def/caw00030 #21;   || conceptos CONTAGEN
    :/contagen/def/camaasic #22;   || asientos cabs. CONTAGEN
    :/contagen/def/camaasil #23;   || asientos lins. CONTAGEN
    :/contagen/def/cacuenta #24;   || cuentas CONTAGEN
    :/contagen/def/canempre #25;   || empresas CONTAGEN
    :/contagen/def/caconasi #26;   || contador asientos CONTAGEN
    :/contagen/def/catradia #27;   || diario de trabajo
    :/contagen/def/cacuebal #28;   || cuentas de balance

     aomparam #13;
     gomantho@;
|FIN;

|VARIABLES;
     aD = "";
     aH = "";
     nEmpresaAnt = 0;
     aObraAnt = "";
     nObra = 0;
     aPath = "";
     aDef = "";
     aEmp = "";
     aAlfa = "";
     aFic = "";
     aDObra = "";
     aHObra = "";

    adig  = "";
    alfa1 = "";
    alfa2 = "";
    alfa3 = "";
    alfa4 = "";
    nume1 = 0;
    nume2 = 0;
    nume3 = 0;
    nume4 = 0;
    para1 = 0;
    para2 = 0;
    para3 = 0;
    para4 = 0;
    fech1 = @;
    fech2 = @;
    fech3 = @;
    fech4 = @;
    elsw1 = 0;
    elsw2 = 0;
    elsw3 = 0;
    elsw4 = 0;
    FEstado = 0;
    seleccio = 0;
    modo = 0;
    sw_rup = 0;

    p_mes = "";
    path_agicon = "";
    path_empres = "";
    path_agicli = "";
    ultimo = 0;
    sw_agicli = 0;
    cod_empre = 0;
    cod_perio = 0;
    vacac = 0;
    prorr = 0;
    deven = 0;
    rirpf = 0;
    rsegu = 0;
    liqui = 0;
    eltc1 = 0;
    elvac = 0;
    signe = "";
    impor = 0;
    conte = "";
    digcu = 0;
    conce = 0;
    descr = "";
    empre = 0;
    cuantos = 0;
    ctana = "";
    nctan = 0;
    aobra = "";
    sw_alguna = 0;

    p_mensa = "    LONGITUD DE CUENTA FUERA DE NIVEL!";
    d_diari = 0;
    d_opera = "";
    d_impor = 0;
    &modcon = 0;
    &codcon = 0;
    importe = 0;
    opera = "";
    ressum = 0;
    ini = 0;
    dig1 = 0;
    dig3 = 0;
    dig4 = 0;
    dig5 = 0;
    dig6 = 0;
    dig7 = 0;
    dig8 = 0;
    dig9 = 0;
    fecalf = "";
    mes = "";
    mesfec = 0;
    mesac = 0;
    a = 0;
    b = 0;
    c = 0;
    ult_apunt = 0;
    p_alfa1 = "";
    p_error = 0;
    p_lugar = 0;
    p_cuan3 = 0;
    p_cuan4 = 0;
    p_cuan5 = 0;
    p_alfa2 = "";
    p_alfa3 = "";
    p_alfa4 = "";
    p_alfa5 = "";
    p_nume1 = 0;
    p_nume2 = 0;
    p_ceros = "";

    juan = 0;
    errant = 0;
    cuini = "            ";
    cufin = "999999999998";
    diini = 0;
    difin = 9;
    fecini = @;
    fecfin = @;
    tiini = "D";
    tifin = "H";
    diain = 0;
    diafi = 99;
    prim = 0;
    cuant = "";
    lon = 0;
    aa = 0;
    mensa = "";
    aar = "";
    traba = "";
    errcuent = "aaaaaaaaaaaa";
    mensa2   = "     No esta definida esta cuenta en Cuentas de Balance";
    uno = "";
    dos = "";
    mascara = "";
    nivel = 0;
|FIN;
____________________________________/
|PROGRAMA;
aD = " " * 20;
aH = "z" * 20;
|CLS;
|CABEZA "Enlace CONTAGEN Obras";
|DDEFECTO #0;
|PINPA #0#0;
|PINDA #0#0;
|HAZ tipo8;
|ABRE #0;
|LEE 101#0p;
|SI FSalida ! 0;
    |GRABA 020#0b;
|FINSI;
    #0#42 = "NO";
|PINDA #0#0;
|ENDATOS #1#0;
|SI FSalida = 0;|Y #0#42 = "SI";
    |HAZ tipo3;
    |SI cuantos ! 0;
            alfa1 = "    Creados [" + cuantos + "] asientos ...";
        |MENAV alfa1;
    |SINO;
        |MENAV "    Seleccion vacia ...";
    |FINSI;
    |GRABA 020#0a;
|FINSI;
|LIBERA #0;
|CIERRA #0;
|FPRO;
____________________________________/ CALCULOS PANTALLA
|PROCESO mayus; |TIPO 0;
    #0Campo = #0Campo > #0Campo;
|PINTA #0Campo;
|FPRC;

|PROCESO defecto; |TIPO 7;
    fech1 = ~;
    alfa1 = fech1;
    alfa1 = alfa1 % -102;
    #0Campo = alfa1;
|PINTA #0Campo;
|FPRC;

|PROCESO mirames; |TIPO 7;
    fech1 = ~;
    alfa1 = fech1;
    alfa1 = alfa1 % 402;
    #0Campo = alfa1;
|PINTA #0Campo;
|FPRC;

|PROCESO pintames; |TIPO 0;
|SI #0Campo =  1;  p_mes = "ENERO     ";  ultimo = 31; |FINSI;
|SI #0Campo =  2;  p_mes = "FEBRERO   ";  ultimo = 28; |FINSI;
|SI #0Campo =  3;  p_mes = "MARZO     ";  ultimo = 31; |FINSI;
|SI #0Campo =  4;  p_mes = "ABRIL     ";  ultimo = 30; |FINSI;
|SI #0Campo =  5;  p_mes = "MAYO      ";  ultimo = 31; |FINSI;
|SI #0Campo =  6;  p_mes = "JUNIO     ";  ultimo = 30; |FINSI;
|SI #0Campo =  7;  p_mes = "JULIO     ";  ultimo = 31; |FINSI;
|SI #0Campo =  8;  p_mes = "AGOSTO    ";  ultimo = 31; |FINSI;
|SI #0Campo =  9;  p_mes = "SEPTIEMBRE";  ultimo = 30; |FINSI;
|SI #0Campo = 10;  p_mes = "OCTUBRE   ";  ultimo = 31; |FINSI;
|SI #0Campo = 11;  p_mes = "NOVIEMBRE ";  ultimo = 30; |FINSI;
|SI #0Campo = 12;  p_mes = "DICIEMBRE ";  ultimo = 31; |FINSI;
|ATRI I;
|PINTA 0721, p_mes;
|ATRI 0;
|FPRC;

|PROCESO tipo8; |TIPO 8;
     |DBASS alfa1;
     path_agicon = alfa1 + "contagen/";

     alfa1 = path_agicon + "pan/";
     |PATH_PAN #24 alfa1;          || para el alta de cuentas (camaapua)
     |PATH_PAN #25 alfa1;          || para la consulta
|FPRC;

|PROCESO desde_hasta; |TIPO 0;
|PARA para1 = 2; |SI para1 [ 11; |HACIENDO para1 = para1 + 1;
    |CAMPO_MODIFICA #0para1, 1, "S";
|SIGUE;
|CAMPO_MODIFICA #0#12, 1, "N";
|CAMPO_MODIFICA #0#13, 1, "N";

|SI #0#1 = 0;
    |PARA para1 = 2; |SI para1 [ 11; |HACIENDO para1 = para1 + 1;
        |CAMPO_MODIFICA #0para1, 1, "N";
    |SIGUE;
    |CAMPO_MODIFICA #0#12, 1, "S";
    |CAMPO_MODIFICA #0#13, 1, "S";
|FINSI;
|FPRC;

|PROCESO lee_emp; |TIPO 0;
|SI #0Campo ! 0;
    |ABRE #1;
        #1#0 = #0Campo;
    |LEE 000#1=;
    |SI FSalida ! 0;
        |MENAV "    Empresa INEXISTENTE ...";
        |ERROR;
    |FINSI;
    |CIERRA #1;
|FINSI;
|FPRC;

|PROCESO pin_emp; |TIPO 7;
|DDEFECTO #1;
|SI #0Campo ! 0;
    |ABRE #1;
        #1#0 = #0Campo;
    |LEE 000#1=;
    |CIERRA #1;
|FINSI;
|ATRI I;   |PINTA 1034, #1#2;  |ATRI 0;
|FPRC;

|PROCESO lee_canempre; |TIPO 0;
     cod_empre = #0#69;
     cod_perio = #0#70;

     |ABRE #25;
     #25#2 = cod_empre;
     #25#3 = cod_perio;
     |LEE 000#25=;
     |SI FSalida = 0;
          dig1 = #25#11;       || desde mes
          dig3 = #25#13;       || nro. niveles
          dig4 = #25#14;       || 1 nivel
          dig5 = #25#15;       || 2 nivel
          dig6 = #25#16;       || 3 nivel
          dig7 = #25#17;       || 4 nivel
          dig8 = #25#18;       || 5 nivel
          dig9 = #25#19;       || 6 nivel

          alfa1 = cod_empre;
          alfa1 = ("00000" + alfa1) % -105;
          path_empres = path_agicon + "fich/" + alfa1 + cod_perio + "/";

          |PATH_DAT #20 path_empres;
          |PATH_DAT #21 path_empres;
          |PATH_DAT #22 path_empres;
          |PATH_DAT #23 path_empres;
          |PATH_DAT #24 path_empres;
          |PATH_DAT #26 path_empres;
          |PATH_DAT #27 path_empres;
          |PATH_DAT #28 path_empres;

          |PINTA 2008, #25#1;
     |SINO;
          alfa1 = "    Empresa [" + cod_empre + "-" + cod_perio + "] inaccesible.";
          |MENAV alfa1;
          |ERROR;
     |FINSI;
     |CIERRA #25;
|FPRC;
____________________________________/ ACTUALIZACION NIVELES SUPERIORES
|PROCESO actuali;
  |SI #27#2 [ juan; |O #27#2 = 9;
      |SI prim = 1;
           |GRABA 020#24a;
      |FINSI;
      prim = 0;
      |VETE acaba;
  |FINSI;
  aa = 100 + lon;
  aar = aa;
  traba = #27#1 % aar;
|ET iniemp;
  |SI prim = 0;
      cuant = traba;
      prim = 1;
      #24#0 = cuant;
      #24#1 = juan;
      |SI #24#0 = errcuent;
          prim = 0;
          |VETE acaba;
      |FINSI;
      |LIBERA #24;
      |LEE 101#24=;
      |SI FSalida ! 0;
          |LIBERA #24;
          |DDEFECTO #24;
          #24#0 = cuant;
          #24#1 = juan;
          |HAZ defect1;
          |PUSHV 0501 2380;
          |PINPA #0#24;
          |PINDA #0#24;
          |ENDATOS #1#24;
          |SI FSalida ! 0;
              |POPV;
              mensa = "Actualizando nivel: " + juan + "        ";
              |PINTA 2410,mensa;
              errant = #24#0;
              prim = 0;
              |VETE acaba;
          |FINSI;
          |GRABA 020#24b;
          |POPV;
          mensa = "Actualizando nivel: " + juan + "        ";
          |PINTA 2410,mensa;
      |FINSI;
  |FINSI;
  |SI cuant ! traba;
      |GRABA 020#24a;
      prim = 0;
      |VETE iniemp;
  |FINSI;
  |HAZ suma;
|ET acaba;
|FPRC;

|PROCESO borrando;
  |BORRA 020#27a;
|FPRC;

|DEFBUCLE camaapua0;
 #27#1;
 ;
 codcon,cuini,diini,fecini,tiini,diain;
 codcon,cufin,difin,fecfin,tifin,diafi;
 be;
 NULL,actuali;
|FIN;

|DEFBUCLE camaapua1;
 #27#1;
 ;
 codcon,cuini,diini,fecini,tiini,diain;
 codcon,cufin,difin,fecfin,tifin,diafi;
 be;
 NULL,borrando;
|FIN;

|PROCESO niveles;
  |PINTA 2410,"Actualizando Niveles Contables Superiores ...";
  |ABRE #24;
  |PARA juan = dig3 - 1; |SI juan ] 1; |HACIENDO juan = juan - 1;
        prim = 0;
        |SI juan = 1; lon = dig4; |FINSI;
        |SI juan = 2; lon = dig5; |FINSI;
        |SI juan = 3; lon = dig6; |FINSI;
        |SI juan = 4; lon = dig7; |FINSI;
        |SI juan = 5; lon = dig8; |FINSI;
        fecini = "01.01.0000";
        fecfin = "31.12.8999";
        |BUCLE camaapua0;
        |SI prim = 1;
            |GRABA 020#24a;
        |FINSI;
  |SIGUE;
  |CIERRA #24;
  fecini = "01.01.0000";
  fecfin = "31.12.8999";
  |BUCLE camaapua1;
  |ABRE #27;
  |LEE 000#27u;
  |SI FSalida ! 0;
       |CIERRA #27;
       |DELINDEX #27;
  |SINO;
       |CIERRA #27;
  |FINSI;
  |BLIN 24;
|FPRC;

|PROCESO suma;
  |SI #27#6 = 0;
      |SI #27#4 = "D";
          #24#9 = #24#9 + #27#5;
          #24#51 = #24#51 + #27#5;
      |SINO;
          #24#10 = #24#10 + #27#5;
          #24#52 = #24#52 + #27#5;
      |FINSI;
      #24#11 = #24#9 - #24#10;
      #24#53 = #24#51 - #24#52;
  |SINO;
      |SI #27#6 = 99;
          |SI #27#4 = "D";
              #24#48 = #24#48 + #27#5;
              #24#51 = #24#51 + #27#5;
          |SINO;
              #24#49 = #24#49 + #27#5;
              #24#52 = #24#52 + #27#5;
          |FINSI;
          #24#50 = #24#48 - #24#49;
          #24#53 = #24#51 - #24#52;
      |SINO;
          |HAZ actualiza;
      |FINSI;
  |FINSI;
|FPRC;

|PROCESO actualiza;
  ini = dig1;
  fecalf = #27#3;
  mes = fecalf % 402;
  mesfec = mes;
  |SI ini = 1;
      mesac = mesfec;
  |SINO;
      mesac = (mesfec - ini) + 1;
      |SI mesac [ 0;
          mesac = (13 - ini) + mesfec;
      |FINSI;
  |FINSI;
  a = (mesac * 3) + 9;
  b = a + 1;
  c = a + 2;
  |SI #27#4 = "D";
      #24a = #24a + #27#5;
      #24#51 = #24#51 + #27#5;
  |SINO;
      #24b = #24b + #27#5;
      #24#52 = #24#52 + #27#5;
  |FINSI;
  #24c = #24a - #24b;
  #24#53 = #24#51 - #24#52;
|FPRC;

|PROCESO defect1;
  |ABRE #28;
  uno = #24#0;
  |QBF uno;
  dos = uno + "000000000000";
  uno = dos % A101;
  #28#0 = uno;
  |LEE 040#28=;
  |SI FSalida = 0;
      |HAZ saca1;
      |VETE findefe;
  |FINSI;
  uno = dos % A102;
  #28#0 = uno;
  |LEE 040#28=;
  |SI FSalida = 0;
      |HAZ saca1;
      |VETE findefe;
  |FINSI;
  uno = dos % A103;
  #28#0 = uno;
  |LEE 040#28=;
  |SI FSalida = 0;
      |HAZ saca1;
      |VETE findefe;
  |FINSI;
  uno = dos % A104;
  #28#0 = uno;
  |LEE 040#28=;
  |SI FSalida = 0;
      |HAZ saca1;
      |VETE findefe;
  |SINO;
      |MENAV mensa2;
  |FINSI;
|ET findefe;
  nivel = #24#1;
|| la 3a clave esta formada por la cta + zzzz y el campo fantasma del nivel;
  mascara = #24#0;
  |QBF mascara;
  mascara = mascara +"zzzzzzzzzzzz";
  mascara = mascara % 112;
  #24#54    = mascara;
  #24#55    = nivel;
  |CIERRA #28;
|FPRC;

|PROCESO saca1;
  #24#4 = #28#1;
  #24#5 = #28#2;
  #24#6 = #28#3;
  #24#7 = #28#4;
  #24#8 = #28#5;
|FPRC;
____________________________________/ GRABACION DE ASIENTOS
|PROCESO actu_diari;
|ABRE #20;
    #20#0 = d_diari;
|LEE 101#20=;
|SI FSalida ! 0;
    |DDEFECTO #20;
        #20#0 = d_diari;
        #20#1 = "<DIARIO SIN DEFINIR>";
    |GRABA 020#20n;
    |LEE 121#20=;
|FINSI;
|SI d_opera = "D";
        #20#2 = #20#2 + d_impor;        || debe
|SINO;
        #20#3 = #20#3 + d_impor;        || haber
|FINSI;
|GRABA 020#20a;
|LIBERA #20;
|CIERRA #20;
|FPRC;

|PROCESO altatra;
    modcon = 1;
|ABRE #27;
    #27#0 = codcon;
    #27#1 = #23#4;
    #27#2 = #23#5;
    #27#3 = #23#1;
    #27#4 = #23#8;
    #27#6 = #23#0;
|LEE 101#27=;
|SI FSalida ! 0;
    |DDEFECTO #27;
        #27#0 = codcon;
        #27#1 = #23#4;
        #27#2 = #23#5;
        #27#3 = #23#1;
        #27#4 = #23#8;
        #27#6 = #23#0;
    |GRABA 101#27b;
|FINSI;
    importe = #23#9;  || aqui si que se usa el importe real
|SI opera = "B";
        importe = -importe;
|FINSI;
    #27#5 = #27#5 + importe;
|GRABA 101#27a;
|LIBERA #27;
|CIERRA #27;
|FPRC;

|PROCESO alta_cuen;
    #24#2 = "";          || descripcion
    #24#3 = "S";         || pase directo
    #24#58 = #0#31;      || fecha ultima operacion
|HAZ defect1;
|PUSHV 0501 2380;
|PINPA #0#24;
|PINDA #0#24;
|ENDATOS #1#24;
|SI FSalida = 0;
    |GRABA 020#24b;
|FINSI;
|POPV;
|FPRC;

|PROCESO actualiz1;
|DDEFECTO #24;
|ABRE #24;
    #24#0 = #23#4;
    #24#1 = #23#5;
|LEE 101#24=;
|SI FSalida ! 0;
    |HAZ alta_cuen;
|FINSI;
|SI #23#0 = 0;
    |SI #23#8 = "D";
            #24#9 = #24#9 + ressum;
            #24#51 = #24#51 + ressum;
    |SINO;
            #24#10 = #24#10 + ressum;
            #24#52 = #24#52 + ressum;
    |FINSI;
        #24#11 = #24#9 - #24#10;
        #24#53 = #24#51 - #24#52;
|SINO;
    |SI #23#0 = 99;
        |SI #23#8 = "D";
                #24#48 = #24#48 + ressum;
                #24#51 = #24#51 + ressum;
        |SINO;
                #24#49 = #24#49 + ressum;
                #24#52 = #24#52 + ressum;
        |FINSI;
            #24#50 = #24#48 - #24#49;
            #24#53 = #24#51 - #24#52;
    |SINO;
        |HAZ actualiz2;
    |FINSI;
|FINSI;
|SI #24#58 < #23#1;
        #24#58 = #23#1;
|FINSI;
|GRABA 020#24a;
|LIBERA #24;
|CIERRA #24;
|FPRC;

|PROCESO actualiz2;
    ini = dig1;      || numero de niveles
    fecalf = #23#1;
    mes = fecalf % 402;
    mesfec = mes;
|SI ini = 1;
        mesac = mesfec;
|SINO;
        mesac = (mesfec - ini) + 1;
    |SI mesac [ 0;
            mesac = (13 - ini) + mesfec;
    |FINSI;
|FINSI;
    a = (mesac * 3) + 9;
    b = a + 1;
    c = a + 2;
|SI #23#8 = "D";
        #24a = #24a + ressum;
        #24#51 = #24#51 + ressum;
|SINO;
        #24b = #24b + ressum;
        #24#52 = #24#52 + ressum;
|FINSI;
    #24c = #24a - #24b;
    #24#53 = #24#51 - #24#52;
|FPRC;

|PROCESO cabecera;
|DDEFECTO #22;
    #22#0 = #23#0;
    #22#1 = #23#1;
    #22#2 = #23#2;
|LEE 101#22=;
    FEstado = FSalida;
    #22#0 = #23#0;         || diario
    #22#1 = #23#1;         || fecha
    #22#2 = #23#2;         || documento
    #22#3 = #22#2;         || asiento
|SI #23#8 = "D";
        #22#4 = #22#4 + #23#9;
    |SI #22#09 = 0; #22#08 = #23#4; #22#09 = #23#5; |FINSI; || contrp.HABER
|FINSI;
|SI #23#8 = "H";
        #22#5 = #22#5 + #23#9;
    |SI #22#11 = 0; #22#10 = #23#4; #22#11 = #23#5; |FINSI; || contrp.DEBE
|FINSI;
|SI FEstado = 0;  |GRABA 020#22a;  |FINSI;
|SI FEstado ! 0;
    |HAZ mensaje;
    |GRABA 020#22n;
|FINSI;
|LIBERA #22;
|FPRC;

|PROCESO grabatra;
    modcon = 0;
|ABRE #27;
|LEE 101#27u;
|SI FSalida ! 0;
    |DDEFECTO #27;
        #27#0 = 1;
|SINO;
        #27#0 = #27#0 + 1;
|FINSI;
|GRABA 101#27n;
    codcon = #27#0;
|LIBERA #27;
|CIERRA #27;
|FPRC;

|PROCESO contador;
|ABRE #26;
|LEE 101#26p;
|SI FSalida ! 0;
    |DDEFECTO #26;
    |GRABA 020#26n;
    |LEE 101#26p;
|FINSI;
    #26#1 = #26#1 + 1;
|GRABA 020#26a;
|LIBERA #26;
|CIERRA #26;
    ult_apunt = 1;
|FPRC;

|PROCESO mensaje;
    cuantos = cuantos + 1;
    alfa1 = "Grabando Asiento: " + #22#2;
|ATRI I;
|PINTA 2321, alfa1;
|ATRI 0;
|FPRC;

|PROCESO la_cuenta;
  p_alfa1 = conte;
  p_error = 0;
  p_lugar = 0;
  p_cuan3 = 0;
  p_cuan4 = 0;
  p_alfa3 = "";
  p_alfa4 = "";
  |PARA p_nume1 = 1; |SI p_nume1 [ 12; |HACIENDO p_nume1 = p_nume1 + 1;
        p_nume2 = (p_nume1 * 100) + 1;
        p_alfa2 = p_alfa1 % p_nume2;
        |SI p_alfa2 ! " ";
            |SI p_alfa2 = ".";
                p_lugar = p_nume1;
            |SINO;
                |SI p_lugar=0; p_alfa3=p_alfa3+p_alfa2; p_cuan3=p_cuan3+1; |FINSI;
                |SI p_lugar!0; p_alfa4=p_alfa4+p_alfa2; p_cuan4=p_cuan4+1; |FINSI;
            |FINSI;
        |FINSI;
  |SIGUE;
  |SI p_lugar ! 0;
      |SI dig3 = 1;  p_cuan5 = dig4;  digcu = 1;  |FINSI;
      |SI dig3 = 2;  p_cuan5 = dig5;  digcu = 2;  |FINSI;
      |SI dig3 = 3;  p_cuan5 = dig6;  digcu = 3;  |FINSI;
      |SI dig3 = 4;  p_cuan5 = dig7;  digcu = 4;  |FINSI;
      |SI dig3 = 5;  p_cuan5 = dig8;  digcu = 5;  |FINSI;
      |SI dig3 = 6;  p_cuan5 = dig9;  digcu = 6;  |FINSI;
      p_nume1 = p_cuan5 - (p_cuan3 + p_cuan4);
      |SI p_nume1 > 0;
          p_ceros = "0" * p_nume1;
          p_alfa5 = p_alfa3 + p_ceros + p_alfa4;
      |SINO;
          |MENAV p_mensa;
          p_error = 1;
      |FINSI;
  |SINO;
      p_alfa5 = p_alfa1;
  |FINSI;
  conte = p_alfa5;
|FPRC;

|PROCESO el_concepto;
|DDEFECTO #21;
|ABRE #21;
    #21#0 = conce;
|LEE 000#21=;
    descr = #21#1;  |QBF descr;
    descr = descr + " " + #0#33;
|CIERRA #21;
|FPRC;

|PROCESO apuntes;
|SI impor ! 0;
    |HAZ la_cuenta;
    |SI conte ! "            ";|Y digcu ! 0;
        |HAZ el_concepto;
        |DDEFECTO #23;
            #23#00 = #0#30;       || diario
            #23#01 = #0#31;       || fecha
            #23#02 = #26#1;       || documento
            #23#03 = ult_apunt;   || apunte
            #23#04 = conte;       || cuenta
            #23#05 = digcu;       || digito cuenta
            #23#06 = conce;       || concepto
            #23#07 = descr;       || descripcion
            #23#08 = signe;       || signo
            #23#09 = impor;       || importe
        |SI #23#08 = "D";
                #23#16 = #23#4;
                #23#17 = #23#5;
        |SINO;
                #23#10 = #23#4;
                #23#11 = #23#5;
        |FINSI;
            nctan = 29;
            #23nctan = ctana;     || cuenta analitica
        |GRABA 020#23n;
        |HAZ cabecera;
            ressum = #23#9;
        |HAZ actualiz1;     || actualiza cuentas
            opera = "A";
        |HAZ altatra;       || actualiza niveles inferiores
            d_diari = #23#0;
            d_opera = #23#8;
            d_impor = #23#9;
        |HAZ actu_diari;    || actualiza diario
            ult_apunt = ult_apunt + 10;
    |FINSI;
|FINSI;
|FPRC;
____________________________________/ CALCULA IMPORTES
|PROCESO costes1;
    prorr = prorr + #2#11;
    vacac = vacac + #2#17;
    deven = deven + #2#5;
    |SI #0#71 = "S";
        deven = deven + #2#11;
    |FINSI;
    rirpf = rirpf + #2#7;
    rsegu = rsegu + #2#6;
    eltc1 = eltc1 + (#2#9 - #2#8);
    elvac = elvac + #2#18;

    sw_alguna = 1;

    #2#21 = "S";
    |GRABA 020#2a;
    |LIBERA #2;
|FPRC;

|DEFBUCLE costes;
 #2#1;
 21;
 #0#41, #0#32, nObra,  0, "     ", 000000, aD, aD, #1#0,     1, "N";
 #0#41, #0#32, nObra, 99, "zzzzz", 999999, aH, aH, #1#0, 99999, "N";
 be;
 NULL, costes1;
|FIN;

|PROCESO calcula;
    prorr = 0;      || prorrata pagas (DEBE/HABER)
    vacac = 0;      || prorrata vacaciones (DEBE/HABER)
    deven = 0;      || total devengado sin desglosar (DEBE)
    rirpf = 0;      || retencion IRPF   (HABER)
    rsegu = 0;      || retencion S.S.   (HABER)
    liqui = 0;      || liquido          (HABER)
    eltc1 = 0;      || importe TC1      (DEBE/HABER)
    elvac = 0;      || ss. vacaciones   (DEBE/HABER)

    nObra = #gomantho@#0;
    |BUCLE costes;

    liqui = deven - (rirpf + rsegu);    || liquido
|FPRC;

|PROCESO obras1;
     |SI aObraAnt ! #gomantho@#0; |O nEmpresaAnt ! #1#0;
          aObraAnt = #gomantho@#0;
          nEmpresaAnt = #1#0;

          sw_alguna = 0;
          |HAZ calcula;
          |SI sw_alguna ! 0;
               |HAZ conta2;
          |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE obras;
     #gomantho@#1002;
     ;
     aDObra, 000001;
     aHObra, 999999;
     ;
     NULL, obras1;
|FIN;
____________________________________/ LECTURA Y CALCULOS PRINCIPALES
|PROCESO conta2;         || contabilizacion
    aobra = #gomantho@#0;

    |ABRE #22;
    |ABRE #23;
    |HAZ contador;
    adig = #0#45; |QBF adig; adig = adig % 101; ctana = aobra + adig;
    signe = "D"; impor = deven; conte = #0#14; conce = #0#22; |HAZ apuntes;
    adig = #0#46; |QBF adig; adig = adig % 101; ctana = aobra + adig;
    signe = "H"; impor = rirpf; conte = #0#15; conce = #0#23; |HAZ apuntes;
    adig = #0#47; |QBF adig; adig = adig % 101; ctana = aobra + adig;
    signe = "H"; impor = rsegu; conte = #0#16; conce = #0#24; |HAZ apuntes;
    adig = #0#48; |QBF adig; adig = adig % 101; ctana = aobra + adig;
    signe = "H"; impor = liqui; conte = #0#17; conce = #0#25; |HAZ apuntes;

    alfa1 = #0#51 + #0#52;
|QBT alfa1;
|SI alfa1 ! "";
    adig = #0#55; |QBF adig; adig = adig % 101; ctana = aobra + adig;
    signe = "D"; impor = vacac; conte = #0#51; conce = #0#53; |HAZ apuntes;
    adig = #0#56; |QBF adig; adig = adig % 101; ctana = aobra + adig;
    signe = "H"; impor = vacac; conte = #0#52; conce = #0#54; |HAZ apuntes;
|FINSI;

    alfa1 = #0#57 + #0#58;
|QBT alfa1;
|SI alfa1 ! "";
    adig = #0#61; |QBF adig; adig = adig % 101; ctana = aobra + adig;
    signe = "D"; impor = prorr; conte = #0#57; conce = #0#59; |HAZ apuntes;
    adig = #0#62; |QBF adig; adig = adig % 101; ctana = aobra + adig;
    signe = "H"; impor = prorr; conte = #0#58; conce = #0#60; |HAZ apuntes;
|FINSI;

|HAZ contador;
    adig = #0#49; |QBF adig; adig = adig % 101; ctana = aobra + adig;
    signe = "D"; impor = eltc1; conte = #0#20; conce = #0#28; |HAZ apuntes;
    adig = #0#50; |QBF adig; adig = adig % 101; ctana = aobra + adig;
    signe = "H"; impor = eltc1; conte = #0#21; conce = #0#29; |HAZ apuntes;

    adig = #0#67; |QBF adig; adig = adig % 101; ctana = aobra + adig;
    signe = "D"; impor = elvac; conte = #0#63; conce = #0#65; |HAZ apuntes;
    adig = #0#68; |QBF adig; adig = adig % 101; ctana = aobra + adig;
    signe = "H"; impor = elvac; conte = #0#64; conce = #0#66; |HAZ apuntes;
|CIERRA #22;
|CIERRA #23;
|FPRC;

|PROCESO conta1;
     |HAZ grabatra;
     |BUCLE obras;
     |SI modcon ! 0; |HAZ niveles;  |FINSI;
|FPRC;

|DEFBUCLE labempre;
 #1#1;
 ;
 #0#12;
 #0#13;
 ;
 NULL, conta1;
|FIN;

|PROCESO tipo3;
     aDObra = ("00000" + #0#43) % -105;
     aHObra = ("00000" + #0#44) % -105;

     |ABRE #13; |LEE 000#13p; |CIERRA #13;
     |HAZ ExisteEmpresa;
     |SI FSalida ! 0;
          |MENAV "0000ERROR en configuracion empresa gestion...";
          |ACABA;
     |FINSI;
     |DBASS aAlfa;
     aPath = aAlfa + #13#1; |QBF aPath;
     aEmp = ("00000" + #13#2) % -105;
     aDef = aPath + "/def/gomantho";
     |CARGA_DEF aDef, gomantho@;
     |SI FSalida = 0;
          aFic = aPath + "/fich/" + aEmp + "/";
          |PATH_DAT #gomantho@#aFic#;

          |SI #0#1 ! 0;
              |ABRE #1;
              |PARA empre = 1; |SI empre [ 11; |HACIENDO empre = empre + 1;
                  #1#0 = #0empre;
                  |LEE 000#1=;
                  |SI FSalida = 0;
                      |HAZ conta1;
                  |FINSI;
              |SIGUE;
              |CIERRA #1;
          |SINO;
              |BUCLE labempre;
          |FINSI;
          |DESCARGA_DEF #gomantho@;
     |FINSI;
|FPRC;
