|FICHEROS;
     nomconli #10;
     nompanta;       ||  Pantallas varias
     labempre #14;   ||  Fichero de empresas
     nomconca #15;   ||  Convenios
     labtraba #16;   ||  Trabajadores
     nomexcli;
     nomexdli;
|FIN;

|VARIABLES;
    swcheck = 0;
    mensaje0 = "0000ATENCION !!! Concepto fuera de rango ...";
    aMensaje1 = "se emite un solo recibo, no es necesario el anticipo 3. ";
    aMensaje2 = "este mes no se emite paga extra. "
    aMensaje3 = "0000Es mas apropiado usar conceptos 906 o 907."
    swMensaje0 = 0;
    swMensaje1 = 0;
    swMensaje2 = 0;
    aAviso = "";
    nPaga = 0;
    victima = "";
    posinum = 0;
    posialf = "";
    positra = 0;
    posipun = 0;
    swident = 0;
    convenio = 0;
    concepto = 0;
    Fconvenio = 0;
    espac = "";
    longi = 0;
    corta = 0;
    corta2 = 0;
    desde = 0;
    hasta = 0;
    alfa1 = "";

     aAlfa = "";
     aAlfa1 = "";
     aAlfa2 = "";
     aMensaje4 = "";
     aMensaje5 = "";
     nConcepto = 0;

     swElegido = 0;
|FIN;

|PROCESO concon; |TIPO 11;
|SI FSalida = 9;
    |HAZ carga_con;
    |HAZ edita_convenio;
    |ERROR;
|FINSI;
|SI FSalida = 10;
    ||HAZ nuevaayuda_900;
    |HAZ ConsultaLineal900;
|FINSI;
|FPRC;

|PROCESO ayuda_900;
|PUSHV 02302378;
|CUADRO 02 30 23 78;
|ATRI R;
||            12345678901234567890123456789012345678901234567
|PINTA 0331, " -----------< LISTA FUNCIONES 900 >----------- ";
|PINTA 0431, " (901) Horas Subvencionadas TC2/1              ";
|PINTA 0531, " (902) Dias A¤o de Indemnizacion               ";
|PINTA 0631, " (903) Jornadas Agrarios-Citricos              ";
|PINTA 0731, " (904) Dias A¤o Vacaciones          [30]       ";
|PINTA 0831, " (905) Horas/Dias Mes Tope Minimo Cotiz. /T.P. ";
|PINTA 0931, " (906) Importe Anticipos 1                     ";
|PINTA 1031, " (907) Importe Anticipos 2                     ";
|PINTA 1131, " (908),(923),(924)  Importe En Especie 1, 2 o 3";
|PINTA 1231, " (909) Horas Convenio Maximo Semana [40] /T.P. ";
|PINTA 1331, " (910) Horas Semana Trabajadas           /T.P. ";
|PINTA 1431, " (911) Horas Convenio Maximo Mes         /T.P. ";
|PINTA 1531, " (912) Horas Mes Trabajadas              /T.P. ";
|PINTA 1631, " (914) Dias Alta                         /T.P. ";
|PINTA 1731, " (915) Dias de Pago por Vacaciones Ptes.       ";
|PINTA 1831, " (916) Forzar Deduccion Seg.Soc. Trabajador.   ";
|PINTA 1931, " (917) Dias TC2                                ";
|PINTA 2031, " (918) Horas TC2                               ";
|PINTA 2131, " (919) Forzar Base I.R.P.F.                    ";
|PINTA 2231, " (920) Promedio para Indemnizaciones.          ";
|PINTA 2331, " (925) Media Conceptos Variables A¤o Anterior  ";
|ATRI 0;
|PAUSA;
|POPV;
|FPRC;

|PROCESO nuevaayuda_900;
     |PUSHV 04012580;
     |ATRI R;
     |PINPA #0#nompanta;
     |MODULO alfa1;
     |QBF alfa1;
     |SI alfa1 = "nomtraac";
          |ATRI S;
          |PINTA 1841, "(989) ATRASOS: Diferencia  base  diaria";
          |PINTA 1941, "      para cotiz. y prestaciones IT    ";
          |PINTA 2041, "(990) ATRASOS: Diferencia  base  diaria";
          |PINTA 2141, "      Expediente de Regulacion Empleo  ";
          |ATRI 0;
     |FINSI;
     |PAUSA;
     |ATRI;
     |POPV;
|FPRC;

|PROCESO chec900;
     |MODULO alfa1;
     |SI alfa1 = "nomcatca"; |ACABA; |FINSI; || si es de convenio/categoria, no
     |ABRE #14;                              || comprueba nada
     #14#0 = #0#0;
     |LEE 020#14=;
     |SI #14#25 = 2; |Y #0Campo = 921;
          swMensaje1 = 1;                    || empresa con un solo recibo
     |SINO;                                  || no tiene por que usar el 921
          aMensaje1 = "";
     |FINSI;
     |CIERRA #14;

     |SI alfa1 = "nomvarca";                 || Solo desde variaciones:
          |ABRE #16;                         || comprueba por si esta usando el 921
          #16#1 = #0#1;                      || en un mes que no tenga paga extra
          |LEE 020#16=;
          |SI FSalida = 0;
               |ABRE #15;
               #15#0 = #16#87;               || busca el convenio del trabajador
               |LEE 020#15=;
               |SI FSalida = 0;
                    |PARA nPaga = 20; |SI nPaga < 28; |HACIENDO nPaga = nPaga + 1;
                    |SI #15nPaga = #0#2; nPaga = 100; |FINSI;
                    |SIGUE;
               |FINSI;
               |CIERRA #15;
          |CIERRA #16;
          |FINSI;

          |SI nPaga < 100;
               swMensaje2 = 1;               || en en este mes NO hay paga extra
          |SINO;                             || no tiene por que usar el 921
               aMensaje2 = "";
          |FINSI;
     |SINO;
          aMensaje2 = "";
     |FINSI;

     |SI alfa1 = "nomtraca"; |Y #0Campo = 921;   || Solo desde variaciones:
          aAviso = "    ATENCION: el Antic. de Pagas solo debe usarse desde ";
          aAviso = aAviso + "variaciones mensuales en meses con paga."
          |MENAV aAviso;
     |FINSI;

                    ||  IMPRESION O NO DE MENSAJE/S
     swMensaje0 = swMensaje1 + swMensaje2;
     |SI swMensaje0 = 0; |ACABA; |FINSI;
     |SI swMensaje0 = 2;
          aAviso = "0000Aviso: " + aMensaje1 + "Ademas, " + aMensaje2;
          |MENAV aAviso;
          |MENAV aMensaje3;
     |FINSI;
     |SI swMensaje0 = 1;
          aAviso = "0000Aviso: " + aMensaje1 + aMensaje2;
          |MENAV aAviso;
          |MENAV aMensaje3;
     |FINSI;
|FPRC;

|PROCESO checcon4; |TIPO 0;     || chequea rangos de concepto
     |SI swElegido = 1;
          swElegido = 0;
          |ACABA;
     |FINSI;
     |HAZ si_modif;
     swcheck = 0;
     |SI #0Campo ] 101; |Y #0Campo [ 199; swcheck = 2; |FINSI;
     |SI #0Campo ] 301; |Y #0Campo [ 310; swcheck = 2; |FINSI;
     |SI #0Campo ] 401; |Y #0Campo [ 420; swcheck = 2; |FINSI;
     |SI #0Campo ] 901; |Y #0Campo [ 999; swcheck = 1; |FINSI;
     |SI swcheck = 0;
          |MENAV mensaje0;
          |ERROR;
     |FINSI;
     |SI swcheck = 2;
          |HAZ carga_con;
          |HAZ compru_convenio;
     |FINSI;
     |SI #0Campo = 921; |HAZ chec900; |FINSI;
     swcheck = 0;
     |SI #0Campo > 900;|Y #0Campo ! 906;|Y #0Campo ! 907;|Y #0Campo ! 908;
                       |Y #0Campo ! 916;|Y #0Campo ! 919;|Y #0Campo ! 921;
                       |Y #0Campo ! 923;|Y #0Campo ! 924;|Y #0Campo ! 925
                       |Y #0Campo ! 926;|Y #0Campo ! 928; |Y #0Campo ! 929;
                       ||Y #0Campo ! 948; |Y #0Campo ! 961; |Y #0Campo ! 965;
                       |Y #0Campo ! 961; |Y #0Campo ! 965;
          |SI #0Campo ] 946; |Y #0Campo [ 960;
               swcheck = 0;
          |SINO;
               swcheck = 1;
          |FINSI;
     |FINSI;
     |SI swcheck = 1;
          |SI #0Campo ] 946; |Y #0Campo [ 960;
               || estos si que son modificables el valor
          |SINO;
               |CIERRA #labempre;
               |HAZ no_modif;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO descrip_900; |TIPO 7;
     |SI #0concep ] 901;|Y #0concep [ 999;
          |SI #0concep = 901;   #0valor = "H.Subv.TC2/1";  |FINSI;
          |SI #0concep = 902;   #0valor = "Indemiza.A¤o";  |FINSI;
          |SI #0concep = 903;   #0valor = "Jornadas    ";  |FINSI;
          |SI #0concep = 904;   #0valor = "Dias Vac.A¤o";  |FINSI;
          |SI #0concep = 905;   #0valor = "H/D Tope Min";  |FINSI;
          |SI #0concep = 909;   #0valor = "H.Max.Semana";  |FINSI;
          |SI #0concep = 910;   #0valor = "H.Sem.Traba.";  |FINSI;
          |SI #0concep = 911;   #0valor = "H.Max.Mes   ";  |FINSI;
          |SI #0concep = 912;   #0valor = "H.Mes.Traba.";  |FINSI;
          |SI #0concep = 914;   #0valor = "Dias Alta TP";  |FINSI;
          |SI #0concep = 915;   #0valor = "Dias Ind.Vac";  |FINSI;
          |SI #0concep = 917;   #0valor = "Dias en TC2 ";  |FINSI;
          |SI #0concep = 918;   #0valor = "Horas en TC2";  |FINSI;
          |SI #0concep = 920;   #0valor = "Prom.Indemn.";  |FINSI;
          |SI #0concep = 922;   #0valor = "Jornadas IT ";  |FINSI;
          |SI #0concep = 927;   #0valor = "Base Reg. 2 ";  |FINSI;
          /*
          |SI #0concep = 931;   #0valor = "Jorn. IT -1-";  |FINSI;
          |SI #0concep = 932;   #0valor = "Jorn. IT -2-";  |FINSI;
          |SI #0concep = 933;   #0valor = "Jorn. IT -3-";  |FINSI;
          |SI #0concep = 934;   #0valor = "Jorn. IT -4-";  |FINSI;
          |SI #0concep = 935;   #0valor = "Jornadas ERE";  |FINSI;
          |SI #0concep = 941;   #0valor = "Jor.Cot.E.T.";  |FINSI;
          |SI #0concep = 942;   #0valor = "Jor.Cot.Emp.";  |FINSI;
          */
          |SI #0concep = 945;   #0valor = "Base Bonifi.";  |FINSI;
          |SI #0concep ] 931; |Y #0concep [ 944;
               #0valor = "<INDEFINIDO>";
          |FINSI;
          |SI #0concep ] 949; |Y #0concep [ 950;
               #0valor = "<INDEFINIDO>";
          |FINSI;
          |SI #0concep = 913; |O #0concep = 930;
               #0valor = "<INDEFINIDO>";
          |FINSI;
          |PINTA #0valor;
     |FINSI;
|FPRC;

|PROCESO checnume; |TIPO 0;             || chequea si es traducible a numero
|SI Campo = valor; |Y #0concep > 900; |Y #0concep ! 906; |Y #0concep ! 907;
                   |Y #0concep ! 908; |Y #0concep ! 916; |Y #0concep ! 919;
                   |Y #0concep ! 921; |Y #0concep ! 923; |Y #0concep ! 924;
                   |Y #0concep ! 925; |Y #0concep ! 926; |Y #0concep ! 928;
                   |Y #0concep ! 929; |Y #0concep ! 949; |Y #0concep ! 961;
                   |Y #0concep ! 946; |Y #0concep ! 947; |Y #0concep ! 965;
                   |Y #0concep ! 948;
    |ACABA;
|FINSI;

    alfa1 = #0Campo % 0;
    longi = alfa1;
    espac = " " * longi;
    corta = (longi + 100) * -1;
|SI longi = 10;
        desde = -9999999.9999;
        hasta =  9999999.9999;
|SINO;
        desde = -9999999.9999;
        hasta =  9999999.9999;
|FINSI;
|SI #0Campo ! espac;
        posipun = 0;
        victima = #0Campo;  posinum = victima;
    |QBT victima;
        victima = espac + victima;
        victima = victima % corta;           || ajusta a la derecha
    |SI posinum ] desde; |Y posinum [ hasta;
        |PARA posinum = 1; |SI posinum [ longi; |Y posinum ! 0; |HACIENDO posinum = posinum + 1;
                positra = 100 * posinum;
                positra = -1 - positra;
                posialf = victima % positra;
            |HAZ identida;
            |SI swident = 4;                || caracter igual al espacio
                posinum = longi;            || sale bien
            |FINSI;
            |SI swident = 3;                || caracter igual al signo (+/-)
                positra = 100 + posinum;
                positra = 0 - positra;
                victima = victima % positra;
                victima = espac + victima;
                victima = victima % corta;
                posinum = longi;            || sale bien
            |FINSI;
            |SI swident = 2;                || caracter igual al punto decimal
                |SI posipun = 0;
                    posipun = posinum;
                |SINO;
                    positra = posinum - posipun;
                    |SI positra = 2;
                        swident = 0;
                    |SINO;
                        posipun = posinum;
                    |FINSI;
                |FINSI;
            |FINSI;
            |SI swident = 0;                || caracter malo, acabar !
                posinum = -1;               || sale mal
            |FINSI;
        |SIGUE;
        |SI posipun ! 0; |Y swident ! 0;        || si hay decimales y es bueno
            posipun = posipun - 1;
            |SI posipun ! 0;
                positra = 100 + posipun;
                positra = 0 - positra;
                posialf = victima % positra;       || separa decimales
                posipun = posipun + 1;
                positra = (posipun + 1) * 100;
                positra = positra + (longi - posipun);
                positra = 0 - positra;
                victima = victima % positra;       || coge la parte entera
                |QBF posialf;
                posialf = posialf + "0000";
                posialf = posialf % 104;           || redondea decimales
                victima = victima + "." + posialf; || construye numero
            |SINO;
                corta2 = (corta * -1) - 1;
                victima = victima % corta2;
            |FINSI;
         |FINSI;
    |SINO;
        posinum = 0;
    |FINSI;
    |SI posinum = 0;
        |ERROR;
    |SINO;
        |QBT victima;
            victima = espac + victima;
            #0Campo = victima % corta;
        |PINTA #0Campo;
    |FINSI;
|FINSI;
|FPRC;

|PROCESO identida;
    swident = 0;
|SI posialf = "-"; |O posialf = "+";    swident = 3; |FINSI; || signos
|SI posialf = ".";                      swident = 2; |FINSI; || punto
|SI posialf = "0"; |O posialf = "1";
                   |O posialf = "2";
                   |O posialf = "3";
                   |O posialf = "4";
                   |O posialf = "5";
                   |O posialf = "6";
                   |O posialf = "7";
                   |O posialf = "8";
                   |O posialf = "9";    swident = 1; |FINSI; || numeros
|SI posialf = " ";                      swident = 4; |FINSI; || espacios
|FPRC;

|PROCESO min10;
|SI FSalida = "POSICION";
        #10#0 = convenio;
        #10#2 = concepto;
|SINO;
        #10#0 = convenio;
        #10#2 = 101;
|FINSI;
|FPRC;

|PROCESO max10;
    #10#0 = convenio;
    #10#2 = 999;
|FPRC;

|PROCESO edita_convenio;
|PUSHV 0501 2380;
|ENTLINEAL #1#10, 2, 3, 21, 1, min10, max10;
|POPV;
|FPRC;

|PROCESO lee_convenio;
     |ABRE #10;
     #10#0 = convenio;
     #10#2 = concepto;
     |LEE 000#10=;
     Fconvenio = FSalida;
     |CIERRA #10;
     |ABRE #15;
     #15#0 = convenio;
     |LEE 000#15=;
     |SI Fconvenio ! 0;
          |SI #15#148 = concepto;
               Fconvenio = 0;
          |FINSI;
     |FINSI;
     |CIERRA #15;
|FPRC;

|PROCESO compru_convenio;
|HAZ lee_convenio;
|PARA; |SI Fconvenio ! 0; |HACIENDO;
    |AVISO;
    alfa1 = "    NConcepto [" + #10#0 + "-" + #10#2 + "] inaccesible o inexistente. Confirma alta S/N: ";
    |CONFI alfa1;
    |SI FSalida = 0;
        |HAZ edita_convenio;
        |HAZ lee_convenio;
    |SINO;
        |SAL;
    |FINSI;
|SIGUE;
|FPRC;

|PROCESO CompExcep;
     |ABRE #nomexcli;    || excepciones por empresa/convenio
     |ABRE #nomexdli;    || excepciones por convenio/contrato
     |ABRE #labtraba;

     aMensaje4 = "    El concepto esta anulado para esta Empresa/Convenio ";
     aMensaje4 = aMensaje4 + "segun las excepciones definidas";

     aMensaje5 = "    El concepto esta anulado para este Convenio/Contrato ";
     aMensaje5 = aMensaje5 + "segun las excepciones definidas";

     #labtraba#0 = #0#0;      || lee trabajador
     #labtraba#1 = #0#1;
     |LEE 000#labtraba.=;

     |MODULO aAlfa1;
     |QBF aAlfa1;
     |SI aAlfa1 = "nomvarca";
          nConcepto = #0#4;
     |FINSI;
     |SI aAlfa1 = "nomtraca"; |O aAlfa1 = "nomw0005";
          nConcepto = #0#2;
     |FINSI;

     #nomexcli#0 = #0#0;
     #nomexcli#1 = #labtraba#87;
     #nomexcli#2 = nConcepto;
     |LEE 000#nomexcli.=;
     |SI FSalida = 0; |Y #nomexcli#4 = "S";
          |MENAV aMensaje4;
          |ERROR;
     |FINSI;

     #nomexdli#0 = #labtraba#87;
     #nomexdli#1 = #labtraba#45;
     #nomexdli#2 = nConcepto;
     |LEE 000#nomexdli.=;
     |SI FSalida = 0; |Y #nomexdli#4 = "S";
          |MENAV aMensaje5;
          |ERROR;
     |FINSI;

     |CIERRA #labtraba;
     |CIERRA #nomexdli;
     |CIERRA #nomexcli;
|FPRC;

|PROCESO MensaUni; |TIPO 7;
     |SI #0#4 = 948;
          |MENSA "    Día que comienza el 2do. ERE/ERTE, se aplicará el promedio del concepto";
     |SINO;
          |MENSA "    Entre: valor numérico / (formato -> +######.##)";
     |FINSI;
|FPRC;

|PROCESO MensaVal; |TIPO 7;
     |SI #0#4 = 948;
          |MENSA "    Promedio para Desempleo que se aplicará en el segundo ERE/ERTE";
     |SINO;
          |MENSA "    Entre: valor numérico / (formato -> +######.##)";
     |FINSI;
|FPRC;
