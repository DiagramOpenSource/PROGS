|FICHEROS;
     cretap28 #0;
     cretam3d;
     cretam02;
     cretae02;

     labempre;
     labcentr;
     nomcenes;
     labtraba;
|FIN;

|VARIABLES;
     &eaFichero = "";
     &aSoloFichero = "";

     nNume = 0;
     aLon = "";
     nLon = 0;
     nEspacio = 0;
     aEspacio = "";
     nBlancos = 0;
     nBlancosIzd = 0;
     nBlancosDer = 0;
     aBlancosIzd = "";
     aBlancosDer = "";

     aCadena = "";
     aCadenaSal = "";

     FLinea = 0;
     nNroLinea = 0;
     aNroLinea = "";
     aAlfa = "";

     nHandle = 0;
     aLinea = "";
     &aTotalLineas = "";
     swAcaba = 0;

     aEmpresa = "";
     aCCC = "";
     nAnyDesde = 0;
     nMesDesde = 0;
     aAnyDesde = "";
     aMesDesde = "";
     nTipo = 0;
     aDia = "";
     aMes = "";
     aAny = "";
     aEtiqueta = "";
     nLoc = 0;
     aValor = "";
     aCampoFecha = "";
     aDiaRec = "";
     aMesRec = "";
     aAnyRec = "";
     aHoraRec = "";
     aEtiquetaIni = "";
     aEtiquetaFin = "";
     aMesHasta = "";
     aAnyHasta = "";
     aTipo = "";

     nEmp = 0;
     nTra = 0;
     swEncontrada = 0;
     aAlfa1 = "";
     aAlfa2 = "";
     &enMes = 0;
     &enAny = 0;
     &enSwAlta = 0;

     aNaf = "";
     aEstado = "";
     fFechaRec = @;

     aSiglas = "";

     aDiaDesdeTramo = "";
     aMesDesdeTramo = "";
     aAnyDesdeTramo = "";
     aFechaDesdeTramo = "";
     aDiaHastaTramo = "";
     aMesHastaTramo = "";
     aAnyHastaTramo = "";
     aFechaHastaTramo = "";

     aDias = "";
     aHoras = "";
     aDescripcion = "";
     nConcepto = 0;
     nImporte = 0;
     aCentro = "";
     aNombreCentro = "";

     nCampo = 0;
     nCampo1 = 0;
     nCampo2 = 0;
     nCampo3 = 0;
     aTipoSuma = "";
     nBase = 0;
     aTipoDCL = "";
     aCodErrCCC1 = ""; aCodErrCCC2 = ""; aCodErrCCC3 = ""; aCodErrCCC4 = ""; aCodErrCCC5 = "";
     aDesErrCCC1 = ""; aDesErrCCC2 = ""; aDesErrCCC3 = ""; aDesErrCCC4 = ""; aDesErrCCC5 = "";
     aTipoRespuesta = "";
     eaReferencia = "";
     nErrores = 0;
     swCCCCorrecto = 0;
     nTipoFichero = 0;
     FEstado = 0;

     nMesHasta = 0;
     nAnyHasta = 0;
     nMesControl = 0;
     nAnyControl = 0;
     nMesClave = 0;
     nAnyClave = 0;
     aMesClave = "";
     aAnyClave = "";
     nMesDatos = 0;
     swNoActiva = 0;
     swAtrasos = 0;

     aIBAN = "";
     aTipoIPF = "";
     aNumeroIpf = "";
     aTitular = "";
     aBuscaCCC = "";
     aAutorizado = "";
|FIN;

|PROCESO Centra;
     aLon = aCadena % 0;
     nLon = aLon;
     nBlancos = nEspacio - nLon;
     nNume = nBlancos / 2;
     nBlancosIzd = nNume ! 0;
     nBlancosDer = nBlancos - nBlancosIzd;
     aBlancosIzd = " " * nBlancosIzd;
     aBlancosDer = " " * nBlancosDer;
     aCadenaSal = aBlancosIzd + aCadena + aBlancosDer;
|FPRC;

/****************************************************************************/
/****************************** REGISTROS ***********************************/
/****************************************************************************/

|PROCESO RegistroCCC;
     |DDEFECTO #cretam3d;

     |ABRE #cretam3d;
     #cretam3d#0 = nEmp;
     #cretam3d#4 = aCCC;
     #cretam3d#13 = eaReferencia;
     |LEE 101#cretam3d.=;
     FEstado = FSalida;

     #cretam3d#6 = aEmpresa;
     |SI nEmp = 0;
          #cretam3d#6 = "********** CCC NO ENCONTRADO ***********";
     |FINSI;
     #cretam3d#14 = aSoloFichero;

     aAlfa = #cretam3d#7;
     |QBF aAlfa;
     aAlfa1 = aAlfa - aCentro;
     |SI aAlfa1 = aAlfa;
          || quiere decir que el centro no estaba en la lista
          #cretam3d#7 = aAlfa + " " + aCentro;
          |SI aAlfa = "";
               #cretam3d#8 = aNombreCentro;
          |SINO;
               #cretam3d#8 = "Varios Centros";
          |FINSI;
     |FINSI;

     |SI nErrores = 0; |Y swCCCCorrecto = 1;
          #cretam3d#9 = "A";
     |SINO;
          #cretam3d#9 = "R";
     |FINSI;

     #cretam3d#15 = aCodErrCCC1;
     #cretam3d#16 = aDesErrCCC1;
     #cretam3d#17 = aCodErrCCC2;
     #cretam3d#18 = aDesErrCCC2;
     #cretam3d#19 = aCodErrCCC3;
     #cretam3d#20 = aDesErrCCC3;
     #cretam3d#21 = aCodErrCCC4;
     #cretam3d#22 = aDesErrCCC4;
     #cretam3d#23 = aCodErrCCC5;
     #cretam3d#24 = aDesErrCCC5;

     #cretam3d#25 = aIBAN;
     #cretam3d#26 = aTipoIPF;
     #cretam3d#27 = aNumeroIpf;
     #cretam3d#28 = aTitular;

     |SI FEstado = 0;
          |GRABA 020#cretam3d.a;
     |SINO;
          |GRABA 020#cretam3d.n;
     |FINSI;

     |LIBERA #cretam3d;
     |CIERRA #cretam3d;
|FPRC;

/****************************************************************************/
/***************************   BORRADO PREVIO *******************************/
/****************************************************************************/

|PROCESO Borra_cretam3d;
     |BORRA 020#cretam3d.a;
     |LIBERA #cretam3d;
|FPRC;

|DEFBUCLE Borra_cretam3d;
     #cretam3d#1;
     ;
     00000, "               ", eaReferencia;
     99999, "zzzzzzzzzzzzzzz", eaReferencia;
     ;
     NULL, Borra_cretam3d;
|FIN;

/****************************************************************************/
/*********** PROCESOS PARA BUSCAR CODIGOS DE EMP, TRA Y CEN *****************/
/****************************************************************************/

|PROCESO BuscaCCCLabempre;
     nEmp = #labempre#0;
     swEncontrada = 1;
|FPRC;

|DEFBUCLE BuscaCCCLabempre;
     #labempre#1;
     13;
     00001, aBuscaCCC;
     99999, aBuscaCCC;
     ;
     NULL, BuscaCCCLabempre;
|FIN;

|PROCESO BuscaCCCLabcentr;
     nEmp = #labcentr#0;
     swEncontrada = 1;

     nNume = #labcentr#1;
     aAlfa = nNume;
     |QBF aAlfa;
     aAlfa = ("000" + aAlfa) % -103;
     aCentro = aAlfa;
     aNombreCentro = #labcentr#2;
|FPRC;

|DEFBUCLE BuscaCCCLabcentr;
     #labcentr#1
     10;
     00001, 001, aBuscaCCC;
     99999, 999, aBuscaCCC;
     ;
     NULL, BuscaCCCLabcentr;
|FIN;

|PROCESO BuscaCCCNomcenes;
     nEmp = #nomcenes#0;
     swEncontrada = 1;

     nNume = #nomcenes#13;
     aAlfa = nNume;
     |QBF aAlfa;
     aAlfa = ("000" + aAlfa) % -103;
     aCentro = aAlfa + "F";
     aNombreCentro = #nomcenes#2;
|FPRC;

|DEFBUCLE BuscaCCCNomcenes;
     #nomcenes#1
     10;
     00001, 001, "87", aBuscaCCC;
     99999, 999, "87", aBuscaCCC;
     ;
     NULL, BuscaCCCNomcenes;
|FIN;

|PROCESO BuscaPorCCC;
     aEmpresa = "";
     swEncontrada = 0;
     swNoActiva = 0;
     aBuscaCCC = aCCC % -111;
     aBuscaCCC = aBuscaCCC + "    ";
     aCentro = "";
     aNombreCentro = "";
     |BUCLE BuscaCCCLabcentr;
     |SI swEncontrada = 0;
          |BUCLE BuscaCCCNomcenes;
     |FINSI;
     |SI swEncontrada = 0;
          |BUCLE BuscaCCCLabempre;
     |FINSI;
     |SI swEncontrada = 1;
          #labempre#0 = nEmp;
          |LEE 000#labempre.=;
          |SI FSalida = 0; |Y #labempre#67 = "S";
               aEmpresa = #labempre#2;
          |SINO;
               swNoActiva = 1;
          |FINSI;
     |FINSI;
|FPRC;

/****************************************************************************/
/********************   TIPOS DE DATOS COMUNES  *****************************/
/****************************************************************************/
|PROCESO CCC;
     |PARA; |SI; |HACIENDO;
          |HAZ LeeLinea;

          aEtiqueta = "Regimen";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               aCCC = aValor;
          |FINSI;

          aEtiqueta = "Provincia";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               aCCC = aCCC + aValor;
          |FINSI;

          aEtiqueta = "Numero";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               aCCC = aCCC + aValor;
          |FINSI;

          aEtiqueta = "Ccc";
          |HAZ BuscaEtiFin;
          |SI nLoc = 1;
               |SAL;
          |FINSI;
     |SIGUE;
|FPRC;

/****************************************************************************/
/***********     PROCESOS PARA BUSCAR LAS ETIQUETAS       *******************/
/****************************************************************************/

|PROCESO BuscaEtiFin;
     nLoc = 0;
     aEtiqueta = "</" + aEtiqueta + ">";
     aAlfa = aLinea - aEtiqueta;
     |SI aAlfa ! aLinea;
          nLoc = 1;
     |FINSI;
|FPRC;

|PROCESO BuscaEtiIni;
     nLoc = 0;
     aEtiqueta = "<" + aEtiqueta;
     aAlfa = aLinea - aEtiqueta;
     |SI aAlfa ! aLinea;
          nLoc = 1;
     |FINSI;
|FPRC;

|PROCESO BuscaEtiIniFin;
     aValor = "";
     nLoc = 0;

     aEtiquetaIni = "<" + aEtiqueta + ">";
     aEtiquetaFin = "</" + aEtiqueta + ">";
     aAlfa = aLinea - aEtiquetaIni - aEtiquetaFin;
     |SI aAlfa ! aLinea;
          aValor = aAlfa;
          |SI aEtiqueta = "Descripcion";
               |QBF aValor;
          |SINO;
               |QBT aValor;
          |FINSI;
          nLoc = 1;
     |FINSI;
|FPRC;

|PROCESO LeeLinea;
     |XLEE nHandle, 250, aLinea;
     FLinea = FSalida;
     nNroLinea = nNroLinea + 1;
     aNroLinea = nNroLinea;
     aAlfa = " Linea " + aNroLinea + " de " + aTotalLineas;

     |ATRI R;
     aCadena = aAlfa; nEspacio = 35; |HAZ Centra;
     |PINTA 2223, aCadenaSal;
     |ATRI 0;
|FPRC;

|PROCESO TipoErrorCCC;
     |PARA; |SI; |HACIENDO;
          |HAZ LeeLinea;

          aEtiqueta = "CodigoErr";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;

               |SI aCodErrCCC1 = "";
                    aCodErrCCC1 = aValor;
               |SINO;
                    |SI aCodErrCCC2 = "";
                         aCodErrCCC2 = aValor;
                    |SINO;
                         |SI aCodErrCCC3 = "";
                              aCodErrCCC3 = aValor;
                         |SINO;
                              |SI aCodErrCCC4 = "";
                                   aCodErrCCC4 = aValor;
                              |SINO;
                                   |SI aCodErrCCC5 = "";
                                        aCodErrCCC5 = aValor;
                                   |FINSI;
                              |FINSI;
                         |FINSI;
                    |FINSI;
               |FINSI;
          |FINSI;

          aEtiqueta = "Descripcion";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               |SI aDesErrCCC1 = "";
                    aDesErrCCC1 = aValor;
               |SINO;
                    |SI aDesErrCCC2 = "";
                         aDesErrCCC2 = aValor;
                    |SINO;
                         |SI aDesErrCCC3 = "";
                              aDesErrCCC3 = aValor;
                         |SINO;
                              |SI aDesErrCCC4 = "";
                                   aDesErrCCC4 = aValor;
                              |SINO;
                                   |SI aDesErrCCC5 = "";
                                        aDesErrCCC5 = aValor;
                                   |FINSI;
                              |FINSI;
                         |FINSI;
                    |FINSI;
               |FINSI;
          |FINSI;

          aEtiqueta = "Errores";
          |HAZ BuscaEtiFin;
          |SI nLoc = 1;
               |SAL;
          |FINSI;
     |SIGUE;
|FPRC;

|PROCESO Accion;
     aCodErrCCC1 = ""; aCodErrCCC2 = ""; aCodErrCCC3 = ""; aCodErrCCC4 = ""; aCodErrCCC5 = "";
     aDesErrCCC1 = ""; aDesErrCCC2 = ""; aDesErrCCC3 = ""; aDesErrCCC4 = ""; aDesErrCCC5 = "";
     aIBAN = "";
     aTipoIPF = "";
     aNumeroIpf = "";
     aTitular = "";

     |PARA; |SI; |HACIENDO;
          |HAZ LeeLinea;

          aEtiqueta = "Ccc";
          |HAZ BuscaEtiIni;
          |SI nLoc = 1;
               |HAZ CCC;
          |FINSI;

          aEtiqueta = "IBAN";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               aIBAN = aValor;
          |FINSI;

          aEtiqueta = "TipoIpf";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               aTipoIPF = aValor;
          |FINSI;

          aEtiqueta = "NumeroIpf";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               aNumeroIpf = aValor;
          |FINSI;

          aEtiqueta = "NombreTitular";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               aTitular = aValor;
          |FINSI;

          aEtiqueta = "Errores";
          |HAZ BuscaEtiIni;
          |SI nLoc = 1;
               |HAZ TipoErrorCCC;

               |SI aCodErrCCC1 = "A9785";    || solicitud aceptada
               |O aCodErrCCC2 = "A9785"; |O aCodErrCCC3 = "A9785";
               |O aCodErrCCC4 = "A9785"; |O aCodErrCCC5 = "A9785";
                    swCCCCorrecto = 1;
               |FINSI;

               |SI aCodErrCCC1 = "R9546"; |O aCodErrCCC2 = "R9546";
               |O aCodErrCCC3 = "R9546"; |O aCodErrCCC4 = "R9546";
               |O aCodErrCCC5 = "R9546";     || error CCC no asignado a autorizacion
                    swCCCCorrecto = 0;
               |FINSI;
          |FINSI;

          aEtiqueta = "AccionDatosBancarios";
          |HAZ BuscaEtiFin;
          |SI nLoc = 1;
               |SAL;
          |FINSI;
     |SIGUE;
|FPRC;

|PROCESO Lectura;
     FLinea = 0;

     aAlfa = eaFichero;
     |XABRE "", aAlfa, nHandle; nHandle = FSalida;
     |SI FSalida ] 0;
          |PARA;
          |SI FLinea ] 0; |Y swAcaba = 0;
          |HACIENDO;
               FLinea = 0;
               |HAZ LeeLinea;

               aEtiqueta = "Autorizado";
               |HAZ BuscaEtiIniFin;
               |SI nLoc = 1;
                    aAutorizado = aValor;
               |FINSI;

               aEtiqueta = "ReferenciaExterna";
               |HAZ BuscaEtiIniFin;
               |SI nLoc = 1;
                    eaReferencia = aValor;
                    |BUCLE Borra_cretam3d;
               |FINSI;

               aEtiqueta = "AccionDatosBancarios";
               |HAZ BuscaEtiIni;
               |SI nLoc = 1;
                    || Comenzamos una nueva Accion
                    nErrores = 0;
                    swCCCCorrecto = 0;
                    nEmp = 0;

                    |HAZ Accion;

                    || se acaba la liquidacion, guardamos registro

                    swNoActiva = 0;
                    |SI nEmp = 0;
                         |HAZ BuscaPorCCC;
                    |FINSI;

                    |HAZ RegistroCCC;
               |FINSI;
          |SIGUE;
     |FINSI;

     |XCIERRA nHandle;
|FPRC;

|PROGRAMA;
     |ABRE #cretam3d;

     |ABRE #labempre;
     |ABRE #labcentr;
     |ABRE #nomcenes;
     |ABRE #labtraba;

     |HAZ Lectura;

     |CIERRA #cretam3d;

     |CIERRA #labempre;
     |CIERRA #labcentr;
     |CIERRA #nomcenes;
     |CIERRA #labtraba;
|FPRO;
