|FICHEROS;
    noypresc #0;         || prestamos cabs.
    noypresl #1;         || prestamos lins.
|FIN;

|VARIABLES;
    alfa1 = "";
    alfa2 = "";
    alfa3 = "";
    alfa4 = "";
    nume1 = 0;
    nume2 = 0;
    nume3 = 0;
    nume4 = 0;
    para1 = 0;
    para2 = 0;
    para3 = 0;
    para4 = 0;
    fech1 = @;
    fech2 = @;
    fech3 = @;
    fech4 = @;
    elsw1 = 0;
    elsw2 = 0;
    elsw3 = 0;
    elsw4 = 0;
    FEstado = 0;
    seleccio = 0;
    modo = 0;

    fiac = "  ";
    relleno = "                                       ";
    aponer = "  ";
    elano = 0;
    elmes = 0;
    sw_lin = 0;
    total = 0;
    impor = 0;
|FIN;
____________________________________/ PROCESOS DESDE CABECERA
|PROCESO nuevo; |TIPO 0;
    sw_lin = 0;
    modo = (FEntrada / 100) ! 0;
|SI modo = 1;
        alfa1 = "S";
|SINO;
        alfa1 = "N";
|FINSI;
|CAMPO_MODIFICA #0#5, 1, alfa1;
|CAMPO_MODIFICA #0#6, 1, alfa1;
|CAMPO_MODIFICA #0#7, 1, alfa1;
|FPRC;

|PROCESO def_mes0; |TIPO 7;
    modo = (FEntrada / 100) ! 0;
|SI modo = 1;
        fech1 = ~;
        alfa1 = fech1;
        alfa2 = alfa1 % -104; nume2 = alfa2;      || año
        alfa3 = alfa1 % 402;  nume3 = alfa3;      || mes
        #0#7 = (nume2 * 100) + nume3;
    |PINTA #0#7;
|FINSI;
|FPRC;

|PROCESO imp_mes; |TIPO 0;
    modo = (FEntrada / 100) ! 0;
|SI modo = 1;
        #0#8 = #0#5 / #0#6;        || importe mes
        #0#9 = #0#5;               || pte. liquidar
    |PINTA #0#8;
    |PINTA #0#9;
|FINSI;
|FPRC;
____________________________________/ PROCESOS DESDE LINEAS
|PROCESO alta; |TIPO 7;
    modo = (FEntrada / 100) ! 0;
|SI modo = 1;|Y sw_lin = 0;

        total = 0;
        elano = (#0#7 - (#0#7 $ 100)) / 100;      || saca el año
        elmes = #0#7 $ 100;                       || saca el mes

    |PARA para1 = 1; |SI para1 [ #0#6; |HACIENDO para1 = para1 + 1;
            impor = #0#8;
            total = total + impor;
        |SI para1 = #0#6;     impor = impor + (#0#5 - total);    |FINSI;

        |DDEFECTO #1;
            #1#0 = #0#0;                     || empresa
            #1#1 = #0#1;                     || trabajador
            #1#2 = #0#2;                     || prestamo
            #1#3 = para1;                    || linea
            #1#4 = (elano * 100) + elmes;    || año/mes
            #1#5 = impor;                    || importe mes
            #1#6 = "N";                      || liquidado NO
        |GRABA 020#1n;
        |SI FSalida = 0; sw_lin = 1;    |FINSI;

            elmes = elmes + 1;
        |SI elmes = 13;       elmes = 1;     elano = elano + 1;  |FINSI;
    |SIGUE;

    |SI sw_lin ! 0;
            aponer = "nosi";
        |HAZ poncontr;
        |PTEC 814;
    |FINSI;
|FINSI;
|FPRC;

|PROCESO poncontr;
    fiac = Control % A109;
    fiac = fiac + relleno;
    fiac = Control % A1001;
    Control = Control + fiac;
    Control = Control + relleno;
    Control = Control % A120;
    Control = Control + aponer;
|FPRC;

|PROCESO def_mes1; |TIPO 7;
    modo = (FEntrada / 100) ! 0;
|SI modo = 1;
        fech1 = ~;
        alfa1 = fech1;
        alfa2 = alfa1 % -104; nume2 = alfa2;      || año
        alfa3 = alfa1 % 402;  nume3 = alfa3;      || mes
        #1#4 = (nume2 * 100) + nume3;
    |PINTA #1#4;
|FINSI;
|FPRC;

|PROCESO tipo2; |TIPO 2;
|SI #1#6 = "N";
        #0#9 = #0#9 +. #1#5;
    |PINTA #0#9;
|FINSI;
|FPRC;

|PROCESO mayus; |TIPO 0;
    #1Campo = #1Campo > #1Campo;
|PINTA #1Campo;
|FPRC;
