|FICHEROS;
    nomypmen #0;    || emision TC2
    labempre #1;    || empresas
    labtraba #5;    || trabajadores
    nomcalca #6;    || calculos cabs.
    nomtpmen #7;    || temporal emision PMEN-2
    nomnotay #12;   || centros notarias/ayuntamientos
|FIN;

|VARIABLES;
    menos = 0;
    alfa1 = "";
    alfa2 = "";
    alfa3 = "";
    alfa4 = "";
    nume1 = 0;
    nume2 = 0;
    nume3 = 0;
    nume4 = 0;
    haentrat = 0;
    antcentr = 0;
    FEstado = 0;
    fecsys = @;
    p_mes = "";

    &t_06 = 0;
    &t_07 = 0;
    &ant0 = "";
    &ant2 = "";
    &ant3 = "";
    &ant4 = "";
    &ant5 = "";
    &ant6 = "";
    &ant7 = "";
    &por_rec = 0;
    &impo_cal = 0;
|FIN;

|INCLUYE i_reco1;
||INCLUYE i_seguim;
____________________________________/ CALCULOS DE PANTALLA
|PROCESO defecto; |TIPO 7;
    fecsys = ~;
    alfa1 = fecsys;
    alfa1 = alfa1 % -104;
    #0Campo = alfa1;
    #0Campo = #0Campo - menos;
|PINTA #0Campo;
|FPRC;

|PROCESO pintames; |TIPO 0;
|SI #0Campo =  1;  p_mes = "ENERO     ";  |FINSI;
|SI #0Campo =  2;  p_mes = "FEBRERO   ";  |FINSI;
|SI #0Campo =  3;  p_mes = "MARZO     ";  |FINSI;
|SI #0Campo =  4;  p_mes = "ABRIL     ";  |FINSI;
|SI #0Campo =  5;  p_mes = "MAYO      ";  |FINSI;
|SI #0Campo =  6;  p_mes = "JUNIO     ";  |FINSI;
|SI #0Campo =  7;  p_mes = "JULIO     ";  |FINSI;
|SI #0Campo =  8;  p_mes = "AGOSTO    ";  |FINSI;
|SI #0Campo =  9;  p_mes = "SEPTIEMBRE";  |FINSI;
|SI #0Campo = 10;  p_mes = "OCTUBRE   ";  |FINSI;
|SI #0Campo = 11;  p_mes = "NOVIEMBRE ";  |FINSI;
|SI #0Campo = 12;  p_mes = "DICIEMBRE ";  |FINSI;
|PINTA 1434, p_mes;
|FPRC;

|PROCESO mirames; |TIPO 7; ||calcula el mes por defecto segun fecha sistema
    fecsys = ~;
    alfa1 = fecsys;
    alfa1 = alfa1 % 402;
    #0Campo = alfa1;
    #0Campo = #0Campo - 1;
    menos = 0;
|SI #0Campo = 0;
        #0Campo = 12;
        menos = 1;
|FINSI;
|PINTA #0Campo;
|FPRC;
____________________________________/ VARIOS
|PROCESO limpieza;
    t_06 = 0;
    t_07 = 0;
|FPRC;

|PROCESO inicio;    || ABRE INFORME PARA NUEVA EMPRESA Y CARGA VARIABLES
|INFOR "nomypmen";
    ant0 = #7#0;
    ant2 = #7#2;
    ant3 = #7#3;
    ant4 = #7#4;
    ant5 = #7#5;
    ant6 = #7#6;
    ant7 = #7#7;
|FPRC;

|PROCESO lee_notar;
|DDEFECTO #12;
|ABRE #12;
    #12#0 = #7#0;        || empresa
    #12#1 = 1;           || centro (siempre el 1)
|LEE 001#12=;
|CIERRA #12;
|FPRC;

|PROCESO el_agicli;
/*
    se_empre = ant0;     || codigo empresa
    se_tipos = "PMEN";   || tipo de modelo
    se_activ = 0;        || tipo 0
    se_impor = impo_cal; || total a pagar
    alfa1 = #0#2;    alfa1 = ("00" + alfa1) % -102;    || mes
    alfa2 = #0#3;                                      || a¤o
    se_fecha = "01." + alfa1 + "." + alfa2;  || fecha emision
    se_prime = 0;        || tipo (con el cero inicializa importe)
|HAZ AlAgicli;
*/
|FPRC;
____________________________________/ RUTINAS DE IMPRESION
|PROCESO pie_pmen;
|SI haentrat = 1;   || si ha impreso cierra el informe, totalizando
    |PIEINF;
    |FININF;                   || cierra el informe
|SINO;
    |FININF;                   || cierra el informe
|FINSI;
|HAZ el_agicli;
|FPRC;

|PROCESO lee_tempo1;
|SI ant0 = -1;  |HAZ inicio;           |FINSI;

|SI ant0 ! #7#0;   || RUPTURA POR EMPRESA
    |PIEINF;
    |FININF;
    |HAZ el_agicli;
    |HAZ limpieza;
    |HAZ inicio;
|FINSI;

    t_06 = t_06 + #7#13;      || total contingencias comunes
    t_07 = t_07 + #7#14;      || total mutualidad

|SI #7#13 ! 0;
        haentrat = 1;
    |PRINT;                        || LINEA DE DETALLE
|FINSI;
|FPRC;

|PROCESO entrat;
|HAZ limpieza;
    ant0 = -1;
    haentrat = 0;
|FPRC;

|DEFBUCLE lee_tempo;
 #7#1;
 ;
     0,     0,    1,  1;
 99999, 99999, 9999, 99;
 e;
 entrat, lee_tempo1, NULL, NULL, pie_pmen;
|FIN;
____________________________________/ GRABACION DEL TEMPORAL (incluye)
|PROCESO gra_tempo1;
|DDEFECTO #7;
    #1#0 = #6#0;
|LEE 020#1=;                                 || empresa
|SI FSalida = 0;
        #5#0 = #1#0;
        #5#1 = #6#1;
    |LEE 020#5=;                         || trabajador
    |SI FSalida = 0; |Y #5#42 ! "H";
                     |Y #5#42 ! "A";|Y #5#42 ! "B";|Y #5#42 ! "E";
            #7#0  = #1#0;          || empresa
            #7#1  = #5#1;          || trabajador
            #7#4  = #0#3;          || a¤o
            #7#17 = #0#2;          || mes

        |LEE 101#7=;
            FEstado = FSalida;

            #7#2  = #1#9;          || provincia empresa
            #7#3  = p_mes;         || mes literal
            #7#5  = #1#2;          || nombre de la empresa
            #7#6  = #1#6;          || localidad empresa
            #7#7  = #1#13;         || cta.cotiz.empresa
            #7#8  = #5#7;          || DNI/NIF trabajador
            #7#9  = #5#75;         || nro. afil. S.S.
            #7#10 = #5#2;          || nombre
            #7#11 = #6#8;          || dias en alta
            #7#12 = #5#33;         || grupo de cotizacion
            #7#13 = #7#13 + #6#39; || Base Contin. Comunes (acumula finiq.)
        |HAZ lee_notar;            || lee los tipos del centro 1
            #7#15 = #7#13 % #12#3; || mutual. empresa
            #7#16 = #7#13 % #12#4; || mutual. trabajador
            #7#14 = #7#15 + #7#16; || mutual. total

        |SI FEstado ! 0;  |GRABA 020#7n;  |FINSI;
        |SI FEstado = 0;  |GRABA 020#7a;  |FINSI;
        |LIBERA #7;
    |FINSI;
|FINSI;
|FPRC;

|DEFBUCLE gra_tempo;
 #6#1;
 ;
 #0#0,     0, #0#2, 0;
 #0#1, 99999, #0#2, 2;
 e;
 NULL, gra_tempo1;
|FIN;
____________________________________/ INICIO Y FIN DEL PROCESO
|PROCESO impre; |TIPO 3;
|IMPRE 0;
|SI FSalida = 0;
        por_rec = #0#4;
    |DELINDEX #7;
    |ABRET #1;      || empresas
    |ABRET #5;      || trabajadores
    |ABRET #7;      || campos (tmp)
    |BUCLE gra_tempo;
    |CIERRAT #1;
    |CIERRAT #5;
    |CIERRAT #7;
    |BUCLE lee_tempo;
|SINO;
    |MENAV "    Proceso abortado ...";
|FINSI;
|FINIMP;
|FPRC;
