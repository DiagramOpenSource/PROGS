|FICHEROS;
     nomytras #0;
     labempre;
     labtraba;
     labcentr;

     nomrevca;
     nomrevli;

     nomw0023;
     nomcontr;

     &bancosc@;
|FIN;

|VARIABLES;
     nMes                = 0;
     nEmbargo            = 0;
     nDevenga            = 0;
     nDeducir            = 0;
     nBaja               = 0;
     nDia                = 0;
     nAny                = 0;
     nDiasTope           = 0;
     nTopeMes            = 0;
     nIde                = 0;
     nDMes               = 0;
     nHMes               = 0;
     nOpcion             = 0;

     aAlfa               = "";
     aMes                = "";
     aMes1               = "";
     aMes2               = "";
     aFicTmp             = "";
     aEmp                = "";
     aCen                = "";
     aNomEmp             = "";

     fFecSys             = @;

     {-1}aMenu           = "";
         aMenu1          = "2400";
         aMenu2          = "1";
         aMenu3          = "Archivo documental activado. Elija opci¢n: ";
         aMenu4          = "IAB";
         aMenu5          = "Imprimir";
         aMenu6          = "Archivar";
         aMenu7          = "Ambos";

     &enCodBanco         = 0;
     &enSwDeDonde        = 0;
     &eaVengoDe          = "";
     &nArchi             = 0;
     &enEmp              = 0;
     &swErrorDSArchi     = 0;
     &enDesdeMes         = 0;
     &enAny              = 0;
     &eaTitulo           = "";
     &eaPeriodos         = "";
     aOficina = "";
     nImporteTran = 0;

     aAlfa1 = "";
     aAlfa2 = "";
     fFecha = @;
     fFecha1 = @;
     fFecha2 = @;
     nNume = 0;

     &enEmpresa = 0;
     &enRegistro = 0;
     fDesdeFecha = @;
     fHastaFecha = @;
     nDesdeReg = 0;
     nHastaReg = 0;
|FIN;

|PROCESO FechaHasta;
     fFecha = #0#24;
     aAlfa = fFecha;
     |QBF aAlfa;
     aAlfa1 = aAlfa % -104;
     aAlfa2 = aAlfa % 402;
     |QBF aAlfa2;
     aAlfa = ("00" + aAlfa2) % -102;

     nAny = aAlfa1;
     nMes = aAlfa2;

     |HAZ CogeNombre;

     aAlfa = nTopeMes;
     aAlfa = aAlfa + "." + aAlfa2 + "." + aAlfa1;
     fFecha = aAlfa;
     #0#25 = fFecha;
     |PINTA #0#25;
|FPRC;

|PROCESO CogeNombre;
     |SI nMes =  1;  aMes = "ENERO";       nTopeMes = 31; |FINSI;
     |SI nMes =  2;
          aMes = "FEBRERO";
          nNume = nAny $ 4;
          |SI nNume ! 0;
               nTopeMes = 28;
          |SINO;
               nTopeMes = 29;
          |FINSI;
     |FINSI;
     |SI nMes =  3;  aMes = "MARZO";       nTopeMes = 31; |FINSI;
     |SI nMes =  4;  aMes = "ABRIL";       nTopeMes = 30; |FINSI;
     |SI nMes =  5;  aMes = "MAYO";        nTopeMes = 31; |FINSI;
     |SI nMes =  6;  aMes = "JUNIO";       nTopeMes = 30; |FINSI;
     |SI nMes =  7;  aMes = "JULIO";       nTopeMes = 31; |FINSI;
     |SI nMes =  8;  aMes = "AGOSTO";      nTopeMes = 31; |FINSI;
     |SI nMes =  9;  aMes = "SEPTIEMBRE";  nTopeMes = 30; |FINSI;
     |SI nMes = 10;  aMes = "OCTUBRE";     nTopeMes = 31; |FINSI;
     |SI nMes = 11;  aMes = "NOVIEMBRE";   nTopeMes = 30; |FINSI;
     |SI nMes = 12;  aMes = "DICIEMBRE";   nTopeMes = 31; |FINSI;
|FPRC;

|PROCESO nomw0023Papel;
     |PRINT;
|FPRC;

|DEFBUCLE nomw0023Papel;
     #nomw0023#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, nomw0023Papel;
|FIN;

|PROCESO Papel;
     Impresora = "CrystalReport";
     |IMPRE -1;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |INFOR "nomytras";

     |BUCLE nomw0023Papel;

     |PIEINF;
     |FININF;
     |FINIMP;
|FPRC;

|PROCESO Mensaje;
     aEmp = "                                             ";
     aCen = "                                             ";

     aAlfa   = ("00000" + #labempre#0) % -105;
     aNomEmp = #labempre#2;
     aNomEmp = aNomEmp % 126;
     aEmp = "Empresa: " + aAlfa + " - " + aNomEmp;
     aEmp = " " + aEmp + " ";

     |CUADRO 1217 1619;
     |ATRI R;
     |PINTA 1318, "      - Procesando archivo de transferencias -      ";
     |PINTA 1418, "                                                    ";
     |PINTA 1518, "                                                    ";
     |PINTA 1518, aEmp;
     |ATRI 0;
|FPRC;

|PROCESO nomw0023Archi;
     |PRINT;
|FPRC;

|DEFBUCLE nomw0023Archi;
     #nomw0023#2;
     ;
     #labempre#0, 0000, 00, 00000;
     #labempre#0, 9999, 99, 99999;
     ;
     NULL, nomw0023Archi;
|FIN;

|PROCESO ArchivaEmpresa;
     #labempre#0 = #nomw0023#2;
     |LEE 000#labempre.=;

     |HAZ Mensaje;
     |HAZ IMPREArchi;

     |BUCLE nomw0023Archi;

     |PIEINF;
     |FININF;
     |FINIMP;

     |SI swErrorDSArchi = 0;
         |HAZ Archivando;
     |FINSI;
|FPRC;

|PROCESO Archivado;
     enDesdeMes   = #nomw0023#1;
     enAny        = #nomw0023#0;
     eaTitulo     = "INFORME DE TRANSFERENCIAS CREADAS";
     fFecha1 = #0#24;
     aAlfa1 = fFecha1;
     fFecha2 = #0#25;
     aAlfa2 = fFecha2;
     eaPeriodos = aAlfa1 + " a " + aAlfa2;

     |ABRE #labempre;
     |PARA enEmp = #nomytras#0;  |SI enEmp [ #nomytras#7;  |HACIENDO enEmp = enEmp + 1;
           |SELECT #2#nomw0023;
           #nomw0023#2 = enEmp;
           #nomw0023#0 = 0;
           #nomw0023#1 = 0;
           #nomw0023#3 = 0
           |LEE 000#nomw0023.];
           |SI FSalida = 0;  |Y #nomw0023#2 = enEmp;
               |HAZ ArchivaEmpresa;
           |FINSI;
     |SIGUE;
     |CIERRA #labempre;
|FPRC;

|PROCESO Emision;
     aAlfa = "RELACION DE TRANSFERENCIAS ";

     |SI #nomytras#20 = "A";
         aAlfa = aAlfa + "ATRASOS. ";
     |FINSI;

     |SI #nomytras#1 = "P";
         aAlfa = aAlfa + " (PAGAS EXTRAS)";
     |FINSI;

     #nomytras#22 = aAlfa;

     eaVengoDe = "nomytras";
     |HAZ MiraSiArchiva;
     |SI nArchi = 0;
         |HAZ Papel;
         |ACABA;
     |FINSI;

     |MENU aMenu;
     nOpcion = FSalida;
     |SI nOpcion = 1;  |HAZ Papel;      |FINSI;
     |SI nOpcion = 2;  |HAZ Archivado;  |FINSI;
     |SI nOpcion = 3;  |HAZ Papel;      |FINSI;
     |SI nOpcion = 3;  |HAZ Archivado;  |FINSI;
|FPRC;

|PROCESO nomrevli;
     #labtraba#0 = #nomrevli#0;
     #labtraba#1 = #nomrevli#3;
     |LEE 000#labtraba.=;

     #labempre#0 = #labtraba#0;
     |LEE 000#labempre.=;

     #labcentr#0 = #labempre#0;
     #labcentr#1 = #labtraba#77;
     |LEE 000#labcentr.=;

     enCodBanco = #labtraba#71;
     |HAZ LeeBancos;
     aOficina = "";
     |SI enSwDeDonde = 0;
          aOficina = "";
     |SINO;
          aOficina = #bancosc@#5;
     |FINSI;

     aAlfa = #nomrevca#4;
     |QBF aAlfa;
     aAlfa1 = aAlfa % -104;
     aAlfa2 = aAlfa % 402;
     nAny = aAlfa1;
     nMes = aAlfa2;

     |SI #nomrevli#13 = 0000;
          #nomrevli#13 = nAny;
     |FINSI;
     |SI #nomrevli#14 = 00;
          #nomrevli#14 = nMes;
     |FINSI;

     #nomw0023#0 = #nomrevli#13;
     #nomw0023#1 = #nomrevli#14;
     #nomw0023#2 = #labtraba#0;
     #nomw0023#23 = #nomrevli#1;
     #nomw0023#24 = #nomrevli#2;
     #nomw0023#3 = #labtraba#1;
     |LEE 000#nomw0023.=;
     |SI FSalida ! 0;
         |DDEFECTO #nomw0023;
         #nomw0023#0 = #nomrevli#13;
         #nomw0023#1 = #nomrevli#14;
         #nomw0023#2 = #labtraba#0;
         #nomw0023#23 = #nomrevli#1;
         #nomw0023#24 = #nomrevli#2;
         #nomw0023#3 = #labtraba#1;
         |GRABA 020#nomw0023.b;
     |FINSI;

     #nomw0023#4  = #labcentr#1;
     #nomw0023#5  = #labtraba#71;
     #nomw0023#6  = #labempre#2;
     #nomw0023#7  = #labempre#135;
     #nomw0023#8  = #labcentr#2;
     #nomw0023#9  = aOficina;
     #nomw0023#10 = #labtraba#7;
     #nomw0023#11 = #labtraba#2;
     #nomw0023#12 = #labtraba#154 % 104;
     #nomw0023#13 = #labtraba#154 % 504;
     #nomw0023#14 = #labtraba#154 % 904;
     #nomw0023#15 = #labtraba#154 % 1304;
     #nomw0023#16 = #labtraba#154 % 1704;
     #nomw0023#17 = #labtraba#154 % 2104;
     #nomw0023#18 = #nomrevli#6;
     #nomw0023#19 = #nomw0023#0 + (("00" + #nomw0023#1) % -102);

     nMes  = #nomw0023#1;  |HAZ CogeNombre;
     aAlfa = aMes + " " + #nomw0023#0;

     #nomw0023#20 = aAlfa;

     #nomw0023#22 = #nomrevca#4;
/*
     |HAZ BuscaTrans;
     #nomw0023#21 = nImporteTran;
*/
     |GRABA 020#nomw0023.a;
     |LIBERA #nomw0023;
|FPRC;

|DEFBUCLE nomrevli;
     #nomrevli#1;
     ;
     #nomrevca#0, #nomrevca#1, 00001;
     #nomrevca#0, #nomrevca#1, 99999;
     ;
     NULL, nomrevli;
|FIN;

|PROCESO nomrevca;
     |BUCLE nomrevli;
|FPRC;

|DEFBUCLE nomrevca;
     #nomrevca#1;
     4;
     #0#0, nDesdeReg, fDesdeFecha;
     #0#7, nHastaReg, fHastaFecha;
     ;
     NULL, nomrevca;
|FIN;

|PROCESO T3;
     aFicTmp = "nomw0023" + Usuario;
     |NOME_DAT #nomw0023#aFicTmp#;
     |ABRE #nomw0023;  |CIERRA #nomw0023;  |DELINDEX #nomw0023;

     |ABRET #nomw0023;
     |ABRE #labempre;
     |ABRE #labtraba;
     |ABRE #labcentr;

     |SI enRegistro = 0;
          || Vengo de la propia pantalla
          nDesdeReg = #0#26;
          nHastaReg = #0#27;

          fDesdeFecha = #0#24;
          fHastaFecha = #0#25;
     |SINO;
          || Vengo del "Revisar Transferencias"
          nDesdeReg = enRegistro;
          nHastaReg = enRegistro;

          fDesdeFecha = "01.01.2000";
          fHastaFecha = "31.12.2999";
     |FINSI;

     |BUCLE nomrevca;

     |CIERRA #labempre;
     |CIERRA #labtraba;
     |CIERRA #labcentr;

     |LEE 000#nomw0023.p;
     |SI FSalida ! 0;
         |MENAV "0000Seleccion vacia.";
     |SINO;
         |HAZ Emision;
     |FINSI;

     |CIERRAT #nomw0023;
     |DELINDEX #nomw0023;
|FPRC;

|PROCESO T0C10; |TIPO 0;
     |SI #nomytras#20 = "M"; |Y #nomytras#Campo# ! 0;
                             |Y #nomytras#Campo# ! 2;
          |MENAV "0000Tipo de Registro no valido para la fuente de datos mensual!";
          |ERROR;
     |FINSI;
|FPRC;

|PROCESO T7C10; |TIPO 7;
     |SI #nomytras#20 = "M";
          |MENSA "0000Tipo de registro: (0) mensual; (2) finiquito.";
     |FINSI;

     |SI #nomytras#20 = "H";
          |MENSA "0000Tipo de registro: (0) mensual; (2) finiquito; (5 + n) atrasos";
     |FINSI;
|FPRC;

|PROCESO T7C4; |TIPO 7;
     fFecSys = ~;
     aAlfa   = fFecSys % -102;
     #nomytras#Campo# = aAlfa;
     |PINTA #nomytras#Campo#;
|FPRC;

|PROCESO T0C3; |TIPO 0;
     |SI #nomytras#20 = "M";  |Y Campo = 15;
         |ACABA;
     |FINSI;

     nMes = #nomytras#Campo#;
     |HAZ CogeNombre;
     nDiasTope = nTopeMes;

     |SI Campo = 3;   aMes1 = (aMes + "          ") % 110;  |FINSI;
     |SI Campo = 15;  aMes2 = (aMes + "          ") % 110;  |FINSI;

     |ATRI I;
     |SI Campo = 3;  |PINTA 1135, aMes1; |FINSI;
     |SI Campo = 15; |PINTA 1151, aMes2; |FINSI;
     |ATRI 0;
|FPRC;

|PROCESO T7C3; |TIPO 7;
     fFecSys = ~;
     aAlfa   = fFecSys % 402;

     #nomytras#Campo# = aAlfa;
     |PINTA #nomytras#Campo#;
|FPRC;

|PROCESO FuenteDatos;
     #nomytras#17 = "2";

     |SI #nomytras#20 = "M";
          aAlfa = "MENSUAL  ";

          |C_V #nomytras#15, 1, "N";
          |C_V #nomytras#16, 1, "N";
          |C_V #nomytras#17, 1, "N";
          |C_V #nomytras#1,  1, "S";
          |C_V #nomytras#7,  1, "S";

          |PINTA 1118,"Mes ........:                               ";
          |PINTA 1218,"A¤o ........:                               ";
          |PINTA 1718,"                        ";

          |PINTA #nomytras#7;
          |PINTA #nomytras#3;
          |PINTA #nomytras#4;
          |PINTA #nomytras#1;
          |PINTA #nomytras#10;
     |FINSI;

     |SI #nomytras#20 = "H";
          aAlfa = "HISTORICO";

          |C_V #nomytras#15, 1, "S";
          |C_V #nomytras#16, 1, "S";
          |C_V #nomytras#17, 1, "S";
          |C_V #nomytras#1,  1, "S";
          |C_V #nomytras#10, 1, "S";
          |C_V #nomytras#7,  1, "S";

          |PINTA #nomytras#7;
          |PINTA #nomytras#3;
          |PINTA #nomytras#4;
          |PINTA #nomytras#15;
          |PINTA #nomytras#16;
          |PINTA #nomytras#17;
          |PINTA #nomytras#1;
          |PINTA #nomytras#10;
     |FINSI;

     |SI #nomytras#20 = "A";
          aAlfa = "ATRASOS  ";

          |C_V #nomytras#7,  1, "N";
          |C_V #nomytras#16, 1, "N";
          |C_V #nomytras#10, 1, "N";
          |C_V #nomytras#1,  1, "N";

          |C_V #nomytras#3,  1, "S";
          |C_V #nomytras#4,  1, "S";
          |C_V #nomytras#15, 1, "S";
          |C_V #nomytras#17, 1, "S";

          |PINTA 0718,"Empresa ....:                               ";
          |PINTA 1118,"Meses ......:                               ";
          |PINTA 1218,"A¤o  .......:                               ";
          |PINTA 1618, "                          ";
          |PINTA 1818, "                          ";

          #nomytras#17 = "1";

          |PINTA #nomytras#7;
          |PINTA #nomytras#16;
          |PINTA #nomytras#10;
          |PINTA #nomytras#1;

          |PINTA #nomytras#3;
          |PINTA #nomytras#4;
          |PINTA #nomytras#15;
          |PINTA #nomytras#17;
     |FINSI;

     |PINTA 0770, aAlfa;
     |PINTA #nomytras#20;
|FPRC;

|PROCESO T7C0; |TIPO 7;
/*
     |SI PARAMETRO = "M"; |O PARAMETRO = "H"; |O PARAMETRO = "A";
         #nomytras#20 = PARAMETRO;
     |FINSI;

     |HAZ FuenteDatos;
     |PINDA #0#nomytras;
*/
|FPRC;

|PROGRAMA;
     |SI enEmpresa ! 0; |Y enRegistro ! 0;
          || vengo del propio mantenimiento de transferencias
          |ABRE #0;
          |LEE 000#0p;

          #0#0 = enEmpresa;
          #0#7 = enEmpresa;
     |SINO;

          |CLS;

          |ABRE #nomcontr; |LEE 000#nomcontr.p; |CIERRA #nomcontr;

          |PINPA #0#0;

          |ABRE #0;
          |DDEFECTO #0;
          |LEE 000#0p;
          |SI FSalida = 0;
               |BORRA 020#0a;
          |FINSI;

          |PINDA #0#0;

          |ENDATOS #1#0;
     |FINSI;

     |SI FSalida = 0;
          |HAZ T3;
     |FINSI;

     |GRABA 000#0n;
     |CIERRA #0;
|FPRO;
