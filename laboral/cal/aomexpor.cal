|FICHEROS;
    aomexpor #0;
    aomentra #1;    || entrada de horas
    aomhoras #2;    || tipos de hora
    labtraba #3;    || trabajadores
    aomobras #4;    || obras
    aomsubob #5;    || subobras
    labempre #6;    || empresas
|FIN;

|VARIABLES;
    seleccio = 0;

    alfa1 = "";
    alfa2 = "";
    alfa3 = "";
    alfa4 = "";
    nume1 = 0;
    nume2 = 0;
    nume3 = 0;
    nume4 = 0;

    fabre = "";
    field = "";
    fcanl = "";

    lafecha = "";
    codobra = "";
    subobra = "";
    empresa = "";
    trabaja = "";
    codhora = "";
    nuhoras = "";
    destajo = "";
    prehora = "";

    carac = 0;
    regis = 0;
|FIN;

|PROGRAMA;
|CLS;
|CABEZA "EXPORTACION HORAS";
|PINPA #0#0;
|DDEFECTO #0;
|ABRE #0;
|LEE 101#0p;
|SI FSalida ! 0;    |GRABA 020#0b; |FINSI;
|PINDA #0#0;
|ENDATOS #1#0;
|SI FSalida = 0;
    |GRABA 020#0a;
|FINSI;
|LIBERA #0;
|CIERRA #0;
|FPRO;

************************************************************************
   RUTINAS DE LECTURA E IMPRESION
************************************************************************

|PROCESO lee_empresa;
|DDEFECTO #6;
    #6#0 = #1#2;
|LEE 000#6=;
|FPRC;

|PROCESO lee_subobra;
|DDEFECTO #5;
    #5#0 = #1#1;
    #5#1 = #1#7;
|LEE 000#5=;
|FPRC;

|PROCESO lee_obra;
|DDEFECTO #4;
    #4#0 = #1#1;
|LEE 000#4=;
|FPRC;

|PROCESO lee_traba;
|DDEFECTO #3;
    #3#0 = #1#2;
    #3#1 = #1#3;
|LEE 000#3=;
|FPRC;

|PROCESO lee_horas;
|DDEFECTO #2;
    #2#0 = #1#4;
|LEE 000#2=;
|FPRC;

|PROCESO grabando;
    field = fcanl + " " + field;
|FGRABA field;
    carac = carac + FSalida;
|FPRC;

|PROCESO exporta1;
    seleccio = 1;

|HAZ lee_empresa;
|HAZ lee_traba;
|HAZ lee_obra;
|HAZ lee_subobra;
|HAZ lee_horas;

    lafecha = #1#0;
    field = lafecha;     |HAZ grabando;

    empresa = #1#2; empresa = ("00000"     + empresa) % -105;
    field = empresa;     |HAZ grabando;
    field = #6#2;        |HAZ grabando;

    trabaja = #1#3; trabaja = ("00000"     + trabaja) % -105;
    field = trabaja;     |HAZ grabando;
    field = #3#2;        |HAZ grabando;

    codobra = #1#1; codobra = ("0000"      + codobra) % -104;
    field = codobra;     |HAZ grabando;
    field = #4#1;        |HAZ grabando;

    subobra = #1#7; subobra = ("00"        + subobra) % -102;
    field = subobra;     |HAZ grabando;
    field = #5#2;        |HAZ grabando;

    codhora = #1#4; codhora = ("00"        + codhora) % -102;
    field = codhora;     |HAZ grabando;
    field = #2#2;        |HAZ grabando;

    prehora = #2#1; prehora = ("      "    + prehora) % -106;
    field = prehora;     |HAZ grabando;

    nuhoras = #1#5; nuhoras = ("         " + nuhoras) % -109;
    field = nuhoras;     |HAZ grabando;

    destajo = #1#6; destajo = ("        "  + destajo) % -108;
    field = destajo;     |HAZ grabando;

    field = &13 + &10;   |HAZ grabando;      || (final linea MS-DOS)

    regis = regis + 1;
|FPRC;

|DEFBUCLE exporta;
 #1#1;
 ;
 #0#1, #0#3, #0#5, #0#7, #0#11, #0#09;
 #0#2, #0#4, #0#6, #0#8, #0#12, #0#10;
 ;
 NULL, exporta1;
|FIN;

************************************************************************
   RUTINAS DE INICIO DE PROCESO
************************************************************************

|PROCESO proceso; |TIPO 3;
|ABRE #2;
|ABRE #3;
|ABRE #4;
|ABRE #5;
|ABRE #6;
    alfa1 = #0#13;  |QBF alfa1;
    fabre = "wb  " + alfa1;
|FABRE fabre;
|SI FSalida ] 0;
        fcanl = FSalida;
    |BUCLE exporta;
    |FCIERRA fcanl;
    |SI seleccio = 0;
        |MENAV "    Seleccion vacia ...";
    |SINO;
            alfa1 = "    Grabados [" + carac + "] caracteres en [" + regis + "] registros ...";
        |MENAV alfa1;
    |FINSI;
|SINO;
        alfa1 = "    [" + (fabre % 5) + "] inaccesible para escritura ...";
    |MENAV alfa1;
|FINSI;
    Pos = -1;
|CIERRA #2;
|CIERRA #3;
|CIERRA #4;
|CIERRA #5;
|CIERRA #6;
|FPRC;
