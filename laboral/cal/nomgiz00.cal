|FICHEROS;
     nomgir00 #0;
     labempre;
     labtraba;
     nomhicca;
     nomir012;
     nomgirca;
     nomgirli;
     nomgirma;
     nomgirm2;
     nomgirhi;
     nomir009;
     nomcontr;
     nomgirtm #100,MANTE;
     nomir013;
     nomgirco;
     nomir003;
|FIN;

|VARIABLES;
     &nEmp = 0;
     &nTra = 0;
     &nMes = 0;
     &nAny = 0;
     nMesX = 0;
     nMesAnt = 0;
     nIRPFX = 0;
     &nIRPFMes = 0;
     nIRPFAnt = 0;
     nEstimado = 0;
     nEstimadoMes = 0;
     nEstimadoAnt = 0;
     nBruto = 0;
     nBrutoMes = 0;
     nBrutoAnt = 0;
     nCuota = 0;
     nCuotaMes = 0;
     nCuotaAnt = 0;
     nVar = 0;
     nVarAnt = 0;
     nVarMes = 0;
     nOtros = 0;
     nOtrosAnt = 0;
     nOtrosMes = 0;
     nEstPagas = 0;
     nEstPagasAnt = 0;
     nEstPagasMes = 0;
     nTipoTra = 1;
/*
     nMargenInfIRPF = -0.05;
     nMargenSupIRPF = 0.05;
     nMargenInfEst = -100;
     nMargenSupEst = 100;
*/
     nMargenInfIRPF = -0.02;
     nMargenSupIRPF = 0.02;
     nMargenInfEst = -100.01;
     nMargenSupEst = 100.01;

     nDiferencia = 0;
     nDiferencia2 = 0;
     nDiferencia3 = 0;
     nDiferencia4 = 0;

     FEstado = 0;
     &aParam = "";
     aAlfa = "";
     &aHora = "";
     &fFecha = @;
     nIncid = 0;
     nMovim = 0;
     nCampo = 0;
     aAlfa1 = "";
     aAlfa2 = "";
     aClave = "";
     aDesdeClave = "";
     aHastaClave = "";
     nAcceso = 0;
     nUltAcceso = 0;
     &swEdicion = 0;
     swCambiaDatos = 0;
     &swBajaEsteMes = 0;
     swSigue = 0;
     swDatosDefecto = 0;
     nLimInfIng = 11500;
     nPara = 0;
     nPara1 = 0;
     nTipoCon = 0;
|FIN;

|PROCESO IniciaVar;
     nBrutoMes = 0;
     nCuotaMes = 0;
     nEstimadoMes = 0;
     nVarMes = 0;
     nBrutoAnt = 0;
     nCuotaAnt = 0;
     nEstimadoAnt = 0;
     nVarAnt = 0;
     nOtros = 0;
     nOtrosAnt = 0;
     nOtrosMes = 0;
     nEstPagas = 0;
     nEstPagasAnt = 0;
     nEstPagasMes = 0;
     nTipoCon = 0;
|FPRC;

|PROCESO graba_nomgirtm;
     |SI #nomgirma#2 = 0;     || estas no las muestro siquiera
          |ACABA;
     |FINSI;

     |DDEFECTO #nomir009;          || si desactivo por empresa
     #nomir009#0 = #nomgirli#0;    || que no me muestre la incidencia
     |LEE 000#nomir009.=;          || aunque si que se graba
     |SI FSalida = 0;
          |SI #nomir009#21 = 0;
               |ACABA;
          |FINSI;
     |SINO;                        || si la empresa no existe
          |ACABA;                  || que tampoco me las muestre
     |FINSI;

     || el resto si
     || las "1" son para avisar, que las muestro siempre, y si no que lo
     || cambien en la configuracion; las "2" son las que me tienen que
     || confirmar para dejarlos pasar, y aunque este confirmado con la
     || "F" se los muestro cada vez que los emitan

     #nomgirtm#0 = #nomgirli#0;
     #nomgirtm#1 = #nomgirli#1;
     #nomgirtm#2 = #nomgirli#2;
     #nomgirtm#3 = #nomgirli#3;
     #nomgirtm#4 = #nomgirli#4;
     #nomgirtm#5 = #nomgirli#5;
     #nomgirtm#6 = #nomgirli#6;
     #nomgirtm#7 = #nomgirli#7;
     #nomgirtm#8 = #nomgirli#8;
     #nomgirtm#9 = #nomgirli#9;
     #nomgirtm#10 = #labempre#2;
     #nomgirtm#11 = #labtraba#2;

     #nomgirm2#0 = #nomgirtm#6;
     |LEE 000#nomgirm2.=;
     #nomgirtm#12 = #nomgirm2#3;
     #nomgirtm#13 = #nomgirm2#4;

     |GRABA 020#nomgirtm.n;
     |LIBERA #nomgirtm;

     |SI #nomgirma#2 ! 0;     || las 0 no las muestro siquiera
          swEdicion = 1;
     |FINSI;
|FPRC;

|PROCESO cuenta_nomgirhi;
     nUltAcceso = #nomgirhi#4;
|FPRC;

|DEFBUCLE lee_nomgirhi;
     #nomgirhi#2;
     ;
     nEmp, nAny, nMes, nTra, 001;
     nEmp, nAny, nMes, nTra, 999;
     ;
     NULL, cuenta_nomgirhi;
|FIN;

|PROCESO borra_nomgirli_X;
     |BORRA 020#nomgirli.a;
     |LIBERA #nomgirli;
|FPRC;

|DEFBUCLE borra_nomgirli_X;
     #nomgirli#1;
     9;
     nEmp, nAny, nMes, aDesdeClave, "X";
     nEmp, nAny, nMes, aHastaClave, "X";
     be;
     NULL, borra_nomgirli_X;
|FIN;

|PROCESO borra_nomgirli;
     |SI #nomgirli#9 = "S"; |O #nomgirli#9 = "N";
          |BORRA 020#nomgirli.a;
          |LIBERA #nomgirli;
     |FINSI;
     |SI #nomgirli#9 = "F";
          || me quedo con los "F" ya que si no vuelven a salir los tengo que
          || borrar ya que quiere decir que estan solucionados, las marco
          #nomgirli#8 = "X";
          |GRABA 020#nomgirli.a;
          |LIBERA #nomgirli;
     |FINSI;
|FPRC;

|DEFBUCLE borra_nomgirli;
     #nomgirli#1;
     ;
     nEmp, nAny, nMes, aDesdeClave;
     nEmp, nAny, nMes, aHastaClave;
     be;
     NULL, borra_nomgirli;
|FIN;

|PROCESO graba_nomgirli;
     #nomgirli#0 = nEmp;
     #nomgirli#1 = nAny;
     #nomgirli#2 = nMes;
     #nomgirli#3 = aClave;
     |LEE 101#nomgirli.=;
     FEstado = FSalida;

     #nomgirli#5 = nTra;
     #nomgirli#6 = nIncid;

     #nomgirma#0 = nIncid;
     |LEE 000#nomgirma.=;
     #nomgirli#7 = #nomgirma#1;

     #nomgirli#8 = #labtraba#7;

     |SI nIncid = 999;
          #nomgirli#9 = "S";
     |SINO;
          |SI FEstado ! 0;
               |SI #nomgirma#2 = 2;
                    #nomgirli#9 = "N";
               |SINO;
                    #nomgirli#9 = "S";
               |FINSI;
          |SINO;
               || es que existia y sobrevivio al borrado previo: era una "F"
               || marcada, mantengo mi "F"
               #nomgirli#9 = "F";
          |FINSI;
     |FINSI;

     |SI FEstado = 0;
          |GRABA 020#nomgirli.a;
     |SINO;
          |GRABA 020#nomgirli.n;
     |FINSI;
     |LIBERA #nomgirli;
|FPRC;

|PROCESO graba_nomgirhi;
     || Ahora grabo el movimiento en el log del registro

     #nomgirhi#0 = nEmp;           || campos clave primaria
     #nomgirhi#1 = nAny;
     #nomgirhi#2 = nMes;
     #nomgirhi#3 = aClave;
     #nomgirhi#4 = nAcceso;
     #nomgirhi#9 = #nomgirli#9;    || estado: S, N, F

     #nomgirhi#5 = nTra;
     #nomgirhi#6 = nIncid;
     #nomgirhi#7 = #nomgirli#7;
     #nomgirhi#8 = #nomgirli#8;
     #nomgirhi#10 = nIRPFMes;
     #nomgirhi#11 = aParam;

     aAlfa = Usuario % 0810; |QBF aAlfa;
     #nomgirhi#12 = aAlfa;
     fFecha = ~;
     #nomgirhi#13 = fFecha;
     #nomgirhi#14 = aHora;
     || #nomgirhi#15 = nAcceso;    || esto ya no lo uso
     #nomgirhi#16 = #nomgirma#2;
     |SI aParam = "nomactca"; |O aParam = "nomrecib";
          #nomgirhi#17 = #nomgirco#1;
     |FINSI;
     |SI aParam = "nomcalcu"; |O aParam = "gomcalcu";
          #nomgirhi#17 = #nomgirco#2;
     |FINSI;
     #nomgirhi#18 = #nomir009#21;

     |GRABA 020#nomgirhi.n;
     |LIBERA #nomgirhi;
|FPRC;

|PROCESO graba_incid;
     aAlfa1 = nTra;
     |QBF aAlfa1; aAlfa1 = ("00000" + aAlfa1) % -105;
     aAlfa2 = nIncid;
     |QBF aAlfa2; aAlfa2 = ("000" + aAlfa2) % -103;
     aClave = aAlfa1 + "." + aAlfa2;
     |ABRE #nomgirco; |LEE 000#nomgirco.p; |CIERRA #nomgirco;
     |HAZ graba_nomgirli;
     |HAZ graba_nomgirtm;
     |HAZ graba_nomgirhi;
|FPRC;

|PROCESO lee_nomhicca;
     nIRPFAnt = 0;

     #nomhicca#0 = nEmp;
     #nomhicca#1 = nTra;

     #nomhicca#2 = nMesAnt;
     #nomhicca#3 = 0;
     #nomhicca#66 = nAny - 2000;
     |LEE 000#nomhicca.=;
     |SI FSalida = 0;
          nIRPFAnt = #nomhicca#13;
     |SINO;
          || no hay nada en el historico el mes anterior
          nIRPFAnt = -1;
     |FINSI;
|FPRC;

|PROCESO Incidencias00;
     || desactivada regularizacion el mes
     |SI #labtraba#29 = 0;
          || desactivada en el trabajador
          nIncid = 001;
          |HAZ graba_incid;
     |SINO;
          #nomir009#0 = #labtraba#0;
          |LEE 000#nomir009.=;
          |SI FSalida = 0;
               |SI #nomir009#nMes# = "N";
                    || desactivada en la empresa
                    nIncid = 002;
                    |HAZ graba_incid;
               |FINSI;
          |SINO;
               |ABRE #nomcontr;
               |LEE 000#nomcontr.p;
               nCampo = nMes + 20;
               |SI #nomcontr#nCampo# = "N";
                    || desactivada en control
                    nIncid = 003;
                    |HAZ graba_incid;
               |FINSI;
               |CIERRA #nomcontr;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO lee_nomir012;
     nEstimado = 0;
     nBruto = 0;
     nCuota = 0;
     nVar = 0;
     nEstPagas = 0;
     nOtros = 0;

     #nomir012#1 = nEmp;
     #nomir012#2 = nTra;
     #nomir012#3 = nAny;
     #nomir012#4 = nMesX;
     |LEE 000#nomir012.=;
     |SI FSalida = 0;
          |SI nMesX = nMes;             || datos del mes de calculo
               |SI #nomir012#108 = "1"; || DATOS por defecto
                    swDatosDefecto = 1;
               |SINO;
                    swDatosDefecto = 0;
               |FINSI;
               nTipoCon = #nomir012#109;
               |SI nTipoCon = 0;
                    #nomir003#0 = #nomir012#0;
                    #nomir003#40 = #nomir012#3;
                    #nomir003#41 = #nomir012#1;
                    |LEE 000#nomir003.=;
                    |SI FSalida = 0;
                         nTipoTra = #nomir003#5;
                    |FINSI;
               |FINSI;
               |SI #nomir012#106 = 1; |O #nomir012#107 = 2;
                    nTipoTra = 2;
               |FINSI;
          |FINSI;

          nEstimado = #nomir012#84;
          nVar = #nomir012#101;
          |PARA nPara = nMesX + 1; |SI nPara [ 12; |HACIENDO nPara = nPara + 1;
               nPara1 = nPara + 17;
               nEstPagas = nEstPagas + #nomir012#nPara1#;
          |SIGUE;
          nBruto = #nomir012#17 + nEstPagas + #nomir012#43 + #nomir012#69;
          nCuota = #nomir012#94;
          nOtros = #nomir012#87;
          |SI nMesX = nMesAnt;
               || estoy buscando estimado a¤o anterior, ya tengo bastante
               |ACABA;
          |FINSI;
          nDiferencia3 = #nomir012#85 - #nomir012#86;
          |SI nIRPFX = #nomir012#85;
               || lo del mant. mensual es igual que lo aplicado segun la
               || reg. del irpf (es lo logico claro)

               |SI nMargenInfIRPF [ nDiferencia3; |Y nDiferencia3 [ nMargenSupIRPF;
                    || todo correcto
                    nIncid = 999
               |SINO;
                    || activada regularizacion pero no he regularizado
                    || el mes
                    swSigue = 0;
                    |SI #nomir012#106 = 1; |O #nomir012#106 = 2; |O #nomir012#107 = 2;
                         |SI #nomir012#106 = 1; |O #nomir012#107 = 2;
                              || no regularizo por ser mayor el % del trab.
                              || que calculado y tenerlo asi configurado
                              || en el trabajador o en regul. autom.
                              |SI #nomir012#86 < #nomir012#85;
                                   |SI #labtraba#30 ! #nomir012#86;
                                        nIncid = 013;
                                        |HAZ graba_incid;
                                        swSigue = 1;
                                   |SINO;
                                        nIncid = 011;
                                        |HAZ graba_incid;
                                        swSigue = 1;
                                   |FINSI;
                              |FINSI;
                         |FINSI;
                         |SI #nomir012#107 = 2;
                              || el % del trab. es menor que el calculado,
                              || deberia regularizar pero lo he ignorado
                              || o aun no he regularizado
                              |SI #nomir012#85 < #nomir012#86;
                                   |SI #labtraba#30 ! #nomir012#86;
                                        nIncid = 014;
                                        |HAZ graba_incid;
                                        swSigue = 1;
                                   |SINO;
                                        nIncid = 011;
                                        |HAZ graba_incid;
                                        swSigue = 1;
                                   |FINSI;
                              |FINSI;
                         |FINSI;
                         |SI #nomir012#106 = 2;
                              || no regularizo por ser mayor el % calculado
                              || que el del trabajador y tenerlo asi configurado
                              || en regul. autom.
                              |SI #nomir012#85 < #nomir012#86;
                                   nIncid = 015;
                                   |HAZ graba_incid;
                                   swSigue = 1;
                              |FINSI;
                         |FINSI;
                    |FINSI;
                    |SI swSigue = 0;
                         |SI #labtraba#30 ! #nomir012#86;
                              nIncid = 016;
                              |HAZ graba_incid;
                         |SINO;
                              nIncid = 011;
                              |HAZ graba_incid;
                         |FINSI;
                    |FINSI;
               |FINSI;
          |SINO;
               || inconsistencia: IRPF en nomir012 distinto del calculado
               || en el mantenimiento de calculos
               || raro, la unica posibilidad es que calcularan estimando y
               || luego volvieran a hacerlo cambiando el IRPF a mano y sin
               || estimar

               nIncid = 012;
               |HAZ graba_incid;
          |FINSI;
     |SINO;
          |SI nMesX = nMesAnt;
               || estoy buscando estimado a¤o anterior, ya tengo bastante
               |ACABA;
          |FINSI;
          |SI nIncid = 999;
               || no se ha grabado nada en nomir012 y no sabemos por que
               nIncid = 004;
               |HAZ graba_incid;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO graba_nomgirca;
     #nomgirca#0 = nEmp;
     #nomgirca#1 = nAny;
     #nomgirca#2 = nMes;
     |LEE 101#nomgirca.=;
     |SI FSalida ! 0;
          |GRABA 020#nomgirca.n;
     |FINSI;
|FPRC;

|PROCESO lee_nomir013;
     |SI nMes = 1;       || en enero nada
          |ACABA;
     |FINSI;

     swCambiaDatos = 0;

     #nomir013#0 = #labtraba#7;
     #nomir013#41 = nEmp;
     #nomir013#40 = nAny;
     #nomir013#120 = nMes;
     |LEE 000#nomir013.=;
     |SI FSalida = 0;
          swCambiaDatos = 1;
          |LEE 000#nomir013.a;
          |SI FSalida = 0;
               |SI #nomir013#0 = #labtraba#7; |Y #nomir013#41 = nEmp;
               |Y #nomir013#40 = nAny;

               |SINO;
                    || es el primer mes del a¤o que se graba, no puedo saber
                    || si han cambiado datos o no

                    swCambiaDatos = 0;
               |FINSI;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO CargaDefectos;
     /*   DE MOMENTO LO QUITO, TOLERANCIA CERO

     |ABRE #nomgirco;
     |LEE 000#nomgirco.p;
     |SI FSalida = 0;
          nMargenInfIRPF = #nomgirco#4;
          nMargenSupIRPF = #nomgirco#5;
          nMargenInfEst = #nomgirco#6;
          nMargenSupEst = #nomgirco#7;
     |SINO;
          nMargenInfIRPF = -0.05;
          nMargenSupIRPF = 0.05;
          nMargenInfEst = -100;
          nMargenSupEst = 100;
     |FINSI;
     |CIERRA #nomgirco;
     */
|FPRC;

|PROCESO DifCuota;
     || aplico nMargenInfEst, el mismo margen de la estimacion con la cuota
     nDiferencia4 = nCuotaMes - nCuotaAnt;
     |SI nMargenInfEst [ nDiferencia4; |Y nDiferencia4 [ nMargenSupEst;
          || cambia la cuota de retencion anual del mes anterior al actual
          nDiferencia2 = nEstimadoMes - nEstimadoAnt;
          |SI nMargenInfEst [ nDiferencia2; |Y nDiferencia2 [ nMargenSupEst;
               || correcto no hay diferencia en estimado
          |SINO;
               || cambia la estimacion del bruto
               |SI nDiferencia2 > 0;         || BRUTO AUMENTA
                    |SI nDiferencia4 < 0;    || RETENCION DISMINUYE, ALGO PASA
                         |SI swCambiaDatos = 0; || NO CAMBIA DATOS
                              nIncid = 038;
                              |HAZ graba_incid; || Grabo incidencia
                         |FINSI;
                    |FINSI;
               |FINSI;
               |SI nDiferencia2 < 0;         || BRUTO DISMINUYE
                    |SI nDiferencia4 > 0;    || RETENCION AUMENTA, ALGO PASA
                         |SI swCambiaDatos = 0; || NO CAMBIA DATOS
                              nIncid = 034;
                              |HAZ graba_incid; || Grabo incidencia
                         |FINSI;
                    |FINSI;
               |FINSI;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Estudio;
     |ABRE #nomgirca;
     |ABRE #nomgirli;
     |ABRE #nomgirhi;
     |ABRE #nomir012;
     |ABRE #nomhicca;
     |ABRE #nomgirma;
     |ABRE #nomgirm2;
     |ABRE #labempre;
     |ABRE #labtraba;
     |ABRE #nomir013;
     |ABRE #nomir009;
     |ABRE #nomir003;

     || inicializo el nomgirtm, el fichero puente para despues mostrar
     || las incidencias al usuario

     aAlfa = Usuario;         |QBF aAlfa;
     aAlfa = "t" +  aAlfa;
     |NOME_DAT #100 aAlfa;
     |ABRE #100;

     aAlfa1 = nTra;
     |QBF aAlfa1; aAlfa1 = ("00000" + aAlfa1) % -105;
     aAlfa2 = nIncid;
     |QBF aAlfa2; aAlfa2 = ("000" + aAlfa2) % -105;
     aClave = aAlfa1 + "." + aAlfa2;
     aDesdeClave = aAlfa1 + ".001";
     aHastaClave = aAlfa1 + ".999";

     |HAZ CargaDefectos;

     |HAZ IniciaVar;

     #labempre#0 = nEmp;
     |LEE 000#labempre.=;

     #labtraba#0 = nEmp;
     #labtraba#1 = nTra;
     |LEE 000#labtraba.=;

     |BUCLE borra_nomgirli;

     |SI #labtraba#247 = 613; |O #labtraba#247 = 163;
          |SI #labtraba#42 ! "A";
               |HAZ CierraFich;
               |ACABA;
          |FINSI;
     |FINSI;

     |SI #labtraba#247 = 138;
          |HAZ CierraFich;
          |ACABA;
     |FINSI;

     |HAZ lee_nomir013;

     nAcceso = 0;
     |BUCLE lee_nomgirhi;     || para saber cual fue el ultimo numero de con-
                                || tador con que se grabo este trabajador
     nAcceso = nUltAcceso + 1;

     || nIRPFMes es el IRPF calculado en el nomcalca

     nIncid = 999;

     |HAZ graba_nomgirca;

     |HAZ Incidencias00;

     nMesX = nMes;
     nIRPFX = nIRPFMes;

     swDatosDefecto = 0;
     |HAZ lee_nomir012;   || primero compruebo mes actual

     nEstimadoMes = nEstimado;
     nBrutoMes = nBruto;
     nCuotaMes = nCuota;
     nVarMes = nVar;
     nOtrosMes = nOtros;

     |SI nMes ! 1; |Y nIncid ! 001; |Y nIncid ! 002; |Y nIncid ! 003;
          || no estoy en enero, miro mes anterior en el historico
          nMesAnt = nMes - 1;
          |HAZ lee_nomhicca;

          nMesX = nMesAnt;
          nIRPFX = nIRPFAnt;
          |HAZ lee_nomir012;

          nEstimadoAnt = nEstimado;
          nBrutoAnt = nBruto;
          nCuotaAnt = nCuota;
          nOtrosAnt = nOtros;
          nVarAnt = nVar;

          || nBrutoMes: el bruto estimado por nominas+fin+manual+Atr del mes
          || nBrutoAnt: el bruto estimado por nominas+fin+manual+Atr del mes anterior
          || nEstimadoMes: lo estimado en el mes, segun nomir012
          || nEstimadoAnt: lo estimado en el mes anterior, segun nomir012
          || nIRPFMes: lo retenido en el mes, segun nomcalca (o nomhicca)
          || nIRPFAnt: lo retenido en el mes anterior, segun nomhicca
          || nCuotaAnt: la cuota IRPF a retener segun la estimacion del mes

          |SI nIRPFAnt ! -1;
               nDiferencia = nIRPFMes - nIRPFAnt;
               |SI nMargenInfIRPF [ nDiferencia; |Y nDiferencia [ nMargenSupIRPF;
                    || el IRPF se mantiene con respecto al mes anterior
                    nDiferencia2 = nEstimadoMes - nEstimadoAnt;
                    |SI nMargenInfEst [ nDiferencia2; |Y nDiferencia2 [ nMargenSupEst;
                         || correcto no hay diferencia en estimado
                         nIncid = 999;
                    |SINO;
                         swSigue = 0;
                         |SI nDiferencia2 > 0;
                              swSigue = 1;

                              |SI nIRPFAnt = 0; |Y nIRPFMes = 0;
                              |Y nEstimadoMes < nLimInfIng;
                                   || paso del 0 al 0, y los ingresos siguen
                                   || siendo muy bajos
                                   swSigue = 0;
                              |FINSI;
                              |SI nTipoTra = 2; |Y nDiferencia3 > 0;
                                   swSigue = 0;
                              |FINSI;
                              |SI nDiferencia > 0;
                                   || peque¤a ya que no supera los limites
                                   || pero alguna diferencia hay
                                   swSigue = 0;
                              |FINSI;
                              |SI nTipoCon = 2; |Y nIRPFAnt = 2; |Y nIRPFMes = 2;
                                   || estoy cogiendo lo del minimo de los contratos
                                   || eventuales
                                   swSigue = 0;
                              |FINSI;
                              |SI swSigue = 1;
                                   || salario sube, IRPF se mantiene
                                   nIncid = 021;
                                   |HAZ graba_incid;
                              |FINSI;
                         |FINSI;

                         swSigue = 0;
                         |SI nDiferencia2 < 0;
                              swSigue = 1;
                              || en principio muestro la incidencia

                              ||SI swBajaEsteMes = 1;
                              |SI nIRPFAnt = 0; |Y nIRPFMes = 0;
                                   || si soy baja este mes y tenia un cero de IRPF
                                   || y me bajan los ingresos debido a la paga y logicamente
                                   || sigo teniendo un cero no hace falta esta incid.

                                   || bueno si no soy baja este mes tambien
                                   || si de un cero paso a un cero, no hace falta

                                   swSigue = 0
                              |FINSI;
                              |SI nTipoTra = 2; |Y nDiferencia3 > 0;
                                   swSigue = 0;
                              |FINSI;
                              |SI nTipoCon = 2; |Y nIRPFAnt = 2; |Y nIRPFMes = 2;
                                   || estoy cogiendo lo del minimo de los contratos
                                   || eventuales
                                   swSigue = 0;
                              |FINSI;
                              |SI nDiferencia < 0;
                                   || peque¤a ya que no supera los limites
                                   || pero alguna diferencia hay
                                   swSigue = 0;
                              |FINSI;
                              |SI swSigue = 1;
                                   || en otro caso si que la dejo
                                   || salario baja, IRPF se mantiene

                                   nIncid = 022;
                                   |HAZ graba_incid;
                              |FINSI;
                         |FINSI;
                    |FINSI;
               |SINO;
                    || cierto cambio en el %IRPF, lo comprobamos
                    |SI nDiferencia > 0;
                         || %IRPF mes mayor que el mes anterior
                         |SI nEstimadoMes > nEstimadoAnt;
                              || logico, en principio, ingresos suben
                              nIncid = 031;
                              |HAZ graba_incid;
                         |SINO;
                              || raro, IRPF sube, ingresos bajan o se mantienen
                              || comprobar
                              |SI swCambiaDatos = 0;
                                   nIncid = 032;
                              |SINO;
                                   nIncid = 033;
                              |FINSI;
                              |HAZ graba_incid;
                         |FINSI;
                    |FINSI;
                    |SI nDiferencia < 0;
                         || %IRPF mes menor que el mes anterior
                         |SI nEstimadoMes < nEstimadoAnt;
                              || logico, en principio, ingresos bajan
                              nIncid = 035;
                              |HAZ graba_incid;
                         |SINO;
                              || raro, IRPF baja, ingresos suben o se mantienen
                              || comprobar
                              |SI swCambiaDatos = 0;
                                   nIncid = 036;
                              |SINO;
                                   nIncid = 037;
                              |FINSI;
                              |HAZ graba_incid;
                         |FINSI;
                    |FINSI;

                    nDiferencia = nBrutoMes - nBrutoAnt;
                    nDiferencia2 = nEstimadoMes - nEstimadoAnt;
                    |SI nMargenInfEst [ nDiferencia2; |Y nDiferencia2 [ nMargenSupEst;
                         || NO ha habido cambios en la estimacion
                    |SINO;
                         || ha habido cambios en la estimacion
                         |SI nDiferencia = 0; |Y nVarAnt = nVarMes;
                         |Y nOtrosAnt = nOtrosMes;
                              || en cambio el bruto no ha cambiado
                              || algo raro pasa
                              nIncid = 040;
                              |HAZ graba_incid;
                         |FINSI;
                    |FINSI;
               |FINSI;

               || Ahora busco diferencias en la cuota a retener
               || lo saco de este proceso que esta excesivamente largo
               |HAZ DifCuota;
          |SINO;
               || no tengo nada en el historico el mes anterior
               || no tengo con que comparar, busco causa
               || nIncid
          |FINSI;
     |SINO;
          || enero, no tengo con que comparar
     |FINSI;

     |SI swDatosDefecto = 1; |Y nIncid ! 001; |Y nIncid ! 002; |Y nIncid ! 003;
          || estoy usando los datos personales de IRPF por defecto
          nIncid = 051;
          |HAZ graba_incid;
     |FINSI;

     |SI nIncid = 999;
          |HAZ graba_incid;
     |FINSI;

     || para cargarme las "F" que salieron y que en la emision actual
     || no vuelven a salir, las que he marcado como "X" en el primer
     || borrado

     |BUCLE borra_nomgirli_X;

     |HAZ CierraFich;
|FPRC;

|PROCESO CierraFich;
     |CIERRA #nomir003;
     |CIERRA #nomir009;
     |CIERRA #nomir013;
     |CIERRA #100;
     |CIERRA #labtraba;
     |CIERRA #labempre;
     |CIERRA #nomgirm2;
     |CIERRA #nomgirma;
     |CIERRA #nomhicca;
     |CIERRA #nomir012;
     |CIERRA #nomgirhi;
     |CIERRA #nomgirli;
     |CIERRA #nomgirca;
|FPRC;
