|FICHEROS;
     labempre;
     labtraba;
     labcentr;
     nomcenes;
     nombotra;

     nomvarca;
     nomvarcb;
     nomabsca;
     nomabsli;
     nomconca;
     nombotra;
     nombonif;
     nomboni3;
     gomdetjr;

     nomcalca;
     nomhicca;
     nomcalac;
     nomhisit;
     nomintal;

     nomcot01;
     nomcot02;
     nomcot03;
     nomcot04;
     nomcot21;
     nomcot09;

     nomcalcu;
|FIN;

|VARIABLES;
     &nEmp = 0;
     &Tra = 0;
     &nElAny = 0;
     &nElMes = 0;

     aAlfa = "";
     aRegimen = "";
     &aCCC = "";
     &aCCCReg = "";
     &aNaf = "";

     &fFechaIniPer = @;
     &fFechaFinPer = @;

     &enMes = 0;
     &enAny = 0;
     &enTipo = 0;

     nDiaFinMes = 0;
     fFecIniMes = @;
     fFecFinMes = @;

     nDesdeDia = 0;
     nHastaDia = 0;

     nDia = 0;
     FEstado = 0;
     nCodTra = 0;
     aCodIT = 0;
     nPropIT = 0;
     nPropERETP = 0;
     nCampo = 0;
     nCampo2 = 0;
     nNume = 0;
     nNume1 = 0;
     nNume2 = 0;
     nNume3 = 0;
     nDiaIniPer = 0;
     nDiaFinPer = 0;
     nTra = 0;
     aTipoIT = "";
     aClaveIT = "";
     nCampoIT = 0;
     nOrigenIT = 0;
     nNroFicha = 0;
     aContrato = "";
     nGrupoCot = 0;
     nCategoria = 0;
     aOcupacion = "";
     nHoras = 0;
     aAltaAfil = "";
     aMotivo = "";
     nAcumDiasEnf = 0;
     nLineaIT = 0;
     aRecaida = "";
     nDiasRecaida = 0;

     aBonif = "";
     nBonif = 0;
     aBonifBis = "";
     fFechaIniBon = @;
     fFechaFinBon = @;
     nFechaIniBon = 0;
     aDiaIniBon = ""; aMesIniBon = ""; aAnyIniBon = "";
     nDiaIniBon = 0; nMesIniBon = 0; nAnyIniBon = 0;
     nAnysTramo = 0;
     nMesesTramo = 0;
     nTiempoBon = 0;
     nTramo = 0;
     nCampoBon = 0;
     nCampoBonDef = 0;
     aBonifNue = "";
     aBonifExi = "";

     nCampoJor = 0;
     nJornada = 0;
     nEmpOri = 0;
     nTraOri = 0;
     aNif = "";
     nMes = 0;
     nAny = 0;
     aDia = "";
     aMes = "";
     aAny = "";
     aFecha = "";
     &eaFichTramos = "";
     fFechaAlta = @;
     fFechaBaja = @;

     nMesesBon = 0;
     nAnysBon = 0;
     nDiaFinBon = 0;
     nMesFinBon = 0;
     nAnyFinBon = 0;
     nMesIn = 0;
     nAnyIn = 0;
     nTopeMes = 0;
     aAlfa1 = "";
     aAlfa2 = "";
     aAlfa3 = "";
     &enSwPasada09 = 0;
     swExiste = 0;
     nTramoSigBon = 0;
     &runnom = "";

     swTrabNoCalc = 0;
     nEmpComp = 0;
     nTraComp = 0;
     swIntervalo = 0;
     swHayIntervalos = 0;
     nCampo1 = 0;
     nUltimaSit = 0;
     nNuevaSit = 0;
     aUltimoCCC = "";
     aCCCComp = "";
     swNoBorres = 0;
     &enAct89 = 0;
     &eaAct60 = "";
     &enAct92 = 0;
     swBon22 = 0;
     swDosBon = 0;
     aTipoITAnt = "";
     swSigue = 0;
     nDiasAntAcum = 0;
     nBuscaAny = 0;
|FIN;

|PROCESO BuscaRecaida;
     nDiasRecaida = 0;

     |ABRE #nomhisit;
     #nomhisit#0 = nEmp;
     #nomhisit#1 = nTra;
     #nomhisit#2 = nElAny;
     #nomhisit#3 = nElMes;
     #nomhisit#8 = nLineaIT;
     |LEE 000#nomhisit.=;
     |SI FSalida = 0;
          nDiasRecaida = #nomhisit#11;
     |FINSI;

     |CIERRA #nomhisit;
|FPRC;

|PROCESO BuscaClaveIT;
     aTipoIT = #nomcot21#1;
     nCampoIT = #nomcot21#5;
|FPRC;

|DEFBUCLE BuscaClaveIT;
     #nomcot21#2;
     ;
     aClaveIT, nOrigenIT;
     aClaveIT, nOrigenIT;
     ;
     NULL, BuscaClaveIT;
|FIN;

|PROCESO JornadasAgrCit;
     #gomdetjr#0 = nEmp;
     #gomdetjr#1 = nTra;
     #gomdetjr#2 = nElAny;
     #gomdetjr#3 = nElMes;
     |LEE 000#gomdetjr.=;
     |SI FSalida = 0;
          nCampoJor = nDia + 3;
          aAlfa = #gomdetjr#nCampoJor#;
          |SI #labtraba#42 = "E";
               |SI aAlfa = "X";
                    nJornada = 1;
               |FINSI;
               |SI aAlfa = "I";
                    nJornada = 2;
               |FINSI;
          |FINSI;
          |SI #labtraba#42 = "C"; |O #labtraba#42 = "F";
               |SI aAlfa = "D";
                    nJornada = 1;
               |FINSI;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO CambioSit;
     aAltaAfil = #labtraba#137;
     |SI aAltaAfil = "S";
          aMotivo = "AFI";
          |ACABA;
     |FINSI;

     aContrato = #labtraba#45;
     nGrupoCot = #labtraba#33;
     nCategoria = #labtraba#17;
     aOcupacion = #labtraba#204;
     nHoras = #labtraba#234;

     |SALVA_FICHA #labtraba;
     #labtraba#0 = #nomcot01#0;
     #labtraba#1 = #nomcot01#7;
     |LEE 000#labtraba.=;
     |SI FSalida = 0;
          || ha de existir

          |SI aContrato ! #labtraba#45;
               aMotivo = "CON";
          |FINSI;
          |SI nGrupoCot ! #labtraba#33;
               aMotivo = "COT";
          |FINSI;
          |SI aOcupacion ! #labtraba#204;
               aMotivo = "OCU";
          |FINSI;
          |SI nHoras ! #labtraba#234;
               aMotivo = "JOR";
          |FINSI;
          /*
          || cambio de categoria, lo grabo como CSA
          |SI nCategoria ! #labtraba#17;
               aMotivo = "CAT";
          |FINSI;
          */
          |SI aMotivo = "";
               aMotivo = "CSA";    || contrato SIN alta afiliacion
                                   || no hay que hacer tramos en fichero de
                                   || bases pero tengo que crearlo para la
                                   || nomina
          |FINSI;
     |FINSI;
     |REPON_FICHA #labtraba;
|FPRC;

|PROCESO BuscaBonifDiaAnt;
     |SALVA_FICHA #nomcot01;
     #nomcot01#0 = nEmp;
     #nomcot01#1 = nElAny;
     #nomcot01#2 = nElMes;
     #nomcot01#3 = enTipo;
     #nomcot01#4 = aCCCReg;
     #nomcot01#5 = aNaf;
     #nomcot01#6 = nDia - 1;
     |LEE 101#nomcot01.=;
     |SI FSalida = 0;
          |PARA nCampoBon = 22; |SI nCampoBon [ 25;
          |HACIENDO nCampoBon = nCampoBon + 1;
               aBonifExi = #nomcot01#nCampoBon# % 103;
               |SI aBonifExi = aBonifNue;
                    nCampoBonDef = nCampoBon;
               |FINSI;
          |SIGUE;
     |FINSI;
     |REPON_FICHA #nomcot01;
|FPRC;

|PROCESO DiaAnterior;
     |DDEFECTO #nomcot01;
     #nomcot01#0 = nEmp;
     #nomcot01#1 = nElAny;
     #nomcot01#2 = nElMes;
     #nomcot01#3 = enTipo;
     #nomcot01#4 = aCCCReg;
     #nomcot01#5 = aNaf;
     #nomcot01#6 = nDesdeDia - 1;
     |LEE 101#nomcot01.=;
     |SI FSalida = 0;
          |HAZ CambioSit;
          |SI aMotivo ! "";
               nNroFicha = #nomcot01#8 + 1
          |SINO;
               nNroFicha = 1;
          |FINSI;

          || para casos de ERE que empiezan y el dia anterior termino otro

          |SI #nomcot01#15 = 1;
               || tenia ERE el dia anterior
               |SI #nomcot01#21 = 0;
                    aTipoITAnt = "ERE";
               |SINO;
                    aTipoITAnt = "ETP";
               |FINSI;
          |FINSI;
     |SINO;
          || nNroFicha = 1;
          nNroFicha = nNroFicha + 1;
     |FINSI;
|FPRC;

|PROCESO nomcot01_rellena;
     swExiste = 1;
     aUltimoCCC = #nomcot01#4;
|FPRC;

|DEFBUCLE nomcot01_rellena;
     #nomcot01#1;
     ;
     || nEmp, nElAny, nElMes, enTipo, "               ", aNaf, nDia;
     || nEmp, nElAny, nElMes, enTipo, "zzzzzzzzzzzzzzz", aNaf, nDia;
     nEmp, nElAny, nElMes, enTipo, aCCCReg, aNaf, nDia;
     nEmp, nElAny, nElMes, enTipo, aCCCReg, aNaf, nDia;
     ;
     NULL, nomcot01_rellena;
|FIN;

|PROCESO Rellena;
     aUltimoCCC = "";

     aAlfa = 01;
     aAlfa = aAlfa % 102;
     nDesdeDia = aAlfa;

     aAlfa = nDiaFinMes;
     aAlfa = aAlfa % 102;
     nHastaDia = aAlfa;

     |PARA nDia = nDesdeDia; |SI nDia [ nHastaDia; |HACIENDO nDia = nDia + 1;
          swExiste = 0;
          |BUCLE nomcot01_rellena;

          |SI swExiste = 0;
               |DDEFECTO #nomcot01;
               #nomcot01#0 = nEmp;
               #nomcot01#1 = nElAny;
               #nomcot01#2 = nElMes;
               #nomcot01#3 = enTipo;
               || #nomcot01#4 = "";
               #nomcot01#4 = aUltimoCCC;
               #nomcot01#5 = aNaf;
               #nomcot01#6 = nDia;
               |LEE 101#nomcot01.=;
               |SI FSalida ! 0;
                    |GRABA 020#nomcot01.n;
               |FINSI;
               |LIBERA #nomcot01;
          |SINO;
               |SI swHayIntervalos = 1;
                    |DDEFECTO #nomcot01;
                    #nomcot01#0 = nEmp;
                    #nomcot01#1 = nElAny;
                    #nomcot01#2 = nElMes;
                    #nomcot01#3 = enTipo;
                    #nomcot01#4 = aUltimoCCC;
                    #nomcot01#5 = aNaf;
                    #nomcot01#6 = nDia;
                    |LEE 101#nomcot01.=;

                    |SI FSalida = 0;
                         |SI #nomcot01#29 ! "INT";
                              #nomcot01#8 = 0;
                              |GRABA 020#nomcot01.a;
                         |FINSI;
                    |FINSI;

                    |LIBERA #nomcot01;
               |FINSI;
          |FINSI;
     |SIGUE;
|FPRC;

|PROCESO GrabaSituacion;
     |SI nCodTra ! 0;
          aMotivo = "";
          || voy a grabar dias de alta del trabajador
          || compruebo si el dia anterior habia ya alguno de alta
          || querra decir que es otra ficha
          |HAZ DiaAnterior;
     |FINSI;

     aTipoITAnt = "";
     |SI aTipoIT = "ERE";
          |HAZ DiaAnterior;
     |FINSI;

     |PARA nDia = nDesdeDia; |SI nDia [ nHastaDia; |HACIENDO nDia = nDia + 1;
          |DDEFECTO #nomcot01;
          #nomcot01#0 = nEmp;
          #nomcot01#1 = nElAny;
          #nomcot01#2 = nElMes;
          #nomcot01#3 = enTipo;
          #nomcot01#4 = aCCCReg;
          #nomcot01#5 = aNaf;
          #nomcot01#6 = nDia;
          |LEE 101#nomcot01.=;
          FEstado = FSalida;

          |SI FSalida ! 0; |Y nCodTra = 0;
               || tiene que existir, no puede haber variacion o bonif
               || respecto a un periodo que no este de alta este nCodTra
               |ACABA;
          |FINSI;

          || vengo de DiasAlta
          |SI nCodTra ! 0;
               #nomcot01#8 = nNroFicha;

               |SI nNroFicha ! 0;
                    |SI #nomcot01#7 = 00000;
                         #nomcot01#7 = nCodTra;
                    |SINO;
                         |SI #nomcot01#26 = 00000;
                              #nomcot01#26 = nCodTra;
                         |SINO;
                              |SI #nomcot01#27 = 00000;
                                   #nomcot01#27 = nCodTra;
                              |SINO;
                                   |SI #nomcot01#28 = 00000;
                                        #nomcot01#28 = nCodTra;
                                   |FINSI;
                              |FINSI;
                         |FINSI;
                    |FINSI;
               |FINSI;

               |SI nNroFicha > 1; |Y nDia = nDesdeDia;
                    || si no es la primera ficha del mes, es decir que ha
                    || habido algun cambio de codigo de trabajador y
                    || el cambio supone tramo nuevo, grabo el motivo
                    #nomcot01#29 = aMotivo;
               |FINSI;

               nJornada = 0;
               |SI #labtraba#42 = "E";
                    |SI nNroFicha ! 0;
                         |HAZ JornadasAgrCit;
                         #nomcot01#20 = nJornada;
                    |FINSI;
               |FINSI;
               |SI #labtraba#42 = "C"; |O #labtraba#42 = "F";
                    |SI nNroFicha ! 0;
                         |HAZ JornadasAgrCit;
                         #nomcot01#19 = nJornada;
                    |FINSI;
               |FINSI;
          |FINSI;
          || vengo de DiasIT o DiasIT_2
          |SI aTipoIT ! "";
               #nomcot01#nCampoIT# = 1;
               |SI aTipoIT =  "ERE";
                    || un ERE que venia de otro ERE el dia anterior
                    || es decir ... ERE del 1 al 15 y ERE del 15 al 30
                    || quiere decir que es OTRO ere que llevara una Base
                    || diferente (probablemente), con lo que he de hacer
                    || otro tramo
                    |SI aTipoITAnt = "ERE"; |Y nPropIT = 0;
                         || un ERE que sigue con otro ERE
                         #nomcot01#nCampoIT# = #nomcot01#nCampoIT# + 1;
                    |FINSI;
                    |SI aTipoITAnt = "ETP"; |Y nPropIT ! 0;
                         || un ETP que sigue con otro ETP
                         #nomcot01#nCampoIT# = #nomcot01#nCampoIT# + 1;
                    |FINSI;
               |FINSI;
               |SI nPropIT ! 0;
                    #nomcot01#21 = nPropIT;
               |FINSI;
               |SI nPropERETP ! 0;
                    #nomcot01#31 = nPropERETP;
               |FINSI;
               |SI aTipoIT = "ENF";
                    nAcumDiasEnf = nDia - nDesdeDia + 1;
                    ||SI nLineaIT = 1;
                         || nAcumDiasEnf = nAcumDiasEnf + #labtraba#89;
                    ||FINSI;
                    nAcumDiasEnf = nAcumDiasEnf + nDiasAntAcum;
                    |SI aRecaida = "S";
                         |HAZ BuscaRecaida;
                         nAcumDiasEnf = nAcumDiasEnf + nDiasRecaida;
                    |FINSI;
                    |SI nAcumDiasEnf ] 16; |Y #labtraba#241 ! "X";
                         #nomcot01#nCampoIT# = 2;
                    |FINSI;
                    || nuevo tramo para identificar la enfermedad a partir del
                    || dia 21, cuando empieza a pagar la prestacion la Seg. Soc.
                    || al 75%
                    swSigue = 0;
                    |SI nElAny ] 2020
                         |SI nElAny = 2020; |Y nElMes ] 5;
                              swSigue = 1;
                         |FINSI;
                         |SI nElAny ] 2021
                              swSigue = 1;
                         |FINSI;
                    |FINSI;
                    |SI swSigue = 1;
                         |SI nAcumDiasEnf ] 21; |Y #labtraba#241 ! "X";
                              #nomcot01#nCampoIT# = 3;
                         |FINSI;
                    |FINSI;
                    |SI #labtraba#241 = "N"; |Y #labtraba#138 = "N"
                         #nomcot01#nCampoIT# = 9;
                    |FINSI;
                    #nomcot01#30 = nAcumDiasEnf;
               |FINSI;
               |SI aTipoIT = "ACC";
                    |SI #labtraba#241 = "N"; |Y #labtraba#138 = "N"
                         #nomcot01#nCampoIT# = 9;
                    |FINSI;
               |FINSI;
               |SI aTipoIT = "MAT"; |O aTipoIT = "PAT";
                    |SI #labtraba#129 = "T";
                         #nomconca#0 = #labtraba#87;
                         |LEE 000#nomconca.=;
                         |SI FSalida = 0;
                              nHoras = #nomconca#162;
                              |SI nHoras = 0; nHoras = 40; |FINSI;
                              nPropIT = #labtraba#234 / nHoras;
                              nPropIT = nPropIT * 100;
                              #nomcot01#21 = nPropIT;
                         |FINSI;
                    |FINSI;
               |FINSI;
          |FINSI;

          || vengo de Bonificaciones

          |SI aBonif ! "";
               nCampoBonDef = 0;
               aBonifNue = aBonif % 103;

               || primero busco si ya existia esta bonificacion en alguno
               || el dia anterior, si es asi, la continuo en la misma columna

               |HAZ BuscaBonifDiaAnt;

               || si no la he encontrado, busco si ya existe esta
               || bonificacion en alguno de sus dias entre los campos
               |SI nCampoBonDef = 0;
                    |PARA nCampoBon = 22; |SI nCampoBon [ 25;
                    |HACIENDO nCampoBon = nCampoBon + 1;
                         aBonifExi = #nomcot01#nCampoBon# % 103;
                         |SI aBonifExi = aBonifNue;
                              nCampoBonDef = nCampoBon;
                         |FINSI;
                    |SIGUE;
               |FINSI;

               || si no existe, la asigno en el primer campo bonif en blanco

               |SI nCampoBonDef = 0;
                    nNume = 22;
                    |SI swDosBon = 1;
                         nNume = 23;
                    |FINSI;
                    |PARA nCampoBon = nNume; |SI nCampoBon [ 25;
                    |HACIENDO nCampoBon = nCampoBon + 1;
                         aAlfa = #nomcot01#nCampoBon#;
                         |QBF aAlfa;
                         |SI aAlfa = "";
                              nCampoBonDef = nCampoBon;
                              |SAL;
                         |FINSI;
                    |SIGUE;
               |FINSI;

               #nomcot01#nCampoBonDef# = aBonif;

               || para marcar la bonificacion

               |SI nCampoBonDef = 22; swBon22 = 1; |FINSI;
          |FINSI;

          || vengo de los intervalos de dias de alta (nomintal)

          |SI swIntervalo = 1;
               aAlfa = fFechaIniPer;
               aAlfa = aAlfa % 102;
               nNume = aAlfa;
               |SI nDia = nDesdeDia;         || primer dia del intervalo
                    #nomcot01#29 = "INT";    || grabo situacion cambio INT
                    |SI nDia ! nNume;
                         || si no es el tramo que incluye el
                         || primer dia del periodo, busco el numero
                         || de la situacion anterior, para asi aumentar uno
                         |SALVA_FICHA #nomcot01;
                         #nomcot01#6 = nDia - 1;
                         |LEE 000#nomcot01.=;
                         |SI FSalida = 0;
                              nUltimaSit = #nomcot01#8;
                         |FINSI;
                         |REPON_FICHA #nomcot01;
                         nNuevaSit = nUltimaSit + 1;
                         #nomcot01#8 = nNuevaSit;
                    |SINO;
                         || si lo es, esta sera la situacion en el intervalo
                         nNuevaSit = #nomcot01#8;
                    |FINSI;
               |SINO;
                    #nomcot01#8 = nNuevaSit;
                    #nomcot01#29 = "INT";    || grabo situacion cambio INT
                                             || en el resto de dias tambien
               |FINSI;
          |FINSI;

          |SI FEstado = 0;
               |GRABA 020#nomcot01.a;
          |SINO;
               |GRABA 020#nomcot01.n;
          |FINSI;
     |SIGUE;
|FPRC;

|PROCESO GrabaBonif;
     nBonif = aBonif;
     |SI fFechaIniBon [ fFechaFinPer; |Y fFechaFinBon ] fFechaIniPer;
          || bonificacion durante el periodo
          nDesdeDia = nDiaIniPer;
          |SI fFechaIniBon ] fFechaIniPer;
               aAlfa = fFechaIniBon;
               aAlfa = aAlfa % 102;
               nDesdeDia = aAlfa;
          |FINSI;
          nHastaDia = nDiaFinPer;
          |SI fFechaFinBon [ fFechaFinPer;
               aAlfa = fFechaFinBon;
               aAlfa = aAlfa % 102;
               nHastaDia = aAlfa;
          |FINSI;

          |HAZ GrabaSituacion;
          aBonif = "";
     |FINSI;
|FPRC;

|PROCESO GeneraTramos;
     |SI nBonif [ 99; |O nBonif ] 500;
          fFechaIniBon = #labtraba#40;
          ||fFechaFinBon = #labtraba#41; || la tengo ya calculada
     |FINSI;
     |SI nBonif ] 100; |Y nBonif [ 499;
          fFechaIniBon = #nombotra#4;
          fFechaFinBon = #nombotra#5;
     |FINSI;

     aAlfa = fFechaIniBon;
     aDiaIniBon = aAlfa % 102;
     aMesIniBon = aAlfa % 402;
     aAnyIniBon = aAlfa % -104;
     nMesIniBon = aMesIniBon;
     nAnyIniBon = aAnyIniBon;
     |SI nMesesTramo ! 0;
          nMesIniBon = nMesIniBon + nMesesTramo;
          |SI nMesIniBon > 12;
               nMesIniBon = nMesIniBon - 12;
               nAnyIniBon = nAnyIniBon + 1;
          |FINSI;
     |FINSI;
     |SI nAnysTramo ! 0;
          nAnyIniBon = nAnyIniBon + nAnysTramo;
     |FINSI;
     aMesIniBon = nMesIniBon; |QBF aMesIniBon; aMesIniBon = ("00" + aMesIniBon) % -102;
     aAnyIniBon = nAnyIniBon;
     aAlfa = aDiaIniBon + "." + aMesIniBon + "." + aAnyIniBon;
     fFechaIniBon = aAlfa;
     |HAZ GrabaBonif;
|FPRC;

|PROCESO TramosBonif;
     || ahora compruebo si esta en periodo de cambio de tramo
     |SI nBonif [ 99; |O nBonif ] 500;
          #nombonif#0 = nBonif;
          |LEE 000#nombonif.=;
          |SI FSalida = 0; |Y #nombonif#27 = "S";
               nTiempoBon = 0;
               nMesesTramo = 0;
               |PARA nTramo = 14; |SI nTramo [ 19; |HACIENDO nTramo = nTramo + 1;
                    nTramoSigBon = nTramo + 1;
                    |SI #nombonif#nTramo# ! 999; |Y #nombonif#nTramo# ! 0;
                    |Y #nombonif#nTramoSigBon# ! 999; |Y #nombonif#nTramoSigBon# ! 0;
                         nTiempoBon = nTiempoBon + #nombonif#nTramo#;
                         nAnysTramo = nTiempoBon / 12;
                         nMesesTramo = nTiempoBon $ 12;

                         aBonif = nBonif;
                         |QBF aBonif;
                         aBonif = ("000" + aBonif) % -103;
                         |SI nTramo = 14; aBonif = aBonif + "A"; |FINSI;
                         |SI nTramo = 15; aBonif = aBonif + "B"; |FINSI;
                         |SI nTramo = 16; aBonif = aBonif + "C"; |FINSI;
                         |SI nTramo = 17; aBonif = aBonif + "D"; |FINSI;
                         |SI nTramo = 18; aBonif = aBonif + "E"; |FINSI;
                         |SI nTramo = 19; aBonif = aBonif + "F"; |FINSI;

                         |HAZ GeneraTramos;
                    |FINSI;
               |SIGUE;
          |FINSI;
     |FINSI;
     |SI nBonif ] 100; |Y nBonif [ 499;
          #nomboni3#0 = nBonif;
          |LEE 000#nomboni3.=;
          |SI FSalida = 0; |Y #nomboni3#11 = "S";
               |SI #nomboni3#6 ! 0; |Y #nomboni3#5 ! #nomboni3#6;
                    nAnysTramo = 1;
                    nMesesTramo = 0;

                    aBonif = nBonif;
                    |QBF aBonif;
                    aBonif = ("000" + aBonif) % -103;
                    aBonif = aBonif + "A";

                    |HAZ GeneraTramos;
               |FINSI;
               |SI #nomboni3#7 ! 0; |Y #nomboni3#6 ! #nomboni3#7;
                    nAnysTramo = 2;
                    nMesesTramo = 0;

                    aBonif = nBonif;
                    |QBF aBonif;
                    aBonif = ("000" + aBonif) % -103;
                    aBonif = aBonif + "B";

                    |HAZ GeneraTramos;
               |FINSI;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO BonFomento;
     #nomboni3#0 = #nombotra#2;
     |LEE 000#nomboni3.=;
     |SI FSalida = 0; |Y #nomboni3#11 = "S";
          aBonif = #nombotra#2;
          fFechaIniBon = #nombotra#4;
          fFechaFinBon = #nombotra#5;
          |HAZ GrabaBonif;
          |HAZ TramosBonif;
     |FINSI;
|FPRC;

|DEFBUCLE BonFomento;
    #nombotra#1;
    ;
    #labtraba#0, #labtraba#1, 001;
    #labtraba#0, #labtraba#1, 999;
    ;
    NULL, BonFomento;
|FIN;

|PROCESO Bonifica;
     || primero las del mantinimiento de toda la vida
     aBonif = "";
     nBonif = 0;

     swBon22 = 0;
     |SI #labtraba#39 ! 000;
          #nombonif#0 = #labtraba#39;
          |LEE 000#nombonif.=;
          |SI FSalida = 0; |Y #nombonif#27 = "S";
               aBonif = #labtraba#39;
               |QBF aBonif;
               aBonif = ("000" + aBonif) % -103;
               |SI aBonif ! "000";
                    aAlfa = #labtraba#40;
                    |QBF aAlfa;
                    fFechaIniBon = aAlfa;
                    aAlfa = #labtraba#41;
                    |QBF aAlfa;
                    |SI aAlfa ! ""; |Y aAlfa ! "..........";
                         fFechaFinBon = #labtraba#41;
                    |SINO;
                         || todo esto es para hallar la fecha de fin de bonificacion
                         || si tenemos una bonificacion que esta limitada
                         || en el tiempo

                         nMesesBon = 0;
                         |PARA nTramo = 14; |SI nTramo [ 19; |HACIENDO nTramo = nTramo + 1;
                              |SI #nombonif#nTramo# ! 999; |Y #nombonif#nTramo# ! 0;
                                   nMesesBon = nMesesBon + #nombonif#nTramo#;
                              |FINSI;
                         |SIGUE;

                         |SI nMesesBon ! 0;
                              aAlfa = fFechaIniBon;
                              aDiaIniBon = aAlfa % 102;
                              aMesIniBon = aAlfa % 402;
                              aAnyIniBon = aAlfa % -104;
                              nDiaIniBon = aDiaIniBon;
                              nMesIniBon = aMesIniBon;
                              nAnyIniBon = aAnyIniBon;

                              nAnysBon = nMesesBon / 12;
                              nAnysBon = (nAnysBon - 0.5) ! 0;
                              nMesesBon = nMesesBon $ 12;

                              nMesFinBon = nMesIniBon + nMesesBon;
                              nAnyFinBon = nAnyIniBon + nAnysBon;
                              |SI nMesFinBon > 12;
                                   nMesFinBon = nMesFinBon - 12;
                                   nAnyFinBon = nAnyFinBon + 1;
                              |FINSI;

                              nDiaFinBon = nDiaIniBon - 1;
                              |SI nDiaFinBon = 0;
                                   nMesFinBon = nMesFinBon - 1;
                                   |SI nMesFinBon = 0;
                                        nAnyFinBon = nAnyFinBon - 1;
                                        nMesFinBon = 12;
                                   |FINSI;
                                   nMesIn = nMesFinBon;
                                   nAnyIn = nAnyFinBon;
                                   |HAZ topemes_cot;
                                   nDiaFinBon = nTopeMes;
                              |FINSI;
                              aAlfa1 = nDiaFinBon; |QBF aAlfa1; aAlfa1 = ("00" + aAlfa1) % -102;
                              aAlfa2 = nMesFinBon; |QBF aAlfa2; aAlfa2 = ("00" + aAlfa2) % -102;
                              aAlfa3 = nAnyFinBon;
                              aAlfa = aAlfa1 + "." + aAlfa2 + "." + aAlfa3;
                              fFechaFinBon = aAlfa;
                         |SINO;
                              fFechaFinBon = "31.12.2099";
                         |FINSI;
                    |FINSI;
                    |HAZ GrabaBonif;
                    |HAZ TramosBonif;
               |FINSI;
          |FINSI;
     |FINSI;

     || Ahora las de Fomento de Empleo

     swDosBon = 0;
     |SI #labtraba#121 = "S";
          |SI swBon22 = 1;
               || hay una bonificacion de las del mant. bonificaciones
               || ojo, que entonces la de fomento de empleo ha de estar en la
               || siguiente columna
               swDosBon = 1;
          |FINSI;
          |BUCLE BonFomento;
     |FINSI;
|FPRC;

|PROCESO nomabsli;
     nOrigenIT = 2;
     aClaveIT = #nomabsli#4;

     aTipoIT = "";
     nCampoIT = 0;

     nPropIT = 0;
     |SI #nomabsli#6 ! 0;
          nPropIT = #nomabsli#6 / #nomabsca#2;
          nPropIT = (nPropIT * 100) ! 2;
          |SI nPropIT = 100;
               nPropIT = 0;
          |FINSI;
     |FINSI;

     |BUCLE BuscaClaveIT;

     |SI aTipoIT ! "";
          aAlfa = #nomabsli#2;
          aAlfa = aAlfa % 102;
          nDesdeDia = aAlfa;

          aAlfa = #nomabsli#3;
          aAlfa = aAlfa % 102;
          nHastaDia = aAlfa;

          |HAZ GrabaSituacion;
     |FINSI;

     aTipoIT = "";
     nCampoIT = 0;

     nPropIT = 0;
|FPRC;

|DEFBUCLE nomabsli;
     #nomabsli#1;
     ;
     nEmp, nTra, fFechaIniPer;
     nEmp, nTra, fFechaFinPer;
     ;
     NULL, nomabsli;
|FIN;

|PROCESO DiasIT_2;
     #nomabsca#0 = nEmp;
     #nomabsca#1 = nTra;
     |LEE 000#nomabsca.=;
     |SI FSalida = 0;
          |BUCLE nomabsli;
     |FINSI;
|FPRC;

|PROCESO DiasMasERE;
     nDesdeDia = #nomvarcb#5;
     nHastaDia = #nomvarcb#6;
     nOrigenIT = 1;
     aClaveIT = #nomvarcb#7;

     aTipoIT = "";
     nCampoIT = 0;
     |BUCLE BuscaClaveIT;

     |SI aTipoIT ! "";
          nPropIT = 0;
          |HAZ GrabaSituacion;
     |FINSI;

     aTipoIT = "";
     nCampoIT = 0;
     nPropIT = 0;
|FPRC;

|DEFBUCLE DiasMasERE;
     #nomvarcb#1;
     ;
     #labtraba#0, #labtraba#1, nElMes, 0, nBuscaAny, 01;
     #labtraba#0, #labtraba#1, nElMes, 0, nBuscaAny, 31;
     ;
     NULL, DiasMasERE;
|FIN;

|PROCESO DiasIT;
     |DDEFECTO #nomvarca;

     #nomvarca#0 = #labtraba#0;
     #nomvarca#1 = #labtraba#1;
     #nomvarca#2 = nElMes;
     #nomvarca#3 = 0;
     |LEE 000#nomvarca.=;

     || carga de arrastre en ITs

     |SI runnom = "nomcalf1";
          #labtraba#89 = enAct89;
          #labtraba#60 = eaAct60;
          #labtraba#92 = enAct92;
     |FINSI;

     nDiasAntAcum = 0;
     |SI #labtraba#89 ! 0;
          |SI #nomvarca#12 = " "; |O #nomvarca#12 = #labtraba#60;
               #nomvarca#12 = #labtraba#60;
               |SI #nomvarca#12 = "E";
                    nDiasAntAcum = #labtraba#89;
               |FINSI;
          |FINSI;

          || lo siguiente es para los casos de ERE a TP, en que ademas estoy
          || de IT y la IT viene del mes pasado y continua, y no se ha puesto
          || desde 0 hasta 0 la IT, para que el programa lo haga solo
          |SI #nomvarca#12 = "D"; |Y #nomvarca#47 ! "100";
               |SI #nomvarca#13 = " ";
                    #nomvarca#13 = #labtraba#60;
                    |SI #nomvarca#13 = "E";
                         nDiasAntAcum = #labtraba#89;
                    |FINSI;
               |FINSI;
          |FINSI;
          |SI #nomvarca#12 = "D"; |Y #nomvarca#13 = "D"; |Y #nomvarca#48 ! 100;
               |SI #nomvarca#14 = " ";
                    #nomvarca#14 = #labtraba#60;
                    |SI #nomvarca#14 = "E";
                         nDiasAntAcum = #labtraba#89;
                    |FINSI;
               |FINSI;
          |FINSI;
          |SI #nomvarca#12 = "D"; |Y #nomvarca#13 = "D";
          |Y #nomvarca#14 = "D"; |Y #nomvarca#49 ! 100;
               |SI #nomvarca#15 = " ";
                    #nomvarca#15 = #labtraba#60;
                    |SI #nomvarca#15 = "E";
                         nDiasAntAcum = #labtraba#89;
                    |FINSI;
               |FINSI;
          |FINSI;
     |FINSI;
     |SI #labtraba#92 ! 0;
          |SI #nomvarca#12 = " ";
               #nomvarca#12 = "A";
          |FINSI;

          || lo siguiente es para los casos de ERE a TP, en que ademas estoy
          || de IT y la IT viene del mes pasado y continua, y no se ha puesto
          || desde 0 hasta 0 la IT, para que el programa lo haga solo
          |SI #nomvarca#12 = "D"; |Y #nomvarca#47 ! "100";
               |SI #nomvarca#13 = " ";
                    #nomvarca#13 = "A";
               |FINSI;
          |FINSI;
          |SI #nomvarca#12 = "D";
          |Y #nomvarca#13 = "D"; |Y #nomvarca#48 ! 100;
               |SI #nomvarca#14 = " ";
                    #nomvarca#14 = "A";
               |FINSI;
          |FINSI;
          |SI #nomvarca#12 = "D"; |Y #nomvarca#13 = "D";
          |Y #nomvarca#14 = "D"; |Y #nomvarca#49 ! 100;
               |SI #nomvarca#15 = " ";
                    #nomvarca#15 = "A";
               |FINSI;
          |FINSI;
     |FINSI;

     |PARA nCampo = 12; |SI nCampo [ 15; |HACIENDO nCampo = nCampo + 1;
          |SI #nomvarca#nCampo# ! " ";
               nLineaIT = nCampo - 11;
               nCampo2 = nCampo - 8;
               nNume = #nomvarca#nCampo2#;
               |SI nNume = 0;
                    nNume = nDiaIniPer;
               |FINSI;
               nDesdeDia = nNume;

               nCampo2 = nCampo - 4;
               nNume = #nomvarca#nCampo2#;
               |SI nNume = 0;
                    nNume = nDiaFinPer;
               |FINSI;
               nHastaDia = nNume;

               nCampo2 = nCampo + 23;
               aRecaida = #nomvarca#nCampo2#;
               || recaidas en ENF que vienen de mes anterior
               |SI nLineaIT = 1; |Y #labtraba#123 = "S";
                    aRecaida = "S";
               |FINSI;

               nOrigenIT = 1;
               aClaveIT = #nomvarca#nCampo#;

               aTipoIT = "";
               nCampoIT = 0;
               |BUCLE BuscaClaveIT;

               |SI aTipoIT ! "";
                    nPropIT = 0;
                    nPropERETP = 0;
                    |SI aTipoIT = "ERE";
                         nCampo2 = nCampo + 35;
                         nPropIT = #nomvarca#nCampo2#
                         nPropERETP = #nomvarca#nCampo2#
                         |SI nPropIT = 100;
                              nPropIT = 0;
                         |FINSI;
                         |SI nPropERETP = 100;
                              nPropERETP = 0;
                         |FINSI;
                    |FINSI;

                    |HAZ GrabaSituacion;
                    |SI aTipoIT = "ENF";
                         nDiasAntAcum = 0;   || solo lo tengo que usar para la
                                             || primera enfermedad que venga
                                             || el resto ya no
                    |FINSI;
               |FINSI;

               aTipoIT = "";
               nCampoIT = 0;
               nPropIT = 0;
               nPropERETP = 0;
          |FINSI;
     |SIGUE;
|FPRC;

|PROCESO Intervalos;
     |PARA nCampo = 4; |SI nCampo [ 42; |HACIENDO nCampo = nCampo + 2;
          nCampo1 = nCampo + 1;
          |SI #nomintal#nCampo# ! 0; |Y #nomintal#nCampo1# ! 0;
               nDesdeDia = #nomintal#nCampo#;
               nHastaDia = #nomintal#nCampo1#;
               |HAZ GrabaSituacion;
          |SINO;
               |SAL;
          |FINSI;
     |SIGUE;
|FPRC;

|PROCESO DiasAlta;
     aAlfa = fFechaIniPer;
     aAlfa = aAlfa % 102;
     nDiaIniPer = aAlfa;           || dia de inicio del periodo
     nDesdeDia = aAlfa;            || dia de inicio del mes

     aAlfa = fFechaFinPer;
     aAlfa = aAlfa % 102;
     nDiaFinPer = aAlfa;           || dia de final del periodo
     nHastaDia = aAlfa;

     nCodTra = nTra;
     |HAZ GrabaSituacion;
     nCodTra = 0;
|FPRC;

|PROCESO nomcot09;
     #labtraba#0 = #nomcot09#0;
     #labtraba#1 = #nomcot09#1;
     |LEE 000#labtraba.=;

     || variables de la clave
     |HAZ Variables;

     || compruebo fechas de alta y baja
     enMes = nElMes;
     enAny = nElAny;
     |HAZ CompAltaTrab_plb;

     aTipoIT = "";
     aBonif = "";
     nBonif = 0;

     |HAZ DiasAlta;

     swIntervalo = 1;
     |ABRE #nomintal;
     #nomintal#0 = #labtraba#0;
     #nomintal#1 = #labtraba#1;
     #nomintal#2 = nElAny;
     #nomintal#3 = nElMes;
     |LEE 000#nomintal.=;
     |SI FSalida = 0;
          |HAZ Intervalos;
     |FINSI;
     |CIERRA #nomintal;
     swIntervalo = 0;

     |HAZ DiasIT;

     nBuscaAny = 0000;
     |BUCLE DiasMasERE;
     nBuscaAny = nElAny;
     |BUCLE DiasMasERE;

     |HAZ DiasIT_2;

     |HAZ Bonifica;
|FPRC;

|DEFBUCLE nomcot09;
     #nomcot09#2;
     ;
     aNif, "01.01.1900";
     aNif, "31.12.2999";
     ;
     NULL, nomcot09;
|FIN;

|PROCESO Comprobaciones;
     |SI nEmpOri = nEmpComp; |Y nTraOri = nTraComp;
          || el trabajador que estoy calculando tengo que borra todo lo que
          || tenga siempre, ya que este lo voy a calcular siempre, si no,
          || si tengo en el mensual el finiquito, no borro los tramos
          || en este no hago ninguna comprobacion
          |ACABA;
     |FINSI;

     #nomcalca#0 = nEmpComp;            || nomina bloqueada, ni calcules
     #nomcalca#1 = nTraComp;            || ni borres tramos
     #nomcalca#2 = nElMes;
     #nomcalca#3 = 0;
     |LEE 000#nomcalca.=;
     |SI FSalida = 0;
          |SI #nomcalca#145 = "S";
               swTrabNoCalc = 1;
          |FINSI;
          |SI #nomcalcu#11 = "N";                      || parametro "en baja" NO
               aAlfa2 = #nomcalca#5 % 402;
               aAlfa3 = #nomcalca#5 % -104;
               nNume2 = aAlfa2;
               nNume3 = aAlfa3;
               |SI nNume2 = nElMes; |Y nNume3 = nElAny;     || soy baja este mes
                    swTrabNoCalc = 1;
               |FINSI;
          |FINSI;
     |FINSI;

     #nomcalca#3 = 2;                   || nomina con finiquito ya calculado,
     |LEE 000#nomcalca.=;               || ni calcules ni borres tramos
     |SI FSalida = 0;
          swTrabNoCalc = 1;
     |FINSI;

     #nomhicca#0 = nEmpComp;            || nomina en historico, ni calcules
     #nomhicca#1 = nTraComp;            || ni borres tramos
     #nomhicca#66 = nElAny - 2000;
     #nomhicca#2 = nElMes;
     #nomhicca#3 = 0;
     |LEE 000#nomhicca.=;
     |SI FSalida = 0;
          swTrabNoCalc = 1;
     |FINSI;

|FPRC;

|PROCESO labtraba_previo;
     || para saber el CCC de este trabajador
     || en CCCReg esta el CCC del principal
     |HAZ ConstruyeCCC;

     |SI nEmpOri = #labtraba#0;
     |Y #labtraba#44 = "A";
     |Y aCCC = aCCCReg;       || solo del mismo CCC
          || sigue
     |SINO;
          |ACABA;
     |FINSI;

     swTrabNoCalc = 0;
     nEmpComp = #labtraba#0;
     nTraComp = #labtraba#1;
     |HAZ Comprobaciones;

     |SI swTrabNoCalc = 1;
          |ACABA;
     |FINSI;
     aFecha = "";

     #nomvarca#0 = #labtraba#0;
     #nomvarca#1 = #labtraba#1;
     #nomvarca#2 = nElMes;
     #nomvarca#3 = 0;
     |LEE 000#nomvarca.=;
     |SI FSalida = 0;
          aFecha = #nomvarca#16;
          |QBF aFecha;
     |FINSI;

     |SI aFecha ! ""; |Y aFecha ! "..........";
          fFechaBaja = aFecha;
     |SINO;
          aFecha = #labtraba#37;
          |QBF aFecha;
          |SI aFecha ! ""; |Y aFecha ! "..........";
               fFechaBaja = aFecha;
          |FINSI;
     |FINSI;

     |SI aFecha = ""; |O aFecha = "..........";
          fFechaBaja = fFecFinMes;
     |FINSI;

     aFecha = #labtraba#36
     fFechaAlta = aFecha;

     |SI fFechaAlta [ fFecFinMes; |Y fFechaBaja ] fFecIniMes;
          |DDEFECTO #nomcot09;
          #nomcot09#0 = #labtraba#0;
          #nomcot09#1 = #labtraba#1;
          |LEE 101#nomcot09.=;
          FEstado = FSalida;
          #nomcot09#2 = #labtraba#7;
          #nomcot09#3 = #labtraba#36;
          |SI nEmpOri = #labtraba#0; |Y nTraOri = #labtraba#1;
               #nomcot09#4 = "S";
          |FINSI;
          |SI FEstado ! 0;
               |GRABA 020#nomcot09.n;
          |FINSI;
          |LIBERA #nomcot09;
     |FINSI;
|FPRC;
/*
|DEFBUCLE labtraba_previo;
     #labtraba#2;
     0, 44;
     aNif, nEmpOri, "A";
     aNif, nEmpOri, "A";
     ;
     NULL, labtraba_previo;
|FIN;
*/

|DEFBUCLE labtraba_previo;
     #labtraba#1;
     7, 44;
     nEmpOri, 00001, aNif, "A";
     nEmpOri, 99999, aNif, "A";
     ;
     NULL, labtraba_previo;
|FIN;

|PROCESO CompPrevia;
     swNoBorres = 0;

     |QBF aCCCComp;
     |SI aCCCComp ! aCCCReg; |Y aCCCComp ! "";
          || no borres si es distinto CCC
          || si es dia sin trabajar, em tonces si

          swNoBorres = 1;
          |SI nEmpComp = nEmpOri; |Y nTraComp = nTraOri;
               || mismo codigo de trabajador pero con un CCC distinto !!!
               || hay que borrarlo, algun resto de algo mal calculado

               swNoBorres = 0;
          |FINSI;
     |FINSI;

     || tengo que mirar tambien que no sea un trabajador que he calculado
     || en este mismo calculo!!!!

     #nomcot09#0 = nEmpComp;
     #nomcot09#1 = nTraComp;
     |LEE 000#nomcot09.=;
     |SI FSalida = 0;
          |SI #nomcot09#4 = "S";
               swNoBorres = 1;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO BorraPrevioBon;
     nEmpComp = #nomcot04#0;
     nTraComp = #nomcot04#7;
     aCCCComp = #nomcot04#4;

     |HAZ CompPrevia;    || comprueba si el trabajador ha de borrarse o no

     |SI swNoBorres = 1;
          |ACABA;
     |FINSI;

     swTrabNoCalc = 0;
     |HAZ Comprobaciones;

     |SI swTrabNoCalc = 0;
          |BORRA 020#nomcot04.a;
          |LIBERA #nomcot04;
     |FINSI;
|FPRC;

|DEFBUCLE BorraPrevioBon;
     #nomcot04#1;
     ;
     || Ojo que aqui no meto aun el trabajador en la clave, me hara falta despues
     nEmp, nElAny, nElMes, enTipo, "             ", aNaf, 01, 001;
     nEmp, nElAny, nElMes, enTipo, "zzzzzzzzzzzzz", aNaf, 31, 999;
     || nEmp, nElAny, nElMes, enTipo, aCCCReg, aNaf, 01, 001;
     || nEmp, nElAny, nElMes, enTipo, aCCCReg, aNaf, 31, 999;
     be;
     NULL, BorraPrevioBon;
|FIN;

|PROCESO BorraPrevioCal;
     nEmpComp = #nomcot03#0;
     nTraComp = #nomcot03#7;
     aCCCComp = #nomcot03#4;

     |HAZ CompPrevia;    || comprueba si el trabajador ha de borrarse o no

     |SI swNoBorres = 1;
          |ACABA;
     |FINSI;

     swTrabNoCalc = 0;
     |HAZ Comprobaciones;     || comprueba si el trabajador se va a calcular
                              || en este calculo o por cualquier motivo no lo
                              || voy a calcular, si es asi, no he de borrar
     |SI swTrabNoCalc = 0;
          |BORRA 020#nomcot03.a;
          |LIBERA #nomcot03;
     |FINSI;
|FPRC;

|DEFBUCLE BorraPrevioCal;
     #nomcot03#1;
     ;
     nEmp, nElAny, nElMes, enTipo, "             ", aNaf, 01, 00001, 001;
     nEmp, nElAny, nElMes, enTipo, "zzzzzzzzzzzzz", aNaf, 31, 99999, 999;
     || nEmp, nElAny, nElMes, enTipo, aCCCReg, aNaf, 01, 00001, 001;
     || nEmp, nElAny, nElMes, enTipo, aCCCReg, aNaf, 31, 99999, 999;
     be;
     NULL, BorraPrevioCal;
|FIN;

|PROCESO BorraPrevioTra;
     nEmpComp = #nomcot02#0;
     nTraComp = #nomcot02#7;
     aCCCComp = #nomcot02#4;

     |HAZ CompPrevia;    || comprueba si el trabajador ha de borrarse o no

     |SI swNoBorres = 1;
          |ACABA;
     |FINSI;

     swTrabNoCalc = 0;
     |HAZ Comprobaciones;

     |SI swTrabNoCalc = 0;
          |BORRA 020#nomcot02.a;
          |LIBERA #nomcot02;
     |FINSI;
|FPRC;

|DEFBUCLE BorraPrevioTra;
     #nomcot02#1;
     ;
     nEmp, nElAny, nElMes, enTipo, "             ", aNaf, 01, 00001;
     nEmp, nElAny, nElMes, enTipo, "zzzzzzzzzzzzz", aNaf, 31, 99999;
     || nEmp, nElAny, nElMes, enTipo, aCCCReg, aNaf, 01, 00001;
     || nEmp, nElAny, nElMes, enTipo, aCCCReg, aNaf, 31, 99999;
     be;
     NULL, BorraPrevioTra;
|FIN;

|PROCESO BorraPrevio;
     nEmpComp = #nomcot01#0;
     nTraComp = #nomcot01#7;
     aCCCComp = #nomcot01#4;

     |HAZ CompPrevia;    || comprueba si el trabajador ha de borrarse o no

     |SI swNoBorres = 1;
          |ACABA;
     |FINSI;

     swTrabNoCalc = 0;
     |HAZ Comprobaciones;

     |SI swTrabNoCalc = 0;
          |BORRA 020#nomcot01.a;
          |LIBERA #nomcot01;
     |FINSI;
|FPRC;

|DEFBUCLE BorraPrevio;
     #nomcot01#1;
     ;
     || nEmp, nElAny, nElMes, enTipo, aCCCReg, aNaf, 01;
     || nEmp, nElAny, nElMes, enTipo, aCCCReg, aNaf, 31;
     nEmp, nElAny, nElMes, enTipo, "             ", aNaf, 01;
     nEmp, nElAny, nElMes, enTipo, "zzzzzzzzzzzzz", aNaf, 31;
     be;
     NULL, BorraPrevio;
|FIN;

|PROCESO ConstruyeCCC;
     aCCC = "";

     aAlfa = #labtraba#247;
     |QBF aAlfa;
     aAlfa = ("0000" + aAlfa) % -104;
     aRegimen = aAlfa;

     #labcentr#0 = #labtraba#0;
     #labcentr#1 = #labtraba#77;
     |LEE 000#labcentr.=;

     |SI #labtraba#45 = 421; |O #labtraba#45 = 422;
          |ABRE #nomcenes;
          #nomcenes#0 = nEmp;
          #nomcenes#13 = #labtraba#77;
          #nomcenes#1 = 87;
          |LEE 000#nomcenes.=;
          |SI FSalida = 0;
               aCCC = #nomcenes#10;
          |SINO;
               aCCC = #labcentr#10;
          |FINSI;
          |CIERRA #nomcenes;
     |SINO;
          aCCC = #labcentr#10;
     |FINSI;
     |QBF aCCC;
     |SI aCCC = "";
          aCCC = #labempre#13;
          |QBF aCCC;
     |FINSI;
     aCCC = aRegimen + aCCC;
|FPRC;

|PROCESO Variables;
     |SI #labtraba#42 = "H"; |O #labtraba#247 = 0138;
          |ACABA;
     |FINSI;

     nEmp = #labtraba#0;
     nTra = #labtraba#1;

     |HAZ ConstruyeCCC;
     aCCCReg = aCCC;

     aNaf = #labtraba#75;
     |QBF aNaf;
     aAlfa1 = aNaf % 102;
     aAlfa2 = aNaf % 310;
     |QBF aAlfa2;
     aAlfa2 = ("0000000000" + aAlfa2) % -110;
     aNaf = aAlfa1 + aAlfa2;

     swHayIntervalos = 0;
     |ABRE #nomintal;
     #nomintal#0 = #labtraba#0;
     #nomintal#1 = #labtraba#1;
     #nomintal#2 = nElAny;
     #nomintal#3 = nElMes;
     |LEE 000#nomintal.=;
     |SI FSalida = 0;
          swHayIntervalos = 1;
     |FINSI;
     |CIERRA #nomintal;
|FPRC;

|PROCESO topemes_cot;
     nTopeMes = 31;
     |SI nMesIn = 4; |O nMesIn = 6; |O nMesIn = 9; |O nMesIn = 11;
          nTopeMes = 30;
     |FINSI;
     |SI nMesIn = 2;
          nNume = nAnyIn $ 4;
          |SI nNume = 0;
               nTopeMes = 29;
          |SINO;
               nTopeMes = 28;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO FechasMes;
     nMes = nElMes;
     nAny = nElAny;

     nDia = 31;
     |SI nMes = 4; |O nMes = 6; |O nMes = 9; |O nMes = 11;
          nDia = 30;
     |FINSI;
     |SI nMes = 2;
          nNume = nAny $ 4;
          |SI nNume = 0;
               nDia = 29;
          |SINO;
               nDia = 28;
          |FINSI;
     |FINSI;

     nDiaFinMes = nDia;

     aDia = "01";
     aMes = nMes; |QBF aMes; aMes = ("00" + aMes) % -102;
     aAny = nAny;
     aFecha = aDia + "." + aMes + "." + aAny;
     fFecIniMes = aFecha;

     aDia = nDia; |QBF aDia; aDia = ("00" + aDia) % -102;
     aMes = nMes; |QBF aMes; aMes = ("00" + aMes) % -102;
     aAny = nAny;
     aFecha = aDia + "." + aMes + "." + aAny;
     fFecFinMes = aFecha;
|FPRC;

|PROCESO Distribucion;
     |SI runnom = "nomcalfi";
          |ACABA;
     |FINSI;
     |SI enSwPasada09 = 1;
          || estoy en la pasada en que calculo las nominas del nomcot09
          || es decir las que he ido guardando en el tmp que tengo que
          || calcular siempre por ser de un NIF

          || no tengo que hacer distribucion porque ya la he hecho en la
          || primera ficha calculada
          |ACABA;
     |FINSI;
     nEmpOri = #labtraba#0;
     nTraOri = #labtraba#1;
     aNif = #labtraba#7;

     |ABRE #nomcot01;
     |ABRE #nomabsca;
     |ABRE #nomabsli;
     |ABRE #nomconca;
     |ABRE #nombonif;
     |ABRE #nomboni3;
     |ABRE #gomdetjr;
     |ABRE #nomvarca;
     |ABRE #nomvarcb;
     |ABRE #labcentr;
     |ABRE #nomhicca;
     |SI runnom = "nomcalf1"; |ABRE #labtraba; |FINSI;
     |SALVA_FICHA #nomconca;
     |SALVA_FICHA #nombonif;
     |SALVA_FICHA #nomboni3;
     |SALVA_FICHA #gomdetjr;
     |SALVA_FICHA #nomvarca;
     |SALVA_FICHA #labtraba;
     |SALVA_FICHA #labcentr;

     || fechas de inicio y final del mes que calculo
     |HAZ FechasMes;

     || variables de la clave, esto lo hago dos veces, aqui y una por trab.
     || aqui es necesario para obtener el CCC
     |HAZ Variables;

     || en el nomcot09, en la primera ficha que calculo del DNI que estoy
     || calculando, me guardo todas las fichas que componen la nomina del trab.
     || asi ya se que de estas fichas ya tengo hecha la distribucion de dias

     || cuando entre en otra de las fichas, antes de hacer nada busco si
     || ya esta en el nomcot09, si es asi es porque ya he hecho los dias de
     || esa ficha por tanto no tengo que hacer nada aqui, SALVO marcarla
     || como "nomina calculada", ya que al final del calculo de nominas
     || se calcularan todas las que no se hayan incluido en el calculo
     || y formen parte del DNI del mes

     |NOME_DAT #nomcot09#eaFichTramos#;
     |ABRE #nomcot09;
     #nomcot09#0 = nEmpOri;
     #nomcot09#1 = nTraOri;
     |LEE 101#nomcot09.=;
     |SI FSalida = 0;
          #nomcot09#4 = "S";
          |GRABA 020#nomcot09.a;
          |LIBERA #nomcot09;
     |SINO;
          || esto es el borrado de todo lo que hubiera de este DNI en la dist.
          || de dias, solo la primera ficha

          |SALVA_FICHA #nomcalca;

          || en los bucles, voy a borrar sin limitar por CCC, despues dentro
          || de cada proceso comparo para que se borre siempre del CCC actual
          || o bien si el CCC esta en blanco
          || tambien ha de borrar siempre el CODIGO DE TRABAJADOR, por la
          || posibilidad de que se haya calculado antes con un CCC erroneo

          |BUCLE BorraPrevio;           || borrado de dist. de dias
          |BUCLE BorraPrevioTra;        || borrado de tramos
          |BUCLE BorraPrevioCal;        || borrado de calculos
          |BUCLE BorraPrevioBon;        || borrado de bonificaciones

          || aqui solo grabo en el nomcot09 los trabajadores que formaran
          || parte de mi distribucion del mes

          |BUCLE labtraba_previo;

          |REPON_FICHA #nomcalca;

          || aqui creo los tramos por cada ficha que forma parte
          |BUCLE nomcot09;

          || por ultimo relleno los dias a cero
          |HAZ Rellena;
     |FINSI;

     |REPON_FICHA #labcentr;
     |REPON_FICHA #labtraba;
     |REPON_FICHA #nomvarca;
     |REPON_FICHA #gomdetjr;
     |REPON_FICHA #nomboni3;
     |REPON_FICHA #nombonif;
     |REPON_FICHA #nomconca;
     |SI runnom = "nomcalf1"; |CIERRA #labtraba; |FINSI;
     |CIERRA #nomhicca;
     |CIERRA #labcentr;
     |CIERRA #nomcot09;
     |CIERRA #nomvarcb;
     |CIERRA #nomvarca;
     |CIERRA #gomdetjr;
     |CIERRA #nomboni3;
     |CIERRA #nombonif;
     |CIERRA #nomconca;
     |CIERRA #nomabsli;
     |CIERRA #nomabsca;
     |CIERRA #nomcot01;
|FPRC;
