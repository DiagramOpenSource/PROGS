|FICHEROS;
     nomdisco #0;
     labtraba #2;         || trabajadores
     nomdator #11;        || bancos soporte magnetico
     nomrevca #12;        || transfer.cabs.
     nomrevli #13;        || transfer.lins.
     nompagli;
     nomcontr;
|FIN;

|VARIABLES;
    seleccio = 0;
    swpri = 0;
    p_mes = "";
    fecsys = @;
    fecalf = "";
    alfa1 = "";
    alfa2 = "";
    alfa3 = "";
    alfa4 = "";
    alfa5 = "";
    alfa6 = "";
    alfa7 = "";
    alfa8 = "";
    alfa9 = "";
    traba = "";
    nume1 = 0;
    nume2 = 0;
    nume3 = 0;
    nume4 = 0;
    fech1 = @;
    fech2 = @;
    fech3 = @;
    fech4 = @;
    empresa = "";
    fiche = "";
    canal = 0;
    espacio = " ";
    longitud = "";
    treintaiseis = "";
    importes = 0;
    registro = 0;
    regis_total = 0;
    contenido = "";
    secuenci = "";
    unida = "";
    sistem = "";
    dire = "";
    fichero1 = "";
    fichero2 = "";
    fichero3="";
    dreg = 0;
    hreg = 0;
    demp = 0;
    hemp = 0;
    para1 = 0;
    para2 = 0;
    aEl0306 = "";
    aEl0606 = "";
    aEl0806 = "";
    aMonDisco = "";
    &eaMoneda = "";
    &enCanti = 0;
    aIP = "";
     aDesdeReg = "";
     aHastaReg = "";
     aAlfa = "";
     aAlfa1 = "";
     aAlfa2 = "";
     &swModulo = 0;
     aTipo = "";
     aUsuario = "";
     aDato = "";
|FIN;
____________________________________/ CALCULOS DE PANTALLA
|PROCESO Ruta;
     |SI FSalida = 10;
          aAlfa = "F"; |BROWSE aAlfa;
          aAlfa = aAlfa % 0299; |QBF aAlfa;
          |SI aAlfa ! "";
               #0#5 = aAlfa; |PINTA #0#5;
          |FINSI;
          |ERROR;
     |FINSI;
|FPRC;

|PROCESO mayus; |TIPO 0;
    #0Campo = #0Campo > #0Campo;
|PINTA #0Campo;
|FPRC;

|PROCESO no_vacio; |TIPO 0;
    alfa1 = #0#15;
|QBF alfa1;
|SI alfa1 = "";
    |AVISO;
    |ERROR;
|FINSI;
|FPRC;

|PROCESO seleccion; |TIPO 0;
|SI #0#2 = "D";
        alfa1 = "S";
        alfa2 = "N";
|SINO;
        alfa1 = "N";
        alfa2 = "S";
|FINSI;
|C_M #0#0, 1, alfa1;
|C_M #0#1, 1, alfa1;
|C_M #0#12, 1, alfa1;
||C_M #0#16, 1, alfa1;

|C_M #0#18, 1, alfa2;
|C_M #0#19, 1, alfa2;
|C_M #0#20, 1, alfa2;
|C_M #0#21, 1, alfa2;
|C_M #0#22, 1, alfa2;
|C_M #0#23, 1, alfa2;
|C_M #0#24, 1, alfa2;
|C_M #0#25, 1, alfa2;
|C_M #0#26, 1, alfa2;
|C_M #0#27, 1, alfa2;
|C_M #0#28, 1, alfa2;
|C_M #0#29, 1, alfa2;
|C_M #0#30, 1, alfa2;
|C_M #0#31, 1, alfa2;
|C_M #0#32, 1, alfa2;
|C_M #0#33, 1, alfa2;
|C_M #0#34, 1, alfa2;
|C_M #0#35, 1, alfa2;
|C_M #0#36, 1, alfa2;
|C_M #0#37, 1, alfa2;
|FPRC;

|PROCESO nomdator;
     #0#9 = #nomdator#0;
     #0#10 = #nomdator#1;
     |PINTA #0#9;
     |PINTA #0#10;
     |ERROR10;
|FPRC;

|DEFBUCLE nomdator;
     #nomdator#1;
     13;
     01, #0#12;
     09, #0#12;
     ;
     NULL, nomdator;
|FIN;

|PROCESO ProponOrd; |TIPO 7;
     swModulo = 0;
     |HAZ BuscaModulo;
     |SI swModulo = 2805; |O swModulo = 1;
          |BUCLE nomdator;
     |FINSI;
|FPRC;

|PROCESO CompOrd;
     swModulo = 0;
     |HAZ BuscaModulo;
     |SI swModulo = 2805; |O swModulo = 1;
          |ABRE #nomdator;
          #nomdator#0 = #0#9;
          |LEE 000#nomdator.=;
          |SI FSalida = 0;
               |SI #nomdator#13 ! #0#12;
                    aAlfa = "    ATENCION: El ordenante no esta asignado a la empresa ";
                    aAlfa = aAlfa + "seleccionada";
                    |MENAV aAlfa;
               |FINSI;
          |FINSI;
          |CIERRA #nomdator;
     |FINSI;
|FPRC;

|PROCESO NormaOrdenante;
     || Esto en verdad es para usar la norma 43 o la norma 43-1, pero no se
     || usa ya que lo que se hacia de toda la vida es correcto, no hace
     || falta diferenciar. El campo en la pantalla del nomdator esta
     || quitado y se deja por defecto a "2" para que lo haga como toda la vida
     || aunque de las dos maneras lo haria bien

     |SI #11#16 = 2;
          alfa1 = #11#2;             || codigo ordenante
     |SINO;
          aDato = #11#2;
          |QBF aDato;
          alfa1 = " " + aDato;
     |FINSI;
|FPRC;

|PROCESO NormaBeneficiario;          || datos del beneficiario
     || Esto en verdad es para usar la norma 43 o la norma 43-1, pero no se
     || usa ya que lo que se hacia de toda la vida es correcto, no hace
     || falta diferenciar. El campo en la pantalla del nomdator esta
     || quitado y se deja por defecto a "2" para que lo haga como toda la vida
     || aunque de las dos maneras lo haria bien

     alfa2 = #13#4 % 112;      || dni
     |QBF alfa2;
     |SI #11#16 = 2;
          alfa2 = ("000000000000" + alfa2) % -112;
     |SINO;
          aDato = alfa2;
          aDato = aDato + "            ";
          aDato = aDato % 112;
          alfa2 = aDato;
     |FINSI;
|FPRC;

____________________________________/ GRABACION SECUENCIAL
|PROCESO grabalin;
    contenido = contenido + longitud;
    contenido = contenido % 173;
    || contenido = contenido + ".";             || total = 2 + 72
    contenido = contenido + " " + &13 + &10;             || total = 2 + 72
|FGRABA contenido;
    regis_total = regis_total + 1;
|FPRC;

|PROCESO ordenante;
|DFICE empresa;
    fiche = "wb  " + empresa + secuenci;
|QBF fiche;
|FABRE fiche;
    canal = FSalida;
|SI canal ] 0;
          |HAZ NormaOrdenante;     || datos del ordenante (variable alfa1)
        alfa2 = espacio * 12;      || libre
        alfa3 = "001";             || numero de dato
        fecsys = ~;   traba = fecsys;
        alfa4 = (traba % 102) + (traba % 402) + (traba % -102);  || fec. envio
        traba = #0#8;
        alfa5 = (traba % 102) + (traba % 402) + (traba % -102);  || fec.emision
        alfa6 = #11#4;   alfa6 = ("0000" + alfa6) % -104;
        |SI #11#15 = "0";
             contenido = canal+" "+aEl0306+alfa1+alfa2+alfa3+alfa4+alfa5+alfa6+#11#5+#11#6+"0   "+#11#7;
        |SINO;
             contenido = canal+" "+aEl0306+alfa1+alfa2+alfa3+alfa4+alfa5+alfa6+#11#5+#11#6+"1   "+#11#7;
        |FINSI;
    |HAZ grabalin;
        traba = "002" + #11#3;  |HAZ cabecera;         || nombre
        traba = "003" + #11#8;  |HAZ cabecera;         || domicilio
        traba = "004" + #11#9;  |HAZ cabecera;         || plaza
        traba = #11#10;  |QBT traba;
    |SI traba ! "";
            traba = "007" + #11#10;  |HAZ cabecera;    || nombre empresa
            traba = "008" + #11#11;  |HAZ cabecera;    || domicilio empresa
    |FINSI;
|FINSI;
|FPRC;

|PROCESO cabecera;
     treintaiseis = espacio * 36;
     traba = traba + treintaiseis;
     traba = traba % 136;
     |HAZ NormaOrdenante;          || datos del ordenante (variable alfa1)
     contenido = canal + " " + aEl0306 + alfa1 + alfa2 + traba;
     |HAZ grabalin;
|FPRC;

|PROCESO beneficiario;
     |HAZ NormaOrdenante;          || datos del ordenante (variable alfa1)
     |HAZ NormaBeneficiario;          || datos del beneficiario (variable alfa2)


     alfa3 = "010";            || numero de dato

     enCanti = #13#6;
     |HAZ ConverMoneda;
     #13#6 = enCanti;

     |SI aMonDisco = "P";
          alfa4 = #13#6;  alfa4 = "0000000000" + alfa4;  alfa4 = alfa4 % -110;
          alfa4 = alfa4 + "00";     || importe con dos decimales
     |FINSI;
     |SI aMonDisco = "E";
          alfa4 = #13#6;  alfa4 = "000000000000" + alfa4;  alfa4 = alfa4 % -112;
     |FINSI;

     |HAZ total;

     #0#4 = 2;
     |SI #0#4 = 1;
         alfa5 = (#13#5 % 104) + (#13#5 % 504) + (#13#5 % 910);  || cuenta cte.
         alfa6 = #13#5 % 1902;     || digito control
     |SINO;
         alfa5 = (#13#5 % 104) + (#13#5 % 504) + (#13#5 % 1110); || cuenta cte.
         alfa6 = #13#5 % 902;      || digito control
     |FINSI;
     contenido = canal+" "+aEl0606+alfa1+alfa2+alfa3+alfa4+alfa5+"11  "+alfa6;
     |HAZ grabalin;

    || traba = "011" + #2#2;   |HAZ lineas;          || nombre
    traba = "011" + #13#8;   |HAZ lineas;          || nombre
     aTipo = #13#7 % 103;

    traba = #2#3;   |QBF traba;  alfa8 = traba + " " + #2#4 + " " + #2#5;
    |SI aTipo = "EMB"; alfa8 = ""; |FINSI;
    traba = "012" + alfa8;  |HAZ lineas;          || domicilio+numero+piso

    alfa9 = alfa8 % 0;  nume1 = alfa9;  nume1 = nume1 - 36;
|SI nume1 > 0;
        nume1 = (37 * 100) + nume1;
        traba = alfa8 % nume1;
        traba = "013" + traba;  |HAZ lineas;          || resto domicilio
|FINSI;

    alfa8 = #2#8;  alfa8 = "00"  + alfa8;  alfa8 = alfa8 % -102;
    alfa9 = #2#9;  alfa9 = "000" + alfa9;  alfa9 = alfa9 % -103;
    traba = "014" + alfa8 + alfa9 + " " + #2#6;       || cod.postal+pob.
    |SI aTipo = "EMB"; traba = "014"; |FINSI;
|HAZ lineas;

    traba = "015" + #2#10;              || provincia
    |SI aTipo = "EMB"; traba = "015"; |FINSI;
|HAZ lineas;

    traba = "016" + #13#7;              || concepto de transferencia
|HAZ lineas;
|FPRC;

|PROCESO lineas;
    treintaiseis = espacio * 36;
    traba = traba + treintaiseis;
    traba = traba % 136;
    |HAZ NormaOrdenante;           || datos del ordenante (variable alfa1)
    contenido = canal + " " + aEl0606 + alfa1 + alfa2 + traba;
|HAZ grabalin;
|FPRC;

|PROCESO totales;
     |HAZ NormaOrdenante;          || datos del ordenante (variable alfa1)
    alfa2 = espacio * 12;      || libre 1
    alfa3 = espacio * 3;       || libre 2

    |SI aMonDisco = "E";
         alfa4 = importes;  alfa4 = "000000000000" + alfa4;  alfa4 = alfa4 % -112;
    |FINSI;                             || suma de importes EURO
    |SI aMonDisco = "P";
         alfa4 = importes;  alfa4 = "0000000000" + alfa4;  alfa4 = alfa4 % -110;
         alfa4 = alfa4 + "00";          || suma de importes PESETAS
    |FINSI;
    alfa5 = registro;  alfa5 = "00000000" + alfa5;
    alfa5 = alfa5 % -108;      || suma de registros '010'

    regis_total = regis_total + 1;
    alfa6 = regis_total;  alfa6 = "0000000000" + alfa6;
    alfa6 = alfa6 % -110;      || suma de registros (todos)
    contenido = canal+" "+aEl0806+alfa1+alfa2+alfa3+alfa4+alfa5+alfa6;
|HAZ grabalin;
    FSalida = canal;
|FCIERRA FSalida;
|FPRC;

|PROCESO total;
    importes = importes + #13#6;   || total importes
    registro = registro + 1;       || total registros '010'
|FPRC;
____________________________________/ LECTURA Y CALCULO
|PROCESO proceso;
     #12#0 = #13#0;
     #12#1 = #13#1;
     |LEE 101#12=;
     |SI FSalida ! 0;    |ACABA;   |FINSI;

     #12#5 = "S";
     |GRABA 020#12.a;
     |LIBERA #12;

|SI #13#6 > 0;
    |HAZ ordena;
        #0#13 = #2#0;  |PINTA #0#13;
        #0#14 = #2#1;  |PINTA #0#14;

        seleccio = 1;
    |SI swpri = 0;
        |HAZ ordenante;
            swpri = 1;
    |FINSI;
    |HAZ beneficiario;
|FINSI;
|FPRC;

|PROCESO ordena;   || LEE EL TRABAJADOR
|DDEFECTO #2;
    #2#0 = #13#0;
    #2#1 = #13#3;
|LEE 000#2=;
|FPRC;

|PROCESO lee_todo;
|DDEFECTO #11;
|ABRE #11;          || lee el banco transferencia
    #11#0 = #0#9;
|LEE 000#11=;
|CIERRA #11;
|FPRC;

|DEFBUCLE leyendo2;
 #13#3;
 ;
 "            ", #12#0, #12#1,      1;
 "zzzzzzzzzzzz", #12#0, #12#1, 999999;
 ;
 NULL, ordena, NULL, NULL, NULL, proceso;
 #2#0, #2#77, #2#1;
|FIN;

|DEFBUCLE leyendo;
 #13#3;
 ;
 "            ", #12#0, #12#1,      1;
 "zzzzzzzzzzzz", #12#0, #12#1, 999999;
 ;
 NULL, proceso;
|FIN;

|PROCESO cabeceraReg;
     |SI #12#5 = "S";
          |SI #nomcontr#42 = "S";
               aAlfa1 = #12#0; aAlfa1 = "00000" + aAlfa1; aAlfa1 = aAlfa1 % -105;
               aAlfa2 = #12#1; aAlfa2 = "000000" + aAlfa2; aAlfa2 = aAlfa2 % -106;

               aAlfa = "    ATENCION: El registro de la transferencia " + aAlfa2 + " de la empresa ";
               aAlfa = aAlfa + aAlfa1 + " ya está pagado.";
               |MENAV aAlfa;
               aAlfa = "    N¿Desea volver a crear la transferencia de este registro?";
               |CONFI aAlfa;
          |SINO;
               FSalida = 0;
          |FINSI;
          |SI FSalida = 0;
               |SI #0#3 = 1;   |BUCLE leyendo;     |FINSI;
               |SI #0#3 = 2;   |BUCLE leyendo2;    |FINSI;
          |FINSI;
     |SINO;
          |SI #0#3 = 1;   |BUCLE leyendo;     |FINSI;
          |SI #0#3 = 2;   |BUCLE leyendo2;    |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE cabeceraReg;
     #12#1;
     ;
     demp, dreg;
     demp, hreg;
     ;
     NULL, cabeceraReg;
|FIN;

|PROCESO MarcaPagado;
     #nompagli#8 = #0#8;
     #nompagli#11 = "S";
     #nompagli#14 = #0#9;
     |GRABA 020#nompagli.a;
|FPRC;

|DEFBUCLE MarcaPagado;
     #nompagli#2;
     ;
     demp, aDesdeReg, 1900, 01, 0, " ", 00001;
     hemp, aHastaReg, 2999, 12, 9, "z", 99999;
     be;
     NULL, MarcaPagado;
|FIN;

|PROCESO tipo3; |TIPO 3;
     aMonDisco = eaMoneda % 101;
     |SI aMonDisco = "E";
          aEl0306 = "0356";
          aEl0606 = "0656";        || sufijos para EUROS
          aEl0806 = "0856";
     |FINSI;
     |SI aMonDisco = "P";
          aEl0306 = "0306";
          aEl0606 = "0606";        || sufijos para PESETAS
          aEl0806 = "0806";
     |FINSI;

     |ABRE #nomcontr; |LEE 000#nomcontr.p; |CIERRA #nomcontr;

    longitud = espacio * 74;  ||(72+2: canal + espacio)
    secuenci = #0#15;
|QBF secuenci;
|HAZ lee_todo;
|ABRE #2;
|ABRE #12;

|SI #0#2 = "D";
        dreg = #0#0;     hreg = #0#1;
        || demp = #0#12;    hemp = #0#16;
        demp = #0#12;    hemp = #0#12;
     |BUCLE cabeceraReg;
    ||SI #0#3 = 1;   |BUCLE leyendo;     |FINSI;
    ||SI #0#3 = 2;   |BUCLE leyendo2;    |FINSI;
|FINSI;
|SI #0#2 = "S";
    |PARA para1 = 18; |SI para1 [ 27; |HACIENDO para1 = para1 + 1;
            para2 = para1 + 10;
            dreg = #0para2;   hreg = dreg;
            demp = #0para1;   hemp = demp;
     |BUCLE cabeceraReg;
    ||SI #0#3 = 1;   |BUCLE leyendo;     |FINSI;
    ||SI #0#3 = 2;   |BUCLE leyendo2;    |FINSI;
    |SIGUE;
|FINSI;
|CIERRA #2;
|CIERRA #12;
|SI seleccio = 0;
    |MENAV "    Seleccion vacia ...";
|SINO;
     aDesdeReg = dreg;
     aDesdeReg = ("000000" + aDesdeReg) % -106;
     aHastaReg = hreg;
     aHastaReg = ("000000" + aHastaReg) % -106;
     |BUCLE MarcaPagado;
    |HAZ totales;
    |HAZ final2;
|FINSI;
|FPRC;

|PROCESO final2;         || copia a disco
     nume1 = importes;
     |SI aMonDisco = "E";
          nume1 = importes / 100;
     |FINSI;
     alfa1 = nume1;     alfa1 = "          " + alfa1;  alfa1 = alfa1 % -110;
     alfa2 = registro;  alfa2 = "          " + alfa2;  alfa2 = alfa2 % -110;
     alfa1 = " Total RETENEDORES: " + alfa1 + "          ";
     alfa2 = " Total PERCEPTORES: " + alfa2 + "          ";
     |HAZ aviso1;

     |CONFI "2305S¿Continuar con la creación del fichero?";
     |SI FSalida = 0;
          |PINTA 2305, "El fichero se grabará en el directorio raíz de la unidad indicada:";
          E_sup = 1;
          E_inf = "ABCDEFGHabcdefgh";
          unida = "C";
          unida = 2372 ? 0;
          |DFICE fiche;
          fiche = fiche + secuenci;
          dire = "";
          |DIRECTORIO dire;
          |QUE_SISTEMA sistem;
          |BLIN 23;
          |PINTA 2305, "Copiando ...";

          fichero1 = empresa + secuenci;
          || fichero2 = "/" + secuenci;
          || ENVIA_FICHERO fichero1, fichero2;
          fichero3 = unida + ":\" + secuenci;
          |IP_REMOTO aIP;

          |SI aIP ! "";
               |ENVIA_FICHERO fichero1, fichero3;
          |SINO;
               |COPIA_FICHERO fichero1, fichero3;
          |FINSI;

          |SI FSalida = 0;
               |RFBORRA fichero1;
               aAlfa = #0#15;
               |QBF aAlfa;
               aAlfa =  "    El fichero " + aAlfa + " se ha creado en el ";
               aAlfa = aAlfa + "directorio raíz de la unidad " + unida + ":";
               |MENAV aAlfa;
          |SINO;
               |MENAV "    Error en la creación del fichero";
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO aviso1;         || desde calculo 'final2'
|PUSHV 05012380;
|HAZ color_f;
|CUADRO 09 30 12 71;
||            1234567890123456789012345678901234567890
|HAZ color_c;
|PINTA 1031, alfa1;
|PINTA 1131, alfa2;
|ATRI 0;
|AVISO;
|FPRC;

|PROCESO color_c;        || activa el color caracter en avisos
|COLOR 14, 0;
|ATRI 0;
|FPRC;

|PROCESO color_f;        || activa el color graficos en avisos
|COLOR 4, 0;
|ATRI 0;
|FPRC;

|PROCESO LaEntrada; |TIPO 8;
     aUsuario = Usuario % 810;
     #0#6 = aUsuario;
     |LEE 000#0=;
     |SI FSalida ! 0;
          |DDEFECTO #0;
          #0#6 = aUsuario;
          |GRABA 020#0b;
     |FINSI;
|FPRC;

|PROGRAMA;
     |CLS;
     |CABEZA "Creacion Soporte Magnetico";
     |ABRE #0;
     |HAZ LaEntrada;
     |PINPA #0#0;
     |PINDA #0#0;
     |ENDATOS #1#0;
     |SI FSalida = 0;
          |GRABA 020#0a;
          |HAZ tipo3
     |FINSI;
     |CIERRA #0;
|FPRO;
