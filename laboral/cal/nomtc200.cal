|FICHEROS;
     nomtc200 #0;
     nomtc201 #1;        || tmp. TC2 cabs.
     nomtc202 #2;        || tmp. TC2 lins.

     labempre #51;       || empresas
     labcentr #53;       || centros principales
     nomcenes #54;       || centros especiales

     silcon02 #101;      || lotes cabs.
     silcon03 #102;      || lotes lins.

     nomcalcu #140;      || calculo mensual
     nomcalat #141;      || calculo atrasos
     nomconst;
|FIN;

|VARIABLES;
     aAlfa1 = "";
     aAlfa2 = "";
     aAlfa3 = "";
     aAlfa4 = "";
     nNume1 = 0;
     nNume2 = 0;
     nNume3 = 0;
     nNume4 = 0;
     nPara1 = 0;
     nPara2 = 0;
     nPara3 = 0;
     nPara4 = 0;
     fFech1 = @;
     fFech2 = @;
     fFech3 = @;
     fFech4 = @;
     nFEstado = 0;
     nSeleccio = 0;
     nModo = 0;

     &eaFuente = "";
     &enTipoAt = 0;
     &enDesEmp = 0;
     &enHasEmp = 0;
     &enDesCen = 0;
     &enHasCen = 0;
     &enAny = 0;
     &enDesMes = 0;
     &enHasMes = 0;
     &eaTempo1 = "";     || lotes cabs.
     &eaTempo2 = "";     || lotes lins.
     &eaTempo3 = "";     || TC2 cabs.
     &eaTempo4 = "";     || TC2 lins.
     &eaTipoLi = "";
     &eaParame = "TC2";

     aFuente = "";
     nSwCorta = 0;
     nMenos = 0;
     nCodEmpre = 0;
     aLibre = "";
     aReser = "";
     nSwTra = 0;
     nAny = 0;
     aClave = "";
     aNumer = "";
     aImpor = "";
     aCta = "";
     &aCon = "";
     &nPer = 0;
     aNSS = "";
     nEpi = 0;
     aBas = "";
     aDed = "";
     aApe = "";
     aIde = "";
     aNif = "";
     nDib = 0;
     nImp = 0;
     aSit = "";
     nCto = 0;
     nDid = 0;
     nDed = 0;
     nFec = 0;
     nBorrados = 0;
     nSwSiHay = 0;
     &eaTitBorra = "";
     &enSwLin = 0;
     nSwOtroTC2 = 0;
     nLinea = 0;
     nHojas = 0;
     nTraba = 0;
     nSwVacia2 = 0;
     nSwVacia3 = 0;
     nSwVacia4 = 0;
     nSwVacia5 = 0;
     nSwVacia6 = 0;
     nSwVacia7 = 0;
     nSwVacia8 = 0;
     nSwVacia9 = 0;
     nSwVacia10= 0;
     nSwVacia11= 0;
     nSwVacia12= 0;
     nSwVacia13= 0;
     nSwVacia14= 0;
     nSwVacia15= 0;
     sw_convert = 0;
     &enB01 = 0;
     &enB02 = 0;
     &enB10 = 0;
     &enB11 = 0;
     &enB21 = 0;
     &enB22 = 0;
     &enD01 = 0;
     &enD02 = 0;
     &enD03 = 0;
     &enD07 = 0;
     &enD09 = 0;
     &enPagina = 0;
     nDAlta = 0;
     nDReal = 0;
     nSwNoGrabes = 0;
     aAbreviado = "";
     nHoras = 0;
     aSalar = "";
     aAntDed = "";
     nAntEpi = 0;
     nBajaAlta = 0;
     nNoSigas = 0;
     nCien = 0;
     aCtaGraba = "";
     nImpAnt = 0;
     &swFiniqRaro = 0;
     &swFiniqRaro2 = 0;
     &swSegundoLote = 0;      || indicador para que vuelva a calcular
     &swCalDos = 0;           || indicador para el silcon10
     nTopeMax = 0;
     nTopeMin = 0;
     nTope = 0;
     aRegimen = "";
     nLabor = 0;
     nPropo = 0;
     nBaseIT = 0;
     aEpi = "";
     aAntEpi = "";
|FIN;
____________________________________/ GESTION PANTALLA
|PROCESO Defecto; |TIPO 7;
     fFech1 = ~;
     aAlfa1 = fFech1;
     aAlfa1 = aAlfa1 % -104;
     #0Campo = aAlfa1;
     #0Campo = #0Campo - nMenos;
     |PINTA #0Campo;
|FPRC;

|PROCESO MiraMes; |TIPO 7;
     |SI aFuente = "M";|O aFuente = "H";
          |SI Campo = 9;
               |ACABA;
          |FINSI;
     |FINSI;
     |SI aFuente = "A";|O aFuente = "T";     |ACABA;   |FINSI;

     fFech1 = ~;
     aAlfa1 = fFech1;
     aAlfa1 = aAlfa1 % 402;
     #0Campo = aAlfa1;

     nMenos = 0;
     |SI Campo = 5;|Y aFuente ! "T";
          #0Campo = #0Campo - 1;
          |SI #0Campo = 0;    nMenos = 1;    #0Campo = 12;   |FINSI;
     |FINSI;

     |PINTA #0Campo;
|FPRC;

|PROCESO PintaMes; |TIPO 0;
     |SI aFuente ! "A";|Y aFuente ! "T";|Y aFuente ! "C";
     |Y Campo = 9;
          |ACABA;
     |FINSI;

     nNume1 = Campo + 1;
     |SI #0Campo =  1; #0nNume1 = "ENERO     ";  |FINSI;
     |SI #0Campo =  2; #0nNume1 = "FEBRERO   ";  |FINSI;
     |SI #0Campo =  3; #0nNume1 = "MARZO     ";  |FINSI;
     |SI #0Campo =  4; #0nNume1 = "ABRIL     ";  |FINSI;
     |SI #0Campo =  5; #0nNume1 = "MAYO      ";  |FINSI;
     |SI #0Campo =  6; #0nNume1 = "JUNIO     ";  |FINSI;
     |SI #0Campo =  7; #0nNume1 = "JULIO     ";  |FINSI;
     |SI #0Campo =  8; #0nNume1 = "AGOSTO    ";  |FINSI;
     |SI #0Campo =  9; #0nNume1 = "SEPTIEMBRE";  |FINSI;
     |SI #0Campo = 10; #0nNume1 = "OCTUBRE   ";  |FINSI;
     |SI #0Campo = 11; #0nNume1 = "NOVIEMBRE ";  |FINSI;
     |SI #0Campo = 12; #0nNume1 = "DICIEMBRE ";  |FINSI;
     |PINTA #0nNume1;
|FPRC;

|PROCESO Tipo8; |TIPO 8;
     nSwCorta = 0;

     aFuente = PARAMETRO;
     |QBF aFuente;
     |SI aFuente = "";
          |MENAV "0000Parametros incorrectos. Instalacion defectuosa.";
          nSwCorta = 1;
     |FINSI;
     |SI aFuente ! "M";|Y aFuente ! "A";|Y aFuente ! "H";|Y aFuente ! "T";
     |Y aFuente ! "C";
          |MENAV "0000Parametros incorrectos. Instalacion defectuosa.";
          nSwCorta = 1;
     |FINSI;
|FPRC;

|PROCESO Parrilla7; |TIPO 7;
     |SI aFuente = "M";
          |DDEFECTO #nomcalcu;
          |ABRE #nomcalcu;
          |LEE 000#nomcalcu.p;
          #nomtc200#0 = #nomcalcu#0;    || desde empresa
          #nomtc200#1 = #nomcalcu#1;    || hasta empresa
          #nomtc200#4 = #nomcalcu#9;    || a¤o
          |CIERRA #nomcalcu;
          |PINDA #0#nomtc200;
     |FINSI;
     |SI aFuente = "A";
          |DDEFECTO #nomcalat;
          |ABRE #nomcalat;
          |LEE 000#nomcalat.p;
          #nomtc200#0 = #nomcalat#0;    || desde empresa
          #nomtc200#1 = #nomcalat#1;    || hasta empresa
          #nomtc200#4 = #nomcalat#9;    || a¤o
          #nomtc200#5 = #nomcalat#8;    || desde mes
          #nomtc200#9 = #nomcalat#10;   || hasta mes
          |CIERRA #nomcalat;
          |PINDA #0#nomtc200;
     |FINSI;
|FPRC;

|PROCESO Retoca7; |TIPO 7;
     |SI aFuente = "A";
          |C_M #0#9, 1, "S";  |C_V #0#9, 1, "S";  |PINTA #0#9;
          |C_M #0#10, 1, "N"; |C_V #0#10, 1, "S"; |PINTA #0#10;
          |PINTA 1418, "Desde Mes ..........:";
          |PINTA 1518, "Hasta Mes ..........:";
     |FINSI;
     |SI aFuente = "T";
          |C_M #0#8, 1, "S";  |C_V #0#8, 1, "S";  |PINTA #0#8;
          |C_M #0#9, 1, "S";  |C_V #0#9, 1, "S";  |PINTA #0#9;
          |C_M #0#10, 1, "N"; |C_V #0#10, 1, "S"; |PINTA #0#10;
          |PINTA 1318, "Tipo Atrasos Hist¢r.:";
          |PINTA 1418, "Desde Mes ..........:";
          |PINTA 1518, "Hasta Mes ..........:";
     |FINSI;
     |SI aFuente ! "T"; |Y aFuente ! "A"; |Y aFuente ! "C";
          |C_M #0#8, 1, "N";  |C_V #0#8, 1, "N";
          |C_M #0#9, 1, "N";  |C_V #0#9, 1, "N";
          |C_M #0#10, 1, "N"; |C_V #0#10, 1, "N";
     |FINSI;
     |SI aFuente = "T";|O aFuente = "A";
          |PINTA 1137, "):";
     |FINSI;
     |SI aFuente = "C";
          ||C_M #0#8, 1, "S";  |C_V #0#8, 1, "S";  |PINTA #0#8;
          |C_M #0#7, 1, "N"; |C_V #0#7, 1, "N";
          |PINTA 1120, "                                    ";
          |C_M #0#9, 1, "S";  |C_V #0#9, 1, "S";  |PINTA #0#9;
          |C_M #0#10, 1, "N"; |C_V #0#10, 1, "S"; |PINTA #0#10;
          ||PINTA 1320, "Tipo Atrasos Hist.:";
          |PINTA 1418, "Desde Mes ..........:";
          |PINTA 1518, "Hasta Mes ..........:";
     |FINSI;
|FPRC;

|PROCESO Mayus; |TIPO 0;
     #0Campo = #0Campo > #0Campo;
     |PINTA #0Campo;
|FPRC;

|PROCESO Segun;
     |SI aFuente = "T";|O aFuente = "A";
          |SI #0Campo = "F";
               |ERROR;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO CambiaMensa; |TIPO 7;
              ||  1234567890123456789012345678901234567890123456789012345678901234567890123456
     |SI aFuente = "T";|O aFuente = "A";
          |MENSA "    Tipo TC2:(A) Aprendices; (R) Resto; (T) Todos";
     |SINO;
          |MENSA "    (A) Formacion; (R) Resto; (T) Todos - NO INCLUYE Finiq.; (F) Finiq";
     |FINSI;
|FPRC;

|PROCESO Aviso; |TIPO 7;
|SI FSalida = 5;|O FSalida = 4;    |ACABA;   |FINSI;
|SI PARAMETRO ! "T"; |ACABA; |FINSI;
|PUSHV 08091370;
|ATRI R;
|PINTA 0809, "Recuerde que si tiene mas de un tipo de atrasos en el mis-";
|PINTA 0909, "mo mes, debera  emitir el TC1 y el resumen de nominas tras";
|PINTA 1009, "la emision del TC2, para cada uno de los  tipos de atrasos";
|PINTA 1109, "calculados en el mismo periodo.                           ";
|ATRI 0;
|PAUSA;
|POPV;
|FPRC;
____________________________________/ IMPRESION BORRADOR TC2
|PROCESO Temporal_1;
     |SI aCta = "";
          aCta = #2#16;
     |FINSI;
     |SI aCta ! #2#16;
          enSwLin = 1;
          |PRINT;
          aCta = #2#16;
     |FINSI;
     enSwLin = 0;
     |PRINT;
|FPRC;

|DEFBUCLE Temporal;
 #2#1;
 ;
 "               ", "       ", 000000, "            ", "   ", 000, "  ", "  ";
 "zzzzzzzzzzzzzzz", "zzzzzzz", 999999, "zzzzzzzzzzzz", "zzz", 999, "zz", "zz";
 ;
 NULL, Temporal_1;
|FIN;

|PROCESO Borrador;
     |INFOR "nomtc299";
     |BUCLE Temporal;
     |PIEINF;
     |FININF;
|FPRC;
____________________________________/ CONTADOR DE TRABAJADORES 2
|PROCESO GrabaTrabaja2;
     |SI nTraba ! 0;
          #1#0 = aCtaGraba;
          |LEE 101#1=;
          |SI FSalida = 0;
               #1#11 = nTraba;
               |GRABA 020#1a;
          |FINSI;
          |LIBERA #1;
     |FINSI;
|FPRC;

____________________________________/ CONTADOR DE TRABAJADORES
|PROCESO GrabaTrabaja;
     #1#0 = aCta;
     |LEE 101#1=;
     |SI FSalida = 0;
          #1#11 = nTraba;
          |GRABA 020#1a;
     |FINSI;
     |LIBERA #1;
|FPRC;

|PROCESO TemporalTrabaja_1;
     |SI nSwOtroTC2 = 0;
          nSwOtroTC2 = 1;
          nTraba = 1;
          aCta = #2#16;
          aNSS = #2#2;
     |FINSI;
     |SI aCta ! #2#16;
          |HAZ GrabaTrabaja;
          nTraba = 1;
          aCta = #2#16;
          aNSS = #2#2;
     |SINO;
          |SI aNSS ! #2#2;
               nTraba = nTraba + 1;
               aNSS = #2#2;
          |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE TemporalTrabaja;
 #2#2;
 ;
 "               ", "            ";
 "zzzzzzzzzzzzzzz", "zzzzzzzzzzzz";
 ;
 NULL, TemporalTrabaja_1;
|FIN;

|PROCESO CuentaTrabajadores;
     nSwOtroTC2 = 0;
     |ABRE #1;
     |BUCLE TemporalTrabaja;
     |HAZ GrabaTrabaja;
     |CIERRA #1;
|FPRC;
____________________________________/ CONTADOR DE HOJAS
|PROCESO GrabaHojas;
     #1#0 = aCta;
     |LEE 101#1=;
     |SI FSalida = 0;
          #1#13 = nHojas;
          |GRABA 020#1a;
     |FINSI;
     |LIBERA #1;
|FPRC;

|PROCESO TemporalCuenta_1;
     |SI nSwOtroTC2 = 0;
          nSwOtroTC2 = 1;
          nLinea = 0;
          nHojas = 1;
          aCta = #2#16;
          aCon = #2#0;
          nPer = #2#1;
     |FINSI;
     |SI aCta ! #2#16;
          |HAZ GrabaHojas;
          nLinea = 0;
          nHojas = 1;
          aCta = #2#16;
          aCon = #2#0;
          nPer = #2#1;
     |SINO;
          |SI aCon ! #2#0;         || salto de pagina en cada convenio
               nLinea = 0;
               nHojas = nHojas + 1;
               aCon = #2#0;
               nPer = #2#1;
          |SINO;
               |SI nPer ! #2#1;    || salto de pagina en cada periodo
                    nLinea = 0;
                    nHojas = nHojas + 1;
                    nPer = #2#1;
               |FINSI;
          |FINSI;
     |FINSI;

     nLinea = nLinea + 1;
     |SI nLinea = 15;
          nLinea = 1;
          nHojas = nHojas + 1;
     |FINSI;
|FPRC;

|DEFBUCLE TemporalCuenta;
 #2#1;
 ;
 "               ", "       ", 000000, "            ", "   ", 000, "  ", "  ";
 "zzzzzzzzzzzzzzz", "zzzzzzz", 999999, "zzzzzzzzzzzz", "zzz", 999, "zz", "zz";
 ;
 NULL, TemporalCuenta_1;
|FIN;

|PROCESO CuentaHojas;
     nSwOtroTC2 = 0;
     |ABRE #1;
     |BUCLE TemporalCuenta;
     |HAZ GrabaHojas;
     |CIERRA #1;
|FPRC;
____________________________________/ IMPRESION TC2 DESDE TEMPORAL
|PROCESO &Totaliza;
     |SI nSwVacia4 = 1;  #2#4 = aBas;   |FINSI;
     |SI nSwVacia5 = 1;  #2#5 = aDed;   |FINSI;
     |SI nSwVacia10= 1;  #2#10 = nImp;  |FINSI;
     |SI nSwVacia14= 1;  #2#14 = nDed;  |FINSI;

     |SI #2#4 = "00";
          enB01 = enB01 + #2#10;
          enB02 = enB02 + #2#10;
     |FINSI;
     |SI #2#4 = "20";
          enB21 = enB21 + #2#10;
          enB22 = enB22 + #2#10;
     |FINSI;

     |SI #2#4 = "01";    enB01 = enB01 + #2#10;   |FINSI;
     |SI #2#4 = "02";    enB02 = enB02 + #2#10;   |FINSI;
     |SI #2#4 = "42";    enB02 = enB02 + #2#10;   |FINSI;
     |SI #2#4 = "10";    enB10 = enB10 + #2#10;   |FINSI;
     |SI #2#4 = "11";    enB11 = enB11 + #2#10;   |FINSI;
     |SI #2#4 = "21";    enB21 = enB21 + #2#10;   |FINSI;
     |SI #2#4 = "22";    enB22 = enB22 + #2#10;   |FINSI;

     |SI #2#5 = "01";    enD01 = enD01 + #2#14;   |FINSI;
     |SI #2#5 = "03";    enD03 = enD03 + #2#14;   |FINSI;
     |SI #2#5 = "02";|O #2#5 = "06";|O #2#5 = "14";|O #2#5 = "17";
          enD02 = enD02 + #2#14;
     |FINSI;
     |SI #2#5 = "07";|O #2#5 = "10";|O #2#5 = "11";|O #2#5 = "13";
     |O #2#5 = "16"; |O #2#5 = "22"; |O #2#5 = "23"; |O #2#5 = "25";
          enD07 = enD07 + #2#14;
     |FINSI;
     |SI #2#5 = "09";    enD09 = enD09 + #2#14;   |FINSI;

     |SI nSwVacia4 = 1;  #2#4 = "";     |FINSI;
     |SI nSwVacia5 = 1;  #2#5 = "";     |FINSI;
     |SI nSwVacia10= 1;  #2#10 = 0;     |FINSI;
     |SI nSwVacia14= 1;  #2#14 = 0;     |FINSI;
|FPRC;

|PROCESO &Inicializa;
     nLinea = 0;
     enB01 = 0;
     enB02 = 0;
     enB10 = 0;
     enB11 = 0;
     enB21 = 0;
     enB22 = 0;
     enD01 = 0;
     enD02 = 0;
     enD03 = 0;
     enD07 = 0;
     enD09 = 0;
|FPRC;

|PROCESO SaltaPagina;
     |PIEINF;
     |FININF;
     enPagina = enPagina + 1;
     |INFOR "nomtc200";
|FPRC;

|PROCESO nomtc202_1;
     |SI #2#5 > "40";         || bonificaciones retroactivas
          aAlfa1 = #2#15;     aAlfa1 = aAlfa1 % 102;
          |SI aAlfa1 = "56";  #2#5 = "06";   |FINSI;
          |SI aAlfa1 = "57";  #2#5 = "07";   |FINSI;
          |SI aAlfa1 = "58";  #2#5 = "16";   |FINSI;
          |SI aAlfa1 = "59";  #2#5 = "17";   |FINSI;

          aAlfa2 = #2#15;     aAlfa2 = aAlfa2 % 3;
          aAlfa2 = "55" + aAlfa2;
          #2#15 = aAlfa2;
     |FINSI;

     |SI nSwOtroTC2 = 0;
          nSwOtroTC2 = 1;
          aCon = #2#0;
          nPer = #2#1;
          |HAZ Inicializa;
          |INFOR "nomtc200";
     |FINSI;
     |SI aCon ! #2#0;         || salto de pagina en cada convenio
          |HAZ SaltaPagina;
          aCon = #2#0;
          nPer = #2#1;
     |SINO;
          |SI nPer ! #2#1;    || salto de pagina en cada periodo
               |HAZ SaltaPagina;
               nPer = #2#1;
          |FINSI;
     |FINSI;

     nLinea = nLinea + 1;
     |SI nLinea = 15;
          nLinea = 1;
     |FINSI;

     nSwVacia2 = 0; nSwVacia3 = 0; nSwVacia4 = 0; nSwVacia5 = 0;
     nSwVacia6 = 0; nSwVacia7 = 0; nSwVacia8 = 0; nSwVacia9 = 0;
     nSwVacia10= 0; nSwVacia11= 0; nSwVacia12= 0; nSwVacia13= 0;
     nSwVacia14= 0; nSwVacia15= 0;

     |SI nLinea ! 1;|Y aApe = #2#6;|Y aIde = #2#7;
                    |Y aNif = #2#8;|Y aNSS = #2#2;
          #2#6 = "";                         nSwVacia6 = 1;
          #2#7 = "";                         nSwVacia7 = 1;
          #2#8 = "";                         nSwVacia8 = 1;
          #2#2 = "";                         nSwVacia2 = 1;
          |SI aEpi = #2#3;    #2#3 = "";      nSwVacia3 = 1; |FINSI;
          |SI nCto = #2#12;   #2#12 = 0;     nSwVacia12= 1; |FINSI;
          |SI aBas = #2#4;    #2#4 = "";     nSwVacia4 = 1; |FINSI;
          |SI aDed = #2#5;    #2#5 = "";     nSwVacia5 = 1; |FINSI;
          |SI nDib = #2#9;    #2#9 = 0;      nSwVacia9 = 1; |FINSI;
          |SI nImp = #2#10;   #2#10 = 0;     nSwVacia10= 1; |FINSI;
          |SI aSit = #2#11;   #2#11 = "";    nSwVacia11= 1; |FINSI;
          |SI nDid = #2#13;   #2#13 = 0;     nSwVacia13= 1; |FINSI;
          |SI nDed = #2#14;   #2#14 = 0;     nSwVacia14= 1; |FINSI;
          |SI nFec = #2#15;   #2#15 = 0;     nSwVacia15= 1; |FINSI;
     |FINSI;

     |SI #2#4 = "01";
          aEpi = "";      || para que en la linea 01 no aparezca el epigrafe
          #2#3 = "";
     |FINSI;

     |PRINT;

     |SI nSwVacia2 = 0;  aNSS = #2#2;   |FINSI;
     |SI nSwVacia3 = 0;  aEpi = #2#3;   |FINSI;
     |SI nSwVacia12= 0;  nCto = #2#12;  |FINSI;
     |SI nSwVacia4 = 0;  aBas = #2#4;   |FINSI;
     |SI nSwVacia5 = 0;  aDed = #2#5;   |FINSI;
     |SI nSwVacia6 = 0;  aApe = #2#6;   |FINSI;
     |SI nSwVacia7 = 0;  aIde = #2#7;   |FINSI;
     |SI nSwVacia8 = 0;  aNif = #2#8;   |FINSI;
     |SI nSwVacia9 = 0;  nDib = #2#9;   |FINSI;
     |SI nSwVacia10= 0;  nImp = #2#10;  |FINSI;
     |SI nSwVacia11= 0;  aSit = #2#11;  |FINSI;
     |SI nSwVacia13= 0;  nDid = #2#13;  |FINSI;
     |SI nSwVacia14= 0;  nDed = #2#14;  |FINSI;
     |SI nSwVacia15= 0;  nFec = #2#15;  |FINSI;
|FPRC;

|DEFBUCLE nomtc202; || cotiz.,convenio,periodo,NAFSS,cto.,base,deduccion
 #2#1;
 ;
 #1#0, "       ", 000000, "            ", "   ", 000, "  ", "  ";
 #1#0, "zzzzzzz", 999999, "zzzzzzzzzzzz", "zzz", 999, "zz", "zz";
 ;
 NULL, nomtc202_1;
|FIN;

|PROCESO nomtc201_1;
     |INFOR "nomtc200";
     |SI FSalida ! 0;
          |MENAV "0000Informe [nomtc200] inaccesible o inexistente ...";
          |ERROR10;
          |ACABA;
     |FINSI;
     |FININF;

     nSwOtroTC2 = 0;
     enPagina = 1;
     |BUCLE nomtc202;
     |SI nSwOtroTC2 = 1;
          |PIEINF;
          |FININF;
     |FINSI;
|FPRC;

|DEFBUCLE nomtc201;
 #1#1;
 ;
 "               ";
 "zzzzzzzzzzzzzzz";
 ;
 NULL, nomtc201_1;
|FIN;

|PROCESO ImprimeTC2;
     |BUCLE nomtc201;
|FPRC;
____________________________________/ REPASA TC2
|PROCESO UnificaBasesTC2;
     |ABRE #2;
     |LEE 000#2p;
     |PARA; |SI FSalida = 0; |HACIENDO;
          |SI #2#4 = "01";         || guarda clave de c.comunes
               nBorrados = 0;
               aCta = #2#16;
               aCon = #2#0;
               nPer = #2#1;
               aNSS = #2#2;
               aEpi = #2#3;
               nCto = #2#12;
               nImp = #2#10;
          |FINSI;
          |SI #2#4 = "02";         || lo compara con el de c.profesionales
               |SI aCta = #2#16;|Y aCon = #2#0;|Y nPer = #2#1; |Y aNSS = #2#2;
                                |Y aEpi = #2#3;|Y nCto = #2#12;|Y nImp = #2#10;
                    nSwNoGrabes = 0;
                    nBorrados = nBorrados + 1;
                    |SI nBorrados = 1;
                         |SALVA_DATOS #2;
                         #2#4 = "01";
                         #2#5 = "  ";
                         |BORRA 020#2c;
                         |REPON_DATOS #2;
                    |SINO;
                         #2#10 = 0;     || quita importe
                    |FINSI;
                    |BORRA 020#2c;      || borra c.profesionales 02
                    #2#4 = "00";
                    |GRABA 020#2n; || graba c.iguales 00
               |FINSI;
          |FINSI;
          |LEE 000#2s;
     |SIGUE;
     |CIERRA #2;
|FPRC;

|PROCESO UnificaBasesTC2_BA20;
     |ABRE #2;
     |LEE 000#2p;
     |PARA; |SI FSalida = 0; |HACIENDO;
          |SI #2#4 = "21";         || guarda clave de c.comunes
               nBorrados = 0;
               aCta = #2#16;
               aCon = #2#0;
               nPer = #2#1;
               aNSS = #2#2;
               aEpi = #2#3;
               nCto = #2#12;
               nImp = #2#10;
          |FINSI;
          |SI #2#4 = "22";         || lo compara con el de c.profesionales
               |SI aCta = #2#16;|Y aCon = #2#0;|Y nPer = #2#1; |Y aNSS = #2#2;
                                |Y aEpi = #2#3;|Y nCto = #2#12;|Y nImp = #2#10;
                    nSwNoGrabes = 0;
                    nBorrados = nBorrados + 1;
                    |SI nBorrados = 1;
                         |SALVA_DATOS #2;
                         #2#4 = "21";
                         #2#5 = "  ";
                         |BORRA 020#2c;
                         |REPON_DATOS #2;
                    |SINO;
                         #2#10 = 0;     || quita importe
                    |FINSI;
                    |BORRA 020#2c;      || borra c.profesionales 22
                    #2#4 = "20";
                    |GRABA 020#2n; || graba c.iguales 20
               |FINSI;
          |FINSI;
          |LEE 000#2s;
     |SIGUE;
     |CIERRA #2;
|FPRC;

____________________________________/ CARGA TC2 DESDE CENDAR
|PROCESO TipoLiquidacion;
     #1#12 = #102#4 % 5903;
     |SI #1#12 = "L00";  #1#12 = "000"; |FINSI;
     |SI #1#12 = "L02";  #1#12 = "002"; |FINSI;
     |SI #1#12 = "L03";  #1#12 = "003"; |FINSI;
     |SI #1#12 = "L04";  #1#12 = "004"; |FINSI;
     |SI #1#12 = "L07";  #1#12 = "007"; |FINSI;
     |SI #1#12 = "L09";  #1#12 = "009"; |FINSI;
     |SI #1#12 = "L10";  #1#12 = "010"; |FINSI;
     |SI #1#12 = "L11";  #1#12 = "011"; |FINSI;
     |SI #1#12 = "L13";  #1#12 = "013"; |FINSI;
     |SI #1#12 = "A70";  #1#12 = "170"; |FINSI;
     |SI #1#12 = "A71";  #1#12 = "171"; |FINSI;
     |SI #1#12 = "A72";  #1#12 = "172"; |FINSI;
     |SI #1#12 = "A73";  #1#12 = "173"; |FINSI;
     |SI #1#12 = "A74";  #1#12 = "174"; |FINSI;
     |SI #1#12 = "A75";  #1#12 = "175"; |FINSI;
     |SI #1#12 = "A76";  #1#12 = "176"; |FINSI;
     |SI #1#12 = "TP1";  #1#12 = "201"; |FINSI;
     |SI #1#12 = "TP2";  #1#12 = "202"; |FINSI;

     |SI aFuente = "A";|O aFuente = "T";     || atrasos es el L03
          #1#12 = "003";
     |FINSI;
     |SI aFuente = "C";                      || complementarias es el L03
          #1#12 = "009";
     |FINSI;
|FPRC;

|PROCESO DatosEspecial;
     #1#0 = #54#10;                     || cod.cta.cotizacion
     #1#2 = #54#3;                      || domicilio
     #1#3 = #54#4;                      || numero
     #1#4 = #54#5;                      || piso
     #1#5 = #54#6;                      || localidad
     #1#6 = #54#7;                      || cod.provincia
     #1#7 = #54#8;                      || cod.poblacion
     #1#8 = #54#9;                      || provincia
|FPRC;

|PROCESO DatosCentro;
     #1#0 = #53#10;                     || cod.cta.cotizacion
     #1#2 = #53#3;                      || domicilio
     #1#3 = #53#4;                      || numero
     #1#4 = #53#5;                      || piso
     #1#5 = #53#6;                      || localidad
     #1#6 = #53#7;                      || cod.provincia
     #1#7 = #53#8;                      || cod.poblacion
     #1#8 = #53#9;                      || provincia
|FPRC;

|PROCESO DatosEmpresa;
     #1#0 = #51#13;                     || cod.cta.cotizacion
     #1#2 = #51#3;                      || domicilio
     #1#3 = #51#4;                      || numero
     #1#4 = #51#5;                      || piso
     #1#5 = #51#6;                      || localidad
     #1#6 = #51#7;                      || cod.provincia
     #1#7 = #51#8;                      || cod.poblacion
     #1#8 = #51#9;                      || provincia
|FPRC;

|PROCESO CargaDatosEmpresa;
     |SELECT #1#51;
     #51#0 = nCodEmpre;
     |LEE 020#51=;

     aLibre = #102#4 % 6402;       || centro principal
     aReser = #102#4 % 6602;       || centro especial
     |SI aLibre = "00";
          |HAZ DatosEmpresa;
     |SINO;
          |SI aReser = "00";
               |DDEFECTO #53;
               #53#0 = nCodEmpre;
               #53#1 = aLibre;
               |LEE 000#53=;
               |SI FSalida ! 0;
                    aAlfa1 = "0000Centro de Trabajo [" + #53#0 + "." + #53#1 + "] inaccesible ...";
                    |MENAV aAlfa1;
               |FINSI;
               |HAZ DatosCentro;
          |SINO;
               |DDEFECTO #54;
               #54#0 = nCodEmpre;
               #54#13 = aLibre;
               #54#1 = aReser;
               |LEE 000#54=;
               |SI FSalida ! 0;
                    aAlfa1 = "0000Centro Especial [" + #54#0 + "." + #54#13 + "." + #54#1 + "] inaccesible ...";
                    |MENAV aAlfa1;
               |FINSI;
               |HAZ DatosEspecial;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO BuscaCodigo;
     nCodEmpre = 0;
     |SELECT #4#51;
     #51#13 = #102#4 % 4011;
     |LEE 000#51=;
     |SI FSalida ! 0;
          aAlfa1 = "0000Cod.Cta.Cotiz.[" + #51#13 + "] inaccesible ...";
          |MENAV aAlfa1;
     |SINO;
          nCodEmpre = #51#0;
     |FINSI;
|FPRC;

|PROCESO parcheformacion;
     |SI aNumer = "11";  || estoy en formacion con bonif horas (CD11)
          #2#4 = "22";
          |LEE 101#2=;
          |SI FSalida = 0;    || quiere decir que existe, es decir que estoy
               #2#4 = "22";   || en formacion y maternidad todo el mes, la linea
          |SINO;              || BA02 no existe, tengo que buscar la BA22
               #2#4 = "02";   || sino busco la BA02 y chim pum (29.06.2005)
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO silcon03_1;
     |SI #102#3 = "EMP";
          |HAZ GrabaTrabaja2;
          nSwTra = 0;
          nTraba = 0;
          aAlfa1 = #102#4 % 5104;  nAny = aAlfa1;      || guarda a¤o

          aAbreviado = #102#4 % 3401;                  || si es abreviado
          |SI aAbreviado = "S";    |ACABA;   |FINSI;

          |DDEFECTO #1;
          |HAZ BuscaCodigo;
          |HAZ CargaDatosEmpresa;
          #1#1 = #51#2;                 || nombre empresa
          #1#9 = #102#4 % 1601;         || tipo ident.empresario
          #1#10 = #102#4 % 2410;        || nro. ident.empresario
          #1#11 = 0;                    || nro. trabajadores (Borrador)
          |HAZ TipoLiquidacion;         || tipo de liquidacion
          nNoSigas = 0;
          ||SI #1#12 = "011";|Y #0#12 = "R";  nNoSigas = 1;  |ACABA;   |FINSI;
          ||SI #1#12 ! "011";|Y #0#12 = "E";  nNoSigas = 1;  |ACABA;   |FINSI;
          #1#13 = 0;                    || total paginas (Borrador)
          #1#14 = nCodEmpre;            || codigo empresa
          #1#15 = aLibre;               || codigo centro
          #1#16 = aReser;               || codigo centro especial
          aRegimen = #102#4 % 0203;
          |GRABA 000#1n;
          |SI FSalida ! 0;
               |SI FSalida = 100;
                    aAlfa1 = "0000EMP: Cod.Cta.Cotizacion duplicado [" + #1#0 + "]";
               |SINO;
                    aAlfa1 = "0000EMP: Problemas en la preparacion de [" + #1#0 + "]";
               |FINSI;
               |MENAV aAlfa1;
          |FINSI;
     |FINSI;

     |SI aAbreviado = "S";    |ACABA;   |FINSI;
     |SI nNoSigas = 1;        |ACABA;   |FINSI;

     |SI #102#3 = "TRA";
          nSwTra = 1;
          |DDEFECTO #2;
          #2#16 = #1#0;                 || cod.cta.cotizacion
          #2#2 = #102#4 % 0112;         || NAFSS
          #2#7 = #102#4 % 1301;         || tipo identificador
          #2#8 = #102#4 % 2110;         || NIF/NIE/Pasaporte
          aCtaGraba = #1#0;
          nTraba = nTraba + 1;
     |FINSI;
     |SI #102#3 = "AYN";|Y nSwTra = 1;
          #2#6 = #102#4 % 5605;         || apellidos y nombre abreviado
     |FINSI;
     |SI #102#3 = "DAT";|Y nSwTra = 1;
          #2#0 = #102#4 % 5407;                        || codigo convenio
          aAlfa1 = #102#4 % 0402;  nNume1 = aAlfa1;
          #2#1 = (nNume1 * 10000) + nAny;              || periodo
          |SI #0#4 [ 2006;
               aAlfa1 = #102#4 % 3603;
          |SINO;
               |SI #0#4 [ 2009;
                    aAlfa1 = #102#4 % 4203;
               |SINO;
                    aAlfa1 = #102#4 % 0801;       || a partir del 2010 si tengo
                    |QBF aAlfa1;
                    |SI aAlfa1 ! "";              || IT guardo el indicador de IT
                         aAlfa1 = aAlfa1;         || en el epigrafe para que no me
                    |SINO;                        || de el error de segmento duplicado
                         aAlfa1 = #102#4 % 4203;
                    |FINSI;
               |FINSI;
          |FINSI;
          #2#3 = aAlfa1;                               || epigrafe
          aAlfa1 = #102#4 % 3303;
          #2#12 = aAlfa1;                              || contrato
          |SI #2#12 = "422"; #2#12 = "000"; |FINSI;
          aAlfa1 = #102#4 % 0701;
          #2#11 = "   ";                               || sit.especial
          |SI aAlfa1 = "P";
               #2#11 = "2 ";                           || sit.especial pluri.
          |FINSI;
          aAlfa1 = #102#4 % 1101;
          |SI aAlfa1 = "P";
               #2#11 = "5 ";                           || sit.especial ERE Par
          |FINSI;
          |SI aAlfa1 = "T";
               #2#11 = "7 ";                           || sit.especial ERE Tot
          |FINSI;
          aAlfa1 = #102#4 % 1303;  nDReal = aAlfa1;    || guarda dias reales
          aAlfa1 = #102#4 % 2904;  nDAlta = aAlfa1;    || guarda dias alta
          aAlfa1 = #102#4 % 6206;  nHoras = aAlfa1;    || guarda horas cto.
          aAlfa1 = #102#4 % 6101;  aSalar = aAlfa1;    || guarda clave salario
          aAlfa1 = #102#4 % 2204;  nPropo = aAlfa1;    || prop. tiempo parcial
          |SI aRegimen = "911"; || especial regimen del carbon
               aAlfa1 = #102#4 % 6206;  nLabor = aAlfa1;   || dias trabajados
          |FINSI;
     |FINSI;
     |SI #102#3 = "EDL";|Y nSwTra = 1;
          aClave = #102#4 % 102;   || tipo elemento
          aNumer = #102#4 % 302;   || nro. elemento
          aImpor = #102#4 % 0807;  || importe
          ||SI sw_convert = 0
              nCien = aImpor;     nCien = nCien / 100;     aImpor = nCien;
          ||    sw_convert = 1;
          ||FINSI;
          |SI aClave = "BA";
               #2#13 = 0;               || pongo a cero dias decuccion
               #2#4 = aNumer;           || clave base
               #2#5 = "  ";             || clave deduccion
               |SI aNumer = "02";|Y nBajaAlta = 1;|Y aAntEpi = #2#3;
                    #2#5 = aAntDed;          || clave deduccion
               |FINSI;
               #2#14 = 0;               || importe deduccion
               aAlfa1 = #102#4 % 2006;
               #2#15 = aAlfa1;          || fecha inicio contingencia
               |LEE 101#2=;
               |SI FSalida = 0;
                    nBajaAlta = 1;
                    |BORRA 020#2a;
               |SINO;
                    nBajaAlta = 0;
                    aAntDed = "  ";
                    aAntEpi = "";
                    #2#9 = 0;
                    #2#10 = 0;
               |FINSI;
               |LIBERA #2;
               #2#10 = #2#10 + aImpor;                 || importe base
               |SI aNumer = "01";
                    |SI aSalar = "X";|O aSalar = "T";|O aSalar = "I";|O aSalar = "P";
                         #2#9 = #2#9 + nHoras;    || horas tiempo parcial
                         |SI #0#7 = "F";
                              #2#9 = nDReal;    || finiquitos
                         |FINSI;
                    |SINO;
                         #2#9 = #2#9 + nDReal;    || dias alta para 01
                         || #2#9 = #2#9 + nDAlta;    || dias reales para 02
                         || CAMBIO POR EXP.REG.EMPLEO
                    |FINSI;
               |FINSI;
               |SI aNumer = "02";
                    |SI aSalar = "X";|O aSalar = "T";|O aSalar = "I";|O aSalar = "P";
                         #2#9 = #2#9 + nHoras;    || horas tiempo parcial
                         |SI #0#7 = "F";
                              #2#9 = nDReal;    || finiquitos
                         |FINSI;
                    |SINO;
                         #2#9 = #2#9 + nDReal;    || dias reales para 02
                    |FINSI;
                    |SI #0#4 [ 2006;
                         |SI aRegimen = "911"; |Y #2#3 ! "126";   || para regimen del carbon
                              #2#9 = nLabor;
                         |FINSI;
                    |SINO;
                         |SI aRegimen = "911"; |Y #2#3 ! "c  ";   || para regimen del carbon
                              #2#9 = nLabor;
                         |FINSI;
                    |FINSI;
               |FINSI;
               |SI aNumer = "10";|O aNumer = "11";
                    #2#9 = 0;                          || h.extras sin dias
               |FINSI;
               |SI aNumer = "21";
                    |SI aSalar = "X";|O aSalar = "T";|O aSalar = "I";|O aSalar = "P";
                         #2#9 = #2#9 + nHoras;    || horas tiempo parcial
                         |SI #0#7 = "F";
                              #2#9 = nDReal;    || finiquitos
                         |FINSI;
                    |SINO;
                         || #2#9 = #2#9 + nDAlta;    || dias alta para 01
                         #2#9 = #2#9 + nDReal;    || dias alta para 01
                    |FINSI;
               |FINSI;
               |SI aNumer = "22";
                    |SI aSalar = "X";|O aSalar = "T";|O aSalar = "I";|O aSalar = "P";
                         #2#9 = #2#9 + nHoras;    || horas tiempo parcial
                         |SI #0#7 = "F";
                              #2#9 = nDReal;    || finiquitos
                         |FINSI;
                    |SINO;
                         #2#9 = #2#9 + nDReal;    || dias reales para 02
                    |FINSI;
               |FINSI;

               |GRABA 000#2n;
               |SI FSalida ! 0;
                    |SI FSalida = 100;
                         aAlfa1 = "0000EDLBA: Duplicado ["+#2#16+"."+#2#0+"."+#2#1+"."+#2#2+"."+#2#3+"."+#2#4+"."+#2#5+"-"+#102#1+"]";
                    |SINO;
                         aAlfa1 = "0000EDLBA: Segmento ["+#2#16+"."+#2#0+"."+#2#1+"."+#2#2+"."+#2#3+"."+#2#4+"."+#2#5+"-"+#102#1+"]";
                    |FINSI;
                    |MENAV aAlfa1;
               |FINSI;
          |FINSI;
          |SI aClave = "CD";
               aAlfa1 = #102#4 % 4203;
               |QBF aAlfa1;

               nNume1 = aAlfa1;
               |SI nNume1 ! 0;
                    aAlfa1 = ("000" + aAlfa1) % -103;
               |FINSI;

               |SI aAlfa1 = "904"; aAlfa1 = "c  "; |FINSI;
               #2#3 = aAlfa1;           || epigrafe al que corresponde
               #2#5 = "  ";             || clave deduccion
               |SI nBajaAlta = 1;|Y aAntEpi = #2#3;
                    #2#5 = aAntDed;          || clave deduccion
               |FINSI;

               |SI #0#4 ] 2010;    || ya no hay ocupacion "c"
                    aAlfa1 = #102#4 % 302;
                    |SI aAlfa1 = "01"; |O aAlfa1 = "03"
                         || clave de prestacion por enf. o acc., a la fuerza
                         || en la linea de situacion "I"

                         #2#3 = "I";
                    |FINSI;
               |FINSI;

               || ATENCION: PIRULA GORDA POR CULPA DE LAS EXP.REG.EMPLEO:
               /*
               aAlfa1 = #2#11; |QBF aAlfa1;
               |SI aAlfa1 = "5"; |O aAlfa1 = "7";
                    |SI #2#4 = "21"; #2#4 = "01"; |FINSI;
                    |SI #2#4 = "22"; #2#4 = "02"; |FINSI;
               |FINSI;
               */
               || PIRULA que por cierto la quito (19.05.2004) porque desde lo
               || de dividir las bonificaciones en parte de baja y alta no
               || es necesaria, al menos de momento, porque ya no creo en el
               || lote el segmento CD02 que solo lo hacia para el TC2, ya no

               || y vuelvo a poner se¤oras y se¤ores porque resulta que si tengo
               || parte en enfermedad y parte en maternidad, la prestacion
               || que es el CD01 me lo pone en la linea de maternidad (BA22)
               || y la descogorcia, tiene que ir en la ba02 del 126 (15.04.2005)

               |SI #2#4 = "22"; |Y aNumer ! "28"; #2#4 = "02"; |FINSI;

               |HAZ parcheformacion;
               nBaseIT = 0;
               |LEE 101#2=;
               |SI FSalida = 0;
                    |BORRA 020#2a;      || la primera vez

                    || ATENCION, esto es nuevo 07.06.2005 me guardo la base IT
                    || que venia en mi segmento de IT (el que viene el BA02 en
                    || baja) y es la que pongo al reconstruirlo con la presta-
                    || cion en lugar de vovler a calcularlo ya que en atrasos
                    || esto me da problemas con los topes
                    nBaseIT = #2#10;
               |SINO;
                    |SI nBajaAlta = 0;
                         #2#5 = aNumer;           || nro. elemento
                         |LEE 101#2=;
                         |SI FSalida = 0;
                              |BORRA 020#2a;
                         |SINO;
                              #2#14 = 0;
                         |FINSI;
                    |SINO;
                         #2#14 = 0;
                    |FINSI;
               |FINSI;
               |LIBERA #2;
               #2#5 = aNumer;           || nro. elemento
               |SI #2#5 ! "02";
               /*   || atencion: lo quito a raiz de tener que desdoblar las
                    || bonificaciones 17.05.2004
                    |SI #2#4 = "21";    #2#4 = "01";   |FINSI;
                    |SI #2#4 = "22";    #2#4 = "02";   |FINSI;
               */
               |SINO;
                    |SI #2#4 = "01";    #2#4 = "21";   |FINSI;
                    |SI #2#4 = "02";    #2#4 = "22";   |FINSI;
               |FINSI;
               aAntDed = #2#5;
               aAntEpi = #2#3;
               |SI aNumer = "01";|O aNumer = "02";|O aNumer = "03";|O aNumer = "14";
                    aAlfa1 = #102#4 % 0503;
                    #2#13 = aAlfa1;     || dias deduccion solo para ILT
                    ||#2#9 = #2#13;       || dias cotizacion = dias deduccion
                    aAlfa1 = #102#4 % 3408;  || promedio ILT base ILT
                    #2#10 = aAlfa1;
                    aAlfa1 = #102#4 % 5909;  || dias cotizacion base ILT
                    nNume1 = aAlfa1;
                    || nNume1 = #2#9;

                    || bueno vamos a ver, esto es lo que habia aqui para topear
                    || las bases, lo quito en principio porque ya deben venir
                    || topeadas, me quedo con el nBaseIT del final

                    || al final no lo quito porque no me atrevo: mirar con mas calma
                    || 07/06/2006

                    |SI aFuente ! "T"; |Y aFuente ! "A"; |Y #2#12 ! 421; |Y #2#12 ! 422;
                         || no topees en formacion 05.07.2005
                         |ABRE #nomconst;
                         #nomconst#0 = #labempre#32;
                         |LEE 000#nomconst.=;
                         |SI FSalida = 0;
                              |SI nNume1 > 30;
                                   nTopeMin = #nomconst#23 * 30; || el tope min
                                   nTopeMax = #nomconst#20 * 30; || el tope max
                              |SINO;
                                   nTopeMin = #nomconst#23 * nNume1; || el tope min
                                   nTopeMax = #nomconst#20 * nNume1; || el tope max
                              |FINSI;
                              |SI aSalar = "X"; |O aSalar = "T";
                                   nNume4 = nPropo / 40;
                                   nTopeMin = nTopeMin * nNume4;
                                   nTopeMax = nTopeMax * nNume4;
                              |FINSI;
                         |FINSI;
                         |CIERRA #nomconst;

                         #2#10 = #2#10 * nNume1;  || base ILT = promedio * dias
                         |SI #2#10 > nTopeMax;
                              #2#10 = nTopeMax;
                         |FINSI;
                         |SI #2#10 < nTopeMin;
                              #2#10 = nTopeMin;
                         |FINSI;
                    |SINO;
                         || #2#10 = #2#10 * nNume1;  || base ILT = promedio * dias
                         #2#10 = nBaseIT;
                    |FINSI;
               |SINO;
                    #2#13 = 0;          || resto de tipos sin dias deduccion
               |FINSI;
               |SI aNumer = "17";       || RDL 16/2001 punto 2.2.1 >65 a¤os
                    aAlfa1 = #102#4 % 0503;
                    #2#13 = aAlfa1;     || dias deduccion para clave 17
               |FINSI;
               #2#14 = #2#14 + aImpor;  || importe deduccion
               aAlfa1 = #102#4 % 2006;
               #2#15 = aAlfa1;          || fecha inicio deduccion
               |GRABA 000#2n;
               |SI FSalida ! 0;
                    |SI FSalida = 100;
                         aAlfa1 = "0000EDLCD: Duplicado ["+#2#16+"."+#2#0+"."+#2#1+"."+#2#2+"."+#2#3+"."+#2#4+"."+#2#5+"-"+#102#1+"]";
                    |SINO;
                         aAlfa1 = "0000EDLCD: Segmento ["+#2#16+"."+#2#0+"."+#2#1+"."+#2#2+"."+#2#3+"."+#2#4+"."+#2#5+"-"+#102#1+"]";
                    |FINSI;
                    |MENAV aAlfa1;
               |FINSI;
          |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE silcon03;
 #102#1;
 ;
     1,     1;
 99999, 99999;
 ;
 NULL, silcon03_1;
|FIN;

|PROCESO CargaTC2;
     |NOME_DAT #101 eaTempo1;                || lotes cabs.
     |NOME_DAT #102 eaTempo2;                || lotes lins.
     |NOME_DAT #1 eaTempo3;   |DELINDEX #1;  || TC2 cabs.
     |NOME_DAT #2 eaTempo4;   |DELINDEX #2;  || TC2 lins.
     |ABRE #1;
     |ABRE #2;
     |ABRE #51;
     |ABRE #53;
     |ABRE #54;
     |BUCLE silcon03;
     |HAZ GrabaTrabaja2;
     |CIERRA #1;
     |CIERRA #2;
     |CIERRA #51;
     |CIERRA #53;
     |CIERRA #54;
|FPRC;
____________________________________/ GRABACION DE TEMPORAL EN CENDAR
|PROCESO Cendar;
     eaFuente = aFuente;
     enTipoAt = #0#8;
     enDesEmp = #0#0;
     enHasEmp = #0#1;
     enDesCen = #0#2;
     enHasCen = #0#3;
     enAny = #0#4;
     enDesMes = #0#5;
     enHasMes = #0#9;
     eaTipoLi = #0#7;

     aAlfa1 = PARAMETRO;
     |QBF aAlfa1;
     |SI aAlfa1 = "M"; |O aAlfa1 = "H";   || ojo, que esto despues se usa con
          enTipoAt = 0; || las bonif formacion y si se quita dara problemas
     |FINSI;

     |SUB_EJECUTA "silcon09.tab";
|FPRC;
____________________________________/ PROCESO PRINCIPAL
|PROCESO MiraSiHay;
     nSwSiHay = 0;
     |ABRE #2;
     |LEE 000#2p;
     |SI FSalida = 0;
          nSwSiHay = 1;
     |FINSI;
     |CIERRA #2;
|FPRC;

|PROCESO NombreTemporales;
     aAlfa1 = Usuario;   |QBF aAlfa1;

     eaTempo1 = "d" + aAlfa1;      || lotes cabs.
     eaTempo2 = "e" + aAlfa1;      || lotes lins.
     eaTempo3 = "u" + aAlfa1;      || TC2 cabs.
     eaTempo4 = "y" + aAlfa1;      || TC2 lins.
|FPRC;

|PROCESO TC2Fini;
     |SI swFiniqRaro2 = 1;
          swSegundoLote = 1;
          #0#5 = 1;           || Enero
          #0#4 = #0#4 + 1;    || A¤o
          swCalDos = 1;
          |VETE Comienzo;
     |FINSI;
|FPRC;

|PROCESO Tipo3; |TIPO 3;
     |SI nSwCorta ! 0;
          |MENAV "    Proceso abortado ...";
          |ACABA;
     |FINSI;

     |IMPRE 0;
     |SI FSalida = 0;
          |ET Comienzo;
          |HAZ NombreTemporales;
          |HAZ Cendar;
          |HAZ CargaTC2;
          |HAZ MiraSiHay;
          |SI nSwSiHay = 1;
               |SI #0#11 = "B";
                    eaTitBorra = "Carga Inicial.";
                    |HAZ Borrador;
               |FINSI;
               |HAZ UnificaBasesTC2;
               |HAZ UnificaBasesTC2_BA20;
               |HAZ CuentaHojas;
               ||HAZ CuentaTrabajadores;
               |SI #0#11 = "B";
                    eaTitBorra = "Carga Definitiva.";
                    |HAZ Borrador;
               |SINO;
                    |HAZ ImprimeTC2;
               |FINSI;
               |DELINDEX #101;          || lotes cabs.
               |DELINDEX #102;          || lotes lins.
               |DELINDEX #1;            || TC2 cabs.
               |DELINDEX #2;            || TC2 lins.
               |SI swSegundoLote ! 1;
                    |HAZ TC2Fini;
               |FINSI;
          |SINO;
               ||SI swSegundoLote = 1;
                    ||HAZ TC2Fini;
               ||SINO;
                    |MENAV "0000Seleccion vacia ...";
               ||FINSI;
          |FINSI;
     |SINO;
          |MENAV "    Proceso abortado ...";
     |FINSI;
     |FINIMP;
|FPRC;
