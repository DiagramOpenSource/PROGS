|FICHEROS;
     nompru03 #0;
     nompru01 #1;
|FIN;

|VARIABLES;
     nAciertos = 0;
     aAlfa = "";
     nCampo = 0;
     nCampo1 = 0;
     nCampo2 = 0;
     nCampo3 = 0;
     nCampo15 = 0;
     nCampoCol = 0;
     nCampoAc = 0;
     nPartido = 0;
     nUnos = 0;
     nEquis = 0;
     nDoses = 0;
     aPrimero = "";
     aSegundo = "";
     aTercero = "";
|FIN;

|PROCESO nompru01_1;
     nCampo = nPartido + 1;
     |SI #1nCampo = "1";
          nUnos = nUnos + 1;
     |FINSI;
     |SI #1nCampo = "X";
          nEquis = nEquis + 1;
     |FINSI;
     |SI #1nCampo = "2";
          nDoses = nDoses + 1;
     |FINSI;
|FPRC;

|DEFBUCLE nompru01_1;
     #1#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, nompru01_1;
|FIN;

|PROCESO Busca;
     |SI FSalida ! 14; |ACABA; |FINSI;
     |PARA nPartido = 1; |SI nPartido [ 15; |HACIENDO nPartido = nPartido + 1;
          nUnos = 0;
          nEquis = 0;
          nDoses = 0;
          |BUCLE nompru01_1;
          |SI nUnos ] nEquis; |Y nUnos ] nDoses;
               aPrimero = "1";
               |SI nEquis ] nDoses;
                    aSegundo = "X";
                    aTercero = "2";
               |SINO;
                    aSegundo = "2";
                    aTercero = "X";
               |FINSI;
          |FINSI;
          |SI nEquis ] nUnos; |Y nEquis ] nDoses;
               aPrimero = "X";
               |SI nUnos ] nDoses;
                    aSegundo = "1";
                    aTercero = "2";
               |SINO;
                    aSegundo = "2";
                    aTercero = "1";
               |FINSI;
          |FINSI;
          |SI nDoses ] nUnos; |Y nDoses ] nEquis;
               aPrimero = "2";
               |SI nUnos ] nEquis;
                    aSegundo = "1";
                    aTercero = "X";
               |SINO;
                    aSegundo = "X";
                    aTercero = "1";
               |FINSI;
          |FINSI;
          nCampo1 = ((nPartido - 1) * 3) + 1;
          nCampo2 = ((nPartido - 1) * 3) + 2;
          nCampo3 = ((nPartido - 1) * 3) + 3;
          |SI aPrimero = "1";
               #0nCampo1 = "X";
          |FINSI;
          |SI aPrimero = "X";
               #0nCampo2 = "X";
          |FINSI;
          |SI aPrimero = "2";
               #0nCampo3 = "X";
          |FINSI;

          |SI aSegundo = "1";
               #0nCampo1 = "X";
          |FINSI;
          |SI aSegundo = "X";
               #0nCampo2 = "X";
          |FINSI;
          |SI aSegundo = "2";
               #0nCampo3 = "X";
          |FINSI;

          |PINTA #0nCampo1;
          |PINTA #0nCampo2;
          |PINTA #0nCampo3;
     |SIGUE;
|FPRC;

|PROCESO nompru01;
     nAciertos = 0;
     aAlfa = #1#0;
     |PINTA 2043, aAlfa;
     aAlfa = #1#1;
     |PINTA 2143, "  ";
     |PINTA 2143, aAlfa;
     |PARA nCampo = 1; |SI nCampo [ 43; |HACIENDO nCampo = nCampo + 3;
          nCampo1 = nCampo;
          nCampo2 = nCampo + 1;
          nCampo3 = nCampo + 2;
          nCampoCol = (nCampo3 / 3) + 1;
          |SI #0nCampo1 = "X"; |Y #1nCampoCol = "1";
               nAciertos = nAciertos + 1;
          |FINSI;
          |SI #0nCampo2 = "X"; |Y #1nCampoCol = "X";
               nAciertos = nAciertos + 1;
          |FINSI;
          |SI #0nCampo3 = "X"; |Y #1nCampoCol = "2";
               nAciertos = nAciertos + 1;
          |FINSI;
          |SI nCampo = 40; |Y nAciertos ! 14;
               |SAL;     || no compruebes el 15 si no tienes ya 14
          |FINSI;
     |SIGUE;
     |SI nAciertos > 7;
          |SI #1#0 = "00/01"; nCampo15 = 46; |FINSI;
          |SI #1#0 = "01/02"; nCampo15 = 54; |FINSI;
          |SI #1#0 = "02/03"; nCampo15 = 62; |FINSI;
          |SI #1#0 = "03/04"; nCampo15 = 70; |FINSI;
          |SI #1#0 = "04/05"; nCampo15 = 78; |FINSI;
          |SI #1#0 = "05/06"; nCampo15 = 86; |FINSI;
          |SI #1#0 = "06/07"; nCampo15 = 94; |FINSI;
          |SI #1#0 = "07/08"; nCampo15 = 102; |FINSI;

          nCampoAc = nCampo15 + 15 - nAciertos;

          #0nCampoAc = #0nCampoAc + 1;
          |PINTA #0nCampoAc;

          nCampoAc = 126 + 15 - nAciertos;
          #0nCampoAc = #0nCampoAc + 1;
          |PINTA #0nCampoAc;
     |FINSI;
|FPRC;

|DEFBUCLE nompru01;
     #1#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, nompru01;
|FIN;

|PROCESO Calculo;
     |SI FSalida = 13;
          |CONFI "    SContinuar?";
          |SI FSalida = 0;
               |PARA nCampo = 46; |SI nCampo [ 133; |HACIENDO nCampo = nCampo + 1;
                    #0nCampo = 0;
                    |SI nCampo < 110; |Y nCampo > 125;
                         |PINTA #0nCampo;
                    |FINSI;
               |SIGUE;
               |BUCLE nompru01;
               |GRABA 000#0a;
          |FINSI;
     |FINSI;
|FPRC;
