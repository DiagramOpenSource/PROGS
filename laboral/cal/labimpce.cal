|FICHEROS;
     labimpce #0;

     labhisce #10,MANTE;

     labcerti #101;
     labempre #102;
     labtraba #103;
     labcerag #104;

     labcontr;
     labnct10;
     labcentr;
|FIN;

|VARIABLES;
     aAntTipo = "";
     aAlfa    = "";
     aAlfa1   = "";
     aAlfa2   = "";
     aDTipo   = "";
     aHTipo   = "";

     nCont    = 0;

     nCalc    = 0;
     nCalc1   = 0;
     nCalc2   = 0;
     nCalc3   = 0;
     nCalc4   = 0;

     nNumDato = 0;
     nCmp     = 0;

     aTipAnt = "";
     nEmpAnt = 0;
     nTraAnt = 0;
     nLinAnt = 0;
     swSal = 0;

     aParam = "";
     aEmision = "";
     aRegimen = "";
     &eaFichero = "";
     nPaso = 0;
     fFecha = @;
     aFecha = "";
     aHora = "";
     nCuantos = 0;
     aArchivo = "";
     aPath = "";
     nCanal = 0;
     aLinea = "";
     &enCanal = 0;
     nNume1 = 0;
     nNume2 = 0;
     nPara1 = 0;

     aReg = "";
     nEmp = 0;
     nTra = 0;
     &aAntCCC = "";
     &swNetContrata = 0;
     aNetContrata = "";
     aUsuario = "";

     aMensa = "";
     aEsc1 = "";
     aEsc2 = "";
     aTecla = "";

     fFechaBaja = @;
     &nArchi = 0;
     &eaOrig = "";
     &eaObservaciones = "";
     &enMes = 0;
     &enAny = 0;
     &eaVengoDe = "";
     &swSigue = 0;
     &enSwErrorRecDoc = 0;
     &enSwDesde9 = 0;
     &enSwMeteGrupoSubTipo = 0;

     aFichHuella = "";
     aFichCertif = "";
     aNif = "";
     aFichero = "";
     aFichero1 = "";
     aCCC = "";
     aFechaAlta = "";
     aRutaBAT = "";
     nHandleBAT = 0;
     aUnidad = "";
     aResto = "";
     aRuta = "";
     {1}aContiene = "";

     &eaCadena = "";
     &eaCadenaSal = "";
     &eaCaracter1 = "";
     &eaCaracter2 = "";
|FIN;

|PROCESO Seleccionados;
     |SI nPaso = 1;
          nCuantos = nCuantos + 1;
          aAlfa1 = #labhisce#1; aAlfa1 = ("00000" + aAlfa1) % -105;
          aAlfa2 = #labhisce#2; aAlfa2 = ("00000" + aAlfa2) % -105;
     |FINSI;
     |SI nPaso = 2;
          aAlfa1 = ("00000" + #labhisce#1) % -105;
          aAlfa2 = ("00000" + #labhisce#2) % -105;
          aAlfa = aAlfa1 + "." + aAlfa2;
          aMensa = "    Creando certificado en xml. Trabajador " + aAlfa;
          |MENSA aMensa;
          aAlfa = aAlfa - ".";
          || la variable aNetContrata vale algo si estoy desde NetContrata
          |SI #labhisce#0 = "G"; aAlfa = "labcerti.tab;" + aNetContrata + aAlfa + "XML"; |FINSI;
          |SI #labhisce#0 = "A"; aAlfa = "labcerag.tab;" + aNetContrata + aAlfa + "XML"; |FINSI;

          |SUB_EJECUTA aAlfa;
     |FINSI;
     |SI nPaso = 3;
          #labhisce#26 = " ";
          |GRABA 020#labhisce.a;
          |LIBERA #labhisce;
     |FINSI;
|FPRC;

|DEFBUCLE Seleccionados;
     #labhisce#4;
     26;
     "               ", aRegimen, 00001, 00001, "*";
     "zzzzzzzzzzzzzzz", aRegimen, 99999, 99999, "*";
     ;
     NULL, Seleccionados;
|FIN;

|PROCESO Cabecera;
     aArchivo = eaFichero;
     aAlfa = aArchivo % -104;
     |SI aAlfa ! ".xml";
          aArchivo = aArchivo + ".xml";
     |FINSI;
     aArchivo = aPath + aArchivo;
     |XABRE "CBW", aArchivo, nCanal;

     aLinea = "<?xml version=" + &34 + "1.0" + &34 + " encoding=";
     aLinea = aLinea + &34 + "ISO-8859-1" + &34 + " ?>" + &13 + &10;
     |XGRABA nCanal, aLinea;
     aLinea = "<Certificado_empresa xmlns:xsi=" + &34;
     aLinea = aLinea + "http://www.w3.org/2001/XMLSchema-instance" + &34;
     aLinea = aLinea + ">" + &13 + &10;
     |XGRABA nCanal, aLinea;
|FPRC;

|PROCESO Pie;
     nCanal = enCanal;
     aLinea = "  </Cuenta_cotizacion>" + &13 + &10;
     |XGRABA nCanal, aLinea;

     aLinea = "</Certificado_empresa>" + &13 + &10;
     |XGRABA nCanal, aLinea;

     |XCIERRA nCanal;
|FPRC;

|PROCESO CreaDirectorio;
     |ABRE #labcontr; |LEE 000#labcontr.p; |CIERRA #labcontr;

     aPath = #labcontr#51;
     |QBF aPath;
     |SI aPath = "";
          aPath = "C:\tmp\xmlcert";
          aAlfa = "    ATENCION. No está definida la ruta para almacenar los fichero xml ";
          aAlfa = aAlfa + "en el Control de la Aplicación.";
          |MENAV aAlfa;
          aAlfa = "    Se usará por defecto la carpeta C:\tmp";
          |MENAV aAlfa;
     |SINO;
          aAlfa = aPath % -101;
          |SI aAlfa ! "/"; |Y aAlfa ! "\";
               aPath = aPath + "\";
          |FINSI;
          |SI swNetContrata = 1;
               aPath = aPath + "NETCONTRATA\CERTIFICADOS\";
          |SINO;
               aPath = aPath + "xmlcert\";
          |FINSI;
     |FINSI;

     aAlfa1 = aPath % 0;  nNume1 = aAlfa1;
     aAlfa2 = "";
     |PARA nPara1 = 1; |SI nPara1 [ nNume1; |HACIENDO nPara1 = nPara1 + 1;
          nNume2 = (nPara1 * 100) + 1;
          aAlfa1 = aPath % nNume2;
          |SI aAlfa1 = "/"; |O aAlfa1 = "\";
               |SI aAlfa2 ! "";
                    |RMKDIR aAlfa2;
               |FINSI;
          |FINSI;
          aAlfa2 = aAlfa2 + aAlfa1;
     |SIGUE;
     |SI aAlfa2 ! "";
          |RMKDIR aAlfa2;
     |FINSI;

     aRuta = aPath; |HAZ CompRuta; aPath = aRuta;
     aRutaBAT = aPath + "ejecuta.bat";
|FPRC;

/****************************************************************************/
/*                          ENVIO A NETCONTRATA                             */
/****************************************************************************/

|VARIABLES;
     aRutaErrores = "";
     aOrigen = "";
     aDestino = "";
     nSwEtiq = 0;
     aEtiqu = "";
     aError = "";
     nNumChars = 0;
     aRutaSal = "";
     aLon = "";
     nLon = 0;
     nPunt = 0;
     nPos = 0;
     aCaracter = "";
     aFich = "";
     nHandle = 0;
|FIN;

|PROCESO CompRuta;
     aRutaSal = "";
     |QBF aRuta;
     aLon = aRuta % 0;
     nLon = aLon;
     |PARA nPunt = 1; |SI nPunt [ nLon; |HACIENDO nPunt = nPunt + 1;
          nPos = (nPunt * 100) + 1;
          aCaracter = aRuta % nPos;
          |SI aCaracter = "/";
               aRutaSal = aRutaSal + "\";
          |SINO;
               aRutaSal = aRutaSal + aCaracter;
          |FINSI;
     |SIGUE;
     aCaracter = aRutaSal % -101;
     |SI aCaracter ! "\";
          aRutaSal = aRutaSal + "\";
     |FINSI;
     aRuta = aRutaSal;

     eaCadena = aRuta;
     eaCaracter1 = "/";
     eaCaracter2 = "\";
     |HAZ sustituye_plb;
     aRuta = eaCadenaSal;
|FPRC;

|PROCESO Mensaje;
/*
     |PUSHV 11151965;
     |BLANCO 12181863;
     |CUADRO 12181862;
     |ATRI S;
          ||   2         3         4         5        6
          ||   0123456789012345678901234567890123467890
     aMensa = "Pulse ENTER cuando haya validado la ope-";
     |PINTA 1320, aMensa;
     aMensa = "raci¢n y enviado  el certificado  online";
     |PINTA 1420, aMensa;
     aMensa = "para archivar los documentos recibidos o";
     |PINTA 1520, aMensa;
     aMensa = "pulse ESC para no archivar.             ";
     |PINTA 1620, aMensa;
     |ATRI 0;

     aEsc1  = &255 + "802";        || ENTER
     aEsc2  = &255 + "806";        || ESC
     |PARA; |SI; |HACIENDO;
          |LEETECLA aTecla;
          |SI aTecla = aEsc1;
               |SAL;
          |FINSI;
          |SI aTecla = aEsc2;
               |MENAV "    Se ha cancelado el envío del documento a DSArchi.";
               nArchi = 0;
               |SAL;
          |FINSI;
     |SIGUE;
     |POPV;
*/

     aAlfa = "    SDespués de enviar el certificado, pulse SI para archivar ";
     aAlfa = aAlfa + "la huella y el certif. o NO para no archivar.";
     |CONFI aAlfa;
     |SI FSalida ! 0;
          nArchi = 0;
     |FINSI;
|FPRC;

|PROCESO NombreFichero;
     aNif = #labtraba#7; |QBF aNif;

     |ABRE #labcentr;
     #labcentr#0 = #labtraba#0;
     #labcentr#1 = #labtraba#77;
     |LEE 000#labcentr.=;
     aCCC = #labcentr#10; |QBF aCCC;
     |CIERRA #labcentr;

     aFechaAlta = #labtraba#34; |QBF aFechaAlta;

     aFichero = aCCC + "_" + aNif + "_" + (aFechaAlta % -104);
     aFichero = aFichero + (aFechaAlta % 402) + (aFechaAlta % 102);
|FPRC;

|PROCESO DSArchi;
     eaVengoDe = "labimpce";
     nArchi = 0;
     |HAZ MiraSiArchiva;

     |SI nArchi ! 1;
          |ACABA;
     |FINSI;

     |HAZ Mensaje;

     |SI nArchi ! 1;
          |ACABA;
     |FINSI;

     swSigue = 1;

     aAlfa = #labcontr#63; |QBF aAlfa;
     aFichCertif = "";
     aFichHuella = "";

     aAlfa = #labcontr#63; |QBF aAlfa;
     |HAZ NombreFichero;
     aFichero = aFichero + ".pdf";
     aFichHuella = aFichero;
     aAlfa = aAlfa + "\CERTIFICA2\" + aFichero;
     |XABRE "CE", aAlfa, nHandle;
     |SI FSalida < 0;
          swSigue = 0;
     |FINSI;
     |XCIERRA nHandle;

     aAlfa = #labcontr#63; |QBF aAlfa;
     |HAZ NombreFichero;
     aFichero = aFichero + "cdo.pdf";
     aFichCertif = aFichero;
     aAlfa = aAlfa + "\CERTIFICA2\" + aFichero;
     |XABRE "CE", aAlfa, nHandle;
     |SI FSalida < 0;
          swSigue = 0;
     |FINSI;
     |XCIERRA nHandle;

     |SI swSigue = 0;
          aAlfa = "    No se han encontrado los documentos de respuesta para archivar";
          |MENAV aAlfa;
          |ACABA;
     |FINSI;

     aAlfa = #labcerti#8;
     aAlfa = aAlfa % 402;
     enMes = aAlfa;
     aAlfa = #labcerti#8;
     aAlfa = aAlfa % -104;
     enAny = aAlfa;

     || aqui le tengo que poner el nombre de los archivos para enviar uno y
     || despues el otro

     aAlfa = #labcontr#63; |QBF aAlfa;
     aAlfa = aAlfa + "\CERTIFICA2\" + aFichCertif;
     eaOrig = aAlfa;
     eaObservaciones = "Certificado de cese en empresa";
     |HAZ Archivando;

     aAlfa = #labcontr#63; |QBF aAlfa;
     aAlfa = aAlfa + "\CERTIFICA2\" + aFichHuella;
     eaOrig = aAlfa;
     eaObservaciones = "Huella del certificado de cese en empresa";
     |HAZ Archivando;

     aAlfa = ":dsarchi/dpntz001;CLI";
     |SUB_EJECUTA aAlfa;

     |SI enSwErrorRecDoc ! 1;
          aAlfa = "    Se han archivado los documentos asociados al ";
          aAlfa = aAlfa + "certificado de empresa del trabajador.";
          |MENAV aAlfa;
     |FINSI;
|FPRC;

|PROCESO EnvioNetContrata;
     || A PARTIR DE AQUI SI ME VALDRA, EL SE PONE A HACER SU NETCONTRATA
     || Y SUS COSAS

     |ABRE #labcontr; |LEE 000#labcontr.p; |CIERRA #labcontr;

     swSigue = 0;
     |SUB_EJECUTA "labnct00";
     |SI swSigue = 0;
          |ACABA;
     |FINSI;

     |SI swSigue = 2;
          aContiene = "";
          |ESPECIFICA "RBASS", aContiene;
          aAlfa = aContiene1;
          |QBF aAlfa;
          aAlfa = aAlfa + "conectass";
          #labcontr#63 = aAlfa;
     |FINSI;

     aFich = aPath + eaFichero;

     aRuta = #labcontr#51;
     |QBF aRuta;
     aAlfa = aRuta % -101;
     |SI aAlfa ! "\"; |Y aAlfa ! "/";
          aRuta = aRuta + "/";
     |FINSI;
     aRuta = aRuta + "NETCONTRATA/CERTIFICADOS";
     |RMKDIR aRuta;

     || ruta para errores
     aAlfa1 = #labcontr#63;
     |QBF aAlfa1;
     aAlfa = aAlfa1 % -101;
     |SI aAlfa ! "\"; |Y aAlfa ! "/";        || ruta de los XML
          aAlfa1 = aAlfa1 + "\";
     |FINSI;

     aAlfa = aAlfa1 + "ERRORES";
     aRutaErrores = aAlfa;
     aRuta = aRutaErrores; |HAZ CompRuta; aRutaErrores = aRuta;

     aAlfa = aRutaErrores + "ERRORESNET.XML";
     |RFBORRA aAlfa;

     |XABRE "CBW", aRutaBAT, nHandleBAT;

     aAlfa = #labcontr#63; |QBF aAlfa;
     aRuta = aAlfa; |HAZ CompRuta; aAlfa = aRuta;

     aUnidad = aAlfa % 102;
     aResto = aAlfa - aUnidad;

     aAlfa = "@echo off" + &13 + &10;
     |XGRABA nHandleBAT, aAlfa;

     aUnidad = aUnidad + &13 + &10;
     |XGRABA nHandleBAT, aUnidad;

     aAlfa = "CD " + aResto + &13 + &10;
     |XGRABA nHandleBAT, aAlfa;

     aAlfa1 = #labcontr#62; |QBF aAlfa1;
     aAlfa = "rdn.exe " + aAlfa1 + " " + aFich + " " + aRutaErrores;

     aAlfa = "start " + aAlfa + &13 + &10;
     |XGRABA nHandleBAT, aAlfa;

     |XCIERRA nHandleBAT;
     |RSYSTEM aRutaBAT;

     |HAZ DSArchi;
|FPRC;

|PROCESO Registro;
     |ABRE #labnct10;

     #labnct10#0 = #labtraba#0;
     #labnct10#1 = #labtraba#1;
     #labnct10#11 = #labtraba#77;
     #labnct10#2 = 201;
     #labnct10#3 = "Certificado de Empresa";

     fFecha = ~;
     aFecha = fFecha;
     #labnct10#4 = fFecha;         || fecha

     |HORA aHora;
     |QBF aHora;
     #labnct10#5 = aHora;          || hora

     aUsuario = Usuario % 810;
     |QBF aUsuario;
     #labnct10#6 = aUsuario;       || usuario
     #labnct10#7 = "labimpce";

     #labnct10#8 = #labtraba#45;             || tipo de contrato
     #labnct10#9 = #labtraba#36;             || fecha de alta
     #labnct10#10 = #labtraba#37;            || fecha de baja

     |GRABA 020#labnct10.n;
     |LIBERA #labnct10;
|FPRC;
/****************************************************************************/

|PROCESO XMLMultiple;
     nPaso = 1;
     nCuantos = 0;
     |BUCLE Seleccionados;

     |SI nCuantos = 0;
          |MENAV "    No se ha seleccionado ningún certificado. No se generará fichero xml.";
          |ACABA;
     |SINO;
          |SI nCuantos = 1;
               |SI swNetContrata = 1;
                    aAlfa = "    S¿Desea enviar online el certificado seleccionado?";
               |SINO;
                    aAlfa = "    S¿Desea generar el certificado seleccionado ";
                    aAlfa = aAlfa + "en formato xml?";
               |FINSI;
          |FINSI;
          |SI nCuantos > 1;
               aAlfa = "    ¿Desea generar los certificados seleccionados ";
               aAlfa = aAlfa + "en formato xml?";
          |FINSI;
          |CONFI aAlfa;
     |FINSI;
     |SI FSalida = 0;
          fFecha = ~;
          aFecha = fFecha;
          aFecha = aFecha - "." - ".";
          |HORA aHora;
          aHora = aHora - ":" - ":";

          |SI nCuantos = 1;
               eaFichero = aAlfa1 + "_" + aAlfa2 + "_cert.xml";
          |FINSI;
          |SI nCuantos > 1;
               eaFichero = "MULTI_" + aFecha + "_" + aHora + "_cert.xml";
          |FINSI;
          |HAZ CreaDirectorio;
          |HAZ Cabecera;

          nPaso = 2;
          enCanal = nCanal;
          aAntCCC = "";
          |BUCLE Seleccionados;
          |HAZ Pie;

          |SI swNetContrata = 1;
               |HAZ Registro;
               |HAZ EnvioNetContrata;
          |SINO;
               aAlfa = "    Se ha generado el fichero " + eaFichero;
               aAlfa = aAlfa + " en la carpeta " + aPath;
               |MENAV aAlfa;
          |FINSI;

          aAlfa = nCuantos;
          |SI nCuantos > 1;
               aAlfa = "    Se incluyen " + aAlfa + " certificados.";
               |MENAV aAlfa;
          |FINSI;
     |FINSI;

     nPaso = 3;
     |BUCLE Seleccionados;
|FPRC;

|PROCESO SacaDato;
   nCmp = 4; aAlfa1 = #labhisce#nCmp#;
   |PARA nCont = 0; |SI nCont [ nNumDato; |HACIENDO nCont = nCont + 1;
      aAlfa = aAlfa1 - "{{"; nCalc1 = FSalida;
      |SI nCalc1 = 0;
         |SI nCmp < 8;
            nCmp = nCmp + 1;
            aAlfa1 = #labhisce#nCmp#;
            aAlfa = aAlfa1 - "{{"; nCalc1 = FSalida;
         |SINO;
            |SAL;
         |FINSI;
      |FINSI;
      aAlfa = aAlfa1 - "}}"; nCalc2 = FSalida;
      aAlfa1 = aAlfa1 - "{{"; aAlfa1 = aAlfa1 - "}}";
   |SIGUE;

   |SI nCalc1 = 0; |Y nCalc2 = 0; aAlfa = ""; |ACABA; |FINSI;

   nCalc3 = (nCalc1 / 100) ! 0;
   nCalc4 = nCalc1 - ((nCalc3 * 100) ! 0);
   nCalc3 = nCalc3 + nCalc4;
   nCalc4 = ((nCalc2 / 100) ! 0) - nCalc3;
   |SI nCalc4 > 100;
          nCalc4 = 99;
   |FINSI;
   nCalc1 = (nCalc3 * 100) + nCalc4;
   aAlfa = aAlfa % nCalc1;
|FPRC;

|PROCESO PantaTipoG;
     |ATRI R; |PINTA 0923, " R‚gimen General "; |ATRI 0;

     |ATRI I;
     || empresa:
     aAlfa = ""; nNumDato = 0; |HAZ SacaDato;
     #labcerti#0 = aAlfa; |PINTA 1035, #labcerti#0;

     || nombre empresa
     |ABRE #labempre;
     #labempre#0 = #labcerti#0;
     |LEE 000#labempre.=;
     |SI FSalida ! 0; |DDEFECTO #labempre; |FINSI;
     |CIERRA #labempre;
     |CAMPO_ANCHO #labempre#2, 1, 38;
     |PINTA 1041, #labempre#2;

     || trabajador
     aAlfa = ""; nNumDato = 1; |HAZ SacaDato;
     #labcerti#1 = aAlfa; |PINTA 1135, #labcerti#1;

     |ABRE #labtraba;
     #labtraba#0 = #labcerti#0;
     #labtraba#1 = #labcerti#1;
     |LEE 000#labtraba.=;
     |SI FSalida ! 0; |DDEFECTO #labtraba; |FINSI;
     |CIERRA #labtraba;
     |CAMPO_ANCHO #labtraba#2, 1, 38;
     |PINTA 1141, #labtraba#2;

     || causa baja
     aAlfa = ""; nNumDato = 78; |HAZ SacaDato;
     #labcerti#78 = aAlfa; |PINTA 1342, #labcerti#78;

     || descripcion causa baja
     |CAMPO_ANCHO #labcerti#9 , 1, 33; ||PEPE
     aAlfa = ""; nNumDato = 9;  |HAZ SacaDato;
     #labcerti#9 = aAlfa; |PINTA 1345, #labcerti#9;

     || fecha suspension
     aAlfa = ""; nNumDato = 8;   |HAZ SacaDato;
     #labcerti#8 = aAlfa; |PINTA 1442, #labcerti#8;

     || duracion contrato
     aAlfa = ""; nNumDato = 84;  |HAZ SacaDato;
     #labcerti#84 = aAlfa; |PINTA 1542, #labcerti#84;

     |ATRI 0;
|FPRC;

|PROCESO PantaTipoA;
     |ATRI R; |PINTA 0923, " R‚gimen Agrario "; |ATRI 0;

     |ATRI I;
     || empresa:
     aAlfa = ""; nNumDato = 0; |HAZ SacaDato;
     #labcerti#0 = aAlfa; |PINTA 1035, #labcerti#0;

     || nombre empresa
     |ABRE #labempre;
     #labempre#0 = #labcerti#0;
     |LEE 000#labempre.=;
     |SI FSalida ! 0; |DDEFECTO #labempre; |FINSI;
     |CIERRA #labempre;
     |CAMPO_ANCHO #labempre#2, 1, 38;
     |PINTA 1041, #labempre#2;

     || trabajador
     aAlfa = ""; nNumDato = 1; |HAZ SacaDato;
     #labcerti#1 = aAlfa; |PINTA 1135, #labcerti#1;

     |ABRE #labtraba;
     #labtraba#0 = #labcerti#0;
     #labtraba#1 = #labcerti#1;
     |LEE 000#labtraba.=;
     |SI FSalida ! 0; |DDEFECTO #labtraba; |FINSI;
     |CIERRA #labtraba;
     |CAMPO_ANCHO #labtraba#2, 1, 38;
     |PINTA 1141, #labtraba#2;

     || causa baja
     aAlfa = ""; nNumDato = 98; |HAZ SacaDato;
     #labcerag#98 = aAlfa; |PINTA 1342, #labcerag#98;

     || descripcion causa baja
     |CAMPO_ANCHO #labcerag#5 , 1, 33; ||PEPE
     aAlfa = ""; nNumDato = 5;  |HAZ SacaDato;
     #labcerag#5 = aAlfa; |PINTA 1345, #labcerag#5;

     || fecha suspension
     aAlfa = ""; nNumDato = 4;   |HAZ SacaDato;
     #labcerag#4 = aAlfa; |PINTA 1442, #labcerag#4;

     || duracion contrato
     aAlfa = ""; nNumDato = 103;  |HAZ SacaDato;
     #labcerag#103 = aAlfa; |PINTA 1542, #labcerag#103;

     |ATRI 0;
|FPRC;

|PROCESO Marca;
     || Marca/Desmarca
     |SI #labhisce#26 = " ";
          #labhisce#26 = "*";
     |SINO;
          #labhisce#26 = " ";
     |FINSI;
     |PINTA #labhisce#26;
     |GRABA 020#labhisce.a;
     |LIBERA #labhisce;
     |ERROR;
|FPRC;

|PROCESO Impresion;
     || la variable aNetContrata vale algo si estoy desde NetContrata
     aAlfa =         (("00000" + #labhisce#1) % -105);
     aAlfa = aAlfa + (("00000" + #labhisce#2) % -105);
     |SI #labhisce#0 = "G"; aAlfa = "labcerti.tab;" + aNetContrata + aAlfa + "IMP"; |FINSI;
     |SI #labhisce#0 = "A"; aAlfa = "labcerag.tab;" + aNetContrata + aAlfa + "IMP"; |FINSI;

     |MENSA "    Emitiendo certificado";
     |SUB_EJECUTA aAlfa;
     |BLIN 4;
     |HAZ Ayuda;
     |ERROR;
|FPRC;

|PROCESO Edicion;
     || la variable aNetContrata vale algo si estoy desde NetContrata
     aAlfa =         (("00000" + #labhisce#1) % -105);
     aAlfa = aAlfa + (("00000" + #labhisce#2) % -105);
     |SI #labhisce#0 = "G"; aAlfa = "labcerti.tab;" + aNetContrata + aAlfa + "EDI"; |FINSI;
     |SI #labhisce#0 = "A"; aAlfa = "labcerag.tab;" + aNetContrata + aAlfa + "EDI"; |FINSI;
     |MENSA "    Edicion de certificado";
     |SUB_EJECUTA aAlfa;
     |BLIN 4;
     |HAZ Ayuda;
     |ERROR;
|FPRC;

|PROCESO XML_NetContrata;
     aReg = #labhisce#0;
     nEmp = #labhisce#1;
     nTra = #labhisce#2;

     #labhisce#26 = "*";
     |GRABA 020#labhisce.a;
     |LIBERA #labhisce;
     |HAZ XMLMultiple;
     #labhisce#0 = aReg;
     #labhisce#1 = nEmp;
     #labhisce#2 = nTra;
     |LEE 000#labhisce.=;

     |PONCONTROL 23, "si";
|FPRC;

|PROCESO Tipo11; |TIPO 11;
     swNetContrata = 0;
     aNetContrata = "";

     |SI FSalida = 10;        || impresion es el F2
          |HAZ Impresion;
     |FINSI;

     |SI FSalida = 11;        || directo a xml es el F3
          |SI aEmision = "IMP"; |ACABA; |FINSI;        || desde labornet
          |HAZ XML_NetContrata;
     |FINSI;

     |SI FSalida = 12;        || marca/desmarca es el F4
          |SI aEmision = "IMP"; |ACABA; |FINSI;        || desde labornet
          ||SI swNetContrata = 1; |ACABA; |FINSI;       || NetContrata
          |HAZ Marca;
     |FINSI;

     |SI FSalida = 13;        || envio a Netcontrata es el F5
          |SI aEmision = "IMP"; |ACABA; |FINSI;        || desde labornet
          swNetContrata = 1;
          aNetContrata = "netcontr";
          |HAZ XML_NetContrata;
     |FINSI;

     |SI FSalida = 15;        || edicion es el F7
          |SI aEmision = "IMP"; |ACABA; |FINSI;        || desde labornet
          aAlfa = "    S¿Desea modificar los datos del certificado seleccionado?";
          |CONFI aAlfa;
          |SI FSalida = 0;
               |HAZ Edicion;
               |PINTA #labhisce#25;
          |FINSI;
     |FINSI;

     |SI FSalida = 16;        || borrado es el F8
          |SI aEmision = "IMP"; |ACABA; |FINSI;        || desde labornet
          aAlfa = "    SSe borrarán los datos del certificado";
          aAlfa = aAlfa + " seleccionado. ¿Continuar?";
          |CONFI aAlfa;
          |SI FSalida = 0;
               |BORRA 020#labhisce.a;
               |LIBERA #labhisce;
               |PONCONTROL 23, "si";
               |PTEC 806;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Tipo13; |TIPO 13;
   |SI aAntTipo ! #labhisce#0; |FINSI;
   |SI #labhisce#0 = "G"; |HAZ PantaTipoG; |FINSI;
   |SI #labhisce#0 = "A"; |HAZ PantaTipoA; |FINSI;

   aAntTipo = #labhisce#0;
|FPRC;

|PROCESO Ayuda; |TIPO 7;
     |SI aEmision = "IMP";         || desde labornet
          aAlfa = "    Pulse < F2 > para imprimir el certificado de empresa";
          |MENSA aAlfa;
          ||                    1         2         3         4
          ||            234567890123456789012345678901234567890
          |PINTA 0528,                           "ÄÄÄÄÄÄÄÄÄÄÄÄÄ";
          |PINTA 0602, "                                       ";
          |PINTA 0702, "                                       ";
          |PINTA 0802, "                                       ";
          ||            4        5         6         7
          ||            12345678901234567890123456789012345678
          |PINTA 0541, "ÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ";
          |PINTA 0641, "                                      ";
          |PINTA 0741, "                                      ";
          |PINTA 0841, "                                      ";
     |SINO;
          |SI swNetContrata = 1;
          |SINO;
               aAlfa = "    Seleccione una de las opciones con la tecla de función correspondiente";
               |MENSA aAlfa;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO BorraRepes;
     |ABRE #labhisce;
     |LEE 000#labhisce.p;
     |SI FSalida = 0;
          aTipAnt = #labhisce#0;
          nEmpAnt = #labhisce#1;
          nTraAnt = #labhisce#2;
          nLinAnt = #labhisce#3;
/*
          #labhisce#20 = "01.01.0000";
          #labhisce#22 = "01.01.0000";
          |GRABA 020#labhisce.a;
          |LIBERA #labhisce;
*/

          |PARA; |SI; |HACIENDO;
               |LEE 000#labhisce.s;
               |SI FSalida ! 0;
                    |SAL;
               |FINSI;

               |SI aTipAnt = #labhisce#0; |Y nEmpAnt = #labhisce#1; |Y nTraAnt = #labhisce#2;
                    || linea repetida;
                    |SALVA_DATOS #labhisce;

                    #labhisce#0 = aTipAnt;
                    #labhisce#1 = nEmpAnt;
                    #labhisce#2 = nTraAnt;
                    #labhisce#3 = nLinAnt;
                    |LEE 101#labhisce.=;
                    |SI FSalida = 0;
                         |BORRA 020#labhisce.a;
                    |FINSI;
                    |LIBERA #labhisce;

                    |REPON_DATOS #labhisce;

                    |LEE 000#labhisce.s;
               |FINSI;

               aTipAnt = #labhisce#0;
               nEmpAnt = #labhisce#1;
               nTraAnt = #labhisce#2;
               nLinAnt = #labhisce#3;
/*
               #labhisce#20 = "01.01.0000";
               #labhisce#22 = "01.01.0000";
               |GRABA 020#labhisce.a;
               |LIBERA #labhisce;
*/
          |SIGUE;
     |FINSI;
     |CIERRA #labhisce;
|FPRC;

|PROCESO inf;
     #labhisce#0 = aDTipo;
     #labhisce#1 = #0#0;
     #labhisce#2 = #0#2;
     #labhisce#25 = " ";
     #labhisce#20 = "01.06.2010";  || fecha en que se pone en marcha esto
     || #labhisce#20 = "01.01.0000";  || todos los certificados
|FPRC;

|PROCESO sup;
     #labhisce#0 = aHTipo;
     #labhisce#1 = #0#1;
     #labhisce#2 = #0#3;
     |SI #0#6 = "S";
          #labhisce#25 = "z";
     |SINO;
          #labhisce#25 = " ";
     |FINSI;
     #labhisce#20 = "31.12.2999";
|FPRC;

|PROGRAMA;
     aParam = PARAMETRO;
     |QBF aParam;

     aAlfa = aParam % -108;
     |SI aAlfa = "netcontr";
          aParam = aParam - aAlfa;
          swNetContrata = 1;
          aNetContrata = "netcontr";
     |FINSI;

     aRegimen = aParam % 101;
     aEmision = aParam % 203;
     || aEmision = "IMP";          || solo dejo imprimir

     |HAZ BorraRepes;

     aDTipo = aRegimen; |QBF aDTipo;
     |SI aDTipo = "";
        aDTipo = " "; aHTipo = "z";
     |SINO;
        aHTipo = aDTipo; |QBF aHTipo;
     |FINSI;

     |CLS;
     |PINPA #0#0;
     |PINDA #0#0;
     |ENDATOS #1#0;
     |SI FSalida = 0;
          |ABRE #labhisce;
          |ENTLINEAL #2#labhisce, -2, 4, 22, 1, inf, sup;
          nPaso = 1;
          nCuantos = 0;
          |BUCLE Seleccionados;

          |SI nCuantos ! 0;
               |HAZ XMLMultiple;
          |FINSI;
          |CIERRA #labhisce;
     |FINSI;
|FPRO;
