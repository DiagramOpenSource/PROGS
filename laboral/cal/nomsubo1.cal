|FICHEROS;
     nomsubo1 #100;

     labempre;
     labtraba;
     nombonif;
|FIN;

|VARIABLES;
     aAlfa = "";
     aFechaBaja = "";
     fFechaBaja = @;
     aFechaAlta = "";
     fFechaAlta = @;
     fFechaIniRef = @;
     fFechaFinRef = @;
     swAcaba = 0;
     swCero = 0;

     swGraba = 0;
     {-1}menu = "";
     menu1 = "2400";
     menu2 = "1";
     menu3 = "";
     menu4 = "RACI";
     menu5 = "Revisar Lineal";
     menu6 = "Actualizar Bonif.";
     menu7 = "Imprimir Informe";
     menu8 = "Cancelar Proceso";
     nOpcion = 0;
     nCuantos = 0;
|FIN;

|PROCESO nombonif;
     #nombonif#0 = #100#0;
     |LEE 101#nombonif.=;
     |SI FSalida = 0;
          |SI #nombonif#27 = "S"; |Y #100#3 = "NO";
               nCuantos = nCuantos + 1;
          |FINSI;
          |SI #nombonif#27 = "N"; |Y #100#3 = "SI";
               nCuantos = nCuantos + 1;
          |FINSI;

          |SI #100#3 = "SI";
               #nombonif#27 = "S";
          |FINSI;
          |SI #100#3 = "NO";
               #nombonif#27 = "N";
          |FINSI;
          |GRABA 020#nombonif.a;
          |LIBERA #nombonif;
     |FINSI;
|FPRC;

|DEFBUCLE nombonif;
     #100#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, nombonif;
|FIN;

|PROCESO actualiza;
     nCuantos = 0;
     |BUCLE nombonif;
     |SI nCuantos > 0;
          aAlfa = nCuantos;
          aAlfa = "    Se ha cambiado el estado en " + aAlfa + " bonificaciones.";
          |MENAV aAlfa;
     |FINSI;
|FPRC;

|PROCESO impresion;
     |PRINT;
|FPRC;

|DEFBUCLE impresion;
     #100#0;
     ;
     INICIO;
     FINAL;
     ;
     NULL, impresion;
|FIN;

|PROCESO imprime;
     Impresora = "CrystalReport";
     |IMPRE -1;
     |INFOR "nomsubo1";

     |BUCLE impresion;

     |PIEINF;
     |FININF;
     |FINIMP;
|FPRC;

|PROCESO Mensa; |TIPO 7;
     |MENSA "    Pulse < F5 > para cambiar el estado de la bonificacion";
|FPRC;

|PROCESO Activa;
     |SI FSalida = 13;
          |SI #100#3 = "SI";
               #100#3 = "NO";
          |SINO;
               #100#3 = "SI";
          |FINSI;
          |GRABA 020#100a;
          |LIBERA #100;
          |PINTA #100#3;
     |FINSI;
|FPRC;

|PROCESO grababonif;
          #100#0 = #nombonif#0;
          |LEE 000#100=;
          |SI FSalida = 0;
               |ACABA;
          |FINSI;

          #100#1 = #nombonif#1;
          #100#2 = #nombonif#2;
          |SI #nombonif#27 = "N";
               #100#3 = "NO";
          |SINO;
               #100#3 = "SI";
          |FINSI;

          #100#4 = "NO";

          |GRABA 020#100n;
|FPRC;

|DEFBUCLE GrabaNombonif;
     #nombonif#1;
     ;
     01;
     99;
     ;
     NULL, grababonif;
|FIN;

|PROCESO labtraba;
     swAcaba = 0;
     swCero = 0;

     #labempre#0 = #labtraba#0;
     |LEE 000#labempre.=;

     #nombonif#0 = #labtraba#39;
     |LEE 000#nombonif.=;
     |SI FSalida ! 0;
          swAcaba = 1;
     |SINO;
     /*
          |SI #nombonif#10 ! 1; |Y #nombonif#10 ! 2;
               swAcaba = 1;
          |FINSI;
     */

          swCero = 0;
          |SI #nombonif#14 = 0; |O #nombonif#3 = 0;
               swCero = swCero + 1;
          |FINSI;
          |SI #nombonif#15 = 0; |O #nombonif#20 = 0;
               swCero = swCero + 1;
          |FINSI;
          |SI #nombonif#16 = 0; |O #nombonif#21 = 0;
               swCero = swCero + 1;
          |FINSI;
          |SI #nombonif#17 = 0; |O #nombonif#22 = 0;
               swCero = swCero + 1;
          |FINSI;
          |SI #nombonif#18 = 0; |O #nombonif#23 = 0;
               swCero = swCero + 1;
          |FINSI;
          |SI #nombonif#19 = 0; |O #nombonif#24 = 0;
               swCero = swCero + 1;
          |FINSI;

          |SI swCero = 6;
               swAcaba = 1;
          |FINSI;
     |FINSI;

     |SI swAcaba = 1;
          |ACABA;
     |FINSI;

     aFechaAlta = #labtraba#40;
     |QBF aFechaAlta;
     fFechaAlta = aFechaAlta;

     aFechaBaja = #labtraba#41;
     |QBF aFechaBaja;
     |SI aFechaBaja ! ""; |Y aFechaBaja ! "..........";
          fFechaBaja = aFechaBaja;
     |SINO;
          fFechaBaja = "31.12.2999";
     |FINSI;

     fFechaIniRef = "01.08.2012";
     fFechaFinRef = "31.08.2012";

     |SI fFechaAlta [ fFechaFinRef; |Y fFechaBaja ] fFechaIniRef;
          |DDEFECTO #100;
          #100#0 = #nombonif#0;
          |LEE 000#100=;
          |SI FSalida = 0;
               |ACABA;
          |FINSI;

          #100#1 = #nombonif#1;
          #100#2 = #nombonif#2;
          |SI #nombonif#27 = "N";
               #100#3 = "NO";
          |SINO;
               #100#3 = "SI";
          |FINSI;
          #100#4 = "SI";

          |GRABA 020#100n;
     |FINSI;
|FPRC;

|DEFBUCLE labtraba;
     #labtraba#8;
     39;
     "A", 00001, 00001, 01;
     "A", 99999, 99999, 99;
     ;
     NULL, labtraba;
|FIN;

|PROCESO inferior;
     #100#0 = 01;
|FPRC;

|PROCESO superior;
     #100#0 = 99;
|FPRC;

|PROGRAMA;
     |CLS;
     |CUADRO 12261652;
     |PINTA 1328, "Cargando bonificaciones.";
     |PINTA 1428, "Espere unos segundos ...";

     |SI FSalida = 0;
          |ABRE #labempre;
          |ABRE #labtraba;
          |ABRE #nombonif;

          aAlfa = Usuario; |QBT aAlfa;    aAlfa = ("b" + aAlfa) % 108;
          |NOME_DAT #100 aAlfa;
          |DELINDEX #100;
          |ABRE #100;

          |BUCLE labtraba;

          |BUCLE GrabaNombonif;

          |PARA; |SI; |HACIENDO;
               |ENTLINEAL #1#100, 1, 4, 14, 1, inferior, superior;
               |MENU menu;
               nOpcion = FSalida;
               |SI nOpcion = 2;     |HAZ actualiza;          |SAL;|FINSI;
               |SI nOpcion = 3;     |HAZ imprime;             |FINSI;
               |SI nOpcion = 4;     |SAL;                         |FINSI;
               menu2 = nOpcion;
          |SIGUE;

          |CIERRA #100;
          |DELINDEX #100;

          |CIERRA #nombonif;
          |CIERRA #labtraba;
          |CIERRA #labempre;
     |FINSI;
|FPRO;
