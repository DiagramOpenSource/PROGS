|FICHEROS;
     laborn99 #0;

     gecom019@ #100;
     gecom011@ #101;
|FIN;

|VARIABLES;
     aGrupo = "";
     aRutaApl = "";
     nGrupo = 0;
     aAlfa = "";
     FEstado = 0;
     nDesdeEmpresa = 0;
     nHastaEmpresa = 0;

     aRutaDef = "";
     aRutaFich = "";
     nEmpExt = 0;
|FIN;

|PROCESO superior;
     #0#0 = 99999;
     #0#1 = 99999;
|FPRC;

|PROCESO inferior;
     #0#0 = 00000;
     #0#1 = 00001;
|FPRC;

|PROCESO graba;
     aGrupo = aRutaApl % -201;
     nGrupo  = aGrupo;
     |SI nGrupo = 0;
         nGrupo = 1;
     |FINSI;
     aAlfa = aRutaApl % -109;
     |SI aAlfa = "dsprofes/";       || estoy en produccion, sin duda
          aGrupo = "1";
          nGrupo = 1
     |FINSI;

     #101#0 = #100#0;      || cliente
     |LEE 000#101=;
     |SI FSalida = 0;
          aAlfa = aGrupo + (("00" + #101#1) % -102) + "01";  || carpeta cliente (del fich)
          #0#0 = aAlfa;
          |LEE 101#0=;
          FEstado = FSalida;

          #0#1 = #101#2;    || nombre cliente gecochek

          |SI FEstado = 0;
               |GRABA 020#0a;
          |SINO;
               |GRABA 020#0n;
          |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE empresas;
     #100#1002;
     ;
     nDesdeEmpresa, 00001;
     nHastaEmpresa, 99999;
     ;
     NULL, graba;
|FIN;

|PROCESO CargaEmpresas;
     |ABRE #0;
     |DBASS aRutaApl;
     |QBF aRutaApl;
     aAlfa = aRutaApl % -109;
     |SI aAlfa = "dsprofes/";       || estoy en produccion, sin duda
          aRutaDef = "/u/dsmedida/gecochek/def/gecom019";      || ruta def actual, para CARGA_DEF
          aRutaFich = "/u/dsmedida/gecochek/fich/";      || ruta def actual, para CARGA_DEF
     |SINO;
          |DBASS aRutaApl;
          aRutaDef = aRutaApl + "/gecochek/def/gecom019";      || ruta def actual, para CARGA_DEF
          aRutaFich = aRutaApl + "/gecochek/fich/";      || ruta def actual, para CARGA_DEF
     |FINSI;
     |CARGA_DEF aRutaDef, gecom019@;
     |PATH_DAT #100 aRutaFich;

     |SI aAlfa = "dsprofes/";       || estoy en produccion, sin duda
          aRutaDef = "/u/dsmedida/gecochek/def/gecom011";      || ruta def actual, para CARGA_DEF
          aRutaFich = "/u/dsmedida/gecochek/fich/";      || ruta def actual, para CARGA_DEF
     |SINO;
          |DBASS aRutaApl;
          aRutaDef = aRutaApl + "/gecochek/def/gecom011";      || ruta def actual, para CARGA_DEF
          aRutaFich = aRutaApl + "/gecochek/fich/";      || ruta def actual, para CARGA_DEF
     |FINSI;
     |CARGA_DEF aRutaDef, gecom011@;
     |PATH_DAT #101 aRutaFich;

     |ABRE #101;
     |SI nEmpExt = 0;
          nDesdeEmpresa = 00001;
          nHastaEmpresa = 99999;
     |SINO;
          nDesdeEmpresa = nEmpExt;
          nHastaEmpresa = nEmpExt;
     |FINSI;

     |BUCLE empresas;
     |CIERRA #101;

     |DESCARGA_DEF #gecom019@;
     |DESCARGA_DEF #gecom011@;
     |CIERRA #0;
|FPRC;

|PROGRAMA;
     |CLS;
     |CABEZA "Control Administrador Labornet";
     |HAZ CargaEmpresas;
     |ENTLINEAL #1#0, -1, 2, 20, 1, inferior, superior;
|FPRO;
