|FICHEROS;
     nomcot33 #0;
     nomcot34;

     nomcot02;
     cretam04;
|FIN;

|VARIABLES;
     aAlfa = "";
     nCampo = 0;
     nCampo1 = 0;
     nCampo2 = 0;
     swDiferentes = 0;
     swHayRegistros = 0;
     aAlfa1 = "";
     nNume1 = 0;
     nNume2 = 0;
     nDiferencia = 0;

     nLimite = 0;
     nLimiteNeg = 0;
     nLimitePos = 0;
     swCambia = 0;
|FIN;

|PROCESO AbreImp;
     Impresora = "CrystalReport";
     |IMPRE -1;
     |SI FSalida = 0;
          |INFOR "nomcot33";
     |FINSI;
|FPRC;

|PROCESO imprime;
     |SI swHayRegistros = 0;
          |HAZ AbreImp;
     |FINSI;
     swHayRegistros = 1;
     |PRINT;
|FPRC;

|DEFBUCLE nomcot34;
     #nomcot34#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, imprime;
|FIN;

|PROCESO Informe;
     |LEE 000#nomcot34.p;
     |SI FSalida = 0;
          |BUCLE nomcot34;

          |SI swHayRegistros = 1;
               |PIEINF;
               |FININF;
               |FINIMP;
          |SINO;
               |MENAV "    No se han encontrado diferencias";
          |FINSI;
     |SINO;
          |MENAV "    No hay datos para el informe";
     |FINSI;
|FPRC;

|PROCESO nomcot02;
     |DDEFECTO #nomcot34;
     |PARA nCampo = 0; |SI nCampo [ 7; |HACIENDO nCampo = nCampo + 1;
          #nomcot02#nCampo# = #cretam04#nCampo#;
     |SIGUE;
     |LEE 000#nomcot02.=;
     |SI FSalida = 0;
          swCambia = 0;
          |SI #cretam04#34 = 563; |Y #nomcot02#32 = 563;
               swCambia = 1;
          |FINSI;
          |SI #cretam04#34 = 502; |Y #nomcot02#32 = 502;
               swCambia = 1;
          |FINSI;
          |SI swCambia = 1;
               nNume1 = #nomcot02#32;
               nNume2 = #nomcot02#33;
               #nomcot02#32 = #nomcot02#34;
               #nomcot02#33 = #nomcot02#35;
               #nomcot02#34 = nNume1;
               #nomcot02#35 = nNume2;
          |FINSI;

          |PARA nCampo = 20; |SI nCampo [ 69; |HACIENDO nCampo = nCampo + 1;
               nNume1 = #cretam04#nCampo#;
               nNume1 = nNume1 ! 2;
               nNume2 = #nomcot02#nCampo#;
               nNume2 = nNume2 ! 2;
               nDiferencia = nNume1 - nNume2;

               nLimite = 0.02;
               |SI nCampo = 24; |O nCampo = 61;
                    nLimite = 2;
               |FINSI;
               |SI nCampo = 28;
                    || es el dia por el que va esta IT, pero parece que si no
                    || hay prestacion, en el cretam04 se queda a cero, no lo
                    || tengo en cuenta
                    nLimite = 10000;
               |FINSI;
               |SI nCampo = 23;
                    || en lo nuevo lo marco siempre para mensuales GC ] 08
                    || en lo antiguo solo si tengo que poner 51M, no lo
                    || modifico y no lo tengo en cuenta en el informe
                    nLimite = 10000;
               |FINSI;
               |SI nCampo = 24; |Y #cretam04#27 ! "ALT";
                    || campo Horas TP, en IT no tienen que llevar horas y en el
                    || nomcot02 de hecho no se graban, peeeero en el cretam04
                    || aunque en el tramo no se pintan, pero se graban
                    nLimite = 10000;
               |FINSI;

               nLimitePos = nLimite; nLimiteNeg = (-1) * nLimite;
               |SI nDiferencia [ nLimiteNeg; |O nDiferencia ] nLimitePos;
                    |PARA nCampo1 = 0; |SI nCampo1 [ 9; |HACIENDO nCampo1 = nCampo1 + 1;
                         #nomcot34#nCampo1# = #cretam04#nCampo1#;
                    |SIGUE;
                    #nomcot34#10 = nCampo;
                    aAlfa1 = nCampo;
                    |QBF aAlfa1;
                    aAlfa1 = ("000" + aAlfa1) % -103;
                    aAlfa1 = "|C" + aAlfa1 + "NOMBRE";
                    #nomcot34#11 = #cretam04#aAlfa1#;
                    #nomcot34#12 = #cretam04#nCampo#;
                    #nomcot34#13 = #nomcot02#nCampo#;
                    |GRABA 020#nomcot34.n;
                    |LIBERA #nomcot34;
               |FINSI;
          |SIGUE;
     |SINO;
          |SI #0#10 = "0";
               |PARA nCampo = 0; |SI nCampo [ 9; |HACIENDO nCampo = nCampo + 1;
                    #nomcot34#nCampo# = #cretam04#nCampo#;
               |SIGUE;
               #nomcot34#10 = 000;
               #nomcot34#11 = "TRAMO NO LOCALIZADO";
          |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE cretam04;
     #cretam04#1;
     ;
     #0#0, #0#6, #0#7, #0#4, "               ", "            ", 01;
     #0#1, #0#6, #0#7, #0#5, "zzzzzzzzzzzzzzz", "zzzzzzzzzzzz", 99;
     ;
     NULL, nomcot02;
|FIN;

|PROGRAMA;
     |CLS;

     |PINPA #0#0;

     |ABRE #0;
     |DDEFECTO #0;
     |LEE 000#0p;
     |SI FSalida = 0;
          |BORRA 020#0a;
     |FINSI;

     |PINDA #0#0;

     |ENDATOS #1#0;
     |SI FSalida = 0;
          aAlfa = Usuario; |QBT aAlfa; aAlfa = "tm_nomcot34_" + aAlfa;
          |NOME_DAT #nomcot34#aAlfa#;
          |DELINDEX #nomcot34;
          |ABRE #nomcot02;
          |ABRE #nomcot34;
          |ABRE #cretam04;

          |BUCLE cretam04;
          ||BUCLE nomcot02;
          ||CONSULTA_CLAVE #1#nomcot34;
          |HAZ Informe;

          |CIERRA #cretam04;
          |CIERRA #nomcot34;
          |DELINDEX #nomcot34;
          |CIERRA #nomcot02;
     |FINSI;


     |GRABA 000#0n;
     |CIERRA #0;
|FPRO;
