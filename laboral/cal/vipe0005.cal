|FICHEROS;
     vipe0005 #0;
     vipe0004 #4;
     vipe0015 #15,MANTE;
     vipe1002;
     labtraba;
     nomconst;
|FIN;

|VARIABLES;
     nModo = 0;
     &enGrid = 0;
     {-1}aVent = "";
     aVent1 = 0;
     aVent2 = 0;
     aVent3 = 0;
     aVent4 = 0;
     aVent5 = "";
     nSubVent = 0;
     nBotonAceptar = 0;
     nBotonAbortar = 0;
     nBarra = 0;

     &nCConst = 0;
     &fCFecha = @;
     &aCError = "";
     nLimite = 0;
     alfa1 = "";
     alfa2 = "";
     alfa3 = "";
     alfa4 = "";
     aAlfa = "";
     &nIdSalir = -1;
|FIN;

/********************** PROCESOS DEL vipe0015 *******************************/

|PROCESO Tipo12; |TIPO 12;

|FPRC;

|PROCESO constante;
     alfa1 = "01";
     alfa2 = #vipe0004#5;  alfa2 = "00" + alfa2;    alfa2 = alfa2 % -102;
     alfa3 = #vipe0004#4;
     alfa4 = alfa1 + "." + alfa2 + "." + alfa3;

     fCFecha = alfa4;         || primer dia de mes de calculo
     aCError = "";            || inicializo errores
     |HAZ LeeCons;
|FPRC;

|PROCESO CompruebaIPREM;
     |ABRE #labtraba;
     #labtraba#0 = #vipe0005#0;
     #labtraba#1 = #vipe0005#1;
     |LEE 000#labtraba.=;

     nCConst = #labtraba#248;
     |HAZ constante;
     nLimite = #nomconst#76;
     nLimite = nLimite % 20;

     |SI #vipe0015#7 > nLimite;
          alfa1 = #vipe0015#7;
          alfa2 = nLimite;
          aAlfa = "    ATENCION: El total del exceso de horas (" + alfa1;
          aAlfa = aAlfa + " euros) es superior al maximo no cotizable (" + alfa2;
          aAlfa = aAlfa + " euros)."
          |MENAV aAlfa;
     |FINSI;

     |CIERRA #labtraba;
|FPRC;

|PROCESO Concepto;
     |SI #vipe0015#4 ] 0;
          #vipe0015#5 = #vipe1002#1;        || concepto (del control)
     |SINO;
          #vipe0015#5 = #vipe1002#2;        || concepto (del control)
     |FINSI;
     |PINTA #vipe0015#5;
|FPRC;

|PROCESO Total;
     |SI #vipe0015#4 ] 0;
          #vipe0015#7 = #vipe0015#4 * #vipe0015#6;
     |SINO;
          #vipe0015#7 = #vipe0015#4 * #vipe0015#6 * -1;
     |FINSI;
     |PINTA #vipe0015#7;
|FPRC;

|PROCESO TotalV;
     #vipe0015#11 = #vipe0015#8 * #vipe0015#10;
     |PINTA #vipe0015#11;
|FPRC;

|PROCESO Vacaciones; |TIPO 0;
     |SI #vipe0015#8 ! 0;
          |SI #vipe0005#23 ! "*";
               |MENAV "    ATENCION: El trabajador no es baja este mes.";
          |FINSI;
          #labtraba#0 = #vipe0005#0;
          #labtraba#1 = #vipe0005#1;
          |LEE 000#labtraba.=;
          |SI FSalida = 0;
               |SI #labtraba#48 ! "S";
                    |MENAV "    ATENCION: El trabajador tiene informado en su ficha Finiquito 'No'";
               |FINSI;
          |FINSI;
     |FINSI;
|FPRC;


/********************** FIN PROCESOS DEL vipe0015 ***************************/

|PROCESO Abortar;
     |PTEC 807;
|FPRC;

|PROCESO Aceptar;
     |PTEC 806;
|FPRC;

|PROCESO DobleClick; |TIPO 7;
     |LEE 101#0=;

     |VENTANA 2303, 2797, -1, 1, "";
     nSubVent = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nSubVent;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     #15#4 = #0#16;      || horas
     #15#5 = #0#17;      || concepto
     #15#6 = #0#18;      || precio
     #15#7 = #0#19;      || total importe horas

     #15#8 = #0#24;      || dias vaciones
     #15#9 = #0#25;      || concepto
     #15#10 = #0#26;      || precio
     #15#11 = #0#29;      || total importe vacaciones

     |PINPA #0#15;
     nBarra = FSalida;
     |PINDA #0#15;

     |CONTROL_SIMPLE nBarra, "BOTON,{{206}}", 2680, 2686, Aceptar;
     nBotonAceptar = FSalida;

     |CONTROL_SIMPLE nBarra, "BOTON,{{207}}", 2688, 2694, Abortar;
     nBotonAbortar = FSalida;

     |ENDATOS #1#15;

     |SI FSalida = 0;
          #0#16 = #15#4;      || horas
          #0#17 = #15#5;      || concepto
          #0#18 = #15#6;      || precio
          #0#19 = #15#7;      || total importe horas

          #0#24 = #15#8;      || dias vaciones
          #0#25 = #15#9;      || concepto
          #0#26 = #15#10;     || precio
          #0#29 = #15#11;     || total importe vacaciones

          |PINTA #0#16;
          |PINTA #0#17;
          |PINTA #0#18;
          |PINTA #0#19;

          |PINTA #0#24;
          |PINTA #0#25;
          |PINTA #0#26;
          |PINTA #0#29;

          |GRABA 020#0a;
          |LIBERA #0;
     |FINSI;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nSubVent;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FIN_CONTROL_WINDOWS nBotonAbortar;
     |FIN_CONTROL_WINDOWS nBotonAceptar;

     |FINVENTANA nSubVent;

     |REFRESCACONTROL enGrid;
     |FOCOTECLADO enGrid;
     |ESTADO_CONTROL nIdSalir, "HIDE";

     |PULSATECLA;
|FPRC;
