|FICHEROS;
     labempre;
     labtraba;
     labcentr;
     nomcenes;
     nomvarca;
     cretam04;
     cretam06;
|FIN;

|VARIABLES;
     &aNif = "";
     &fFechaIniPer = @;
     &fFechaFinPer = @;
     &nEmp = 0;
     &nTra = 0;
     &nAny = 0;
     &nMes = 0;
     &nTipo = 0;
     &aCCC = "";
     &aNAFSS = "";
     &nDiasVac1 = 0;
     &nDiasVac2 = 0;

     aAlfa = "";
     nNume = 0;
     swSigue = 0;
     nCampo = 0;
     nCampo1 = 0;
     nCampo2 = 0;
     nLin = 0;
     fFechaA = @;
     fFechaB = @;
     aCCC_Dupli = "";
|FIN;

|PROCESO CCC_Dupli;
     aCCC_Dupli = #labempre#13;          || cta.cot. de la empresa
     |SI #labtraba#45 = "085"; |O #labtraba#45 = "087"; |O #labtraba#45 = "097";
     |O #labtraba#45 = "421"; |O #labtraba#45 = "422";
          |DDEFECTO #nomcenes;
          |ABRE #nomcenes;
          #nomcenes#0 = #labtraba#0;
          #nomcenes#13 = #labtraba#77;
          #nomcenes#1 = 87;
          |LEE 000#nomcenes.=;
          |SI FSalida = 0;
               aAlfa = #nomcenes#10;      || cta.cto. del centro de trabajo especial
               |QBF aAlfa;
               |SI aAlfa ! "";
                    aCCC_Dupli = aAlfa;         || cta.cto. del centro de trabajo especial
               |FINSI;
          |FINSI;
          |CIERRA #nomcenes;
     |SINO;
          |DDEFECTO #labcentr;
          |ABRE #labcentr;
          #labcentr#0 = #labtraba#0;
          #labcentr#1 = #labtraba#77;
          |LEE 000#labcentr.=;
          |SI FSalida = 0;
               aAlfa = #labcentr#10;      || cta.cot. centro de trabajo
               |QBF aAlfa;
               |SI aAlfa ! "";
                    aCCC_Dupli = aAlfa;
               |FINSI;
          |FINSI;
          |CIERRA #labcentr;
     |FINSI;
     |QBF aCCC_Dupli;
     |SI aCCC_Dupli ! "";
          aCCC_Dupli = ("00000000000" + aCCC_Dupli) % -111;
     |FINSI;
     aAlfa = #labtraba#247;
     |QBF aAlfa;
     aAlfa = ("0000" + aAlfa) % -104;
     |SI aCCC_Dupli ! "";
          aCCC_Dupli = aAlfa + aCCC_Dupli;
     |FINSI;
|FPRC;

|PROCESO lineas_cretam06;
     |SI #cretam06#7 ! #cretam04#7;
          |PARA nCampo1 = 30; |SI nCampo1 [ 49; |HACIENDO nCampo1 = nCampo1 + 2;
               nCampo2 = nCampo1 + 1;
               |SI #cretam04#nCampo1# = #cretam06#nCampo1#;
                    #cretam04#nCampo2# = #cretam04#nCampo2# + #cretam06#nCampo2#;
               |FINSI;
          |SIGUE;
          |PARA nCampo1 = 60; |SI nCampo1 [ 69; |HACIENDO nCampo1 = nCampo1 + 2;
               nCampo2 = nCampo1 + 1;
               |SI #cretam04#nCampo1# = #cretam06#nCampo1#;
                    #cretam04#nCampo2# = #cretam04#nCampo2# + #cretam06#nCampo2#;
               |FINSI;
          |SIGUE;
          |GRABA 020#cretam04.a;
          |LIBERA #cretam04;
     |FINSI;
|FPRC;

|DEFBUCLE lineas_cretam06;
     #cretam06#1;
     ;
     #cretam04#0, #cretam04#1, #cretam04#2, #cretam04#3, #cretam04#4, #cretam04#5, nLin, 00001;
     #cretam04#0, #cretam04#1, #cretam04#2, #cretam04#3, #cretam04#4, #cretam04#5, nLin, 99999;
     ;
     NULL, lineas_cretam06;
|FIN;

|PROCESO lineas_cretam04;
     nLin = #cretam04#6;
     |BUCLE lineas_cretam06;
|FPRC;

|DEFBUCLE lineas_cretam04;
     #cretam04#1;
     ;
     nEmp, nAny, nMes, nTipo, aCCC, aNAFSS, 01;
     nEmp, nAny, nMes, nTipo, aCCC, aNAFSS, 99;
     be;
     NULL, lineas_cretam04;
|FIN;

|PROCESO FichasDuplicadas;
     || ahora buscamos todas las lineas del cretam04 y les sumamos
     || las del cretam06

     |SALVA_FICHA #cretam04;
     |BUCLE lineas_cretam04;
     |REPON_FICHA #cretam04;
|FPRC;

|PROCESO graba_cretam06;
     |PARA nCampo = 0; |SI nCampo [ 84; |HACIENDO nCampo = nCampo + 1;
          #cretam06#nCampo# = #cretam04#nCampo#;
     |SIGUE;
     |GRABA 020#cretam06.n;
     |LIBERA #cretam06;
|FPRC;

|DEFBUCLE graba_cretam06;
     #cretam04#1;
     7;
     nEmp, nAny, nMes, nTipo, aCCC, aNAFSS, 01, nTra;
     nEmp, nAny, nMes, nTipo, aCCC, aNAFSS, 99, nTra;
     be;
     NULL, graba_cretam06;
|FIN;

|PROCESO borra_cretam06;
     |BORRA 020#cretam06.a;
     |LIBERA #cretam06;
|FPRC;

|DEFBUCLE borra_cretam06;
     #cretam06#1;
     ;
     #cretam04#0, #cretam04#1, #cretam04#2, #cretam04#3, #cretam04#4, #cretam04#5, 01, #cretam04#7;
     #cretam04#0, #cretam04#1, #cretam04#2, #cretam04#3, #cretam04#4, #cretam04#5, 99, #cretam04#7;
     be;
     NULL, borra_cretam06;
|FIN;

|PROCESO DosIguales;
     |ABRE #cretam06;

     || primero borro cualquier rastro que hubiera en el auxiliar de este
     || trabajador
     |BUCLE borra_cretam06;

     || ahora grabo todas las lineas que tuviera ya calculadas
     |SALVA_FICHA #cretam04;
     |BUCLE graba_cretam06;
     |REPON_FICHA #cretam04;

     |CIERRA #cretam06;
|FPRC;

|PROCESO labtraba_alta;
     |HAZ CCC_Dupli;
     |SI nTra ! #labtraba#1; |Y aCCC = aCCC_Dupli;
     |Y #labtraba#129 = " ";
          aAlfa = #labtraba#36;
          fFechaA = aAlfa;
          fFechaB = fFechaFinPer;

          aAlfa = #labtraba#37;
          |QBF aAlfa;
          |SI aAlfa = "";
               |ABRE #nomvarca;
               #nomvarca#0 = #labtraba#0;
               #nomvarca#1 = #labtraba#1;
               #nomvarca#2 = nMes;
               #nomvarca#3 = 0;
               |LEE 000#nomvarca.=;
               |SI FSalida = 0;
                    aAlfa = #nomvarca#16;
                    |QBF aAlfa;
                    |SI aAlfa ! "";
                         fFechaB = aAlfa;
                    |FINSI;
               |FINSI;
          |SINO;
               fFechaB = aAlfa;
          |FINSI;
          |CIERRA #nomvarca;
/*
          || esto no, ya que si acabo contrato el dia 13 por ejemplo y me liquidan
          || vacaciones 6 dias, luego puedo volver a empezar el dia 15 y se
          || meteria aqui sin tener que hacerlo 003923.01414, trab 230.443 - 444

          |SI nTipo = 02;
               nNume = fFechaB;
               nNume = nNume + nDiasVac1 + nDiasVac2;
               fFechaB = nNume;
          |FINSI;
*/
          |SI fFechaA [ fFechaFinPer; |Y fFechaB ] fFechaIniPer;
               || adelante
               swSigue = 1;
          |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE labtraba_alta;
     #labtraba#2;
     0, 44;
     aNif, nEmp, "A";
     aNif, nEmp, "A";
     ;
     NULL, labtraba_alta;
|FIN;

|PROCESO FichasDupli;
     || lo primero es comprobar si de este codigo de trabajador que estoy
     || calculando, tengo algun duplicado

     swSigue = 0;

     |SALVA_DATOS #labtraba;
     |BUCLE labtraba_alta;
     |REPON_DATOS #labtraba;

     |SI swSigue = 1;
          || me lo llevo al cretam06, mi auxiliar para contemplar estos casos
          |HAZ DosIguales;

          || aqui me voy a buscar al trabajador, para ver si es un caso de los
          || que tiene dos fichas de alta en el mismo periodo, para ello me voy
          || al cretam06 y busco si hay algun otro trabajador con codigo DISTINTO
          || calculado

          |HAZ FichasDuplicadas
     |FINSI;
|FPRC;
