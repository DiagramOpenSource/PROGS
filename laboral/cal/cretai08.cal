|FICHEROS;
     cretai08 #0;

     cretam3a;
     cretam3b;
|FIN;

|VARIABLES;
     &nError = 0;
     nCampo = 0;
     nCampo1 = 0;
     nCampo2 = 0;
     FEstado = 0;
     aAlfa = "";

     fFecha = @;
     aFecha = "";
     nMes = 0;
     nAny = 0;
     aReferencia = "";
     nNume = 0;
     nTramos = 0;
     nImpresion = 0;
|FIN;

|PROCESO Defectos;
     fFecha = ~;
     aFecha = fFecha;
     aAlfa = aFecha % -104;
     nAny = aAlfa;

     aAlfa = aFecha % 402;
     nMes = aAlfa;
     nMes = nMes - 1;
     |SI nMes = 0;
          nMes = 12;
          nAny = nAny - 1;
     |FINSI;

     #0#2 = nAny;
     #0#3 = nMes;
     |PINTA #0#2;
     |PINTA #0#3;
|FPRC;

|PROCESO AvisoTipos;
     |SI #0Campo = 01;
          aAlfa = "    ATENCION: con la selección 'Desde tipo 01'";
          aAlfa = aAlfa + " no se incluyen las liquidaciones L00";
          |MENAV aAlfa;
     |FINSI;
|FPRC;

|PROCESO AyudaTipos; |TIPO 7;
     |PUSHV 08622380;
     |ATRI R;
     ||            6         7
     ||            34567890123456789
     |PINTA 0863, "  - Opciones -   ";
     |PINTA 0963, " 00 :Normal      ";
     |PINTA 1063, " 02 :Finiquitos  ";
     |PINTA 1163, "5-20:Atrasos     ";
     |PINTA 1263, " 90 :Complement. ";
     |PINTA 1363, "                 ";
     |PINTA 1463, "                 ";
     |ATRI 0;
|FPRC;

|PROCESO Restaura;
     |POPV;
|FPRC;

|PROCESO ElIMPRE;
     Impresora = "CrystalReport";
     |IMPRE -1;
     |SI FSalida = 0;
          |INFOR "cretai08";
     |FINSI;
|FPRC;

|PROCESO ElFINIMP;
     |FININF;

     |FINIMP;
|FPRC;

|PROCESO cretam3b;
     |SI nImpresion = 0;
          |HAZ ElIMPRE;
     |FINSI;

     |PARA nCampo = 15; |SI nCampo [ 129; |HACIENDO nCampo = nCampo + 6;
          |SI #cretam3b#nCampo# = 998;
               nCampo1 = nCampo + 3;
               nCampo2 = nCampo + 4;
               #cretam3b#135 = #cretam3b#nCampo1#;
               #cretam3b#136 = #cretam3b#nCampo2#;
          |FINSI;
          |SI #cretam3b#nCampo# = 000;
               |SAL;
          |FINSI;
     |SIGUE;

     |PRINT;
     nImpresion = nImpresion + 1;
|FPRC;

|DEFBUCLE cretam3b;
     #cretam3b#1;
     ;
     #cretam3a#0, #cretam3a#1, #cretam3a#2, #cretam3a#3, #cretam3a#4, "            ", 01, 2000, 01;
     #cretam3a#0, #cretam3a#1, #cretam3a#2, #cretam3a#3, #cretam3a#4, "zzzzzzzzzzzz", 31, 2099, 12;
     ;
     NULL, cretam3b;
|FIN;

|PROCESO cretam3a;
     |PARA nCampo = 31; |SI nCampo [ 126; |HACIENDO nCampo = nCampo + 5;
          |SI #cretam3a#nCampo# = 998;
               nCampo1 = nCampo + 2;
               nCampo2 = nCampo + 3;
               #cretam3a#131 = #cretam3a#nCampo1#;
               #cretam3a#132 = #cretam3a#nCampo2#;
          |FINSI;
          |SI #cretam3a#nCampo# = 000;
               |SAL;
          |FINSI;
     |SIGUE;

     |BUCLE cretam3b;
     |PIEINF;
|FPRC;

|DEFBUCLE cretam3a_L90;
     #cretam3a#3;
     ;
     #0#0, #0#2, #0#3, #0#6, "               ";
     #0#1, #0#2, #0#3, #0#7, "zzzzzzzzzzzzzzz";
     ;
     NULL, cretam3a;
|FIN;

|DEFBUCLE cretam3a;
     #cretam3a#1;
     ;
     #0#0, #0#2, #0#3, #0#6, "               ", 0000;
     #0#1, #0#2, #0#3, #0#7, "zzzzzzzzzzzzzzz", 2999;
     ;
     NULL, cretam3a;
|FIN;

|PROCESO Seleccion;
     |BUCLE cretam3a;
     |BUCLE cretam3a_L90;
|FPRC;

|PROGRAMA;
     |CLS;
     |PINPA #0#0;

     |ABRE #0;
     |DDEFECTO #0;
     |LEE 000#0p;
     |SI FSalida = 0;
          |BORRA 020#0a;
     |SINO;
          |HAZ Defectos;
     |FINSI;

     |PINDA #0#0;

     |ENDATOS #1#0;
     |SI FSalida = 0;
          |ABRE #cretam3a;
          |ABRE #cretam3b;

          |HAZ Seleccion;

          |CIERRA #cretam3b;
          |CIERRA #cretam3a;


          |SI nImpresion ! 0;
               |HAZ ElFINIMP;
          |SINO;
               aAlfa = "    No se han encontrado datos de Cálculos para imprimir ";
               aAlfa = aAlfa + "con la selección realizada.";
               |MENAV aAlfa;
          |FINSI;
     |FINSI;

     |GRABA 000#0n;
     |CIERRA #0;
|FPRO;
