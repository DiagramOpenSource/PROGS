|FICHEROS;
    nomrecdt #0;
    labempre #1;
    labtraba #2;
    nomhicca #3;
    nomconca #9;
|FIN;

|VARIABLES;
    alfa1 = "";
    alfa2 = "";
    alfa3 = "";
    alfa4 = "";
    nume1 = 0;
    nume2 = 0;
    nume3 = 0;
    nume4 = 0;
    para1 = 0;
    para2 = 0;
    para3 = 0;
    para4 = 0;
    fech1 = @;
    fech2 = @;
    fech3 = @;
    fech4 = @;
    FEstado = 0;

    p_mes = "";
    bisiesto = 0;
    d_dto = 0;
    any = 0;
    de_mes = 0;
    ha_mes = 0;
    mas = 0;
    rompe = 0;
    desde = 0;
    hasta = 0;
    dedia = 0;
    hadia = 0;
    mesco = 0;
    des1 = 0;
    des2 = 0;
    has1 = 0;
    has2 = 0;
    des = 0;
    has = 0;
    act = 0;
    paga = 0;
|FIN;
____________________________________/ CALCULOS PANTALLA
|PROCESO defecto; |TIPO 7;
    fech1 = ~;
    alfa1 = fech1;
    alfa1 = alfa1 % -104;
    #0Campo = alfa1;
|PINTA #0Campo;
|FPRC;

|PROCESO mirames; |TIPO 7; ||CALCULA EL MES POR DEFECTO SEGUN FECHA SISTEMA
    fech1 = ~;
    alfa1 = fech1;
    alfa1 = alfa1 % 402;
    #0Campo = alfa1;
|PINTA #0Campo;
|FPRC;

|PROCESO pintames; |TIPO 0;      || PINTA EL MES LITERAL
|SI #0Campo =  1;  p_mes = "ENERO     ";  |FINSI;
|SI #0Campo =  2;  p_mes = "FEBRERO   ";  |FINSI;
|SI #0Campo =  3;  p_mes = "MARZO     ";  |FINSI;
|SI #0Campo =  4;  p_mes = "ABRIL     ";  |FINSI;
|SI #0Campo =  5;  p_mes = "MAYO      ";  |FINSI;
|SI #0Campo =  6;  p_mes = "JUNIO     ";  |FINSI;
|SI #0Campo =  7;  p_mes = "JULIO     ";  |FINSI;
|SI #0Campo =  8;  p_mes = "AGOSTO    ";  |FINSI;
|SI #0Campo =  9;  p_mes = "SEPTIEMBRE";  |FINSI;
|SI #0Campo = 10;  p_mes = "OCTUBRE   ";  |FINSI;
|SI #0Campo = 11;  p_mes = "NOVIEMBRE ";  |FINSI;
|SI #0Campo = 12;  p_mes = "DICIEMBRE ";  |FINSI;
|PINTA 1448, p_mes;
|FPRC;

|PROCESO mayus; |TIPO 0;
    #0Campo = #0Campo > #0Campo;
|PINTA #0Campo;
|FPRC;
____________________________________/
|PROCESO lee_histo1;
|SI #1#26 = "S";
        #2d_dto = #2d_dto + #3#31 + #3#65;   || enf./mat.
|FINSI;

|SI #1#27 = "S";
        #2d_dto = #2d_dto + #3#32;           || acc.
|FINSI;

|SI #3#2 = 8;|Y #0#8 = "S";   || en agosto el absentismo se cuenta o no
        #3#34 = 0;
|FINSI;

    nume1 = #3#34 * 100;
    #3#34 = (nume1 - (nume1 $ 100)) / 100;   || quita los decimales

    #2d_dto = #2d_dto + #3#34;               || abs.
|FPRC;

|DEFBUCLE lee_histo;
 #3#1;
 ;
 #2#0, #2#1, any, de_mes, 0;
 #2#0, #2#1, any, ha_mes, 0;
 e;
 NULL, lee_histo1;
|FIN;

|PROCESO busca;  || BUSCA SI HAY PRORRATA EN LA PAGA ACTUAL Y VA CONTANDO DIAS
|SI paga = 1; d_dto = 50; |FINSI;
|SI paga = 2; d_dto = 51; |FINSI;
|SI paga = 3; d_dto = 52; |FINSI;
|SI paga = 4; d_dto = 53; |FINSI;
|SI paga = 5; d_dto = 54; |FINSI;
|SI paga = 6; d_dto = 78; |FINSI;
|SI paga = 7; d_dto = 79; |FINSI;
|SI paga = 8; d_dto = 80; |FINSI;
    dedia = paga + 107;    || desde dia prorrata
    hadia = paga + 115;    || hasta dia prorrata
    desde = paga + 3;      || desde mes prorrata
    hasta = paga + 11;     || hasta mes prorrata
    mesco = paga + 19;     || mes de confeccion

    mas = 0;
    rompe = 0;
|SI #9hasta = #9desde; |Y #9dedia > #9hadia;  rompe = 1;  |FINSI;
|SI #9hasta < #9desde; |O rompe = 1;
        nume1 = #0#6 - 1;
        des1  = #9desde;   des2 =       1;
        has1  =      12;   has2 = #9hasta;
    |SI #9desde [ #0#7;                   || si ya ha empezado el ejer. actual
            mas = 1;                      || ejemplo: desde=4, hasta=3, actual=4
    |FINSI;                               || empieza a contar en este a¤o !!!
|SINO;
        nume1 = #0#6;
        des1 = #9desde;   des2 =  0;
        has1 = #9hasta;   has2 = -1;
|FINSI;
    nume1 = nume1 + mas;
    nume2 = #0#6 + mas;

    des = (nume1 * 100) + #9desde;  || mes desde
    has = (nume2 * 100) + #9hasta;  || mes hasta
    act = (#0#6  * 100) + #0#7;     || mes actual

|FPRC;

|PROCESO lee_convenio;
    #0#09 = #2#0;    |PINTA #0#9;
    #0#10 = #2#1;    |PINTA #0#10;

    #9#0 = #2#87;
|LEE 000#9=;                  || lee el convenio
    FEstado = FSalida;
|SI FEstado = 0;
        #1#0 = #2#0;
    |LEE 000#1=;              || lee la empresa
        FEstado = FSalida;
|FINSI;
|FPRC;

|PROCESO lee_traba1;
|HAZ lee_convenio;
|SI FEstado = 0;
        #2#50 = 0;  #2#51 = 0;     #2#52 = 0;     #2#53 = 0;  || acum. a cero
        #2#54 = 0;  #2#78 = 0;     #2#79 = 0;     #2#80 = 0;
    |PARA paga = 1; |SI paga [ 8; |HACIENDO paga = paga + 1;
        |HAZ busca;
        |SI #9mesco ! 0;
                nume1 = (des - (des $ 100)) / 100;
                nume2 = (act - (act $ 100)) / 100;
            |SI nume1 ! nume2;
                    any = nume1 $ 100;  de_mes=des $ 100;   ha_mes=12;
                |BUCLE lee_histo;
                    any = nume2 $ 100;  de_mes=1;           ha_mes=act $ 100;
                |BUCLE lee_histo;
            |SINO;
                    any = nume1 $ 100;  de_mes=des $ 100;   ha_mes=act $ 100;
                |BUCLE lee_histo;
            |FINSI;
        |FINSI;
    |SIGUE;
    |GRABA 020#2a;
|FINSI;
|LIBERA #2;
|FPRC;

|DEFBUCLE lee_traba;
 #2#1;
 ;
 #0#0, #0#2;
 #0#1, #0#3;
 be;
 NULL, lee_traba1;
|FIN;
____________________________________/ PROCESO INICIAL
|PROCESO recupera; |TIPO 3;
|SI #0#12 = "SI";
    |ATRI P;   |MENSA "    Recuperando Acumulados Dto. Dias ...";     |ATRI 0;
    |ABRE #1;
    |ABRE #9;
    |BUCLE lee_traba;
    |CIERRA #1;
    |CIERRA #9;
|FINSI;
|FPRC;
