|FICHEROS;
     nomir020;
     nomir001;
     labtraba;
     nomauto1,MANTE;
     nomauto2;
|FIN;

|VARIABLES;
     aAlfa = "";
     &enBoton = 0;
     nOpcion = 0;
     swBotonSiNo = 0;
     &nEmpAuto = 0;
     &nTraAuto = 0;
     &nAnyAuto = 0;
     &nCuotaAuto = 0;
     nCampo = 0;
     nCampo2 = 0;
|FIN;

|PROCESO Reduccion102_020; |TIPO 0;
     |SI #nomir020#0 > 120000;
          aAlfa = "    ATENCION: no se permiten en esta reduccion ";
          aAlfa = aAlfa + "importes superiores a 120000 euros";
          |MENAV aAlfa;
          |ERROR;
     |FINSI;
|FPRC;

|PROCESO Gastos;
     |SI nEmpAuto ! 0; |Y nTraAuto ! 0;
          |PUSHV 05012380;

          |ABRE #nomauto1;
          #nomauto1#0 = nEmpAuto;
          #nomauto1#1 = nTraAuto;
          nAnyAuto = #nomir001#2;

          |CLS;
          |PINPA #0#nomauto1;
          |PINDA #0#nomauto1;

          |LEE 000#nomauto1.=;
          |SI FSalida = 0;
               |ENDATOS #2#nomauto1;
          |SINO;
               |ENDATOS #1#nomauto1;
          |FINSI;

          |POPV;

          |CIERRA #nomauto1;
          #nomir020#2 = nCuotaAuto;
          |PINTA #nomir020#2;
     |FINSI;
|FPRC;

|PROCESO nomauto2;
     |PARA nCampo = 3; |SI nCampo [ 26; |HACIENDO nCampo = nCampo + 2;
          nCampo2 = nCampo + 1;
          nCuotaAuto = nCuotaAuto + #nomauto2#nCampo2#;
     |SIGUE;
|FPRC;

|DEFBUCLE nomauto2;
     #nomauto2#1;
     ;
     #labtraba#0, #labtraba#1, #nomir001#2;
     #labtraba#0, #labtraba#1, #nomir001#2;
     ;
     NULL, nomauto2;
|FIN;

|PROCESO labtraba;
     |SI #nomir001#2 ] 2013;
          |SI #labtraba#42 = "H"; |Y #labtraba#44 = "A";
               |CAMPO_MODIFICA #nomir020#2, 1, "N";
               |CAMPO_VISUAL #nomir020#2, 1, "S";
               |PINTA 1803, "Gastos deducibles (Art. 19.2: Seg. Social) ->                    :          ";
               |CONTROL_SIMPLE 0, "BOTON,Pulse para introducir gastos", 1849, 1867, Gastos;
               enBoton = FSalida;
               |ESTADO_CONTROL enBoton, "ENABLE";

               |PINTA #nomir020#2;

               nEmpAuto = #labtraba#0;
               nTraAuto = #labtraba#1;

               nCuotaAuto = 0;
               |BUCLE nomauto2;
               #nomir020#2 = nCuotaAuto;
               |PINTA #nomir020#2;

               |ERROR10;
          |FINSI;
     |SINO;
          |SI #labtraba#42 = "H"; |Y #labtraba#44 = "A";
               |CAMPO_MODIFICA #nomir020#2, 1, "S";
               |CAMPO_VISUAL #nomir020#2, 1, "S";
               |PINTA 1803, "Gastos deducibles (Art. 19.2: Seg. Social, mutualidades, etc.) ..:          ";
               |PINTA #nomir020#2;
               |ERROR10;
          |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE labtraba;
     #labtraba#2;
     ;
     #nomir001#0;
     #nomir001#0;
     ;
     NULL, labtraba;
|FIN;

|PROCESO Autonomos; |TIPO 7;
     |CAMPO_MODIFICA #nomir020#2, 1, "N";
     |CAMPO_VISUAL #nomir020#2, 1, "N";
     |PINTA 1868, "            ";

     nEmpAuto = 0;
     nTraAuto = 0;

     |BUCLE labtraba;
|FPRC;
