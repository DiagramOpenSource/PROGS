|FICHEROS;
    nomvauxi #0;
    labtraba #1;
    nomtraca #2;
    nomtrali #3;
    nomcatca #4;
    nomcatli #5;
|FIN;

|VARIABLES;
 campo = 0;
 campo1 = 0;
 campo2 = 0;
    xxxxfiac = "  ";
    xxaponer = "  ";
    xrelleno = "                              ";
    mensaje4 = "0000ATENCION !!! Concepto fuera de rango ...";
    dia1 = "";
    mes1 = "";
    any1 = "";
    dia = 0;
    mes = 0;
    any = 0;
    tope = 0;
    sw = 0;
    fec = "";
    blanco = "          ";
    puntos = "..........";
    varnum = 0;
    desnum = 0;
    hasnum = 0;
    fecsys = @;
    alfa1 = "";
    alfa2 = "";
    swcheck = 0;
    victima = "";
    posinum = 0;
    posialf = "";
    positra = 0;
    posipun = 0;
    swident = 0;
|FIN;

||************************************************************************
||                CALCULOS DESDE EL MANTENIMIENTO
||************************************************************************

|PROCESO checcon5; |TIPO 0;    || chequea rangos de conceptos
|SI #0Campo ! 0; |Y #0Campo ! 999;
        swcheck = 0;
    |SI #0Campo ] 101; |Y #0Campo [ 199; swcheck = 1; |FINSI;
    |SI #0Campo ] 201; |Y #0Campo [ 208; swcheck = 1; |FINSI;
    |SI #0Campo ] 301; |Y #0Campo [ 310; swcheck = 1; |FINSI;
    |SI #0Campo ] 401; |Y #0Campo [ 420; swcheck = 1; |FINSI;
    |SI #0Campo ] 501; |Y #0Campo [ 506; swcheck = 1; |FINSI;
    |SI #0Campo ] 901; |Y #0Campo [ 910; swcheck = 1; |FINSI;
    |SI swcheck = 0;
        |MENAV mensaje4;
        |ERROR;
    |FINSI;
|FINSI;
|FPRC;

|PROCESO checnume1; |TIPO 0;
|SI #0Campo ! "          ";
        posipun = 0;
        victima = #0Campo;  posinum = victima;
    |QBT victima;
        victima = "          " + victima;
        victima = victima % -110;                     || ajusta a la derecha
    |SI posinum ] -999999.99; |Y posinum [ 999999.99;
        |PARA posinum = 1; |SI posinum [ 10; |Y posinum ! 0; |HACIENDO posinum = posinum + 1;
                positra = 100 * posinum;
                positra = -1 - positra;
                posialf = victima % positra;
            |HAZ identida;
            |SI swident = 4;                || caracter igual al espacio
                posinum = 10;               || sale bien
            |FINSI;
            |SI swident = 3;                || caracter igual al signo (+/-)
                positra = 100 + posinum;
                positra = 0 - positra;
                victima = victima % positra;
                victima = "          " + victima;
                victima = victima % -110;
                posinum = 10;               || sale bien
            |FINSI;
            |SI swident = 2;                || caracter igual al punto decimal
                |SI posipun = 0;
                    posipun = posinum;
                |SINO;
                    positra = posinum - posipun;
                    |SI positra = 2;
                        swident = 0;
                    |SINO;
                        posipun = posinum;
                    |FINSI;
                |FINSI;
            |FINSI;
            |SI swident = 0;                || caracter malo, acabar !
                posinum = -1;               || sale mal
            |FINSI;
        |SIGUE;
        |SI posipun ! 0; |Y swident ! 0;        || si hay decimales y es bueno
            posipun = posipun - 1;
            |SI posipun ! 0;
                positra = 100 + posipun;
                positra = 0 - positra;
                posialf = victima % positra;       || separa decimales
                posipun = posipun + 1;
                positra = (posipun + 1) * 100;
                positra = positra + (10 - posipun);
                positra = 0 - positra;
                victima = victima % positra;       || coge la parte entera
            |QBF posialf;
                posialf = posialf + "00";
                posialf = posialf % 102;           || redondea decimales
                victima = victima + "." + posialf; || construye numero
            |SINO;
                victima = victima % 109;
            |FINSI;
         |FINSI;
    |SINO;
        posinum = 0;
    |FINSI;
    |SI posinum = 0;
        |ERROR;
    |SINO;
        |QBT victima;
            victima = "          " + victima;
            #0Campo = victima % -110;
        |PINTA #0Campo;
    |FINSI;
|FINSI;
|FPRC;

|PROCESO checnume2; |TIPO 0;
|SI #0Campo ! "            ";
        posipun = 0;
        victima = #0Campo;  posinum = victima;
    |QBT victima;
        victima = "            " + victima;
        victima = victima % -112;                     || ajusta a la derecha
    |SI posinum ] -99999999.99; |Y posinum [ 99999999.99;
        |PARA posinum = 1; |SI posinum [ 12; |Y posinum ! 0; |HACIENDO posinum = posinum + 1;
                positra = 100 * posinum;
                positra = -1 - positra;
                posialf = victima % positra;
            |HAZ identida;
            |SI swident = 4;                || caracter igual al espacio
                posinum = 12;               || sale bien
            |FINSI;
            |SI swident = 3;                || caracter igual al signo (+/-)
                positra = 100 + posinum;
                positra = 0 - positra;
                victima = victima % positra;
                victima = "            " + victima;
                victima = victima % -112;
                posinum = 12;               || sale bien
            |FINSI;
            |SI swident = 2;                || caracter igual al punto decimal
                |SI posipun = 0;
                    posipun = posinum;
                |SINO;
                    positra = posinum - posipun;
                    |SI positra = 2;
                        swident = 0;
                    |SINO;
                        posipun = posinum;
                    |FINSI;
                |FINSI;
            |FINSI;
            |SI swident = 0;                || caracter malo, acabar !
                posinum = -1;               || sale mal
            |FINSI;
        |SIGUE;
        |SI posipun ! 0; |Y swident ! 0;        || si hay decimales y es bueno
            posipun = posipun - 1;
            |SI posipun ! 0;
                positra = 100 + posipun;
                positra = 0 - positra;
                posialf = victima % positra;       || separa decimales
                posipun = posipun + 1;
                positra = (posipun + 1) * 100;
                positra = positra + (12 - posipun);
                positra = 0 - positra;
                victima = victima % positra;       || coge la parte entera
            |QBF posialf;
                posialf = posialf + "00";
                posialf = posialf % 102;           || redondea decimales
                victima = victima + "." + posialf; || construye numero
            |SINO;
                victima = victima % 111;
            |FINSI;
         |FINSI;
    |SINO;
        posinum = 0;
    |FINSI;
    |SI posinum = 0;
        |ERROR;
    |SINO;
        |QBT victima;
            victima = "            " + victima;
            #0Campo = victima % -112;
        |PINTA #0Campo;
    |FINSI;
|FINSI;
|FPRC;

|PROCESO identida;
    swident = 0;
|SI posialf = "-"; |O posialf = "+";    swident = 3; |FINSI; || signos
|SI posialf = ".";                      swident = 2; |FINSI; || punto
|SI posialf = "0"; |O posialf = "1";
                   |O posialf = "2";
                   |O posialf = "3";
                   |O posialf = "4";
                   |O posialf = "5";
                   |O posialf = "6";
                   |O posialf = "7";
                   |O posialf = "8";
                   |O posialf = "9";    swident = 1; |FINSI; || numeros
|SI posialf = " ";                      swident = 4; |FINSI; || espacios
|FPRC;

|PROCESO nomodi; |TIPO 7;
|SI #0#0 = "C";
    |HAZ nomodifi;
|FINSI;
|FPRC;

|PROCESO nomodifi;             || RUTINA DE CONVERSION A NO MODIFICABLES
                               || VARIABLES: A_
    xxaponer = Anono;
    xxxxfiac = Control % A1001;
    Control = Control + xxxxfiac;
    Control = Control + xrelleno;
    Control = Control % A120;
    Control = Control + xxaponer;
|FPRC;


||************************************************************************
||                CALCULOS DEL PROGRAMA
||************************************************************************

|PROCESO trabajad;
|SI #1#44 = "A";
    |PINTA 0667,#1#0;
    |PINTA 0767,#1#1;
    |HAZ cabtraba;
    |HAZ lintraba;
|FINSI;
|FPRC;

|PROCESO categori;
|PINTA 1067,#4#0;
|PINTA 1167,#4#1;

|PARA campo = 9; |SI campo [ 32; |HACIENDO campo = campo + 1;
   |SI #0campo ! 0;
      campo1 = campo + 24;
      campo2 = campo + 48;
      |DDEFECTO #5;
       #5#0 = #4#0;
       #5#1 = #4#1;
       #5#2 = #0campo;
       |LEE 140#5=;
       |SI FSalida = 0;
          |SI #0campo1 ! "          ";  #5#3 = #0campo1; |FINSI;
          |SI #0campo2 ! "          ";  #5#4 = #0campo2; |FINSI;
          |GRABA 020#5a;
       |SINO;
          |DDEFECTO #5;
          #5#0 = #4#0;
          #5#1 = #4#1;
          #5#2 = #0campo;
          #5#3 = #0campo1;
          #5#4 = #0campo2;
          |GRABA 020#5n;
       |FINSI;
       |LIBERA #5;
   |FINSI;
|SIGUE;
|FPRC;

|DEFBUCLE nomvauxi0;
#1#1;
87,17;
#0#1, #0#3, #0#5, #0#7;
#0#2, #0#4, #0#6, #0#8;
e;
NULL,trabajad;
|FIN;

|DEFBUCLE nomvauxi1;
#4#1;
;
#0#5, #0#7;
#0#6, #0#8;
e;
NULL,categori;
|FIN;

|PROCESO cabtraba;  || Cabecera Auxiliares Trabajadores
#2#0 = #1#0;
#2#1 = #1#1;
|LEE 140#2=;
|SI FSalida ! 0;
   #2#0 = #1#0;
   #2#1 = #1#1;
   #2#2 = #1#2;
   |GRABA 020#2n;
|FINSI;
|LIBERA #2;
|FPRC;

|PROCESO lintraba;   || Linea Auxiliares Trabajadores

|PARA campo = 9; |SI campo [ 32; |HACIENDO campo = campo + 1;
   |SI #0campo ! 0;
      campo1 = campo + 24;
      campo2 = campo + 48;
      |DDEFECTO #3;
      #3#0 = #1#0;
      #3#1 = #1#1;
      #3#2 = #0campo;
      |LEE 140#3=;
      |SI FSalida = 0;
          |SI #0campo1 ! "          ";  #3#3 = #0campo1; |FINSI;
          |SI #0campo2 ! "          ";  #3#4 = #0campo2; |FINSI;
          |GRABA 020#3a;
      |SINO;
          |DDEFECTO #3;
          #3#0 = #1#0;
          #3#1 = #1#1;
          #3#2 = #0campo;
          #3#3 = #0campo1;
          #3#4 = #0campo2;
          |GRABA 020#3n;
      |FINSI;
      |LIBERA #3;
   |FINSI;
|SIGUE;
|FPRC;

|PROCESO auxtrab;  || Calculo para Auxiliares de Trabajadores
|ABRE #2;
|ABRE #3;
|BUCLE nomvauxi0;
|CIERRA #2;
|CIERRA #3;
|FPRC;

|PROCESO auxcate; || Calculo para Auxiliares de Categorias
|ABRE #5;
|BUCLE nomvauxi1;
|CIERRA #5;
|FPRC;

|PROCESO pase; |TIPO 3;
|SI #0#0 = "T"; |HAZ auxtrab; |SINO; |HAZ auxcate; |FINSI;
|FPRC;
