|FICHEROS;
     semcarse #0;
     labtraba #1;        || trabajadores
     semtraba #2,MANTE;  || semanal cabs.
     semtrcon #3,MANTE;  || semanal lins.
     semconce #4,MANTE;  || control de conceptos
     nomtrcal #5;        || calendario laboral
     semempre #6;        || control de empresas
     semequiv #7;        || equivalencia de codigos
     semimppc #8;        || predeterminados cabs.
     semimppl #9;        || predeterminados lins.
     nomvarca #10;       || variaciones cabs.
     labempre #11;       || empresas (liquido)
     nomtraca #12;       || trabajadores cabs. (liquido)
     semliqui #13,MANTE; || ayuda (liquido)
     nomconst #14;       || constantes (liquido)
     semcntcu;
     nomabsca;
     nomabsli;
|FIN;

|VARIABLES;
     sdia = 0;
     smas = 0;
     sdife = 0;
     srest = 0;

     alfa1 = "";
     alfa2 = "";
     fech1 = @;
     fech2 = @;
     nume1 = 0;
     nume2 = 0;
     nume3 = 0;
     para1 = 0;
     para2 = 0;

     DiasLabs = 0;   || para contar por Horas
     DiasNats = 0;   || para contar por Dias
     Comodin  = "";
     Comodin1 = "";
     Dia1 = 0;
     Mes1 = 0;
     Dia2 = 0;
     Mes2 = 0;
     Dia = "";
     Mes = "";

     Cont = 0;
     Calc = 0;

     nCuantos = 0;
     aAlfa1 = "";
     aAlfa2 = "";
     nNume1 = 0;
     nNume2 = 0;
     fFech1 = @;
     nViernes = 0;
     nSoloViernes = 0;
     nMas = 0;
     nDesde = 0;
     nHasta = 0;
     nEstado = 0;
     nVarMes = 0;
     tdenfe_30 = 0;
     tdenfe_21 = 0;
     tdenfe_22 = 0;
     tdenfe_23 = 0;
     tdenfe_24 = 0;
     denfe_30 = 0;
     denfe_21 = 0;
     denfe_22 = 0;
     denfe_23 = 0;
     denfe_24 = 0;
     topemes1 = 0;
     topemes2 = 0;
     denfe_A = 0;
     denfe_B = 0;
     denfe_C = 0;
     denfe_D = 0;
     denfe_A0 = 0;
     denfe_B0 = 0;
     denfe_C0 = 0;
     denfe_D0 = 0;
     denfe_0 = 0;
     nCtoPres = 0;
     aDesPres = "";
     nImpPres = 0;
     base_1 = 0;
     base_2 = 0;
     base_3 = 0;
     prom_enf = 0;
     prom_acc = 0;
     dacci_2 = 0;
     dmate_2 = 0;
     denfe_4 = 0;
     nTopeMes = 0;
     CodigoCons = 0;
     &EURODB = 0;
     aDef = "";

     swNoEntra = 0;
     nVeces = 0;
     nVez = 0;
     fFechaDesde = @;
     fFechaHasta = @;
     aFechaDesde = "";
     aFechaHasta = "";
     alfa3 = "";
     alfa4 = "";
|FIN;

|PROCESO Novecientos; |TIPO 0;
     |C_M #semconce#1, 1, "S";
     |C_M #semconce#2, 1, "S";
     |C_M #semconce#3, 1, "S";
     |C_M #semconce#4, 1, "S";
     |C_M #semconce#5, 1, "S";
     |SI #semconce#0 = 102;
          #semconce#1 = "ANTIGUEDAD";
          #semconce#2 = "S";
          #semconce#3 = "A";
          #semconce#4 = "S";
          #semconce#5 = 2;
          |PINDA #0#semconce;
          |C_M #semconce#1, 1, "N";
          |C_M #semconce#2, 1, "N";
          |C_M #semconce#3, 1, "N";
          |C_M #semconce#4, 1, "N";
          |C_M #semconce#5, 1, "N";
     |FINSI;
     |SI #semconce#0 = 103;
          #semconce#1 = "BENEFICIOS";
          #semconce#2 = "S";
          #semconce#3 = "A";
          #semconce#4 = "S";
          #semconce#5 = 3;
          |PINDA #0#semconce;
          |C_M #semconce#1, 1, "N";
          |C_M #semconce#2, 1, "N";
          |C_M #semconce#3, 1, "N";
          |C_M #semconce#4, 1, "N";
          |C_M #semconce#5, 1, "N";
     |FINSI;
     |SI #semconce#0 = 906;
          #semconce#1 = "ANTICIPOS";
          #semconce#2 = "S";
          #semconce#3 = "A";
          #semconce#4 = "N";
          #semconce#5 = 0;
          |PINDA #0#semconce;
          |C_M #semconce#1, 1, "N";
          |C_M #semconce#2, 1, "N";
          |C_M #semconce#3, 1, "N";
          |C_M #semconce#4, 1, "N";
          |C_M #semconce#5, 1, "N";
     |FINSI;
     |SI #semconce#0 = 971;|O #semconce#0 = 972;|O #semconce#0 = 973;
          nNume1 = #semconce#0 - 970;   || descripcion
          nNume2 = #semconce#0 - 961;   || situacion
          #semconce#1 = "ESPECIAL" + nNume1;
          #semconce#2 = "S";
          #semconce#3 = "A";
          #semconce#4 = "N";
          #semconce#5 = nNume2;
          |PINDA #0#semconce;
          |C_M #semconce#1, 1, "N";
          |C_M #semconce#2, 1, "N";
          |C_M #semconce#3, 1, "N";
          |C_M #semconce#4, 1, "N";
          |C_M #semconce#5, 1, "N";
     |FINSI;
     |SI #semconce#0 = 981;|O #semconce#0 = 982;|O #semconce#0 = 983;
          nNume1 = #semconce#0 - 980;   || descripcion
          nNume2 = #semconce#0 - 971;   || situacion
          #semconce#1 = "PRESTAC." + nNume1;
          #semconce#2 = "S";
          #semconce#3 = "A";
          #semconce#4 = "N";
          #semconce#5 = nNume2;
          |PINDA #0#semconce;
          |C_M #semconce#1, 1, "N";
          |C_M #semconce#2, 1, "N";
          |C_M #semconce#3, 1, "N";
          |C_M #semconce#4, 1, "N";
          |C_M #semconce#5, 1, "N";
     |FINSI;
|FPRC;

|PROCESO PresentaLineas;
     Calc = ((12 + Cont) * 100) + 42;
     |PINTA Calc, #semtrcon#2;
     Calc = ((12 + Cont) * 100) + 46;
     |PINTA Calc, #semtrcon#3;
     Calc = ((12 + Cont) * 100) + 70;
     |PINTA Calc, #semtrcon#7;
     Calc = ((12 + Cont) * 100) + 72;
     |PINTA Calc, #semtrcon#4;
     Cont = Cont + 1;
     |SI Cont > 10; |ERROR10; |FINSI;
|FPRC;

|DEFBUCLE PresentaLineas;
     #semtrcon#1;
     ;
     #semcarse#0,#semcarse#1,#semtraba#0,#semtraba#1,001;
     #semcarse#0,#semcarse#1,#semtraba#0,#semtraba#1,999;
     ;
     NULL,PresentaLineas;
|FIN;

|PROCESO RecalculaBruto; |TIPO 0;
     #semliqui#3 = ((#semliqui#0 * 100) / (100 - (#semliqui#4 + #semliqui#5))) ! EURODB;
     #semliqui#1 = (#semliqui#3 % #semliqui#4) ! EURODB;    || cuota IRPF
     #semliqui#2 = (#semliqui#3 % #semliqui#5) ! EURODB;    || cuota SS
     |PINTA #semliqui#1;
     |PINTA #semliqui#2;
     |PINTA #semliqui#3;
|FPRC;

|PROCESO RecalculaLiquido; |TIPO 0;
     #semliqui#1 = (#semliqui#3 % #semliqui#4) ! EURODB;    || cuota IRPF
     #semliqui#2 = (#semliqui#3 % #semliqui#5) ! EURODB;    || cuota SS
     #semliqui#0 = #semliqui#3 - (#semliqui#1 + #semliqui#2);
     |PINTA #semliqui#1;
     |PINTA #semliqui#2;
     |PINTA #semliqui#0;
|FPRC;

|PROCESO Liquido; |TIPO 0;
     |SI FSalida = 13;
          |ABRE #labtraba;
          #labtraba#0 = #semtraba#0;
          #labtraba#1 = #semtraba#1;
          |LEE 020#labtraba.=;
          |CIERRA #labtraba;

          CodigoCons = 0;
          |DDEFECTO #nomtraca;
          |ABRE #nomtraca;
          #nomtraca#0 = #labtraba#0;
          #nomtraca#1 = #labtraba#1;
          |LEE 000#nomtraca.=;
          |SI FSalida = 0;|Y #nomtraca#3 ! 0;
               CodigoCons = #nomtraca#3;
          |FINSI;
          |CIERRA #nomtraca;

          |ABRE #labempre;
          #labempre#0 = #semtraba#0;
          |LEE 020#labempre.=;
          |CIERRA #labempre;
          |SI CodigoCons = 0;
               CodigoCons = #labempre#32;
          |FINSI;

          |ABRE #nomconst;
          #nomconst#0 = CodigoCons;
          |LEE 020#nomconst.=;
          |CIERRA #nomconst;

          #semliqui#0 = #semtrcon#4;         || liquido
          #semliqui#4 = #labtraba#30;        || % IRPF
          #semliqui#5 = #nomconst#7 + #nomconst#13 + #nomconst#15 + #nomconst#17 + #nomconst#19;   || % SS
          #semliqui#3 = ((#semliqui#0 * 100) / (100 - (#semliqui#4 + #semliqui#5))) ! EURODB;
          #semliqui#1 = (#semliqui#3 % #semliqui#4) ! EURODB;    || cuota IRPF
          #semliqui#2 = (#semliqui#3 % #semliqui#5) ! EURODB;    || cuota SS
          |AVISO;
          |PUSHV 0501 2380;
          |PINPA #0#semliqui;
          |PINDA #0#semliqui;
          |ENDATOS #1#semliqui;
          |SI FSalida = 0;
               #semtrcon#4 = #semliqui#3;
          |FINSI;
          |POPV;
          |PINTA #semtrcon#4;
          |ERROR;
     |FINSI;
|FPRC;

|PROCESO Presenta; |TIPO 7;
     |PINTA 1242, "   "; |PINTA 1246, "                       "; |PINTA 1270, " "; |PINTA 1272, "         ";
     |PINTA 1342, "   "; |PINTA 1346, "                       "; |PINTA 1370, " "; |PINTA 1372, "         ";
     |PINTA 1442, "   "; |PINTA 1446, "                       "; |PINTA 1470, " "; |PINTA 1472, "         ";
     |PINTA 1542, "   "; |PINTA 1546, "                       "; |PINTA 1570, " "; |PINTA 1572, "         ";
     |PINTA 1642, "   "; |PINTA 1646, "                       "; |PINTA 1670, " "; |PINTA 1672, "         ";
     |PINTA 1742, "   "; |PINTA 1746, "                       "; |PINTA 1770, " "; |PINTA 1772, "         ";
     |PINTA 1842, "   "; |PINTA 1846, "                       "; |PINTA 1870, " "; |PINTA 1872, "         ";
     |PINTA 1942, "   "; |PINTA 1946, "                       "; |PINTA 1970, " "; |PINTA 1972, "         ";
     |PINTA 2042, "   "; |PINTA 2046, "                       "; |PINTA 2070, " "; |PINTA 2072, "         ";
     |PINTA 2142, "   "; |PINTA 2146, "                       "; |PINTA 2170, " "; |PINTA 2172, "         ";
     Cont = 0;
     |BUCLE PresentaLineas;
|FPRC;

|PROCESO Importes;
     |SI nSoloViernes = 1;    || semana ultimo viernes para los "M"
          |SI #semimppl#8 = "S";   || solo entran los N y los T
               |ACABA;
          |FINSI;
     |FINSI;
     |SI nSoloViernes = 2;    || semana sin ultimo viernes para los "M"
          |SI #semimppl#8 = "N";   || solo entran los S y los T
               |ACABA;
          |FINSI;
     |FINSI;

     |DDEFECTO #semtrcon;
     #semtrcon#5 = #semcarse#0;
     #semtrcon#6 = #semcarse#1;
     #semtrcon#0 = #labtraba#0;
     #semtrcon#1 = #labtraba#1;
     #semtrcon#2 = #semimppl#3;
     |LEE 101#semtrcon.=;
     |SI FSalida = 0;
          |SI #semcarse#7 = "S";
               |BORRA 020#semtrcon.a;
               |LIBERA #semtrcon;
          |SINO;
               |LIBERA #semtrcon;
               |ACABA;
          |FINSI;
     |FINSI;

     #semtrcon#5 = #semcarse#0;    || a¤o
     #semtrcon#6 = #semcarse#1;    || semana
     #semtrcon#0 = #labtraba#0;    || empresa
     #semtrcon#1 = #labtraba#1;    || trabajador
     #semtrcon#2 = #semimppl#3;    || concepto
     #semtrcon#3 = #semimppl#4;    || descripcion
     |SI #semimppl#6 = "G";
          #semtrcon#4 = #semimppl#5;    || importe glogal
     |SINO;
          |SI #semcarse#5 ! 0;
               #semtrcon#4 = #semimppl#5 * #semcarse#5;     || por dias
          |SINO;
               #semtrcon#4 = #semimppl#5 * DiasNats;
          |FINSI;
     |FINSI;
     #semtrcon#7 = #semimppl#7;    || tipo
     #semtrcon#8 = 0;              || unidades mes1
     #semtrcon#9 = 0;              || unidades mes2
     |GRABA 020#semtrcon.n;
|FPRC;

|DEFBUCLE Importes;
     #semimppc#1;
     ;
     #labtraba#0, #labtraba#1;
     #labtraba#0, #labtraba#1;
     ;
     NULL, NULL, Importes;
|FIN;

|PROCESO Conceptos;
     |DDEFECTO #semtrcon;
     #semtrcon#0 = #labtraba#0;
     #semtrcon#1 = #labtraba#1;
     #semtrcon#2 = #semconce#0;
     #semtrcon#5 = #semcarse#0;
     #semtrcon#6 = #semcarse#1;
     |LEE 101#semtrcon.=;
     |SI FSalida = 0;
          |SI #semcarse#7 = "S";
               |BORRA 020#semtrcon.a;
               |LIBERA #semtrcon;
          |SINO;
               |LIBERA #semtrcon;
               |ACABA;
          |FINSI;
     |FINSI;

     #semtrcon#0 = #labtraba#0;
     #semtrcon#1 = #labtraba#1;
     #semtrcon#2 = #semconce#0;
     #semtrcon#3 = #semconce#1;
     #semtrcon#4 = 0;
     |SI #semconce#3 = "D";
          |SI #semcarse#5 ! 0;
               #semtrcon#4 = #semcarse#5;
          |SINO;
               #semtrcon#4 = DiasNats;
          |FINSI;
     |FINSI;
     |SI #semconce#3 = "H";
          |SI #semcarse#12 ! 0;|O #semcarse#13 ! 0;
               #semtrcon#4 = #semcarse#12 + #semcarse#13;
               #semtrcon#8 = #semcarse#12;
               #semtrcon#9 = #semcarse#13;
          |SINO;
               #semtrcon#8 = 0;
               #semtrcon#9 = 0;
               |SI #semcarse#6 ! 0;
                    #semtrcon#4 = #semcarse#6;
               |SINO;
                    #semtrcon#4 = DiasLabs * #0#4;
               |FINSI;
          |FINSI;
     |FINSI;
     #semtrcon#5 = #semcarse#0;
     #semtrcon#6 = #semcarse#1;
     #semtrcon#7 = #semconce#2;
     |GRABA 020#semtrcon.n;
|FPRC;

|DEFBUCLE Conceptos;
     #semconce#1;
     4;
       1, "S";
     999, "S";
     ;
     NULL, Conceptos;
|FIN;

|PROCESO PagaPrestaciones;
     #semtrcon#5 = #semcarse#0;
     #semtrcon#6 = #semcarse#1;
     #semtrcon#0 = #labtraba#0;
     #semtrcon#1 = #labtraba#1;
     #semtrcon#2 = nCtoPres;
     |LEE 101#semtrcon.=;
     |SI FSalida = 0;
          |BORRA 020#semtrcon.a;
          |LIBERA #semtrcon;
     |FINSI;
     |SI nImpPres = 0;   |ACABA;   |FINSI;

     |DDEFECTO #semtrcon;
     #semtrcon#5 = #semcarse#0;    || a¤o
     #semtrcon#6 = #semcarse#1;    || semana
     #semtrcon#0 = #labtraba#0;    || empresa
     #semtrcon#1 = #labtraba#1;    || trabajador
     #semtrcon#2 = nCtoPres;       || concepto
     #semtrcon#3 = aDesPres;       || descripcion
     #semtrcon#4 = nImpPres;       || importe
     #semtrcon#7 = "S";            || tipo
     #semtrcon#8 = 0;              || unidades mes1
     #semtrcon#9 = 0;              || unidades mes2
     |GRABA 020#semtrcon.n;
|FPRC;

|PROCESO Prestaciones;
     denfe_0 = 0;
     |SI #labtraba#60 = "E";
          denfe_0 = #labtraba#89;
     |FINSI;

     |PARA para1 = 1; |SI para1 [ 4; |HACIENDO para1 = para1 + 1;
          denfe_A0 = 0;
          denfe_B0 = 0;         || acumuladores parciales
          denfe_C0 = 0;
          denfe_D0 = 0;
          |SI para1 = 1;  nume1 = denfe_21 + denfe_0 + denfe_30; |FINSI;
          |SI para1 = 2;  nume1 = denfe_22;            |FINSI;    || dias enfermo
          |SI para1 = 3;  nume1 = denfe_23;            |FINSI;
          |SI para1 = 4;  nume1 = denfe_24;            |FINSI;
          |SI nume1 ! 0;
               |SI nume1 ] 3;
                    denfe_A0 = 3;     nume1 = nume1 - 3;       || al  0%
               |SINO;
                    denfe_A0 = nume1; nume1 = 0;
               |FINSI;
               |SI nume1 ] 12;
                    denfe_D0 = 12;    nume1 = nume1 - 12;      || al 60% (empresario)
               |SINO;
                    denfe_D0 = nume1; nume1 = 0;
               |FINSI;
               |SI nume1 ] 5;
                    denfe_B0 = 5;     nume1 = nume1 - 5;       || al 60% (seg.social)
               |SINO;
                    denfe_B0 = nume1; nume1 = 0;
               |FINSI;
               |SI nume1 ] 1;
                    denfe_C0 = nume1;                          || al 75%
               |SINO;
                    denfe_C0 = nume1; nume1 = 0;
               |FINSI;

               |SI para1 = 1;  nume1 = denfe_21;            |FINSI;
               |SI para1 = 2;  nume1 = denfe_22;            |FINSI;    || dias enfermo
               |SI para1 = 3;  nume1 = denfe_23;            |FINSI;
               |SI para1 = 4;  nume1 = denfe_24;            |FINSI;
               |SI denfe_C0 > nume1;
                    denfe_A0 = 0;
                    denfe_D0 = 0;
                    denfe_B0 = 0;
                    denfe_C0 = nume1;
               |SINO;
                    nume1 = nume1 - denfe_C0;
               |FINSI;
               |SI denfe_B0 > nume1;
                    denfe_A0 = 0;
                    denfe_D0 = 0;
                    denfe_B0 = nume1;
               |SINO;
                    nume1 = nume1 - denfe_B0;
               |FINSI;
               |SI denfe_D0 > nume1;
                    denfe_A0 = 0;
                    denfe_D0 = nume1;
               |SINO;
                    nume1 = nume1 - denfe_D0;
               |FINSI;
               |SI denfe_A0 > nume1;
                    denfe_A0 = nume1;
               |FINSI;

               denfe_A = denfe_A + denfe_A0;
               denfe_B = denfe_B + denfe_B0;
               denfe_C = denfe_C + denfe_C0;
               denfe_D = denfe_D + denfe_D0;
          |FINSI;
     |SIGUE;

     base_3 = prom_acc * .75 * dacci_2;      || accidente  al 75% (504)
     denfe_4 = prom_enf * .60 * denfe_D;     || enfer.empr.al 60% (515)
     base_2 = prom_enf * .60 * denfe_B;      || enfermedad al 60% (503)
     base_1 = prom_enf * .75 * denfe_C;      || enfermedad al 75% (503)

     nCtoPres = 982;
     nImpPres = denfe_4 + base_2;
     nNume1 = denfe_D + denfe_B;
     nNume2 = (prom_enf * .60) ! EURODB;
     aDesPres = "IT 60% (" + nNume1 + ")*(" + nNume2 + ")";
     |HAZ PagaPrestaciones;

     nCtoPres = 983;
     nImpPres = base_3 + base_1;
     nNume1 = dacci_2 + denfe_C;
     nNume2 = (prom_enf * .75) ! EURODB;
     aDesPres = "IT 75% (" + nNume1 + ")*(" + nNume2 + ")";
     |HAZ PagaPrestaciones;
|FPRC;

|PROCESO AcumPrestaciones;
     tdenfe_30 = 0;
     tdenfe_21 = 0;
     tdenfe_22 = 0;
     tdenfe_23 = 0;
     tdenfe_24 = 0;
     |PARA nEstado = 12; |SI nEstado [ 15; |HACIENDO nEstado = nEstado + 1;
          nDesde = nEstado - 8;
          nHasta = nEstado - 4;
          |SI #nomvarca#nHasta# ] topemes1;
               |SI topemes1 < #nomvarca#nDesde#;  topemes1 = #nomvarca#nDesde#; |FINSI;
               |SI topemes2 > #nomvarca#nHasta#;  topemes2 = #nomvarca#nHasta#; |FINSI;
               |SI #nomvarca#nDesde# [ topemes2;
                    nume1 = topemes2 - topemes1 + 1;
                    |SI #nomvarca#nEstado# = "E";
                         |SI tdenfe_21 = 0;
                              tdenfe_21 = nume1;
                              nume1 = 0;
                              |SI #nomvarca#nDesde# < topemes1;
                                   || viene enfermo de semanas anteriores
                                   |SI #nomvarca#nDesde# = 0;
                                        tdenfe_30 = tdenfe_30 + (topemes1 - 1);
                                   |SINO;
                                        tdenfe_30 = tdenfe_30 + (topemes1 - #nomvarca#nDesde#);
                                   |FINSI;
                              |FINSI;
                         |FINSI;
                         |SI tdenfe_22 = 0;  tdenfe_22 = nume1;  nume1 = 0;  |FINSI;
                         |SI tdenfe_23 = 0;  tdenfe_23 = nume1;  nume1 = 0;  |FINSI;
                         |SI tdenfe_24 = 0;  tdenfe_24 = nume1;  nume1 = 0;  |FINSI;
                    |FINSI;
                    |SI #nomvarca#nEstado# = "A";
                         dacci_2 = dacci_2 + nume1;
                    |FINSI;
                    |SI #nomvarca#nEstado# = "M";
                         dmate_2 = dmate_2 + nume1;
                    |FINSI;
               |FINSI;
          |FINSI;
     |SIGUE;
     denfe_30 = denfe_30 + tdenfe_30;
     denfe_21 = denfe_21 + tdenfe_21;
     denfe_22 = denfe_22 + tdenfe_22;
     denfe_23 = denfe_23 + tdenfe_23;
     denfe_24 = denfe_24 + tdenfe_24;
|FPRC;

|PROCESO nomabsca;
     swNoEntra = 0;
     nNume1 = 8;

     |ABRE #nomabsca;
     #nomabsca#0 = #labtraba#0;
     #nomabsca#1 = #labtraba#1;
     |LEE 000#nomabsca.=;
     |SI FSalida = 0;
          nNume1 = #nomabsca#2;
          swNoEntra = 1;
     |FINSI;

     |CIERRA #nomabsca;
|FPRC;

|PROCESO Absentismos;
     alfa1 = #nomabsli#2;
     alfa1 = alfa1 % 102;
     topemes1 = alfa1;

     alfa2 = #nomabsli#3;
     alfa2 = alfa2 % 102;
     topemes2 = alfa2;

|| xxxxxxxxxxx
     |HAZ nomabsca;

     |SI #nomabsli#6 = 0;
          nMas = nMas - 1;
     |SINO;
          nMas = nMas - ((#nomabsli#6 / nNume1) ! 3);
     |FINSI;
|FPRC;

|DEFBUCLE AbsHueVac;
     #nomabsli#1;
     4;
     #labtraba#0, #labtraba#1, fFechaDesde, "A";
     #labtraba#0, #labtraba#1, fFechaHasta, "A";
     ;
     NULL, Absentismos;
|FIN;

|PROCESO AbsNuevo;
     alfa1 = Cont; alfa1 = ("00" + alfa1) % -102;
     alfa2 = Cont; alfa2 = ("00" + alfa2) % -102;
     alfa3 = nVarMes; alfa3 = ("00" + alfa3) % -102;
     alfa4 = #0#0;

     aFechaDesde = alfa1 + "." + alfa3 + "." + alfa4;
     aFechaHasta = alfa2 + "." + alfa3 + "." + alfa4;

     fFechaDesde = aFechaDesde;
     fFechaHasta = aFechaHasta;
     |BUCLE AbsHueVac;
|FPRC;

|PROCESO Variacion;
     |PARA nEstado = 12; |SI nEstado [ 15; |HACIENDO nEstado = nEstado + 1;
          |SI #nomvarca#nEstado# ! " ";
               nDesde = nEstado - 8;
               nHasta = nEstado - 4;
               |SI #nomvarca#nHasta# = 0;    #nomvarca#nHasta# = 31;  |FINSI;
               |SI Cont ] #nomvarca#nDesde#;|Y Cont [ #nomvarca#nHasta#;
                    nMas = 0;
                    |SI #nomvarca#nEstado# = "H";
                         #nomvarca#22 = 0;
                    |FINSI;
                    |SI #nomvarca#nEstado# = "F";
                         nMas = nMas - (#nomvarca#23 / 100);
                         #nomvarca#23 = 0;
                    |FINSI;
                    |SAL;
               |FINSI;
          |FINSI;
     |SIGUE;
|FPRC;

|PROCESO Baja;
     |SI #labtraba#37 = "          ";|O #labtraba#37 = "..........";
          |ACABA;
     |FINSI;

     fFech1 = #labtraba#37;
     |SI fFech1 ] #semcarse#2;|Y fFech1 [ #semcarse#3;
          aAlfa1 = fFech1;
          aAlfa2 = aAlfa1 % 102;
          nNume1 = aAlfa2;
          |SI Cont > nNume1;
               nMas = 0;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Alta;
     |SI #labtraba#36 = "          ";|O #labtraba#36 = "..........";
          |ACABA;
     |FINSI;

     fFech1 = #labtraba#36;
     |SI fFech1 ] #semcarse#2;|Y fFech1 [ #semcarse#3;
          aAlfa1 = fFech1;
          aAlfa2 = aAlfa1 % 102;
          nNume1 = aAlfa2;
          |SI Cont < nNume1;
               nMas = 0;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO CuentaDias;
     nMas = 1;

     |HAZ Alta;     |SI nMas = 0;  |ACABA;   |FINSI;
     |HAZ Baja;     |SI nMas = 0;  |ACABA;   |FINSI;
     |HAZ Variacion;
     |HAZ AbsNuevo;
|FPRC;

|PROCESO LeeVariacion;
     |DDEFECTO #nomvarca;
     |ABRE #nomvarca;
     #nomvarca#0 = #labtraba#0;
     #nomvarca#1 = #labtraba#1;
     #nomvarca#2 = nVarMes;
     #nomvarca#3 = 0;
     |LEE 000#nomvarca.=;
     |SI FSalida = 0;
          prom_enf = #labtraba#95;
          prom_acc = #labtraba#96;
          |SI #nomvarca#16 ! "          ";|Y #nomvarca#16 ! "..........";
               #labtraba#37 = #nomvarca#16;
          |FINSI;
          |SI #nomvarca#17 ! 0;  prom_enf = #nomvarca#17;  |FINSI;
          |SI #nomvarca#18 ! 0;  prom_acc = #nomvarca#18;  |FINSI;
     |FINSI;
     |CIERRA #nomvarca;
|FPRC;

|PROCESO UltimoDia;
     nNume1 = #semcarse#0 $ 4;
     |SI nNume1 = 0;
          nNume1 = 1;
     |SINO;
          nNume1 = 0;
     |FINSI;
     |SI Mes1 = 1;  nTopeMes = 31;          |FINSI;
     |SI Mes1 = 2;  nTopeMes = 28 + nNume1; |FINSI;
     |SI Mes1 = 3;  nTopeMes = 31; |FINSI;
     |SI Mes1 = 4;  nTopeMes = 30; |FINSI;
     |SI Mes1 = 5;  nTopeMes = 31; |FINSI;
     |SI Mes1 = 6;  nTopeMes = 30; |FINSI;
     |SI Mes1 = 7;  nTopeMes = 31; |FINSI;
     |SI Mes1 = 8;  nTopeMes = 31; |FINSI;
     |SI Mes1 = 9;  nTopeMes = 30; |FINSI;
     |SI Mes1 = 10;  nTopeMes = 31; |FINSI;
     |SI Mes1 = 11;  nTopeMes = 30; |FINSI;
     |SI Mes1 = 12;  nTopeMes = 31; |FINSI;
|FPRC;

|PROCESO Borra_semtrcon_1;
     |BORRA 020#semtrcon.a;
     |LIBERA #semtrcon;
|FPRC;

|DEFBUCLE Borra_semtrcon;
     #semtrcon#1;
     ;
     #semcarse#0, #semcarse#1, #labtraba#0, #labtraba#1,   0;
     #semcarse#0, #semcarse#1, #labtraba#0, #labtraba#1, 999;
     be;
     NULL, Borra_semtrcon_1;
|FIN;

|PROCESO Borra_semtraba_1;
     |BORRA 020#semtraba.a;
     |LIBERA #semtraba;
|FPRC;

|DEFBUCLE Borra_semtraba;
     #semtraba#1;
     ;
     #semcarse#0, #semcarse#1, #labtraba#0, #labtraba#1;
     #semcarse#0, #semcarse#1, #labtraba#0, #labtraba#1;
     be;
     NULL, Borra_semtraba_1;
|FIN;

|PROCESO BorraLoQueHay;
    |CIERRA #semtraba;
    |CIERRA #semtrcon;
    |BUCLE Borra_semtraba;
    |BUCLE Borra_semtrcon;
    |ABRE #semtraba;
    |ABRE #semtrcon;
|FPRC;

|PROCESO Trabajadores;
     |SI #semcarse#7 = "S";
          |HAZ BorraLoQueHay;
     |FINSI;

     #semequiv#1 = #labtraba#0;
     #semequiv#2 = #labtraba#1;
     |LEE 000#semequiv.=;
     |SI FSalida ! 0;         || sin equivalencia
          |ACABA;
     |FINSI;

     nSoloViernes = 0;
     |SI #semequiv#4 = "M";
          |SI nViernes = 1;
               nSoloViernes = 1;   || mensual en el ultimo viernes
          |SINO;
               nSoloViernes = 2;   || mensual en resto de semanas
          |FINSI;
     |FINSI;

     |SI #labtraba#36 ! "          ";|Y #labtraba#36 ! "..........";
          fFech1 = #labtraba#36;
          |SI fFech1 > #semcarse#3;
               |ACABA;                  || alta posterior
          |FINSI;
     |FINSI;

     #nomtrcal#0  = #labtraba#156;
     #nomtrcal#13 = #0#0;
     |LEE 000#nomtrcal.=;
     |SI FSalida = 0;
          Dia = #0#2 % 0102;  Dia1 = Dia;
          Mes = #0#2 % 0402;  Mes1 = Mes;

          Dia = #0#3 % 0102;  Dia2 = Dia;
          Mes = #0#3 % 0402;  Mes2 = Mes;

          |DDEFECTO #nomvarca;
          |ABRE #nomvarca;
          #nomvarca#0 = #labtraba#0;
          #nomvarca#1 = #labtraba#1;
          #nomvarca#2 = Mes1;
          #nomvarca#3 = 0;
          |LEE 000#nomvarca.=;
          |SI FSalida = 0;
               |SI #nomvarca#16 ! "          ";|Y #nomvarca#16 ! "..........";
                    #labtraba#37 = #nomvarca#16;
               |FINSI;
          |FINSI;
          #nomvarca#2 = Mes2;
          #nomvarca#3 = 0;
          |LEE 000#nomvarca.=;
          |SI FSalida = 0;
               |SI #nomvarca#16 ! "          ";|Y #nomvarca#16 ! "..........";
                    #labtraba#37 = #nomvarca#16;
               |FINSI;
          |FINSI;
          |CIERRA #nomvarca;
          |SI #labtraba#37 ! "          ";|Y #labtraba#37 ! "..........";
               fFech1 = #labtraba#37;
               |SI fFech1 < #semcarse#2;
                    |HAZ BorraLoQueHay;
                    |ACABA;                  || baja anterior
               |FINSI;
          |FINSI;

          |HAZ UltimoDia;

          DiasLabs = 0;
          DiasNats = 0;
          denfe_30 = 0;
          denfe_21 = 0;
          denfe_22 = 0;
          denfe_23 = 0;
          denfe_24 = 0;
          topemes1 = 0;
          topemes2 = 0;
          dacci_2 = 0;
          dmate_2 = 0;
          base_1 = 0;
          base_2 = 0;
          base_3 = 0;
          denfe_4 = 0;
          denfe_B = 0;
          denfe_C = 0;
          denfe_D = 0;

          |DDEFECTO #semtraba;

          |SI Mes1 ! Mes2;
               Comodin = #nomtrcal#Mes1#;
               nVarMes = Mes1;     |HAZ LeeVariacion;
               |PARA Cont = Dia1; |SI Cont [ nTopeMes; |HACIENDO Cont = Cont + 1;
                    Calc = (Cont * 100) + 1;
                    Comodin1 = Comodin % Calc;
                    |HAZ CuentaDias;
                    |SI Comodin1 = "0"; DiasLabs = DiasLabs + nMas;   |FINSI;
                    DiasNats = DiasNats + nMas;
               |SIGUE;
               topemes1 = Dia1;    topemes2 = nTopeMes; |HAZ AcumPrestaciones;

               #semtraba#8 = DiasNats;

               Comodin = #nomtrcal#Mes2#;
               nVarMes = Mes2;     |HAZ LeeVariacion;
               |PARA Cont = 1; |SI Cont [ Dia2; |HACIENDO Cont = Cont + 1;
                    Calc = (Cont * 100) + 1;
                    Comodin1 = Comodin % Calc;
                    |HAZ CuentaDias;
                    |SI Comodin1 = "0"; DiasLabs = DiasLabs + nMas;   |FINSI;
                    DiasNats = DiasNats + nMas;
               |SIGUE;
               topemes1 = 1;       topemes2 = Dia2;    |HAZ AcumPrestaciones;

               #semtraba#9 = DiasNats - #semtraba#8;

          |SINO;
               Comodin = #nomtrcal#Mes1#;
               nVarMes = Mes1;     |HAZ LeeVariacion;
               |PARA Cont = Dia1; |SI Cont [ Dia2; |HACIENDO Cont = Cont + 1;
                    Calc = (Cont * 100) + 1;
                    Comodin1 = Comodin % Calc;
                    |HAZ CuentaDias;
                    |SI Comodin1 = "0"; DiasLabs = DiasLabs + nMas;   |FINSI;
                    DiasNats = DiasNats + nMas;
               |SIGUE;
               topemes1 = Dia1;    topemes2 = Dia2;    |HAZ AcumPrestaciones;

               #semtraba#8 = DiasNats;
               #semtraba#9 = DiasLabs;

          |FINSI;
          |SI nSoloViernes = 0;
               |HAZ Prestaciones;  || calcula prestaciones
               |BUCLE Conceptos;   || conceptos por dias/horas
          |FINSI;
          |BUCLE Importes;         || conceptos predeterminados (971,972,973)

          #semtraba#0 = #labtraba#0;
          #semtraba#1 = #labtraba#1;
          #semtraba#3 = #semcarse#0;
          #semtraba#4 = #semcarse#1;
          |LEE 101#semtraba.=;
          |SI FSalida = 0;
               |SI #semcarse#7 = "S";
                    |BORRA 020#semtraba.a;
                    |LIBERA #semtraba;
               |SINO;
                    |LIBERA #semtraba;
                    |ACABA;
               |FINSI;
          |FINSI;
          #semtraba#0 = #labtraba#0;
          #semtraba#1 = #labtraba#1;
          #semtraba#2 = #labtraba#2;
          #semtraba#3 = #semcarse#0;
          #semtraba#4 = #semcarse#1;
          #semtraba#5 = #semcarse#4;
          #semtraba#6 = #semcarse#2;
          #semtraba#7 = #semcarse#3;
          |GRABA 020#semtraba.n;
          nCuantos = nCuantos + 1;
     |SINO;
          aAlfa1 = "0000Calendario [" + #nomtrcal#0 + "." + #nomtrcal#13 + "] inaccesible.";
          |MENAV aAlfa1;
     |FINSI;
|FPRC;

|DEFBUCLE Trabajadores;
     #labtraba#1;
     44;
     #semempre#0, #semcarse#10, "A";
     #semempre#0, #semcarse#11, "A";
     ;
     NULL, Trabajadores;
|FIN;

|PROCESO Empresas;
     |BUCLE Trabajadores;
|FPRC;

|DEFBUCLE Empresas;
     #semempre#1;
     2;
     #semcarse#8, "S";
     #semcarse#9, "S";
     ;
     NULL, Empresas;
|FIN;

|PROCESO FijasTotal; |TIPO 0;
     aAlfa1 = #semcarse#2;    aAlfa1 = aAlfa1 % 402;
     aAlfa2 = #semcarse#3;    aAlfa2 = aAlfa2 % 402;
     |SI #semcarse#6 = 0;|Y aAlfa1 ! aAlfa2;
          |C_M #semcarse#12, 1, "S";
          |C_M #semcarse#13, 1, "S";
     |SINO;
          #semcarse#12 = 0;   |PINTA #semcarse#12;
          #semcarse#13 = 0;   |PINTA #semcarse#13;
          |C_M #semcarse#12, 1, "N";
          |C_M #semcarse#13, 1, "N";
     |FINSI;
|FPRC;

|PROCESO Defecto; |TIPO 7;    || calcula el a¤o por defecto segun sistema
     fFech1 = ~;
     aAlfa1 = fFech1;
     aAlfa1 = aAlfa1 % -104;
     #0Campo = aAlfa1;
     |PINTA #0Campo;
|FPRC;

|PROCESO mirasem; |TIPO 7;
    alfa1 = #0#0;
    alfa1 = "01.01." + alfa1;
    fech1 = alfa1;            || primero saca el dia de la semana
    fech2 = fech1 % 1;        ||/que fue el uno de enero
    alfa1 = fech2 % 11;

|SI alfa1 = "Lunes";     sdia=1; smas=1; |FINSI;
|SI alfa1 = "Martes";    sdia=7; smas=1; |FINSI;
|SI alfa1 = "Miercoles"; sdia=6; smas=1; |FINSI;
|SI alfa1 = "Jueves";    sdia=5; smas=1; |FINSI; ||saca el dia del mes que fue
|SI alfa1 = "Viernes";   sdia=4; smas=1; |FINSI; ||el primer lunes del a¤o
|SI alfa1 = "Sabado";    sdia=3; smas=1; |FINSI;
|SI alfa1 = "Domingo";   sdia=2; smas=1; |FINSI;

    alfa1 = ("00" + sdia) % -102;  || construye el primer lunes del a¤o
    fech1 = ~;                     ||/como fecha inicial de calculo
    alfa2 = fech1 % -104;
    alfa1 = alfa1 + ".01." + alfa2;
    fech1 = alfa1;
    nume1 = fech1;                 || calcula dias hasta primer lunes a¤o

    fech1 = ~;                     || calcula dias hasta fecha actual
    nume2 = fech1;

    sdife = nume2 - nume1;         || calcula la diferencia desde el primer
    |SI sdife ] 0;
        srest = sdife $ 7;           || lunes hasta la fecha actual,la redondea,
        sdife = sdife + (7 - srest); || la divide por 7 y le suma 1 por la
        #0Campo = (sdife / 7) + smas;|| primera semana (en su caso !!!)
    |SINO;
        #0Campo = 1;
    |FINSI;

    |ABRE #semcntcu;
    |LEE 000#semcntcu.p;
    |CIERRA #semcntcu;

    #0Campo = #0Campo - #semcntcu#3;
    |SI #0Campo = 0; #0Campo = 52; |FINSI;
    |PINTA #0Campo;
|HAZ deshas;
|FPRC;

|PROCESO deshas; |TIPO 7;   || calcula segun semana, desde dia hasta dia
    |ABRE #semcntcu;
    |LEE 000#semcntcu.p;
    |CIERRA #semcntcu;

    nume2 = (#0#1 + #semcntcu#3) - smas;
    nume2 = (nume2 * 7) + (sdia - 1);   || dias hasta
    nume1 = nume2 - 6;                  || dias desde

    nume3 = #0#0;
    nume3 = nume3 - 1;
    alfa1 = "31.12." + nume3;    || saca los dias que hay hasta el 31.12
    fech1 = alfa1;
    nume3 = fech1;

    nume1 = nume1 + nume3;
    fech1 = nume1;         || fecha desde
    #0#2 = fech1;

    nume2 = nume2 + nume3;
    fech1 = nume2;         || fecha hasta
    #0#3 = fech1;

|PINTA #0#2;
|PINTA #0#3;
|FPRC;

|PROCESO Avisa; |TIPO 0;
     |SI #semcarse#7 = "S";
          |AVISO;
          |AVISO;
          |CONFI "0000NLa informacion seleccionada ya existente se perdera. Continuar: ";
          |SI FSalida ! 0;
               #semcarse#7 = "N";
               |PINTA #semcarse#7;
               |ERROR;
          |FINSI;
     |FINSI;
|FPRC;

|RUTINA inf2;
   #semtrcon#0 = #semtraba#0;
   #semtrcon#1 = #semtraba#1;
   #semtrcon#2 = 001;
   #semtrcon#5 = #semcarse#0;
   #semtrcon#6 = #semcarse#1;
|FRUT;

|RUTINA sup2;
     #semtrcon#0 = #semtraba#0;
     #semtrcon#1 = #semtraba#1;
     #semtrcon#2 = 999;
     #semtrcon#5 = #semcarse#0;
     #semtrcon#6 = #semcarse#1;
|FRUT;

|PROCESO EntraLinCon; |TIPO 11;
     |SI FSalida = 9;
          |ENTLINEAL #1#semtrcon,5,3,21,1,inf2,sup2;
          |HAZ Presenta;
     |FINSI;
|FPRC;

|RUTINA inf1;
     #semtraba#3 = #semcarse#0;         || a¤o
     #semtraba#4 = #semcarse#1;         || mes
     #semtraba#0 = #semcarse#8;         || empresa
     #semtraba#1 = #semcarse#10;        || trabajador
|FRUT;

|RUTINA sup1;
     #semtraba#3 = #semcarse#0;         || a¤o
     #semtraba#4 = #semcarse#1;         || mes
     #semtraba#0 = #semcarse#9;         || empresa
     #semtraba#1 = #semcarse#11;        || trabajador
     FSalida = 12;
|FRUT;

|PROCESO UltimoViernes;
     nViernes = 0;
     |PARA fech1 = #semcarse#2; |SI fech1 [ #semcarse#3; |HACIENDO fech1 = fech1 + 1;
          fech2 = fech1 % 1;
          alfa1 = fech2 % 11;
          |QBF alfa1;
          |SI alfa1 = "Viernes";
               alfa1 = fech1 % 402;
               nume1 = fech1;
               nume1 = nume1 + 7;
               fech2 = nume1;
               alfa2 = fech2 % 402;
               |SI alfa2 ! alfa1;
                    nViernes = 1;
               |FINSI;
          |FINSI;
     |SIGUE;

     |SI nViernes = 1;
          ||AVISO;
          ||CONFI "0000SSemana con el ultimo viernes del mes. Incluir tipo S: ";
          ||SI FSalida ! 0;
          ||     nViernes = 0;
          ||FINSI;
          |ATRI P;
          |PINTA 1530, ">ULTIMO VIERNES<";
          |ATRI 0;
     |FINSI;
|FPRC;

/*
|PROGRAMA;
     |CLS;
     |CABEZA "Carga semanal";

     |PINPA #0#0;
     |PINDA #0#0;
     |ENDATOS #1#0;
*/

|PROCESO Tipo3; |TIPO 3;
     |SI FSalida < 0; |ACABA; |FINSI;

     |HAZ UltimoViernes;      || comprueba si el viernes es el ultimo del mes

     |ABRE #semtraba;
     |ABRE #semtrcon;
     |ABRE #nomtrcal;
     |ABRE #semequiv;
     |SELECT #2#semequiv;

     |BUCLE Empresas;
     |SI nCuantos ! 0;
          aAlfa1 = "0000Cargados [" + nCuantos + "] trabajadores.";
          |MENAV aAlfa1;
     |FINSI;
     |ENTLINEAL #1#semtraba, 4, 3, 22, 1, inf1, sup1;

     |CIERRA #semequiv;
     |CIERRA #semtraba;
     |CIERRA #semtrcon;
     |CIERRA #nomtrcal;
|FPRC;
