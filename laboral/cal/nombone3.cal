|FICHEROS;
     nombone3 #0;

     nomtmp07 #10,MANTE;
     labempre;
     labtraba;
     nomhicca;
     nombotra;
|FIN;

|VARIABLES;
   aAlfa  = "";
   aAlfa1 = "";
   aAlfa2 = "";
   aAlfa3 = "";
   nCambiados = 0;
   nCalc  = 0;
   nOpcion = 0;
   nNumTrab = 0;

{-1}Menu  = "";
    Menu1 = "2400";
    Menu2 = "1";
    Menu3 = "";
    Menu4 = "RAC";
    Menu5 = " Revisar ";
    Menu6 = " Actualizar ";
    Menu7 = " Cancelar ";

     &nMes = 0;
     &nAny = 0;
     swSal = 0;
     nNume = 0;
     nDesdeMes = 0;
     nDesdeAny = 0;
     nHastaMes = 0;
     nHastaAny = 0;
     nCuota = 0;
     &nTopemes = 0;
     nBonif = 0;
     nAplicado = 0;
     swSigue = 0;
     aFechaIniBon = "";
     aFechaAlta = "";
     fFechaIniBon = @;
     fFechaFinBon = @;
     fFechaAlta = @;
     aFechaIni = "";
     fFechaIni = @;
     nDia = 0;
     nDiasBon = 0;
     nDiferencia = 0;
     fPeriodo = @;
     swNoEntero = 0;
     fFechaBaja = @;
     fFechaFin = @;
     aFechaFin = "";
|FIN;

|PROCESO Imprime;
     |PRINT;
|FPRC;

|DEFBUCLE nomtmp07;
     #nomtmp07#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, Imprime;
|FIN;

|PROCESO GrabaLinea;
     |DDEFECTO #nomtmp07;
     #nomtmp07#0 = #nomhicca#0;
     #nomtmp07#1 = #nomhicca#1;
     #nomtmp07#2 = #nomhicca#66 + 2000;
     #nomtmp07#3 = #nomhicca#2;


     #nomtmp07#4 = #labempre#2;
     #nomtmp07#5 = #labtraba#2;

     #nomtmp07#6 = nDiasBon;                      || dias
     #nomtmp07#7 = nBonif;
     #nomtmp07#8 = nAplicado;

     #nomtmp07#9 = #labtraba#7;                        || NIF
     #nomtmp07#10 = #labtraba#75;                      || NAFSS

     |GRABA 020#nomtmp07.n;
|FPRC;

|PROCESO Trabajadores;
     |SI #nomhicca#66 = nDesdeAny; |Y #nomhicca#2 < nDesdeMes;
          |ACABA;
     |FINSI;
     |SI #nomhicca#66 = nHastaAny; |Y #nomhicca#2 > nHastaMes;
          |ACABA;
     |FINSI;

     #labempre#0 = #nomhicca#0;
     |LEE 000#labempre.=;

     #labtraba#0 = #nomhicca#0;
     #labtraba#1 = #nomhicca#1;
     |LEE 000#labtraba.=;

     swSigue = 0;
     #nombotra#0 = #nomhicca#0;
     #nombotra#1 = #nomhicca#1;
     #nombotra#2 = 111;
     |LEE 000#nombotra.=;
     |SI FSalida = 0;
          swSigue = 1;
     |FINSI;


     |SI swSigue = 1;
          swSigue = 0;

          fFechaIniBon = #nombotra#4;
          aAlfa = fFechaIniBon;
          aAlfa = "01."+ (aAlfa % -107);
          fFechaIniBon = aAlfa;
          fFechaFinBon = #nombotra#5;
          nMes = #nomhicca#2;
          nAny = #nomhicca#66 + 2000;
          aAlfa1 = "01";
          aAlfa2 = nMes;
          aAlfa2 = ("00" + aAlfa2) % -102;
          aAlfa3 = nAny;
          aAlfa = aAlfa1 + "." + aAlfa2 + "." + aAlfa3;
          fPeriodo = aAlfa;

          |SI fFechaIniBon [ fPeriodo;
          ||SI #nomhicca#177 = 111; |O #nomhicca#178 = 111;
               |SI #labtraba#129 = "G"; |O #labtraba#129 = "R";
                    |SI #labtraba#42 = "T"; |O #labtraba#42 = "X";
                         swSigue = 1;
                    |FINSI;
               |FINSI;
          |FINSI;
     |FINSI;
     |SI swSigue = 1;
          aFechaIniBon = #nombotra#4;
          aFechaAlta = #nomhicca#4;
          fFechaIniBon = aFechaIniBon;
          fFechaAlta = aFechaAlta;
          |SI fFechaIniBon < fFechaAlta;
               fFechaIni = fFechaAlta;
          |SINO;
               fFechaIni = fFechaIniBon;
          |FINSI;
          aFechaIni = fFechaIni;

          aAlfa = aFechaIni % 102; nDia = aAlfa;
          aAlfa = aFechaIni % 402; nMes = aAlfa;
          aAlfa = aFechaIni % -104; nAny = aAlfa;
          nAny = nAny - 2000;

          swNoEntero = 0;
          |SI nMes = #nomhicca#2; |Y nAny = #nomhicca#66; |Y nDia > 1;
               swNoEntero = 1;
          |FINSI;

          |SI swNoEntero = 0;
               fFechaFinBon = #nombotra#5;
               fFechaBaja = #labtraba#37;
               aAlfa = fFechaBaja; |QBF aAlfa;
               |SI aAlfa ! "";
                    |SI fFechaBaja [ fFechaFinBon;
                         fFechaFin = fFechaBaja;
                    |SINO;
                         fFechaFin = fFechaFinBon;
                    |FINSI;
               |SINO;
                    fFechaFin = fFechaFinBon;
               |FINSI;
               aFechaFin = fFechaFin;
               aAlfa = aFechaFin % 102; nDia = aAlfa;
               aAlfa = aFechaFin % 402; nMes = aAlfa;
               aAlfa = aFechaFin % -104; nAny = aAlfa;
               nAny = nAny - 2000;

               |SI nMes = #nomhicca#2; |Y nAny = #nomhicca#66;
                    nAny = nAny + 2000;
                    |HAZ topemes_plb;
                    nAny = nAny - 2000;
                    |SI nDia < nTopemes;
                         swNoEntero = 2;
                    |FINSI;
               |FINSI;
          |FINSI;

          |SI swNoEntero ! 0;
               || la bonificacion no se aplica el mes entero
               nAny = nAny + 2000;
               |HAZ topemes_plb;
               nAny = nAny - 2000;
               |SI swNoEntero = 1;
                    nDiasBon = nTopemes - nDia + 1;
               |SINO;
                    nDiasBon = nDia;
               |FINSI;
               nBonif = ((100 / 30) ! 2) * nDiasBon;
          |SINO;
               || la bonificacion se aplica el mes entero: tendrian
               || que haber sido 100 euros
               nDiasBon = 30;
               nBonif = 100;
          |FINSI;

          nAplicado = 0;
          nDiferencia = 0;

          |SI #nomhicca#177 = 111;
               nAplicado = #nomhicca#138
          |FINSI;
          |SI #nomhicca#178 = 111;
               nAplicado = #nomhicca#175
          |FINSI;
          |SI nAplicado = 0;
               nAplicado = #nomhicca#138;
          |FINSI;
          nDiferencia = nBonif - nAplicado;
          |SI nDiferencia > 0;
               |HAZ GrabaLinea;
          |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE Trabajadores;
     #nomhicca#1;
     ;
     #0#0, #0#2, nDesdeAny, 01, 0;
     #0#1, #0#3, nHastaAny, 12, 0;
     ;
     NULL, Trabajadores;
|FIN;

|PROGRAMA;
     |CLS;
     |CABEZA " ";

     |PINPA #0#0; |PINDA #0#0;
     |ENDATOS #1#0;
     |SI FSalida = 0;
          aAlfa = "04" + Usuario; |NOME_DAT #nomtmp07#aAlfa#;
          |DELINDEX #nomtmp07;
          |ABRE #nomtmp07;
          |ABRE #nomhicca;
          |ABRE #labempre;
          |ABRE #labtraba;
          |ABRE #nombotra;

          nNumTrab = 0;

          nDesdeMes = #0#5;
          nDesdeAny = #0#6 - 2000;
          nHastaMes = #0#7;
          nHastaAny = #0#8 - 2000;
          |BUCLE Trabajadores;

          |CIERRA #nombotra;
          |CIERRA #labtraba;
          |CIERRA #labempre;
          |CIERRA #nomhicca;

          Impresora = "CrystalReport";
          |IMPRE -1;
          |INFOR "nombone3";
          |BUCLE nomtmp07;
          |PIEINF;
          |FININF;
          |FINIMP;

          |CIERRA #nomtmp07;
          |DELINDEX #nomtmp07;
     |FINSI;
|FPRO;
