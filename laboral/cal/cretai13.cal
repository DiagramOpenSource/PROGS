|FICHEROS;
     cretai13 #0;

     cretam3e;
     silcon22;
     silcon23;
     cretat3e;      || TMP informe
|FIN;

|VARIABLES;
     &nMes = 0;
     &nAny = 0;
     &nTopemes = 0;
     fDesdeFecha = @;
     fHastaFecha = @;

     aValor = "";
     fValor = @;
     nValor = 0;
     nLinea = 0;

     aAlfa = "";
     aAlfa1 = "";
     aAlfa2 = "";
     aAlfa3 = "";
     nCampo = 0;
     aHojaExcel = "";
     aBase = "";
     aOrigen = "";
     aDestino = "";
     aDestinoTrab = "";
     aIPRemoto = "";
     aResultado = "";
     aCopia = "";
     aPath = "";
     {1}aVersion = "";

     aLongitud = "";
     nLongitud = 0;
     aCaracter = "";
     nCaracter = 0;
     nPos = 0;
     aCol = "";

     nAntEmp = 0;

     swCrystal = 0;
     swExcel = 0;
     nImpresion = 0;
|FIN;

|PROCESO Suma2000;
     |SI #0Campo [ 2000;
          #0Campo = #0Campo + 2000;
          |PINTA #0Campo;
     |FINSI;
|FPRC;

|PROCESO Impresion;
     |SI #0#9 ! 00; |Y #0#9 ! 01; |Y #0#9 ! 50;
          |MENAV "    Tipo de impresión no válido";
          |ERROR;
     |FINSI;
|FPRC;

|PROCESO ElIMPRE;
     Impresora = "CrystalReport";
     |IMPRE -1;
     |SI FSalida = 0;
          |SI #0#9 = "00";
               |INFOR "cretai13";
          |FINSI;
          |SI #0#9 = "01";
               |INFOR "cretaj13";
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO ElFINIMP;
     |PIEINF;
     |FININF;
     |FINIMP;
|FPRC;

|PROCESO impresion;
     |SI nImpresion = 0;
          |HAZ ElIMPRE;
     |FINSI;

     ||HAZ Tildes;

     |PRINT;
     nImpresion = nImpresion + 1;
|FPRC;

|DEFBUCLE cretat3e;
     #cretat3e#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, impresion;
|FIN;

|PROCESO BuscaSiTabla;
     || tabla 96 - Causa fin/extincion pago prestaciones

     |SI #silcon23#6 = 96;         || codigos transformados segun
                                   || tipo de IT
          aAlfa = aValor;
          |QBF aAlfa;
          aAlfa = ("00" + aAlfa)  % -102;

          |SI #cretam3e#66 = "RE"; |O #cretam3e#66 = "RL";
               aAlfa = "1" + aAlfa;
          |FINSI;
          |SI #cretam3e#66 = "CM";
               aAlfa = "2" + aAlfa;
          |FINSI;
          |SI #cretam3e#66 = "NC";
               aAlfa = "3" + aAlfa;
          |FINSI;
     |SINO;
          aAlfa = (aValor + "    ") % 104;
     |FINSI;

     #silcon22#0 = #silcon23#6;
     #silcon22#1 = aAlfa;
     |LEE 000#silcon22.=;
     |SI FSalida = 0;
          aAlfa = #silcon22#2;
          |QBF aAlfa;
          aValor = aValor + " - " + aAlfa;
     |FINSI;
|FPRC;

|PROCESO SinTabla;
     |SI nCampo = 0; aCol = "A"; |FINSI;
     |SI nCampo = 1; aCol = "C"; |FINSI;
     |SI aCol ! "";
          nValor = #cretam3e#nCampo#;
          aValor = nValor;
          |QBF aValor;
          aValor = ("00000" + aValor) % -105;
     |FINSI;
|FPRC;

|PROCESO cretam3e;
     |SI #0#9 = 00;
          |SI #cretam3e#74 ! "01.01.0000";   || nacimientos, no van al informe
               |ACABA;                       || en crystal resumido
          |FINSI;

          |SI #cretam3e#21 = "0";            || no hay contingencia
          |Y #cretam3e#22 = "N";             || no hay carencia
          |Y #cretam3e#53 = "  ";            || no es otra IP
          |Y #cretam3e#64 = " ";             || no es jubilacion
          |Y #cretam3e#66 = "  ";            || no es otra prestacion
          |Y #cretam3e#74 = "01.01.0000";    || no es nacimiento
               |ACABA;   || sin datos que sacar
          |FINSI;
     |FINSI;

     nLinea = nLinea + 1;
     |SI nAntEmp ! #cretam3e#0; |Y nAntEmp ! 0;
          |SI swExcel = 1;
               aValor = "  ";
               aAlfa = aCol + nLinea + "," + aValor;
               |ALFACALLDLL "agixl97.dll","poke_dato", aAlfa;
          |FINSI;
          nLinea = nLinea + 1;
     |FINSI;
     |PARA nCampo = 0; |SI nCampo [ 74; |HACIENDO nCampo = nCampo + 1;
          aValor = "";
          aCol = "";
          |HAZ SinTabla;

          #silcon23#0 = "FIE";
          #silcon23#8 = nCampo;
          |LEE 000#silcon23.=;
          |SI FSalida = 0;
               |SI #silcon23#3 = "A"; |O #silcon23#3 = "H";
                    aValor = #cretam3e#nCampo#;
               |FINSI;
               |SI #silcon23#3 = "F";
                    fValor = #cretam3e#nCampo#;
                    |SI fValor ! "01.01.0000";
                         aValor = fValor;
                    |FINSI;
               |FINSI;
               |SI #silcon23#3 = "N";
                    nValor = #cretam3e#nCampo#;
                    |SI nValor ! 0;
                         aValor = nValor;
                    |FINSI;
               |FINSI;

               aCol = #silcon23#10; |QBF aCol;
          |FINSI;

          |QBF aValor;

          |SI #silcon23#6 ! 0;
               |HAZ BuscaSiTabla;
          |FINSI;

          |SI swCrystal = 1;
               |SI #silcon23#6 ! 0;
                    #cretat3e#nCampo# = aValor;
               |SINO;
                    #cretat3e#nCampo# = #cretam3e#nCampo#;
               |FINSI;
          |FINSI;

          |SI swExcel = 1;
               |SI aValor ! ""; |Y aCol ! "";
                    aAlfa = aCol + nLinea + "," + aValor;
                    |ALFACALLDLL "agixl97.dll","poke_dato", aAlfa;
               |FINSI;
          |FINSI;
     |SIGUE;

     nAntEmp = #cretam3e#0;

     |SI swCrystal = 1;
          |GRABA 020#cretat3e.n;
          |LIBERA #cretat3e;
     |FINSI;
|FPRC;

|DEFBUCLE cretam3e;
     #cretam3e#2;
     ;
     #0#0, #0#2, fDesdeFecha;
     #0#1, #0#3, fHastaFecha;
     ;
     NULL, cretam3e;
|FIN;

|PROCESO informe;
     aAlfa1 = "01";
     aAlfa2 = #0#4; |QBF aAlfa2; aAlfa2 = ("00" + aAlfa2) % -102;
     aAlfa3 = #0#5; |QBF aAlfa3;
     aAlfa = aAlfa1 + "." + aAlfa2 + "." + aAlfa3;
     fDesdeFecha = aAlfa;

     nMes = #0#6;
     nAny = #0#7;
     |HAZ topemes_plb;
     aAlfa1 = nTopemes; |QBF aAlfa1; aAlfa1 = ("00" + aAlfa1) % -102;

     aAlfa2 = #0#6; |QBF aAlfa2; aAlfa2 = ("00" + aAlfa2) % -102;
     aAlfa3 = #0#7; |QBF aAlfa3;
     aAlfa = aAlfa1 + "." + aAlfa2 + "." + aAlfa3;
     fHastaFecha = aAlfa;

     |ABRE #silcon22;
     |ABRE #silcon23;

     |SELECT #3#silcon23;

     nLinea = 2;

     |SI swExcel = 1;
          aAlfa1 = #0#4; |QBF aAlfa1; aAlfa1 = ("00" + aAlfa1) % -102;
          aAlfa2 = #0#5;
          aValor = "Del " + aAlfa1 + "/" + aAlfa2 + " al ";
          aAlfa1 = #0#6; |QBF aAlfa1; aAlfa1 = ("00" + aAlfa1) % -102;
          aAlfa2 = #0#7;
          aValor = aValor + aAlfa1 + "/" + aAlfa2;
          aAlfa =  "B1,"+ aValor;
          |ALFACALLDLL "agixl97.dll","poke_dato", aAlfa;
     |FINSI;

     nLinea = 3;
     |BUCLE cretam3e;

     |CIERRA #silcon23;
     |CIERRA #silcon22;
|FPRC;

|PROCESO InicioExcel;
     |DBASE aBase;  |QBF aBase;
     aHojaExcel = "informe_datos_fie.xls";
     aOrigen = aBase + "documentos/informe_datos_fie.xls";
     aDestinoTrab = "C:\DOCUMENTOS\LABORAL\";         |RMKDIR aDestino;
     aDestino = aDestinoTrab + aHojaExcel;

     aIPRemoto = "";
     |IP_REMOTO aIPRemoto;  |QBT aIPRemoto;
     |SI aIPRemoto ! "";
          |ENVIA_FICHERO aOrigen, aDestino;
     |SINO;
          |COPIA_FICHERO aOrigen, aDestino;
     |FINSI;

     aOrigen = aDestinoTrab + aHojaExcel;
     aDestino = aDestinoTrab + "trabajo.xls";
     |RCOPIA_FICHERO aOrigen, aDestino;

     |CARGADLL "agixl97.dll";
     |SI FSalida < 0;
          |MENAV "0000No se puede usar la DLL de Excel.";
          |ACABA;
     |FINSI;

     |ALFACALLDLL "agixl97.dll", "carga_book", aDestino;

     aAlfa = aDestinoTrab + aHojaExcel;
     |RFBORRA aAlfa;

     |ALFACALLDLL "agixl97.dll", "selecciona_hoja", "Hoja1";
|FPRC;

|PROCESO FinExcel;
     aAlfa = aDestinoTrab + aHojaExcel;
     |ALFACALLDLL "agixl97.dll", "fichero_destino", aAlfa;
     |ALFACALLDLL "agixl97.dll", "fin_book", "salva";

     aAlfa = aDestinoTrab + "trabajo.xls";
     |RFBORRA aAlfa;

     aResultado = aAlfa;

     |DESCARGADLL "agixl97.dll";

     aAlfa = aDestinoTrab + aHojaExcel;
     aAlfa = "cmd " + &34 + "/c START " + aAlfa + &34;
     |RSYSTEM aAlfa;
|FPRC;

|PROGRAMA;
     |CLS;

     |ABRE #0;
     |DDEFECTO #0;
     |LEE 000#0p;
     |SI FSalida = 0;
          |BORRA 020#0a;
     |FINSI;

     #0#2 = 00000;
     #0#3 = 99999;

     |PINPA #0#0;
     |PINDA #0#0;
     |ENDATOS #1#0;

     |SI #0#9 = 00; |O #0#9 = 01;
          swCrystal = 1;
     |FINSI;
     |SI #0#9 = 50; swExcel = 1; |FINSI;

     |SI FSalida = 0;
          |SI swCrystal = 1;
               aAlfa = Usuario; |QBT aAlfa; aAlfa = "tm_cretat3e_" + aAlfa;
               |NOME_DAT #cretat3e#aAlfa#;
               |DELINDEX #cretat3e;

               |ABRE #cretat3e;
          |FINSI;

          |SI swExcel = 1;
               |HAZ InicioExcel;
          |FINSI;

          |HAZ informe;

          |SI swExcel = 1;
               |HAZ FinExcel;
          |FINSI;

          |SI swCrystal = 1;
               |BUCLE cretat3e;
               |SI nImpresion ! 0;
                    |HAZ ElFINIMP;
               |SINO;
                    aAlfa = "    No se han encontrado datos de Liquidaciones para ";
                    aAlfa = aAlfa + "la selección realizada.";
                    |MENAV aAlfa;
               |FINSI;
               |CIERRA #cretat3e;
               |DELINDEX #cretat3e;
          |FINSI;
     |FINSI;

     |GRABA 000#0n;
     |CIERRA #0;
|FPRO;
