|FICHEROS;
     nomxacti #0;
     labempre #1;
     labtraba #2;
     labcentr;

     nomauxi1;
     nomauxi2;

     dsnetm00@ #100;    || parametros dsnetcom
     copiadef@ #500;
|FIN;

|VARIABLES;
    alfa1 = "";
    alfa2 = "";
    alfa3 = "";
    alfa4 = "";
    nume1 = 0;
    nume2 = 0;
    nume3 = 0;
    nume4 = 0;
    seleccio = 0;

    p_mes = "";
    fecsys = @;
    swfuera = 0;
    fecalf = "";
    dia = 0;
    mes = 0;
    any = 0;

     aDirDes = "";
     aRutaDes = "";
     aRuta = "";
     aRutaDatos = "";
     nCanal = 0;
     aArchivo = "";
     aCabecera = "";
     aTipoResumen = "";
     aTipoNomina = "";
     aEmpresa = "";
     aAnyoDesde = "";
     aAnyoHasta = "";
     aMesDesde = "";
     aMesHasta = "";
     aOrdenacion = "";
     aMes = ""
     aAnyo = "";
     aDireccion = "";
     aTrabajadorDesde = "";
     aTrabajadorHasta = "";
     nEmpresa = 0;
     nAnyo = 0;
     nMes = 0;
     nMesDesde = 0;
     nMesHasta = 0;
     nOrdenacion = 0;
     nDireccion = 0;
     nTrabajadorDesde = 0;
     nTrabajadorHasta = 0;

     cParam         = "";
     cParte         = "";
     &cOrigen        = "";
     &cDestino       = "";
     cDirBase       = "";
     cUsuario       = "";
     cIp            = "";
     cDirUsu        = "";
     cNumUsu        = "";
     aAlfa = "";
     aAlfa1 = "";
     aTabla = "";
     aDir = "";
     &nErrorInf = 0;
     aSystem = "";
     aBaseBin = "";
     aDirOri = "";

     &swDesdeWeb = 0;

     swActiva = 0;
     aMas = "";
     aFich = "";
     aFabre = "";

     nNume = 0;
     aCol = "";
     aValor = "";
     nLinea = 0;
     nAntEmp = 0;
     aBase = "";
     aHojaExcel = "";
     aOrigen = "";
     aDestino = "";
     aDestinoTrab = "";
     aIPRemoto = "";
     aResultado = "";
     aRaizMacro = "";
     aNombreMacro = "";
     swNegrita = 0;
     swBordes = 0;
     swGris = 0;
|FIN;
____________________________________/ CALCULOS DE PANTALLA
|PROCESO defecto; |TIPO 7; ||calcula el periodo por defecto segun fecha sistema
    fecsys = ~;
    alfa1 = fecsys;
    alfa1 = alfa1 % -104;
    #0Campo = alfa1;
|PINTA #0Campo;
|FPRC;

|PROCESO Formato;
     |SI #0Campo ! 00; |Y #0Campo ! 50;
          |ERROR;
     |FINSI;
|FPRC;

|PROCESO MesLestra;
     |SI #0#4 =  1;  p_mes = "ENERO     ";  |FINSI;
     |SI #0#4 =  2;  p_mes = "FEBRERO   ";  |FINSI;
     |SI #0#4 =  3;  p_mes = "MARZO     ";  |FINSI;
     |SI #0#4 =  4;  p_mes = "ABRIL     ";  |FINSI;
     |SI #0#4 =  5;  p_mes = "MAYO      ";  |FINSI;
     |SI #0#4 =  6;  p_mes = "JUNIO     ";  |FINSI;
     |SI #0#4 =  7;  p_mes = "JULIO     ";  |FINSI;
     |SI #0#4 =  8;  p_mes = "AGOSTO    ";  |FINSI;
     |SI #0#4 =  9;  p_mes = "SEPTIEMBRE";  |FINSI;
     |SI #0#4 = 10;  p_mes = "OCTUBRE   ";  |FINSI;
     |SI #0#4 = 11;  p_mes = "NOVIEMBRE ";  |FINSI;
     |SI #0#4 = 12;  p_mes = "DICIEMBRE ";  |FINSI;
|FPRC;

|PROCESO pintames; |TIPO 0;
     |HAZ MesLestra;
     |PINTA 1331, p_mes;
|FPRC;

|PROCESO mirames; |TIPO 7; ||calcula el mes por defecto segun fecha sistema
    fecsys = ~;
    alfa1 = fecsys;
    alfa1 = alfa1 % 402;
    #0Campo = alfa1;
|PINTA #0Campo;
|FPRC;

|PROCESO activasino;
     swActiva = 1;

     |ABRE #1;
     #1#0 = #2#0;
     |LEE 000#1=;
     |SI #1#50 = "S";
          swActiva = 0;  || empresa no activa
     |FINSI;
     |CIERRA #1;

     |SI swActiva = 0;
          |ACABA;
     |FINSI;

     aFabre = "";
     |DIRECTORIO aFabre;
     aFabre = "rt  " + aFabre + "integral/bin/dsam0000.tab";
     |FABRE aFabre;
     |SI FSalida ] 0;
          |FCIERRA FSalida;
          |DBASS aBase;  |QBF aBase;
          aMas  = aBase + "integral/def/dsam0000.mas";
          aFich = aBase + "integral/fich/";

          |CARGA_DEF aMas, copiadef@;
          |SI FSalida ! 0;
              aAlfa = "0000 No puedo cargar el Def " + aMas;
              |MENAV aAlfa;
              |ACABA;
          |FINSI;

          |ABRE #500;
          #500#0 = #2#0;
          |LEE 000#500=;
          |SI FSalida = 0;
               |SI #500#85 = "I";
                    swActiva = 0;  || empresa no activa
               |FINSI;
          |FINSI;
          |CIERRA #500;
          |DESCARGA_DEF #copiadef@;
     |FINSI;
|FPRC;

|PROCESO Gris;
     aRaizMacro = "Módulo2.";
     aNombreMacro = "CeldaGris";
     aAlfa = aCol + nLinea + "," + aRaizMacro + aNombreMacro;
     |ALFACALLDLL "agixl97.dll","ejecuta_celda", aAlfa;
|FPRC;

|PROCESO Negrita;
     aRaizMacro = "Módulo1.";
     aNombreMacro = "CeldaNegrita";
     aAlfa = aCol + nLinea + "," + aRaizMacro + aNombreMacro;
     |ALFACALLDLL "agixl97.dll","ejecuta_celda", aAlfa;
|FPRC;

|PROCESO Bordes;
     aRaizMacro = "Módulo1.";
     aNombreMacro = "CeldaBordes";
     aAlfa = aCol + nLinea + "," + aRaizMacro + aNombreMacro;
     |ALFACALLDLL "agixl97.dll","ejecuta_celda", aAlfa;
|FPRC;

|PROCESO PonValor;
     aAlfa = aCol + nLinea + "," + aValor;
     |ALFACALLDLL "agixl97.dll","poke_dato", aAlfa;
     |SI swNegrita = 1;
          |HAZ Negrita;
     |FINSI;
     |SI swGris = 1;
          |HAZ Gris;
     |FINSI;
     |SI swBordes = 1;
          |HAZ Bordes;
     |FINSI;
|FPRC;

|PROCESO LineaExcel;
     |SI nAntEmp ! #labtraba#0;
          swNegrita = 1;
          swBordes = 1;
          swGris = 1;

          || cabecera empresa
          nLinea = nLinea + 2;
          aAlfa1 = #labempre#0; |QBF aAlfa1; aAlfa1 = ("00000" + aAlfa1) % -105;
          aValor = aAlfa1 + " " + #labempre#2;
          aCol = "A";
          |HAZ PonValor;

          nLinea = nLinea + 2;
          aCol = "A"; aValor = "Trabajador"; |HAZ PonValor;
          aCol = "B"; aValor = "Nro. Afiliaci¢n"; |HAZ PonValor;
          aCol = "C"; aValor = "DNI"; |HAZ PonValor;
          aCol = "D"; aValor = "Sexo"; |HAZ PonValor;
          aCol = "E"; aValor = "Contrato"; |HAZ PonValor;
          aCol = "F"; aValor = "Jornada"; |HAZ PonValor;
          aCol = "G"; aValor = "Fecha alta"; |HAZ PonValor;
          aCol = "H"; aValor = "Fecha baja"; |HAZ PonValor;
          aCol = "I"; aValor = "Categor¡a"; |HAZ PonValor;
          aCol = "J"; aValor = "Provincia centro"; |HAZ PonValor;
          swGris = 0;
          swNegrita = 0;
          swBordes = 0;
     |FINSI;

     nLinea = nLinea + 1;
     aAlfa1 = #labtraba#1; |QBF aAlfa1; aAlfa1 = ("00000" + aAlfa1) % -105;
     aAlfa1 = aAlfa1 + " " + #labtraba#2;
     aCol = "A"; aValor = aAlfa1; |HAZ PonValor;
     aCol = "B"; aValor = #labtraba#75; |HAZ PonValor;
     aCol = "C"; aValor = #labtraba#7; |HAZ PonValor;
     |SI #labtraba#252 = "V"; #labtraba#252 = "H"; |FINSI;
     aCol = "D"; aValor = #labtraba#252; |HAZ PonValor;
     aCol = "E"; aValor = #labtraba#45; |HAZ PonValor;
     nNume = #labtraba#234;
     |SI #labtraba#42 = "X"; |O #labtraba#42 = "T";
     |O #labtraba#42 = "P"; |O #labtraba#42 = "I";
          aAlfa1 = nNume;
          |QBF aAlfa1;
          aAlfa1 = aAlfa1 + " h";
     |SINO;
          aAlfa1 = "";
     |FINSI;
     aCol = "F"; aValor = aAlfa1; |HAZ PonValor;
     aCol = "G"; aValor = #labtraba#36; |HAZ PonValor;
     aCol = "H"; aValor = #labtraba#37; |HAZ PonValor;
     aAlfa1 = #labtraba#17; |QBF aAlfa1; aAlfa1 = ("000" + aAlfa1) % -103;
     aAlfa1 = aAlfa1 + " " + #labtraba#18;
     aCol = "I"; aValor = aAlfa1; |HAZ PonValor;
     aAlfa1 = #labcentr#7; |QBF aAlfa1; aAlfa1 = ("00" + aAlfa1) % -102;
     aAlfa1 = aAlfa1 + " " + #labcentr#9;
     aCol = "J"; aValor = aAlfa1; |HAZ PonValor;

     nAntEmp = #labtraba#0;
|FPRC;

|PROCESO imprimir;
     seleccio = 1;
     |SI #0#12 = 00;
          |PRINT;
     |FINSI;
     |SI #0#12 = 50;
          |HAZ LineaExcel;
     |FINSI;
|FPRC;

|PROCESO desglosa;
     alfa1 = fecalf %  102;   dia = alfa1;
     alfa1 = fecalf %  402;   mes = alfa1;
     alfa1 = fecalf % -104;   any = alfa1;
     |SI alfa1 = "....";
          dia = 0;
          mes = 0;
          any = 0;
     |FINSI;
|FPRC;

|PROCESO chequea;
     |HAZ activasino;
     |SI swActiva = 0;
          |ACABA;
     |FINSI;
     swfuera = 0;

     fecalf = #2#36;                           || fecha de alta
     |HAZ desglosa;
     |SI any > #0#5;|O any = 0;
          swfuera = 1;
     |SINO;
          |SI mes > #0#4; |Y any = #0#5;
               swfuera = 1;
          |FINSI;
     |FINSI;

     |SI swfuera = 0;
          fecalf = #2#37;                       || fecha de baja
          |HAZ desglosa;
          |SI any < #0#5; |Y any ! 0;
               swfuera = 1;
          |SINO;
               |SI mes < #0#4; |Y any = #0#5; |Y mes ! 0;
                    swfuera = 1;
               |FINSI;
          |FINSI;
     |FINSI;

     |SI #0#7 = 3;
          |DDEFECTO #nomauxi1;
          |ABRE #nomauxi1;
          #nomauxi1#0 = #labtraba#62;
          |LEE 000#nomauxi1.=;
          |CIERRA #nomauxi1;

          |DDEFECTO #nomauxi2;
          |ABRE #nomauxi2;
          #nomauxi2#0 = #labtraba#62;
          #nomauxi2#1 = #labtraba#63;
          |LEE 000#nomauxi2.=;
          |CIERRA #nomauxi2;
     |SINO;
          #nomauxi1#0 = "";
          #nomauxi1#1 = "";
          #nomauxi2#1 = "";
          #nomauxi2#2 = "";
     |FINSI;

     #labcentr#0 = #labtraba#0;
     #labcentr#1 = #labtraba#77;
     |LEE 000#labcentr.=;

     |SI #0#10 [ #labtraba#77; |Y #labtraba#77 [ #0#11;
          || adelante
     |SINO;
          swfuera = 1;
     |FINSI;

     |SI swfuera = 0;    |HAZ imprimir; |FINSI;
|FPRC;

|DEFBUCLE lee_traba;
     #2#1;
     ;
     #0#0, #0#2;
     #0#1, #0#3;
     ;
     NULL, chequea;
|FIN;

|PROCESO informe;
     nLinea = 1;
     aAlfa = #0#5;
     |HAZ MesLestra;
     |QBF p_mes;
     aValor = p_mes + " - " + aAlfa;
     aCol = "B";
     aAlfa = aCol + nLinea + "," + aValor;
     |ALFACALLDLL "agixl97.dll","poke_dato", aAlfa;

     |ABRE #labcentr;
     |BUCLE lee_traba;
     |CIERRA #labcentr;
|FPRC;

|PROCESO Excel;
     |DBASE aBase;  |QBF aBase;
     aHojaExcel = "informe_trabajadores_activo.xls";
     aOrigen = aBase + "documentos/" + aHojaExcel;
     aDestinoTrab = "C:\DOCUMENTOS\LABORAL\";
     |RMKDIR aDestinoTrab;
     aDestino = aDestinoTrab + aHojaExcel;

     aIPRemoto = "";
     |IP_REMOTO aIPRemoto;  |QBT aIPRemoto;
     |SI aIPRemoto ! "";
          |ENVIA_FICHERO aOrigen, aDestino;
     |SINO;
          |COPIA_FICHERO aOrigen, aDestino;
     |FINSI;

     aOrigen = aDestinoTrab + aHojaExcel;
     aDestino = aDestinoTrab + "trabajo.xls";
     |RCOPIA_FICHERO aOrigen, aDestino;

     |CARGADLL "agixl97.dll";
     |SI FSalida < 0;
          |MENAV "0000No se puede usar la DLL de Excel.";
          |ACABA;
     |FINSI;

     |ALFACALLDLL "agixl97.dll", "carga_book", aDestino;

     aAlfa = aDestinoTrab + aHojaExcel;
     |RFBORRA aAlfa;

     |ALFACALLDLL "agixl97.dll", "selecciona_hoja", "Hoja1";

     |HAZ informe;

     aAlfa = aDestinoTrab + aHojaExcel;
     |ALFACALLDLL "agixl97.dll", "fichero_destino", aAlfa;
     |ALFACALLDLL "agixl97.dll", "fin_book", "salva";

     aAlfa = aDestinoTrab + "trabajo.xls";
     |RFBORRA aAlfa;

     aResultado = aAlfa;

     |DESCARGADLL "agixl97.dll";

     || aAlfa  = "cmd " + &34 + "/c START /WAIT " + aPath + " " + aAlfa + &34;
     || aAlfa  = "cmd " + &34 + "/c START /WAIT " + aResultado + &34;
     aAlfa = aDestinoTrab + aHojaExcel;
     aAlfa = "cmd " + &34 + "/c START " + aAlfa + &34;
     |RSYSTEM aAlfa;
|FPRC;

|PROCESO Normal;
     |SI swDesdeWeb = 0;
          |IMPRE 0;
     |SINO;
          |HAZ prepara_impresion;
     |FINSI;
     |SI FSalida = 0;
          seleccio = 0;
          |SI Impresora = "CrystalReport";
               |INFOR "nomxact2";
          |SINO;
               |INFOR "nomxacti";
          |FINSI;

          |ABRE #labcentr;
          |BUCLE lee_traba;
          |CIERRA #labcentr;

          |SI seleccio = 1;
               |PIEINF;
               |FININF;
          |SINO;
               |MENAV "    Seleccion vacia ...";
          |FINSI;
     |SINO;
          |MENAV "    Proceso Abortado ...";
     |FINSI;
     |FINIMP;
|FPRC;

|PROCESO impre; |TIPO 3;
     |SI #0#12 = 00;
          |HAZ Normal;
     |FINSI;
     |SI #0#12 = 50;
          |HAZ Excel;
     |FINSI;
|FPRC;

-------------------------------------------------------------------------------
                    ||   PROCESOS COMUNES

|PROCESO prepara_impresion;
     aDir = "/tmp/";
     Impresora = aDir + Usuario;
     |PINTA 2001, Usuario;
     |PINTA 2101, Impresora;
     |IMPRE -77;
     |PINTA 2201, FSalida;
     |SI FSalida ! 0;
          nErrorInf = 1;
     |FINSI;
     aAlfa1 = "chmod 777 " + Impresora;;
     |SYSTEM aAlfa1;
|FPRC;

|PROCESO traete_fichero;
     |FINIMP;
     cOrigen = aDir + Usuario;
     cDestino = aDirDes;
     |QBF cDestino;
     aArchivo = aArchivo - ".txt";
     cDestino = cDestino + aArchivo;
     |COPIA_FICHERO cOrigen, cDestino;
     |SI FSalida ! 0;
          nErrorInf = 0;
     |FINSI;
     aAlfa1 = "chmod 777 " + cDestino;
     |SYSTEM aAlfa1;
|FPRC;

|PROCESO ConviertePdf;
     |DBASS aBaseBin;  |QBF aBaseBin;
     aBaseBin = aBaseBin + "bin/topdf.sh ";

     aSystem = aBaseBin + cDestino;
     |SYSTEM aSystem;

     aSystem = "chmod 777 " + cDestino + ".pdf";
     |SYSTEM aSystem;

     |FBORRA cDestino;

     ||SI #0#6 = "S";  |HAZ AlaWeb;  |FINSI;
     ||SI #0#5 = "S";  |HAZ Correo;  |FINSI;

     ||FBORRA aPdf;
|FPRC;
-------------------------------------------------------------------------------
|PROCESO LeeParam;
     |DBASS aRutaDes;
     |DBASS aRutaDatos;
     |QBF aRutaDes;
     |QBF aRutaDatos;
     aRutaDes = aRutaDes + "dsnetcom/def/dsnetm00.mas";
     |CARGA_DEF aRutaDes, dsnetm00@;
     |SI FSalida ! 0;
          |MENAV "    Error en CARGADEF: dsnetm00";
     |SINO;
          aRutaDatos = aRutaDatos + "dsnetcom/fich/";
          |PATH_DAT #100 aRutaDatos;
          |ABRE #100;
          |LEE 000#100p;
          |SI FSalida = 0;
               aDirDes = #100#7;
               aDirOri = aDirDes;
          |SINO;
               |MENAV "    Error de datos en fichero de configuracion dsnetm00";
          |FINSI;
          |CIERRA #100;
     |FINSI;
     |DESCARGA_DEF #dsnetm00@;
|FPRC;

|PROCESO LeeOrigen;

     aArchivo = PARAMETRO;
     |QBF aDirOri;
     aRuta = aDirOri + aArchivo;

     |XABRE "U", aRuta, nCanal;
     |SI FSalida ] 0;
          |XLEE nCanal, 100, aCabecera;
          |XLEE nCanal,  10, aEmpresa;
          |XLEE nCanal,  10, aMes;
          |XLEE nCanal,  10, aAnyo;
     |FINSI;
     |XCIERRA nCanal;
     |QBF aCabecera;
     |QBF aEmpresa;
     |QBF aMes;
     |QBF aAnyo;
     nEmpresa = aEmpresa;
     nMes = aMes;
     nAnyo = aAnyo;
|FPRC;

|PROCESO PasaParametros;
     #0#0 = nEmpresa;
     #0#1 = nEmpresa;
     #0#2 = 1;           || desde trabajador
     #0#3 = 99999;       || hasta trabajador
     #0#4 = nMes;
     #0#5 = nAnyo;
     #0#6 = ~;
|FPRC;

|PROGRAMA;
     |CLS;
     |CABEZA "INFORME DE VENCIMIENTOS";
     |ABRE #0;
     |LEE 001#0p;
     |SI FSalida ! 0;
          |DDEFECTO #0;
          |GRABA 020#0n;
     |FINSI;
     |LEE 101#0=;
     |PINPA #0#0;
     |PINDA #0#0;
     |SI PARAMETRO ! "";
          |HAZ LeeParam;
          |HAZ LeeOrigen;
          |HAZ PasaParametros;
          swDesdeWeb = 1;
          |HAZ impre;
          |HAZ traete_fichero;
          |HAZ ConviertePdf;
     |SINO;
          |ENDATOS #1#0;
          |SI FSalida = 0;
               |GRABA 020#0a;
          |FINSI;
          |LIBERA #0;
          |CIERRA #0;
     |FINSI;
|FPRO;
