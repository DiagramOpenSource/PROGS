|FICHEROS;
     cretap22 #0;
     cretam34;
     cretam35;

     cretam18;

     cretam02;
     cretae02;

     cretat12;

     labempre;
     labcentr;
     nomcenes;
     labtraba;
|FIN;

|VARIABLES;
     &eaFichero = "";
     &aSoloFichero = "";

     nNume = 0;
     nNume1 = 0;
     nNume2 = 0;
     aLon = "";
     nLon = 0;
     nEspacio = 0;
     aEspacio = "";
     nBlancos = 0;
     nBlancosIzd = 0;
     nBlancosDer = 0;
     aBlancosIzd = "";
     aBlancosDer = "";

     &aCadena = "";
     &swQuitaTildes = 0;
     aCadenaSal = "";

     FLinea = 0;
     nNroLinea = 0;
     aNroLinea = "";
     aAlfa = "";

     nHandle = 0;
     aLinea = "";
     &aTotalLineas = "";
     swAcaba = 0;

     aCCC = "";
     nAnyDesde = 0;
     nMesDesde = 0;
     nAnyHasta = 0;
     nMesHasta = 0;
     aAnyDesde = "";
     aMesDesde = "";
     aDia = "";
     aMes = "";
     aAny = "";
     aEtiqueta = "";
     nLoc = 0;
     nTipo = 0;
     aValor = "";
     aCampoFecha = "";
     aDiaRec = "";
     aMesRec = "";
     aAnyRec = "";
     aHoraRec = "";
     aEtiquetaIni = "";
     aEtiquetaFin = "";
     aMesHasta = "";
     aAnyHasta = "";
     aTipo = "";

     nEmp = 0;
     nTra = 0;
     swEncontrada = 0;
     aAlfa1 = "";
     aAlfa2 = "";
     &enMes = 0;
     &enAny = 0;
     &enSwAlta = 0;

     aNaf = "";
     aEstado = "";
     fFechaRec = @;

     aTipoIPF = "";
     aNumeroIPF = "";
     aSiglas = "";

     aDiaDesdeTramo = "";
     aMesDesdeTramo = "";
     aAnyDesdeTramo = "";
     aFechaDesdeTramo = "";
     aDiaHastaTramo = "";
     aMesHastaTramo = "";
     aAnyHastaTramo = "";
     aFechaHastaTramo = "";

     aDias = "";
     aHoras = "";
     aDescripcion = "";
     nConcepto = 0;
     nImporte = 0;
     aCentro = "";

     nCampo = 0;
     nCampo1 = 0;
     nCampo2 = 0;
     nCampo3 = 0;
     aTipoSuma = "";
     nBase = 0;
     aTipoDCL = "";

     nMesClave = 0;
     nAnyClave = 0;
     nMesControl = 0;
     nAnyControl = 0;

     nPos = 0;
     swBlancoAnt = 0;
     nCaracter = 0;
     nMesDatos = 0;
     swAtrasos = 0;
     swNoActiva = 0;
     swActualiza = 0;

     nAnyAtrasos = 0;
     nDesdeAnyAtr = 0;
     nHastaAnyAtr = 0;
     nSumaTipo = 0;
     nBuscaTipo = 0;
|FIN;

|PROCESO Centra;
     aLon = aCadena % 0;
     nLon = aLon;
     nBlancos = nEspacio - nLon;
     nNume = nBlancos / 2;
     nBlancosIzd = nNume ! 0;
     nBlancosDer = nBlancos - nBlancosIzd;
     aBlancosIzd = " " * nBlancosIzd;
     aBlancosDer = " " * nBlancosDer;
     aCadenaSal = aBlancosIzd + aCadena + aBlancosDer;

|FPRC;

|PROCESO cretae02_atrasos;
     |SI nMesDesde = #cretae02#17;
     |Y nMesHasta = #cretae02#18;
     |Y nAnyDesde = #cretae02#19;
          || este es el tpo de atrasos que buscaba
          nTipo = #cretae02#3;
     |FINSI;
|FPRC;

|DEFBUCLE cretae02_atrasos;
     #cretae02#1;
     ;
     00001, nAnyControl, nMesControl, 05, aCCC, "        ";
     99999, nAnyControl, nMesControl, 20, aCCC, "zzzzzzzz";
     ;
     NULL, cretae02_atrasos;
|FIN;

|PROCESO BuscaTipoAtrasos;
     nTipo = 0;
     |BUCLE cretae02_atrasos;
|FPRC;

/****************************************************************************/
/***************************   BORRADO PREVIO *******************************/
/****************************************************************************/

|PROCESO Borra_cretam35;
     |BORRA 020#cretam35.a;
     |LIBERA #cretam35;
|FPRC;

|DEFBUCLE Borra_cretam35;
     #cretam35#2;
     ;
     aCCC, nAnyClave, nMesClave, nTipo, nDesdeAnyAtr;
     aCCC, nAnyClave, nMesClave, nTipo, nHastaAnyAtr;
     ;
     NULL, Borra_cretam35;
|FIN;

|PROCESO IniciaCCC;
     #cretat12#0 = #cretam34#0;
     #cretat12#1 = #cretam34#1;
     #cretat12#2 = #cretam34#2;
     #cretat12#3 = #cretam34#3;
     #cretat12#4 = #cretam34#4;
     |SI #cretam34#3 = 50;
     |O #cretam34#3 = 60;
          #cretat12#3 = 00;
     |FINSI;
     |LEE 000#cretat12.=;
     |SI FSalida ! 0;
          |GRABA 020#cretat12.n;
          |LIBERA #cretat12;

          #cretam02#0 = #cretam34#0;
          #cretam02#1 = #cretam34#1;
          #cretam02#2 = #cretam34#2;
          #cretam02#3 = #cretam34#3;
          #cretam02#4 = #cretam34#4;
          |SI #cretam34#3 = 50;
          |O #cretam34#3 = 60;
               #cretam02#3 = 00;
          |FINSI;
          |LEE 000#cretam02.=;
          |SI FSalida = 0;
               #cretam02#16 = 0;
               #cretam02#23 = "";
               |GRABA 020#cretam02.a;
               |LIBERA #cretam02;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Borra_cretam34;
     |BORRA 020#cretam34.a;
     |LIBERA #cretam34;

     |HAZ IniciaCCC;
|FPRC;

|DEFBUCLE Borra_cretam34;
     #cretam34#2;
     ;
     aCCC, nAnyDesde, nMesClave, nTipo, nDesdeAnyAtr;
     aCCC, nAnyDesde, nMesClave, nTipo, nHastaAnyAtr;
     ;
     NULL, Borra_cretam34;
|FIN;

/****************************************************************************/
/*********** PROCESOS PARA BUSCAR CODIGOS DE EMP, TRA Y CEN *****************/
/****************************************************************************/

|PROCESO BuscaCCC;
     nEmp = #cretam02#0;
     swEncontrada = 1;
|FPRC;

|DEFBUCLE BuscaCCC_At;
     #cretam02#1;
     19;
     00001, 2000, nMesDesde, nTipo, aCCC, nAnyDesde;
     99999, 2999, nMesHasta, nTipo, aCCC, nAnyDesde;
     ;
     NULL, BuscaCCC;
|FIN;

|DEFBUCLE BuscaCCC;
     #cretam02#1;
     ;
     00001, nAnyDesde, nMesDesde, nBuscaTipo, aCCC;
     99999, nAnyDesde, nMesDesde, nBuscaTipo, aCCC;
     ;
     NULL, BuscaCCC;
|FIN;

|PROCESO BuscaPorCCC;
     swEncontrada = 0;
     |SI swAtrasos = 0;
          nBuscaTipo = nTipo;
          |SI nTipo = 50; |O nTipo = 60;
               nBuscaTipo = 00;
          |FINSI;
          |SI nTipo = 52; |O nTipo = 62;
               nBuscaTipo = 02;
          |FINSI;
          |BUCLE BuscaCCC;
     |SINO;
          |BUCLE BuscaCCC_At;
     |FINSI;
|FPRC;

/****************************************************************************/
/********************   TIPOS DE DATOS COMUNES  *****************************/
/****************************************************************************/

|PROCESO TipoFecha;
     aDia = "";
     aMes = "";
     aAny = "";

     |PARA; |SI; |HACIENDO;
          |HAZ LeeLinea;

          aEtiqueta = "Dia";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               aDia = aValor;
          |FINSI;

          aEtiqueta = "Mes";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               aMes = aValor;
          |FINSI;

          aEtiqueta = "Anho";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               aAny = aValor;
          |FINSI;

          aEtiqueta = aCampoFecha;
          |HAZ BuscaEtiFin;
          |SI nLoc = 1;
               |SAL;
          |FINSI;
     |SIGUE;
|FPRC;

|PROCESO FechaHoraRec;
     |PARA; |SI; |HACIENDO;
          |HAZ LeeLinea;

          aEtiqueta = "FechaRecaudacion";
          |HAZ BuscaEtiIni;
          |SI nLoc = 1;
               aCampoFecha = "FechaRecaudacion";
               |HAZ TipoFecha;
               aDiaRec = aDia;
               aMesRec = aMes;
               aAnyRec = aAny;
               aAlfa = aDiaRec + "." + aMesRec + "." + aAnyRec;
               fFechaRec = aAlfa;
          |FINSI;

          aEtiqueta = "HoraRecaudacion";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               aHoraRec = (aValor % 102) + ":" + (aValor % 302) + ":" + (aValor % 502);
          |FINSI;

          aEtiqueta = "FechaHoraRecaudacion";
          |HAZ BuscaEtiFin;
          |SI nLoc = 1;
               |SAL;
          |FINSI;
     |SIGUE;
|FPRC;

|PROCESO CCC;
     |PARA; |SI; |HACIENDO;
          |HAZ LeeLinea;

          aEtiqueta = "Regimen";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               aCCC = aValor;
          |FINSI;

          aEtiqueta = "Provincia";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               aCCC = aCCC + aValor;
          |FINSI;

          aEtiqueta = "Numero";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               aCCC = aCCC + aValor;
          |FINSI;

          aEtiqueta = "Ccc";
          |HAZ BuscaEtiFin;
          |SI nLoc = 1;
               |SAL;
          |FINSI;
     |SIGUE;
|FPRC;

/****************************************************************************/
/***********     PROCESOS PARA BUSCAR LAS ETIQUETAS       *******************/
/****************************************************************************/

|PROCESO BuscaEtiFin;
     nLoc = 0;
     aEtiqueta = "</" + aEtiqueta + ">";
     aAlfa = aLinea - aEtiqueta;
     |SI aAlfa ! aLinea;
          nLoc = 1;
     |FINSI;
|FPRC;

|PROCESO BuscaEtiIni;
     nLoc = 0;
     aEtiqueta = "<" + aEtiqueta;
     aAlfa = aLinea - aEtiqueta;
     |SI aAlfa ! aLinea;
          nLoc = 1;
     |FINSI;
|FPRC;

|PROCESO QuitaBlancos;
     aAlfa = aValor;
     aValor = "";
     aLon = aAlfa % 0;
     nLon = aLon;
     |PARA nPos = 1; |SI nPos [ nLon; |HACIENDO nPos = nPos + 1;
          nCaracter = (nPos * 100) + 1;
          aAlfa1 = aAlfa % nCaracter;
          |SI aAlfa1 = " "; |Y swBlancoAnt = 1;
               || quito el blanco este que me sobra
          |SINO;
               aValor = aValor + aAlfa1;
          |FINSI;
          |SI aAlfa1 = " ";
               swBlancoAnt = 1;
          |SINO;
               swBlancoAnt = 0;
          |FINSI;
     |SIGUE;
|FPRC;

|PROCESO BuscaEtiIniFin;
     aValor = "";
     nLoc = 0;

     aEtiquetaIni = "<" + aEtiqueta + ">";
     aEtiquetaFin = "</" + aEtiqueta + ">";
     aAlfa = aLinea - aEtiquetaIni - aEtiquetaFin;
     |SI aAlfa ! aLinea;
          aValor = aAlfa;
          |SI aEtiqueta = "RazonSocial"; |O aEtiqueta = "EntidadAtEp";
          |O aEtiqueta = "DescripcionLDCL"; |O aEtiqueta = "DescripcionTipoLiquidacion";
          |O aEtiqueta = "CuotaALiquidar";
               |QBF aValor;
          |SINO;
               |QBT aValor;
          |FINSI;
          nLoc = 1;
          |SI aEtiqueta = "RazonSocial";
               |HAZ QuitaBlancos;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO LeeLinea;
     |XLEE nHandle, 250, aLinea;
     FLinea = FSalida;
     nNroLinea = nNroLinea + 1;
     aNroLinea = nNroLinea;
     aAlfa = " Linea " + aNroLinea + " de " + aTotalLineas;

     |ATRI R;
     aCadena = aAlfa; nEspacio = 35; |HAZ Centra;
     |PINTA 2223, aCadenaSal;
     |ATRI 0;
|FPRC;

|PROCESO LineaDCL;
     FLinea = 0;
     swAcaba = 0;

     nConcepto = 0;
     aDescripcion = "";
     nBase = 0;
     nImporte = 0;

     |PARA;
     |SI FLinea ] 0; |Y swAcaba = 0;
     |HACIENDO;
          FLinea = 0;
          |HAZ LeeLinea;

          aEtiqueta = "DescripcionLDCL";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               aDescripcion = aValor;

               aCadena = aDescripcion;
               swQuitaTildes = 1;
               |HAZ QuitaRaros;
               aDescripcion = aCadena;

               aAlfa = " " * 100;
               aDescripcion = aDescripcion + aAlfa;
               aDescripcion = (aDescripcion % 199) + " ";
               |SELECT #2#cretam18;
               #cretam18#1 = aDescripcion;
               |LEE 000#cretam18.=;
               |SI FSalida = 0;
                    nConcepto = #cretam18#0;
               |FINSI;
          |FINSI;

          aEtiqueta = "BaseLDCL";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               nBase = aValor;
               nBase = nBase / 100;
          |FINSI;

          aEtiqueta = "ImporteLDCL";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               nImporte = aValor;
               nImporte = nImporte / 100;
          |FINSI;

          aEtiqueta = "LineaDCL";
          |HAZ BuscaEtiFin;
          |SI nLoc = 1;
               || ya tengo los datos para guardar la linea (el cretam35)
               |DDEFECTO #cretam35;
               #cretam35#0 = nEmp;
               #cretam35#1 = nAnyClave;
               #cretam35#2 = nMesClave;
               #cretam35#3 = nTipo;
               #cretam35#4 = aCCC;
               #cretam35#5 = nConcepto;
               #cretam35#10 = nAnyAtrasos;
               |LEE 000#cretam35.=;
               |SI FSalida ! 0;
                    || tiene que entrar siempre, no puede existir
                    #cretam35#6 = aDescripcion;
                    #cretam35#7 = nBase;
                    #cretam35#8 = nImporte;
                    #cretam35#9 = aSoloFichero;

                    |GRABA 020#cretam35.n;
                    |LIBERA #cretam35;
                    |SI nConcepto = 998;
                         |PARA nMesDatos = nMesDesde;
                         |SI nMesDatos [ nMesHasta;
                         |HACIENDO nMesDatos = nMesDatos + 1;
                              swActualiza = 0;
                              #cretam02#0 = nEmp;
                              #cretam02#1 = nAnyDesde;
                              #cretam02#2 = nMesDatos;
                              #cretam02#3 = nTipo;
                              |SI nTipo = 50;
                                   #cretam02#3 = 00;
                              |FINSI;
                              |SI nTipo = 60;
                                   #cretam02#3 = 00;
                              |FINSI;
                              #cretam02#4 = aCCC;
                              |LEE 101#cretam02.=;
                              |SI FSalida = 0;
                              |Y #cretam02#20 = nMesControl;
                              |Y #cretam02#21 = nAnyControl;
                                   swActualiza = 1;
                              |SINO;
                                   || en atrasos puede ser la clave el a¤o
                                   || desde o el a¤o clave, hago esto para
                                   || buscar uno de los dos
                                   || probablemente serviria solo el AnyClave
                                   || pero con atrasos no me fio
                                   #cretam02#0 = nEmp;
                                   #cretam02#1 = nAnyClave;
                                   #cretam02#2 = nMesDatos;
                                   #cretam02#3 = nTipo;
                                   #cretam02#4 = aCCC;
                                   |LEE 101#cretam02.=;
                                   |SI FSalida = 0;
                                   |Y #cretam02#20 = nMesControl;
                                   |Y #cretam02#21 = nAnyControl;
                                        swActualiza = 1;
                                   |FINSI;
                              |FINSI;

                              |SI nTipo = 00; |Y #cretam02#23 = "E1";
                                   || en el mes que hay exoneracion de cuotas
                                   || cuando me venga el tipo 00 no debo actua-
                                   || lizar si ya existia el tipo 50 actualizado
                                   || El tipo 00 es "T" y el 50 es "E1"

                                   swActualiza = 0;
                              |FINSI;
                              |SI nTipo = 00; |Y #cretam02#23 = "E4";
                                   |SI nImporte = #cretam02#16;
                                        nImporte = 0;
                                   |FINSI;
                              |FINSI;
                              |SI nTipo = 60; |Y #cretam02#23 = "T ";
                                   || si existe el tipo "T" ya grabado, el 60
                                   || no lo tiene que grabar
                                   |SI nImporte = #cretam02#16;
                                        nImporte = 0;
                                   |FINSI;
                              |FINSI;
                              |SI nTipo = 60; |Y #cretam02#23 = "E1";
                                   swActualiza = 0;
                              |FINSI;
                              |SI swActualiza = 1;
                                   #cretam02#16 = nImporte;
                                   #cretam02#22 = "B";
                                   aAlfa = #cretam34#14;
                                   #cretam02#23 = aAlfa;
                                   |GRABA 020#cretam02.a;
                              |FINSI;
                              |LIBERA #cretam02;
                         |SIGUE;
                    |FINSI;
               |FINSI;

               |SAL;
          |FINSI;
     |SIGUE;
|FPRC;

|PROCESO DatosCabeceraDCL;
     || primero grabo ya mi cabecera (el cretam34)
     nEmp = 0;
     |HAZ BuscaPorCCC;

     swNoActiva = 0;
     #labempre#0 = nEmp;
     |LEE 000#labempre.=;
     |SI FSalida = 0; |Y #labempre#67 = "S";
          || esta activa
     |SINO;
          swNoActiva = 1;
          |ACABA;
     |FINSI;

     #cretam34#0 = nEmp;
     #cretam34#1 = nAnyClave;
     #cretam34#2 = nMesClave;
     #cretam34#3 = nTipo;
     #cretam34#4 = aCCC;
     #cretam34#33 = nAnyAtrasos;
     |LEE 000#cretam34.=;
     |SI FSalida ! 0;
          || tiene que entrar siempre
          #cretam34#6 = #labempre#2;
          #cretam34#11 = aTipo;
          #cretam34#14 = aTipoDCL
          #cretam34#12 = fFechaRec;
          #cretam34#13 = aHoraRec;
          |SI nTipo = 90;
               nNume = aMesRec;
               |SI nNume = 01;
                    nNume1 = 12;
                    nNume2 = aAnyRec;
                    nNume2 = nNume2 - 1;
               |SINO;
                    nNume1 = nNume - 1;
                    nNume2 = aAnyRec;
               |FINSI;
               #cretam34#31 = nNume2;
               #cretam34#32 = nNume1;
          |FINSI;

          #cretam34#17 = aMesDesde;
          #cretam34#18 = aAnyDesde;
          #cretam34#19 = aMesHasta;
          #cretam34#20 = aAnyHasta;
          #cretam34#28 = aSoloFichero;
          |GRABA 020#cretam34.n;

          || la leo otra vez para dejarla leida y despues grabarla de nuevo

          #cretam34#0 = nEmp;
          #cretam34#1 = nAnyClave;
          #cretam34#2 = nMesClave;
          #cretam34#3 = nTipo;
          #cretam34#4 = aCCC;
          #cretam34#33 = nAnyAtrasos;
          |LEE 000#cretam34.=;
     |FINSI;

     FLinea = 0;
     swAcaba = 0;

     |PARA;
     |SI FLinea ] 0; |Y swAcaba = 0;
     |HACIENDO;
          FLinea = 0;
          |HAZ LeeLinea;

          aEtiqueta = "AlcanceCalculo";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               #cretam34#29 = aValor;
          |FINSI;

          aEtiqueta = "CuotaALiquidar";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               #cretam34#30 = aValor;
          |FINSI;

          aEtiqueta = "Autorizado";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               #cretam34#15 = aValor;
          |FINSI;

          aEtiqueta = "Envio";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               #cretam34#16 = aValor;
          |FINSI;

          aEtiqueta = "DescripcionTipoLiquidacion";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               #cretam34#21 = aValor;
          |FINSI;

          aEtiqueta = "RazonSocial";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               #cretam34#22 = aValor;
          |FINSI;

          aEtiqueta = "TipoIpf";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               #cretam34#23 = aValor;
          |FINSI;

          aEtiqueta = "NumeroIpf";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               #cretam34#24 = (aValor % -109);
          |FINSI;

          aEtiqueta = "EntidadAtEp";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               #cretam34#25 = aValor;
          |FINSI;

          aEtiqueta = "NumeroTrabajadores";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               #cretam34#26 = aValor;
          |FINSI;

          aEtiqueta = "NumeroLiquidacion";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               #cretam34#27 = aValor;
          |FINSI;

          aEtiqueta = "DatosCabeceraDCL";
          |HAZ BuscaEtiFin;
          |SI nLoc = 1;
               |GRABA 020#cretam34.a;
               |LIBERA #cretam34;
               |SAL;
          |FINSI;
     |SIGUE;
|FPRC;

|PROCESO LecturaXML;
     FLinea = 0;

     aAlfa = eaFichero;
     |XABRE "", aAlfa, nHandle; nHandle = FSalida;
     |SI FSalida ] 0;
          |PARA;
          |SI FLinea ] 0; |Y swAcaba = 0;
          |HACIENDO;
               FLinea = 0;
               |HAZ LeeLinea;

               aEtiqueta = "Ccc";
               |HAZ BuscaEtiIni;
               |SI nLoc = 1;
                    |HAZ CCC;
               |FINSI;

               aEtiqueta = "PeriodoDesde";
               |HAZ BuscaEtiIni;
               |SI nLoc = 1;
                    aCampoFecha = "PeriodoDesde";
                    |HAZ TipoFecha;
                    aMesDesde = aMes;
                    nMesDesde = aMes;
                    aAnyDesde = aAny;
                    nAnyDesde = aAny;
               |FINSI;

               aEtiqueta = "PeriodoHasta";
               |HAZ BuscaEtiIni;
               |SI nLoc = 1;
                    aCampoFecha = "PeriodoHasta";
                    |HAZ TipoFecha;
                    aMesHasta = aMes;
                    nMesHasta = aMes;
                    aAnyHasta = aAny;
                    nAnyHasta = aAny;
               |FINSI;

               aEtiqueta = "Tipo";
               |HAZ BuscaEtiIniFin;
               |SI nLoc = 1;
                    swAtrasos = 0;
                    aTipo = aValor;
                    |SI aAlfa = "L00";
                         nTipo = 00;
                         nMesClave = nMesDesde;
                         nAnyClave = nAnyDesde;
                    |FINSI;
                    |SI aAlfa = "L13";
                         nTipo = 02;
                         nMesClave = nMesDesde;
                         nAnyClave = nAnyDesde;
                    |FINSI;
                    |SI aAlfa = "L90";
                         nTipo = 90;
                         nMesClave = nMesDesde;
                         nAnyClave = nAnyDesde;
                    |FINSI;
                    |SI aTipo = "L03";
                         swAtrasos = 1;
                         nTipo = 999;
                         || son atrasos, hay que buscar el nTipo
                         || pero esto lo hare despues en la fecha de control
                         nAnyAtrasos = nAnyDesde;
                    |FINSI;

                    |SI nTipo = 00; |O nTipo = 02;
                         nTipo = nTipo + nSumaTipo;
                    |FINSI;

                    || con el tipo a 999, en atrasos no borrara nada, lo
                    || hara despues

                    nDesdeAnyAtr = 0000;
                    nHastaAnyAtr = 0000;

                    |BUCLE Borra_cretam34;
                    |BUCLE Borra_cretam35;

                    |SELECT #1#cretam34;
                    |SELECT #1#cretam35;
               |FINSI;

               aEtiqueta = "FechaControl";
               |HAZ BuscaEtiIni;
               |SI nLoc = 1;
                    aCampoFecha = "FechaControl";
                    |HAZ TipoFecha;

                    nMesControl = aMes;
                    nAnyControl = aAny;

                    nMesClave = nMesControl;
                    nAnyClave = nAnyControl;

                    || si encuentro este campo es que estoy en atrasos
                    || y ya tengo leido que es tipo L03, ahora ire a buscar que
                    || tipo de atrasos eran

                    |HAZ BuscaTipoAtrasos;

                    |SI nTipo = 00;
                         aAlfa = "    ATENCION. No se ha encontrado registro de Atrasos ";
                         aAlfa = aAlfa + " del mes " + aMesDesde + " al " + aMesHasta;
                         aAlfa = aAlfa + " de " + aAnyDesde + " en CCC: " + aCCC;
                         |MENAV aAlfa;
                    |FINSI;

                    nDesdeAnyAtr = 0000; nHastaAnyAtr = 0000;
                    |BUCLE Borra_cretam34;
                    nDesdeAnyAtr = nAnyAtrasos; nHastaAnyAtr = nAnyAtrasos;
                    |BUCLE Borra_cretam34;
                    nDesdeAnyAtr = 0000; nHastaAnyAtr = 0000;
                    |BUCLE Borra_cretam35;
                    nDesdeAnyAtr = nAnyAtrasos; nHastaAnyAtr = nAnyAtrasos;
                    |BUCLE Borra_cretam35;
                    |SELECT #1#cretam34;
                    |SELECT #1#cretam35;
               |FINSI;

               aEtiqueta = "FechaHoraRecaudacion";
               |HAZ BuscaEtiIni;
               |SI nLoc = 1;
                    |HAZ FechaHoraRec;
               |FINSI;

               aEtiqueta = "TipoDCL";
               |HAZ BuscaEtiIniFin;
               |SI nLoc = 1;
                    aTipoDCL = aValor;
               |FINSI;

               aEtiqueta = "DatosCabeceraDCL";
               |HAZ BuscaEtiIni;
               |SI nLoc = 1;
                    |HAZ DatosCabeceraDCL;
                    |SI swNoActiva = 1;
                         |ACABA;
                    |FINSI;
               |FINSI;

               aEtiqueta = "LineaDCL";
               |HAZ BuscaEtiIni;
               |SI nLoc = 1;
                    |HAZ LineaDCL;
               |FINSI;
          |SIGUE;
     |FINSI;

     |XCIERRA nHandle;
|FPRC;

|PROCESO LecturaPreviaXML;
     FLinea = 0;

     aAlfa = eaFichero;
     |XABRE "", aAlfa, nHandle; nHandle = FSalida;
     |SI FSalida ] 0;
          |PARA;
          |SI FLinea ] 0; |Y swAcaba = 0;
          |HACIENDO;
               FLinea = 0;
               nNume = nNume + 1;
               |XLEE nHandle, 250, aLinea;

               aEtiqueta = "CuotaALiquidar";
               |HAZ BuscaEtiIniFin;
               |SI nLoc = 1;
                    |SI aValor = "Total";
                         nSumaTipo = 0;
                    |FINSI;
                    |SI aValor = "No exonerada";
                         nSumaTipo = 50;
                    |FINSI;
                    |SI aValor = "Exonerada";
                         nSumaTipo = 60;
                    |FINSI;
                    |SAL;
               |FINSI;
               |SI nNume = 50;     || asumo que no la he encontrado por lo que sea
                    |SAL;
               |FINSI;
          |SIGUE;
     |FINSI;

     |XCIERRA nHandle;
|FPRC;

|PROGRAMA;
     |ABRE #cretam34;
     |ABRE #cretam35;
     |ABRE #cretam02;

     |ABRE #cretam18;

     |ABRE #labempre;
     |ABRE #labcentr;
     |ABRE #nomcenes;
     |ABRE #labtraba;

     || se inicia y se borra desde el cretap13
     aAlfa = Usuario; |QBT aAlfa; aAlfa = "tm_cretat12_" + aAlfa;
     |NOME_DAT #cretat12#aAlfa#;
     |ABRE #cretat12;

     nSumaTipo = 0;
     |HAZ LecturaPreviaXML;   || para determinar el tipo de liquidacion
                              || Total, Exonerada o No Exonerada
     |HAZ LecturaXML;

     |CIERRA #cretat12;
     |CIERRA #cretam02;
     |CIERRA #cretam35;
     |CIERRA #cretam34;

     |CIERRA #cretam18;

     |CIERRA #labempre;
     |CIERRA #labcentr;
     |CIERRA #nomcenes;
     |CIERRA #labtraba;
|FPRO;
