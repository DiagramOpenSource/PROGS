|FICHEROS;
    nomssflc #0;
    labempre #1;         || empresas
    labtraba #2;         || trabajadores
    labcentr #6;         || centros
    nomtrflc #7;         || temporal FLC
    nomcalac #11;        || atrasos calculos cabs.
    nomcenes #35;        || centros especiales
     nomflctm #100;      || tmp trabajadores
|FIN;

|VARIABLES;
    alfa1 = "";
    alfa2 = "";
    alfa3 = "";
    alfa4 = "";
    nume1 = 0;
    nume2 = 0;
    nume3 = 0;
    nume4 = 0;
    para1 = 0;
    para2 = 0;
    para3 = 0;
    para4 = 0;
    fech1 = @;
    fech2 = @;
    fech3 = @;
    fech4 = @;
    FEstado = 0;
    seleccio = 0;
    p_mes = "";
    d_mes = 0;
    h_mes = 0;
    meses = 0;
    otra = 0;
    &ss1 = "";
    &ss2 = "";
    &ss3 = "";
    &trab = 0;
    &base = 0;
    &tipo = 0;
    &cuot = 0;
    &dmes = 0;
    &dany = 0;
    &hmes = 0;
    &hany = 0;
    &pago = "";
    &recp = 0;
    &recc = 0;
    &impo = 0;
    cta_co = "";
     baseP = 0;
     baseM = 0;
     baseIT = 0;
     diff = 0;
     swNoSigas = 0;
     aCNAE = "";
     &aEjemplar = "";
|FIN;

|INCLUYE i_tempo;
||INCLUYE i_verif;
||INCLUYE i_seguim;
____________________________________/ CALCULOS DE PANTALLA
|PROCESO AvisoCrystal; |TIPO 7;
     |PINTA 0547, "ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿";
     |PINTA 0647, "³ Este modelo se puede imprimir ³";
     |PINTA 0747, "³  en formato                   ³";
     |PINTA 0847, "³                               ³";
     |PINTA 0947, "ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ";
     |ATRI I;
     |PINTA 0761, "CRYSTAL REPORTS.";
     |ATRI 0;
|FPRC;

|PROCESO defecto; |TIPO 7; ||calcula el a¤o por defecto segun fecha sistema
    fech1 = ~;
    alfa1 = fech1;
    alfa1 = alfa1 % -104;
    #0Campo = alfa1;
|PINTA #0Campo;
|FPRC;

|PROCESO meses; |TIPO 0;
|SI #0#6 = "A";
        #0#4 = 1;        |PINTA #0#4;
        #0#8 = 12;       |PINTA #0#8;
|FINSI;
|FPRC;
____________________________________/ LECTURA DEL TEMPORAL E IMPRIME
|PROCESO puente;

|SI #0#6 = "A";
        trab = (#7#1/meses)!2;  || trabajadores (anual)
|SINO;
        trab = #7#1;            || trabajadores (mensual)
|FINSI;

    base = #7#2;         || base AT y ET
    tipo = #7#3;         || (%)tipo
    cuot = #7#4;         || cuota
    dmes = #7#5;         || desde mes
    dany = #7#7;         || desde a¤o
    hmes = #7#6;         || hasta mes
    hany = #7#8;         || hasta a¤o
    pago = #7#9;         || forma pago
    recp = #7#10;        || (%) recargo
    recc = #7#11;        || cuota recargo
    impo = #7#12;        || importe a ingresar
|FPRC;

|PROCESO lee_empresa;
|DDEFECTO #1;
    #1#0 = #7#0;
|LEE 000#1=;

    alfa1 = #7#13;
|QBF alfa1;
    alfa2 = alfa1 % 0;
    nume1 = alfa2;
    nume1 = (nume1 - 4) + 300;
    ss1 = alfa1 % 102;
    ss2 = alfa1 % nume1;
    ss3 = alfa1 % -102;

    #labempre#128 = #7#14;
|FPRC;

|PROCESO lee_tmp1;
|HAZ lee_empresa;
|SI #1#42 = "S"; |Y #7#2 > 0;
    |HAZ puente;
    |INFOR "nomyyflc";
    aEjemplar = "Ejemplar para la oficina recaudadora";
    |PRINT;
    |PIEINF;
    aEjemplar = "Ejemplar para el interesado";
    |PRINT;
    |PIEINF;
/*
        se_empre = #1#0;      || codigo empresa
        se_tipos = "flc";     || tipo de modelo
        se_activ = 0;         || tipo 0
        se_impor = impo;      || total a pagar
        alfa1 = #0#8;    alfa1 = ("00" + alfa1) % -102;     || mes
        alfa2 = #0#5;                                       || a¤o
        se_fecha = "01." + alfa1 + "." + alfa2;   || fecha emision
        se_prime = 0;         || tipo (con el cero inicializa importe)
    |HAZ AlAgicli;
*/
|FINSI;
|FPRC;

|DEFBUCLE lee_tmp;
 #7#1;
 ;
     1, "               ";
 99999, "zzzzzzzzzzzzzzz";
 ;
 NULL, lee_tmp1;
|FIN;
____________________________________/ PREPARA TEMPORAL
|PROCESO cta_cotizacion;
|DDEFECTO #1;
    #1#0 = #7#0;
|LEE 000#1=;

     aCNAE = #1#128;

|SI #2#45 = "087";|O #2#45 = "097";|O #2#45 = "085";
                  |O #2#45 = "064";|O #2#45 = "065"; |O #2#45 = "421";
     || swNoSigas = 1; || trabajadores en formacion no entran 606.715
     swNoSigas = 0; || trabajadores en formacion ahora resulta que si: 3916.23
    |DDEFECTO #35;
        #35#0 = #2#0;
        #35#13= #2#77;
    |SI #2#45 = "097"; |O #2#45 = "085"; |O #2#45 = "421";
            #35#1 = "87";
    |SINO;
        |SI #2#45 = "065";
                #35#1 = "64";
        |SINO;
                #35#1 = #2#45 % 2;
        |FINSI;
    |FINSI;
    |LEE 000#35=;
        cta_co = #35#10;      || cta.cto. del centro de trabajo especial
|SINO;
        cta_co = #1#13;       || cta.cot. de la empresa
    |DDEFECTO #6;
        #6#0 = #2#0;
        #6#1 = #2#77;
    |LEE 000#6=;
            cta_co = #6#10;        || cta.cot. centro de trabajo
            aCNAE = #6#22; |QBF aCNAE;
            |SI aCNAE = "";
                 aCNAE = #1#128; |QBF aCNAE;
            |FINSI;
|FINSI;
|FPRC;

|PROCESO lee_calculos1;
    swNoSigas = 0;
    #2#0 = #11#0;
    #2#1 = #11#1;
|LEE 000#2=;
|SI FSalida = 0;|Y #2#87 ] #0#2;|Y #2#87 [ #0#3; |Y #2#42 ! "H";
|Y #2#247 ! 163; |Y #2#247 ! 613;
        seleccio = 1;
    |DDEFECTO #7;
        #7#0 = #11#0;             || empresa
    |HAZ cta_cotizacion;
     |SI swNoSigas = 1;
          |ACABA;
     |FINSI;
        #7#13 = cta_co;
    |LEE 101#7=;
        FEstado = FSalida;

          #100#0 = #2#0;
          #100#1 = #2#7;
          #100#2 = #11#2;
          |LEE 101#100=;
          |SI FSalida = 0;
          |SINO;
               #7#1 = #7#1 + 1;          || nro. trabajadores
               |GRABA 020#100n;
          |FINSI;
          |LIBERA #100;

          |SI #11#65 = 0;     || tenemos maternidad
               baseP = #11#40;
          |SINO;
               baseP = #11#40;
               baseM = (#11#35 * #11#65) ! 2;

               baseIT = (#11#35 * (#11#65 + #11#31 + #11#32)) ! 2;
               |SI baseIT ! #11#62;        || comparo base en baja de mto. con base en baja real
                    diff = baseIT - #11#62;  || si son distintas ajusto restando diferencia
                    baseM = baseM - diff;  || a base de comunes de y profesionales, esto es
               |FINSI;                    || por lo del swAjuste3 en nomcal_3
               baseP = baseP + baseM;
          |FINSI;

          #7#2 = #7#2 + baseP;     || base A.T. y E.P.
        #7#3 = #0#9;              || (%) a aplicar
        #7#4 = #7#2 % #7#3;       || cuota
        #7#5 = d_mes;             || desde mes
        #7#6 = h_mes;             || hasta mes
        #7#7 = #0#5;              || desde a¤o
        #7#8 = #7#7;              || hasta a¤o
        #7#9 = #0#6;              || forma de pago
        #7#10 = #0#7;             || (%) recargo
        #7#11 = #7#4 % #7#10;     || cuota recargo
        #7#12 = #7#4 + #7#11;     || importe a ingresar

        #7#14 = aCNAE;
    |SI FEstado = 0;  |GRABA 020#7a;  |FINSI;
    |SI FEstado ! 0;  |GRABA 020#7n;  |FINSI;
    |LIBERA #7;
|FINSI;
|FPRC;

|DEFBUCLE lee_calculos;
 #11#1;
 ;
 #0#0,     1, d_mes, 5;
 #0#1, 99999, h_mes, 7;
 ;
 NULL, lee_calculos1;
|FIN;

|PROCESO imprimir;
|SI seleccio ! 0;
        otra = 1;
    |PINTA 2305, "Imprimiendo ...";
    |ABRE #1;
    |BUCLE lee_tmp;
    |CIERRA #1;
|FINSI;
|DELINDEX #7;
|FPRC;

|PROCESO todo;
     seleccio = 0;
     |PINTA 2305, "Preparando Temporal ...";
     |HAZ nom_usu;
     alfa1 = nom_tmp;
     |NOME_DAT #7 alfa1;
     |DELINDEX #7;

     alfa2 = Usuario; |QBF alfa2;    alfa1 = "l" + alfa2;
     |NOME_DAT #100 alfa2;
     |DELINDEX #100;

     |ABRE #1;
     |ABRE #2;
     |ABRE #6;
     |ABRE #7;
     |ABRE #35;
     |ABRE #100;
     |BUCLE lee_calculos;
     |CIERRA #1;
     |CIERRA #2;
     |CIERRA #6;
     |CIERRA #7;
     |CIERRA #35;
     |CIERRA #100;
     |BLIN 23;
     |HAZ imprimir;
|FPRC;

|PROCESO imprime; |TIPO 3;
|IMPRE 0;
|SI FSalida = 0;
        meses = (#0#8 - #0#4) + 1;
    |SI #0#6 = "A";
            d_mes = #0#4;          || pago anual
            h_mes = #0#8;
        |HAZ todo;
    |SINO;
        |PARA para1 = #0#4; |SI para1 [ #0#8; |HACIENDO para1 = para1 + 1;
                d_mes = para1;
                h_mes = para1;          || pago mensual
            |HAZ todo;
        |SIGUE;
    |FINSI;
    |SI otra = 0;
        |MENAV "    Seleccion vacia ...";
    |FINSI;
|SINO;
    |MENAV "    Proceso abortado ...";
|FINSI;
    |FININF;
|FINIMP;
|FPRC;
