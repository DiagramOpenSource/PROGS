|FICHEROS;
     syke0009 #0;
     syke0010 #100;

     nomauxi1;
     nomauxi2;
     syke0014; ||Lineas prorrateo Area / Seccion por fechas
     labtraba;
     labempre;
     ref0001@  #900;
|FIN;

|VARIABLES;
     {1}aVersion      = "";
     aPath = "";
     aLongitud = "";
     nLongitud = 0;
     nPos = 0;
     nCaracter = 0;
     aCaracter = "";

     nLinea = 0;
     nLinea1 = 0;
     nLinea2 = 0;
     nLinea3 = 0;
     nLinea4 = 0;
     aColumna = "";


     fecsys = @;
     aAlfa = "";
     aAlfa1 = "";
     nPara = 0;
     nNum = 0;
     nNum1 = 0;
     aDirBase = "";
     aBase = "";
     aOrigen = "";
     aDestino = "";
     aDestinoTrab = "";
     aIPRemoto = "";
     nCodAscii = 0;
     nResul = 0;
     nResul1 = 0;
     nResul2 = 0;
     nResul3 = 0;
     nLaLinea = 0;

     aArea = "";
     aNomArea = "";
     aSeccion = "";
     aNomSeccion = "";
     nEmpre = 0;
     aNomEmpre = "";
     nEmpreAnt = 0;
     aAreaAnt = "";
     aSeccionAnt = "";

     fAlta = @;
     fBaja = @;

     fFec1 = @;
     fFec2 = @;
     nTerm = 0;
     nLess = 0;
     nHC = 0;
     nTermR = 0;
     nLessR = 0;
     nHCR = 0;
     aResultado = "";
|FIN;

|PROCESO mirames; |TIPO 7; ||calcula el mes por defecto segun fecha sistema
     fecsys = ~;
     aAlfa = fecsys;
     aAlfa = aAlfa % 402;
     #0Campo = aAlfa;
     |PINTA #0Campo;
|FPRC;

|PROCESO miraanyo; |TIPO 7; ||calcula el periodo por defecto segun fecha sistema
     fecsys = ~;
     aAlfa = fecsys;
     aAlfa = aAlfa % -104;
     #0Campo = aAlfa;
     |PINTA #0Campo;
|FPRC;

|PROCESO SalvaTmp;
     #100#0 = nEmpre;
     #100#2 = aArea;
     #100#4 = aSeccion;
     #100#6 = #0#2;
     #100#7 = nPara;
     |LEE 000#100=;
     |SI FSalida = 0;
          nNum = #100#8;
          nNum = nNum + nHC;
          #100#8 = nNum;

          nNum = #100#9;
          nNum = nNum + nTerm;
          #100#9 = nNum;

          nNum = #100#10;
          nNum = nNum + nLess;
          #100#10 = nNum;

          |GRABA 020#100a;
     |SINO;
          #100#1 = aNomEmpre;
          #100#3 = aNomArea;
          #100#5 = aNomSeccion;
          #100#8 = nHC;
          #100#9 = nTerm;
          #100#10 = nLess;
          |GRABA 020#100n;
     |FINSI;
|FPRC;

|PROCESO labtraba;
     nTerm = 0;
     nLess = 0;
     ||AREA labtraba#62
     ||SECCION labtraba#63

     #nomauxi1#0 = #labtraba#62;
     |LEE 000#nomauxi1.=;
     |SI FSalida = 0;
          aArea = #nomauxi1#0;
          aNomArea = #nomauxi1#1;
     |FINSI;

     #nomauxi2#0 = #labtraba#62;
     #nomauxi2#1 = #labtraba#63;
     |LEE 000#nomauxi2.=;
     |SI FSalida = 0;
          ||aArea = #nomauxi2#2;
          ||SISCO. 2.1.2007 Lo he cambiado a Seccion ponia aArea y aSeccion no se inicializaba en ningun sitio
          aSeccion = #nomauxi2#1;
          aNomSeccion = #nomauxi2#2;
     |FINSI;

     aAlfa = #labtraba#0; |QBF aAlfa;
     aAlfa = "00000" + aAlfa;
     aAlfa = aAlfa % -105;
     nEmpre = aAlfa;

     |LEE 000#labempre.=;
     |SI FSalida = 0;
          aNomEmpre = #labempre#2;
     |FINSI;
     || #0#2
     aAlfa = "01.01." + #0#2;
     fFec1 = aAlfa;
     aAlfa = "00" + #0#3;
     aAlfa = aAlfa % -102;
     aAlfa = "31." + aAlfa + "." + #0#2;
     fFec2 = aAlfa;
     fAlta = #labtraba#36;
     fBaja = #labtraba#37;

     |SI fAlta > fFec2;  |ACABA;   |FINSI;
     aAlfa = #labtraba#37;    |QBF aAlfa;
     |SI aAlfa ! ""; |Y fBaja < fFec1;  |ACABA;   |FINSI;

     |PARA nPara = 1; |SI nPara [ #0#3; |HACIENDO nPara = nPara + 1;
          nTerm = 0;
          nLess = 0;
          aAlfa = "00" + nPara;    aAlfa = aAlfa % -102;
          aAlfa = "01." + aAlfa + "." + #0#2;
          fFec1 = aAlfa;

          nNum = nPara + 1;
          aAlfa = "00" + nNum;     aAlfa = aAlfa % -102;
          aAlfa = "01." + aAlfa + "." + #0#2;
          fFec2 = aAlfa;

          aAlfa = #labtraba#37;    |QBF aAlfa;
          |SI aAlfa ! ""; |Y fBaja < fFec2; |Y fBaja ] fFec1;
               nTerm = 1;
               nNum = fAlta;
               nNum1 = fBaja;
               nNum = nNum1 - nNum;
               |SI nNum [ 120;
                    nLess = 1;
               |FINSI;
          |FINSI;
          nHC = 1;
          |SI aAlfa ! ""; |Y fBaja < fFec1;
               nHC = 0;
          |FINSI;

          #syke0014#0 = #labtraba#0;
          #syke0014#1 = #labtraba#1;
          #syke0014#2 = #0#2;
          #syke0014#3 = nPara;
          #syke0014#4 = 1;
          |LEE 000#syke0014.];
          |SI FSalida = 0; |Y #syke0014#0 = #labtraba#0; |Y #syke0014#1 = #labtraba#1; |Y #syke0014#2 = #0#2; |Y #syke0014#3 = nPara;
               nHCR = nHC;
               nTermR = nTerm;
               nLessR = nLess;
               |PARA; |SI FSalida = 0; |Y #syke0014#0 = #labtraba#0; |Y #syke0014#1 = #labtraba#1; |Y #syke0014#2 = #0#2; |Y #syke0014#3 = nPara; |HACIENDO;
                    aArea = #syke0014#7;
                    aNomArea = #syke0014#8;
                    aSeccion = #syke0014#9;
                    aNomSeccion = #syke0014#10;
                    nHC = nHCR * #syke0014#11;
                    nTerm = nTermR * #syke0014#11;
                    nLess = nLessR * #syke0014#11;
                    |HAZ SalvaTmp;

                    |LEE 000#syke0014.s;
               |SIGUE;
          |SINO;
               #syke0014#0 = #labtraba#0;
               #syke0014#1 = #labtraba#1;
               #syke0014#2 = #0#2;
               #syke0014#3 = 99;
               #syke0014#4 = 1;
               |LEE 000#syke0014.];
               |SI FSalida = 0; |Y #syke0014#0 = #labtraba#0; |Y #syke0014#1 = #labtraba#1; |Y #syke0014#2 = #0#2; |Y #syke0014#3 = 99;
                    nHCR = nHC;
                    nTermR = nTerm;
                    nLessR = nLess;
                    |PARA; |SI FSalida = 0; |Y #syke0014#0 = #labtraba#0; |Y #syke0014#1 = #labtraba#1; |Y #syke0014#2 = #0#2; |Y #syke0014#3 = 99; |HACIENDO;
                         aArea = #syke0014#7;
                         aNomArea = #syke0014#8;
                         aSeccion = #syke0014#9;
                         aNomSeccion = #syke0014#10;
                         nHC = nHCR * #syke0014#11;
                         nTerm = nTermR * #syke0014#11;
                         nLess = nLessR * #syke0014#11;
                         |HAZ SalvaTmp;

                         |LEE 000#syke0014.s;
                    |SIGUE;
               |SINO;
                    |HAZ SalvaTmp;
               |FINSI;
          |FINSI;
     |SIGUE;
|FPRC;

|DEFBUCLE labtraba;
     #labtraba#1;
     ;
     #0#0, 00001;
     #0#1, 99999;
     ;
     NULL, labtraba;
|FIN;

|PROCESO ElProceso;
     |ABRE #labempre;
     |ABRE #labtraba;
     |ABRE #nomauxi1;
     |ABRE #nomauxi2;
     |ABRE #syke0014;

     aAlfa1 = Usuario; |QBT aAlfa1;    aAlfa1 = ("s" + aAlfa1) % 108;
     |NOME_DAT #100 aAlfa1;
     |DELINDEX #100;

     |ABRE #100;
     |BUCLE labtraba;
     ||CONSULTA_CLAVE #1#100;

     |CIERRA #100;
     |CIERRA #labtraba;
     |CIERRA #labempre;
     |CIERRA #nomauxi1;
     |CIERRA #nomauxi2;
     |CIERRA #syke0014;
|FPRC;


|PROCESO Datos;
     nLinea = nLinea + 1;
     nLinea1 = nLinea + 1;
     nLinea2 = nLinea + 2;
     nLinea3 = nLinea + 3;
     nLinea4 = nLinea + 4;

     aAlfa = "E" + nLinea + "," + "Total Headcount"; |ALFACALLDLL "agixl97.dll","poke_dato", aAlfa;
     aAlfa = "E" + nLinea1 + "," + "Termination"; |ALFACALLDLL "agixl97.dll","poke_dato", aAlfa;
     aAlfa = "E" + nLinea2 + "," + "120 Days or Less"; |ALFACALLDLL "agixl97.dll","poke_dato", aAlfa;
     aAlfa = "E" + nLinea3 + "," + "Termination %"; |ALFACALLDLL "agixl97.dll","poke_dato", aAlfa;
     aAlfa = "E" + nLinea4 + "," + "120 Days or Less %"; |ALFACALLDLL "agixl97.dll","poke_dato", aAlfa;
     aColumna = "E";
     |PARA nPara = 1; |SI nPara [ #0#3; |HACIENDO nPara = nPara + 1;
          #900#0 = #100#0;
          #900#2 = #100#2;
          #900#4 = #100#4;
          #900#6 = #100#6;
          #900#7 = nPara;
          |LEE 000#900=;
          |SI FSalida = 0;
               nCodAscii = &aColumna;
               nCodAscii = nCodAscii + 1;
               aColumna = &nCodAscii;
               aAlfa = aColumna + nLinea + "," + #900#8; |ALFACALLDLL "agixl97.dll","poke_dato", aAlfa;
               aAlfa = aColumna + nLinea1 + "," + #900#9; |ALFACALLDLL "agixl97.dll","poke_dato", aAlfa;
               aAlfa = aColumna + nLinea2 + "," + #900#10; |ALFACALLDLL "agixl97.dll","poke_dato", aAlfa;
               nNum = 100 * #900#9 / #900#8;
               nNum = nNum ! 2;
               aAlfa = nNum + " %";
               aAlfa = aColumna + nLinea3 + "," + aAlfa; |ALFACALLDLL "agixl97.dll","poke_dato", aAlfa;
               nNum = 100 * #900#10 / #900#8;
               nNum = nNum ! 2;
               aAlfa = nNum + " %";
               aAlfa = aColumna + nLinea4 + "," + aAlfa; |ALFACALLDLL "agixl97.dll","poke_dato", aAlfa;
          |SINO;
               nCodAscii = &aColumna;
               nCodAscii = nCodAscii + 1;
               aColumna = &nCodAscii;
               aAlfa = aColumna + nLinea + ",0" ; |ALFACALLDLL "agixl97.dll","poke_dato", aAlfa;
               aAlfa = aColumna + nLinea1 + ",0" ; |ALFACALLDLL "agixl97.dll","poke_dato", aAlfa;
               aAlfa = aColumna + nLinea2 + ",0" ; |ALFACALLDLL "agixl97.dll","poke_dato", aAlfa;
               aAlfa = aColumna + nLinea3 + ",0 %" ; |ALFACALLDLL "agixl97.dll","poke_dato", aAlfa;
               aAlfa = aColumna + nLinea4 + ",0 %" ; |ALFACALLDLL "agixl97.dll","poke_dato", aAlfa;
         |FINSI;
     |SIGUE;
     nLinea = nLinea + 4;
|FPRC;

|PROCESO SumaFila;
     aColumna = "E";
     nResul = 0;
     |PARA nPara = 1; |SI nPara [ #0#3; |HACIENDO nPara = nPara + 1;
          nCodAscii = &aColumna;
          nCodAscii = nCodAscii + 1;
          aColumna = &nCodAscii;

          aAlfa = aColumna + nLaLinea; |ALFACALLDLL "agixl97.dll", "peek_dato", aAlfa; nNum = aAlfa;
          nResul = nResul + nNum;
     |SIGUE;
     aAlfa = "R" + nLaLinea + "," + nResul; |ALFACALLDLL "agixl97.dll","poke_dato", aAlfa;
|FPRC;

|PROCESO Impresion;
     nEmpre = #100#0;
     aArea = #100#2;     |QBF aArea;
     aSeccion = #100#4;  |QBF aSeccion;

     |SI nEmpre = nEmpreAnt; |Y aArea = aAreaAnt; |Y aSeccion = aSeccionAnt;    |ACABA;   |FINSI;

     nLinea = nLinea + 1;

     ||Empresa
     ||aAlfa = "B" + nLinea + "," + #100#1; |ALFACALLDLL "agixl97.dll","poke_dato", aAlfa;
     ||Area
     aAlfa = "C" + nLinea + "," + #100#3; |ALFACALLDLL "agixl97.dll","poke_dato", aAlfa;
     ||Seccion
     aAlfa = "D" + nLinea + "," + #100#5; |ALFACALLDLL "agixl97.dll","poke_dato", aAlfa;


     |HAZ Datos;
     nLinea1 = nLinea - 4;
     nLaLinea = nLinea1;
     |HAZ SumaFila;

     nLinea2 = nLinea - 3;
     nLaLinea = nLinea2;
     |HAZ SumaFila;

     nLinea3 = nLinea - 2;
     nLaLinea = nLinea3;
     |HAZ SumaFila;

     aAlfa = "R" + nLinea1; |ALFACALLDLL "agixl97.dll", "peek_dato", aAlfa; nNum = aAlfa;
     nResul1 = nNum;
     aAlfa = "R" + nLinea2; |ALFACALLDLL "agixl97.dll", "peek_dato", aAlfa; nNum = aAlfa;
     nResul2 = nNum;
     aAlfa = "R" + nLinea3; |ALFACALLDLL "agixl97.dll", "peek_dato", aAlfa; nNum = aAlfa;
     nResul3 = nNum;

     nNum = 100 * nResul2 / nResul1;
     nNum = nNum ! 2;
     aAlfa = nNum + " %";
     nLinea4 = nLinea - 1;
     aAlfa = "R" + nLinea4 + "," + aAlfa; |ALFACALLDLL "agixl97.dll","poke_dato", aAlfa;

     nNum = 100 * nResul3 / nResul1;
     nNum = nNum ! 2;
     aAlfa = nNum + " %";
     aAlfa = "R" + nLinea + "," + aAlfa; |ALFACALLDLL "agixl97.dll","poke_dato", aAlfa;

     nLinea = nLinea + 1;

     nEmpreAnt = #100#0;
     aAreaAnt = #100#2;       |QBF aAreaAnt;
     aSeccionAnt = #100#4;    |QBF aSeccionAnt;
|FPRC;

|DEFBUCLE Impresion;
     #100#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, Impresion;
|FIN;

|PROCESO ElInforme;
     |DBASE aDirBase;    |QBF aDirBase;
     aAlfa = aDirBase + "def/syke0010.mas";
     |CARGA_DEF aAlfa, ref0001@;
     |SI FSalida ! 0;    |ACABA;   |FINSI;
     aAlfa1 = Usuario; |QBT aAlfa1;    aAlfa1 = ("s" + aAlfa1) % 108;
     |NOME_DAT #900 aAlfa1;

     |ABRE #100;
     |CARGADLL "agixl97.dll";
     |SI FSalida < 0;
          |MENAV "0000No se puede usar la DLL de Excel.";
          |ACABA;
     |FINSI;

     |DBASE aBase;  |QBF aBase;
     aOrigen = aBase + "documentos/attrition_template.xls";
     aDestinoTrab = "C:\DOCUMENTOS";         |RMKDIR aDestino;
     aDestino = aDestinoTrab + "\trabajo.xls";

     aIPRemoto = "";
     |IP_REMOTO aIPRemoto;  |QBT aIPRemoto;
     |SI aIPRemoto ! "";
          |ENVIA_FICHERO aOrigen, aDestino;
     |SINO;
          |COPIA_FICHERO aOrigen, aDestino;
     |FINSI;

     |ALFACALLDLL "agixl97.dll", "carga_book", aDestino;
     |ALFACALLDLL "agixl97.dll", "selecciona_hoja", "ATTRITION TEMPLATE";

     nLinea = 3;
     ||HAZ Cabecera;
     aAlfa = "C2,Attrition - " + #0#2; |ALFACALLDLL "agixl97.dll","poke_dato", aAlfa;

     |ABRE #900;
     |BUCLE Impresion;
     |CIERRA #900;

     ||ABRE #200;
     ||CONSULTA_CLAVE #1#200;
     ||CIERRA #200;


     |DESCARGA_DEF #900;
     aAlfa   = aDestinoTrab + "\attrition_template.xls";
     |RFBORRA aAlfa;

     |ALFACALLDLL "agixl97.dll", "fichero_destino", aAlfa;
     aAlfa = aResultado;

     |ALFACALLDLL "agixl97.dll", "fin_book", "salva";
     |DESCARGADLL "agixl97.dll";

     aDestino   = aDestinoTrab + "\trabajo.xls";
     |RFBORRA aDestino;

     aVersion1 = "Excel.Sheet";
     |ESPECIFICA "VERSION_PROGRAMA", aVersion;
     |HAZ ExtraePath;
     |SI aPath = "NO LOCALIZADO";
         |MENAV "0000 No puedo encontrar automaticamente la Excel";
         |ACABA;
     |FINSI;

     |QBF aPath;

     || aAlfa  = "cmd " + &34 + "/c START /WAIT " + aPath + " " + aAlfa + &34;
     aAlfa  = "cmd " + &34 + "/c START /WAIT " + aResultado + &34;
     |RSYSTEM aAlfa;

     |CIERRA #100;
|FPRC;

|PROCESO ExtraePath;
     aPath     = aVersion1;
     aLongitud = aPath % 0;
     nLongitud = aLongitud;
     |PARA nPos = 1; |SI nPos [ nLongitud; |HACIENDO nPos = nPos + 1;
           nCaracter = (nPos * 100) + 1;
           aCaracter = aPath % nCaracter;
           |SI aCaracter = ",";
               nPos = nLongitud - nPos;
               nPos = nPos + 100;
               nPos = (-1) * nPos;
               aPath = aPath % nPos;
               |SAL;
          |FINSI;
     |SIGUE;

     aPath = aPath - "[";
     aPath = aPath - "]";
     |QBF aPath;
|FPRC;

|PROGRAMA;
     |CLS;
     |PINPA #0#0;
     |PINDA #0#0;
     |ENDATOS #1#0;
     |SI FSalida = 0;
          |HAZ ElProceso;
          |HAZ ElInforme;
     |FINSI;
|FPRO;
