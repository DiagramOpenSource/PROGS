|FICHEROS;
     silcon18 #0;
|FIN;

|VARIABLES;
     aParam = "";
     aAlfa = "";
     aLimiteInf = "";
     aLimiteSup = "";
     swHayAlgo = 0;
     &eaVengoDe = "";
     &nEmpExt = 0;
     &nTraExt = 0;
     &fFecExt = @;
     &aAccExt = "";
     swCambia = 0;
     swHanTocado = 0;
     swHanTocado2 = 0;
     nOpcion = 0;
     &nContador = 0;
     &swParteOnlineOK = 0;
|FIN;

|PROCESO Pantalla;
     |SI aParam = "PEN";
          aAlfa = "PARTES DE ALTA/BAJA POR I.T.: MOVIMIENTOS PENDIENTES";
     |FINSI;
     |SI aParam = "ENV";
          aAlfa = " PARTES DE ALTA/BAJA POR I.T.: MOVIMIENTOS ENVIADOS ";
     |FINSI;
     |ATRI R;
     |PINTA 0514, aAlfa;
     |ATRI 0;
     |SI eaVengoDe = ""; |Y aParam = "PEN";
          |MENSA "     <F5> Consultar/Modificar/Enviar Online este parte; <F6> Borrar el Parte";
     |FINSI;
     |SI eaVengoDe = ""; |Y aParam = "ENV";
          |MENSA "     Pulse F5 para dejar el movimiento como PENDIENTE";
     |FINSI;
     |SI eaVengoDe = "silcon19";
          |MENSA "     Pulse F5 para seleccionar movimientos a enviar";
     |FINSI;
|FPRC;

|PROCESO Borra;
     |SI nOpcion = 0;
         |BORRA 020#silcon18.a;
     |SINO;
         #silcon18#21 = " ";
         |GRABA 020#silcon18.a;
     |FINSI;
|FPRC;

|DEFBUCLE Borra;
     #silcon18#1;
     21;
     "-", 000001, "*";
     "-", 999999, "*";
     be;
     NULL, Borra;
|FIN;

|PROCESO ElBorrador;
     aAlfa = "     Esta seguro que desea eliminar los registros pendientes ";
     aAlfa = aAlfa + "seleccionados?"
     |CONFI aAlfa;
     nOpcion = FSalida;
     |BUCLE Borra;
|FPRC;

|PROCESO Actualiza4;
     |SI swCambia = 1;
          #silcon18#18 = "";
          #silcon18#19 = "";
          #silcon18#20 = #silcon18#21;
          #silcon18#20 = "-";
          #silcon18#21 = " ";
          #silcon18#31 = 0;
          |GRABA 020#silcon18.a;
     |SINO;
          #silcon18#21 = #silcon18#20;
          |GRABA 020#silcon18.a;
     |FINSI;
|FPRC;

|DEFBUCLE Actualiza4;
     #silcon18#1;
     21;
     "*", 000001, "*";
     "*", 999999, "*";
     be;
     NULL, Actualiza4;
|FIN;

|PROCESO Actualiza3;
     aAlfa = "     Desea dejar los movimientos seleccionados como PENDIENTES?";
     |CONFI aAlfa;
     |SI FSalida = 0;
          swCambia = 1;
     |FINSI;
     |BUCLE Actualiza4;
|FPRC;

|PROCESO Actualiza2;
     #silcon18#20 = #silcon18#21;
     |GRABA 020#silcon18.a;
|FPRC;

|DEFBUCLE Actualiza2;
     #silcon18#1;
     21;
     "-", 000001, "*";
     "-", 999999, "*";
     be;
     NULL, Actualiza2;
|FIN;

|PROCESO Actualiza;
/*
     aAlfa = "     Continuar con la emision de los partes seleccionados?";
     |CONFI aAlfa;
     |SI FSalida = 0;
          |BUCLE Actualiza2;
     |FINSI;
*/
|FPRC;

|PROCESO marca; |TIPO 0;
     ||SELECT #1#silcon18;
     |SI FSalida = 13; |O FSalida = 14; |O FSalida = 0;
          swHanTocado = 1;
     |FINSI;
     |SI FSalida = 13; |Y eaVengoDe = ""; |Y aParam = "PEN";
          nEmpExt = #silcon18#0;
          nTraExt = #silcon18#1;
          fFecExt = #silcon18#7;
          aAccExt = #silcon18#3;
          |SUB_EJECUTA "silcon17;MODIF";
          |SI swParteOnlineOK = 1;
               #silcon18#20 = "*";
          |FINSI;
          |PONCONTROL 23, "SI";
          |PTEC 815;
     |FINSI;
     |SI FSalida = 14; |Y eaVengoDe = ""; |Y aParam = "PEN";
          |SI #silcon18#21 = "*";
               swHanTocado2 = swHanTocado2 - 1;
               #silcon18#21 = " ";
          |SINO;
               swHanTocado2 = swHanTocado2 + 1;
               #silcon18#21 = "*";
          |FINSI;
          |GRABA 020#silcon18.a;

/*
          aAlfa = "     Esta seguro de que desea borrar el parte asociado ";
          aAlfa = aAlfa + "a este movimiento?";
          |CONFI aAlfa;
          |SI FSalida = 0;
               |BORRA 101#silcon18.a;
               |PTEC 806;

               ||LEE 000#silcon18.a;
               ||SI FSalida = 111;
                    ||PTEC 806;
               ||FINSI;
          |FINSI;
          ||PONCONTROL 23, "si";
*/
     |FINSI;

     |SI eaVengoDe = ""; |Y aParam = "ENV";
          |SI FSalida = 13; |O FSalida = 0;
               |SI #silcon18#21 = "*";
                    #silcon18#21 = " ";
               |SINO;
                    #silcon18#21 = "*";
               |FINSI;
               |GRABA 020#silcon18.a;
          |FINSI;
     |FINSI;

     |SI eaVengoDe = "silcon19";
          |SI FSalida = 13; |O FSalida = 0;
               |SI #silcon18#21 = "*";
                    #silcon18#21 = " ";
               |SINO;
                    #silcon18#21 = "*";
               |FINSI;
               |GRABA 020#silcon18.a;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO BuscaRegistros;
     swHayAlgo = 1;
|FPRC;

|DEFBUCLE BuscaRegistros;
     #silcon18#1;
     ;
     aLimiteInf, 000001;
     aLimiteSup, 999999;
     ;
     NULL, BuscaRegistros;
|FIN;

|PROCESO inferior;
     #silcon18#2 = 000001;
     #silcon18#20 = aLimiteInf;
|FPRC;

|PROCESO superior;
     #silcon18#2 = 999999;
     #silcon18#20 = aLimiteSup;
|FPRC;

|PROGRAMA;
     aParam = PARAMETRO;
     |QBF aParam;
     |SI aParam = "PEN";
          aLimiteInf = "-";
          aLimiteSup = "-";
     |SINO;
          aLimiteInf = "*";
          aLimiteSup = "*";
     |FINSI;
     swHayAlgo = 0;
     |BUCLE BuscaRegistros;
     |SI swHayAlgo = 1;
          |HAZ Pantalla;
          |ENTLINEAL #1#silcon18, -1, 4, 19, 1, inferior, superior;
          |SI swHanTocado ! 0;
               |SI eaVengoDe = "silcon19";
                    |HAZ Actualiza;
               |FINSI;
               |SI eaVengoDe = ""; |Y aParam = "ENV";
                    |HAZ Actualiza3;
               |FINSI;
               |SI eaVengoDe = ""; |Y aParam = "PEN"; |Y swHanTocado2 > 0;
                    |HAZ ElBorrador;
               |FINSI;
          |FINSI;
     |SINO;
          |SI aParam = "PEN";
               aAlfa = "pendientes";
          |SINO;
               aAlfa = "enviados";
          |FINSI;
          aAlfa = "    No se han encontrado movimientos " + aAlfa;
          |MENAV aAlfa;
     |FINSI;
|FPRO;
