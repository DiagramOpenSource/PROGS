|FICHEROS;
     laborn11 #0;
     laborn01;
     laborn02;
     laborn07;
     labempre;
     labmunic;

     nomconca;
     nomcateg;
     nomregim;
     labtraba;
     silcon15;

     labort01,MANTE;
     labort02,MANTE;
     labort03,MANTE;
     labore01;

     laborn90;
     labcontr;

     nompmail;

     agim0003@;          || fichero NIF basica
     dsam0103@;          || fichero NIF PI
     agidanif@;          || fichero datos personales
|FIN;

|VARIABLES;
     aHora = "";
     &eaVarDni  = "";
     &enCalcCif = 0;

     &eaDirDest  = "";
     &eaAsunto   = "";
     &eaTexto    = "";

     nume1 = 0;
     nume2 = 0;
     nume3 = 0;
     nume4 = 0;
     alfa1 = "";
     alfa2 = "";
     alfa3 = "";
     alfa4 = "";
     corta = 0;
     sitio = 0;
     jg = 0;

     nCampo = 0;
     nCampo1 = 0;
     nCampo2 = 0;
     nCampo3 = 0;

     aAlfa = "";
     nNume = 0;

     &eaUsuario = "";
     &eaFecha = @;
     &eaHora = "";
     &eaEstado = "";
     &aParam = "";

     aAlfa1 = "";
     aAlfa2 = "";
     aAlfa3 = "";
     aFechaNac = "";
     fFechaHoy = @;

     aDuracion = "";
     aDiaAlta = "";
     aMesAlta = "";
     aAnyAlta = "";
     aDiaBaja = "";
     aMesBaja = "";
     aAnyBaja = "";

     nDiaAlta = 0;
     nMesAlta = 0;
     nAnyAlta = 0;
     nDiaBaja = 0;
     nMesBaja = 0;
     nAnyBaja = 0;

     nAnys = 0;
     nMeses = 0;
     nDias = 0;
     aDias = "";
     aMeses = "";
     aAnys = "";
     aFechaBaja = "";

     xmesnume = 0;
     xtopemes = 0;
     xmod = 0;
     xanynume = 0;
     fech1 = @;
     fech2 = @;
     aFecha1 = "";
     aFecha2 = "";
     aFechaIni = "";
     fFechaFin = @;
     aFechaFin = "";
     fFecha = @;
     aFecha = "";
     nConvenio = 0;

     dia1 = "";
     mes1 = "";
     any1 = "";
     dia = 0;
     mes = 0;
     any = 0;
     tope = 0;
     sw = 0;
     fec = "";
     mod = 0;

     &eaEmpresa = "";
     &eaTrabajador = "";
     &eaAccion = "";
     &aCadena = "";
     &eaNom = "";
     &eaApel1 = "";
     &eaApel2 = "";

     swPI = 0;
     aBase = "";
     aMas = "";
     swDatosPer = 0;
     swDatosFam = 0;
     nAscendientes = 0;
     nDescendientes = 0;
     swPorDNI = 0;
     aMensaje = "";
     nEmpresas = 0;
     nCodEmp = 0;
     aNomEmp = "";

     enBoton1 = 0;
     enBoton2 = 0;
     &enBoton3 = 0;
     enBoton4 = 0;
     &enEmpresa = 0;
     FEstado = 0;
     &swBoton3 = 0;
     aAlfat01 = "";
     &swCambioManReg = 0;
     &eaQueEs            = "";
|FIN;

|PROCESO Stop;
     |SI FSalida = 2;
          |ACABA;
     |FINSI;
     aAlfa = #0Campo;
     aAlfat01 = #labort01#Campo#;
     nNume = #0Campo;
     |QBF aAlfa;
     aMensaje = "";
     |SI aAlfa = ""; |Y Campo = 9;
          aMensaje = "    ATENCION: Es obligatorio rellenar el NIF del trabajador";
     |FINSI;
     |SI aAlfa = ""; |Y Campo = 6;
          aMensaje = "    ATENCION: Es obligatorio rellenar el nombre del trabajador";
     |FINSI;
     |SI aAlfa = ""; |Y Campo = 7;
          aMensaje = "    ATENCION: Es obligatorio rellenar el primer apellido del trabajador";
     |FINSI;
     |SI aAlfa = ""; |Y Campo = 11;
          aMensaje = "    ATENCION: Es obligatorio rellenar el número de afiliación del trabajador";
     |FINSI;
     |SI aAlfa = ""; |Y Campo = 12;
          aMensaje = "    ATENCION: Es obligatorio rellenar el campo 'sexo' del trabajador";
     |FINSI;
     |SI aAlfa = ""; |Y Campo = 16;
          aMensaje = "    ATENCION: Es obligatorio rellenar la localidad de nacimiento del trabajador";
     |FINSI;
     |SI aAlfa = "1900"; |Y Campo = 15;
          |SI #laborn11#13 = 1; |Y #laborn11#14 = 1;
               aMensaje = "    ATENCION: Es obligatorio rellenar la fecha de nacimiento del trabajador";
          |FINSI;
     |FINSI;
     |SI nNume = 0; |Y Campo = 216;
          aMensaje = "    ATENCION: Es obligatorio rellenar el código de provincia";
     |FINSI;
     |SI Campo = 26;
          |SI aAlfat01 = ""; |Y #laborn11#25 = 2;
               aMensaje = "    ATENCION: Es obligatorio rellenar el NIF del cónyuge ";
               aMensaje = aMensaje + "para la situación familiar tipo 2";
          |FINSI;
     |FINSI;
     |SI Campo = 28; |O Campo = 29; |O Campo = 30;
          aAlfa1 = #laborn01#1 % 105;
          |SI #laborn01#1 ! "WEB       ";
          |Y aAlfa1 ! "NUBEA";
               |SI aAlfat01 = ""; |Y #labort01#25 = 2;
                    aMensaje = "    ATENCION: Es obligatorio rellenar todos los datos del cónyuge ";
                    aMensaje = aMensaje + "para la situación familiar tipo 2";
               |FINSI;
          |FINSI;
     |FINSI;
     |SI aAlfat01 = "1900"; |Y Campo = 33; |Y #laborn11#25 = 2;
          |SI #labort01#31 = 1; |Y #labort01#32 = 1;
               aMensaje = "    ATENCION: Es obligatorio rellenar todos los datos del cónyuge ";
               aMensaje = aMensaje + "para la situación familiar tipo 2";
          |FINSI;
     |FINSI;

     |SI aMensaje ! "";
          |MENAV aMensaje;
          |ERROR;
     |FINSI;
|FPRC;

|PROCESO agidanif;
     |SI #agidanif@#2 = "CONYUGE     ";
          #laborn11#26 = #agidanif@#8;
          #laborn11#28 = #agidanif@#11;
          #laborn11#29 = #agidanif@#9;
          #laborn11#30 = #agidanif@#10;

          aFecha = #agidanif@#13;
          aAlfa = aFecha % 102;
          #laborn11#31 = aAlfa;
          aAlfa = aFecha % 402;
          #laborn11#32 = aAlfa;
          aAlfa = aFecha % -104;
          #laborn11#33 = aAlfa;
     |FINSI;
     |SI #agidanif@#2 = "DESCENDIENTE";
          nDescendientes = nDescendientes + 1;
          nCampo = ((nDescendientes - 1) * 8) + 113;
          #0nCampo = #agidanif@#11;
          nCampo = nCampo + 1; #0nCampo = #agidanif@#9;
          nCampo = nCampo + 1; #0nCampo = #agidanif@#10;
          aFecha = #agidanif@#13;
          aAlfa = aFecha % -104;
          nCampo = nCampo + 1; #0nCampo = aAlfa;
          nCampo = nCampo + 1; #0nCampo = #agidanif@#41;
          |SI #agidanif@#3 = " "; |O #agidanif@#3 = "3"; |O #agidanif@#3 = "4";
               aAlfa  = "N";
          |SINO;
               aAlfa = "S";
          |FINSI;
          nCampo = nCampo + 1; #0nCampo = aAlfa;
          nCampo = nCampo + 1; #0nCampo = #agidanif@#17;
          nCampo = nCampo + 1; #0nCampo = #agidanif@#18;
     |FINSI;
     |SI #agidanif@#2 = "ASCENDIENTE ";
          nAscendientes = nAscendientes + 1;
          nCampo = ((nAscendientes - 1) * 7) + 42;
          #0nCampo = #agidanif@#11;
          nCampo = nCampo + 1; #0nCampo = #agidanif@#9;
          nCampo = nCampo + 1; #0nCampo = #agidanif@#10;
          aFecha = #agidanif@#13;
          aAlfa = aFecha % -104;
          nCampo = nCampo + 1; #0nCampo = aAlfa;
          nCampo = nCampo + 1; #0nCampo = #agidanif@#5;
          nCampo = nCampo + 1; #0nCampo = #agidanif@#17;
          nCampo = nCampo + 1; #0nCampo = #agidanif@#18;
     |FINSI;
|FPRC;

|DEFBUCLE agidanif;
     #agidanif@#1002;
     ;
     #laborn11#9, 01;
     #laborn11#9, 99;
     ;
     NULL, agidanif;
|FIN;

|PROCESO Integrada;
     swPI = 0;

     |DBASS aBase;
     |QBF aBase;
     aMas  = aBase + "integral/def/dsam0103.mas";
     |CARGA_DEF aMas, dsam0103@;
     |SI FSalida = 0;
          swPI = 1
     |SINO;
          swPI = 0;
     |FINSI;
     |DESCARGA_DEF #dsam0103@;
|FPRC;

|PROCESO CargaDatosPer;
     swDatosPer = 0;
     swDatosFam = 0;
     nDescendientes = 0;
     nAscendientes = 0;

     |SI swPI = 1;
          aMas  = aBase + "integral/def/dsam0103.mas";
          |CARGA_DEF aMas, dsam0103@;

          |ABRE #dsam0103@;
          #dsam0103@#0 = #laborn11#9;
          |LEE 000#dsam0103@.=;
          |SI FSalida = 0;
               swDatosPer = 1;
          |FINSI;
     |SINO;
          aMas  = aBase + "basica/def/agim0003.mas";
          |CARGA_DEF aMas, agim0003@;

          |ABRE #agim0003@;
          #agim0003@#0 = #laborn11#9;
          |LEE 000#agim0003@.=;
          |SI FSalida = 0;
               swDatosPer = 1;
          |FINSI;
     |FINSI;

     aMas  = aBase + "basica/def/agidanif.mas";
     |CARGA_DEF aMas, agidanif@;
     |SI FSalida = 0;
          |ABRE #agidanif@;
          #agidanif@#0 = #laborn11#9;
          #agidanif@#1 = 01;
          |LEE 000#agidanif@.=;
          swDatosFam = 1;
     |FINSI;
|FPRC;

|PROCESO DescargaDatosPer;
     |CIERRA #agidanif@;
     |DESCARGA_DEF #agidanif@;

     |SI swPI = 1;
          aMas = aBase + "integral/def/dsam0103.mas";
          |CIERRA #dsam0103@;
          |DESCARGA_DEF #dsam0103@;
     |SINO;
          aMas = aBase + "basica/def/agim0003.mas";
          |CIERRA #agim0003@;
          |DESCARGA_DEF #agim0003@;
     |FINSI;
|FPRC;

|PROCESO BuscaDNI;
     swPorDNI = 1;

     aCadena = #labtraba#2;
     |HAZ SeparaNom_plb;
     #laborn11#6 = eaNom;
     #laborn11#7 = eaApel1;
     #laborn11#8 = eaApel2;

     #laborn11#11 = #labtraba#75;
     #laborn11#12 = #labtraba#252;
     aFecha = #labtraba#11;
     aAlfa = aFecha % 102;
     #laborn11#13 = aAlfa;
     aAlfa = aFecha % 402;
     #laborn11#14 = aAlfa;
     aAlfa = aFecha % -104;
     #laborn11#15 = aAlfa;
     #laborn11#16 = #labtraba#76;
|FPRC;

|PROCESO DatosPersonales;
     |HAZ CargaDatosPer;

     |SI swPI = 1;
          #laborn11#223 = #dsam0103@#9;
          #laborn11#17 = #dsam0103@#2;
          #laborn11#224 = #dsam0103@#3;
          #laborn11#226 = #dsam0103@#10;
          #laborn11#227 = #dsam0103@#11;
          #laborn11#225 = #dsam0103@#16;
          #laborn11#216 = #dsam0103@#4;
          #laborn11#217 = #dsam0103@#5;
          #laborn11#19 = #dsam0103@#6;
          #laborn11#20 = #dsam0103@#7;
          #laborn11#221 = #dsam0103@#8;

     |SINO;
          #laborn11#223 = #agim0003@#9;
          #laborn11#17 = #agim0003@#2;
          #laborn11#224 = #agim0003@#3;
          #laborn11#226 = #agim0003@#10;
          #laborn11#227 = #agim0003@#11;
          #laborn11#225 = #agim0003@#16;
          #laborn11#216 = #agim0003@#4;
          #laborn11#217 = #agim0003@#5;
          #laborn11#19 = #agim0003@#6;
          #laborn11#20 = #agim0003@#7;
          #laborn11#221 = #agim0003@#8;
     |FINSI;

     aAlfa = #laborn11#6;
     |QBF aAlfa;
     |SI aAlfa = "";     || no ha encontrado el nombre en la ficha del trab.
          |SI swPI = 1;
               aCadena = #dsam0103@#1;
          |SINO;
               aCadena = #agim0003@#1;
          |FINSI;
          |QBF aCadena;
          |SI aCadena ! "";
               |HAZ SeparaNom_plb;
               #laborn11#6 = eaNom;
               #laborn11#7 = eaApel1;
               #laborn11#8 = eaApel2;
          |FINSI;
     |FINSI;

     |SI swDatosFam = 1;
          #agidanif@#0 = #laborn11#9;
          #agidanif@#1 = 01;
          |LEE 000#agidanif@.=;
          |SI FSalida = 0;
               #laborn11#25 = #agidanif@#15;
               #laborn11#34 = #agidanif@#16;
               #laborn11#23 = #agidanif@#17;
               |SI #agidanif@#17 > 0;
                    #laborn11#22 = "S";
               |FINSI;
               #laborn11#24 = #agidanif@#18;
               #laborn11#35 = #agidanif@#38;
               #laborn11#36 = #agidanif@#39;

               aFecha = #agidanif@#40;
               aAlfa = aFecha % 102;
               #laborn11#37 = aAlfa;
               aAlfa = aFecha % 402;
               #laborn11#38 = aAlfa;
               aAlfa = aFecha % -104;
               #laborn11#39 = aAlfa;

               |SI swPorDNI = 0;   || no se ha encontrado la ficha del trab.
                    aFecha = #agidanif@#13;
                    aAlfa = aFecha % 102;
                    #laborn11#13 = aAlfa;
                    aAlfa = aFecha % 402;
                    #laborn11#14 = aAlfa;
                    aAlfa = aFecha % -104;
                    #laborn11#15 = aAlfa;
               |FINSI;
          |FINSI;

          |BUCLE agidanif;
     |FINSI;

     |PINDA #0#laborn11;

     |HAZ DescargaDatosPer;
|FPRC;

|DEFBUCLE BuscaDNI;
     #labtraba#2;
     ;
     #laborn11#9;
     #laborn11#9;
     ;
     NULL, BuscaDNI;
|FIN;

|PROCESO DNI;
     |SI aParam = "MODIF";
          |ACABA;
     |FINSI;

     |SI #0Campo ! Contenido;
          |PDEFECTO #0, 6, 9;
          |PDEFECTO #0, 11, 209;
          |PDEFECTO #0, 211, 234;
          |PINDA #0#laborn11;

          |HAZ Integrada;
          |BUCLE BuscaDNI;
          |HAZ DatosPersonales;
          |PINDA #0#0;

          |PARA nCampo = 22; |SI nCampo [ 192; |HACIENDO nCampo = nCampo + 1;
               #labort01#nCampo# =  #0nCampo;
          |SIGUE;
     |FINSI;
|FPRC;

|PROCESO Campos;
     /*
     || campos para la ficha del trabajador

     del 6 al 21


     || campos para la datos personales de IRPF

     del 22 al 192


     || campos para el contrato

     del 193 al 199
     del 203 al 208

     */
|FPRC;

|PROCESO FechaDef; |TIPO 7
     |SI aParam ! ""; |ACABA; |FINSI;
     fFecha = ~;
     aFecha = fFecha;
     alfa1 = aFecha % 102;
     alfa2 = aFecha % 402;
     alfa3 = aFecha % -104;

     nCampo = 193;
     #0nCampo = alfa1;
     nCampo = nCampo + 1;
     #0nCampo = alfa2;
     nCampo = nCampo + 1;
     #0nCampo = alfa3;
     nCampo = 196;
     #0nCampo = alfa1;
     nCampo = nCampo + 1;
     #0nCampo = alfa2;
     nCampo = nCampo + 1;
     #0nCampo = alfa3;
|FPRC;

|PROCESO Restaura;
     |POPV;
|FPRC;

|PROCESO diaanter;                          || RUTINA DEL CALCULO duracion
     |SI xmesnume = 1;
          xtopemes = 31;
     |FINSI;
     |SI xmesnume = 2;
             xmod = xanynume $ 4;
         |SI xmod = 0;
                 xtopemes = 29;
         |SINO;
                 xtopemes = 28;
         |FINSI;
     |FINSI;
     |SI xmesnume = 3;   xtopemes = 31; |FINSI;
     |SI xmesnume = 4;   xtopemes = 30; |FINSI;
     |SI xmesnume = 5;   xtopemes = 31; |FINSI;
     |SI xmesnume = 6;   xtopemes = 30; |FINSI;
     |SI xmesnume = 7;   xtopemes = 31; |FINSI;
     |SI xmesnume = 8;   xtopemes = 31; |FINSI;
     |SI xmesnume = 9;   xtopemes = 30; |FINSI;
     |SI xmesnume = 10;  xtopemes = 31; |FINSI;
     |SI xmesnume = 11;  xtopemes = 30; |FINSI;
     |SI xmesnume = 12;  xtopemes = 31; |FINSI;
|FPRC;

|PROCESO verifech; |TIPO 0;
     |SI FSalida = 2; |ACABA;   |FINSI;

     |SI Campo = 198; |Y #laborn11#196 = 0;
          |ACABA;
     |FINSI;

     nCampo1 = Campo - 2;
     nCampo2 = Campo - 1;
     nCampo3 = Campo;
     aAlfa1 = #0nCampo1; |QBF aAlfa1; aAlfa1 = ("00" + aAlfa1) % -102;
     aAlfa2 = #0nCampo2; |QBF aAlfa2; aAlfa2 = ("00" + aAlfa2) % -102;
     aAlfa3 = #0nCampo3;
     fec = aAlfa1 + "." + aAlfa2 + "." + aAlfa3;
     sw = 0;

     |SI fec ! "..";
          dia1 = fec % A102;
          mes1 = fec % A402;
          any1 = fec % A704;
          dia = dia1; mes = mes1; any = any1;
          |SI dia ! 0; |Y mes ! 0; |Y any ! 0;
               |SI mes [ 12; |Y mes ] 1;
                    |SI mes = 1; |O mes = 3; |O mes = 5; |O mes = 7; |O mes = 8; |O mes = 10; |O mes = 12;
                         tope = 31;
                    |SINO;
                         |SI mes = 2;
                              mod = any $ 4;
                              |SI mod = 0;
                                   tope = 29;
                              |SINO;
                                   tope = 28;
                              |FINSI;
                         |SINO;
                              tope = 30;
                         |FINSI;
                    |FINSI;
                    |SI dia [ tope;
                         sw = 1;
                    |FINSI;
               |FINSI;
          |FINSI;
     |SINO;
          sw = 1;
     |FINSI;
     |SI sw = 0;
          |MENAV "    ATENCION: la fecha introducida no es válida";;
          |ERROR;
     |FINSI;
|FPRC;

|PROCESO Duracion; |TIPO 7;
     fech1 = aFecha1;
     fech2 = aFecha2;

     aDuracion = "";

     aDiaAlta = (aFecha1 % 102);
     aMesAlta = (aFecha1 % 402);
     aAnyAlta = (aFecha1 % -104);
     aDiaBaja = (aFecha2 % 102);
     aMesBaja = (aFecha2 % 402);
     aAnyBaja = (aFecha2 % -104);

     nDiaAlta = aDiaAlta;
     nMesAlta = aMesAlta;
     nAnyAlta = aAnyAlta;
     nDiaBaja = aDiaBaja;
     nMesBaja = aMesBaja;
     nAnyBaja = aAnyBaja;

     nAnys = nAnyBaja - nAnyAlta;

     nMeses = nMesBaja - nMesAlta;
     |SI nMeses [ 0;
          |SI nAnys ! 0;
               nAnys = nAnys - 1;
               nMeses = 12 + nMeses;
          |FINSI;
     |FINSI;

     nDias = nDiaBaja - nDiaAlta + 1;
     nume1 = nDiaAlta - 1;
     xmesnume = nMesBaja;
     xanynume = nAnyBaja;
     |HAZ diaanter;
     |SI nDiaBaja = nume1;
          nDias = 0;
     |SINO;
          |SI nDiaAlta = 1; |Y nDiaBaja = xtopemes;
               nMeses = nMeses + 1;
               nDias = 0;
          |FINSI;
     |FINSI;
     |SI nDias < 0;
          xmesnume = nMesAlta;
          xanynume = nAnyAlta;
          |HAZ diaanter;
          nDias = xtopemes - nDiaAlta + 1;
          nDias = nDias + nDiaBaja;
          nMeses = nMeses - 1;
     |FINSI;
     |SI nMeses = 12;
          nAnys = nAnys + 1;
          nMeses = 0;
     |FINSI;
|FPRC;

|PROCESO FechaNac;
     aFechaNac = "";
     aAlfa = #laborn11#13; aAlfa = ("00" + aAlfa) % -102;
     aFechaNac = aFechaNac + aAlfa;
     aAlfa = #laborn11#14; aAlfa = ("00" + aAlfa) % -102;
     aFechaNac = aFechaNac + "." + aAlfa;
     aAlfa = #laborn11#15;
     aFechaNac = aFechaNac + "." + aAlfa;

     fech2 = ~;

     aFecha1 = aFechaNac;
     aFecha2 = fech2;

     fech1 = aFecha1;
     fech2 = ~;

     |SI fech2 < fech1; |Y FSalida ! 2; |Y FSalida ! 4;
          aAlfa = "    ATENCION: La fecha de nacimiento del trabajador es ";
          aAlfa = aAlfa + "superior a la fecha actual.";
          |MENAV aAlfa;
          |ERROR;
     |SINO;
          |HAZ Duracion;
          |SI nAnys > 90; |Y FSalida ! 2; |Y FSalida ! 4;
               |SI #laborn11#13 ! 1; |O #laborn11#14 ! 1; |O #laborn11#15 ! 1900;
                    aAlfa = "    ATENCION: El trabajador introducido tiene más de ";
                    aAlfa = aAlfa + "90 años. Confirme que este dato es correcto.";
                    |MENAV aAlfa;
               |FINSI;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO ConsultaPais; |TIPO 66;
     |HAZ SacaVentana;
|FPRC;

|PROCESO ConsultaCP;
/*
     |SI FSalida = 11;
          aAlfa = #laborn11#9;
          aAlfa = aAlfa % 101;

          |SI aAlfa ! "X"; |Y aAlfa ! "Y"; |Y aAlfa ! "Z";
               || nCampo = Cod. Postal
               || nCampo1 = Nombre Municipio
               || nCampo2 = Codigo Provincia
               || nCampo3 = Nombre Provincia

               |SI Campo = 16;
                    nCampo = 0;
                    nCampo1 = 16;
                    nCampo2 = 0;
                    nCampo3 = 0;
               |FINSI;
               |SI Campo = 18;
                    nCampo = 18;
                    nCampo1 = 19;
                    nCampo2 = 0;
                    nCampo3 = 20;
               |FINSI;

               |ABRE #labmunic;
               |CONSULTA_CLAVE #1#labmunic;
               |SI FSalida = 0;
                    |SI nCampo ! 0;
                         #0nCampo = #labmunic#4;     |PINTA #0nCampo;
                    |FINSI;
                    |SI nCampo1 ! 0;
                         #0nCampo1 = #labmunic#3;     |PINTA #0nCampo1;
                    |FINSI;
                    |SI nCampo2 ! 0;
                         #0nCampo2 = (#labmunic#4 % 102);     |PINTA #0nCampo2;
                    |FINSI;
                    |SI nCampo3 ! 0;
                         #0nCampo3 = #labmunic#2;     |PINTA #0nCampo3;
                    |FINSI;
                    |ACABA;
               |FINSI;
               |CIERRA #labmunic;
          |SINO;
               |HAZ ConsultaPais;
          |FINSI;
     |FINSI;
*/
|FPRC;

|PROCESO CompruebaCP; |TIPO 0;
     || nCampo = Cod. Postal
     || nCampo1 = Nombre Municipio
     || nCampo2 = Codigo Provincia
     || nCampo3 = Nombre Provincia

     |SI Campo = 18;
          nCampo = 18;
          nCampo1 = 19;
          nCampo2 = 0;
          nCampo3 = 20;
     |FINSI;

     |ABRE #labmunic;
     |SELECT #4#labmunic;
     #labmunic#4 = #0Campo;
     |LEE 000#labmunic.=;
     |SI FSalida = 0;
          |SI #0nCampo ! Contenido;
               nCampo = Campo;
               #0nCampo = #labmunic#4;     |PINTA #0nCampo;
               #0nCampo1 = #labmunic#3;     |PINTA #0nCampo1;
               ||#0nCampo2 = (#labmunic#0 % 102);     |PINTA #0nCampo2;
               #0nCampo3 = #labmunic#2;     |PINTA #0nCampo3;
          |FINSI;
     |SINO;
          |SI #0Campo ! "     ";
               nCampo = Campo;
               #0nCampo = "";     |PINTA #0nCampo;
               #0nCampo1 = "";     |PINTA #0nCampo1;
               ||#0nCampo2 = "";     |PINTA #0nCampo2;
               #0nCampo3 = "";     |PINTA #0nCampo3;
               |MENAV "    El Código Postal no existe. Pulse F11 para ver la lista."
               |ERROR;
          |FINSI;
     |FINSI;
     |CIERRA #labmunic;
|FPRC;

|PROCESO DigitoNAFSS; |TIPO 0;
     aAlfa = #0Campo;
     |QBF aAlfa;
     aAlfa = aAlfa % 0;
     nNume = aAlfa;
     aAlfa = "    ATENCION. El número de afiliación a la Seg. Social ";
     aAlfa = aAlfa + "no puede exceder de 12 dígitos."
     |SI nNume > 12;
          |MENAV aAlfa;
          |ERROR;
     |FINSI;

     |SI FSalida = 9;
          alfa1 = #0Campo;
          |QBF alfa1;

          aAlfa = alfa1 % 0;            || si mide mas de 10 digitos y me piden
          nNume = aAlfa;                || que calcule el digito de control
          |SI nNume > 10;               || quito el que me hubieran introducido
               alfa1 = alfa1 % 110;
               #0Campo = alfa1;
          |FINSI;

          alfa2 = alfa1 % 102; alfa3 = alfa1 % -108;
          alfa4 = alfa3 % 101;
          |SI alfa4 = "0";
               alfa3 = alfa3 % 207;
               alfa1 = alfa2 + alfa3;
          |FINSI;
          nume1 = alfa1;
          nume2 = nume1 / 97;
          |HAZ trunca2;
          |SI nume1 ] 0; |Y nume1 [ 99;
               alfa1 = #0Campo;
               |QBF alfa1;
               alfa2 = nume1;
               alfa2 = "00" + alfa2;
               alfa2 = alfa2 % -102;
               alfa1 = alfa1 + alfa2;
               #0Campo = alfa1;
               |PINTA #0Campo;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO trunca2;
     corta = 0;
     alfa1 = nume2;  alfa2 = alfa1 % 0;  nume3 = alfa2;
     |PARA jg = nume3; |SI jg ] 1;|Y corta = 0; |HACIENDO jg = jg - 1;
          sitio = (jg * 100) + 1;
          alfa2 = alfa1 % sitio;
          |SI alfa2 = ".";  corta = jg;  |FINSI;
     |SIGUE;
     |SI corta ! 0;
          sitio = 100 + corta - 1;
          alfa1 = alfa1 % sitio;
     |FINSI;
     nume3 = alfa1;
     nume1 = nume1 - (nume3 * 97);
|FPRC;

|PROCESO LetraNIF;
     eaVarDni  = #0Campo;
     enCalcCif = 1;
     |HAZ CalculoDNI;
     #0Campo = eaVarDni;
     |PINTA #0Campo;
|FPRC;

|PROCESO Email;
     aAlfa = #laborn11#5; |QBF aAlfa;
     eaEmpresa = ("00000" + aAlfa) % -105;
     eaEmpresa = eaEmpresa + " - " + #laborn11#210;
     || aAlfa = #laborn11#6; |QBF aAlfa;
     || eaTrabajador = ("00000" + aAlfa) % -105;
     aAlfa1 = #laborn11#6; |QBF aAlfa1;
     aAlfa = aAlfa1 + " ";
     aAlfa1 = #laborn11#7; |QBF aAlfa1;
     aAlfa = aAlfa + aAlfa1 + " ";
     aAlfa1 = #laborn11#8; |QBF aAlfa1;
     aAlfa = aAlfa + aAlfa1 + " ";
     eaTrabajador = aAlfa;
     eaAccion = "alta";

     |HAZ ConsEmail;
|FPRC;

|PROCESO labempre;
     nEmpresas = nEmpresas + 1;
     nCodEmp = #labempre#0;
     aNomEmp = #labempre#2;
|FPRC;

|DEFBUCLE labempre;
     #labempre#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, labempre;
|FIN;

|PROCESO Empresas; |TIPO 7;
     |ABRE #labempre;
     |BUCLE labempre;
     |CIERRA #labempre;

     |SI nEmpresas = 1;
          || tengo una unica empresa que elegir

          #laborn11#5 = nCodEmp;
          #laborn11#210 = aNomEmp;
          |CAMPO_MODIFICA #laborn11#5, 1, "N";
          |CAMPO_MODIFICA #laborn11#205, 1, "N";
          |PINTA #laborn11#5;
          |PINTA #laborn11#210;
     |FINSI;
|FPRC;

|PROCESO DatosAdicionales;
     |ABRE #labore01;
     #labore01#0 = #laborn01#0;
     #labore01#1 = #laborn01#1;
     #labore01#2 = #laborn01#2;
     #labore01#3 = #laborn01#3;
     #labore01#4 = #laborn01#4;
     |LEE 000#labore01.=;
     FEstado = FSalida;
     |SI FSalida ! 0;
         |DDEFECTO #labore01;
     |FINSI;

     |PUSHV 05012380;
     |PINPA #0#labore01;
     |PINDA #0#labore01;

     enEmpresa = #laborn11#5;
     |SI aParam = "MODIF"; |O aParam = "";
          |ENDATOS #1#labore01;
          |SI FEstado = 0;
               |GRABA 020#labore01.a;
          |SINO;
               |GRABA 020#labore01.n;
          |FINSI;
          |LIBERA #labore01;
     |SINO;
          |PAUSA;
     |FINSI;

          ||PAUSA;

     |CIERRA #labore01;
     |POPV;
|FPRC;

|PROCESO DatosContrato;
     |PUSHV 05012380;
     |PINPA #0#labort02;
     |PINDA #0#labort02;
     enEmpresa = #laborn11#5;
     |SI aParam = "MODIF"; |O aParam = "";
          |HAZ RecogeDatosCont;

          |ENDATOS #1#labort02;
     |SINO;
          |PAUSA;
     |FINSI;

     |SI FSalida = 0;
          |PARA nCampo = 193; |SI nCampo [ 208; |HACIENDO nCampo = nCampo + 1;
               #0nCampo = #labort02#nCampo#;
          |SIGUE;
          |PARA nCampo = 211; |SI nCampo [ 214; |HACIENDO nCampo = nCampo + 1;
               #0nCampo = #labort02#nCampo#;
          |SIGUE;
          #laborn11#218 = #labort02#218;
          |PARA nCampo = 228; |SI nCampo [ 234; |HACIENDO nCampo = nCampo + 1;
               #0nCampo = #labort02#nCampo#;
          |SIGUE;
          |PARA nCampo = 235; |SI nCampo [ 238; |HACIENDO nCampo = nCampo + 1;
               nCampo1 = nCampo;
               nCampo2 = nCampo;
               #0nCampo1 = #labort02#nCampo2#;
          |SIGUE;
     |FINSI;
     |FIN_CONTROL_WINDOWS enBoton3;
     swBoton3 = 0;
     |POPV;
|FPRC;

|PROCESO DatosIRPF;
     |PUSHV 05012380;
     |PINPA #0#labort01;
     |PINDA #0#labort01;
     enEmpresa = #laborn11#5;
     |SI aParam = "MODIF"; |O aParam = "";
          |ENDATOS #1#labort01;
     |SINO;
          |PAUSA;
     |FINSI;
     |SI FSalida = 0;
          |PARA nCampo = 22; |SI nCampo [ 192; |HACIENDO nCampo = nCampo + 1;
               #0nCampo = #labort01#nCampo#;
          |SIGUE;
     |FINSI;
     |POPV;
     |PINDA #0#0;
|FPRC;

|PROCESO Boton4_Fin;
     aAlfa = eaUsuario; |QBF aAlfa;
     aAlfa1 = eaUsuario % 105;
     |SI aAlfa ! "WEB";
     |Y aAlfa1 ! "NUBEA";
          |ACABA;
     |FINSI;

     |FIN_CONTROL_WINDOWS enBoton4;
|FPRC;

|PROCESO Boton4;
     aAlfa = eaUsuario; |QBF aAlfa;
     aAlfa1 = eaUsuario % 105; |QBF aAlfa;
     |SI aAlfa ! "WEB";
     |Y aAlfa1 ! "NUBEA";
          |ACABA;
     |FINSI;

     |CONTROL_SIMPLE 0, "BOTON,F7 - Datos Adicionales", 0965, 0979, DatosAdicionales;
     enBoton4 = FSalida;
     |ESTADO_CONTROL enBoton4, "ENABLE";
|FPRC;

|PROCESO Boton2_Fin;
      |FIN_CONTROL_WINDOWS enBoton2;
|FPRC;

|PROCESO Boton2;
     |CONTROL_SIMPLE 0, "BOTON,F5 - Datos Contrato", 0765, 0779, DatosContrato;
     enBoton2 = FSalida;
     |ESTADO_CONTROL enBoton2, "ENABLE";
|FPRC;

|PROCESO Boton1_Fin;
      |FIN_CONTROL_WINDOWS enBoton1;
|FPRC;

|PROCESO Boton1; |TIPO 8;
     |CONTROL_SIMPLE 0, "BOTON,F6 - Datos IRPF", 0865, 0879, DatosIRPF;
     enBoton1 = FSalida;
     |ESTADO_CONTROL enBoton1, "ENABLE";
|FPRC;

|PROCESO TeclasFuncion;
     |SI FSalida = 13;
          |HAZ DatosContrato;
          |ERROR;
     |FINSI;
     |SI FSalida = 14;
          |HAZ DatosIRPF;
          |ERROR;
     |FINSI;
     |SI FSalida = 15;
          aAlfa = eaUsuario; |QBF aAlfa;
          aAlfa1 = eaUsuario % 105; |QBF aAlfa;
          |SI aAlfa = "WEB";
          |O aAlfa1 = "NUBEA";
               |HAZ DatosAdicionales;
          |FINSI;
          |ERROR;
     |FINSI;
|FPRC;

|PROCESO CamposBajaAuto;
     #laborn02#5 = #laborn01#5;
     #laborn02#6 = #laborn01#6;
     #laborn02#7 = #laborn01#9;

     aAlfa1 = #laborn01#196; |QBF aAlfa1; aAlfa1 = ("00" + aAlfa1) % -102;
     aAlfa2 = #laborn01#197; |QBF aAlfa2; aAlfa2 = ("00" + aAlfa2) % -102;
     aAlfa3 = #laborn01#198;
     aAlfa = aAlfa1 + "." + aAlfa2 + "." + aAlfa3;
     fFecha = aAlfa;
     #laborn02#8 = fFecha;

     #laborn02#9 = #laborn01#239;      || motivo baja
     aAlfa = #laborn02#10; |QBF aAlfa;
     |SI aParam ! "MODIF"; |O aAlfa = "";
          aAlfa1 = #laborn01#242;
          |QBF aAlfa1;
          aAlfa1 = ("00000" + aAlfa1) % -105;
          aAlfa = "Baja autom tica generada a partir de movimiento de alta ";
          aAlfa = aAlfa + "n£mero " + aAlfa1;
          #laborn02#10 = aAlfa;
     |FINSI;
     #laborn02#13 = #laborn01#240;     || dias vacaciones
     #laborn02#14 = #laborn01#241;     || indemnizacion
     #laborn02#15 = #laborn01#210;     || nombre empresa

     || nombre trabajador

     aAlfa1 = #laborn01#6; |QBF aAlfa1;
     aAlfa2 = #laborn01#7; |QBF aAlfa2;
     aAlfa3 = #laborn01#8; |QBF aAlfa3;

     aAlfa = aAlfa2;
     |SI aAlfa3 ! "";
          aAlfa = aAlfa + " " + aAlfa3 + ", ";
     |SINO;
          aAlfa = aAlfa + ", ";
     |FINSI;

     |SI aAlfa1 ! "";
          aAlfa = aAlfa + aAlfa1;
     |SINO;
          aAlfa = aAlfa - ", ";
     |FINSI;

     #laborn02#16 = aAlfa;
|FPRC;

|PROCESO RecogeDatosCont;
     |PARA nCampo = 193; |SI nCampo [ 208; |HACIENDO nCampo = nCampo + 1;
          #labort02#nCampo# = #0nCampo;
     |SIGUE;
     |PARA nCampo = 211; |SI nCampo [ 214; |HACIENDO nCampo = nCampo + 1;
          #labort02#nCampo# = #0nCampo;
     |SIGUE;
     #labort02#218 = #laborn11#218;
     |PARA nCampo = 228; |SI nCampo [ 234; |HACIENDO nCampo = nCampo + 1;
          #labort02#nCampo# = #0nCampo;
     |SIGUE;
     |PARA nCampo = 235; |SI nCampo [ 238; |HACIENDO nCampo = nCampo + 1;
          nCampo1 = nCampo;
          nCampo2 = nCampo;
          #labort02#nCampo1# = #0nCampo2;
     |SIGUE;
|FPRC;

|PROCESO RecogeDatos;
     |ABRE #laborn01;
     |SI aParam = "MODIF";
          #laborn01#0 = "P";         || estado: pendiente, consulta usuario
     |SINO;
          || consulta
          #laborn01#0 = "A";
     |FINSI;
     #laborn01#1 = eaUsuario;
     #laborn01#2 = "";
     #laborn01#3 = eaFecha;
     #laborn01#4 = eaHora;
     |LEE 101#laborn01.=;
     |SI FSalida = 0;
          || datos generales
          |PARA nCampo = 5; |SI nCampo [ 234; |HACIENDO nCampo = nCampo + 1;
               #laborn11#nCampo# = #laborn01#nCampo#;
          |SIGUE;
          |PARA nCampo = 235; |SI nCampo [ 239; |HACIENDO nCampo = nCampo + 1;
               nCampo1 = nCampo;
               nCampo2 = nCampo + 3;
               #laborn11#nCampo1# = #laborn01#nCampo2#;
          |SIGUE;
          #laborn11#241 = #laborn01#248;
          #laborn11#242 = #laborn01#249;
     |FINSI;
     |CIERRA #laborn01;

     || datos IRPF
     |PARA nCampo = 22; |SI nCampo [ 192; |HACIENDO nCampo = nCampo + 1;
          #labort01#nCampo# = #0nCampo;
     |SIGUE;

     || datos contrato

     |HAZ RecogeDatosCont;

     || datos calendario
     |ABRE #laborn07;
     |SI aParam = "MODIF";
          #laborn07#0 = "P";         || estado: pendiente, consulta usuario
     |SINO;
          #laborn07#0 = "A";         || consulta
     |FINSI;
     #laborn07#1 = eaUsuario;
     #laborn07#2 = "";
     #laborn07#3 = eaFecha;
     #laborn07#4 = eaHora;
     |LEE 101#laborn07.=;
     |SI FSalida = 0;
          |PARA nCampo = 7; |SI nCampo [ 71; |HACIENDO nCampo = nCampo + 1;
               nCampo1 = nCampo - 5;
               #labort03#nCampo1# = #laborn07#nCampo#;
          |SIGUE;
     |FINSI;
     |CIERRA #laborn07;

     |PINPA #0#0;
     |PINDA #0#0;
     |HAZ Boton1;
     |HAZ Boton2;
     |HAZ Boton4;

     aAlfa = eaUsuario; |QBF aAlfa;
     aAlfa1 = eaUsuario % 105;
     |SI aAlfa ! "WEB"; |Y aAlfa1 ! "NUBEA";
          |CAMPO_VISUAL #0#241, 1, "N";
          |PINTA 1962, "                  ³";
     |SINO;
          |CAMPO_VISUAL #0#241, 1, "S";
          |ATRI R;
          |PINTA 1962, "ALTA AFILIACION:";
          |ATRI I;
          |PINTA #0#241;
     |FINSI;

     |SI aParam = "MODIF";
          |ENDATOS #1#0;
     |SINO;
          |PAUSA;
     |FINSI;

     |HAZ Boton1_Fin;
     |HAZ Boton2_Fin;
     |HAZ Boton4_Fin;
|FPRC;

|PROCESO ContadorBaja;
     |ABRE #laborn90;

     #laborn90#0 = #laborn02#5;
     #laborn90#1 = 99999;

     |LEE 000#laborn90.];
     |SI FSalida = 0;
          |SI #laborn90#0 ! #laborn02#5;
               |LEE 000#laborn90.a;
          |FINSI;
     |SINO;
          |LEE 000#laborn90.u;
     |FINSI;

     |SI FSalida = 0;
     |Y #laborn90#0 = #laborn02#5;
          nNume = #laborn90#1;

          |DDEFECTO #laborn90;
          #laborn90#0 = #laborn02#5;
          #laborn90#1 = nNume + 1;

     |SINO;
          || es el primero de esta empresa !!!
          |DDEFECTO #laborn90;
          #laborn90#0 = #laborn02#5;
          #laborn90#1 = 1;
     |FINSI;

     #laborn90#2 = "B";
     #laborn90#3 = "Baja";
     #laborn90#4 = #laborn02#1;
     #laborn90#5 = #laborn02#2;
     #laborn90#6 = #laborn02#3;
     #laborn90#7 = #laborn02#4;

     |GRABA 020#laborn90.n;

     #laborn02#23 = #laborn90#1;

     |LIBERA #laborn90;
     |CIERRA #laborn90;
|FPRC;

|PROCESO Impresion;
     |ABRE #labcontr; |LEE 000#labcontr.p; |CIERRA #labcontr;
     |SI #labcontr#84 ! "S";
          |ACABA;
     |FINSI;

     eaQueEs = "C";

     aAlfa = "*laborn10;X";
     aAlfa = aAlfa + "11";
     aAlfa = aAlfa + #laborn01#0;
     aAlfa = aAlfa + #laborn01#1;
     aAlfa1 = #laborn01#3;
     aAlfa = aAlfa + aAlfa1;
     aAlfa = aAlfa + #laborn01#4;
     aAlfa = aAlfa + "IMPRE";

     |MENSA "    Imprimiendo solicitud de alta";

     |SUB_EJECUTA aAlfa;
     |PULSATECLA;
     |PULSATECLA;
     |PULSATECLA;
     |PULSATECLA;
     |PULSATECLA;

     |SI #laborn01#238 = "S";
          aAlfa = "*laborn10;X";
          aAlfa = aAlfa + "12";
          aAlfa = aAlfa + #laborn02#0;
          aAlfa = aAlfa + #laborn02#1;
          aAlfa1 = #laborn02#3;
          aAlfa = aAlfa + aAlfa1;
          aAlfa = aAlfa + #laborn02#4;
          aAlfa = aAlfa + "IMPRE";

          |MENSA "    Imprimiendo solicitud de baja";
          |SUB_EJECUTA aAlfa;
          |PULSATECLA;
          |PULSATECLA;
          |PULSATECLA;
          |PULSATECLA;
          |PULSATECLA;
     |FINSI;
|FPRC;

|PROCESO Contador;
     |ABRE #laborn90;

     #laborn90#0 = #laborn01#5;
     #laborn90#1 = 99999;

     |LEE 000#laborn90.];
     |SI FSalida = 0;
          |SI #laborn90#0 ! #laborn01#5;
               |LEE 000#laborn90.a;
          |FINSI;
     |SINO;
          |LEE 000#laborn90.u;
     |FINSI;

     |SI FSalida = 0;
     |Y #laborn90#0 = #laborn01#5;
          nNume = #laborn90#1;

          |DDEFECTO #laborn90;
          #laborn90#0 = #laborn01#5;
          #laborn90#1 = nNume + 1;

     |SINO;
          || es el primero de esta empresa !!!
          |DDEFECTO #laborn90;
          #laborn90#0 = #laborn01#5;
          #laborn90#1 = 1;
     |FINSI;

     #laborn90#2 = "A";
     #laborn90#3 = "Alta";
     #laborn90#4 = #laborn01#1;
     #laborn90#5 = #laborn01#2;
     #laborn90#6 = #laborn01#3;
     #laborn90#7 = #laborn01#4;

     |GRABA 020#laborn90.n;

     #laborn01#242 = #laborn90#1;

     |LIBERA #laborn90;
     |CIERRA #laborn90;
|FPRC;

|PROCESO ActuaLabore01;
     |ABRE #labore01;
     #labore01#0 = #laborn01#0;
     #labore01#1 = #laborn01#1;
     #labore01#2 = #laborn01#2;
     #labore01#3 = #laborn01#3;
     #labore01#4 = #laborn01#4;
     |LEE 101#labore01.=;
     |SI FSalida ! 0;
         |DDEFECTO #labore01;
         #labore01#0 = #laborn01#0;
         #labore01#1 = #laborn01#1;
         #labore01#2 = #laborn01#2;
         #labore01#3 = #laborn01#3;
         #labore01#4 = #laborn01#4;

         |GRABA 020#labore01.n;
         |LIBERA #labore01;
     |FINSI;
     |CIERRA #labore01;
|FPRC;

|PROGRAMA;
     |CLS;
     aParam = PARAMETRO;
     |SI aParam = "MODIF";
          |HAZ RecogeDatos;
     |SINO;
          |SI aParam = "CONSU";
               |HAZ RecogeDatos;
          |SINO;
               || la otra posibilidad es el alta

               |PINPA #0#0;
               |PINDA #0#0;
               |HAZ Boton1;
               |HAZ Boton2;
               |HAZ Boton4;

               |ENDATOS #1#0;

               |HAZ Boton1_Fin;
               |HAZ Boton2_Fin;
               |HAZ Boton4_Fin;
          |FINSI;
     |FINSI;

     |SI FSalida = 0; |Y aParam ! "CONSU";
          |SI aParam = "MODIF";
               |ABRE #laborn01;
               #laborn01#0 = "P";         || estado: pendiente, consulta usuario
               #laborn01#1 = eaUsuario;
               #laborn01#2 = "";
               #laborn01#3 = eaFecha;
               #laborn01#4 = eaHora;
               |LEE 101#laborn01.=;
               |SI FSalida = 0;
                    |PARA nCampo = 5; |SI nCampo [ 234; |HACIENDO nCampo = nCampo + 1;
                         #laborn01#nCampo# = #laborn11#nCampo#;
                    |SIGUE;
                    |PARA nCampo = 235; |SI nCampo [ 238; |HACIENDO nCampo = nCampo + 1;
                         nCampo1 = nCampo + 3;
                         nCampo2 = nCampo;
                         #laborn01#nCampo1# = #laborn11#nCampo2#;
                    |SIGUE;
                    #laborn01#248 = #laborn11#241;
                    #laborn01#249 = #laborn11#242;
                    |GRABA 020#laborn01.a;
               |FINSI;
               |CIERRA #laborn01;

               |HAZ ActuaLabore01;

               |ABRE #laborn07;
               #laborn07#0 = "P";         || estado: pendiente, consulta usuario
               #laborn07#1 = eaUsuario;
               #laborn07#2 = "";
               #laborn07#3 = eaFecha;
               #laborn07#4 = eaHora;
               |LEE 101#laborn07.=;
               FEstado = FSalida;
               |PARA nCampo = 7; |SI nCampo [ 71; |HACIENDO nCampo = nCampo + 1;
                    nCampo1 = nCampo - 5;
                    #laborn07#nCampo# = #labort03#nCampo1#;
               |SIGUE;
               |SI FEstado = 0;
                    |GRABA 020#laborn07.a;
               |SINO;
                    |GRABA 020#laborn07.n;
               |FINSI;
               |CIERRA #laborn07;

               |SI #laborn01#238 = "S";
                    |ABRE #laborn02;
                    #laborn02#0 = "P";    || Estado: Pendiente
                    #laborn02#1 = eaUsuario;     || Usuario
                    #laborn02#2 = "";     || Perfiles, en principio no lo uso
                    #laborn02#3 = eaFecha;      || Fecha
                    #laborn02#4 = eaHora;  || Hora
                    |LEE 101#laborn02.=;
                    FEstado = FSalida;

                    |HAZ CamposBajaAuto;

                    |SI #laborn02#23 = 00000;
                         |HAZ ContadorBaja;
                    |FINSI;

                    |SI FEstado = 0;
                         |GRABA 020#laborn02.a;
                    |SINO;
                         |GRABA 020#laborn02.n;
                    |FINSI;

                    |CIERRA #laborn02;
               |FINSI;

               ||MENAV "    La solicitud de alta ha sido modificada";
          |SINO;
               |ABRE #laborn01;
               #laborn01#0 = "P";    || Estado: Pendiente
               #laborn01#1 = Usuario % 0810;     || Usuario
               #laborn01#2 = "";     || Perfiles, en principio no lo uso
               #laborn01#3 = ~;      || Fecha
               |HORA aHora;
               #laborn01#4 = aHora;  || Hora
               |PARA nCampo = 5; |SI nCampo [ 234; |HACIENDO nCampo = nCampo + 1;
                    #laborn01#nCampo# = #laborn11#nCampo#;
               |SIGUE;
               |PARA nCampo = 235; |SI nCampo [ 238; |HACIENDO nCampo = nCampo + 1;
                    nCampo1 = nCampo + 3;
                    nCampo2 = nCampo;
                    #laborn01#nCampo1# = #laborn11#nCampo2#;
               |SIGUE;
               #laborn01#248 = #laborn11#241;
               #laborn01#249 = #laborn11#242;

               |HAZ Contador;

               |GRABA 020#laborn01.n;
               |CIERRA #laborn01;

               |HAZ ActuaLabore01;

               |ABRE #laborn07;
               #laborn07#0 = "P";         || estado: pendiente, consulta usuario
               #laborn07#1 = Usuario % 0810;
               #laborn07#2 = "";
               #laborn07#3 = ~;
               #laborn07#4 = aHora;
               |PARA nCampo = 7; |SI nCampo [ 71; |HACIENDO nCampo = nCampo + 1;
                    nCampo1 = nCampo - 5;
                    #laborn07#nCampo# = #labort03#nCampo1#;
               |SIGUE;
               |GRABA 020#laborn07.n;
               |CIERRA #laborn07;

               |SI #laborn01#238 = "S";
                    |ABRE #laborn02;
                    #laborn02#0 = "P";    || Estado: Pendiente
                    #laborn02#1 = Usuario % 0810;     || Usuario
                    #laborn02#2 = "";     || Perfiles, en principio no lo uso
                    #laborn02#3 = ~;      || Fecha
                    #laborn02#4 = aHora;  || Hora

                    |HAZ CamposBajaAuto;

                    |HAZ ContadorBaja;

                    |GRABA 020#laborn02.n;
                    |CIERRA #laborn02;
               |FINSI;

               |MENAV "    La solicitud de alta ha sido grabada";

               |HAZ Impresion;

               |ABRE #nompmail; |LEE 000#nompmail.p; |CIERRA #nompmail;
               |SI #nompmail#11 = "S";
                    |HAZ Email;
               |FINSI;
          |FINSI;
     |FINSI;

     |DFICE aAlfa1;
     |DFICB aAlfa2;

     |SI aAlfa1 ! aAlfa2;
          |PATH_DAT #labempre#aAlfa2#;
     |FINSI;
|FPRO;
