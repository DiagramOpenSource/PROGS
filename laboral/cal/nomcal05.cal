|FICHEROS;
     nomcal05 #0;
     nomcalca #1;
     nomhicca;
     labtraba #2;
     labempre #3;
     labcentr #4;

     nomcal03;
     nomcal04;
     nomvarca;
     nomhortp;
     nomfical;
     nomabsli;
     nomcontr;
|FIN;

|VARIABLES;
     aFecha = "";
     aMes = "";
     aAny = "";
     fFechaRef = @;
     &aMesLetra = "";

     topemes = 0;
     fIniMes = @;
     fFinMes = @;
     fFechaIniMes = @;
     fFechaFinMes = @;
     &aFechaIniMes = "";
     &aFechaFinMes = "";
     nNume1 = 0;
     nNume2 = 0;
     &nDias = 0;
     swSigue = 0;

     aAlfa = "";
     aNombreMes = "";
     nNume = 0;
     nCampo = 0;
     nDesde = 0;
     nHasta = 0;
     nCampoD = 0;
     nCampoH = 0;
     nCampoP = 0;
     nTDMes = 0;
     nDia = 0;
     nMes = 0;
     nAny = 0;
     nPeso = 0;
     nEmp = 0;
     nTra = 0;
     FEstado = 0;
     aAlfa0 = "";
     aAlfa1 = "";
     aAlfa2 = "";
     aAlfa3 = "";
     fFecha = @;
     fFecha1 = @;
     fFecha2 = @;
     aDiaSemana = "";
     aDiaQueBusco = "";
     nDiaQueBusco = 0;
     aDesde = "";
     aHasta = "";
     nNPeso = 0;
     fFechaAlta = @;
     fFechaBaja = @;
     nCal = 0;
     nModo = 0;
     nCuantos = 0;
     nSuma = 0;
     swFestivo = 0;
     nHorasHuelgaTP = 0;
|FIN;

|PROCESO nom_mes;
|SI nMes = 1;  aMesLetra = "ENERO"; |FINSI;
|SI nMes = 2;  aMesLetra = "FEBRERO"; |FINSI;
|SI nMes = 3;  aMesLetra = "MARZO"; |FINSI;
|SI nMes = 4;  aMesLetra = "ABRIL"; |FINSI;
|SI nMes = 5;  aMesLetra = "MAYO"; |FINSI;
|SI nMes = 6;  aMesLetra = "JUNIO"; |FINSI;
|SI nMes = 7;  aMesLetra = "JULIO"; |FINSI;
|SI nMes = 8;  aMesLetra = "AGOSTO"; |FINSI;
|SI nMes = 9;  aMesLetra = "SEPTIEMBRE"; |FINSI;
|SI nMes = 10; aMesLetra = "OCTUBRE"; |FINSI;
|SI nMes = 11; aMesLetra = "NOVIEMBRE "; |FINSI;
|SI nMes = 12; aMesLetra = "DICIEMBRE "; |FINSI;
|FPRC;

|PROCESO Defecto; |TIPO 7;
     fFecha = ~;
     aFecha = fFecha;
     aMes = aFecha % 402;
     aAny = aFecha % -104;
     nAny = aAny;
     nMes = aMes;
     |HAZ nom_mes;
     #0#2 = nMes;
     #0#3 = nAny;
     |PINTA #0#2;
     |PINTA #0#3;
|FPRC;

|PROCESO MesLetra;
     nMes = #0#2;
     |HAZ nom_mes;
|FPRC;

|PROCESO ultidia;
     topemes = 31;
     |SI nMes = 4; |O  nMes = 6; |O  nMes = 9; |O  nMes = 11;
          topemes = 30;
     |FINSI;
     |SI nMes = 2;
          topemes = 28;
          nNume = #0#3 $ 4;
          |SI nNume = 0;
               topemes = 29;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO ConsultaTra; |TIPO 66;
     |ABRE #labtraba;
     |SI Campo = 5;
          #labtraba#0 = #0#0;
          #labtraba#1 = #0#5;
     |FINSI;
     |SI Campo = 6;
          #labtraba#0 = #0#1;
          #labtraba#1 = #0#6;
     |FINSI;
     |LEE 000#labtraba.=;
     |CONSULTA_CLAVE #1#labtraba;
     |SI FSalida = 0;
           #0Campo = #labtraba#1;
           |PINTA #0Campo;
     |SINO;
           |ERROR;
     |FINSI;
     |CIERRA #labtraba;
|FPRC;

|PROCESO ConsultaEmp; |TIPO 66;
     |ABRE #labempre;
     |CONSULTA_CLAVE #1#labempre;
     |SI FSalida = 0;
           #0Campo = #labempre#0;
           |PINTA #0Campo;
     |SINO;
           |ERROR;
     |FINSI;
     |CIERRA #labempre;
|FPRC;

|PROCESO LeeTrab;
     |DDEFECTO #nomvarca;
     |ABRE #nomvarca;
     #nomvarca#0 = #labtraba#0;
     #nomvarca#1 = #labtraba#1;
     #nomvarca#2 = #0#2;
     #nomvarca#3 = 0;
     |LEE 000#nomvarca.=;
     |CIERRA #nomvarca;
|FPRC;

|PROCESO nomabsli;
     |SI nPeso = 1;
          |ACABA;
          || si es festivo no tento que mirar aqui nada, aunque sea huelga
          || o lo que sea
     |FINSI;
     |SI #nomabsli#2 [ fFecha; |Y fFecha [ #nomabsli#3;
          |SI #nomabsli#4 = "V";
               nPeso = 6;
          |FINSI;
          |SI #nomabsli#4 = "H";
               |SI #nomabsli#6 = 0
                    nPeso = 7;
               |SINO;
                    nPeso = 9;
                    nHorasHuelgaTP = #nomabsli#6;
               |FINSI;
          |FINSI;
          |SI #nomabsli#4 = "A";
               nPeso = 8;
          |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE nomabsli;
     #nomabsli#1;
     ;
     #labtraba#0, #labtraba#1, fFecha1;
     #labtraba#0, #labtraba#1, fFecha2;
     ;
     NULL, nomabsli;
|FIN;

|PROCESO ITS;
     |ABRE #nomcalca;
     |ABRE #nomhicca;
     |ABRE #nomvarca;

     || PRIMERO MIRO MENSUAL

     #nomcalca#0 = #labtraba#0;
     #nomcalca#1 = #labtraba#1;
     #nomcalca#2 = #nomcal04#3;
     #nomcalca#3 = 0;
     |LEE 000#nomcalca.=;
     |SI FSalida = 0;
          nNume = #nomcalca#31 + #nomcalca#32 + #nomcalca#33 + #nomcalca#34;
          nNume = nNume +  #nomcalca#65 + #nomcalca#66 + #nomcalca#78 + #nomcalca#131;

          |SI nNume ! 0;
               |PARA nCampo = 27; |SI nCampo [ 30; |HACIENDO nCampo = nCampo + 1;
                    nDesde = 0;
                    nHasta = 0;
                    nCampoD = nCampo - 8;
                    nCampoH = nCampo - 4;
                    |SI #nomcalca#nCampo# ! " ";
                         |SI #nomcalca#nCampoD# = 0;
                              nDesde = 1;
                         |SINO;
                              nDesde = #nomcalca#nCampoD#;
                         |FINSI;
                         |SI #nomcalca#nCampoH# = 0;
                              nHasta = nTDMes;
                         |SINO;
                              nHasta = #nomcalca#nCampoH#;
                         |FINSI;
                         |SI nDesde [ nDia; |Y nDia [ nHasta;
                              |SI #nomcalca#nCampo# = "D";
                                   nCampoP = nCampo + 92;
                                   |SI #nomcalca#nCampoP# ! 100;
                                        |SI nPeso = 0;
                                             nPeso = 5;
                                        |FINSI;
                                   |SINO;
                                        nPeso = 4;
                                   |FINSI;
                              |SINO;
                                   nPeso = 3;
                              |FINSI;
                         |FINSI;
                    |FINSI;
               |SIGUE;
          |FINSI;
     |SINO;
/*
          || SI NO, MIRO VARIACIONES

          || DE MOMENTO NO, METERME EN ESTO ES UN LIO

          #nomvarca#0 = #labtraba#0;
          #nomvarca#1 = #labtraba#1;
          #nomvarca#2 = #1#3;
          #nomvarca#3 = 0;
          |LEE 000#nomvarca.=;
          |SI FSalida = 0;
               |PARA nCampo = 12; |SI nCampo [ 15; |HACIENDO nCampo = nCampo + 1;
                    nDesde = 0;
                    nHasta = 0;
                    nCampoD = nCampo - 8;
                    nCampoH = nCampo - 4;
                    |SI #nomvarca#nCampo# ! " ";
                         |SI #nomvarca#nCampoD# = 0;
                              nDesde = 1;
                         |SINO;
                              nDesde = #nomvarca#nCampoD#;
                         |FINSI;
                         |SI #nomvarca#nCampoH# = 0;
                              nHasta = nTDMes;
                         |SINO;
                              nHasta = #nomvarca#nCampoH#;
                         |FINSI;
                         |SI nDesde [ nDia; |Y nDia [ nHasta;
                              nPeso = 3;
                         |FINSI;
                    |SIGUE;
                    nNume = #labtraba#89 + #labtraba#92;
                    |SI nNume ! 0;
                         |SI #nomvarca#12 = " "; |Y #nomvarca#13 = " ";
                         |Y #nomvarca#14 = " "; |Y #nomvarca#15 = " ";
                              nPeso = 3;
                         |FINSI;
                    |FINSI;
               |FINSI;
          |FINSI;
*/
     |FINSI;

     #nomhicca#0 = #labtraba#0;
     #nomhicca#1 = #labtraba#1;
     #nomhicca#66 = #nomcal04#2 - 2000;
     #nomhicca#2 = #nomcal04#3;
     #nomhicca#3 = 0;
     |LEE 000#nomhicca.=;
     |SI FSalida = 0;
          nNume = #nomhicca#31 + #nomhicca#32 + #nomhicca#33 + #nomhicca#34;
          nNume = nNume +  #nomhicca#65 + #nomhicca#105 + #nomhicca#119 + #nomhicca#173;

          |SI nNume ! 0;
               |PARA nCampo = 27; |SI nCampo [ 30; |HACIENDO nCampo = nCampo + 1;
                    nDesde = 0;
                    nHasta = 0;
                    nCampoD = nCampo - 8;
                    nCampoH = nCampo - 4;
                    |SI #nomhicca#nCampo# ! " ";
                         |SI #nomhicca#nCampoD# = 0;
                              nDesde = 1;
                         |SINO;
                              nDesde = #nomhicca#nCampoD#;
                         |FINSI;
                         |SI #nomhicca#nCampoH# = 0;
                              nHasta = nTDMes;
                         |SINO;
                              nHasta = #nomhicca#nCampoH#;
                         |FINSI;
                         |SI nDesde [ nDia; |Y nDia [ nHasta;
                              |SI #nomhicca#nCampo# = "D";
                                   nCampoP = nCampo + 134;
                                   |SI #nomhicca#nCampoP# ! 100;
                                        |SI nPeso = 0;
                                             nPeso = 5;
                                        |FINSI;
                                   |SINO;
                                        nPeso = 4;
                                   |FINSI;
                              |SINO;
                                   nPeso = 3;
                              |FINSI;
                         |FINSI;
                    |FINSI;
               |SIGUE;
          |FINSI;
     |FINSI;

     aAlfa1 = "01"; |QBF aAlfa1; aAlfa1 = ("00" + aAlfa1) % -102;
     aAlfa2 = #0#2; |QBF aAlfa2; aAlfa2 = ("00" + aAlfa2) % -102;
     aAlfa3 = #0#3;
     aAlfa = aAlfa1 + "." + aAlfa2 + "." + aAlfa3;
     fFecha1 = aAlfa;

     aAlfa1 = nTDMes; |QBF aAlfa1; aAlfa1 = ("00" + aAlfa1) % -102;
     aAlfa = aAlfa1 + "." + aAlfa2 + "." + aAlfa3;
     fFecha2 = aAlfa;

     nHorasHuelgaTP = 0;
     |BUCLE nomabsli;

     |CIERRA #nomvarca;
     |CIERRA #nomhicca;
     |CIERRA #nomcalca;
|FPRC;

|PROCESO DiasMes;
     nTDMes = 31;
     |SI nMes = 4; |O nMes = 6; |O nMes = 9; |O nMes = 11;
          nTDMes = 30;
     |SINO;
          |SI nMes = 2;
               nTDMes = 28;
               nNume = nAny $ 4;
               |SI nNume = 0;
                    nTDMes = 29;
               |FINSI;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO CargaHorario;
     |ABRE #nomhortp;
     |DDEFECTO #nomhortp;
     #nomhortp#0 = #nomcal04#0;
     #nomhortp#1 = #nomcal04#1;
     |LEE 000#nomhortp.=;
     |SI FSalida = 0;
          aAlfa = #nomcal04#5;
          aAlfa = aAlfa % 102;

          |SI aAlfa = "Lu";    nCampo = 2;    nSuma = 0; |FINSI;
          |SI aAlfa = "Ma";    nCampo = 7;    nSuma = 1; |FINSI;
          |SI aAlfa = "Mi";    nCampo = 12;   nSuma = 2; |FINSI;
          |SI aAlfa = "Ju";    nCampo = 17;   nSuma = 3; |FINSI;
          |SI aAlfa = "Vi";    nCampo = 22;   nSuma = 4; |FINSI;
          |SI aAlfa = "S ";    nCampo = 27;   nSuma = 5; |FINSI;
          |SI aAlfa = "Do";    nCampo = 32;   nSuma = 6; |FINSI;

          |SI #nomhortp#nCampo# ! 0;
               |SI nPeso = 0; |O nPeso = 9;
                    #nomcal04#6 = #nomhortp#nCampo#;
                    nCampo = nCampo + 1;
                    #nomcal04#8 = #nomhortp#nCampo#;
                    nCampo = nCampo + 1;
                    #nomcal04#9 = #nomhortp#nCampo#;
                    nCampo = nCampo + 1;
                    #nomcal04#10 = #nomhortp#nCampo#;
                    nCampo = nCampo + 1;
                    #nomcal04#11 = #nomhortp#nCampo#;

                    nCampo = nCampo + 33 - nSuma;
                    #nomcal04#15 = #nomhortp#nCampo#;
                    nCampo = nCampo + 1;
                    #nomcal04#16 = #nomhortp#nCampo#;
                    nCampo = nCampo + 1;
                    #nomcal04#17 = #nomhortp#nCampo#;
                    nCampo = nCampo + 1;
                    #nomcal04#18 = #nomhortp#nCampo#;
               |FINSI;
          |SINO;
               || esto es para contemplar el caso de un dia que tuviera
               || que trabajar por no ser festivo, no tuviera horas con lo
               || que no tendria que trabajar en verdad y estuviera de IT
               || este dia no se tiene que contar para el AFI de dias trabajados
               |SI nPeso = 3;
                    |SI swFestivo = 0;
                         #nomcal04#13 = "IT (SinH)"
                    |FINSI;
               |FINSI;
          |FINSI;
     |FINSI;
     |CIERRA #nomhortp;
|FPRC;

|PROCESO CreaMes;
     |HAZ DiasMes;

     |PARA nDia = 1; |SI nDia [ nTDMes; |HACIENDO nDia = nDia + 1;
          |DDEFECTO #nomcal04;
          #nomcal04#0 = nEmp;         || Empresa
          #nomcal04#1 = nTra;         || Trabajador
          #nomcal04#2 = nAny;         || A¤o
          #nomcal04#3 = nMes;         || Mes
          #nomcal04#4 = nDia;         || Dia
          |LEE 000#nomcal04.=;
          FEstado = FSalida;
          |SI FEstado = 0;
               || si existia inicio los campos

               #nomcal04#6 = 0;
               #nomcal04#7 = 0;
               #nomcal04#8 = "";
               #nomcal04#9 = "";
               #nomcal04#10 = "";
               #nomcal04#11 = "";
               #nomcal04#12 = 0;
               #nomcal04#13 = "";

               #nomcal04#15 = "";
               #nomcal04#16 = "";
               #nomcal04#17 = "";
               #nomcal04#18 = "";
          |FINSI;

          || 0: laborable
          || 1: festivo

          nPeso = 0;

          || busco que dia de la semana es el dia en que estoy
          aAlfa0 = nDia; |QBF aAlfa0;
          aAlfa0 = ("00" + aAlfa0) % -102;
          aAlfa1 = nMes; |QBF aAlfa1;
          aAlfa1 = ("00" + aAlfa1) % -102;
          aAlfa2 = nAny;
          aAlfa3 = aAlfa0 + "."  + aAlfa1 + "." + aAlfa2;
          fFecha = aAlfa3;
          fFecha1 = fFecha % 1;
          aAlfa3 = fFecha1 % 1102;

          |SI aAlfa3 = "Lu"; aDiaSemana = "Lunes    "; |FINSI;
          |SI aAlfa3 = "Ma"; aDiaSemana = "Martes   "; |FINSI;
          |SI aAlfa3 = "Mi"; aDiaSemana = "Mi‚rcoles"; |FINSI;
          |SI aAlfa3 = "Ju"; aDiaSemana = "Jueves   "; |FINSI;
          |SI aAlfa3 = "Vi"; aDiaSemana = "Viernes  "; |FINSI;
          |SI aAlfa3 = "Sa"; aDiaSemana = "S bado   "; |FINSI;
          |SI aAlfa3 = "Do"; aDiaSemana = "Domingo  "; |FINSI;

          || Ahora asigno pesos segun si el dia es laborable, festivo, alta, baja o IT
          swFestivo = 0;
          nPeso = 0;     || por defecto laborale

          || Primero miro el calendario semanal

          |SI aAlfa3 = "Lu"; nPeso = #nomfical#2; |FINSI;
          |SI aAlfa3 = "Ma"; nPeso = #nomfical#3; |FINSI;
          |SI aAlfa3 = "Mi"; nPeso = #nomfical#4; |FINSI;
          |SI aAlfa3 = "Ju"; nPeso = #nomfical#5; |FINSI;
          |SI aAlfa3 = "Vi"; nPeso = #nomfical#6; |FINSI;
          |SI aAlfa3 = "Sa"; nPeso = #nomfical#7; |FINSI;
          |SI aAlfa3 = "Do"; nPeso = #nomfical#8; |FINSI;

          || despues el diario

          |PARA nCampo = 11; |SI nCampo [ 98; |HACIENDO nCampo = nCampo + 3;
               |SI #nomfical#nCampo# = 1;
                    nCampoD = nCampo - 2;
                    nCampoH = nCampo - 1;
                    aDesde = (#nomfical#nCampoD# % 302) + (#nomfical#nCampoD# % 102);
                    aHasta = (#nomfical#nCampoH# % 302) + (#nomfical#nCampoH# % 102);
                    nDesde = aDesde;

                    nHasta = aHasta;
                    aAlfa1 = nMes; |QBF aAlfa1;
                    aAlfa1 = ("00" + aAlfa1) % -102;
                    aAlfa2 = nDia; |QBF aAlfa2;
                    aAlfa2 = ("00" + aAlfa2) % -102;
                    aDiaQueBusco = aAlfa1 + aAlfa2; || (mes+dia)
                    nDiaQueBusco = aDiaQueBusco;
                    |SI nDesde [ nDiaQueBusco; |Y nDiaQueBusco [ nHasta;
                         nPeso = #nomfical#nCampo#;
                    |FINSI;
               |FINSI;
          |SIGUE;

          |PARA nCampo = 100; |SI nCampo [ 158; |HACIENDO nCampo = nCampo + 2;
               |SI #nomfical#nCampo# = 1;
                    nCampoD = nCampo - 1;
                    nCampoH = nCampo - 1;
                    aDesde = (#nomfical#nCampoD# % 302) + (#nomfical#nCampoD# % 102);
                    aHasta = (#nomfical#nCampoH# % 302) + (#nomfical#nCampoH# % 102);
                    nDesde = aDesde;

                    nHasta = aHasta;
                    aAlfa1 = nMes; |QBF aAlfa1;
                    aAlfa1 = ("00" + aAlfa1) % -102;
                    aAlfa2 = nDia; |QBF aAlfa2;
                    aAlfa2 = ("00" + aAlfa2) % -102;
                    aDiaQueBusco = aAlfa1 + aAlfa2; || (mes+dia)
                    nDiaQueBusco = aDiaQueBusco;
                    |SI nDesde [ nDiaQueBusco; |Y nDiaQueBusco [ nHasta;
                         nPeso = #nomfical#nCampo#;
                    |FINSI;
               |FINSI;
          |SIGUE;

          |SI nPeso = 1;
               swFestivo = 1;
          |FINSI;

          nNPeso = nDia + 50;

          || PESOS
          || 0: laborable
          || 1: festivo
          || 2: no esta en alta
          || 3: esta en IT

          || comprobacion de fechas de alta y baja

          |HAZ LeeTrab;
          fFechaAlta = #labtraba#36;
          |QBF aAlfa;
          |SI #nomvarca#16 ! "          "; |Y #nomvarca#16 ! "..........";
               aAlfa = #nomvarca#16;
          |SINO;
               aAlfa = #labtraba#37;
          |FINSI;
          |QBF aAlfa;
          |SI aAlfa = ""; |O aAlfa = "..........";
               fFechaBaja = "31.12.2999";
          |SINO;
               fFechaBaja = aAlfa;
          |FINSI;

          aAlfa1 = nDia; |QBF aAlfa1; aAlfa1 = ("00" + aAlfa1) % -102;
          aAlfa2 = #nomcal04#3; |QBF aAlfa2; aAlfa2 = ("00" + aAlfa2) % -102;
          aAlfa3 = nAny;
          aAlfa = aAlfa1 + "." + aAlfa2 + "." + aAlfa3;
          fFecha = aAlfa;
          |SI fFechaAlta [ fFecha; |Y fFecha [ fFechaBaja;
               || correcto, el trabajador esta de alta
          |SINO;
               nPeso = 2;     || TRABAJADOR EN BAJA
          |FINSI;

          || comprobacion de periodos en IT

          |SI nPeso ! 2;
               |HAZ ITS;
          |FINSI;

          #nomcal04#5 = aDiaSemana;

          |SI nPeso = 1; #nomcal04#13 = "Festivo"; |FINSI;
          |SI nPeso = 2; #nomcal04#13 = "Sin alta"; |FINSI;
          |SI nPeso = 3; #nomcal04#13 = "IT     "; |FINSI;
          |SI nPeso = 4; #nomcal04#13 = "ERE    "; |FINSI;
          |SI nPeso = 5; #nomcal04#13 = "ERE TP "; |FINSI;
          |SI nPeso = 6; #nomcal04#13 = "Vacac. "; |FINSI;
          |SI nPeso = 7; #nomcal04#13 = "Huelga "; |FINSI;
          |SI nPeso = 8; #nomcal04#13 = "Absent."; |FINSI;
          |SI nPeso = 9; #nomcal04#13 = "Huel.TP"; |FINSI;

          |SI nPeso = 5;
               nPeso = 0;
          |FINSI;

          |SI nPeso = 3;
               |SI swFestivo = 1;
                    #nomcal04#13 = "IT (Fes)";
               |FINSI;
          |FINSI;

          |SI nPeso = 0;
          |O nPeso = 9
               #nomcal04#12 = 0;
          |SINO;
               #nomcal04#12 = 1;
          |FINSI;

          |SI nPeso = 0; |O nPeso = 3;
          |O nPeso = 9
               |HAZ CargaHorario;
               |SI #nomcal04#6 ! 0;
                    |SI nPeso = 9;
                         #nomcal04#6 = #nomcal04#6 - nHorasHuelgaTP;
                    |FINSI;
                    #nomcal03#5 = #nomcal03#5 + #nomcal04#6;
                    #nomcal03#7 = #nomcal03#7 + #nomcal04#6;
                    |GRABA 020#nomcal03.a;
               |FINSI;
          |FINSI;

          |SI FEstado = 0;
               |GRABA 020#nomcal04.a;
          |SINO;
               |GRABA 020#nomcal04.n;
          |FINSI;
          |LIBERA #nomcal04;
     |SIGUE;
|FPRC;

|PROCESO Calendario;
     nEmp = #labtraba#0;
     nTra = #labtraba#1;
     nAny = #0#3;
     nMes = #0#2;

     nCal = #labtraba#156;

     |ABRE #nomfical;
     #nomfical#0 = nCal;
     #nomfical#1 = nAny;
     |LEE 000#nomfical.=;
     |CIERRA #nomfical;

     #nomcal03#0 = #labtraba#0;
     #nomcal03#1 = #labtraba#1;
     #nomcal03#2 = #0#3;
     #nomcal03#3 = #0#2;
     |LEE 000#nomcal03.=;

     |HAZ CreaMes;

     nCuantos = nCuantos + 1;
|FPRC;

|PROCESO GrabaDatos;
     #labempre#0 = #labtraba#0;
     |LEE 000#labempre.=;

     #labcentr#0 = #labtraba#0;
     #labcentr#1 = #labtraba#77;
     |LEE 000#labcentr.=;

     |ABRE #nomcal03;
     |DDEFECTO #nomcal03;
     |ABRE #nomcal04;

     #nomcal03#0 = #labtraba#0;
     #nomcal03#1 = #labtraba#1;
     #nomcal03#2 = #0#3;
     #nomcal03#3 = #0#2;
     |LEE 000#nomcal03.=;
     |SI FSalida ! 0;
          |GRABA 020#nomcal03.n;
          |HAZ Calendario;
     |SINO;
          ||SI #0#4 = "S";
               #nomcal03#4 = 0;
               #nomcal03#5 = 0;
               #nomcal03#6 = 0;
               #nomcal03#7 = 0;
               |GRABA 020#nomcal03.a;
               |HAZ Calendario;
          ||FINSI;
     |FINSI;

     |LIBERA #nomcal03;
     |CIERRA #nomcal03;
     |CIERRA #nomcal04;
|FPRC;

|PROCESO labtraba;
     |SI #0#7 = "C";
          |SI #labtraba#42 = "X"; |O #labtraba#42 = "T"; |O #labtraba#42 = "P";
               |ACABA;
          |FINSI;
     |FINSI;
     |SI #0#7 = "P";
          |SI #labtraba#42 ! "X"; |Y #labtraba#42 ! "T"; |Y #labtraba#42 ! "P";
               |ACABA;
          |FINSI;
     |FINSI;

     swSigue = 0;

     nMes = #0#2;
     aMes = nMes;
     aMes = ("00" + aMes) % -102;
     aAny = #0#3;

     aFecha = "01" + "." + aMes + "." + aAny;
     fIniMes = aFecha;

     |HAZ ultidia;
     aFecha = topemes;
     aFecha = ("00" + aFecha) % -102;
     aFecha = aFecha + "." + aMes + "." + aAny;
     fFinMes = aFecha;

     aAlfa = #labtraba#36;
     |QBF aAlfa;
     |SI aAlfa = ""; |O aAlfa = "..........";
          |ACABA;
     |FINSI;
     fFechaAlta = aAlfa;
     aAlfa = #labtraba#37;
     |QBF aAlfa;
     |SI aAlfa = ""; |O aAlfa = "..........";
          aAlfa = "31.12.2999";
     |FINSI;
     fFechaBaja = aAlfa;

     |SI fFechaAlta [ fFinMes; |Y fFechaBaja ] fIniMes;
          swSigue = 1;
     |FINSI;
/*
     #nomcalca#0 = #labtraba#0;
     #nomcalca#1 = #labtraba#1;
     #nomcalca#2 = #0#2;
     #nomcalca#3 = 0;
     |LEE 000#nomcalca.=;
     |SI FSalida = 0;
          swSigue = 1;
     |SINO;
          #nomhicca#0 = #labtraba#0;
          #nomhicca#1 = #labtraba#1;
          #nomhicca#66 = nAny;
          #nomhicca#2 = #0#2;
          #nomhicca#3 = 0;
          |LEE 000#nomhicca.=;
          |SI FSalida = 0;
               swSigue = 1;
          |FINSI;
     |FINSI;
*/
     |SI swSigue = 1;
          |HAZ GrabaDatos;
     |FINSI;
|FPRC;

|DEFBUCLE labtraba_todos;
     #labtraba#1;
     ;
     #0#0, #0#5;
     #0#1, #0#6;
     ;
     NULL, labtraba;
|FIN;

|DEFBUCLE labtraba3;
     #labtraba#1;
     42;
     #0#0, #0#5, "P";
     #0#1, #0#6, "P";
     ;
     NULL, labtraba;
|FIN;

|DEFBUCLE labtraba2;
     #labtraba#1;
     42;
     #0#0, #0#5, "X";
     #0#1, #0#6, "X";
     ;
     NULL, labtraba;
|FIN;

|DEFBUCLE labtraba1;
     #labtraba#1;
     42;
     #0#0, #0#5, "T";
     #0#1, #0#6, "T";
     ;
     NULL, labtraba;
|FIN;

|PROGRAMA;
     |CLS;
     |PINPA #0#0

     |ABRE #0;
     |DDEFECTO #0;
     |LEE 000#0p;
     |SI FSalida = 0;  |BORRA 020#0a;  |FINSI;
     |PINDA #0#0;

     |ABRE #nomcontr; |LEE 000#nomcontr.=; |CIERRA #nomcontr;
     |SI #nomcontr#75 = "S";
          |CAMPO_MODIFICA #0#7, 1, "S";
          |CAMPO_VISUAL #0#7, 1, "S";
          |PINTA 1515, "³ Tipo de contrato:    ";
          |PINTA 1615, "ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ";
          |PINTA #0#7;
     |SINO;
          |CAMPO_MODIFICA #0#7, 1, "N";
          |CAMPO_VISUAL #0#7, 1, "N";
          |PINTA 1515, "³                      ";
          |PINTA 1615, "ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ";
          #0#7 = "P";
     |FINSI;
     |ENDATOS #1#0;

     |SI FSalida = 0;
          nMes = #0#2;
          nAny = #0#3 - 2000;

          |ABRE #nomcalca;
          |ABRE #nomhicca;
          |ABRE #labempre;
          |ABRE #labcentr;
          |SI #0#7 = "P";
               |BUCLE labtraba1;
               |BUCLE labtraba2;
               |BUCLE labtraba3;
          |SINO;
               |BUCLE labtraba_todos;
          |FINSI;
          |CIERRA #labcentr;
          |CIERRA #labempre;
          |CIERRA #nomhicca;
          |CIERRA #nomcalca;

          |SI nCuantos > 0;
               aAlfa = nCuantos;
               |QBF aAlfa;
               aAlfa = "    Se ha creado el calendario de horas a " + aAlfa;
               aAlfa = aAlfa + " trabajadores.";
          |SINO;
               aAlfa = "    No se ha encontrado ningún trabajador de alta ";
               aAlfa = aAlfa + "para la selección realizada";
          |FINSI;
          |MENAV aAlfa;
     |FINSI;
     |GRABA 000#0n;
     |CIERRA #0;
|FPRO;
