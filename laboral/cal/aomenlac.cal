|FICHEROS;
     aomenlac #0;
     labtraba;
     aontraem;
     aomcentr;
     aomparam;
     gotrabaj@ #100;
     gotrabal@ #101;
     gotrarec@ #102;
     gotrarel@ #103;
|FIN;

|VARIABLES;
     aBase = "";
     aPathFich = "";
     aPathDef = "";
     &nEmpr = 0;
     &nTrab = 0;
     aEmpr = "";
     aAlfa = "";
     aAlfa1 = "";
     aAlfa2 = "";
     nNume = 0;
     nNume1 = 0;
     nNume2 = 0;
     fFecha = @;
     nEdad = 0;
     nSalto = 0;
     nUltimo = 0;

     nProd = 0;
     &emp_ori = 0;
     &emp_des = 0;
     &tra_ori = 0;
     &tra_des = 0;
     nCodGestion = 0;
     &aFechaAlta = "";
     aParam = "";
     aFechaBajaAct = "";
     nEmprCons = 0;
     aEmprCons = "";
     aBaseP = "";
     aFabre = "";
     nNuevoCod = 0;
     nLimite = 0;
     nCodigo = 0;
|FIN;

|PROCESO CreaCodigo;
/*
     #aomcentr#1 = #0#0;    || zona
     |LEE 000#aomcentr.=;
     nSalto = #aomcentr#2;
     nUltimo = #aomcentr#3;
     |SI nUltimo = 0;
          nUltimo = nSalto;
     |FINSI;
*/

/*
     || este es el proceso que genera el codigo por NIF, busca para el salto
     || de la empresa y hasta el siguiente salta que encuentre el ultimo codigo
     || de trabajador disponible, y ese es el que asigna, lo quieto porque
     || ACISA ya no lo usa y a visever tampoco le hara falta, usaran el salto
     || a pelo y los trabajadores no se agruparan por NIF, simplemente al
     || codigo del trabajador se le suma el salto de la empresa y listo
     || aunque esto esta preparado y deberia funcionar, esta hecho aunque de
     || momento no se usara 10.04.2006

     #aontraem#0 = #labtraba#0;    || empresa
     |LEE 000#aontraem.=;
     nSalto = #aontraem#2;
     |SELECT #2#aontraem;
     #aontraem#2 = nSalto;
     |LEE 000#aontraem.=;
     |LEE 000#aontraem.s;

     |SI FSalida = 0;
          nLimite = #aontraem#2;
     |SINO;
          nLimite = 99999;
     |FINSI;

     #100#0 = nLimite;
     |LEE 000#100.];
     |SI FSalida = 0;
          |SI nLimite = #100#0;
               |LEE 000#100a;
               |SI FSalida = 0;
                    |SI #100#0 ] nSalto;
                         nCodigo = #100#0 + 1;
                    |SINO;
                         nCodigo = nSalto;
                    |FINSI;
               |SINO;
                    nCodigo = nSalto;
               |FINSI;
          |SINO;
               |SI #100#0 ] nSalto;
                    nCodigo = #100#0 + 1;
               |SINO;
                    nCodigo = nSalto;
               |FINSI;
          |FINSI;
     |SINO;
          nCodigo = nSalto;
     |FINSI;
*/

     #aontraem#0 = #labtraba#0;    || empresa
     |LEE 000#aontraem.=;
     nSalto = #aontraem#2;

     nCodigo = #labtraba#1 + nSalto;
|FPRC;

|PROCESO BuscaCod;
     #101#0 = nCodGestion;         || codigo trabajador
     #101#1 = 9999;
     |LEE 000#101.>;
     |LEE 000#101.a;
     |SI #101#0 = nCodGestion;
          nNuevoCod = #101#1 + 1;
     |SINO;
          nNuevoCod = 1;
     |FINSI;
|FPRC;

|PROCESO ElPaseALTA;
     #labtraba#0 = nEmpr;
     #labtraba#1 = nTrab;
     |LEE 000#labtraba.=;
     |ABRE #100;
     |ABRE #101;
     |ABRE #102;
     |ABRE #103;

     |HAZ CreaCodigo;
     || #100#0 = nUltimo + 1;              || codigo trabajador
     #100#0 = nCodigo;                  || codigo trabajador
     #100#1 = #labtraba#2;              || nombre trabajador
     #100#2 = #labtraba#87;             || codigo convenio
     #100#3 = #labtraba#17;             || codigo categoria
     #100#4 = #labtraba#3;              || domicilio
     #100#5 = #labtraba#4;              || numero
     #100#6 = #labtraba#5;              || piso
     #100#7 = #labtraba#6;              || localidad
     #100#8 = #labtraba#8;              || C.P. Provincia
     #100#9 = #labtraba#9;              || C.P. Poblacion
     #100#10 = #labtraba#76;            || nacido en
     #100#11 = #labtraba#11;            || fecha nacimiento
     #100#12 = #labtraba#7;             || N.I.F.
     #100#13 = #labtraba#75;            || NAFSS
     #100#14 = #labtraba#19;            || profesion
     #100#15 = #labtraba#238;           || puesto
     #100#16 = #labtraba#244;           || telefono 1
     #100#17 = "";                      || telefono 2
     #100#18 = "";                      || telefono 3
     #100#19 = "";                      || movil
     #100#20 = #labtraba#36;            || fecha inicio contrato
     aAlfa = #labtraba#37;
     |QBF aAlfa;
     |SI aAlfa = ""; |O aAlfa = "..........";
          aAlfa = "";
     |FINSI;
     #100#21 = aAlfa;                   || fecha fin contrato
     #100#22 = #labtraba#234;           || horas jornada (horas t.p.)
     #100#23 = #labtraba#98;            || salario pactado (ajustes)
     #100#24 = #labtraba#25;            || descripcion convenio
     #100#25 = #labtraba#18;            || descripcion categoria

     aAlfa1 = #labtraba#11; aAlfa1 = aAlfa1 % -104; nNume1 = aAlfa1; || Anyo Nacimiento
     fFecha = ~; aAlfa2 = fFecha; aAlfa2 = aAlfa2 % -104; nNume2 = aAlfa2; || Anyo Actual
     nEdad = nNume2 - nNume1;
     aAlfa1 = #labtraba#11; aAlfa1 = aAlfa1 % 402; nNume1 = aAlfa1; || Mes Nacimiento
     fFecha = ~; aAlfa2 = fFecha; aAlfa2 = aAlfa2 % 402; nNume2 = aAlfa2; || Mes Actual
     nNume = nNume2 - nNume1;
     |SI nNume2 < nNume1;
          nEdad = nEdad - 1;
     |FINSI;
     #100#26 = nEdad;                        || edad
     #100#27 = "";                           || e-mail

     |GRABA 020#100n;
     |LIBERA #100;

     |SI FSalida = 0;
          #101#0 = #100#0;                        || codigo trabajador
          || #101#1 = 1;                          || codigo contrato
          #101#1 = #labtraba#45;                  || codigo contrato
          #101#2 = #labtraba#36;                  || Fecha Inicio
          #101#3 = #100#21;                       || Fecha Fin
          #101#4 = #labtraba#1;                   || codigo aginom trab
          #101#5 = #labtraba#0;                   || codigo aginom empr
                                                  || OJO que estan cambiados
          |GRABA 020#101n;


          #aomcentr#1 = #0#0;
          |LEE 101#aomcentr.=;
          |SI FSalida = 0;
               #aomcentr#3 = #100#0;
               |GRABA 020#aomcentr.a;
          |FINSI;
          |LIBERA #aomcentr;

          #102#0 = #100#0;              || codigo trabajador
          #102#1 = #100#12;             || NIF

          || la zona no se usa, aunque la sigo guardando
          || por lo que se hizo para ACISA
          #102#2 = #aomcentr#1;         || codigo Zona
          #102#3 = #aomcentr#4;         || descripcion zona

          #102#4 = #labtraba#2;         || nombre trabajador
          |GRABA 020#102n;
          |SI FSalida = 0;
               #103#0 = #100#0;         || codigo trabajador (gestion)
               #103#4 = #labtraba#36;   || fecha inicio contrato
               #103#5 = #100#21;        || fecha fin contrato
               #103#1 = #labtraba#0;    || codigo empresa (nomina)
               #103#2 = #labtraba#1;    || codigo trabajador (nomina)
                                        || OJO, aqui no estan cambiados
               #103#3 = #labtraba#45;   || codigo contrato
               |GRABA 020#103n;
          |FINSI;
          |LIBERA #102;
          |LIBERA #103;
     |FINSI;

     |CIERRA #103;
     |CIERRA #102;
     |CIERRA #101;
     |CIERRA #100;
|FPRC;

|PROCESO BuscaTraba;
     nCodGestion = #103#0;
     aFechaBajaAct = #103#5;
|FPRC;

|DEFBUCLE BuscaTraba;
     #103#1002;
     1,2;
     00001, "          ", emp_ori, tra_ori;
     99999, "zzzzzzzzzz", emp_ori, tra_ori;
     ;
     NULL, BuscaTraba;
|FIN;

|PROCESO ElPaseDUPLI;
     |ABRE #100;
     |ABRE #101;
     |ABRE #102;
     |ABRE #103;

     |BUCLE BuscaTraba;

     #labtraba#0 = emp_ori;
     #labtraba#1 = tra_ori;
     |LEE 000#labtraba.=;

     || grabo la nueva linea en el fichero del trabajador
     #103#0 = nCodGestion;         || codigo trabajador (gestion)
     #103#1 = emp_des;             || codigo empresa (nomina)
     #103#2 = tra_des;             || codigo trabajador (nomina)
     #103#3 = #labtraba#45;     || codigo contrato
     #103#4 = aFechaAlta;          || fecha inicio contrato
     #103#5 = "";                  || fecha fin contrato
     |GRABA 020#103n;


     || ahora busco el trabajador anterior para modificarle su fecha baja
     #103#0 = nCodGestion;         || codigo trabajador (gestion)
     #103#4 = #labtraba#36
     #103#1 = emp_ori;             || codigo empresa (nomina)
     #103#2 = tra_ori;             || codigo trabajador (nomina)
     #103#3 = #labtraba#45;     || codigo contrato
     |LEE 020#103=;
     |SI FSalida = 0;
          #103#5 = #labtraba#37;        || fecha fin contrato trab origen
          |GRABA 020#103a;
     |FINSI;

     |HAZ BuscaCod;

     || #101#0 = nCodGestion;         || codigo trabajador
     || #101#1 = nNuevoCod;           || codigo contrato
     || #101#2 = aFechaAlta;          || Fecha Inicio
     #101#3 = #labtraba#37;                  || Fecha Fin
     || #101#4 = #labtraba#1;      || codigo aginom
     |GRABA 020#101a;

     #101#0 = nCodGestion;         || codigo trabajador
     #101#1 = nNuevoCod;           || codigo contrato
     #101#2 = aFechaAlta;          || Fecha Inicio
     #101#3 = "";                  || Fecha Fin
     || #101#4 = #labtraba#1;      || codigo aginom
     |GRABA 020#101n;

     |CIERRA #103;
     |CIERRA #102;
     |CIERRA #101;
     |CIERRA #100;
|FPRC;

|PROGRAMA;
     aParam = PARAMETRO;
     |QBF aParam;
     |HAZ ElProceso;
|FPRO;

|PROCESO ElProceso;
     |ABRE #aomparam;
     |LEE 000#aomparam.p;
     nEmprCons = #aomparam#2;
     |CIERRA #aomparam;

     |DBASS aBase;
     |QBF aBase;

     || nEmprCons = 1;         || PARA ACISA: SIEMPRE LA EMPRESA 1

     aEmprCons = nEmprCons;
     aEmprCons = "00000" + aEmprCons;
     aEmprCons = aEmprCons % -105;

     aPathFich = aBase + "dscomer9/fich/" + aEmprCons + "/";
     aPathDef = aBase + "dscomer9/def/";

     aAlfa = aPathDef + "gotrabaj.mas";
     |CARGA_DEF aAlfa, gotrabaj@;
     |PATH_DAT #100 aPathFich;
     |SI FSalida ! 0;
          aAlfa = "    ATENCION: Referencia erronea en fichero " + aAlfa;
          |MENAV aAlfa;
          |ACABA;
     |FINSI;

     aAlfa = aPathDef + "gotrabal.mas";
     |CARGA_DEF aAlfa, gotrabal@;
     |PATH_DAT #101 aPathFich;
     |SI FSalida ! 0;
          aAlfa = "    ATENCION: Referencia erronea en fichero " + aAlfa;
          |MENAV aAlfa;
          |DESCARGA_DEF #gotrabaj@;
          |ACABA;
     |FINSI;

     aAlfa = aPathDef + "gotrarec.mas";
     |CARGA_DEF aAlfa, gotrarec@;
     |PATH_DAT #102 aPathFich;
     |SI FSalida ! 0;
          aAlfa = "    ATENCION: Referencia erronea en fichero " + aAlfa;
          |MENAV aAlfa;
          |DESCARGA_DEF #gotrabal@;
          |DESCARGA_DEF #gotrabaj@;
          |ACABA;
     |FINSI;

     aAlfa = aPathDef + "gotrarel.mas";
     |CARGA_DEF aAlfa, gotrarel@;
     |PATH_DAT #103 aPathFich;
     |SI FSalida ! 0;
          aAlfa = "    ATENCION: Referencia erronea en fichero " + aAlfa;
          |MENAV aAlfa;
          |DESCARGA_DEF #gotrabal@;
          |DESCARGA_DEF #gotrabaj@;
          |DESCARGA_DEF #gotrarec@;
          |ACABA;
     |FINSI;

     |ABRE #labtraba;
     |ABRE #aomcentr;
     |ABRE #aontraem;

     |HAZ ElPaseALTA;

/*
     |SI aParam = "ALTA";
          |HAZ ElPaseALTA;
     |SINO;
          |HAZ ElPaseDUPLI;
     |FINSI;
*/
     |CIERRA #aontraem;
     |CIERRA #aomcentr;
     |CIERRA #labtraba;

     |DESCARGA_DEF #gotrarel@;
     |DESCARGA_DEF #gotrarec@;
     |DESCARGA_DEF #gotrabal@;
     |DESCARGA_DEF #gotrabaj@;
|FPRC;
