|FICHEROS;
     nomtrcop #0;
     nomtrctm #100,MANTE;
     labempre;
     labtraba;
     labcentr;
     copiadef@ #500;
     copiacop@ #501;
|FIN;

|VARIABLES;
     nCampo = 0;
     nCampo1 = 0;
     nCampo2 = 0;
     aAlfa = "";
     aAlfa1 = "";
     aAlfa2 = "";
     aAlfa3 = "";
     aAlfa4 = "";
     aAlfa5 = "";
     aAlfa6 = "";
     nCentroDes = 0;
     nMoverDoc  = 0;
     aRutaDef = "";
     aRutaDatos = "";
     nAntEmpOri = "";
     nAntTraOri = "";
     nAntEmpDes = "";
     nAntTraDes = "";
     swRepes = 0;
     aQueQuiero = "";
     aResultado = "";
     aDefCopia = "";
     nDesde1 = 0;
     nHasta1 = 0;
     nDesde2 = 0;
     nHasta2 = 0;
     nDesde3 = 0;
     nHasta3 = 0;
     nDesde4 = 0;
     nHasta4 = 0;
     aDesde1 = "";
     aHasta1 = "";
     aDesde2 = "";
     aHasta2 = "";
     aDesde3 = "";
     aHasta3 = "";
     nTipo = 0;
     nLimite = 0;
     swSePasa = 0;
     nContador = 0;
     aContador = "";

     &eaPathDsArchi = "";
|FIN;

/*********************** PROCESOS PARA LA PANTALLA **************************/

|PROCESO CompEmp;
     |SI #0#0 = #0#3;
          |MENAV "    La empresa origen no puede ser igual que la empresa destino";
          |ERROR;
     |FINSI;
|FPRC;

|PROCESO CompCen;
     nCampo1 = Campo - 10;
     |SI #0nCampo1 ! 0;
          |ABRE #labcentr;
          #labcentr#0 = #0#3;
          #labcentr#1 = #0Campo;
          |LEE 000#labcentr.=;
          |SI FSalida ! 0;
               aAlfa1 = #0#3;
               aAlfa1 = "00000" + aAlfa1;
               aAlfa1 = aAlfa1 % -105;
               aAlfa2 = #0Campo;
               aAlfa2 = "00" + aAlfa2;
               aAlfa2 = aAlfa2 % -102;
               aAlfa = "    AVISO: El centro " + aAlfa2 + " no existe en la ";
               aAlfa = aAlfa + "empresa " + aAlfa1 + ". ";
               aAlfa = aAlfa + "Revise sus datos si es necesario";
               |MENAV aAlfa;
          |FINSI;
          |CIERRA #labcentr;
     |FINSI;
|FPRC;

|PROCESO Centros;
     nCampo = nCampo + 1;
     #0nCampo = #labcentr#1;
     |PINTA #0nCampo;
     |CAMPO_MODIFICA #0nCampo, 1, "S";
     nCampo1 = nCampo + 10;
     |CAMPO_MODIFICA #0nCampo1, 1, "S";
|FPRC;

|DEFBUCLE Centros;
     #labcentr#1;
     ;
     #0#0, 000;
     #0#0, 999;
     ;
     NULL, Centros;
|FIN;

|PROCESO BuscaCentros; |TIPO 0;
     |PARA nCampo = 4; |SI nCampo [ 23; |HACIENDO nCampo = nCampo + 1;
          #0nCampo = 0;
          |PINTA #0nCampo;
     |SIGUE;
     nCampo = 3;
     |BUCLE Centros;
     nCampo = nCampo + 1;
     |PARA nCampo1 = nCampo; |SI nCampo1 [ 13; |HACIENDO nCampo1 = nCampo1 + 1;
          |CAMPO_MODIFICA #0nCampo1, 1, "N";
          nCampo2 = nCampo1 + 10;
          |CAMPO_MODIFICA #0nCampo2, 1, "N";
     |SIGUE;
|FPRC;

/************** PROCESOS PARA PRESENTAR EL LINEAL ***************************/

|PROCESO BuscaRepes;
     |SI nAntEmpDes = #100#4; |Y nAntTraDes = #100#5;
          swRepes = 0;
          aAlfa1 = nAntEmpOri;
          aAlfa2 = nAntTraOri;
          aAlfa3 = #100#0;
          aAlfa4 = #100#1;
          aAlfa5 = nAntEmpDes;
          aAlfa6 = nAntTraDes;
          aAlfa1 = ("00000" + aAlfa1) % -105;
          aAlfa2 = ("00000" + aAlfa2) % -105;
          aAlfa3 = ("00000" + aAlfa3) % -105;
          aAlfa4 = ("00000" + aAlfa4) % -105;
          aAlfa5 = ("00000" + aAlfa5) % -105;
          aAlfa6 = ("00000" + aAlfa6) % -105;
          |ERROR10;
     |FINSI;
     nAntEmpOri = #100#0;
     nAntTraOri = #100#1;
     nAntEmpDes = #100#4;
     nAntTraDes = #100#5;
|FPRC;

|DEFBUCLE BuscaRepes;
     #100#2;
     ;
     "*", 00001, 00001;
     "*", 99999, 99999;
     ;
     NULL, BuscaRepes;
|FIN;

|PROCESO CompRepes;
     swRepes = 1;
     nAntEmpOri = 0;
     nAntTraOri = 0;
     nAntEmpDes = 0;
     nAntTraDes = 0;
     |BUCLE BuscaRepes;
     |SI swRepes = 0;
          aAlfa = "    ATENCION. Los trabajadores " + aAlfa1 + "." + aAlfa2;
          aAlfa = aAlfa + " y " + aAlfa3 + "." + aAlfa4 + " tienen el mismo codigo";
          aAlfa = aAlfa + " destino: " + aAlfa5 + "." + aAlfa6;
          |MENAV aAlfa;
          aAlfa = "    Es necesario repasar la lista.";
          |MENAV aAlfa;
     |FINSI;
|FPRC;

|PROCESO Cambia;
     |ABRE #labtraba;
     #labtraba#0 = #100#4;
     #labtraba#1 = #100#5;
     |LEE 000#labtraba.=;
     |SI FSalida = 0;
          #100#7 = " ";
          #100#8 = "N";
     |SINO;
          #100#8 = " ";
     |FINSI;
     |GRABA 020#100a;
     |CIERRA #labtraba;
|FPRC;

|PROCESO Marca; |TIPO 11;
     |SI FSalida = 9;
          |SI #100#7 = "*";
               #100#7 = " ";
          |SINO;
               |SI #100#8 ! "N";
                    #100#7 = "*";
               |SINO;
                    |SI #100#5 ! 0;
                         aAlfa = "    Este trabajador ya existe en la empresa destino. ";
                         aAlfa = aAlfa + " Cambie el codigo y vuelva a intentarlo.";
                    |SINO;
                         aAlfa = "    El codigo 00000 no es correcto. Cambie ";
                         aAlfa = aAlfa + "el codigo y vuelva a intentarlo";
                    |FINSI;
                    |MENAV aAlfa;
               |FINSI;
          |FINSI;
          |PINTA #100#7;
          |ERROR;
          |GRABA 020#100a;
     |FINSI;
|FPRC;

|PROCESO Tipo5; |TIPO 5;
     #100#8 = "N";
     |LINEAS_BI_ATRI #100#8, S, D, -1;
|FPRC;

|PROCESO inferior;
     #100#0 = 00001;
     #100#1 = 00001;
|FPRC;

|PROCESO superior;
     #100#0 = 99999;
     #100#1 = 99999;
|FPRC;

|PROCESO MuestraTrab;
     |ENTLINEAL #1#100, -1, 2, 21, 1, inferior, superior;
|FPRC;


/************** PROCESOS PARA LA RELLENAR EL TEMPORAL ***********************/

|PROCESO CentroDestino;
     |PARA nCampo1 = 4; |SI nCampo1 [ 13; |HACIENDO nCampo1 = nCampo1 + 1;
          nCampo2 = nCampo1 + 10;
          |SI #0nCampo1 = #labtraba#77;
               nCentroDes = #0nCampo2;
               |SAL;
          |FINSI;
     |SIGUE;
|FPRC;

|PROCESO Rellena;
     |ABRE #100;
     #100#0 = #labtraba#0;         || empresa origen
     #100#1 = #labtraba#1;         || trabajador origen
     #100#2 = #labtraba#2;         || nombre trabajador
     #100#3 = #labtraba#77;        || centro origen
     #100#4 = #0#3;                || empresa destino
     #100#5 = #labtraba#1 + #0#25; || trabajador destino
     |HAZ CentroDestino;
     #100#6 = nCentroDes;          || centro destino
     |SI #0#26 = "S";
          #100#7 = "*";            || seleccionado S/N (si - "*")
     |SINO;
          #100#7 = " ";            || seleccionado S/N (no - " ")
     |FINSI;

     #500#0 = #100#4;
     #500#1 = #100#5;
     |LEE 000#500=;
     |SI FSalida = 0;
          #100#7 = " ";
          #100#8 = "N";       || trabajador incorrecto ("N"): existe
     |SINO;
          #100#8 = " ";       || trabajador correcto (" "): no existia
     |FINSI;

     #100#9 = #labtraba#7;    || DNI, me hara falta para los nomir
     |SI #100#5 > 99999;
          swSePasa = 1;
          #100#5 = 0;
          nLimite = 99999 - #100#1;

          #100#7 = " ";
          #100#8 = "N";       || trabajador incorrecto ("N"): existe
     |FINSI;
     |GRABA 020#100n;
|FPRC;

|DEFBUCLE Rellena;
     #labtraba#1;
     44;
     #0#0, #0#1, aAlfa1;
     #0#0, #0#2, aAlfa2;
     ;
     NULL, Rellena;
|FIN;

|PROCESO RellenaTemporal;
     |ABRE #labtraba;
     aAlfa = Usuario; |QBF aAlfa;    aAlfa = "T" + aAlfa;
     |NOME_DAT #100 aAlfa;
     |DELINDEX #100;

     |DBASS aRutaDef;
     |DBASS aRutaDatos;
     |QBF aRutaDef;
     |QBF aRutaDatos;
     aRutaDef = aRutaDef + "laboral/def/labtraba.mas";
     |CARGA_DEF aRutaDef, copiadef@;
     |SI FSalida ! 0;
          |MENAV "    Error en CARGADEF: labtraba !!!";
          |ACABA;
     |SINO;
          aRutaDatos = aRutaDatos + "laboral/fich/";
          |PATH_DAT #500 aRutaDatos;
          |ABRE #500;
     |FINSI;

     |SI #0#24 = "A";
          aAlfa1 = "A";
          aAlfa2 = "A";
     |SINO;
          aAlfa1 = "A";
          aAlfa2 = "B";
     |FINSI;
     swSePasa = 0;

     |BUCLE Rellena;
     |CIERRA #labtraba;
     |CIERRA #500;
     |DESCARGA_DEF #500;

     |SI swSePasa = 1;
          aAlfa = "    ATENCION: Los codigos de destino generados exceden el ";
          aAlfa = aAlfa + "99999";
          |MENAV aAlfa;
          aAlfa = nLimite;
          aAlfa = "    El limite de generacion de codigos es " + aAlfa;
          aAlfa = aAlfa + ". Repase los trabajadores con codigo destino cero."
          |MENAV aAlfa;
     |FINSI;
|FPRC;

/****************************************************************************/

/**************************** PROCESO DE COPIA ******************************/

/*
|PROCESO RecogeDatosDef;
     aQueQuiero = "|K001";
     aResultado = #501aQueQuiero;
     aNCCla = aResultado % 103;
     nNCCla = aNCCla;
     |PARA nCampo = 1; |SI nCampo [ nNCCla; |HACIENDO nCampo = nCampo + 1;

     |SIGUE;
|FPRC;
*/

|PROCESO GrabaNuevo26;
     #501#0 = #100#4;
     #501#1 = #100#5;

     |SI aDefCopia = "vipe0002";
         |HAZ CambiaCentro;
     |FINSI;

     |GRABA 020#501n;
     |LIBERA #501;
|FPRC;

|PROCESO GrabaNuevo25;
     aAlfa = " " * 75;
     aAlfa1 = #100#4; aAlfa1 = "00000" + aAlfa1; aAlfa1 = aAlfa1 % -105;
     aAlfa1 = aAlfa1 + aAlfa;
     aAlfa2 = #100#5; aAlfa2 = "00000" + aAlfa2; aAlfa2 = aAlfa2 % -105;
     aAlfa2 = aAlfa2 + aAlfa;
     |BORRA 020#501a;
     #501#1 = #100#4;
     #501#2 = #100#5;
     |GRABA 020#501n;
     |LIBERA #501;
|FPRC;

|PROCESO GrabaNuevo24;
     |BORRA 020#501a;
     #501#1 = #100#4;
     #501#2 = #100#5;
     |GRABA 020#501n;
     |LIBERA #501;
|FPRC;

|PROCESO GrabaNuevo23;
     #501#5 = #100#4;
     #501#6 = #100#5;
     |GRABA 020#501n;
     |LIBERA #501;
|FPRC;

|PROCESO GrabaNuevo22;
     #501#1 = #100#4;
     #501#2 = #100#5;
     |GRABA 020#501a;
     |LIBERA #501;
|FPRC;

|PROCESO GrabaNuevo15;
     #501#0 = #100#9;
     #501#44 = #100#4;
     |GRABA 000#501n;
     |LIBERA #501;
|FPRC;

|PROCESO GrabaNuevo13;
     #501#0 = #100#9;
     #501#41 = #100#4;
     |GRABA 000#501n;
     |LIBERA #501;
|FPRC;

|PROCESO GrabaNuevo11;
     #501#0 = #100#9;
     #501#2 = #100#4;
     #501#3 = #100#5;
     |GRABA 020#501n;
     |LIBERA #501;
|FPRC;

|PROCESO GrabaNuevo20;
     #501#77 = #100#4;
     #501#78 = #100#5;
     |GRABA 020#501n;
     |LIBERA #501;
|FPRC;

|PROCESO GrabaNuevo;
     #501#0 = #100#4;
     #501#1 = #100#5;
     |SI aDefCopia = "vipe0007";
         |HAZ CambiaCentro;
     |FINSI;
     |GRABA 020#501n;
     |LIBERA #501;
|FPRC;

|DEFBUCLE Mas4;
     #501#1006;
     ;
     #100#0, #100#1, nDesde1, nDesde2, nDesde3, nDesde4;
     #100#0, #100#1, nHasta1, nHasta2, nHasta3, nHasta4;
     be;
     NULL, GrabaNuevo;
|FINSI;

|DEFBUCLE Mas3;
     #501#1005;
     ;
     #100#0, #100#1, nDesde1, nDesde2, nDesde3;
     #100#0, #100#1, nHasta1, nHasta2, nHasta3;
     be;
     NULL, GrabaNuevo;
|FINSI;

|DEFBUCLE Mas26;
     #501#1003;
     ;
     #100#0, #100#1, "01.01.1900";
     #100#0, #100#1, "31.12.2999";
     be;
     NULL, GrabaNuevo26;
|FINSI;

|DEFBUCLE Mas25;
     #501#1002;
     1, 2;
     000001, "  ", aDesde1, aDesde2;
     999999, "zz", aHasta1, aHasta2;
     be;
     NULL, GrabaNuevo25;
|FINSI;

|DEFBUCLE Mas24;
     #501#1003;
     ;
     #100#0, #100#1, "01.01.1900";
     #100#0, #100#1, "31.12.2999";
     be;
     NULL, GrabaNuevo24;
|FINSI;

|DEFBUCLE Mas23;
     #501#1007;
     ;
     "labtraba", 000, aDesde1, aDesde2, aDesde3, aDesde3, aDesde3;
     "labtraba", 999, aDesde1, aDesde2, aDesde3, aDesde3, aDesde3;
     be;
     NULL, GrabaNuevo23;
|FINSI;

|DEFBUCLE Mas22;
     #501#2002;
     ;
     #100#0, #100#1;
     #100#0, #100#1;
     be;
     NULL, GrabaNuevo22;
|FINSI;

|DEFBUCLE Mas20;
     #501#1004;
     ;
     #100#0, #100#1, aDesde1, nDesde2;
     #100#0, #100#1, aHasta1, nHasta2;
     be;
     NULL, GrabaNuevo20;
|FINSI;

|DEFBUCLE Mas2;
     #501#1004;
     ;
     #100#0, #100#1, nDesde1, nDesde2;
     #100#0, #100#1, nHasta1, nHasta2;
     be;
     NULL, GrabaNuevo;
|FINSI;

|DEFBUCLE Mas15;
     #501#1003;
     ;
     #100#9, 1900, #100#0;
     #100#9, 2999, #100#0;
     be;
     NULL, GrabaNuevo15;
|FINSI;

|DEFBUCLE Mas13;
     #501#1003;
     ;
     #100#9, 1900, #100#0;
     #100#9, 2999, #100#0;
     be;
     NULL, GrabaNuevo13;
|FINSI;

|DEFBUCLE Mas11;
     #501#1003;
     ;
     #100#9, #100#0, #100#1;
     #100#9, #100#0, #100#1;
     be;
     NULL, GrabaNuevo11;
|FINSI;

|DEFBUCLE Mas1;
     #501#1003;
     ;
     #100#0, #100#1, nDesde1;
     #100#0, #100#1, nHasta1;
     be;
     NULL, GrabaNuevo;
|FINSI;

|PROCESO CambiaCentro;
     |PARA nCampo1 = 4; |SI nCampo1 [ 13; |HACIENDO nCampo1 = nCampo1 + 1;
           nCampo2 = nCampo1 + 10;
           |SI aDefCopia = "labtraba";
               |SI #501#77 = #0nCampo1;
                   #501#77 = #0nCampo2;
                   |SAL;
               |FINSI;
           |FINSI;
           |SI aDefCopia = "vipe0002";
               |SI #501#11 = #0nCampo1;
                   #501#11 = #0nCampo2;
                   |SAL;
               |FINSI;
           |FINSI;
           |SI aDefCopia = "vipe0007";
               |SI #501#26 = #0nCampo1;
                   #501#26 = #0nCampo2;
                   |SAL;
               |FINSI;
           |FINSI;
     |SIGUE;
|FPRC;

|PROCESO Mas0;
     #501#0 = #100#0;
     #501#1 = #100#1;
     |LEE 000#501=;
     |SI FSalida = 0;
          |SI aDefCopia = "labtraba";
               |HAZ CambiaCentro;

               |SI nMoverDoc = 1;
                   aAlfa = "CAMB";
                   aAlfa = aAlfa + (("000000000000000" + #100#0) % -115);
                   aAlfa = aAlfa + (("000000000000000" + #100#4) % -115);
                   aAlfa = aAlfa + (("00000" + #100#1) % -105);
                   aAlfa = aAlfa + (("00000" + #100#5) % -105);
                   aAlfa = "|:dsarchi/dsarz016;" + aAlfa;
                   |SUB_EJECUTA aAlfa;
               |FINSI;
          |FINSI;

          #501#0 = #100#4;
          #501#1 = #100#5;
          |GRABA 020#501n;
     |FINSI;
     |LIBERA #501;
|FPRC;

|PROCESO Copiando;
     |SI nTipo = 0;      |HAZ Mas0;          |FINSI;
     |SI nTipo = 1;      |BUCLE Mas1;        |FINSI;
     |SI nTipo = 2;      |BUCLE Mas2;        |FINSI;
     |SI nTipo = 3;      |BUCLE Mas3;        |FINSI;
     |SI nTipo = 4;      |BUCLE Mas4;        |FINSI;
     |SI nTipo = 20;     |BUCLE Mas20;       |FINSI;
     ||SI nTipo = 21;     |BUCLE Mas21;       |FINSI;
     |SI nTipo = 22;     |BUCLE Mas22;       |FINSI;
     |SI nTipo = 23;
          aAlfa  = " " * 30;
          aDesde1 = #100#0;
          aDesde1 = (aDesde1 + aAlfa) % 130;
          aDesde2 = #100#1;
          aDesde2 = (aDesde2 + aAlfa) % 130;
          aDesde3 = " " * 30;
          |BUCLE Mas23;
     |FINSI;
     |SI nTipo = 24;     |BUCLE Mas24;       |FINSI;
     |SI nTipo = 26;     |BUCLE Mas26;       |FINSI;
     |SI nTipo = 25;
          aAlfa = " " * 80;
          aDesde1 = #100#0;
          aDesde1 = (aDesde1 + aAlfa) % 180;
          aHasta1 = aDesde1;
          aDesde2 = #100#1;
          aDesde2 = (aDesde2 + aAlfa) % 180;
          aHasta2 = aDesde2;
          |BUCLE Mas25;
     |FINSI;
     |SI nTipo = 11;     |BUCLE Mas11;       |FINSI;
     |SI nTipo = 13;     |BUCLE Mas13;       |FINSI;
     |SI nTipo = 15;     |BUCLE Mas15;       |FINSI;
     |SI nContador ! 100000;
          nContador = nContador + 1;
          aContador = nContador;
     |FINSI;
|FPRC;

|DEFBUCLE CargaCopia;
     #100#1;
     7;
     00001, 00001, "*";
     99999, 99999, "*";
     ;
     NULL, Copiando;
|FIN;

|PROCESO CargaCopia;
     |DBASS aRutaDef;
     |DBASS aRutaDatos;
     |QBF aRutaDef;
     |QBF aRutaDatos;
     aRutaDef = aRutaDef + "laboral/def/" + aDefCopia + ".mas";
     |CARGA_DEF aRutaDef, copiacop@;
     |SI FSalida ! 0;
          |SI aDefCopia = "vipe0002"; |O aDefCopia = "vipe0007";
              |ACABA;
          |SINO;
              aAlfa = "    Error en CARGADEF: " + aDefCopia + " !!!";
              |MENAV aAlfa;
              |ACABA;
          |FINSI;
     |FINSI;

     aRutaDatos = aRutaDatos + "laboral/fich/";
     |PATH_DAT #501 aRutaDatos;
     |ABRE #501;

     |BUCLE CargaCopia;

     |CIERRA #501;
     |DESCARGA_DEF #501;
|FPRC;


|| empresa y trabajador en clave principal en campos 1 y 2 de la clave:
|| tipo 0: Clave formada por empresa y trabajador
|| tipo 1: Clave formada por empresa y trabajador y 1 campo mas
|| tipo 2: Clave formada por empresa y trabajador y 2 campo mas
|| tipo 20: Clave formada por empresa y trabajador y 2 campo mas, especial
||        para nomatcon, 3 campo clave es alfanumerico
|| tipo 3: Clave formada por empresa y trabajador y 3 campo mas
|| tipo 4: Clave formada por empresa y trabajador y 4 campo mas

|| tipo 11: nomir001
|| tipo 13: nomir003
|| tipo 15: nomir005

|| tipo 21: nomrevli
|| tipo 22: labregis
|| tipo 23: labvaltb
|| tipo 24: labparma y labparhi
|| tipo 25: labhisto

|PROCESO LaCopia;
     nContador = 0;
     aDefCopia = "labtraba"; nTipo = 0; |HAZ CargaCopia;

     nContador = 100000;
     aDefCopia = "nomtraca"; nTipo = 0; |HAZ CargaCopia;

     aDefCopia = "nomtrali";
     nTipo = 1;
     nDesde1 = 101; nHasta1 = 999;
     |HAZ CargaCopia;

     aDefCopia = "nomtpsem"; nTipo = 0; |HAZ CargaCopia;

     aDefCopia = "nomepitr";
     nTipo = 1;
     nDesde1 = 000; nHasta1 = 999;
     |HAZ CargaCopia;

     aDefCopia = "nomauxc3"; nTipo = 2;
     nDesde1 = 0000; nHasta1 = 2999;
     nDesde2 = 00; nHasta2 = 12;
     |HAZ CargaCopia;

     aDefCopia = "nomauxl3";
     nTipo = 3;
     nDesde1 = 0000; nHasta1 = 2999;
     nDesde2 = 00; nHasta2 = 12;
     nDesde3 = 000; nHasta3 = 999;
     |HAZ CargaCopia;

     nDesde1 = 01; nHasta1 = 12;
     nDesde2 = 0; nHasta2 = 9;
     aDefCopia = "nomvarca"; nTipo = 2; |HAZ CargaCopia;

     nDesde1 = 01; nHasta1 = 12;
     nDesde2 = 0; nHasta2 = 9;
     nDesde3 = 101; nHasta3 = 999;
     aDefCopia = "nomvarli"; nTipo = 3; |HAZ CargaCopia;

     nDesde1 = 01; nHasta1 = 12;
     nDesde2 = 0; nHasta2 = 9;
     aDefCopia = "nomcalca"; nTipo = 2; |HAZ CargaCopia;

     nDesde1 = 01; nHasta1 = 12;
     nDesde2 = 0; nHasta2 = 9;
     nDesde3 = 101; nHasta3 = 999;
     aDefCopia = "nomcalli"; nTipo = 3; |HAZ CargaCopia;

     nDesde1 = 2002; nHasta1 = 3999;
     nDesde2 = 01; nHasta2 = 12;
     aDefCopia = "gomdetjr"; nTipo = 2; |HAZ CargaCopia;

     nDesde1 = 00; nHasta1 = 99;
     nDesde2 = 01; nHasta2 = 12;
     nDesde3 = 0;  nHasta3 = 99;
     aDefCopia = "nomhicca"; nTipo = 3; |HAZ CargaCopia;

     nDesde1 = 00; nHasta1 = 99;
     nDesde2 = 01; nHasta2 = 12;
     nDesde3 = 0;  nHasta3 = 99;
     nDesde4 = 101;  nHasta4 = 999;
     aDefCopia = "nomhicli"; nTipo = 4; |HAZ CargaCopia;

     nDesde1 = 00; nHasta1 = 99;
     aDefCopia = "nomtraac"; nTipo = 1; |HAZ CargaCopia;

     nDesde1 = 101; nHasta1 = 999;
     nDesde2 = 00; nHasta2 = 99;
     aDefCopia = "nomtraal"; nTipo = 2; |HAZ CargaCopia;

     nDesde1 = 01; nHasta1 = 12;
     nDesde2 = 0; nHasta2 = 9;
     aDefCopia = "nomcalac"; nTipo = 2; |HAZ CargaCopia;

     nDesde1 = 01; nHasta1 = 12;
     nDesde2 = 0; nHasta2 = 9;
     nDesde3 = 101; nHasta3 = 999;
     aDefCopia = "nomcalal"; nTipo = 3; |HAZ CargaCopia;

     aDesde1 = "    "; aHasta1 = "zzzz";
     nDesde2 = 01; nHasta2 = 12;
     aDefCopia = "nomatcon"; nTipo = 20; |HAZ CargaCopia;

     aDefCopia = "nomir001"; nTipo = 11; |HAZ CargaCopia;

     aDefCopia = "nomir003"; nTipo = 13; |HAZ CargaCopia;

     aDefCopia = "nomir005"; nTipo = 15; |HAZ CargaCopia;

     aDefCopia = "nomir007"; nTipo = 0; |HAZ CargaCopia;

     aDefCopia = "nomir008"; nTipo = 0; |HAZ CargaCopia;

     aDefCopia = "labbasec"; nTipo = 0; |HAZ CargaCopia;

     nDesde1 = 001; nHasta1 = 999;
     aDefCopia = "labbasel"; nTipo = 1; |HAZ CargaCopia;

     aDefCopia = "nomtrahx"; nTipo = 0; |HAZ CargaCopia;

     || aDefCopia = "nomrevli"; nTipo = 21; |HAZ CargaCopia;

     aDefCopia = "nombotmp"; nTipo = 0; |HAZ CargaCopia;

     aDefCopia = "labpadre"; nTipo = 0; |HAZ CargaCopia;

     aDefCopia = "labregis"; nTipo = 22; |HAZ CargaCopia;
     || el labregis se machaca, no creo nuevos codigos de registro

     aDefCopia = "labhisto"; nTipo = 25; |HAZ CargaCopia;

     aDefCopia = "labvaltb"; nTipo = 23; |HAZ CargaCopia;

     aDefCopia = "labparma"; nTipo = 24; |HAZ CargaCopia;

     aDefCopia = "labparhi"; nTipo = 24; |HAZ CargaCopia;

     || ... Def de Vipei, si procede
     aDefCopia = "vipe0002"; nTipo = 26; |HAZ CargaCopia;

     nDesde1 = 1900; nHasta1 = 2999;
     nDesde2 = 00;   nHasta2 = 99;
     aDefCopia = "vipe0007"; nTipo = 2; |HAZ CargaCopia;
|FPRC;

/****************************************************************************/


|PROGRAMA;
     |CLS;
     |PINPA #0#0;
     |PINDA #0#0;
     |ENDATOS #1#0;
     |SI FSalida = 0;
          || EMPIEZA EL SHOW
          |HAZ RellenaTemporal;
          |PARA; |SI swRepes = 0; |HACIENDO;
               |HAZ MuestraTrab;
               |HAZ CompRepes;
          |SIGUE;
          |CONFI "    SEsta seguro de continuar con la copia de los trabajadores seleccionados?";
          |SI FSalida = 0;
               nMoverDoc = 0;
               |HAZ BusquedaApli;
               |SI eaPathDsArchi ! "";
                   |CONFI "0000NMover los documentos del trabajador a la nueva empresa";
                   |SI FSalida = 0;  nMoverDoc = 1;  |FINSI;
               |FINSI;

               |HAZ LaCopia;
               aAlfa = "    Se ha completado el proceso de copia en " + aContador;
               aAlfa = aAlfa + " trabajadores";
               |MENAV aAlfa;
          |FINSI;
     |FINSI;
|FPRO;
