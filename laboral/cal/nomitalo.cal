|FICHEROS;
    nomitalo #0;
    labempre #1;
    labtraba #2;
    nomcalca #3;
    nomtmlon #10;        || tmp. vencimientos

    nomhicca #4;
    nomcalac #5;
|FIN;

|VARIABLES;
    mensaje3="";
    antempre = 0;
    anttraba = 0;
    swsalta = 0;
    mesos=0;
    alguna=0;
    seleccio = 0;
    swresu = 0;
    devenga = 0;
    deducir = 0;
    swcab = 0;
    numero = 0;
    swbaja = 0;
    mes = 0;
    any = 0;
    dia = 0;
    fecsys = @;
    fecalf = "";
    alfa1 = "";
    p_mes = "";
    mensaje1 = "    ATENCION!!! Esta empresa emite paga y mes en un solo recibo ...";
    mensaje2 = "    ATENCION!!! Coloque el papel para el Resumen de Talones ...";
    &liquido = 0;
    &talon = "";
    &recibi = @;
    &vtos = "";
    linea = 0;
    sw_vtos = 0;
    fech1 = @;
    alfa2 = "";
    &diav = 0;
    &mesv = "";
    &anyv = 0;
    bisiesto = 0;
    anyo = 0;
    topemes = 0;
    nume1 = 0;
     &nEmpPag = 0;
     &nAnyPag = 0;
     &nMesPag = 0;
     &nTipPag = 0;
     &aNorPag = "";
     &nTraPag = 0;
     &fFecPag = @;
     &aRegPag = "";
     nEmbargo = 0;
|FIN;

|INCLUYE i_cuadr;

|PROCESO PParametro; |TIPO 7;
     |SI PARAMETRO="M"; |O PARAMETRO="H"; |O PARAMETRO="A";
          |CAMPO_MODIFICA #0#13,1,"N";
          #0#13=PARAMETRO;
     |FINSI;
|FPRC;


|| ************************************************************************
||                    CALCULOS DESDE EL MANTENIMIENTO
|| ************************************************************************

|PROCESO ControlaTipo;|TIPO 0;
     |SI #0#13="H"; |O #0#13="M";
          |SI #0#8!0; |Y #0#8!2;
               |MENAV "0000Tipo de Talon no Valido!";
               |ERROR;
          |FINSI;
     |SINO;
          |SI #0#8!1; |Y #0#8!2;
               |MENAV "0000Tipo de Talon no Valido!";
               |ERROR;
          |FINSI;
     |FINSI;
|FPRC;


|PROCESO FuenteDatos;|TIPO 0;
     |SI #0#13="M"; |O #0#13="H";
          |SI #0#13="M";
               alfa1="MENSUAL  ";
          |SINO;
               alfa1="HISTORICO";
          |FINSI;

          #0#8=0;

          |CAMPO_VISUAL #0#3,1,"S";
          |CAMPO_VISUAL #0#1,1,"S";

          |CAMPO_VISUAL #0#11,1,"N";
          |CAMPO_VISUAL #0#12,1,"N";


          |PINTA 1318,"Mes ................:                       ";
          |PINTA 1618,"Mes / Paga .........:                       ";
          |PINTA 1818,"Tipo talones (0/2) .:";

          |PINTA #0#1;
          |PINTA #0#3;
          |PINTA #0#8;
     |FINSI;
     |SI #0#13="A";
          alfa1="ATRASOS  ";

          #0#1="M";
          #0#8=1;
          |CAMPO_VISUAL #0#3,1,"N";
          |CAMPO_VISUAL #0#1,1,"N";

          |CAMPO_VISUAL #0#11,1,"S";
          |CAMPO_VISUAL #0#12,1,"S";


          |PINTA 1318,"Desde Mes .....:     Hasta Mes .....:       ";
          |PINTA 1618,"                                            ";
          |PINTA 1818,"Tipo talones (1/2) .:";

          |PINTA #0#11;
          |PINTA #0#12;
          |PINTA #0#8;
     |FINSI;

     |PINTA 0738,alfa1;
     |PINTA #0#13;
|FPRC;


|PROCESO defecto; |TIPO 7; ||calcula el a¤o por defecto segun fecha sistema
    fecsys = ~;
    alfa1 = fecsys;
    alfa1 = alfa1 % -102;
    #0Campo = alfa1;
|PINTA #0Campo;
|FPRC;

|PROCESO mirames; |TIPO 7; ||CALCULA EL MES POR DEFECTO SEGUN FECHA SISTEMA
    fecsys = ~;
    alfa1 = fecsys;
    alfa1 = alfa1 % 402;
    #0Campo = alfa1;
|PINTA #0Campo;
|FPRC;

|PROCESO pintames; |TIPO 0;      || PINTA EL MES LITERAL
|SI #0#4 < 80;
        anyo = #0#4 + 2000;
|SINO;
        anyo = #0#4 + 1900;
|FINSI;

    nume1 = anyo $ 4;
|SI nume1 = 0;
        bisiesto = 1;
|SINO;
        bisiesto = 0;
|FINSI;
|SI #0Campo =  1;  p_mes = "ENERO     ";  topemes = 31; |FINSI;
|SI #0Campo =  2;  p_mes = "FEBRERO   ";  topemes = 28 + bisiesto; |FINSI;
|SI #0Campo =  3;  p_mes = "MARZO     ";  topemes = 31; |FINSI;
|SI #0Campo =  4;  p_mes = "ABRIL     ";  topemes = 30; |FINSI;
|SI #0Campo =  5;  p_mes = "MAYO      ";  topemes = 31; |FINSI;
|SI #0Campo =  6;  p_mes = "JUNIO     ";  topemes = 30; |FINSI;
|SI #0Campo =  7;  p_mes = "JULIO     ";  topemes = 31; |FINSI;
|SI #0Campo =  8;  p_mes = "AGOSTO    ";  topemes = 31; |FINSI;
|SI #0Campo =  9;  p_mes = "SEPTIEMBRE";  topemes = 30; |FINSI;
|SI #0Campo = 10;  p_mes = "OCTUBRE   ";  topemes = 31; |FINSI;
|SI #0Campo = 11;  p_mes = "NOVIEMBRE ";  topemes = 30; |FINSI;
|SI #0Campo = 12;  p_mes = "DICIEMBRE ";  topemes = 31; |FINSI;
|PINTA 1443, p_mes;
|FPRC;

|PROCESO miratipo; |TIPO 0;   || CHEQUEA MES/PAGA SEGUN LA CLAVE RECIBO EXTRA
|SI #0Campo = "P"; |Y #1#25 = 2;
    |MENAV mensaje1;
    |ERROR;
|FINSI;
|FPRC;

|PROCESO noentres; |TIPO 7;
     |SI #0#1 = "M";
         |CAMPO_MODIFICA #0#8, 1, "S";
     |SINO;
         |CAMPO_MODIFICA #0#8, 1, "N";
     |FINSI;

     |SI #0#13="M"; |O #0#13="H";
          |MENSA "0000Entre: (0) talones del mes; (2) talones finiquitos";
     |SINO;
          |MENSA "0000Entre: (1) talones por meses; (2) talones refundidos";
     |FINSI;
|FPRC;

|PROCESO no_vtos; |TIPO 7;
    alfa1 = #0#9;
|CAMPO_MODIFICA #0#10, 1, alfa1;
|FPRC;

|| ************************************************************************
||                INTRODUCCION DE VENCIMIENTOS POR TALON
|| ************************************************************************

|PROCESO lee_vtos1;
    liquido = #10#4;

|DDEFECTO #2;
    #2#0 = #10#1;
    #2#1 = #10#2;
|LEE 020#2=;

    fech1 = #10#5 % 0;
    alfa1 = fech1;            mesv = alfa1 % 11;  || mes en letra

    alfa1 = #10#5;
    alfa2 = alfa1 % 102;      diav = alfa2;       || dia
    alfa2 = alfa1 % -104;     anyv = alfa2;       || a¤o

|HAZ imp_talon;
|FPRC;

|DEFBUCLE lee_vtos;
 #10#1;
 ;
     1;
 99999;
 ;
 NULL, lee_vtos1;
|FIN;

|PROCESO min;
    #10#0 = 1;
|FPRC;

|PROCESO max;
    #10#0 = 99999;
|FPRC;

|PROCESO edi_vtos;
|CIERRA #10;

|PUSHV 0501 2380;
|ENTLINEAL #1#10, 1, 3, 20, 1, min, max;
|POPV;
|BUCLE lee_vtos;
|FPRC;

|PROCESO gra_vtos;
|SI sw_vtos = 0;
        sw_vtos = 1;
        alfa1 = Usuario;     |QBT alfa1;
    |NOME_DAT #10 alfa1;
    |DELINDEX #10;
    |ABRE #10;
|FINSI;

|DDEFECTO #10;
    linea = linea + 1;
    #10#0 = linea;       || linea
    #10#1 = #2#0;        || empresa
    #10#2 = #2#1;        || trabajador
    #10#3 = #2#2;        || nombre
    #10#4 = liquido;     || importe liquido
    #10#5 = #0#10;       || vto. por defecto
|GRABA 020#10n;
|FPRC;

|| ************************************************************************
||                           RUTINAS DEL DEFBUCLE
|| ************************************************************************

|PROCESO RegPagado;
     nEmpPag = #labtraba#0;
     nAnyPag = 2000 + #0#4;
     |SI #0#13 = "M";
          nMesPag = #nomcalca#2;
          nTipPag = #nomcalca#3;
     |FINSI;
     |SI #0#13 = "H";
          nMesPag = #nomhicca#2;
          nTipPag = #nomhicca#3;
     |FINSI;
     |SI #0#13 = "A";
          nMesPag = #nomcalac#2;
          nTipPag = #nomcalac#3;
     |FINSI;
     |SI #0#1 = "M";
          aNorPag = "N";
     |SINO;
          aNorPag = "P";
     |FINSI;
     nTraPag = #labtraba#1;
     fFecPag = ~;
     aRegPag = "";
     |HAZ Pagado;
|FPRC;

|PROCESO imp_talon;
|INFOR "nomitalo";
|PRINT;
|PIEINF;
|FININF;
     |HAZ RegPagado;
|FPRC;

|PROCESO talonesM;     || EMITE TALONES
|HAZ leetrabaM;
|SI #2#70 = "L";
        seleccio = 1;
    |SI #0#1 = "M";  |HAZ mensualM;  |FINSI;
    |SI #0#1 = "P";  |HAZ pagasM;    |FINSI;
        liquido = devenga - deducir;
    |SI #0#9 = "N";
        |HAZ imp_talon;
    |SINO;
        |HAZ gra_vtos;
    |FINSI;
|FINSI;
|FPRC;

|PROCESO cierra;
|SI swresu = 0; |Y seleccio = 1;
    |SI #0#9 = "S";
        |HAZ edi_vtos;
    |FINSI;
    |FINIMP;
    |MENAV mensaje2;
    |IMPRE 0;
    |SI FSalida = 0;
            swresu = 1;
    |SINO;
            swresu = 2;
        |MENAV "    ATENCION!!! Resumen Abortado ...";
    |FINSI;
|FINSI;
|FPRC;

|PROCESO resumenM;   || EMITE RESUMEN TALONES
|SI seleccio = 1;|Y swresu = 1;
    |SI swcab = 0;
            numero = #0#2;
            swcab = 1;
        |INFOR "nomtalom";
    |FINSI;
    |HAZ leetrabaM;
    |HAZ nrotalon;
    |SI #2#70 = "L";
        |SI #0#1 = "M";  |HAZ mensualM;  |FINSI;
        |SI #0#1 = "P";  |HAZ pagasM;    |FINSI;
            liquido = devenga - deducir;
        |PRINT;
            numero = numero + 1;
    |FINSI;
|SINO;
    |ERROR10;
|FINSI;
|FPRC;

|DEFBUCLE nomtalon0;
 #3#1;
 ;
 #0#0, #0#5, #0#3, #0#8;
 #0#0, #0#6, #0#3, #0#8;
 ;
 NULL, talonesM, NULL, NULL, cierra, resumenM;
 #3#0, #3#1, #3#2;
|FIN;


|| ************************************************************************
||                         RUTINAS GENERALES
|| ************************************************************************

|PROCESO leetrabaM;    || LEE EL TRABAJADOR
|DDEFECTO #2;
    #2#0 = #3#0;
    #2#1 = #3#1;
|LEE 020#2=;

    swbaja = 0;
    fecalf = #3#5;
|HAZ desglosa;
|SI mes = #0#3; |Y any = #0#4; |Y dia ! topemes;    swbaja = 1;    |FINSI;
|FPRC;

|PROCESO nrotalon; || CONSTRUYE EL NRO. DE TALON
    alfa1 = "00000" + #0#0;
    alfa1 = alfa1 % -105;
    talon = alfa1 + "/";           || codigo empresa
    alfa1 = "00000" + #2#1;
    alfa1 = alfa1 % -105;
    talon = talon + alfa1 + "/";   || codigo trabajador
    alfa1 = "000" + numero;
    alfa1 = alfa1 % -103;          || nro. talon
    talon = talon + alfa1;
|FPRC;

|PROCESO mensualM;   || CALCULO LIQUIDO PARA CLAVE 'M'
     nEmbargo = #3#104;
     |SI #3#16 ! 0;
          nEmbargo = ((#3#104 * (#3#46 / (#3#46 + #3#47))) ! 2);
     |FINSI;
    devenga = #3#46;
    deducir = #3#48 +#3#49 +#3#50 +#3#51 +#3#52 +#3#53 +#3#54 +#3#75 +#3#89 + nEmbargo + #3#165;
|SI swbaja = 1; |O #1#25 = 2;
        devenga = devenga + #3#16;   || si es baja este mes o la paga va
        deducir = deducir + #3#17;   || /junto con el recibo, acumula la paga
    |SI #1#25 = 2;                   || si las pagas van en un solo recibo
        deducir = deducir + #3#86;   || mete anticipos de paga extra
    |FINSI;
|FINSI;
|FPRC;

|PROCESO pagasM;   || CALCULO LIQUIDO PARA CLAVE 'P'
     nEmbargo = #3#104;
     |SI nEmbargo ! 0;
          nEmbargo = #3#104 - ((#3#104 * (#3#46 / (#3#46 + #3#47))) ! 2);
     |FINSI;
    devenga = #3#16;
    deducir = #3#17 + #3#86 + nEmbargo;   || mete el anticipo de paga extra
|FPRC;

|PROCESO totalemp;
|PIEINF;
|FININF;
|FPRC;

|| ************************************************************************
||                            SUBRUTINAS
|| ************************************************************************

|PROCESO desglosa;     || DESGLOSA EN DIA, MES Y ANYO UNA FECHA DD.MM.AAAA
    alfa1 = fecalf %  102;   dia = alfa1;
    alfa1 = fecalf %  402;   mes = alfa1;
    alfa1 = fecalf % -104;   any = alfa1;
|SI alfa1 = "....";
        dia = 0;
        mes = 0;
        any = 0;
|FINSI;
|FPRC;

|PROCESO PMensual;
|HAZ cuadres;
|SI sw_cuadre = 0;
    |IMPRE 0;
    |SI FSalida = 0;
        |ABRE #2;
            recibi = #0#7;
            vtos = #0#9;
        |BUCLE nomtalon0;
        |SI seleccio = 1;|Y swresu = 1;
            |HAZ totalemp;
        |SINO;
            |SI seleccio = 0;
                |MENAV "    * SELECCION VACIA *";
            |FINSI;
        |FINSI;
        |CIERRA #2;
    |SINO;
        |MENAV "    ATENCION!!! Proceso Abortado ...";
    |FINSI;
    |FINIMP;
|FINSI;
|FPRC;

|DEFBUCLE nomhilon0;
 #4#1;
 ;
 #0#0, #0#5, #0#4, #0#3, #0#8;
 #0#0, #0#6, #0#4, #0#3, #0#8;
 ;
 NULL, talonesH, NULL, NULL, cierra, resumenH;
 #4#0, #4#1, #4#2;
|FIN;

|PROCESO leetrabaH;    || LEE EL TRABAJADOR
    #2#0 = #4#0;
    #2#1 = #4#1;
|LEE 020#2=;
|SI FSalida ! 0;
    |DDEFECTO #2;
|FINSI;
    swbaja = 0;
    fecalf = #4#5;
|HAZ desglosa;
|SI mes = #0#3; |Y any = #0#4; |Y dia ! topemes;    swbaja = 1;    |FINSI;
|FPRC;

|PROCESO mensualH;   || CALCULO LIQUIDO PARA CLAVE 'M'
     nEmbargo = #4#146;
     |SI #4#16 ! 0;
          nEmbargo = ((#4#146 * (#4#46 / (#4#46 + #4#47))) ! 2);
     |FINSI;
    devenga = #4#46;
    deducir = #4#48 +#4#49 +#4#50 +#4#51 +#4#52 +#4#53 +#4#54 +#4#114 +#4#131 + nEmbargo + #4#207;
|SI swbaja = 1; |O #1#25 = 2;
        devenga = devenga + #4#16;   || si es baja este mes o la paga va
        deducir = deducir + #4#17;   || /junto con el recibo, acumula la paga
    |SI #1#25 = 2;                   || si las pagas van en un solo recibo
        deducir = deducir + #4#128;  || mete anticipos de paga extra
    |FINSI;
|FINSI;
|FPRC;

|PROCESO pagasH;   || CALCULO LIQUIDO PARA CLAVE 'P'
     nEmbargo = #4#146;
     |SI nEmbargo ! 0;
          nEmbargo = #4#146 - ((#4#146 * (#4#46 / (#4#46 + #4#47))) ! 2);
     |FINSI;
    devenga = #4#16;
    deducir = #4#17 + #4#128;      || mete el anticipo de paga extra
|FPRC;

|PROCESO talonesH;     || EMITE TALONES
|HAZ leetrabaH;
|SI #2#70 = "L";
        seleccio = 1;
    |SI #0#1 = "M";  |HAZ mensualH;  |FINSI;
    |SI #0#1 = "P";  |HAZ pagasH;    |FINSI;
        liquido = devenga - deducir;
    |SI #0#9 = "N";
        |HAZ imp_talon;
    |SINO;
        |HAZ gra_vtos;
    |FINSI;
|FINSI;
|FPRC;


|PROCESO resumenH;   || EMITE RESUMEN TALONES
|SI seleccio = 1;|Y swresu = 1;
    |SI swcab = 0;
            numero = #0#2;
            swcab = 1;
        |INFOR "nomhilom";
    |FINSI;
    |HAZ leetrabaH;
    |HAZ nrotalon;
    |SI #2#70 = "L";
        |SI #0#1 = "M";  |HAZ mensualH;  |FINSI;
        |SI #0#1 = "P";  |HAZ pagasH;    |FINSI;
            liquido = devenga - deducir;
        |PRINT;
            numero = numero + 1;
    |FINSI;
|SINO;
    |ERROR10;
|FINSI;
|FPRC;

|PROCESO PHistorico;
|HAZ cuadres;
|SI sw_cuadre = 0;
    |IMPRE 0;
    |SI FSalida = 0;
            recibi = #0#7;
            vtos = #0#9;
        |ABRE #2;
        |BUCLE nomhilon0;
        |SI seleccio = 1; |Y swresu = 1;
            |HAZ totalemp;
        |SINO;
            |SI seleccio = 0;
                |MENAV "    Seleccion vacia ...";
            |FINSI;
        |FINSI;
        |CIERRA #2;
    |SINO;
        |MENAV "    Proceso abortado ...";
    |FINSI;
    |FINIMP;
|FINSI;
|FPRC;



|PROCESO talonesA;     || EMITE TALONES
|SI #0#8 = 2;  |HAZ ruptura1A;  |FINSI;
|HAZ leetrabaA;
|SI #2#70 = "L";
        seleccio = 1;
    |SI #0#8 = 1;  |HAZ talon1A;  |FINSI;
    |SI #0#8 = 2;  |HAZ talon2A;  |FINSI;
|FINSI;
|FPRC;

|PROCESO talon1A;
|HAZ mensualA;
    liquido = devenga - deducir;

|SI #0#9 = "N";
    |HAZ imp_talon;
|SINO;
    |HAZ gra_vtos;
|FINSI;
|FPRC;

|PROCESO talon2A;
|HAZ refundi1A;
|HAZ RegPagado;
|FPRC;

|PROCESO cierraA;
|SI swresu = 0; |Y seleccio = 1;
    |SI #0#8 = 2;
            liquido = devenga - deducir;
        |SI #0#9 = "N";
            |HAZ imp_talon;
        |SINO;
            |HAZ gra_vtos;
        |FINSI;
            antempre = 0;
            anttraba = 0;
    |FINSI;
    |SI #0#9 = "S";
        |HAZ edi_vtos;
    |FINSI;
    |FINIMP;
    |MENAV mensaje2;
    |IMPRE 0;
    |SI FSalida = 0;
            swresu = 1;
    |SINO;
            swresu = 2;
        |MENAV "    ATENCION!!! Resumen Abortado ...";
    |FINSI;
|FINSI;
|FPRC;

|PROCESO resumenA;   || EMITE RESUMEN TALONES
|SI seleccio = 1; |Y swresu = 1;
    |SI swcab = 0;
            numero = #0#2;
            swcab = 1;
        |HAZ refundi2A;
        |INFOR "nomatlom";
    |FINSI;
    |SI #0#8 = 2;  |HAZ ruptura2A;  |FINSI;
    |HAZ leetrabaA;
    |HAZ nrotalon;
    |SI #2#70 = "L";
        |SI #0#8 = 1;  |HAZ resum1A;  |FINSI;
        |SI #0#8 = 2;  |HAZ resum2A;  |FINSI;
    |FINSI;
|SINO;
    |ERROR10;
|FINSI;
|FPRC;

|PROCESO resum1A;
|HAZ mensualA;
    liquido = devenga - deducir;
|PRINT;
    numero = numero + 1;
|FPRC;

|PROCESO resum2A;
|HAZ refundi1A;
|FPRC;

|DEFBUCLE nomatlon0;
 #5#1;
 ;
 #0#0, #0#5, #0#11, 5;
 #0#0, #0#6, #0#12, 5;
 ;
 NULL, talonesA, NULL, NULL, cierraA, resumenA;
 #5#0, #5#1, #5#2;
|FIN;

|DEFBUCLE nomatlon1;
 #5#1;
 ;
 #0#0, #0#5, mesos, 5;
 #0#0, #0#6, mesos, 5;
 ;
 NULL, talonesA, NULL, NULL, cierraA, resumenA;
 #5#0, #5#1, #5#2;
|FIN;

|PROCESO leetrabaA;    || LEE EL TRABAJADOR
    #2#0 = #5#0;
    #2#1 = #5#1;
|LEE 020#2=;
|SI FSalida ! 0;
    |DDEFECTO #2;
|FINSI;
    swbaja = 0;
    fecalf = #5#5;
|HAZ desglosa;
|SI mes = #0#11; |Y any = #0#4;
        swbaja = 1;
|FINSI;
|FPRC;

|PROCESO mensualA;   || CALCULO LIQUIDO PARA CLAVE 'M' (POR MESES)
    devenga = #5#46;
    deducir = #5#48 +#5#49 +#5#50 +#5#51 +#5#52 +#5#53 +#5#54 +#5#75 +#5#89;
|SI swbaja = 1; |O #1#25 = 2;
        devenga = devenga + #5#16;   || si es baja este mes o la paga va
        deducir = deducir + #5#17;   || /junto con el recibo, acumula la paga
|FINSI;
|FPRC;

|PROCESO refundi1A;   || CALCULO LIQUIDO PARA CLAVE 'M' (REFUNDIDO)
    devenga = devenga + #5#46;
    deducir = deducir + #5#48 +#5#49 +#5#50 +#5#51 +#5#52 +#5#53 +#5#54 +#5#75 +#5#89;
|SI swbaja = 1; |O #1#25 = 2;
        devenga = devenga + #5#16;   || si es baja este mes o la paga va
        deducir = deducir + #5#17;   || /junto con el recibo, acumula la paga
|FINSI;
|FPRC;

|PROCESO refundi2A;
    devenga = 0;
    deducir = 0;
|FPRC;

|PROCESO pagasA;   || CALCULO LIQUIDO PARA CLAVE 'P'
    devenga = #5#16;
    deducir = #5#17;
|FPRC;

|PROCESO ruptura1A;
|SI antempre = 0;|Y anttraba = 0; antempre = #5#0; anttraba = #5#1; |FINSI;
|SI antempre ! #5#0; |O anttraba ! #5#1;
        liquido = devenga - deducir;
    |SI #0#9 = "N";
        |HAZ imp_talon;
    |SINO;
        |HAZ gra_vtos;
    |FINSI;
        numero = numero + 1;
        antempre = #5#0;
        anttraba = #5#1;
    |HAZ refundi2A;
|FINSI;
|FPRC;

|PROCESO ruptura2A;
|SI antempre = 0;|Y anttraba = 0; antempre = #5#0; anttraba = #5#1; |FINSI;
|SI antempre ! #5#0; |O anttraba ! #5#1;
        liquido = devenga - deducir;
    |PRINT;
        antempre = #5#0;
        anttraba = #5#1;
    |HAZ refundi2A;
|FINSI;
|FPRC;


|PROCESO PAtrasos;
|HAZ cuadres;
|SI sw_cuadre = 0;
    |IMPRE 0;
    |SI FSalida = 0;
        |ABRE #2;
            recibi = #0#7;
            vtos = #0#9;
        |SI #0#8 = 2;
            |BUCLE nomatlon0;
            |SI seleccio = 1;|Y swresu = 1;
                    liquido = devenga - deducir;
                |PRINT;
                |HAZ totalemp;
            |SINO;
                |SI seleccio = 0;
                    |MENAV "    ATENCION !!! Seleccion vacia ...";
                |FINSI;
            |FINSI;
        |SINO;
            |PARA mesos = #0#11; |SI mesos [ #0#12; |HACIENDO mesos = mesos + 1;
                        swsalta = 0;
                        sw_vtos = 0;
                |SI swresu ! 0;
                    |FINIMP;
                    |MENAV mensaje3;
                    |IMPRE 0;
                    |SI FSalida ! 0;
                            swsalta = 1;
                    |FINSI;
                |FINSI;
                |SI swsalta = 0;
                        seleccio = 0;
                        swcab = 0;
                        swresu = 0;
                    |BUCLE nomatlon1;
                    |SI seleccio = 1;
                            alguna = 1;
                    |FINSI;
                    |SI seleccio = 1;|Y swresu = 1;
                        |HAZ totalemp;
                    |FINSI;
                |FINSI;
            |SIGUE;
            |SI alguna = 0;
                |MENAV "    ATENCION!!! Seleccion vacia ...";
            |FINSI;
        |FINSI;
        |CIERRA #2;
    |SINO;
        |MENAV "    ATENCION!!! Proceso Abortado ...";
    |FINSI;
    |FINIMP;
|FINSI;
|FPRC;


|PROCESO impre; |TIPO 3;   || LANZA EL PROCESO
     |SI #0#13="H";
          |HAZ PHistorico;
     |FINSI;
     |SI #0#13="M";
          |HAZ PMensual;
     |FINSI;
     |SI #0#13="A";
          |HAZ PAtrasos;
     |FINSI;
|FPRC;
