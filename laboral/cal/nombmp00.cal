|FICHEROS;
     nombmp00 #0;
     nombmp02 #1;
|FIN;

|VARIABLES;
     &p1       = 0;
     &p2       = 0;
     &tem      = "";
     &tip      = "";
     &mod      = "";
     fichero   = "";
     base      = "";
     posi1     = 0;
     posi2     = 0;
     posi1_ant = 0;
     posi2_ant = 0;
     numero    = "";
     ind       = 0;
     linea     = "";
     colum     = "";
     tecla     = "";
     f1        = "823";
     f2        = "824";
     f3        = "825";
     esc       = "806";
     ctrl_c    = "807";
     arriba    = "808";
     abajo     = "809";
     izquierda = "810";
     derecha   = "811";
     tama_org  = 0;
     tama_nue  = 0;
     mascara   = "";
     num_mov   = 0;
     guarda    = 0;
     pos       = "";
     sw        = 0;
     linea_sup = "";
     linea_inf = "";
     colum_der = "";
     colum_izq = "";
     l_sup     = 0;
     l_inf     = 0;
     c_der     = 0;
     c_izq     = 0;
     tmp       = 0;
     sistema   = "";
     alfa      = "";
     diff1     = 0;
     diff2     = 0;
|FIN;

||---------------------------------------------------------------------------
|| PINTA LA VENTANA CON EL GRAFICO.
||---------------------------------------------------------------------------
|PROCESO pinta;
     |POPV;
     pos = "0000" + posi1
     numero = pos % -104;
     |QBF numero;
     pos = "0000" + posi2;
     pos = pos % -104;
     |QBF pos;
     numero = numero + pos;
     guarda = numero;
     |PUSHV guarda;
     |PINVE posi1,posi2,fichero;
     |PINTA posi1,"*";
     |PINTA posi2,"*";
     |SI sw = 2;
          |ATRI R;
          |PINTA posi1,"*";
          |ATRI r;
     |FINSI;
     |SI sw = 3;
          |ATRI R;
          |PINTA posi2,"*";
          |ATRI r;
     |FINSI;
|FPRC;

||---------------------------------------------------------------------------
|| CONTROLA LA POSICION DE LA VENTANA PARA QUE NO SOBREPASE LOS LIMITES.
||---------------------------------------------------------------------------
|PROCESO ctrl_posicion;
|| Control posicion superior izquierda.............................
     numero = "0000" + posi1
     numero = numero % -104;
     linea = numero %  102;
     colum = numero % -102;
     |SI linea < "06"; linea = "05"; |FINSI;
     |SI colum < "02"; colum = "01"; |FINSI;
     numero = linea + colum;
     posi1 = numero;
|| Control posicion inferior derecha...............................
     numero = "0000" + posi2
     numero = numero % -104;
     linea = numero %  102;
     colum = numero % -102;
     |SI linea > "22"; linea = "23"; |FINSI;
     |SI colum > "79"; colum = "80"; |FINSI;
     numero = linea + colum;
     posi2 = numero;
|FPRC;

||---------------------------------------------------------------------------
|| CAMBIA EL TAMA¥O DE LA PANTALLA.
||---------------------------------------------------------------------------
|PROCESO ctrl_tama¤o;
|| Control posicion superior izquierda.......................................
     numero = "0000" + posi1
     numero = numero % -104;
     linea_sup = numero %  102;
     colum_izq = numero % -102;
     |SI linea_sup < "06"; linea_sup = "05"; |FINSI;
     |SI colum_izq < "02"; colum_izq = "01"; |FINSI;
     numero = linea_sup + colum_izq;
     posi1 = numero;
|| Control posicion inferior derecha.........................................
     numero = "0000" + posi2
     numero = numero % -104;
     linea_inf = numero %  102;
     colum_der = numero % -102;
     |SI linea_inf > "22"; linea_inf = "23"; |FINSI;
     |SI colum_der > "79"; colum_der = "80"; |FINSI;
     numero = linea_inf + colum_der;
     posi2 = numero;
|| Control ventana minima....................................................
     l_sup = linea_sup;
     l_inf = linea_inf;
     c_izq = colum_izq;
     c_der = colum_der;
     tmp = l_sup + 5;
     |SI tmp > l_inf; posi1 = posi1_ant; |FINSI;
     tmp = c_izq + 15;
     |SI tmp > c_der; posi1 = posi1_ant; |FINSI;
     tmp = l_inf - 5;
     |SI tmp < l_sup; posi2 = posi2_ant; |FINSI;
     tmp = c_der - 15;
     |SI tmp < c_izq; posi2 = posi2_ant; |FINSI;
|FPRC;

||---------------------------------------------------------------------------
|| Guarda la posicion anterior de la ventana.
||---------------------------------------------------------------------------
|PROCESO guarda;
     posi1_ant = posi1;
     posi2_ant = posi2;
     diff1 = posi1 - posi2;
     |SI sw = 1;
          posi1 = posi1 + num_mov;
          posi2 = posi2 + num_mov;
     |FINSI;
     |SI sw = 2;
          posi1 = posi1 + num_mov;
     |FINSI;
     |SI sw = 3;
          posi2 = posi2 + num_mov;
     |FINSI;
|FPRC;

||---------------------------------------------------------------------------
|| RUTINA DE MOVIMIENTO DE LA VENTANA.
||---------------------------------------------------------------------------
|PROCESO mueve;
     |PARA ind = 0; |SI ind = 0; |HACIENDO;
          num_mov = 0;
          |LEETECLA tecla;
          |QBF tecla;
          tecla = tecla % -103;
          |SI tecla = f1; sw = 1; |HAZ guarda; |HAZ pinta; |FINSI;
          |SI tecla = f2; sw = 2; |HAZ guarda; |HAZ pinta; |FINSI;
          |SI tecla = f3; sw = 3; |HAZ guarda; |HAZ pinta; |FINSI;
          |SI tecla = arriba;    num_mov = -100; |HAZ guarda; |FINSI;
          |SI tecla = abajo;     num_mov =  100; |HAZ guarda; |FINSI;
          |SI tecla = izquierda; num_mov =   -1; |HAZ guarda; |FINSI;
          |SI tecla = derecha;   num_mov =    1; |HAZ guarda; |FINSI;
          |SI posi1 ! posi1_ant; |O posi2 ! posi2_ant;
               |SI tecla = f1; |O sw = 1;    || Mueve la ventana.
                    |HAZ ctrl_posicion;
                    tama_nue = posi2 - posi1;
                    diff2 = posi1 - posi2;
                    |SI diff1 ! diff2;
                         posi1 = posi1_ant;
                         posi2 = posi2_ant;
                    |FINSI;
               |FINSI;
               |SI tecla = f2; |O sw = 2; |O sw = 3;|| Cambia tama¤o ventana.
                    |HAZ ctrl_tama¤o;
                    tama_org = posi2 - posi1;
               |FINSI;
               |HAZ pinta;
          |FINSI;
          |SI tecla = esc; |O tecla = ctrl_c; ind = 1; |FINSI;
     |SIGUE;
|FPRC;

||---------------------------------------------------------------------------
|| Entrada al programa.
||---------------------------------------------------------------------------
|PROGRAMA;
     |DDEFECTO #1;
     |ABRE #1;
     #1#0 = 1;
     |LEE 000#1=;
     |SI FSalida ! 0;
          |MENAV "    <Utilidades/Configurar Imagenes> sin definir, aplicando predeterminados";
     |FINSI;
     |CIERRA #1;

     |MENSA "0000< F1 > Mueve ventana | < F2 o F3 > Cambia tama¤o | < ESC > Salir";
     sw = 0;

     posi1 = p1;
     posi2 = p2;
     tama_org = #1#2 - #1#1;            || Tama¤o original de la ventana.
     |DBASE base;
     |QUE_SISTEMA sistema;
     fichero = base + "imagenes/";
     |MKDIR fichero;
     fichero = fichero + tem;
     |QBF fichero;
     |SI #1#3 = "S"; |HAZ desco; |FINSI;     || DESCOMPRIME IMAGEN.
     fichero = fichero + ".bmp";
     |PUSHV 0501 2380;
     |PINVE posi1,posi2,fichero;
     |HAZ mueve;
     |POPV;
     |SI tecla ! ctrl_c;
          p1 = posi1;
          p2 = posi2;
     |FINSI;
     |SI #1#3 = "S"; |HAZ compr; |FINSI;     || COMPRIME IMAGEN.
|FPRO;

||---------------------------------------------------------------------------
|| COMPRIME Y DESCOMPRIME LAS IMAGENES.
||---------------------------------------------------------------------------
|PROCESO compr;
     |COMPRIME fichero;
     |SI FSalida = 0;
          |FBORRA fichero;
     |SINO;
          |MENAV "0000NO SE PUDO COMPRIMIR EL FICHERO DE LA IMAGEN";
     |FINSI;
|FPRC;

|PROCESO desco;
     |DESCOMPRIME fichero;
     alfa = fichero + ".bmp";
     |COPIA_FICHERO fichero, alfa;
     |FBORRA fichero;
|FPRC;
