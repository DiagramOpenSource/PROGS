|FICHEROS;
     labnct01;
|FIN;

|VARIABLES;
     aIPRemoto = "";
     {1}aContiene = "";
     aDestino = "";
     aRemoto = "";
     aOrigen = "";
     aServidor = "";
     nInstalar = 0;
     aFic = "";
     aAlfa = "";
     aCHMOD = "";
     nConta = 0;
     nHandle = 0;
     aFecha = "";
     fFecha = @;
     aBase = "";
     aAbre = "";
     aWeb = "";
     aIP = "";
     aPathWeb = "";
     aPrograma = "";
     aParametro = "";
     aUsuario = "";
     aIni = "";
     nIni = 0;
     aBaseLocal = "";
     {1}aSerie = "";

     &aIdCont = "";
     &aCenFor = "";
     &aFechaIni = "";
     &aDNI = "";

     &eaDSMAC = "";
     FEstado = 0;
     aHora = "";
     aEjecutable = "";
     aWebEje = "";

     nNume = 0;
     &swNuevo = 0;
     aRespuesta = "";
     aLinea = "";
     aFicheroBAT = "";
     nCanal = 0;
     aUnidad = "";

     aUtilidad = "";
|FIN;

|PROCESO TraeUtilidad;
     |DBASE aBase;  |QBF aBase;
     aOrigen = aBase + "ejecutables/" + aUtilidad;

     aContiene = "";
     |ESPECIFICA "RBASS", aContiene;
     aBaseLocal = aContiene1 % 102;
     aDestino = aBaseLocal + "/DOCUMENTOS/" + aUtilidad;

     aIPRemoto = ""; |IP_REMOTO aIPRemoto;  |QBT aIPRemoto;
     aContiene1 = aDestino;
     |SI aIPRemoto ! "";
          |ESPECIFICA "REMOTOMD5", aContiene;
     |SINO;
          |ESPECIFICA "MD5", aContiene;
     |FINSI;
     aRemoto = FSalida;  |QBF aRemoto;

     aContiene1 = aOrigen;
     |ESPECIFICA "MD5", aContiene;
     aServidor = FSalida;  |QBF aServidor;

     |PULSATECLA;

     |SI aRemoto ! aServidor;
         |SI aIPRemoto ! "";
             |ENVIA_FICHERO aOrigen, aDestino;
         |SINO;
             |COPIA_FICHERO aOrigen, aDestino;
         |FINSI;
     |FINSI;

     |PULSATECLA;
|FPRC;

|PROCESO Ejecutables;
     aUtilidad = "dsmini.exe";
     |HAZ TraeUtilidad;
|FPRC;

|PROCESO AveriguaINI;
     aSerie = "";
     |ESPECIFICA "SERIALIZACION", aSerie;
     aSerie = aSerie % 106;          ||Esto es el numero de ORO
     nIni = aSerie;
     aIni = ("000000" + nIni) % -106;
|FPRC;

|PROCESO EnviaConMD5;
     aIPRemoto = ""; |IP_REMOTO aIPRemoto;  |QBT aIPRemoto;

     aContiene1 = aDestino;
     |SI aIPRemoto ! "";
          |ESPECIFICA "REMOTOMD5", aContiene;
     |SINO;
          |ESPECIFICA "MD5", aContiene;
     |FINSI;
     aRemoto = FSalida;  |QBF aRemoto;

     aContiene1 = aOrigen;
     |ESPECIFICA "MD5", aContiene;
     aServidor = FSalida;  |QBF aServidor;

     |PULSATECLA;

     nInstalar = 0;

     |SI aRemoto ! aServidor;
         |SI aIPRemoto ! "";
             |ENVIA_FICHERO aOrigen, aDestino;
         |SINO;
             |COPIA_FICHERO aOrigen, aDestino;
         |FINSI;

         nInstalar = 1;
     |FINSI;

     |PULSATECLA;
|FPRC;

|PROCESO LeeWebEje;
     |DBASE aBase;  |QBF aBase;
     aAbre = aBase + "ejecutables/" + aWebEje + ".txt";

     |XABRE "AB", aAbre, nHandle;
     |SI FSalida < 0;  |ACABA;  |FINSI;

     |XLEE nHandle, 250, aWeb;
     |QBF aWeb;

     |PARA;  |SI;  |HACIENDO;
          aWeb = aWeb - &13;
          |SI FSalida = 0;  |SAL;  |FINSI;
     |SIGUE;

     |PARA;  |SI;  |HACIENDO;
          aWeb = aWeb - &10;
          |SI FSalida = 0;  |SAL;  |FINSI;
     |SIGUE;

     |XCIERRA nHandle;
|FPRC;

|PROCESO EnvioFormacion;
     |HAZ AveriguaINI;
     aUsuario = Usuario % 810;
     fFecha = ~;
     aFecha = fFecha;
     aFecha = (aFecha % 102) + "/" + (aFecha % 402) + "/" +(aFecha % -104);
     aFechaIni = (aFechaIni % 102) + "/" + (aFechaIni % 402) + "/" + (aFechaIni % -104);

     |QBF aCenFor;
     aCenFor = ("0000000000" + aCenFor) % -110;
     |QBF aIdCont;
     aIdCont = ("000000" + aIdCont) % -106;

     aContiene = "";
     |ESPECIFICA "RBASS", aContiene;
     aBaseLocal = aContiene1;
     aUnidad = aBaseLocal % 102;

     aRespuesta = aBaseLocal + "bin/estado_registro.txt";
     |RFBORRA aRespuesta;

     |IP_REMOTO aIP;
     |SI aIP = "";                 || para modo LOCAL
          |QUE_SISTEMA aAlfa;
          |SI aAlfa = "ESDOS";          || y solamente WINDOWS
               aWebEje = "ds_con_php";
               |HAZ LeeWebEje;

               |DBASE aBase;  |QBF aBase;
               aOrigen  = aBase + "ejecutables/ds_con_php.exe";

               |DBASS aBase;  |QBF aBase;
               aPathWeb = aBase + "bin/";
               aDestino = aPathWeb + "ds_con_php.exe";

               |HAZ EnviaConMD5;

               |DBASS aBase;  |QBF aBase;
               aPrograma  = aPathWeb + "ds_con_php ";
               aParametro = "?ini=" + aIni + "&path=L_" + aBase + "?usuario=" + aUsuario;
               aParametro = aParametro + "&fecha=" + aFecha;
               aParametro = aParametro + "&idcont=" + aIdCont;
               aParametro = aParametro + "&cenfor=" + aCenFor;
               aParametro = aParametro + "&fechaini=" + aFechaIni;
               aParametro = aParametro + "&dni=" + aDNI;

               aAlfa = aPrograma + &34 + aWeb + aParametro + &34 + " " + aRespuesta;
               |SYSTEM aAlfa;
          |FINSI;
     |SINO;
          aWebEje = "ds_con_php";
          |HAZ LeeWebEje;

          |DBASE aBase;  |QBF aBase;
          aOrigen  = aBase + "ejecutables/ds_con_php.exe";

          aPathWeb   = aBaseLocal + "bin/";
          aDestino = aPathWeb + "ds_con_php.exe";

          |HAZ EnviaConMD5;

          |DBASS aBase;  |QBF aBase;
          aPrograma  = aPathWeb + "ds_con_php ";
          aParametro = "?ini=" + aIni + "&path=" + aBase + "&usuario=" + aUsuario;
          aParametro = aParametro + "&fecha=" + aFecha;
          aParametro = aParametro + "&idcont=" + aIdCont;
          aParametro = aParametro + "&cenfor=" + aCenFor;
          aParametro = aParametro + "&fechaini=" + aFechaIni;
          aParametro = aParametro + "&dni=" + aDNI;

          aAlfa = aPrograma + &34 + aWeb + aParametro + &34 + " " + aRespuesta;

          |RSYSTEM aAlfa;
     |FINSI;
|FPRC;

|PROCESO RegConectaSS;
     |HAZ Ejecutables;

     |ABRE #labnct01;
     #labnct01#0 = eaDSMAC;
     |LEE 101#labnct01.=;
     FEstado = FSalida;

     #labnct01#0 = eaDSMAC;
     #labnct01#1 = aIP;
     #labnct01#2 = aIni;
     #labnct01#3 = fFecha;
     #labnct01#4 = aHora;
     #labnct01#5 = aBase;
     #labnct01#6 = aUsuario;

     |SI FEstado = 0;
          |GRABA 020#labnct01.a;
     |SINO;
          swNuevo = 1;
          |GRABA 020#labnct01.n;
     |FINSI;

     |LIBERA #labnct01;
     |CIERRA #labnct01;
     |CIERRA #labnct01;
|FPRC;

|PROCESO EnvioRegConectaSS;
     aEjecutable = "ds_con_php";

     |HAZ AveriguaINI;

     aUsuario = Usuario % 810;
     fFecha = ~;
     aFecha = fFecha;
     aFecha = (aFecha % 102) + "/" + (aFecha % 402) + "/" +(aFecha % -104);
     aFechaIni = (aFechaIni % 102) + "/" + (aFechaIni % 402) + "/" + (aFechaIni % -104);

     |HORA aHora;

     |DBASS aBase;  |QBF aBase;

     |IP_REMOTO aIP;
     |HAZ CogeMac;
     swNuevo = 0;
     |HAZ RegConectaSS;
     |SI swNuevo = 0;
          ||ACABA;
          || antes entraba solo si swNuevo ! 0, ahora entramos tambien si
          || ya existe, para el modo consulta
     |FINSI;

     || Si swNuevo = 0, quiere decir que lo que hago es una consulta
     || a ver si esta activo

     aContiene = "";
     |ESPECIFICA "RBASS", aContiene;
     aBaseLocal = aContiene1;
     aUnidad = aBaseLocal % 102;

     aRespuesta = aUnidad + "\DOCUMENTOS\estado_conectass.txt";
     |RFBORRA aRespuesta;

     |SI aIP = "";                 || para modo LOCAL
          |QUE_SISTEMA aAlfa;
          |SI aAlfa = "ESDOS";          || y solamente WINDOWS
               aWebEje = "ds_con_php2";
               |HAZ LeeWebEje;

               |DBASE aBase;  |QBF aBase;
               aOrigen  = aBase + "ejecutables/" + aEjecutable + ".exe";

               |DBASS aBase;  |QBF aBase;
               aPathWeb = aBase + "bin/";
               aDestino = aPathWeb + aEjecutable + ".exe";

               |HAZ EnviaConMD5;

               aPrograma  = aPathWeb + aEjecutable + " ";
               aParametro = "?ini=" + aIni + "&path=L_" + aBase + "&usuario=" + aUsuario;
               aParametro = aParametro + "&fecha=" + aFecha;
               aParametro = aParametro + "&mac=" + eaDSMAC;
               |SI swNuevo = 0;
                    aParametro = aParametro + "&estado=";
               |FINSI;

               aLinea = aPrograma + &34 + aWeb + aParametro + &34 + " " + aRespuesta;
               aFicheroBAT = aUnidad + "\DOCUMENTOS\" + Usuario + ".bat";
               |FBORRA aFicheroBAT;
               |XABRE "CBW", aFicheroBAT, nCanal;
               |SI FSalida ] 0;
                    aAlfa = "@echo off" + &13 + &10;
                    |XGRABA nCanal, aAlfa;
                    aLinea = "START /WAIT " + aLinea;
                    |XGRABA nCanal, aLinea;
               |FINSI;
               |XCIERRA nCanal;

               aAlfa = aUnidad + "\DOCUMENTOS\dsmini.exe " + aFicheroBAT;
               |SYSTEM aAlfa;
          |FINSI;
     |SINO;
          aWebEje = "ds_con_php2";
          |HAZ LeeWebEje;

          aOrigen  = aBase + "ejecutables/" + aEjecutable + ".exe";
          aPathWeb   = aBaseLocal + "bin/";
          aDestino = aPathWeb + aEjecutable + ".exe";

          |HAZ EnviaConMD5;

          |DBASS aBase;  |QBF aBase;
          aPrograma  = aPathWeb + aEjecutable + " ";
          aParametro = "?ini=" + aIni + "&path=" + aBase + "&usuario=" + aUsuario;
          aParametro = aParametro + "&fecha=" + aFecha;
          aParametro = aParametro + "&mac=" + eaDSMAC;
          |SI swNuevo = 0;
               aParametro = aParametro + "&estado=";
          |FINSI;

          aLinea = aPrograma + &34 + aWeb + aParametro + &34 + " " + aRespuesta;

          aFicheroBAT = aUnidad + "\DOCUMENTOS\" + Usuario + ".bat";
          |RFBORRA aFicheroBAT;
          |XABRE "CBW", aFicheroBAT, nCanal;
          |SI FSalida ] 0;
               aAlfa = "@echo off" + &13 + &10;
               |XGRABA nCanal, aAlfa;
               aLinea = "START /WAIT " + aLinea;
               |XGRABA nCanal, aLinea;
          |FINSI;
          |XCIERRA nCanal;

          aAlfa = aUnidad + "\DOCUMENTOS\dsmini.exe " + aFicheroBAT;
          |RSYSTEM aAlfa;
     |FINSI;
|FPRC;
