|FICHEROS;
     cretap22 #0;
     cretam34;
     cretam35;

     cretam18;

     cretam02;

     labempre;
     labcentr;
     nomcenes;
     labtraba;
|FIN;

|VARIABLES;
     &eaFichero = "";
     &aSoloFichero = "";

     nNume = 0;
     aLon = "";
     nLon = 0;
     nEspacio = 0;
     aEspacio = "";
     nBlancos = 0;
     nBlancosIzd = 0;
     nBlancosDer = 0;
     aBlancosIzd = "";
     aBlancosDer = "";

     aCadena = "";
     aCadenaSal = "";

     FLinea = 0;
     nNroLinea = 0;
     aNroLinea = "";
     aAlfa = "";

     nHandle = 0;
     aLinea = "";
     &aTotalLineas = "";
     swAcaba = 0;

     aCCC = "";
     nAnyDesde = 0;
     nMesDesde = 0;
     aAnyDesde = "";
     aMesDesde = "";
     nTipo = 0;
     aDia = "";
     aMes = "";
     aAny = "";
     aEtiqueta = "";
     nLoc = 0;
     aValor = "";
     aCampoFecha = "";
     aDiaRec = "";
     aMesRec = "";
     aAnyRec = "";
     aHoraRec = "";
     aEtiquetaIni = "";
     aEtiquetaFin = "";
     aMesHasta = "";
     aAnyHasta = "";
     aTipo = "";

     nEmp = 0;
     nTra = 0;
     swEncontrada = 0;
     aAlfa1 = "";
     aAlfa2 = "";
     &enMes = 0;
     &enAny = 0;
     &enSwAlta = 0;

     aNaf = "";
     aEstado = "";
     fFechaRec = @;

     aTipoIPF = "";
     aNumeroIPF = "";
     aSiglas = "";

     aDiaDesdeTramo = "";
     aMesDesdeTramo = "";
     aAnyDesdeTramo = "";
     aFechaDesdeTramo = "";
     aDiaHastaTramo = "";
     aMesHastaTramo = "";
     aAnyHastaTramo = "";
     aFechaHastaTramo = "";

     aDias = "";
     aHoras = "";
     aDescripcion = "";
     nConcepto = 0;
     nImporte = 0;
     aCentro = "";

     nCampo = 0;
     nCampo1 = 0;
     nCampo2 = 0;
     nCampo3 = 0;
     aTipoSuma = "";
     nBase = 0;
     aTipoDCL = "";
|FIN;

|PROCESO Centra;
     aLon = aCadena % 0;
     nLon = aLon;
     nBlancos = nEspacio - nLon;
     nNume = nBlancos / 2;
     nBlancosIzd = nNume ! 0;
     nBlancosDer = nBlancos - nBlancosIzd;
     aBlancosIzd = " " * nBlancosIzd;
     aBlancosDer = " " * nBlancosDer;
     aCadenaSal = aBlancosIzd + aCadena + aBlancosDer;

|FPRC;

/****************************************************************************/
/***************************   BORRADO PREVIO *******************************/
/****************************************************************************/

|PROCESO Borra_cretam35;
     |BORRA 020#cretam35.a;
     |LIBERA #cretam35;
|FPRC;

|DEFBUCLE Borra_cretam35;
     #cretam35#2;
     ;
     aCCC, nAnyDesde, nMesDesde, nTipo;
     aCCC, nAnyDesde, nMesDesde, nTipo;
     ;
     NULL, Borra_cretam35;
|FIN;

|PROCESO Borra_cretam34;
     |BORRA 020#cretam34.a;
     |LIBERA #cretam34;
|FPRC;

|DEFBUCLE Borra_cretam34;
     #cretam34#2;
     ;
     aCCC, nAnyDesde, nMesDesde, nTipo;
     aCCC, nAnyDesde, nMesDesde, nTipo;
     ;
     NULL, Borra_cretam34;
|FIN;

/****************************************************************************/
/*********** PROCESOS PARA BUSCAR CODIGOS DE EMP, TRA Y CEN *****************/
/****************************************************************************/

|PROCESO BuscaCCC;
     nEmp = #cretam02#0;
     swEncontrada = 1;
|FPRC;

|DEFBUCLE BuscaCCC;
     #cretam02#1;
     ;
     00001, nAnyDesde, nMesDesde, nTipo, aCCC;
     99999, nAnyDesde, nMesDesde, nTipo, aCCC;
     ;
     NULL, BuscaCCC;
|FIN;

|PROCESO BuscaPorCCC;
     swEncontrada = 0;
     |BUCLE BuscaCCC;
|FPRC;

/****************************************************************************/
/********************   TIPOS DE DATOS COMUNES  *****************************/
/****************************************************************************/

|PROCESO TipoFecha;
     aDia = "";
     aMes = "";
     aAny = "";

     |PARA; |SI; |HACIENDO;
          |HAZ LeeLinea;

          aEtiqueta = "Dia";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               aDia = aValor;
          |FINSI;

          aEtiqueta = "Mes";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               aMes = aValor;
          |FINSI;

          aEtiqueta = "Anho";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               aAny = aValor;
          |FINSI;

          aEtiqueta = aCampoFecha;
          |HAZ BuscaEtiFin;
          |SI nLoc = 1;
               |SAL;
          |FINSI;
     |SIGUE;
|FPRC;

|PROCESO FechaHoraRec;
     |PARA; |SI; |HACIENDO;
          |HAZ LeeLinea;

          aEtiqueta = "FechaRecaudacion";
          |HAZ BuscaEtiIni;
          |SI nLoc = 1;
               aCampoFecha = "FechaRecaudacion";
               |HAZ TipoFecha;
               aDiaRec = aDia;
               aMesRec = aMes;
               aAnyRec = aAny;
               aAlfa = aDiaRec + "." + aMesRec + "." + aAnyRec;
               fFechaRec = aAlfa;
          |FINSI;

          aEtiqueta = "HoraRecaudacion";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               aHoraRec = (aValor % 102) + ":" + (aValor % 302) + ":" + (aValor % 502);
          |FINSI;

          aEtiqueta = "FechaHoraRecaudacion";
          |HAZ BuscaEtiFin;
          |SI nLoc = 1;
               |SAL;
          |FINSI;
     |SIGUE;
|FPRC;

|PROCESO CCC;
     |PARA; |SI; |HACIENDO;
          |HAZ LeeLinea;

          aEtiqueta = "Regimen";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               aCCC = aValor;
          |FINSI;

          aEtiqueta = "Provincia";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               aCCC = aCCC + aValor;
          |FINSI;

          aEtiqueta = "Numero";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               aCCC = aCCC + aValor;
          |FINSI;

          aEtiqueta = "Ccc";
          |HAZ BuscaEtiFin;
          |SI nLoc = 1;
               |SAL;
          |FINSI;
     |SIGUE;
|FPRC;

/****************************************************************************/
/***********     PROCESOS PARA BUSCAR LAS ETIQUETAS       *******************/
/****************************************************************************/

|PROCESO BuscaEtiFin;
     nLoc = 0;
     aEtiqueta = "</" + aEtiqueta + ">";
     aAlfa = aLinea - aEtiqueta;
     |SI aAlfa ! aLinea;
          nLoc = 1;
     |FINSI;
|FPRC;

|PROCESO BuscaEtiIni;
     nLoc = 0;
     aEtiqueta = "<" + aEtiqueta;
     aAlfa = aLinea - aEtiqueta;
     |SI aAlfa ! aLinea;
          nLoc = 1;
     |FINSI;
|FPRC;

|PROCESO BuscaEtiIniFin;
     aValor = "";
     nLoc = 0;

     aEtiquetaIni = "<" + aEtiqueta + ">";
     aEtiquetaFin = "</" + aEtiqueta + ">";
     aAlfa = aLinea - aEtiquetaIni - aEtiquetaFin;
     |SI aAlfa ! aLinea;
          aValor = aAlfa;
          |SI aEtiqueta = "RazonSocial"; |O aEtiqueta = "EntidadAtEp";
          |O aEtiqueta = "DescripcionLDCL";
               |QBF aValor;
          |SINO;
               |QBT aValor;
          |FINSI;
          nLoc = 1;
     |FINSI;
|FPRC;

|PROCESO LeeLinea;
     |XLEE nHandle, 250, aLinea;
     FLinea = FSalida;
     nNroLinea = nNroLinea + 1;
     aNroLinea = nNroLinea;
     aAlfa = " Linea " + aNroLinea + " de " + aTotalLineas;

     |ATRI R;
     aCadena = aAlfa; nEspacio = 35; |HAZ Centra;
     |PINTA 2223, aCadenaSal;
     |ATRI 0;
|FPRC;

|PROCESO LineaDCL;
     FLinea = 0;
     swAcaba = 0;

     nConcepto = 0;
     aDescripcion = "";
     nBase = 0;
     nImporte = 0;

     |PARA;
     |SI FLinea ] 0; |Y swAcaba = 0;
     |HACIENDO;
          FLinea = 0;
          |HAZ LeeLinea;

          aEtiqueta = "DescripcionLDCL";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               aDescripcion = aValor;
               aAlfa = " " * 100;
               aDescripcion = aDescripcion + aAlfa;
               aDescripcion = (aDescripcion % 199) + " ";
               |SELECT #2#cretam18;
               #cretam18#1 = aDescripcion;
               |LEE 000#cretam18.=;
               |SI FSalida = 0;
                    nConcepto = #cretam18#0;
               |FINSI;
          |FINSI;

          aEtiqueta = "BaseLDCL";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               nBase = aValor;
               nBase = nBase / 100;
          |FINSI;

          aEtiqueta = "ImporteLDCL";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               nImporte = aValor;
               nImporte = nImporte / 100;
          |FINSI;

          aEtiqueta = "LineaDCL";
          |HAZ BuscaEtiFin;
          |SI nLoc = 1;
               || ya tengo los datos para guardar la linea (el cretam35)

               |DDEFECTO #cretam35;
               #cretam35#0 = nEmp;
               #cretam35#1 = nAnyDesde;
               #cretam35#2 = nMesDesde;
               #cretam35#3 = nTipo;
               #cretam35#4 = aCCC;
               #cretam35#5 = nConcepto;
               |LEE 000#cretam35.=;
               |SI FSalida ! 0;
                    || tiene que entrar siempre, no puede existir
                    #cretam35#6 = aDescripcion;
                    #cretam35#7 = nBase;
                    #cretam35#8 = nImporte;
                    #cretam35#9 = aSoloFichero;

                    |GRABA 020#cretam35.n;
                    |LIBERA #cretam35;
               |FINSI;

               |SAL;
          |FINSI;
     |SIGUE;
|FPRC;

|PROCESO DatosCabeceraDCL;
     || primero grabo ya mi cabecera (el cretam34)

     |HAZ BuscaPorCCC;

     #labempre#0 = nEmp;
     |LEE 000#labempre.=;

     #cretam34#0 = nEmp;
     #cretam34#1 = nAnyDesde;
     #cretam34#2 = nMesDesde;
     #cretam34#3 = nTipo;
     #cretam34#4 = aCCC;
     |LEE 000#cretam34.=;
     |SI FSalida ! 0;
          || tiene que entrar siempre
          #cretam34#6 = #labempre#2;
          #cretam34#11 = aTipo;
          #cretam34#14 = aTipoDCL
          #cretam34#12 = fFechaRec;
          #cretam34#13 = aHoraRec;

          #cretam34#17 = aMesDesde;
          #cretam34#18 = aAnyDesde;
          #cretam34#19 = aMesHasta;
          #cretam34#20 = aAnyHasta;
          #cretam34#28 = aSoloFichero;
          |GRABA 020#cretam34.n;

          || la leo otra vez para dejarla leida y despues grabarla de nuevo

          #cretam34#0 = nEmp;
          #cretam34#1 = nAnyDesde;
          #cretam34#2 = nMesDesde;
          #cretam34#3 = nTipo;
          #cretam34#4 = aCCC;
          |LEE 000#cretam34.=;
     |FINSI;

     FLinea = 0;
     swAcaba = 0;

     |PARA;
     |SI FLinea ] 0; |Y swAcaba = 0;
     |HACIENDO;
          FLinea = 0;
          |HAZ LeeLinea;

          aEtiqueta = "AlcanceCalculo";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               #cretam34#29 = aValor;
          |FINSI;

          aEtiqueta = "CuotaALiquidar";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               #cretam34#30 = aValor;
          |FINSI;

          aEtiqueta = "Autorizado";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               #cretam34#15 = aValor;
          |FINSI;

          aEtiqueta = "Envio";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               #cretam34#16 = aValor;
          |FINSI;

          aEtiqueta = "DescripcionTipoLiquidacion";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               #cretam34#21 = aValor;
          |FINSI;

          aEtiqueta = "RazonSocial";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               #cretam34#22 = aValor;
          |FINSI;

          aEtiqueta = "TipoIpf";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               #cretam34#23 = aValor;
          |FINSI;

          aEtiqueta = "NumeroIpf";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               #cretam34#24 = (aValor % -109);
          |FINSI;

          aEtiqueta = "EntidadAtEp";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               #cretam34#25 = aValor;
          |FINSI;

          aEtiqueta = "NumeroTrabajadores";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               #cretam34#26 = aValor;
          |FINSI;

          aEtiqueta = "NumeroLiquidacion";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               #cretam34#27 = aValor;
          |FINSI;

          aEtiqueta = "DatosCabeceraDCL";
          |HAZ BuscaEtiFin;
          |SI nLoc = 1;
               |GRABA 020#cretam34.a;
               |LIBERA #cretam34;
               |SAL;
          |FINSI;
     |SIGUE;
|FPRC;

|PROCESO Lectura;
     FLinea = 0;

     aAlfa = eaFichero;
     |XABRE "", aAlfa, nHandle; nHandle = FSalida;
     |SI FSalida ] 0;
          |PARA;
          |SI FLinea ] 0; |Y swAcaba = 0;
          |HACIENDO;
               FLinea = 0;
               |HAZ LeeLinea;

               aEtiqueta = "Ccc";
               |HAZ BuscaEtiIni;
               |SI nLoc = 1;
                    |HAZ CCC;
               |FINSI;

               aEtiqueta = "PeriodoDesde";
               |HAZ BuscaEtiIni;
               |SI nLoc = 1;
                    aCampoFecha = "PeriodoDesde";
                    |HAZ TipoFecha;
                    aMesDesde = aMes;
                    nMesDesde = aMes;
                    aAnyDesde = aAny;
                    nAnyDesde = aAny;
               |FINSI;

               aEtiqueta = "PeriodoHasta";
               |HAZ BuscaEtiIni;
               |SI nLoc = 1;
                    aCampoFecha = "PeriodoHasta";
                    |HAZ TipoFecha;
                    aMesHasta = aMes;
                    aAnyHasta = aAny;
               |FINSI;

               aEtiqueta = "Tipo";
               |HAZ BuscaEtiIniFin;
               |SI nLoc = 1;
                    aTipo = aValor;
                    |SI aAlfa = "L00";
                         nTipo = 00;
                    |FINSI;
                    |SI aAlfa = "L13";
                         nTipo = 02;
                    |FINSI;
                    |BUCLE Borra_cretam34;
                    |BUCLE Borra_cretam35;
                    |SELECT #1#cretam34;
                    |SELECT #1#cretam35;
               |FINSI;

               aEtiqueta = "FechaHoraRecaudacion";
               |HAZ BuscaEtiIni;
               |SI nLoc = 1;
                    |HAZ FechaHoraRec;
               |FINSI;

               aEtiqueta = "TipoDCL";
               |HAZ BuscaEtiIniFin;
               |SI nLoc = 1;
                    aTipoDCL = aValor;
               |FINSI;

               aEtiqueta = "DatosCabeceraDCL";
               |HAZ BuscaEtiIni;
               |SI nLoc = 1;
                    |HAZ DatosCabeceraDCL;
               |FINSI;

               aEtiqueta = "LineaDCL";
               |HAZ BuscaEtiIni;
               |SI nLoc = 1;
                    |HAZ LineaDCL;
               |FINSI;
          |SIGUE;
     |FINSI;

     |XCIERRA nHandle;
|FPRC;

|PROGRAMA;
     |ABRE #cretam34;
     |ABRE #cretam35;

     |ABRE #cretam18;

     |ABRE #labempre;
     |ABRE #labcentr;
     |ABRE #nomcenes;
     |ABRE #labtraba;

     |HAZ Lectura;

     |CIERRA #cretam35;
     |CIERRA #cretam34;

     |CIERRA #cretam18;

     |CIERRA #labempre;
     |CIERRA #labcentr;
     |CIERRA #nomcenes;
     |CIERRA #labtraba;
|FPRO;
