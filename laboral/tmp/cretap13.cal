|FICHEROS;
     cretap13 #0;
     cretam00;
     cretat05,MANTE;
     cretae15;

     cretat07;
     cretam40;
|FIN;

|VARIABLES;
     fFechaRefPruebas = @;
     aAlfa = "";
     aParam = "";
     nContador = 0;
     nContadorResp = 0;
     aFichero = "";
     aTipo = "";
     aDesTipo = "";
     aValor = "";
     nLoc = 0;
     aEtiqueta = "";
     aEtiquetaIni = "";
     aEtiquetaFin = "";
     aLinea = "";
     aReferencia = "";
     nLinea = 0;
     aRutaComFich = "";
     nHandle = 0;
     FLinea = 0;
     swAcaba = 0;
     aPatron = "";
     aRutaCompleta = "";
     aRuta = "";
     aRutaBAT = "";
     aRutaFichero = "";
     nCanal = 0;
     aAny = "";
     aDesdeMes = "";
     aHastaMes = "";
     aDiaRec = "";
     aMesRec = "";
     aAnyRec = "";
     aHora = "";
     nHora = 0;
     swMarcaTodas = 0;
     aMarca = "";

 {-1}aMenu  = "";
     aMenu1 = "2400";
     aMenu2 = "1";
     aMenu3 = "";
     aMenu4 = "PRC";
     aMenu5 = " Procesar ";
     aMenu6 = " Revisar ";
     aMenu7 = " Cancelar ";
     nOpcion = 0;

     aDesdeSel = "";
     aHastaSel = "";

     &nFichero = 0;
     &nTotalFicheros = 0;
     &aSoloFichero = "";
     swCuenta = 0;

     FEstado = 0;
     fFecha = @;
     aFecha = "";

     aOrigen = "";
     aDestino = "";
     swRespuesta = 0;
     aCarpeta = "";
     aCadena = "";
     fFechaRef = @;
     nHoraRef = 0;
     aRutaDir = "";

     swRNT = 0;
     swTRT = 0;
     swBAS = 0;
     swDCL = 0;
     swRLC = 0;
     swBOR = 0;
     swCAL = 0;
     swCON = 0;

     {1}aContiene = "";
     aPathDestino = "";
     aUnidad = "";
     aEjecutable = "";
     aFicheroBAT = "";

     nAscii = 0;
     aCaracter = "";
     aCaracterSal = "";
     aCadenaSal = "";
     aLongitud = "";
     nLongitud = 0;
     nPos = 0;
     nBusca = 0;
     aListaFicheros = "";
     aAlfa1 = "";
     aUltimo = "";
     nPos1 = 0;

     swMsDos = 0;
     swBasico = 0;
     nRespuestasNuevas = 0;

     aLon = "";
     nLon = 0;
     nBlancos = 0;
     nEspacio = 0;
     nNume = 0;
     nBlancosIzd = 0;
     nBlancosDer = 0;
     aBlancosIzd = "";
     aBlancosDer = "";
     swNoMensaje = 0;
     swRutaErronea = 0;

     nFichBases = 0;
     nFichTT = 0;
     nFichRNT = 0;
     nFichDCL = 0;
     nFichRLC = 0;
     nFichCalculos = 0;
     nFichJusti = 0;

     nFichRtaTT = 0;
     nFichRtaBor = 0;
     nFichRtaCon = 0;
     nFichRtaCal = 0;

     nPosicion = 0;
     aIP = "";
     swPrimeraVez = 0;
     aBase = "";
     aBaseLocal = "";
     aIPRemoto = "";
|FIN;

|PROCESO Centra;
     aLon = aCadena % 0;
     nLon = aLon;
     nBlancos = nEspacio - nLon;
     nNume = nBlancos / 2;
     nBlancosIzd = nNume ! 0;
     nBlancosDer = nBlancos - nBlancosIzd;
     aBlancosIzd = " " * nBlancosIzd;
     aBlancosDer = " " * nBlancosDer;
     aCadenaSal = aBlancosIzd + aCadena + aBlancosDer;
|FPRC;

|PROCESO Opciones; |TIPO 0;
|FPRC;

|PROCESO Tipo12; |TIPO 12;

|FPRC;

|PROCESO MarcaTodas;
     #cretat05#10 = aMarca;
     |PINTA #cretat05#10;
     |GRABA 020#cretat05.a;
|FPRC;

|DEFBUCLE MarcaTodas;
     #cretat05#1;
     ;
     INICIO;
     FINAL;
     be;
     NULL, MarcaTodas;
|FIN;

|PROCESO Seleccion;
     |SI FSalida = 13; |O FSalida = 0;
          |SI #cretat05#10 = "*";
               #cretat05#10 = " ";
          |SINO;
               #cretat05#10 = "*";
          |FINSI;
          |PINTA #cretat05#10;
          |GRABA 020#cretat05.a;
     |FINSI;

     |SI FSalida = 18;
          |SI swMarcaTodas = 0;
               aMarca = "*";
          |SINO;
               aMarca = " ";
          |FINSI;

          |SI swMarcaTodas = 0;
               swMarcaTodas = 1;
          |SINO;
               swMarcaTodas = 0;
          |FINSI;

          |BUCLE MarcaTodas;

          |LEE 000#cretat05.p;

          |PONCONTROL 23, "si";
     |FINSI;
|FPRC;

|PROCESO Seleccion0; |TIPO 0;
     |HAZ Seleccion;
     |ERROR;
|FPRC;

|PROCESO Seleccion11; |TIPO 11;
     |HAZ Seleccion;
|FPRC;

|PROCESO GrabaRegistro;
     #cretae15#0 = #cretat05#5;         || Any
     #cretae15#1 = #cretat05#6;         || desde mes
     #cretae15#2 = #cretat05#7;         || hasta mes
     #cretae15#3 = #cretat05#2;         || tipo
     #cretae15#4 = #cretat05#1;         || nombre de fichero
     |LEE 101#cretae15.=;
     FEstado = FSalida;

     #cretae15#5 = #cretat05#3;         || descripcion tipo
     #cretae15#6 = #cretat05#11;        || Referencia
     #cretae15#7 = #cretat05#8;         || Fecha Fichero (Liquidacion)
     #cretae15#8 = #cretat05#9;         || Hora Fichero
     fFecha = ~;
     aFecha = fFecha;
     #cretae15#9 = (aFecha % 102) + "." + (aFecha % 402) + "." + (aFecha % -104);
     |HORA aHora;
     #cretae15#10 = (aHora % 102) + ":" + (aHora % 402);

     |SI FEstado = 0;
          |GRABA 020#cretae15.a;
     |SINO;
          |GRABA 020#cretae15.n;
     |FINSI;

     |LIBERA #cretae15;

     || esto grabara en el control de la ultima fecha y hora recogidos
     || la fecha y hora del ultimo fichero

     |ABRE #cretam40;
     |LEE 000#cretam40.p;
     |SI FSalida = 0;
          |SI #cretat05#12 > #cretam40#1;
               #cretam40#1 = #cretat05#12;
               #cretam40#2 = #cretat05#13;
          |SINO;
               |SI #cretat05#12 = #cretam40#1;
                    |SI #cretat05#13 > #cretam40#2;
                         #cretam40#2 = #cretat05#13;
                    |FINSI;
               |FINSI;
          |FINSI;
          |GRABA 000#cretam40.a;
     |FINSI;
     |CIERRA #cretam40;

/*
     aOrigen = aPatron - "*.xml" + #cretat05#1;
     aDestino = #cretam00#2;
     |QBF aDestino;
     aAlfa = aDestino % -101;
     |SI aAlfa ! "\";
          aDestino = aDestino + "\";
     |FINSI;
     aDestino = aDestino + "RESPUESTAS\";
     |RMKDIR aDestino;
     aDestino = aDestino + #cretat05#1;
     |RCOPIA_FICHERO aOrigen, aDestino;
     |SI FSalida = 0;
          ||RFBORRA aOrigen;
     |FINSI;
*/
|FPRC;

|PROCESO Resultado;
     |BLANCO 13062376;
     |CUADRO 13062376;

     aAlfa = "Detalle de mensajes recogidos desde SILTRA";
     nEspacio = 67;
     aCadena = aAlfa;
     |HAZ Centra;
     |ATRI R;
     |PINTA 1408, aCadenaSal;
     |ATRI 0;

     nPosicion = 1408;
     nEspacio = 67;

     |SI nFichBases ! 0;
          nPosicion = nPosicion + 100;
          aAlfa = nFichBases; |QBF aAlfa;
          aAlfa = aAlfa + " respuestas a Ficheros de Bases";

          aCadena = aAlfa; |HAZ Centra; |PINTA nPosicion, aCadenaSal;
     |FINSI;
     |SI nFichTT ! 0;
          nPosicion = nPosicion + 100;
          aAlfa = nFichTT; |QBF aAlfa;
          aAlfa = aAlfa + " Ficheros de Trabajadores y Tramos";

          aCadena = aAlfa; |HAZ Centra; |PINTA nPosicion, aCadenaSal;
     |FINSI;
     |SI nFichRNT ! 0;
          nPosicion = nPosicion + 100;
          aAlfa = nFichRNT; |QBF aAlfa;
          aAlfa = aAlfa + " Relaciones Nominales de Trabajadores";

          aCadena = aAlfa; |HAZ Centra; |PINTA nPosicion, aCadenaSal;
     |FINSI;
     |SI nFichDCL ! 0;
          nPosicion = nPosicion + 100;
          aAlfa = nFichDCL; |QBF aAlfa;
          aAlfa = aAlfa + " Documentos de Cálculo de Liquidaciones";

          aCadena = aAlfa; |HAZ Centra; |PINTA nPosicion, aCadenaSal;
     |FINSI;
     |SI nFichRLC ! 0;
          nPosicion = nPosicion + 100;
          aAlfa = nFichRLC; |QBF aAlfa;
          aAlfa = aAlfa + " Recibos de Liquidación de Cotizaciones";

          aCadena = aAlfa; |HAZ Centra; |PINTA nPosicion, aCadenaSal;
     |FINSI;
     |SI nFichCalculos ! 0;
          nPosicion = nPosicion + 100;
          aAlfa = nFichCalculos; |QBF aAlfa;
          aAlfa = aAlfa + " Ficheros de Cálculos";

          aCadena = aAlfa; |HAZ Centra; |PINTA nPosicion, aCadenaSal;
     |FINSI;
     |SI nFichJusti ! 0;
          nPosicion = nPosicion + 100;
          aAlfa = nFichJusti; |QBF aAlfa;
          aAlfa = aAlfa + " Justificantes de Envíos de Ficheros";

          aCadena = aAlfa; |HAZ Centra; |PINTA nPosicion, aCadenaSal;
     |FINSI;
     |SI nFichRtaTT ! 0;
          nPosicion = nPosicion + 100;
          aAlfa = nFichRtaTT; |QBF aAlfa;
          aAlfa = aAlfa + " Respuesta a Solicitud de Trab. y Tramos";

          aCadena = aAlfa; |HAZ Centra; |PINTA nPosicion, aCadenaSal;
     |FINSI;
     |SI nFichRtaBor ! 0;
          nPosicion = nPosicion + 100;
          aAlfa = nFichRtaBor; |QBF aAlfa;
          aAlfa = aAlfa + " Respuesta a Solicitud de Borrador";

          aCadena = aAlfa; |HAZ Centra; |PINTA nPosicion, aCadenaSal;
     |FINSI;
     |SI nFichRtaCon ! 0;
          nPosicion = nPosicion + 100;
          aAlfa = nFichRtaCon; |QBF aAlfa;
          aAlfa = aAlfa + " Respuesta a Solicitud de Confirmación";

          aCadena = aAlfa; |HAZ Centra; |PINTA nPosicion, aCadenaSal;
     |FINSI;
     |SI nFichRtaCal ! 0;
          nPosicion = nPosicion + 100;
          aAlfa = nFichRtaCal; |QBF aAlfa;
          aAlfa = aAlfa + " Respuesta a Solicitud de Cálculos";

          aCadena = aAlfa; |HAZ Centra; |PINTA nPosicion, aCadenaSal;
     |FINSI;

     |PAUSA;
|FPRC;

|PROCESO LeeRespuestas;
     |SI swCuenta = 1;
          nTotalFicheros = nTotalFicheros + 1;
     |SINO;
          nFichero = nFichero + 1;
          aSoloFichero = #cretat05#1;
          |QBF aSoloFichero;
/*
          |SI #cretat05#2 = "C";
               || de momento no leo el Fichero de calculos

               |ACABA;
          |FINSI;
*/
          aAlfa = #cretam00#3;
          |QBF aAlfa;
          |SI #cretat05#2 = "R"; |O #cretat05#2 = "D"; |O #cretat05#2 = "L";
               |SI #cretat05#2 = "R";
                    aPatron = aAlfa + "XDCR\RNT\";
               |FINSI;
               |SI #cretat05#2 = "D";
                    aPatron = aAlfa + "XDCR\DCL\";
               |FINSI;
               |SI #cretat05#2 = "L";
                    aPatron = aAlfa + "XDCR\RLC\";
               |FINSI;
          |SINO;
               aPatron = aAlfa + "SVA\Msjrec\";
          |FINSI;
          aFichero = aPatron + #cretat05#1;

          aCadena = aFichero;
          swMsDos = 0;
          swBasico = 1;
          |HAZ Tildes;
          aFichero = aCadenaSal;

          |DBASE aDestino;
          aDestino = aDestino + "tmp/" + aSoloFichero;

          aAlfa = "cretap12;" + aDestino;
          |SUB_EJECUTA_NP aAlfa;

          |FBORRA aDestino;

          |HAZ GrabaRegistro;

          || B - Fichero de Bases
          || T - Fichero de Trabajadores y Tramos
          || R - RNT
          || D - DCL
          || L - RLC
          || C - Fichero de Calculos
          || M - Mensaje de transmision de ficheros

          || t - Rpta. solicitud de Fichero de Trabajadores y Tramos
          || r - Rpta. solicitud de Borrador
          || f - Rpta. solicitud de Confirmacion
          || c - Rpta. solicitud de Calculos

          |SI #cretat05#2 = "B"; nFichBases = nFichBases + 1; |FINSI;
          |SI #cretat05#2 = "T"; nFichTT = nFichTT + 1; |FINSI;
          |SI #cretat05#2 = "R"; nFichRNT = nFichRNT + 1; |FINSI;
          |SI #cretat05#2 = "D"; nFichDCL = nFichDCL + 1; |FINSI;
          |SI #cretat05#2 = "L"; nFichRLC = nFichRLC + 1; |FINSI;
          |SI #cretat05#2 = "C"; nFichCalculos = nFichCalculos + 1; |FINSI;
          |SI #cretat05#2 = "M"; nFichJusti = nFichJusti + 1; |FINSI;
          |SI #cretat05#2 = "t"; nFichRtaTT = nFichRtaTT + 1; |FINSI;
          |SI #cretat05#2 = "r"; nFichRtaBor = nFichRtaBor + 1; |FINSI;
          |SI #cretat05#2 = "f"; nFichRtaCon = nFichRtaCon + 1; |FINSI;
          |SI #cretat05#2 = "c"; nFichRtaCal = nFichRtaCal + 1; |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE LeeRespuestas;
     #cretat05#3;
     ;
     001, aDesdeSel;
     999, aHastaSel;
     ;
     NULL, LeeRespuestas;
|FIN;

|PROCESO Respuestas;
     |ATRI R;
     |BLANCO 19232357;
     |ATRI 0;

     |SI #0#0 = "T";
          aDesdeSel = " ";
          aHastaSel = "*";
     |FINSI;
     |SI #0#0 = "S";
          aDesdeSel = "*";
          aHastaSel = "*";
     |FINSI;

     swCuenta = 1;
     |BUCLE LeeRespuestas;

     swCuenta = 0;
     |BUCLE LeeRespuestas;
|FPRC;

|PROCESO inferior;
     #cretat05#0 = 001;
|FPRC;

|PROCESO superior;
     #cretat05#0 = 999;
|FPRC;

|PROCESO Muestra;
     nOpcion = 2;
     |PARA; |SI; |HACIENDO;
          |PINPA #0#cretat05;
          |ENTLINEAL #2#cretat05, 1, 4, 22, 0, inferior, superior;
          |MENU aMenu;
          nOpcion = FSalida;

          |SI nOpcion = 1;
               |HAZ Respuestas;
               |SAL;
          |FINSI;
          |SI nOpcion = 2;
               || vuelta al lineal
          |FINSI;
          |SI nOpcion = 3;
               |SAL;
          |FINSI;
     |SIGUE;
|FPRC;

|PROCESO Renumera;
     nContador = nContador + 1;
     #cretat05#0 = nContador;
     |GRABA 020#cretat05.a;
     |LIBERA #cretat05;
|FPRC;

|DEFBUCLE Renumera;
     #cretat05#1;
     ;
     INICIO;
     FINAL;
     be;
     NULL, Renumera;
|FIN;

|PROCESO GrabaFichero;
     nContador = nContador + 1;
     || #cretat05#0 = nContador;

     #cretat05#1 = aFichero;
     #cretat05#2 = aTipo;
     #cretat05#3 = aDesTipo;
     #cretat05#4 = "Pendiente";
     #cretat05#5 = aAny;
     #cretat05#6 = aDesdeMes;
     #cretat05#7 = aHastaMes;
     aFecha = aDiaRec + "." + aMesRec + "." + aAnyRec;
     fFecha = aFecha;
     #cretat05#8 = fFecha;
     #cretat05#9 = (aHora % 102) + ":" + (aHora % 302);
     #cretat05#11 = aReferencia;
     #cretat05#12 = #cretat07#2;   || fecha del fichero
     #cretat05#13 = #cretat07#3;   || hora del fichero

     |GRABA 020#cretat05.n;
     |LIBERA #cretat05;
|FPRC;

|PROCESO BuscaEtiIni;
     nLoc = 0;
     aEtiqueta = "<" + aEtiqueta;
     aAlfa = aLinea - aEtiqueta;
     |SI aAlfa ! aLinea;
          nLoc = 1;
     |FINSI;
|FPRC;

|PROCESO BuscaEtiIniFin;
     aValor = "";
     nLoc = 0;

     aEtiquetaIni = "<" + aEtiqueta + ">";
     aEtiquetaFin = "</" + aEtiqueta + ">";
     aAlfa = aLinea - aEtiquetaIni - aEtiquetaFin;
     |SI aAlfa ! aLinea;
          aValor = aAlfa;
          |QBT aValor;
          nLoc = 1;
     |FINSI;
|FPRC;

|PROCESO TipoFichero;
     aReferencia = "";
     nLinea = 0;
     swAcaba = 0;
     aAny = "";
     aDesdeMes = "";
     aHastaMes = "";
     aDiaRec = "";
     aMesRec = "";
     aAnyRec = "";
     aHora = "";
     swRespuesta = 0;
     swTRT = 0
     swRNT = 0;
     swBAS = 0;
     swDCL = 0;
     swRLC = 0;
     swBOR = 0;
     swCAL = 0;
     swCON = 0;

     FLinea = 0;

     |DBASE aDestino;
     aDestino = aDestino + "tmp/";
     |MKDIR aDestino;
     aDestino = aDestino + aFichero;

     |IP_REMOTO aIP;
     |SI aIP ! "";
          |RECIBE_FICHERO aRutaComFich, aDestino;
     |SINO;
          |COPIA_FICHERO aRutaComFich, aDestino;
     |FINSI;

     aAlfa = aRutaComFich % -103;
     |SI aAlfa = "t01";
          || tengo claro lo que es: un mensaje de transmision
          aTipo = "M";
          aDesTipo = "Mensaje Transm. ";

          |ACABA;
     |FINSI;

     |XABRE "", aDestino, nHandle; nHandle = FSalida;
     |SI FSalida ] 0;
          |PARA;
          |SI FLinea ] 0; |Y swAcaba = 0;
          |HACIENDO;
               nLinea = nLinea + 1;
               swAcaba = 0;
               FLinea = 0;

               |XLEE nHandle, 250, aLinea;
               FLinea = FSalida;

               aEtiqueta = "ReferenciaExterna";
               |HAZ BuscaEtiIniFin;
               |SI nLoc = 1;
                    aReferencia = aValor;
               |FINSI;

               aEtiqueta = "Respuesta";
               |HAZ BuscaEtiIni;
               |SI nLoc = 1;
                    swRespuesta = 1;
               |FINSI;

               aEtiqueta = "TrabajadoresTramos";
               |HAZ BuscaEtiIni;
               |SI nLoc = 1;
                    swTRT = 1;
               |FINSI;

               aEtiqueta = "RelacionNominalTrabajadores";
               |HAZ BuscaEtiIni;
               |SI nLoc = 1;
                    swRNT = 1;
               |FINSI;

               aEtiqueta = "Calculos";
               |HAZ BuscaEtiIni;
               |SI nLoc = 1;
                    swCAL = 1;
               |FINSI;

               aEtiqueta = "ReciboLiquidacionCotizaciones";
               |HAZ BuscaEtiIni;
               |SI nLoc = 1;
                    swRLC = 1;
               |FINSI;

               aEtiqueta = "DCL";
               |HAZ BuscaEtiIni;
               |SI nLoc = 1;
                    swDCL = 1;
               |FINSI;

               aEtiqueta = "PeriodoDesde";
               |HAZ BuscaEtiIni;
               |SI nLoc = 1;
                    |XLEE nHandle, 250, aLinea;
                    aEtiqueta = "Mes";
                    |HAZ BuscaEtiIniFin;
                    aDesdeMes = aValor;
               |FINSI;

               aEtiqueta = "PeriodoHasta";
               |HAZ BuscaEtiIni;
               |SI nLoc = 1;
                    |XLEE nHandle, 250, aLinea;
                    aEtiqueta = "Mes";
                    |HAZ BuscaEtiIniFin;
                    aHastaMes = aValor;

                    |XLEE nHandle, 250, aLinea;
                    aEtiqueta = "Anho";
                    |HAZ BuscaEtiIniFin;
                    aAny = aValor;
               |FINSI;

               || PARA EL FICHERO DE BASES Y DE TRABAJADORES Y TRAMOS
               || Y el RNT y el DCL y el RLC

               aEtiqueta = "FechaRecaudacion";
               |HAZ BuscaEtiIni;
               |SI nLoc = 1;
                    |XLEE nHandle, 250, aLinea;
                    aEtiqueta = "Dia";
                    |HAZ BuscaEtiIniFin;
                    |SI nLoc = 1;
                         aDiaRec = aValor;
                    |FINSI;

                    |XLEE nHandle, 250, aLinea;
                    aEtiqueta = "Mes";
                    |HAZ BuscaEtiIniFin;
                    |SI nLoc = 1;
                         aMesRec = aValor;
                    |FINSI;

                    |XLEE nHandle, 250, aLinea;
                    aEtiqueta = "Anho";
                    |HAZ BuscaEtiIniFin;
                    |SI nLoc = 1;
                         aAnyRec = aValor;
                    |FINSI;
               |FINSI;

               aEtiqueta = "HoraRecaudacion";
               |HAZ BuscaEtiIniFin;
               |SI nLoc = 1;
                    aHora = aValor;
               |FINSI;

               || PARA EL FICHERO DE CALCULOS

               aEtiqueta = "FechaUltimoCalculo";
               |HAZ BuscaEtiIni;
               |SI nLoc = 1;
                    |XLEE nHandle, 250, aLinea;
                    aEtiqueta = "Dia";
                    |HAZ BuscaEtiIniFin;
                    |SI nLoc = 1;
                         aDiaRec = aValor;
                    |FINSI;

                    |XLEE nHandle, 250, aLinea;
                    aEtiqueta = "Mes";
                    |HAZ BuscaEtiIniFin;
                    |SI nLoc = 1;
                         aMesRec = aValor;
                    |FINSI;

                    |XLEE nHandle, 250, aLinea;
                    aEtiqueta = "Anho";
                    |HAZ BuscaEtiIniFin;
                    |SI nLoc = 1;
                         aAnyRec = aValor;
                    |FINSI;
               |FINSI;

               aEtiqueta = "HoraUltimoCalculo";
               |HAZ BuscaEtiIniFin;
               |SI nLoc = 1;
                    aHora = aValor;
               |FINSI;

               |SI nLinea ] 100;
                    swAcaba = 1;
               |FINSI;
          |SIGUE;
     |FINSI;
     |XCIERRA nHandle;

     aAlfa = aReferencia % 101;
     |SI swRespuesta = 1;
          |SI aReferencia = "";
               aTipo = "X";
               aDesTipo = "No procesable";
          |FINSI;
          |SI aAlfa = "B";    || el fichero de BASES
               swBAS = 1;
          |FINSI;
          |SI aAlfa = "T";    || la respuesta a la sol. del fich. de TRAB-TRAM
               swTRT = 2;
          |FINSI;
          |SI aAlfa = "R";    || la respuesta a la sol. de BORRADOR
               swBOR = 2;
          |FINSI;
          |SI aAlfa = "C";    || la respuesta a la sol. de CALCULOS
               swCAL = 2;
          |FINSI;
          |SI aAlfa = "F"; |O aReferencia = "C.OFICIO";
                              || la respuesta a la sol. de CONFIRMACION
               swCON = 2;
          |FINSI;
     |FINSI;

     |SI swTRT = 1;
          aTipo = "T";
          aDesTipo = "Trabajadores y Tramos";
     |FINSI;
     |SI swTRT = 2;
          aTipo = "t";
          aDesTipo = "Rpta. Trabajadores y Tramos";
     |FINSI;
     |SI swBOR = 2;
          aTipo = "r";
          aDesTipo = "Rpta. Solic. Borrador";
     |FINSI;
     |SI swCAL = 1;
          aTipo = "C";
          aDesTipo = "Fichero de Calculos";
     |FINSI;
     |SI swCAL = 2;
          aTipo = "c";
          aDesTipo = "Rpta. Solic. Calculos";
     |FINSI;
     |SI swCON = 2;
          aTipo = "f";
          |SI aReferencia = "C.OFICIO";
               aDesTipo = "Confirmacion de Oficio";
          |SINO;
               aDesTipo = "Rpta. Solic. Confirmacion";
          |FINSI;
     |FINSI;
     |SI swBAS = 1;
          aTipo = "B";
          aDesTipo = "Fichero de Bases";
     |FINSI;
     |SI swRNT = 1;            || el RNT no lleva Referencia
          aTipo = "R";
          aDesTipo = "Rel. Nominal Trab.";
     |FINSI;
     |SI swDCL = 1;            || el DCL tampoco lleva Referencia
          aTipo = "D";
          aDesTipo = "Doc. Calculo Liq.";
     |FINSI;
     |SI swRLC = 1;            || el RLC tampoco lleva Referencia
          aTipo = "L";
          aDesTipo = "Rec. Liq. Cotiz.";
     |FINSI;
|FPRC;

|PROCESO Cabecera;
     |ATRI R;
     |PINTA 1223, "   RECOGIDA DE RESPUESTAS SILTRA   ";
     |ATRI 0;
     |BLANCO 13231757;
     |CUADRO 13231757;

     || 2      3         4         5
     || 345678901234567890123456789012345

     aAlfa = " Buscando nuevas respuestas ";
     aCadena = aAlfa; nEspacio = 33; |HAZ Centra;
     |PINTA 1424, aCadenaSal;
|FPRC;

|PROCESO LeeFicheros;
     nContadorResp = nContadorResp + 1;

     aAlfa = nRespuestasNuevas;
     aAlfa1 = nContadorResp;
     |QBF aAlfa;
     aAlfa = " Fichero " + aAlfa1 + " de " + aAlfa + " ";
     aCadena = aAlfa; nEspacio = 33; |HAZ Centra;
     |PINTA 1524, aCadenaSal;

     aFichero = #cretat07#0;
     |QBF aFichero;
     aRutaComFich = #cretat07#1;
     |QBF aRutaComFich;

     aCadena = aRutaComFich;
     swMsDos = 0;
     swBasico = 1;
     |HAZ Tildes;
     aRutaComFich = aCadenaSal;

     |HAZ TipoFichero;


     || B - Fichero de Bases
     || T - Fichero de Trabajadores y Tramos
     || R - RNT
     || D - DCL
     || L - RLC
     || C - Fichero de Calculos
     || M - Mensaje de transmision de ficheros

     || t - Rpta. solicitud de Fichero de Trabajadores y Tramos
     || r - Rpta. solicitud de Borrador
     || f - Rpta. solicitud de Confirmacion
     || c - Rpta. solicitud de Calculos

     |SI aTipo = "B"; |O aTipo = "R"; |O aTipo = "D"; |O aTipo = "L";
     |O aTipo = "F"; |O aTipo = "M"; |O aTipo = "C";
          |HAZ GrabaFichero;
     |FINSI;
     |SI aTipo = "T";
          || Fichero de trabajadores y tramos
          |HAZ GrabaFichero;
     |FINSI;
     |SI aTipo = "t"; |O aTipo = "r"; |O aTipo = "c"; |O aTipo = "f";
          |HAZ GrabaFichero;
     |FINSI;
     |SI aTipo = "X";
          || tambien, aunque no se procesen, para que me conincida el numero
          || de ficheros con el del DIR que hago
          |HAZ GrabaFichero;
     |FINSI;
|FPRC;

|DEFBUCLE LeeFicheros;
     #cretat07#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, LeeFicheros;
|FIN;

|PROCESO sustituye2;
     nAscii = & aCaracter;
     aCaracterSal = aCaracter;
     |SI nAscii = 160; |O nAscii = 225;             || el caracter  
          |SI swBasico = 1;
               aCaracterSal = "á";
          |FINSI;
          |SI swMsDos = 1;
               aCaracterSal = " ";
          |FINSI;
     |FINSI;
     |SI nAscii = 130; |O nAscii = 233;             || el caracter ‚
          |SI swBasico = 1;
               aCaracterSal = "é";
          |FINSI;
          |SI swMsDos = 1;
               aCaracterSal = "‚";
          |FINSI;
     |FINSI;
     |SI nAscii = 161; |O nAscii = 237;             || el caracter ¡
          |SI swBasico = 1;
               aCaracterSal = "í";
          |FINSI;
          |SI swMsDos = 1;
               aCaracterSal = "¡";
          |FINSI;
     |FINSI;
     |SI nAscii = 162; |O nAscii = 243;            || el caracter ¢
          |SI swBasico = 1;
               aCaracterSal = "ó";
          |FINSI;
          |SI swMsDos = 1;
               aCaracterSal = "¢";
          |FINSI;
     |FINSI;
     |SI nAscii = 163; |O nAscii = 250;            || el caracter £
          |SI swBasico = 1;
               aCaracterSal = "ú";
          |FINSI;
          |SI swMsDos = 1;
               aCaracterSal = "£";
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Tildes;
     aCadenaSal = "";
     aLongitud = aCadena % 0;
     nLongitud = aLongitud;
     |PARA nPos = 1; |SI nPos [ nLongitud; |HACIENDO nPos = nPos + 1;
          nBusca = (nPos * 100) + 1;
          aCaracter = aCadena % nBusca;
          |HAZ sustituye2;
          aCadenaSal = aCadenaSal + aCaracterSal;
     |SIGUE;
|FPRC;

|PROCESO CambiaTildes;
     aCadena = aRutaDir;
     || swMsDos = 0;
     || swBasico = 1;
     || estos dos parametros los dejo configurados antes

     |HAZ Tildes;
     aRutaDir = aCadenaSal;
|FPRC;

|PROCESO Ejecutables;
     |DBASE aBase;  |QBF aBase;
     aOrigen = aBase + "ejecutables/dsmini.exe";

     aContiene = "";
     |ESPECIFICA "RBASS", aContiene;
     aBaseLocal = aContiene1 % 102;

     aDestino = aBaseLocal + "/DOCUMENTOS/dsmini.exe";
     |XABRE "CE", aDestino, nCanal;
     |SI FSalida ] 0;
          || existe ya
     |SINO;

          || me lo traigo
          |IP_REMOTO aIPRemoto;
          |SI aIPRemoto ! "";
               |ENVIA_FICHERO aOrigen, aDestino;
          |SINO;
               |COPIA_FICHERO aOrigen, aDestino;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO PruebaBAT;
     || en primer lugar tengo que ver de que manera tengo que tratar las tildes
     || ya que en unos equipos me salen de una manera y en otros de otra

     aFicheroBAT = aUnidad + "\DOCUMENTOS\" + Usuario + "_prueba.bat";
     |RFBORRA aFicheroBAT;
     |XABRE "CBW", aFicheroBAT, nCanal;
     |SI FSalida ] 0;
          aAlfa = "@echo off" + &13 + &10;
          |XGRABA nCanal, aAlfa;

          || respuestas a fichero de bases y de trabajadores y tramos
          || y resultados de la transmision

          aListaFicheros = aUnidad + "\DOCUMENTOS\dir_sva_prueba.txt";
          aRutaDir = aRuta + "SVA\Msjrec\";

          swMsDos = 1;
          swBasico = 0;
          |HAZ CambiaTildes;
          aAlfa = "dir /O:-D " + &34 + aRutaDir + "*.xml" + &34 + " > " + aListaFicheros + &13 + &10;
          |XGRABA nCanal, aAlfa;

          aAlfa = "exit" + &13 + &10;
          |XGRABA nCanal, aAlfa;

          |XCIERRA nCanal;

          || ahora lo ejecuto
          || aAlfa = "cmd " + &34 + "/c START /WAIT " + aFicheroBAT + &34;

          aAlfa = "1. Comprobando carpetas";
          aCadena = aAlfa; nEspacio = 33; |HAZ Centra;
          |PINTA 1524, aCadenaSal;

          aAlfa = aUnidad + "\DOCUMENTOS\dsmini.exe ";
          aAlfa = aAlfa + aUnidad + "\DOCUMENTOS\" + Usuario + "_prueba.bat";
          |RSYSTEM aAlfa;
          |SLEEP 2;

          || ahora examino el fichero generado

          FLinea = 0;
          nLinea = 0;

          |XABRE "C", aListaFicheros, nHandle;
          nHandle = FSalida;
          |SI FSalida ] 0;
               |PARA; |SI FLinea ] 0; |HACIENDO;
                    |XLEE nHandle, 250, aLinea;
                    FLinea = FSalida;
                    nLinea = nLinea + 1
                    |SI nLinea = 3;
                         || suficiente para saber que todo va bien
                         |SAL;
                    |FINSI;
               |SIGUE;
          |FINSI;
          |XCIERRA nHandle;

          |SI nLinea > 1;
               || esta configuracion es la buena

               swMsDos = 1;
               swBasico = 0;
          |SINO;
               || es esta, la otra posibilidad

               swMsDos = 0;
               swBasico = 1;
          |FINSI;
          |RFBORRA aListaFicheros;
     |FINSI;

|FPRC;

|PROCESO GeneraBAT;
     || primero hago la prueba de directorios por el rollo de las tildes
     |HAZ PruebaBAT;

     || esto es solo para hacer el BAT
     aFicheroBAT = aUnidad + "\DOCUMENTOS\" + Usuario + ".bat";
     |RFBORRA aFicheroBAT;
     |XABRE "CBW", aFicheroBAT, nCanal;
     |SI FSalida ] 0;
          aAlfa = "@echo off" + &13 + &10;
          |XGRABA nCanal, aAlfa;

          || respuestas a fichero de bases y de trabajadores y tramos
          || y resultados de la transmision

          aListaFicheros = aUnidad + "\DOCUMENTOS\dir_sva.txt";
          aRutaDir = aRuta + "SVA\Msjrec\";
          |HAZ CambiaTildes;
          aAlfa = "dir /O:-D " + &34 + aRutaDir + "*.xml" + &34 + " > " + aListaFicheros + &13 + &10;
          |XGRABA nCanal, aAlfa;

          aListaFicheros = aUnidad + "\DOCUMENTOS\dir_sva2.txt";
          aRutaDir = aRuta + "SVA\Msjrec\";
          |HAZ CambiaTildes;
          aAlfa = "dir /O:-D " + &34 + aRutaDir + "*.t01" + &34 + " > " + aListaFicheros + &13 + &10;
          |XGRABA nCanal, aAlfa;

          || RNT

          aListaFicheros = aUnidad + "\DOCUMENTOS\dir_rnt.txt";
          aRutaDir = aRuta + "XDCR\RNT\";
          |HAZ CambiaTildes;
          aAlfa = "dir /O:-D " + &34 + aRutaDir + "*.xml" + &34 + " > " + aListaFicheros + &13 + &10;
          |XGRABA nCanal, aAlfa;

          || DCL

          aListaFicheros = aUnidad + "\DOCUMENTOS\dir_dcl.txt";
          aRutaDir = aRuta + "XDCR\DCL\";
          |HAZ CambiaTildes;
          aAlfa = "dir /O:-D " + &34 + aRutaDir + "*.xml" + &34 + " > " + aListaFicheros + &13 + &10;
          |XGRABA nCanal, aAlfa;

          || RLC

          aListaFicheros = aUnidad + "\DOCUMENTOS\dir_rlc.txt";
          aRutaDir = aRuta + "XDCR\RLC\";
          |HAZ CambiaTildes;
          aAlfa = "dir /O:-D " + &34 + aRutaDir + "*.xml" + &34 + " > " + aListaFicheros + &13 + &10;
          |XGRABA nCanal, aAlfa;

          aAlfa = "exit" + &13 + &10;
          |XGRABA nCanal, aAlfa;

          |XCIERRA nCanal;
     |FINSI;
|FPRC;

|PROCESO ExaminaDir;
     FLinea = 0;

     |XABRE "C", aListaFicheros, nHandle;
     nHandle = FSalida;
     |SI FSalida ] 0;
          |PARA; |SI FLinea ] 0; |HACIENDO;
               |XLEE nHandle, 250, aLinea;
               FLinea = FSalida;

               |QBF aLinea;

               aAlfa = aLinea;
               aAlfa1 = aLinea - "xml";
               |SI aAlfa = aAlfa1;
                    aAlfa1 = aLinea - "t01";
               |FINSI;
               aFichero = "";
               |SI aAlfa ! aAlfa1;
                    aFecha = aLinea % 110;
                    aFecha = (aFecha % 102) + "." + (aFecha % 402) + "."  + (aFecha % -104);
                    fFecha = aFecha;
                    aHora = aLinea % 1305;
                    aHora = (aHora % 102) + (aHora % 402);
                    nHora = aHora;

                    swAcaba = 0;
                    |SI fFecha < fFechaRef;
                         swAcaba = 1;
                    |SINO;
                         |SI fFecha = fFechaRef;
                              |SI nHora [ nHoraRef;
                                   swAcaba = 1;
                              |FINSI;
                         |FINSI;
                    |FINSI;
/*
                    fFechaRefPruebas = "17.12.2014";
                    |SI fFecha > fFechaRefPruebas;
                         swAcaba = 1;
                    |FINSI;
*/
                    |PARA nPos = 1; |SI swAcaba = 0; |HACIENDO nPos = nPos + 1;
                         nPos1 = ((nPos * 100) + 1) * (-1);
                         aUltimo = aLinea % nPos1;
                         |SI aUltimo ! " ";
                              aFichero = aUltimo + aFichero;
                         |SINO;
                              swAcaba = 1;
                         |FINSI;
                    |SIGUE;

                    || ya tengo el xml en aFichero y se que la fecha es
                    || superior a la ultima fecha en que recogi algo

                    || ahora lo meto en el temporal del que luego cogere los
                    || xml que tengo que leer

                    |QBF aFichero;
                    |SI aFichero ! "";
                         #cretat07#0 = aFichero;
                         #cretat07#1 = aRutaDir + aFichero;
                         #cretat07#2 = fFecha;
                         #cretat07#3 = nHora;
                         |GRABA 020#cretat07.n;
                         |LIBERA #cretat07;

                         nRespuestasNuevas = nRespuestasNuevas + 1;
                    |FINSI;
               |FINSI;
          |SIGUE;
     |FINSI;

     |XCIERRA nHandle;

     |RFBORRA aListaFicheros;
|FPRC;

|PROCESO SeleccionaFicheros;
     || en primero lugar me tengo que hacer un BAT que me lea los ficheros xml
     || de los directorios donde estan y me los deje grabados en un fichero

     aAlfa = aUnidad + "\DOCUMENTOS\dir.txt";
     |RFBORRA aAlfa;

     || creo el BAT (en C:\DOCUMENTOS) y lo ejecuto

     |HAZ GeneraBAT;
     || aAlfa = "cmd " + &34 + "/c START /WAIT " + aFicheroBAT + &34;

     aAlfa = "2. Explorando carpetas";
     aCadena = aAlfa; nEspacio = 33; |HAZ Centra;
     |PINTA 1524, aCadenaSal;

     aAlfa = aUnidad + "\DOCUMENTOS\dsmini.exe ";
     aAlfa = aAlfa + aUnidad + "\DOCUMENTOS\" + Usuario + ".bat";

     |RSYSTEM aAlfa;
     |SLEEP 2;

     || Ya tengo en C:\documentos\dir_XXX.txt los xml de cada tipo, ahora
     || examino las lineas
     || para meterme en un fichero los que tengo que coger segun la fecha

     aAlfa = "3. Buscando respuestas SVA";
     aCadena = aAlfa; nEspacio = 33; |HAZ Centra;
     |PINTA 1524, aCadenaSal;

     aRutaDir = aRuta + "SVA\Msjrec\";
     aListaFicheros = aUnidad + "\DOCUMENTOS\dir_sva.txt";
     |HAZ ExaminaDir;

     aAlfa = "4. Buscando respuestas SVA2";
     aCadena = aAlfa; nEspacio = 33; |HAZ Centra;
     |PINTA 1524, aCadenaSal;

     aRutaDir = aRuta + "SVA\Msjrec\";
     aListaFicheros = aUnidad + "\DOCUMENTOS\dir_sva2.txt";
     |HAZ ExaminaDir;

     aAlfa = "5. Buscando respuestas RNT";
     aCadena = aAlfa; nEspacio = 33; |HAZ Centra;
     |PINTA 1524, aCadenaSal;

     aRutaDir = aRuta + "XDCR\RNT\";
     aListaFicheros = aUnidad + "\DOCUMENTOS\dir_rnt.txt";
     |HAZ ExaminaDir;

     aAlfa = "6. Buscando respuestas DCL";
     aCadena = aAlfa; nEspacio = 33; |HAZ Centra;
     |PINTA 1524, aCadenaSal;

     aRutaDir = aRuta + "XDCR\DCL\";
     aListaFicheros = aUnidad + "\DOCUMENTOS\dir_dcl.txt";
     |HAZ ExaminaDir;

     aAlfa = "7. Buscando respuestas RLC";
     aCadena = aAlfa; nEspacio = 33; |HAZ Centra;
     |PINTA 1524, aCadenaSal;

     aRutaDir = aRuta + "XDCR\RLC\";
     aListaFicheros = aUnidad + "\DOCUMENTOS\dir_rlc.txt";
     |HAZ ExaminaDir;

     aAlfa = "Búsqueda finalizada";
     aCadena = aAlfa; nEspacio = 33; |HAZ Centra;
     |PINTA 1524, aCadenaSal;

     ||RFBORRA aFicheroBAT;
|FPRC;

|PROCESO CompruebaRuta;
     aRuta = #cretam00#3;
     |QBF aRuta;

     swMsDos = 0;
     swBasico = 1;

     aAlfa = aRuta + "Creta.jar"; || busco si existe
     aCadena = aAlfa;
     |HAZ Tildes;
     aAlfa = aCadenaSal;
     |XABRE "CE", aAlfa, nCanal;        || spoolpdf
     |SI FSalida ] 0;
          || existe
          swRutaErronea = 0;
     |SINO;
          swRutaErronea = 1;
     |FINSI;
     |XCIERRA nCanal;

     |SI swRutaErronea = 1;
          aAlfa = aRuta + "SILTRAPracticas.jar"; || busco si existe
          aCadena = aAlfa;
          |HAZ Tildes;
          aAlfa = aCadenaSal;
          |XABRE "CE", aAlfa, nCanal;        || spoolpdf
          |SI FSalida ] 0;
               || existe
               swRutaErronea = 0;
          |SINO;
               swRutaErronea = 1;
          |FINSI;
          |XCIERRA nCanal;
     |FINSI;
|FPRC;

|PROCESO Recogida;
     |DDEFECTO #cretam40;
     |ABRE #cretam40;
     |LEE 000#cretam40.p;
     |SI FSalida ! 0;
          fFechaRef = ~;
          nHoraRef = 0000;
          |GRABA 000#cretam40.n;
     |SINO;
          fFechaRef = #cretam40#1;
          nHoraRef = #cretam40#2;
     |FINSI;

     |CIERRA #cretam40;

     aContiene = "";
     |ESPECIFICA "RBASS", aContiene;
     aPathDestino = aContiene1;
     |QBF aPathDestino;
     aUnidad = aPathDestino % 102;

     aAlfa = Usuario;  |QBF aAlfa; aAlfa = "c2" + aAlfa;
     |NOME_DAT #cretat07#aAlfa#;
     |DELINDEX #cretat07;
     |ABRE #cretat07;


     aAlfa = #cretam00#3;
     |QBF aAlfa;
     aRuta = aAlfa;

     nRespuestasNuevas = 0;

     || antes que nada compruebo que la ruta exista

     swRutaErronea = 0;

     |HAZ CompruebaRuta;

     |SI swRutaErronea = 1;
          |SI swPrimeraVez = 1;
               aAlfa = "    ATENCION. En primer lugar, es necesario activar Cret@ y configurar la ruta ";
               aAlfa = aAlfa + "de instalación de SILTRA.";
               |MENAV aAlfa;
               aAlfa = "    Revise los Parámetros del Sistema de Liquidación Directa";
               |MENAV aAlfa;

               |SUB_EJECUTA "cretam00.tab";
          |SINO;
               aAlfa = "    ATENCION. No se ha encontrado la ruta de instalación ";
               aAlfa = aAlfa + "de SILTRA";
               |MENAV aAlfa;

               aAlfa = "    Revise los Parámetros del Sistema de Liquidación Directa";
               |MENAV aAlfa;
          |FINSI;

          swNoMensaje = 1;

          |CIERRA #cretat07;
          |ACABA;
     |FINSI;

     || primero genero el BAT, con los directorios que quiero examinar

     |HAZ GeneraBAT;

     || despues leo los tres directorios y me guardo en el cretat07
     || los ficheros que cumplen con la condicion de la fecha

     |HAZ SeleccionaFicheros;

     |DBASS aAlfa; |QBF aAlfa;
     aAlfa = aAlfa % -109;
     |SI aAlfa = "dsprofes/";       || estoy en produccion, sin duda
          ||CONSULTA_CLAVE #1#cretat07;
     |FINSI;

     |SI #0#0 ! "T";
          |HAZ Cabecera;
     |FINSI;

     |SI nRespuestasNuevas ! 0;
          |SI aParam = "A"; |Y #cretam00#6 = 1;
               || no preguntes, recoge directamente
               FSalida = 0;
          |SINO;
               aAlfa = "    SSe han encontrado nuevas res- puestas en SILTRA ";
               aAlfa = aAlfa + "¿Desea recogerlas ahora?";
               |CONFI aAlfa;
          |FINSI;
          |SI FSalida = 0;
               nContadorResp = 0;
               |BUCLE LeeFicheros;
               aAlfa = nRespuestasNuevas;
               |QBF aAlfa;

               aAlfa = "Encontradas " + nRespuestasNuevas + " nuevas respuestas";
               aCadena = aAlfa; nEspacio = 33; |HAZ Centra;
               |PINTA 1424, aCadenaSal;

               aAlfa = " Recogiendo respuestas ";
               aCadena = aAlfa; nEspacio = 33; |HAZ Centra;
               |PINTA 1524, aCadenaSal;
          |SINO;
               swNoMensaje = 1;
          |FINSI;
     |SINO;
          |SI aParam = "A";
               |SI #cretam00#6 = 1; |O #cretam00#6 = 2;
                    swNoMensaje = 1;
               |FINSI;
          |FINSI;
     |FINSI;

     |CIERRA #cretat07;
     |DELINDEX #cretat07;
|FPRC;

|PROGRAMA;
     |CLS;
/*
     |ABRE #cretam40;
     |LEE 000#cretam40.p;
     FEstado = FSalida;
     #cretam40#1 = "01.01.2015";
     #cretam40#2 = "0000";
     |SI FEstado ! 0;
          |GRABA 020#cretam40.n;
     |SINO;
          |GRABA 020#cretam40.a;
     |FINSI;
     |CIERRA #cretam40;
*/
     |HAZ Ejecutables;

     |ABRE #cretam00;
     |LEE 000#cretam00.p;
     |SI FSalida ! 0;
          swPrimeraVez = 1;
     |FINSI;
     |CIERRA #cretam00;

     aParam = PARAMETRO;
     |QBF aParam;
     |SI aParam = "M";
                         || con menu
          |PINPA #0#0;
          |PINDA #0#0;
          |ENDATOS #1#0;
     |SINO;
          |SI aParam = "0 laboral fich NULL creta";
               aParam = "A";       || automatico, al entrar
          |FINSI;

          |SI aParam = "A"; |Y #cretam00#6 = 3;
               #0#0 = "C";
          |SINO;
               |HAZ Cabecera;
               #0#0 = "T";
          |FINSI;
     |FINSI;

     |SI #0#0 = "S"; |O #0#0 = "T";
          aAlfa = Usuario;  |QBF aAlfa; aAlfa = "c1" + aAlfa;
          |NOME_DAT #cretat05#aAlfa#;
          |DELINDEX #cretat05;
          |ABRE #cretat05;
          |ABRE #cretae15;

          |HAZ Recogida;

          || esto realmente ya no hace falta, lo tenia para que en el lineal
          || las respuestas salieran ordenadas por fecha

          nContador = 0;
          |BUCLE Renumera;

          |SI nContador ! 0;
               |SI #0#0 = "S";
                    || muestro el lineal;
                    |HAZ Muestra;
               |FINSI;
               |SI #0#0 = "T";
                    |HAZ Respuestas;
               |FINSI;

               |SI #0#0 ! "C"; |Y nOpcion ! 3;
                    aAlfa = nTotalFicheros;
                    |QBF aAlfa;
                    |HAZ Resultado;
                    || aAlfa = "    Se han recogido y registrado " + aAlfa + " ficheros de respuesta";
                    || MENAV aAlfa;
               |FINSI;
          |SINO;
               |SI swNoMensaje = 0;
                    |MENAV "    No se han encontrado nuevas respuestas pendientes de leer. ";
               |FINSI;
          |FINSI;

          |CIERRA #cretae15;
          |CIERRA #cretat05;
     |FINSI;
|FPRO;
