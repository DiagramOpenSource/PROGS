|FICHEROS;
     cretap11 #0;
     silcon01;
     labempre;
     labtraba;
     labcentr;
     nomcenes;

     tsilco14,MANTE;     || tmp parrilla empresas
     cretam00;
     cretam01;
     cretam02;
     cretam03;
     cretam04;
     cretat02;
     cretat08;

     cretae02;
|FIN;

|VARIABLES;
     nDesdeMes = 0;
     nHastaMes = 0;
     &nMes = 0;
     &aMes = "";
     &nAny = 0;
     aTmp_tsilco14 = "";
     aTmp_cretat02 = "";

     aHora = "";
     fFecha = @;
     aFecha = "";
     aFichero = "";
     &eaFichero = "";
     &aHoraEntrada = "";
     &aFechaEntrada = "";

     aAlfa = "";

     &enSwAlta = 0;
     &enMes = 0;
     &enAny = 0;

     aCentro = "";
     &eaRegimen = "";

     aCadena = "";
     nNivel = 0;
     swSinReturn = 0;
     nHandle = 0;
     aValor = "";
     aTipoFichero = "";
     aOrigen = "";
     &eaTipoXML = "";
     &eaReferencia = "";
     aCadenaSal = "";
     aLongitud = "";
     nLongitud = 0;
     nPos = 0;
     nBusca = 0;
     aBusca = "";
     aCaracter = "";
     aCambia = "";
     aRuta = "";
     aDestino = "";
     aIP = "";

     nNume1 = 0;
     nNume2 = 0;

     nEmpresas = 0;
     nCentros = 0;
     nCCCs = 0;
     nAntCentros = 0;

     aAlfa1 = "";
     aAlfa2 = "";
     aAlfa3 = "";
     aAlfa4 = "";
     alfa4 = "";
     nNume = 0;
     aLon = "";
     nLon = 0;
     nEspacio = 0;
     aEspacio = "";
     nBlancos = 0;
     nBlancosIzd = 0;
     nBlancosDer = 0;
     aBlancosIzd = "";
     aBlancosDer = "";
     aLinea = "";

     &efFecha = @;
     &eaHora = "";
     FEstado = 0;
     aParam = "";
     aCCC = "";

     &enEmp = 0;
     &enCen = 0;
     swLabcentr = 0;
     swNomcenes = 0;
     &enTipo = 0;
|FIN;

/****************************************************************************/
/********************   PROCESOS DESDE LOS CAMPOS DE PANTALLA ***************/
/****************************************************************************/

|PROCESO AyudaTipos; |TIPO 7;
     |PUSHV 08542380;
     |ATRI R;
     ||            5     6         7
     ||            45678901234567890123456789
     |PINTA 0854, "       - Opciones -       ";
     |PINTA 0954, " 00 :Normal               ";
     |PINTA 1054, " 02 :Finiquitos           ";
     |PINTA 1154, "5-20:Atrasos              ";
     |PINTA 1254, " 98 :Salarios de tramita- ";
     |PINTA 1354, "     ci¢n                 ";
     |PINTA 1454, "                          ";
     |ATRI 0;
|FPRC;

|PROCESO TipoSolicitud; |TIPO 7;
|ACABA;
     |PUSHV 07541780;
     |ATRI R;
               ||  5     6         7        8
               ||  56789012345678901234567890
     |PINTA 0855, "   TIPOS DE SOLICITUDES   ";
     |PINTA 0955, "   --------------------   ";
     |PINTA 1055, " 1. Trabajadores y tramos ";
     |PINTA 1155, " 2. Borrador              ";
     |PINTA 1255, " 3. Confirmaci¢n          ";
     |PINTA 1355, " 4. C lculos              ";
     |PINTA 1455, "                          ";
     |ATRI 0;
|FPRC;

|PROCESO Restaura;
     |POPV;
|FPRC;

|PROCESO PintaSol;
/*
     |ATRI I;
     |SI #0#5 = " ";
          aAlfa = "                                  ";
     |FINSI;
     |SI #0#5 = "1";
          aAlfa = "Solicitud de trabajadores y tramos";
     |FINSI;
     |SI #0#5 = "2";
          aAlfa = "Solicitud de borrador             ";
     |FINSI;
     |SI #0#5 = "4";
          aAlfa = "Solicitud de c lculos             ";
     |FINSI;
     |SI #0#5 = "3";
          aAlfa = "Solicitud de confirmaci¢n         ";
     |FINSI;
     |PINTA 1122, aAlfa;
     |ATRI 0;
*/
     |ATRI R;
     |SI #0#5 = " ";
          aAlfa = "                                  ";
     |FINSI;
     ||                      1         2         3
     ||             123456789012345678901234567890123456789
     |SI #0#5 = "1";
          aAlfa = " SOLICITUD DE TRABAJADORES Y TRAMOS ";
          |PINTA 0621, aAlfa;
          |CAMPO_MODIFICA #0#6, 1, "N";
          |ATRI 0;
          |PINTA 0921, "                          ";
     |FINSI;
     |SI #0#5 = "2";
          aAlfa = " SOLICITUD DE BORRADOR ";
          |PINTA 0628, aAlfa;
     |FINSI;
     |SI #0#5 = "3";
          aAlfa = " SOLICITUD DE CONFIRMACION ";
          |PINTA 0626, aAlfa;
     |FINSI;
     |SI #0#5 = "4";
          aAlfa = " SOLICITUD DE CALCULOS ";
          |PINTA 0628, aAlfa;
     |FINSI;
     |ATRI 0;
|FPRC;

|PROCESO PintaAyuda11;
||             0      1         2         3         4         5         6         7
||             3456789012345678901234567890123456789012345678901234567890123456789012345678
     |SI #0#5 = "1";
     aAlfa1 =       "l Fichero de Trabajadores y Tramos, que el usuario enviar  a trav‚s de";
     aAlfa2 = "SILTRA para obtener la informaci¢n de la que la TGSS dispone de los trabaja-";
     aAlfa3 = "dores que se incluir n en la liquidaci¢n.                                   ";
     |FINSI;
     |SI #0#5 = "2";
     aAlfa1 =       "l Borrador de la liquidaci¢n. El usuario enviar  este fichero a trav‚s";
     aAlfa2 = "de SILTRA para obtener el borrador y la liquidaci¢n, que podr  ser confirma-";
     aAlfa3 = "da posteriormente a trav‚s de la Solicitud de Confirmaci¢n si es v lida.    ";
     |FINSI;
     |SI #0#5 = "3";
     aAlfa1 =       " Confirmaci¢n de la liquidaci¢n. El usuario enviar  esta solicitud me-";
     aAlfa2 = "diante SILTRA para confirmar el pago de las liquidaciones a Tesorer¡a de los";
     aAlfa3 = "seguros sociales.                                                           ";
     |FINSI;
     |SI #0#5 = "4";
     aAlfa1 =       " C lculos de las liquidaciones.  El usuario enviar  esta solicitud me-";
     aAlfa2 = "diante SILTRA para  solicitar el detalle por  trabajador de los c lculos que";
     aAlfa3 = "se han realizado por parte de Tesorer¡a para elaborar las liquidaciones.    ";
     |FINSI;
     aAlfa4 = "ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ";

     |PINTA 2009, aAlfa1;
     |PINTA 2103, aAlfa2;
     |PINTA 2203, aAlfa3;
     |PINTA 2301, aAlfa4;
|FPRC;

|PROCESO Defectos;
     fFecha = ~;
     aAlfa = fFecha;
     aAlfa = aAlfa % 402;
     nMes = aAlfa;

     aAlfa = fFecha;
     aAlfa = aAlfa % -104;
     nAny = aAlfa;

     |SI nMes = 1;
          nAny = nAny - 1;
          nMes = 12;
     |SINO;
          nMes = nMes - 1;
     |FINSI;

     #0#0 = nAny;
     #0#1 = nMes;

     |PINTA #0#0;
     |PINTA #0#1;
|FPRC;

|PROCESO pintames;
     nMes = #0Campo;
     |HAZ MesLetra_plb;
     |ATRI I;
     |PINTA 1145, aMes;
     |ATRI 0;
|FPRC;

|PROCESO DiaHora;
     |HORA aHora;
     fFecha = ~;
     aFecha = fFecha;

     efFecha = fFecha;
     eaHora = aHora;

     aHoraEntrada = aHora - ":" - ":";
     aFechaEntrada = aFecha - "." - ".";
|FPRC;

|PROCESO Fichero;
     aAlfa = "";
     |SI #0#5 = "1";
          aAlfa = "SOLTRA";
     |FINSI;
     |SI #0#5 = "2";
          aAlfa = "SOLBOR";
     |FINSI;
     |SI #0#5 = "4";
          aAlfa = "SOLCAL";
     |FINSI;
     |SI #0#5 = "3";
          aAlfa = "SOLCON";
     |FINSI;

     |SI aAlfa = "";
          aAlfa = "    Debe elegir un tipo de solicitud para poder continuar.";
          |MENAV aAlfa;
          |ERROR;
          |ACABA;
     |FINSI;

     aAlfa1 = #0#1;
     |QBF aAlfa1;
     aAlfa1 = ("00" + aAlfa1) % -102;
     aAlfa2 = #0#0;

     aFichero = aAlfa + "_" + aAlfa2 + aAlfa1 + "_" + aFechaEntrada + "_" + aHoraEntrada + ".xml"
     eaFichero = aFichero;

     #0#3 = aFichero;
     |PINTA #0#3;
|FPRC;

/****************************************************************************/
/*************  PROCESOS PARA ELABORAR LA PARRILLA DE SELECCION  ************/
/****************************************************************************/

|PROCESO BuscaLiq;
     enSwAlta = 1;
     |ERROR10;
|FPRC;

|DEFBUCLE BuscaLiq;
     #cretam04#1;
     ;
     #cretam01#0, #0#0, #0#1, enTipo, "               ", "            ", 01;
     #cretam01#0, #0#0, #0#1, enTipo, "zzzzzzzzzzzzzzz", "zzzzzzzzzzzz", 99;
     ;
     NULL, BuscaLiq;
|FIN;

|PROCESO CompTraEmp;
     enSwAlta = 0;
     |BUCLE BuscaLiq;
|FPRC;

|PROCESO EmpresasTMP;
     nDesdeMes = #0#1;
     nHastaMes = #0#1;
     nAny = #0#0;

     #labempre#0 = #cretam01#0;
     |LEE 000#labempre.=;
     |SI FSalida = 0;
          |HAZ CompTraEmp;     || doble comprobacion, tampoco esta de mas
          |SI enSwAlta = 1;
               |DDEFECTO #tsilco14;
               #tsilco14#0 = #cretam01#0;
               #tsilco14#1 = #labempre#2;
               |LEE 101#tsilco14.=;
               |SI FSalida ! 0
                    #tsilco14#2 = " ";
                    |GRABA 020#tsilco14.n;
               |FINSI;
          |FINSI;
          |LIBERA #tsilco14;
     |FINSI;
|FPRC;

|DEFBUCLE EmpresasTMP;
     #cretam01#1;
     ;
     00001, #0#0, #0#1, enTipo;
     99999, #0#0, #0#1, enTipo;
     ;
     NULL, EmpresasTMP;
|FIN;

|PROCESO min14;
    #tsilco14#0 = 00001;
|FPRC;

|PROCESO max14;
    #tsilco14#0 = 99999;
|FPRC;

|PROCESO parrilla;
     |SI FSalida = 0; |Y #0#2 = "S";
          |ATRI P;   |MENSA "    Cargando seleccion ...";    |ATRI 0;

          |BUCLE EmpresasTMP;

          |ATRI R;
          |BLIN 4;
          |ATRI 0;

          |PUSHV 0501 2380;
          |ENTLINEAL #1#tsilco14, 1, 4, 22, 1, min14, max14;
          |POPV;
     |FINSI;
|FPRC;

/****************************************************************************/
/******        GRABACION DEL TEMPORAL DE CCCs A ENVIAR   ********************/
/****************************************************************************/
/*
|PROCESO labtraba;
     ||SI #labtraba#247 = 121; |O #labtraba#247 = 138;
     ||O #labtraba#247 = 163; |O #labtraba#247 = 613;

     |SI #labtraba#247 ! 111;
     |O #labtraba#42 = "H";
          || excluyo de momento autonomos, mar, hogar y agrarios
     |SINO;
          enMes = #0#1;
          enAny = #0#0;

          |HAZ CompAltaTrab_plb
     |FINSI;

     |SI enSwAlta = 1;
          |ERROR10;
     |FINSI;
|FPRC;

|DEFBUCLE labtraba;
     #labtraba#1;
     77;
     #labcentr#0, 00001, #labcentr#1;
     #labcentr#0, 99999, #labcentr#1;
     ;
     NULL, labtraba;
|FIN;
*/

|PROCESO cretam03;
     enSwAlta = 1;
     |ERROR10;
|FPRC;

|DEFBUCLE cretam03;
     #cretam03#1;
     7;
     #labempre#0, #0#0, #0#1, enTipo, aCCC, "            ", enCen;
     #labempre#0, #0#0, #0#1, enTipo, aCCC, "zzzzzzzzzzzz", enCen;
     ;
     NULL, cretam03;
|FIN;

|PROCESO centros;
     enSwAlta = 0;
     ||BUCLE labtraba;

     |SI swLabcentr = 1;
          enEmp = #labcentr#0;
          enCen = #labcentr#1;
          aAlfa = #labcentr#10;
     |FINSI;
     |SI swNomcenes = 1;
          enEmp = #nomcenes#0;
          enCen = #nomcenes#13;
          aAlfa = #nomcenes#10;
     |FINSI;

     |QBT aAlfa;

     |SI aAlfa = "";
          aAlfa = #labempre#13;
          |QBT aAlfa;
     |FINSI;
     aAlfa = aAlfa % -111;

     enMes = #0#1;
     enAny = #0#0;

     |HAZ RegimenCCC_plb;
     aCCC = eaRegimen + aAlfa;

     |SI eaRegimen ! "0111";
          |ACABA;
     |FINSI;

     || para comprobar si tengo trabajadores con la liquidacion calculada
     || para mandar la solicitud

     |BUCLE cretam03;

     |SI enSwAlta = 1;
          aCentro = enCen;
          |QBF aCentro;
          aCentro = ("00" + aCentro) % -102;

          #cretat02#0 = enEmp;
          #cretat02#1 = aCCC;
          |LEE 101#cretat02.=;
          |SI FSalida = 0;
               aAlfa = #cretat02#3;
               |QBF aAlfa;
               aAlfa1 = aAlfa - aCentro;
               |SI aAlfa1 = aAlfa;
                    || quiere decir que el centro no estaba en la lista
                    #cretat02#3 = aAlfa + " " + aCentro;
               |SINO;
                    || para que no lo cuente de mas despues
                    nCentros = nCentros - 1;
               |FINSI;
               |GRABA 020#cretat02.a;
          |SINO;
               #cretat02#2 = #labempre#2;
               #cretat02#3 = aCentro;
               |GRABA 020#cretat02.n;
               nCCCs = nCCCs + 1;
          |FINSI;
          nCentros = nCentros + 1;
          |LIBERA #cretat02;
     |FINSI;
|FPRC;

|DEFBUCLE nomcenes;
     #nomcenes#1;
     ;
     #labempre#0, 01, "87";
     #labempre#0, 98, "87";
     ;
     NULL, centros;
|FIN;

|DEFBUCLE labcentr;
     #labcentr#1;
     ;
     #labempre#0, 01;
     #labempre#0, 98;
     ;
     NULL, centros;
|FIN;

|PROCESO Empresa;
     |SI #0#2 = "S";
          #labempre#0 = #tsilco14#0;
     |SINO;
          #labempre#0 = #cretam01#0;
     |FINSI;
     |LEE 000#labempre.=;

     nEmpresas = nEmpresas + 1;

     swLabcentr = 1; swNomcenes = 0;
     |BUCLE labcentr;
     swLabcentr = 0; swNomcenes = 1;
     |BUCLE nomcenes;

     |SI nCentros = nAntCentros;
          || el numero de centros no ha aumentado tras mirar que centros habia
          || que meter, con lo que de esta empresa no habia ningun centro
          || a incluir en el XML

          nEmpresas = nEmpresas - 1;
     |FINSI;

     nAntCentros = nCentros;
|FPRC;

|DEFBUCLE ConSeleccion;
     #tsilco14#1;
     2;
     00001, "*";
     99999, "*";
     ;
     NULL, Empresa;
|FIN;

|DEFBUCLE SinSeleccion;
     #cretam01#1;
     ;
     00001, #0#0, #0#1, enTipo;
     99999, #0#0, #0#1, enTipo;
     ;
     NULL, Empresa;
|FIN;

/****************************************************************************/
/**************            CREACION DEL XML             *********************/
/****************************************************************************/

|PROCESO Linea;
     |QBF aAlfa;
     aCadena = aAlfa;
     |HAZ QuitaRaros;
     |SI nNivel = 1;
          aCadena = aCadena;
     |FINSI;
     |SI nNivel = 2;
          || aCadena = &09 + aCadena;
          aCadena = "  " + aCadena;
     |FINSI;
     |SI nNivel = 3;
          aCadena = "  " + "  " + aCadena;
     |FINSI;
     |SI nNivel = 4;
          aCadena = "  " + "  " + "  " + aCadena;
     |FINSI;
     |SI nNivel = 5;
          aCadena = "  " + "  " + "  " + "  " + aCadena;
     |FINSI;
     |SI nNivel = 6;
          aCadena = "  " + "  " + "  " + "  " + "  " + aCadena;
     |FINSI;
     |SI nNivel = 7;
          aCadena = "  " + "  " + "  " + "  " + "  " + "  " + aCadena;
     |FINSI;
     |SI nNivel = 8;
          aCadena = "  " + "  " + "  " + "  " + "  " + "  " + "  " + aCadena;
     |FINSI;
     |SI nNivel = 9;
          aCadena = "  " + "  " + "  " + "  " + "  " + "  " + "  " + "  " + aCadena;
     |FINSI;
     |SI nNivel = 10;
          aCadena = "  " + "  " + "  " + "  " + "  " + "  " + "  " + "  " + "  " + aCadena;
     |FINSI;
     |SI swSinReturn = 0;
          aCadena = aCadena + &13 + &10;
     |FINSI;
     aAlfa = aCadena;
     |XGRABA nHandle, aAlfa;
|FPRC;

|PROCESO RegistroCCC;
     |DDEFECTO #cretae02;

     |ABRE #cretae02;
     #cretae02#0 = #cretat02#0;
     #cretae02#1 = #0#0;
     #cretae02#2 = #0#1;
     #cretae02#3 = enTipo;
     #cretae02#4 = #cretat02#1;
     #cretae02#6 = eaReferencia;
     |LEE 101#cretae02.=;
     FEstado = FSalida;

     #cretae02#7 = eaTipoXML;
     #cretae02#9 = #cretat02#3;

     #cretae02#11 = #0#3;
     #cretae02#12 = efFecha;
     #cretae02#13 = (eaHora % 102) + ":" + (eaHora % 402);

     |SI FEstado = 0;
          |GRABA 020#cretae02.a;
     |SINO;
          |GRABA 020#cretae02.n;
     |FINSI;

     |LIBERA #cretae02;
     |CIERRA #cretae02;


     |DDEFECTO #cretam02;

     |ABRE #cretam02;
     #cretam02#0 = #cretat02#0;
     #cretam02#1 = #0#0;
     #cretam02#2 = #0#1;
     #cretam02#3 = enTipo;
     #cretam02#4 = #cretat02#1;
     |LEE 101#cretam02.=;
     |SI FSalida = 0;
          |SI #0#5 = 1;
               #cretam02#9 = "G";
               #cretam02#10 = eaReferencia;
               |GRABA 020#cretam02.a;
          |FINSI;
          |SI #0#5 = 2;
               #cretam02#13 = "G";
               #cretam02#14 = eaReferencia;
               |GRABA 020#cretam02.a;
          |FINSI;
     |FINSI;

     |LIBERA #cretam02;
     |CIERRA #cretam02;
|FPRC;

|PROCESO Liquidacion;
     nNivel = 2;

     aAlfa = "<Liquidacion>";
     |HAZ Linea;

     nNivel = 3;

     aAlfa = #cretat02#0;
     |QBF aAlfa;
     aAlfa = ("00000" + aAlfa) % -105;
     aAlfa = aAlfa + "  " + #cretat02#2;
     aAlfa = "<!-- " + aAlfa + " -->";
     |HAZ Linea;

     aAlfa = #cretat02#3;
     |QBF aAlfa;
     aAlfa = "<!-- CENTRO: " + aAlfa + " -->";
     |HAZ Linea;

     aAlfa = "<Ccc>";
     |HAZ Linea;
     |HAZ RegistroCCC;

     nNivel = 4;
     aValor = #cretat02#1 % 0104;
     aAlfa = "<Regimen>" + aValor + "</Regimen>";
     |HAZ Linea;
     aValor = #cretat02#1 % 0502;
     aAlfa = "<Provincia>" + aValor + "</Provincia>";
     |HAZ Linea;
     aValor = #cretat02#1 % 0709;
     aAlfa = "<Numero>" + aValor + "</Numero>";
     |HAZ Linea;

     nNivel = 3;
     aAlfa = "</Ccc>";
     |HAZ Linea;

     nNivel = 3;
     aAlfa = "<PeriodoDesde>";
     |HAZ Linea;

     nNivel = 4;
     aValor = #0#1;
     |QBF aValor;
     aValor = ("00" + aValor) % -102;
     aAlfa = "<Mes>" + aValor + "</Mes>";
     |HAZ Linea;

     nNivel = 4;
     aValor = #0#0;
     |QBF aValor;
     aAlfa = "<Anho>" + aValor + "</Anho>";
     |HAZ Linea;

     nNivel = 3;
     aAlfa = "</PeriodoDesde>";
     |HAZ Linea;

     nNivel = 3;
     aAlfa = "<PeriodoHasta>";
     |HAZ Linea;

     nNivel = 4;
     aValor = #0#1;
     |QBF aValor;
     aValor = ("00" + aValor) % -102;
     aAlfa = "<Mes>" + aValor + "</Mes>";
     |HAZ Linea;

     nNivel = 4;
     aValor = #0#0;
     |QBF aValor;
     aAlfa = "<Anho>" + aValor + "</Anho>";
     |HAZ Linea;

     nNivel = 3;
     aAlfa = "</PeriodoHasta>";
     |HAZ Linea;

     nNivel = 3;
     |SI enTipo = 00;
          aAlfa = "L00";
     |FINSI;
     |SI enTipo = 02;
          aAlfa = "L13";
     |FINSI;
     |SI enTipo ] 05; |Y enTipo [ 20;
          aAlfa = "L03";
     |FINSI;
     aAlfa = "<Tipo>" + aAlfa + "</Tipo>";
     |HAZ Linea;

     |SI #0#5 = "2";
          || aAlfa = "<AceptarBasesAnteriores>" + "S" + "</AceptarBasesAnteriores>";
     |FINSI;

     |SI #0#5 = "4";
          nNivel = 3;
          aAlfa = "<PeriodoPresentacion>";
          |HAZ Linea;

          nNume1 = #0#1 + 1;
          |SI nNume1 = 1;
               nNume2 = #0#0 + 1;
          |SINO;
               nNume2 = #0#0;
          |FINSI;

          nNivel = 4;
          aValor = nNume1;
          |QBF aValor;
          aValor = ("00" + aValor) % -102;
          aAlfa = "<Mes>" + aValor + "</Mes>";
          |HAZ Linea;

          nNivel = 4;
          aValor = nNume2;
          |QBF aValor;
          aAlfa = "<Anho>" + aValor + "</Anho>";
          |HAZ Linea;

          nNivel = 3;
          aAlfa = "</PeriodoPresentacion>";
          |HAZ Linea;

          nNivel = 4;
          aValor = "S";
          |QBF aValor;
          aAlfa = "<IndicadorCalculosDesglosados>" + aValor + "</IndicadorCalculosDesglosados>";
          |HAZ Linea;
     |FINSI;

     nNivel = 2;
     aAlfa = "</Liquidacion>";
     |HAZ Linea;
|FPRC;

|PROCESO Cabecera;
     |ABRE #silcon01;
     |DDEFECTO #silcon01;
     |LEE 000#silcon01.p;
     |CIERRA #silcon01;

     |SI #0#5 = "1";
          aTipoFichero = "SolicitudTrabajadoresTramos";
     |FINSI;
     |SI #0#5 = "2";
          aTipoFichero = "SolicitudBorrador";
     |FINSI;
     |SI #0#5 = "4";
          aTipoFichero = "SolicitudCalculos";
     |FINSI;
     |SI #0#5 = "3";
          aTipoFichero = "SolicitudConfirmacion";
     |FINSI;

     |XABRE "A", aOrigen, nHandle;
     |SI FSalida ] 0;
          nNivel = 1;
          aAlfa = "<?xml version=" + &34 + "1.0" + &34 + " encoding=" + &34 + "ISO-8859-1" + &34 + " ?>";
          |HAZ Linea;
          swSinReturn = 1;
          aAlfa = "<" + aTipoFichero + " xmlns:xsi=" + &34 + "http://www.w3.org/2001/XMLSchema-instance" +  &34;
          |HAZ Linea;
          aAlfa = " xmlns=" + &34 + "http://www.seg-social.es/creta/esquemas/V100/" + aTipoFichero + &34;
          |HAZ Linea;
          aAlfa = " xsi:schemaLocation=" + &34 + "http://www.seg-social.es/creta/esquemas/V100/" + aTipoFichero + " " + aTipoFichero + ".xsd" + &34 + ">";
          swSinReturn = 0;
          |HAZ Linea;

          aValor = #silcon01#0;
          |QBF aValor;
          aValor = ("00000000" + aValor) % -108;
          nNivel = 2;
          aAlfa = "<Autorizado>" + aValor + "</Autorizado>";
          |HAZ Linea;

          |SI #0#5 = "1";
               || CLAVE T - solicitud de trabajadores y tramos
               eaTipoXML = "T";
          |FINSI;
          |SI #0#5 = "2";
               || CLAVE R - solicitud de borrador
               eaTipoXML = "R";
          |FINSI;
          |SI #0#5 = "3";
               || CLAVE F - solicitud de confirmacion
               eaTipoXML = "F";
          |FINSI;
          |SI #0#5 = "4";
               || CLAVE C - solicitud de calculos
               eaTipoXML = "C";
          |FINSI;

          enMes = #0#1;
          enAny = #0#0;
          |HAZ RefCreta_plb;

          aValor = eaReferencia;
          aAlfa = "<ReferenciaExterna>" + aValor + "</ReferenciaExterna>";
          |HAZ Linea;
     |FINSI;
|FPRC;

|PROCESO Pie;
     nNivel = 1;
     aAlfa = "</" + aTipoFichero + ">";
     |HAZ Linea;

     |XCIERRA nHandle;
|FPRC;

|DEFBUCLE Liquidacion;
     #cretat02#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, Liquidacion;
|FIN;

|PROCESO sustituye;
     || aCadena es la cadena a buscar un caracter
     || aBusca es el caracter a buscar
     || aCambia es por el que lo queremos cambiar

     aCadenaSal = "";
     aLongitud = aCadena % 0;
     nLongitud = aLongitud;
     |PARA nPos = 1; |SI nPos [ nLongitud; |HACIENDO nPos = nPos + 1;
          nBusca = (nPos * 100) + 1;
          aCaracter = aCadena % nBusca;
          |SI aCaracter = aBusca;
               aCadenaSal = aCadenaSal + aCambia;
          |SINO;
               aCadenaSal = aCadenaSal + aCaracter;
          |FINSI;
     |SIGUE;
|FPRC;

|PROCESO Ruta;
     aCadena = #cretam00#2;
     aBusca = "/";
     aCambia = "\";
     |HAZ sustituye;
     aRuta = aCadenaSal;

     |QBF aRuta;
     aAlfa = aRuta % -101;
     |SI aAlfa ! "\";
          aRuta = aRuta + "\";
     |FINSI;
     aRuta = aRuta + "SOLICITUDES\";

     |SI #0#5 = "1";
          aRuta = aRuta + "TRABAJADORES\";
     |FINSI;
     |SI #0#5 = "2";
          aRuta = aRuta + "BORRADOR\";
     |FINSI;
     |SI #0#5 = "4";
          aRuta = aRuta + "CALCULOS\";
     |FINSI;
     |SI #0#5 = "3";
          aRuta = aRuta + "CONFIRMACION\";
     |FINSI;

     |RMKDIR aRuta;

     aFichero = #0#3;
     |QBF aFichero;
     aAlfa = aFichero % -104;
     |SI aAlfa ! ".xml";
          aFichero = aFichero + ".xml";
     |FINSI;

     aRuta = aRuta + aFichero;
|FPRC;

|PROCESO Alinea;
     aAlfa = nNume; |QBF aAlfa;
     |SI nNume < 10;
          aAlfa = "  " + aAlfa;
     |SINO;
          |SI nNume < 100;
               aAlfa = " " + aAlfa;
          |FINSI;
     |FINSI;
|FPRC;

|PROCESO Centra;
     aLon = aCadena % 0;
     nLon = aLon;
     nBlancos = nEspacio - nLon;
     nNume = nBlancos / 2;
     nBlancosIzd = nNume ! 0;
     nBlancosDer = nBlancos - nBlancosIzd;
     aBlancosIzd = " " * nBlancosIzd;
     aBlancosDer = nBlancosDer * " ";
     aCadenaSal = aBlancosIzd + aCadena + aBlancosDer;
|FPRC;

|PROCESO Mensaje;
     |BLANCO 08101770;
     |CUADRO 08101770;
               ||  1       2         3         4         5         6
               ||  234567890123456789012345678901234567890123456789012345678
     |SI #0#5 = 1;
          aAlfa = "trabajadores y tramos";
     |FINSI;
     |SI #0#5 = 2;
          aAlfa = "borrador";
     |FINSI;
     |SI #0#5 = 3;
          aAlfa = "confirmaci¢n";
     |FINSI;
     |SI #0#5 = 4;
          aAlfa = "c lculos";
     |FINSI;
     aLinea = "El fichero de solicitud de " + aAlfa + " se ha";
     aCadena = aLinea; nEspacio = 57; |HAZ Centra;
     |PINTA 0912, aCadenaSal;
     aLinea = "grabado en la carpeta: ";
     aCadena = aLinea; nEspacio = 57; |HAZ Centra;
     |PINTA 1012, aCadenaSal;
     aAlfa = aDestino - aFichero;
     aCadena = aAlfa; nEspacio = 57; |HAZ Centra;
     |ATRI S;
     |PINTA 1112, aCadenaSal;
     |ATRI 0;
     |SI aParam = "cretat08";
          |PINTA 1212, "      para los CCCs de formaci¢n cuyos trabajadores      ";
          |PINTA 1312, "       no tienen horas de formaci¢n subvencionadas       ";
     |SINO;
          |PINTA 1212, "       Se realiza la solicitud para un total de:         ";
          nNume = nEmpresas; |HAZ Alinea
          aLinea = "                    " + aAlfa + " Empresas                         ";
          |PINTA 1312, aLinea;
          nNume = nCentros; |HAZ Alinea
          aLinea = "                    " + aAlfa + " Centros                          ";
          |PINTA 1412, aLinea;
          nNume = nCCCs; |HAZ Alinea
          aLinea = "                    " + aAlfa + " CCCs                             ";
          |PINTA 1512, aLinea;

          |PINTA 1612, "                                                         ";
     |FINSI;

     |PAUSA;
|FPRC;

|PROCESO CreacionXML;
     |HAZ Ruta;

     |DFICE aOrigen;
     aOrigen = aOrigen + aFichero;
     aDestino = aRuta;
     |QBF aDestino;
     aDestino = aRuta;

     |HAZ Cabecera;

     |BUCLE Liquidacion;

     |HAZ Pie;

     |IP_REMOTO aIP;
     |SI aIP ! "";
          |ENVIA_FICHERO aOrigen, aDestino;
     |SINO;
          |COPIA_FICHERO aOrigen, aDestino;
     |FINSI;

     |SI FSalida = 0;
          |HAZ Mensaje;
          aAlfa = "    A continuación, puede enviar el fichero creado a la TGSS a través ";
          aAlfa = aAlfa + "de la aplicación SILTRA.";
          |MENAV aAlfa;
     |FINSI;

     |FBORRA aOrigen;
|FPRC;

|PROCESO ElProceso;
     |SI #0#2 = "S";
          |BUCLE ConSeleccion;
     |SINO;
          |BUCLE SinSeleccion;
     |FINSI;

     ||CONSULTA_CLAVE #1#cretat02;
|FPRC;

|PROCESO cretat08;
     nCCCs = nCCCs + 1;
     #cretat02#0 = #cretat08#0;
     #cretat02#1 = #cretat08#1;
     #cretat02#2 = #cretat08#2;
     #cretat02#3 = #cretat08#3;
     |GRABA 020#cretat02.n;
     |LIBERA #cretat02;
|FPRC;

|DEFBUCLE cretat08;
     #cretat08#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, cretat08;
|FIN;

|PROCESO TraspasoCCCsEnvio;
     alfa4 = Usuario; |QBT alfa4;    alfa4 = ("c3" + alfa4) % 108;
     |NOME_DAT #cretat08#alfa4#;

     |BUCLE cretat08;

     |CIERRA #cretat08;
     |DELINDEX #cretat08;
|FPRC;

|PROGRAMA;
     |CLS;

     aParam = PARAMETRO;
     |QBF aParam;

     |SI aParam = "cretat08";
          #0#5 = 2;

          #0#0 = enAny;
          #0#1 = enMes;

          |HAZ DiaHora;
          |HAZ Fichero;
     |SINO;
          #0#5 = aParam;

          |PINPA #0#0;
          |PINDA #0#0;

          |HAZ Defectos;
          Campo = 1;
          |HAZ pintames;

          |HAZ PintaSol;
          |HAZ DiaHora;
          |HAZ Fichero;
          |HAZ PintaAyuda11;
     |FINSI;

     aTmp_tsilco14 = Usuario; |QBT aTmp_tsilco14;    aTmp_tsilco14 = ("t" + aTmp_tsilco14) % 108;
     |NOME_DAT #tsilco14#aTmp_tsilco14#;
     |DELINDEX #tsilco14;

     aTmp_cretat02 = Usuario; |QBT aTmp_cretat02;    aTmp_cretat02 = ("c2" + aTmp_cretat02) % 108;
     |NOME_DAT #cretat02#aTmp_cretat02#;
     |DELINDEX #cretat02;

     |ABRE #cretat02;
     |ABRE #labempre;
     |ABRE #labtraba;
     |ABRE #tsilco14;

     |DDEFECTO #cretam00;
     |ABRE #cretam00; |LEE 000#cretam00.p; |CIERRA #cretam00;

     |SI aParam = "cretat08";
          #0#2 = "S";
          |HAZ TraspasoCCCsEnvio;
          FSalida = 0;
     |SINO;
          |ENDATOS #1#0;
     |FINSI;

     |SI FSalida = 0;
          enTipo = #0#6;

          |HAZ ElProceso;

          |SI nCCCs ! 0;
               |HAZ CreacionXML;
          |SINO;
               aAlfa = "    No se han encontrado datos para realizar la solicitud";
               aAlfa = aAlfa + " con la selección realizada."
               |MENAV aAlfa;
          |FINSI;
     |FINSI;

     |DELINDEX #tsilco14;
     |DELINDEX #cretat02;

     |CIERRA #cretat02;
     |CIERRA #tsilco14;
     |CIERRA #labtraba;
     |CIERRA #labempre;
|FPRO;
