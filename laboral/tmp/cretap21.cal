|FICHEROS;
     cretap21 #0;
     cretam31;
     cretam33;

     cretam15;
     cretam02;

     labempre;
     labcentr;
     nomcenes;
     labtraba;
|FIN;

|VARIABLES;
     &eaFichero = "";
     &aSoloFichero = "";

     nNume = 0;
     aLon = "";
     nLon = 0;
     nEspacio = 0;
     aEspacio = "";
     nBlancos = 0;
     nBlancosIzd = 0;
     nBlancosDer = 0;
     aBlancosIzd = "";
     aBlancosDer = "";

     aCadena = "";
     aCadenaSal = "";

     FLinea = 0;
     nNroLinea = 0;
     aNroLinea = "";
     aAlfa = "";

     nHandle = 0;
     aLinea = "";
     &aTotalLineas = "";
     swAcaba = 0;

     aCCC = "";
     nAnyDesde = 0;
     nMesDesde = 0;
     aAnyDesde = "";
     aMesDesde = "";
     nMesHasta = 0;
     nAnyHasta = 0;
     nTipo = 0;
     aDia = "";
     aMes = "";
     aAny = "";
     aEtiqueta = "";
     nLoc = 0;
     aValor = "";
     aCampoFecha = "";
     aDiaRec = "";
     aMesRec = "";
     aAnyRec = "";
     aHoraRec = "";
     aEtiquetaIni = "";
     aEtiquetaFin = "";
     aMesHasta = "";
     aAnyHasta = "";
     aTipo = "";

     nEmp = 0;
     nTra = 0;
     swEncontrada = 0;
     aAlfa1 = "";
     aAlfa2 = "";
     &enMes = 0;
     &enAny = 0;
     &enSwAlta = 0;

     aNaf = "";
     aEstado = "";
     fFechaRec = @;

     aTipoIPF = "";
     aNumeroIPF = "";
     aSiglas = "";

     aDiaDesdeTramo = "";
     aMesDesdeTramo = "";
     aAnyDesdeTramo = "";
     aFechaDesdeTramo = "";
     aDiaHastaTramo = "";
     aMesHastaTramo = "";
     aAnyHastaTramo = "";
     aFechaHastaTramo = "";

     aDias = "";
     aHoras = "";
     aDescripcion = "";
     nConcepto = 0;
     nImporte = 0;
     aCentro = "";

     nCampo = 0;
     nCampo1 = 0;
     nCampo2 = 0;
     nCampo3 = 0;
     aTipoSuma = "";

     &eaCadena = "";
     &eaCadenaSal = "";
     aLongitud = "";
     nLongitud = "";
     &eaCaracter1 = "";
     &eaCaracter2 = "";
     nNume1 = 0;
     nNume2 = 0;

     nMesXX = 0;
     swAtrasos = 0;
|FIN;

|PROCESO Centra;
     aLon = aCadena % 0;
     nLon = aLon;
     nBlancos = nEspacio - nLon;
     nNume = nBlancos / 2;
     nBlancosIzd = nNume ! 0;
     nBlancosDer = nBlancos - nBlancosIzd;
     aBlancosIzd = " " * nBlancosIzd;
     aBlancosDer = " " * nBlancosDer;
     aCadenaSal = aBlancosIzd + aCadena + aBlancosDer;

|FPRC;

|PROCESO sustituye_plb;
     || eaCadena: contiene la cadena donde vamos a buscar
     || eaCaracter1: contiene el caracter que queremos sustituir
     || eaCaracter2: contiene el caracter por el que lo vamos a sustituir
     || eaCadenaSal: contiene el resultado

     eaCadenaSal = "";
     aLongitud = eaCadena % 0;
     nLongitud = aLongitud;

     |QBF eaCadena;
     FSalida = 1;
     |PARA; |SI FSalida ! 0; |HACIENDO;
          aAlfa = eaCadena - eaCaracter1;
          |SI FSalida ! 0;
               aAlfa = FSalida;
               |QBF aAlfa;
               aAlfa = ("0000" + aAlfa) % -104;
               aAlfa1 = aAlfa % 102;
               aAlfa2 = aAlfa % 302;
               nNume1 = aAlfa1;
               nNume2 = aAlfa2;
               nNume2 = nNume2 + nNume1;

               nNume1 = nNume1 - 1;
               nNume1 = 100 + nNume1;

               nNume2 = (nNume2 * 100) + (nLongitud - nNume2 + 1);

               aAlfa1 = (eaCadena % nNume1);
               aAlfa2 = (eaCadena % nNume2);
               aAlfa1 = aAlfa1 % -101;
               aAlfa2 = aAlfa2 % 101;
               |SI aAlfa1 ! " "; |Y aAlfa2 ! " ";
                    || para que siempre haya una separacion y no se coma todos
                    || los espacios entre palabras

                    eaCadenaSal = (eaCadena % nNume1) + " " + (eaCadena % nNume2);
               |SINO;
                    eaCadenaSal = (eaCadena % nNume1) + eaCaracter2 + (eaCadena % nNume2);
               |FINSI;

               eaCadena = eaCadenaSal;
          |SINO;
               |SAL;
          |FINSI;
     |SIGUE;
/*

     |PARA nPos = 1; |SI nPos [ nLongitud; |HACIENDO nPos = nPos + 1;
          nBusca = (nPos * 100) + 1;
          aCaracter = aCadena % nBusca;
          |SI aCaracter = "/";
               aCaracterSal = "\";
          |SINO;
               aCaracterSal = aCaracter;
          |FINSI;
          aCadenaSal = aCadenaSal + aCaracterSal;
     |SIGUE;
*/
|FPRC;

/****************************************************************************/
/***************************   BORRADO PREVIO *******************************/
/****************************************************************************/

|PROCESO Borra_cretam33;
     |BORRA 020#cretam33.a;
     |LIBERA #cretam33;
|FPRC;

|DEFBUCLE Borra_cretam33;
     #cretam33#2;
     ;
     aCCC, nAnyDesde, nMesDesde, nTipo;
     aCCC, nAnyDesde, nMesHasta, nTipo;
     ;
     NULL, Borra_cretam33;
|FIN;

|PROCESO Borra_cretam31;
     |BORRA 020#cretam31.a;
     |LIBERA #cretam31;
|FPRC;

|DEFBUCLE Borra_cretam31;
     #cretam31#2;
     ;
     aCCC, nAnyDesde, nMesDesde, nTipo;
     aCCC, nAnyDesde, nMesHasta, nTipo;
     ;
     NULL, Borra_cretam31;
|FIN;

/****************************************************************************/
/*********** PROCESOS PARA BUSCAR CODIGOS DE EMP, TRA Y CEN *****************/
/****************************************************************************/

|PROCESO BuscaCCC;
     nEmp = #cretam02#0;
     swEncontrada = 1;
|FPRC;

|DEFBUCLE BuscaCCC;
     #cretam02#1;
     ;
     00001, nAnyDesde, nMesDesde, nTipo, aCCC;
     99999, nAnyDesde, nMesDesde, nTipo, aCCC;
     ;
     NULL, BuscaCCC;
|FIN;

|PROCESO BuscaPorCCC;
     swEncontrada = 0;
     |BUCLE BuscaCCC;
|FPRC;

|PROCESO BuscaPorNaf;
     #labempre#0 = #labtraba#0;
     |LEE 000#labempre.=;

     #labcentr#0 = #labtraba#0;
     #labcentr#1 = #labtraba#77;
     |LEE 000#labcentr.=;
     |SI FSalida = 0;
          aAlfa1 = #labcentr#10;
          aCentro = #labcentr#2;
          |QBF aAlfa1;
          |SI aAlfa1 = "";
               aAlfa1 = #labempre#13;
          |FINSI;
     |FINSI;

     |SI #labtraba#45 = "421"; |O #labtraba#45 = "422";
          #nomcenes#0 = #labtraba#0;
          #nomcenes#13 = #labtraba#77;
          #nomcenes#1 = "87";
          |LEE 000#nomcenes.=;
          |SI FSalida = 0;
               aAlfa1 = #nomcenes#10;
               aCentro = #nomcenes#2;
          |FINSI;
     |FINSI;

     aAlfa2 = aCCC % 511;
     |SI aAlfa1 = aAlfa2;
          || el CCC al que pertenece el trabajador es el mismo que me viene
          || en el XML, ahora voy a ver si esta de alta en este periodo

          enMes = nMesDesde;
          enAny = nAnyDesde;
          |HAZ CompAltaTrab_plb;
          |SI enSwAlta = 1;
               nEmp = #labtraba#0;
               nTra = #labtraba#1;
               |ERROR;
          |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE BuscaPorNaf;
     #labtraba#1;
     75;
     00001, 00001, aNaf;
     99999, 99999, aNaf;
     ;
     NULL, BuscaPorNaf;
|FIN;

/****************************************************************************/
/********************   TIPOS DE DATOS COMUNES  *****************************/
/****************************************************************************/

|PROCESO TipoFecha;
     aDia = "";
     aMes = "";
     aAny = "";

     |PARA; |SI; |HACIENDO;
          |HAZ LeeLinea;

          aEtiqueta = "Dia";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               aDia = aValor;
          |FINSI;

          aEtiqueta = "Mes";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               aMes = aValor;
          |FINSI;

          aEtiqueta = "Anho";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               aAny = aValor;
          |FINSI;

          aEtiqueta = aCampoFecha;
          |HAZ BuscaEtiFin;
          |SI nLoc = 1;
               |SAL;
          |FINSI;
     |SIGUE;
|FPRC;

|PROCESO FechaHoraRec;
     |PARA; |SI; |HACIENDO;
          |HAZ LeeLinea;

          aEtiqueta = "FechaRecaudacion";
          |HAZ BuscaEtiIni;
          |SI nLoc = 1;
               aCampoFecha = "FechaRecaudacion";
               |HAZ TipoFecha;
               aDiaRec = aDia;
               aMesRec = aMes;
               aAnyRec = aAny;
               aAlfa = aDiaRec + "." + aMesRec + "." + aAnyRec;
               fFechaRec = aAlfa;
          |FINSI;

          aEtiqueta = "HoraRecaudacion";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               aHoraRec = (aValor % 102) + ":" + (aValor % 302) + ":" + (aValor % 502);
          |FINSI;

          aEtiqueta = "FechaHoraRecaudacion";
          |HAZ BuscaEtiFin;
          |SI nLoc = 1;
               |SAL;
          |FINSI;
     |SIGUE;
|FPRC;

|PROCESO CCC;
     |PARA; |SI; |HACIENDO;
          |HAZ LeeLinea;

          aEtiqueta = "Regimen";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               aCCC = aValor;
          |FINSI;

          aEtiqueta = "Provincia";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               aCCC = aCCC + aValor;
          |FINSI;

          aEtiqueta = "Numero";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               aCCC = aCCC + aValor;
          |FINSI;

          aEtiqueta = "Ccc";
          |HAZ BuscaEtiFin;
          |SI nLoc = 1;
               |SAL;
          |FINSI;
     |SIGUE;
|FPRC;

/****************************************************************************/
/***********     PROCESOS PARA BUSCAR LAS ETIQUETAS       *******************/
/****************************************************************************/

|PROCESO BuscaEtiFin;
     nLoc = 0;
     aEtiqueta = "</" + aEtiqueta + ">";
     aAlfa = aLinea - aEtiqueta;
     |SI aAlfa ! aLinea;
          nLoc = 1;
     |FINSI;
|FPRC;

|PROCESO BuscaEtiIni;
     nLoc = 0;
     aEtiqueta = "<" + aEtiqueta;
     aAlfa = aLinea - aEtiqueta;
     |SI aAlfa ! aLinea;
          nLoc = 1;
     |FINSI;
|FPRC;

|PROCESO BuscaEtiIniFin;
     aValor = "";
     nLoc = 0;

     aEtiquetaIni = "<" + aEtiqueta + ">";
     aEtiquetaFin = "</" + aEtiqueta + ">";
     aAlfa = aLinea - aEtiquetaIni - aEtiquetaFin;
     |SI aAlfa ! aLinea;
          aValor = aAlfa;
          |SI aEtiqueta = "RazonSocial"; |O aEtiqueta = "EntidadAtEp";
          |O aEtiqueta = "DescripcionLRNT"; |O aEtiqueta = "DescripcionSumaIRNT";
               |QBF aValor;
          |SINO;
               |QBT aValor;
          |FINSI;
          nLoc = 1;
     |FINSI;
|FPRC;

|PROCESO LeeLinea;
     |XLEE nHandle, 250, aLinea;
     FLinea = FSalida;
     nNroLinea = nNroLinea + 1;
     aNroLinea = nNroLinea;
     aAlfa = " Linea " + aNroLinea + " de " + aTotalLineas;

     |ATRI R;
     aCadena = aAlfa; nEspacio = 35; |HAZ Centra;
     |PINTA 2223, aCadenaSal;
     |ATRI 0;
|FPRC;

|PROCESO DatosPiePaginaRNT;

     FLinea = 0;
     swAcaba = 0;

     || vuelvo a leer cabecera

     #cretam31#0 = nEmp;
     #cretam31#1 = nAnyDesde;
     #cretam31#2 = nMesDesde;
     #cretam31#3 = nTipo;
     #cretam31#4 = aCCC;
     |LEE 000#cretam31.=;

     aTipoSuma = "";
     aDescripcion = "";
     nImporte = 0;
     nConcepto = 0;

     |PARA;
     |SI FLinea ] 0; |Y swAcaba = 0;
     |HACIENDO;
          |HAZ LeeLinea;

          aEtiqueta = "FechaAutorizacionPieRNT";
          |HAZ BuscaEtiIni;
          |SI nLoc = 1;
               aCampoFecha = "FechaAutorizacionPieRNT";
               |HAZ TipoFecha;
               aValor = aDia + "." + aMes + "." + aAny;
               #cretam31#29 = aValor;
          |FINSI;

          aEtiqueta = "TitularAutorizacionPieRNT";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               #cretam31#30 = aValor;
          |FINSI;

          aEtiqueta = "ReferenciaCI";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               #cretam31#31 = aValor;
          |FINSI;

          aEtiqueta = "FechaCI";
          |HAZ BuscaEtiIni;
          |SI nLoc = 1;
               aCampoFecha = "FechaCI";
               |HAZ TipoFecha;
               aValor = aDia + "." + aMes + "." + aAny;
               #cretam31#32 = aValor;
          |FINSI;

          aEtiqueta = "HoraCI";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               #cretam31#33 = (aValor % 102) + ":" + (aValor % 302) + ":" + (aValor % 502);
          |FINSI;

          aEtiqueta = "HuellaCI";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               #cretam31#34 = aValor;
          |FINSI;

          aEtiqueta = "DatosPieRNT";
          |HAZ BuscaEtiFin;
          |SI nLoc = 1;
               |GRABA 020#cretam31.a;
               |LIBERA #cretam31;
               |SAL;
          |FINSI;
     |SIGUE;
|FPRC;

|PROCESO Totales;
     |PARA nCampo = 40; |SI nCampo [ 76; |HACIENDO nCampo = nCampo + 4;
          nCampo1 = nCampo + 1;
          nCampo2 = nCampo + 2;
          nCampo3 = nCampo + 3;

          |SI #cretam31#nCampo# = 0;
               #cretam31#nCampo# = nConcepto;
               #cretam31#nCampo1# = aDescripcion;
               #cretam31#nCampo2# = nImporte;
               #cretam31#nCampo3# = aTipoSuma;

               |SAL;
          |FINSI;
     |SIGUE;
|FPRC;

|PROCESO LineaPieRNT;
     FLinea = 0;
     swAcaba = 0;

     || vuelvo a leer cabecera
     |DDEFECTO #cretam31;
     #cretam31#0 = nEmp;
     #cretam31#1 = nAnyDesde;
     #cretam31#2 = nMesDesde;
     #cretam31#3 = nTipo;
     #cretam31#4 = aCCC;
     |LEE 101#cretam31.=;

     aTipoSuma = "";
     aDescripcion = "";
     nImporte = 0;
     nConcepto = 0;

     |PARA;
     |SI FLinea ] 0; |Y swAcaba = 0;
     |HACIENDO;
          FLinea = 0;
          |HAZ LeeLinea;

          aEtiqueta = "TipoLineaPieIRNT";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               aTipoSuma = aValor;
          |FINSI;

          aEtiqueta = "ImporteSumaIRNT";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               nImporte = aValor;
               nImporte = nImporte / 100;
          |FINSI;

          aEtiqueta = "DescripcionSumaIRNT";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               aDescripcion = aValor;
               aAlfa = " " * 50;
               aDescripcion = aDescripcion + aAlfa;
               aDescripcion = aDescripcion % 150;
               |SELECT #2#cretam15;
               #cretam15#1 = aDescripcion;
               |LEE 000#cretam15.=;
               |SI FSalida = 0;
                    nConcepto = #cretam15#0;
               |FINSI;
          |FINSI;

          aEtiqueta = "LineaPieIRNT";
          |HAZ BuscaEtiFin;
          |SI nLoc = 1;
               |HAZ Totales;

               |GRABA 020#cretam31.a;
               |LIBERA #cretam31;
               |SAL;
          |FINSI;
     |SIGUE;
|FPRC;

|PROCESO LineaRNT;
     FLinea = 0;
     swAcaba = 0;

     |PARA;
     |SI FLinea ] 0; |Y swAcaba = 0;
     |HACIENDO;
          FLinea = 0;
          |HAZ LeeLinea;

          aEtiqueta = "NafLRNT";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               aNaf = aValor;

               nTra = 0;
               aCentro = "";
               aNaf = (aNaf + "                  ") % 115;
               |BUCLE BuscaPorNaf;

               |QBF aNaf

               #labtraba#0 = nEmp;
               #labtraba#1 = nTra;
               |LEE 000#labtraba.=;

               || ya tengo nEmp y nTra
               || regrabo los centros
               #cretam31#0 = nEmp;
               #cretam31#1 = nAnyDesde;
               #cretam31#2 = nMesDesde;
               #cretam31#3 = nTipo;
               #cretam31#4 = aCCC;
               |LEE 000#cretam31.=;
               |SI FSalida = 0;
                    aAlfa2 = #labtraba#77;
                    |QBF aAlfa2;
                    aAlfa2 = ("00"+ aAlfa2) % -102;

                    aAlfa1 = #cretam31#9; |QBF aAlfa1;
                    |SI aAlfa1 = "";
                         #cretam31#7 = aAlfa2;
                         #cretam31#8 = aCentro;
                         #cretam31#9 = aAlfa2;
                         |GRABA 020#cretam31.a;
                         |LIBERA #cretam31;
                    |SINO;
                         aAlfa2 = aAlfa1;
                         aAlfa2 = aAlfa1 - aAlfa2;
                         |SI aAlfa2 = aAlfa1;
                              || centro nuevo, hay mas de un centro en este CCC !!
                              #cretam31#7 = "00";
                              #cretam31#8 = "Varios";
                              #cretam31#9 = aAlfa1 + " " + aAlfa2;
                              |GRABA 020#cretam31.a;
                              |LIBERA #cretam31;
                         |FINSI;
                    |FINSI;
               |FINSI;
          |FINSI;

          aEtiqueta = "TipoIpf";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               aTipoIPF = aValor;
          |FINSI;

          aEtiqueta = "NumeroIpf";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               aNumeroIPF = aValor % -109;
          |FINSI;

          aEtiqueta = "CafLRNT";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               aSiglas = aValor;
          |FINSI;

          aEtiqueta = "FechaDesdeLRNT";
          |HAZ BuscaEtiIni;
          |SI nLoc = 1;
              aCampoFecha = "FechaDesdeLRNT";
              |HAZ TipoFecha;
              aDiaDesdeTramo = aDia;
              aMesDesdeTramo = aMes;
              aAnyDesdeTramo = aAny;
              aFechaDesdeTramo = aDiaDesdeTramo + "." + aMesDesdeTramo + "." + aAnyDesdeTramo;
          |FINSI;

          aEtiqueta = "FechaHastaLRNT";
          |HAZ BuscaEtiIni;
          |SI nLoc = 1;
              aCampoFecha = "FechaHastaLRNT";
              |HAZ TipoFecha;
              aDiaHastaTramo = aDia;
              aMesHastaTramo = aMes;
              aAnyHastaTramo = aAny;
              aFechaHastaTramo = aDiaHastaTramo + "." + aMesHastaTramo + "." + aAnyHastaTramo;
          |FINSI;

          aEtiqueta = "DiasCotizadosLRNT";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               aDias = aValor;
          |FINSI;

          aEtiqueta = "HorasCotizadasLRNT";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               aHoras = aValor;
          |FINSI;

          aEtiqueta = "DescripcionLRNT";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               aDescripcion = aValor;
               aAlfa = " " * 50;
               aDescripcion = aDescripcion + aAlfa;
               aDescripcion = aDescripcion % 150;
               |SELECT #2#cretam15;
               #cretam15#1 = aDescripcion;
               |LEE 000#cretam15.=;
               |SI FSalida = 0;
                    nConcepto = #cretam15#0;
               |FINSI;
          |FINSI;

          aEtiqueta = "ImporteLRNT";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               nImporte = aValor;
               nImporte = aValor / 100;
          |FINSI;

          aEtiqueta = "LineaRNT";
          |HAZ BuscaEtiFin;
          |SI nLoc = 1;
               || ya tengo los datos para guardar la linea (el cretam33)

               #cretam33#0 = nEmp;
               #cretam33#1 = nAnyDesde;
               #cretam33#2 = nMesDesde;
               #cretam33#3 = nTipo;
               #cretam33#4 = aCCC;
               #cretam33#5 = aNaf;
               #cretam33#6 = aDiaDesdeTramo;
               #cretam33#16 = nConcepto;
               |LEE 000#cretam33.=;
               |SI FSalida ! 0;
                    || tiene que entrar siempre, no puede existir
                    #cretam33#7 = #labtraba#1;
                    #cretam33#8 = #labtraba#2;
                    #cretam33#9 = aTipoIPF;
                    #cretam33#10 = aNumeroIPF;
                    #cretam33#11 = aSiglas;
                    #cretam33#12 = aFechaDesdeTramo;
                    #cretam33#13 = aFechaHastaTramo;

                    #cretam33#14 = aDias;
                    #cretam33#15 = aHoras;
                    #cretam33#16 = nConcepto;
                    #cretam33#17 = aDescripcion;
                    #cretam33#18 = nImporte;
                    #cretam33#19 = aSoloFichero;

                    |GRABA 020#cretam33.n;
                    |LIBERA #cretam33;
               |FINSI;

               |SAL;
          |FINSI;
     |SIGUE;
|FPRC;

|PROCESO DatosCabeceraRNT;
     || primero grabo ya mi cabecera (el cretam31)

     |HAZ BuscaPorCCC;

     #labempre#0 = nEmp;
     |LEE 000#labempre.=;

     |DDEFECTO #cretam31;
     #cretam31#0 = nEmp;
     #cretam31#1 = nAnyDesde;
     #cretam31#2 = nMesXX;
     #cretam31#3 = nTipo;
     #cretam31#4 = aCCC;
     |LEE 000#cretam31.=;
     |SI FSalida ! 0;
          || tiene que entrar siempre
          #cretam31#6 = #labempre#2;
          #cretam31#11 = aTipo;
          #cretam31#12 = aEstado;
          #cretam31#13 = fFechaRec;
          #cretam31#14 = aHoraRec;

          #cretam31#17 = aMesDesde;
          #cretam31#18 = aAnyDesde;
          #cretam31#19 = aMesHasta;
          #cretam31#20 = aAnyHasta;
          #cretam31#35 = aSoloFichero;
          |GRABA 020#cretam31.n;

          || la leo otra vez para dejarla leida y despues grabarla de nuevo

          #cretam31#0 = nEmp;
          #cretam31#1 = nAnyDesde;
          #cretam31#2 = nMesXX;
          #cretam31#3 = nTipo;
          #cretam31#4 = aCCC;
          |LEE 000#cretam31.=;
     |FINSI;

     FLinea = 0;
     swAcaba = 0;

     |PARA;
     |SI FLinea ] 0; |Y swAcaba = 0;
     |HACIENDO;
          FLinea = 0;
          |HAZ LeeLinea;

          aEtiqueta = "Autorizado";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               #cretam31#15 = aValor;
          |FINSI;

          aEtiqueta = "Envio";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               #cretam31#16 = aValor;
          |FINSI;

          aEtiqueta = "DescripcionTipoLiquidacion";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               #cretam31#21 = aValor;
          |FINSI;

          aEtiqueta = "RazonSocial";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               eaCadena = aValor;
               eaCaracter1 = "  ";
               eaCaracter2 = "";
               |HAZ sustituye_plb;
               #cretam31#22 = eaCadena;
          |FINSI;

          aEtiqueta = "TipoIpf";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               #cretam31#23 = aValor;
          |FINSI;

          aEtiqueta = "NumeroIpf";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               #cretam31#24 = (aValor % -109);
          |FINSI;

          aEtiqueta = "EntidadAtEp";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               #cretam31#25 = aValor;
          |FINSI;

          aEtiqueta = "NumeroTrabajadores";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               #cretam31#26 = aValor;
          |FINSI;

          aEtiqueta = "NumeroLiquidacion";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               #cretam31#27 = aValor;
          |FINSI;

          aEtiqueta = "AlcanceCalculo";
          |HAZ BuscaEtiIniFin;
          |SI nLoc = 1;
               #cretam31#28 = aValor;
          |FINSI;

          aEtiqueta = "DatosCabeceraRNT";
          |HAZ BuscaEtiFin;
          |SI nLoc = 1;
               |GRABA 020#cretam31.a;
               |LIBERA #cretam31;
               |SAL;
          |FINSI;
     |SIGUE;
|FPRC;

|PROCESO BuscaTipoAtrasos;
     |SI nAnyDesde = #cretam02#19;
          |SI nMesDesde = #cretam02#17; |Y nMesHasta = #cretam02#18;
               nTipo = #cretam02#3;
          |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE BuscaTipoAtrasos;
     #cretam02#1;
     ;
     00001, nAnyDesde, nMesDesde, 05, aCCC;
     99999, nAnyDesde, nMesDesde, 20, aCCC;
     ;
     NULL, BuscaTipoAtrasos;
|FIN;

|PROCESO Lectura;
     FLinea = 0;

     aAlfa = eaFichero;
     |XABRE "", aAlfa, nHandle; nHandle = FSalida;
     |SI FSalida ] 0;
          |PARA;
          |SI FLinea ] 0; |Y swAcaba = 0;
          |HACIENDO;
               FLinea = 0;
               |HAZ LeeLinea;

               aEtiqueta = "Ccc";
               |HAZ BuscaEtiIni;
               |SI nLoc = 1;
                    |HAZ CCC;
               |FINSI;

               aEtiqueta = "PeriodoDesde";
               |HAZ BuscaEtiIni;
               |SI nLoc = 1;
                    aCampoFecha = "PeriodoDesde";
                    |HAZ TipoFecha;
                    aMesDesde = aMes;
                    nMesDesde = aMes;
                    aAnyDesde = aAny;
                    nAnyDesde = aAny;
               |FINSI;

               aEtiqueta = "PeriodoHasta";
               |HAZ BuscaEtiIni;
               |SI nLoc = 1;
                    aCampoFecha = "PeriodoHasta";
                    |HAZ TipoFecha;
                    aMesHasta = aMes;
                    nMesHasta = aMes;
                    aAnyHasta = aAny;
                    nAnyHasta = aAny;
               |FINSI;

               aEtiqueta = "Tipo";
               |HAZ BuscaEtiIniFin;
               |SI nLoc = 1;
                    aTipo = aValor;
                    |SI aAlfa = "L00";
                         nTipo = 00;
                    |FINSI;
                    |SI aAlfa = "L13";
                         nTipo = 02;
                    |FINSI;
                    |SI aAlfa = "L03";
                         || tenemos que buscar que tipo de atrasos son
                         || pongamos el 05 por defecto
                         nTipo = 05;
                         |BUCLE BuscaTipoAtrasos;
                    |FINSI;

                    |SI 05 [ nTipo; |Y nTipo [ 20;
                    || por aqui
                         swAtrasos = 1;
                    |FINSI;

                    |BUCLE Borra_cretam31;
                    |BUCLE Borra_cretam33;
                    |SELECT #1#cretam31;
                    |SELECT #1#cretam33;
               |FINSI;

               aEtiqueta = "Estado";
               |HAZ BuscaEtiIniFin;
               |SI nLoc = 1;
                    aEstado = aValor;
               |FINSI;

               aEtiqueta = "FechaHoraRecaudacion";
               |HAZ BuscaEtiIni;
               |SI nLoc = 1;
                    |HAZ FechaHoraRec;
               |FINSI;

               aEtiqueta = "DatosCabeceraRNT";
               |HAZ BuscaEtiIni;
               |SI nLoc = 1;
                    |SI swAtrasos = 1;
                         |PARA nMesXX = nMesDesde; |SI nMesXX [ nMesHasta; |HACIENDO nMesXX = nMesXX + 1;
                              |HAZ DatosCabeceraRNT;
                         |SIGUE;
                    |SINO;
                         nMesXX = nMesDesde;
                         |HAZ DatosCabeceraRNT;
                    |FINSI;
               |FINSI;

               aEtiqueta = "LineaRNT";
               |HAZ BuscaEtiIni;
               |SI nLoc = 1;
                    |HAZ LineaRNT;
               |FINSI;

               aEtiqueta = "LineaPieIRNT";
               |HAZ BuscaEtiIni;
               |SI nLoc = 1;
                    |HAZ LineaPieRNT;
               |FINSI;

               aEtiqueta = "DatosPiePaginaRNT";
               |HAZ BuscaEtiIni;
               |SI nLoc = 1;
                    |HAZ DatosPiePaginaRNT;
               |FINSI;
          |SIGUE;
     |FINSI;

     |XCIERRA nHandle;
|FPRC;

|PROGRAMA;
     |ABRE #cretam31;
     |ABRE #cretam33;

     |ABRE #cretam15;

     |ABRE #labempre;
     |ABRE #labcentr;
     |ABRE #nomcenes;
     |ABRE #labtraba;

     |HAZ Lectura;

     |CIERRA #cretam31;
     |CIERRA #cretam33;

     |CIERRA #cretam15;

     |CIERRA #labempre;
     |CIERRA #labcentr;
     |CIERRA #nomcenes;
     |CIERRA #labtraba;
|FPRO;
