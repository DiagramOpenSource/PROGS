||      Pases desde contabilidad especiales

|FICHEROS;
     isoz0001 #0;

     isom0000 #100;
     isom0001 #101;
     isom0002 #102;
     isom0006 #106;
     isom0008 #108;
     zregdocu #600;

     isom0009 #109;
     isom0010 #110;

     isom0499 #499;
     isom0505 #505;
     isom0507 #507;

     maempr11 #201;

     sodacoli #1;
     :/modelos/def/dsmom042 #50;  || Amortizaciones Lineas
     :/modelos/def/dsmom041 #51;  || Amortizaciones Cabeceras

     soda8m04 #444;

     :/contagen/def/cacieex6 #310;
     :/contagen/def/catw0011 #311;
     :/contagen/def/catw0012 #312;
     :/contagen/def/catw0015 #315;  || Distribucion Resultados
     :/contagen/def/canempre #330;
|FIN;

|VARIABLES;
     &enEmpresa  = 0;
     &enAnyo     = 0;
     &enCooperativa = 0;

     &aCuenta   = "";
     aSigno     = "";
     nImporte   = 0;
     nImpTotal  = 0;
     aDCta      = "";
     aHCta      = "";
     aAlfa      = "";
     aAlfa1     = "";
     aAlfa2     = "";
     aAlfa3     = "";
     nNume1     = 0;
     nNume2     = 0;
     nNume3     = 0;
     nPara1     = 0;
     nHay       = 0;
     aCasilla   = "";
     nValor1    = 0;
     nValor2    = 0;
     nValorRet  = 0;
     nAnota     = 0;
     nCampo     = 0;
     nGraba     = 0;
     aTexto     = "";
     nEjer      = 0;
     nConta     = 0;
     &eaPrograma  = "";
     &eaExtension = "";
     &eaPathContagen = "";
     aPath       = "";
     nDesde      = 0;
     nHasta      = 0;
     nAnyo       = 0;
     aAnyo       = "";
     nTope       = 0;
     &enSwDis    = 0;
     nCampos     = 0;
     nAnyoRet    = 0;
     nOpera      = 0;
|FIN;

|PROCESO isom0505;
     #106#0  = enEmpresa;
     #106#1  = #505#0;
     #106#2  = #505#1;
     |LEE 101#106=;
     |SI FSalida ! 0;
         |DDEFECTO #106;
         #106#0  = enEmpresa;
         #106#1  = #505#0;
         #106#2  = #505#1;
         |GRABA 020#106b;
     |FINSI;

     #106#3  = #505#4;
     #106#4  = #505#2;
     #106#6  = #505#3;
     |GRABA 020#106a;
     |LIBERA #106;
|FPRC;

|DEFBUCLE isom0505;
     #505#1;
     ;
     enAnyo, 0000;
     enAnyo, 9999;
     ;
     NULL, isom0505;
|FIN;

|PROCESO MontaEstructura505;
     |BUCLE isom0505;
|FPRC;

|PROCESO isom0006;
     |SI #106#5 ! 0;  |ACABA;  |FINSI;
     |SI #106#7 ! 0;  |ACABA;  |FINSI;

     |BORRA 020#106a;
     |LIBERA #106;
|FPRC;

|DEFBUCLE isom0006;
     #106#1;
     ;
     enEmpresa, enAnyo, 0000;
     enEmpresa, enAnyo, 9999;
     be;
     NULL, isom0006;
|FIN;

|PROCESO BonImporte;
     nNume1 = aAlfa1; nNume1 = nNume1 * 100;
     aAlfa2 = nNume1; aAlfa3 = aAlfa2 % 0;
     nNume1 = aAlfa3;
     aAlfa1 = "";
     nNume3 = 0;
     |PARA nPara1 = nNume1; |SI nPara1 ] 1; |HACIENDO nPara1 = nPara1 - 1;
           nNume2 = (nPara1 * 100 + 1);
           aAlfa3 = aAlfa2 % nNume2;
           aAlfa1 = aAlfa3 + aAlfa1;
           nNume3 = nNume3 + 1; |SI nNume3 = 2; aAlfa1 = "." + aAlfa1; |FINSI;
     |SIGUE;
     aAlfa1 = ("               " + aAlfa1) % -115;
|FPRC;

|PROCESO CalValor;
     |DDEFECTO #51;
     #51#0  = #50#0;
     #51#2  = #50#2;
     #51#3  = #50#3;
     |LEE 040#51=;

     |SI #50#13  = 0;  |ACABA; |FINSI;

     aTexto = #50#2 + "/"
     aAlfa1 = #50#3; aAlfa1 = ("00" + aAlfa1) % -102;
     aTexto = aTexto + aAlfa1;
     aTexto = aTexto + " " + #51#4;
     nImporte = #50#13;

     |SI #50#13 > 0;
         nValor1  = (nValor1 + #50#13) ! 2;
     |SINO;
         nValor2  = (nValor2 - #50#13) ! 2;
     |FINSI;

     |SI nAnota [ 22;
         aAlfa1 = nImporte; |HAZ BonImporte;
         aTexto = aTexto + " Importe:" + aAlfa1;
         #499nAnota = aTexto;
         nAnota     = nAnota + 1;
     |SINO;
         nImpTotal = nImpTotal + nImporte;
         aAlfa1    = nImpTotal; |HAZ BonImporte;
         aTexto    = "Resto de Bienes Amortizables                         Importe:" + aAlfa1;
         #499#22   = aTexto;
     |FINSI;
|FPRC;

|DEFBUCLE dsmom042;
      #50#2;
      5;
      enEmpresa, "        ",  00,  "A", "01.01.1900", 001, enAnyo;
      enEmpresa, "zzzzzzzz",  99,  "A", "31.12.2999", 999, enAnyo;
      ;
      NULL, CalValor;
|FIN;

|PROCESO Graba106;
     #106#0  = enEmpresa;
     #106#1  = enAnyo;
     #106#4  = aCasilla;
     |LEE 101#106=;
     |SI FSalida = 0;
         #106#5 = nValor1;
         #106#7 = nValor2;
         |GRABA 020#106a;
     |FINSI;
     |LIBERA #106;

     nGraba = 1;
     |PARA nCampo = 3;  |SI nCampo [ 22;  |HACIENDO nCampo = nCampo + 1;
           aAlfa = #499nCampo;  |QBF aAlfa;
           |SI aAlfa ! "";  nGraba = 0;  |SAL;  |FINSI;
     |SIGUE;

     |SI nGraba = 0;
         |GRABA 020#499a;
     |SINO;
         |BORRA 020#499a;
     |FINSI;
     |LIBERA #499;
|FPRC;

|PROCESO PaseAmortiza;
     |ABRET #106;
     |HAZ MontaEstructura505;

     |SELECT #2#106;

     aCasilla = "303";
     nValor1  = 0;
     nValor2  = 0;

     |ABRE #499;
     |DDEFECTO #499;
     #499#0 = enEmpresa;
     #499#1 = enAnyo;
     #499#2 = aCasilla;
     |LEE 141#499=;
     |SI FSalida ! 0;
         #499#0 = enEmpresa;
         #499#1 = enAnyo;
         #499#2 = aCasilla;
         |GRABA 020#499b;
     |FINSI;

     |PDEFECTO #499, 3, 23;
     nAnota    = 3;
     nImpTotal = 0;
     |ABRE #51;
     |BUCLE dsmom042;
     |CIERRA #51;
     |HAZ Graba106;
     |CIERRA  #499;

     |SELECT #1#106;

     |ABRE #101;
     |HAZ TotalFich106;
     |CIERRA #101;

     |CIERRAT #106;

     |BUCLE isom0006;
|FPRC;

|| ///////////////////////////////////////////////////////////////////////
|PROCESO MiroContabilidad;
     nConta = 0;
     |HAZ BusquedaApli;
     |SI eaPathContagen ! "";
         aAlfa1 = enAnyo;
         aAlfa1 = aAlfa1 % -102;
         nEjer  = aAlfa1;
         |ABRE #330;
         |SELECT #4#330;
         #330#2  = enEmpresa;
         #330#26 = nEjer;
         |LEE 040#330=;
         |SI FSalida = 0;
             nConta = 1;
             aAlfa1 = #330#2; aAlfa1 = ("00000" + aAlfa1) % -105;
             aAlfa2 = #330#3; aAlfa2 = ("0"     + aAlfa2) % -101;
             aPath = eaPathContagen + "fich/" + aAlfa1 + aAlfa2 +  "/";
         |FINSI;
         |CIERRA #330;
     |FINSI;
|FPRC;

|| ///////////////////////////////////////////////////////////////////////
|PROCESO LeoRetencionEsp;
     #0#6 = enAnyo; #0#7 = #311#1;
     || |SI PARAMETRO = ""; |PINTA #0#6; |PINTA #0#7; |FINSI;

     |SI nOpera = 1;
         |DDEFECTO #444;
         #444#4 = nAnyoRet;
         #444#0 = #311#1;
         |LEE 041#444=;
         |SI FSalida = 0;
             |ACABA;
         |FINSI;
     |FINSI;

     |SI #444#3 = "+";
         nValor1  = nValor1 + #311#3;
     |SINO;
         nValor1  = nValor1 - #311#3;
     |FINSI;
|FPRC;

|DEFBUCLE LeoRetencionEsp;
    #311#1;
    ;
    "               ", 14, aDCta;
    "zzzzzzzzzzzzzzz", 14, aHCta;
    e;
    NULL, LeoRetencionEsp;
|FIN;

|PROCESO LeoRetencion;
      #0#6 = enAnyo; #0#7 = #1#3;
      || |SI PARAMETRO = ""; |PINTA #0#6; |PINTA #0#7; |FINSI;

      |SI #444#3 = "+";
          nValor1  = nValor1 + #1#8;
      |SINO;
          nValor1  = nValor1 - #1#8;
      |FINSI;
|FPRC;

|DEFBUCLE LeoRetencion;
    #1#2;
    ;
    enEmpresa, enAnyo, aDCta;
    enEmpresa, enAnyo, aHCta;
    e;
    NULL, LeoRetencion;
|FIN;

|PROCESO Carga_soda8m04;

    aAlfa1 = #444#0; |QBF aAlfa1;
    aDCta = (aAlfa1 + "            ") % 112;
    aHCta = (aAlfa1 + "zzzzzzzzzzzz") % 112;

    |SI enAnyo ] 2017;
        |SI aCasilla ! #444#6;
            |SI nValor1 ! 0; |Y aCasilla ! "";
                |HAZ CasRetencion;
            |FINSI;
            nValor1 = 0;
        |FINSI;
        aCasilla = #444#6;
    |FINSI;

    |SI nConta = 0;
        |BUCLE LeoRetencion;
    |SINO;
        |BUCLE LeoRetencionEsp;
    |FINSI;
|FPRC;

|DEFBUCLE Leo_soda8m04;
     #444#2;
     ;
     nAnyoRet, "     ", "            ";
     nAnyoRet, "zzzzz", "zzzzzzzzzzzz";
     e;
     NULL, Carga_soda8m04;
|FIN;

|PROCESO Leosoda8m04;
     nAnyoRet = #444#4;
|FPRC;

|DEFBUCLE Leosoda8m04;
     #444#1;
     ;
     2008,  "            ";
     enAnyo,"zzzzzzzzzzzz";
     e;
     NULL, Leosoda8m04;
|FIN;

|PROCESO PaseRetencion;

    |HAZ MiroContabilidad;
    |SI nConta = 1;
        |PATH_DAT #311 aPath;
    |SINO;
        |SI enAnyo ] 2017;
            |ACABA;       || si no tiene contabilidad no se hace este pase
        |FINSI;
    |FINSI;

    nAnyoRet = 0;
    |BUCLE Leosoda8m04;

    nValor1    = 0;
    nValorRet  = 0;
    aCasilla = "595  "; |SI enAnyo ] 2017;  aCasilla = ""; |FINSI;

    |BUCLE Leo_soda8m04;

    |SI aCasilla ! "";
        |HAZ CasRetencion;
    |FINSI;

    |SI enAnyo ] 2017; |Y nConta = 1;
        nValor1 = 0;
        |ABRE #444;
        aDCta = "            ";
        aHCta = "zzzzzzzzzzzz";
        nOpera = 1;
        |BUCLE LeoRetencionEsp;
        nOpera = 0;
        |CIERRA #444;
        |SI nValor1 ! 0;
            nValor1 = nValor1 + nValorRet;
            aCasilla = "1798 ";
            |HAZ CasRetencion;
        |FINSI;
    |FINSI;
|FPRC;

|PROCESO CasRetencion;

   |SI aCasilla ! "0000 ";

       |ABRE #101;
       |SELECT #2#101;
       #101#0 = enEmpresa;
       #101#1 = enAnyo;
       #101#3 = aCasilla;
       |LEE 101#101=;
       |SI FSalida = 0;
           #101#5 = nValor1;
           |GRABA 020#101a;
       |FINSI;
       |LIBERA #101;
       |CIERRA #101;
       |SI aCasilla = "1798 "; nValorRet = nValor1; |FINSI;
   |FINSI;
|FPRC;

|| ///////////////////////////////////////////////////////////////////////
|PROCESO Leocatw0011Dif;
     #0#6 = enAnyo; #0#7 = #311#1;
     || |SI PARAMETRO = ""; |PINTA #0#6; |PINTA #0#7; |FINSI;

     |SI #311#3  = 0;  |ACABA; |FINSI;

     nValor1  = nValor1 + #311#3;

     aTexto   = "Cuenta: " + #311#1 + " " + #311#2;
     nImporte = #311#3;

     |SI nAnota [ 22;
         aAlfa1 = nImporte; |HAZ BonImporte;
         aTexto = aTexto + " Importe:" + aAlfa1;
         #499nAnota = aTexto;
         nAnota     = nAnota + 1;
     |SINO;
         nImpTotal = nImpTotal + nImporte;
         aAlfa1    = nImpTotal; |HAZ BonImporte;
         |SI nCampos = 7;
             aTexto    = "Resto Diferencias Permanentes                        Importe:" + aAlfa1;
         |SINO;
             aTexto    = "Resto Dif. Provision de deterioro                    Importe:" + aAlfa1;
         |FINSI;
         #499#22   = aTexto;
     |FINSI;
|FPRC;

|DEFBUCLE catw0011Dif;
    #311#1;
    ;
    "               ", nCampos, aDCta;
    "zzzzzzzzzzzzzzz", nCampos, aHCta;
    e;
    NULL, Leocatw0011Dif;
|FIN;

|PROCESO PaseDiferencias;

     |HAZ MiroContabilidad;
     |SI nConta = 1;
         |PATH_DAT #311 aPath;

         |ABRET #106;
         |HAZ MontaEstructura505;

         |SELECT #2#106;

         aCasilla = #0#20;
         nValor1  = 0;
         nValor2  = 0;

         |ABRE #499;
         |DDEFECTO #499;
         #499#0 = enEmpresa;
         #499#1 = enAnyo;
         #499#2 = aCasilla;
         |LEE 141#499=;
         |SI FSalida ! 0;
             #499#0 = enEmpresa;
             #499#1 = enAnyo;
             #499#2 = aCasilla;
             |GRABA 020#499b;
         |FINSI;

         |PDEFECTO #499, 3, 23;

         nCampos   = 7;
         nAnota    = 3;
         nImpTotal = 0;
         aDCta     = "            ";
         aHCta     = "zzzzzzzzzzzz";

         |BUCLE catw0011Dif;

         |HAZ Graba106;
         |CIERRA  #499;

         |SELECT #1#106;

         |ABRE #101;
         |HAZ TotalFich106;
         |CIERRA #101;

         |CIERRAT #106;

         |BUCLE isom0006;
     |FINSI;
|FPRC;

|PROCESO PaseProv33;

     |HAZ MiroContabilidad;
     |SI nConta = 1;
         |PATH_DAT #311 aPath;

         |ABRET #106;
         |HAZ MontaEstructura505;

         |SELECT #2#106;

         aCasilla = #0#24;
         nValor1  = 0;
         nValor2  = 0;

         |ABRE #499;
         |DDEFECTO #499;
         #499#0 = enEmpresa;
         #499#1 = enAnyo;
         #499#2 = aCasilla;
         |LEE 141#499=;
         |SI FSalida ! 0;
             #499#0 = enEmpresa;
             #499#1 = enAnyo;
             #499#2 = aCasilla;
             |GRABA 020#499b;
         |FINSI;

         |PDEFECTO #499, 3, 23;

         nCampos   = 33;
         nAnota    = 3;
         nImpTotal = 0;
         aDCta     = "            ";
         aHCta     = "zzzzzzzzzzzz";

         |BUCLE catw0011Dif;

         |HAZ Graba106;
         |CIERRA  #499;

         |SELECT #1#106;

         |ABRE #101;
         |HAZ TotalFich106;
         |CIERRA #101;

         |CIERRAT #106;

         |BUCLE isom0006;
     |FINSI;
|FPRC;

|PROCESO PaseProv35;

     |HAZ MiroContabilidad;
     |SI nConta = 1;
         |PATH_DAT #310 aPath;

         |ABRE #310;
         |LEE 001#310p;
         |SI FSalida = 0; |Y #310#35 ! 0;

             |ABRET #106;
             |HAZ MontaEstructura505;

             |SELECT #2#106;

             aCasilla = #0#22;
             nValor1  = 0;
             nValor2  = 0;

             |ABRE #499;
             |DDEFECTO #499;
             #499#0 = enEmpresa;
             #499#1 = enAnyo;
             #499#2 = aCasilla;
             |LEE 141#499=;
             |SI FSalida ! 0;
                 #499#0 = enEmpresa;
                 #499#1 = enAnyo;
                 #499#2 = aCasilla;
                 |GRABA 020#499b;
             |FINSI;

             |PDEFECTO #499, 3, 23;

             nValor1  = #310#35;
             nImporte = #310#35;
             aTexto   =  "Provision por deterioro en Cdtos Comerciales"
             aAlfa1   = nImporte; |HAZ BonImporte;
             aTexto   = aTexto + " Importe:" + aAlfa1;
             #499#3   = aTexto;

             |HAZ Graba106;
             |CIERRA  #499;

             |SELECT #1#106;

             |ABRE #101;
             |HAZ TotalFich106;
             |CIERRA #101;

             |CIERRAT #106;

             |BUCLE isom0006;
         |FINSI;
     |FINSI;
|FPRC;


|| ///////////////////////////////////////////////////////////////////////
|PROCESO isom0507Pase7;
     |SI nDesde = 0;
         #109#0  = enEmpresa;
         #109#1  = #507#0;
         #109#2  = #507#1;
         |LEE 101#109=;
         |SI FSalida ! 0;
             |DDEFECTO #109;
             #109#0  = enEmpresa;
             #109#1  = #507#0;
             #109#2  = #507#1;
             |GRABA 020#109b;
         |FINSI;

         #109#3  = #507#7;
         #109#4  = #507#2;
         #109#5  = #507#4;
         #109#7  = #507#5;
         #109#9  = #507#6;

         #109#8  = 0;
         #109#10 = #109#6;

         |GRABA 020#109a;
         |LIBERA #109;
     |FINSI;

     |SI nDesde ! 0;
         #110#0  = enEmpresa;
         #110#1  = #507#0;
         #110#2  = #507#1;
         |LEE 101#110=;
         |SI FSalida ! 0;
             |DDEFECTO #110;
             #110#0  = enEmpresa;
             #110#1  = #507#0;
             #110#2  = #507#1;
             #110#4  = "";
             |GRABA 020#110b;
         |FINSI;

         #110#3  = #507#7;
         #110#5  = #507#4;
         #110#7  = #507#5;
         #110#9  = #507#6;

         #110#8  = 0;
         #110#10 = #110#6;

         |GRABA 020#110a;
         |LIBERA #110;
     |FINSI;
|FPRC;

|DEFBUCLE isom0507Pase7;
     #507#1;
     ;
     enAnyo, nDesde;
     enAnyo, nHasta;     || Del 1 al 999 es el isom0009
     ;
     NULL, isom0507Pase7;
|FIN;

|PROCESO isom0010BPase7;
     |SI #110#6  ! 0;  |ACABA;  |FINSI;
     |SI #110#8  ! 0;  |ACABA;  |FINSI;
     |SI #110#10 ! 0;  |ACABA;  |FINSI;

     |BORRA 020#110a;
     |LIBERA #110;
|FPRC;

|DEFBUCLE isom0010BPase7;
     #110#1;
     ;
     enEmpresa, enAnyo, 0000;
     enEmpresa, enAnyo, 9999;
     be;
     NULL, isom0010BPase7;
|FIN;

|PROCESO isom0009BPase7;
     |SI #109#6  ! 0;  |ACABA;  |FINSI;
     |SI #109#8  ! 0;  |ACABA;  |FINSI;
     |SI #109#10 ! 0;  |ACABA;  |FINSI;

     |BORRA 020#109a;
     |LIBERA #109;
|FPRC;

|DEFBUCLE isom0009BPase7;
     #109#1;
     ;
     enEmpresa, enAnyo, 0000;
     enEmpresa, enAnyo, 9999;
     be;
     NULL, isom0009BPase7;
|FIN;

|PROCESO Graba561Pase7;
     |SELECT #3#110;
     #110#0 = enEmpresa;
     #110#1 = enAnyo;
     #110#4 = aAnyo;
     |LEE 101#110=;
     |SI FSalida = 0;
         #110#8  = #110#8  + #312#4;
         #110#10 = #110#6  - #110#8;
         |GRABA 020#110a;
         |LIBERA #110;
         |ACABA;
     |FINSI;

     |SELECT #1#110;
     |PARA nCampo = 1005;  |SI nCampo [ 1065;  |HACIENDO nCampo = nCampo + 5;
         #110#0 = enEmpresa;
         #110#1 = enAnyo;
         #110#2 = nCampo;
         |LEE 101#110=;
         |SI FSalida = 0;
             |SI #110#4 = "    ";
                 #110#4  = aAnyo;
                 #110#8  = #312#4;
                 #110#10 = #110#6 - #110#8;
                 |GRABA 020#110a;
                 |LIBERA #110;
                 |SAL;
             |FINSI;
             |LIBERA #110;
         |FINSI;
     |SIGUE;
|FPRC;

|PROCESO Graba547Pase7;
     #109#0 = enEmpresa;
     #109#1 = enAnyo;
     #109#4 = aAnyo;
     |LEE 101#109=;
     |SI FSalida = 0;
         #109#8  = #109#8 + #312#7;
         #109#10 = #109#6 - #109#8;
         |GRABA 020#109a;
     |FINSI;
     |LIBERA #109;
|FPRC;

|PROCESO LeeCatw0012;
     aAnyo = #312#6;
     |SI enCooperativa = 0;
         |SI #312#7 ! 0;
             |HAZ Graba547Pase7;
         |FINSI;
     |SINO;
         |SI #312#4 ! 0;
             |HAZ Graba561Pase7;
         |FINSI;
     |FINSI;
|FPRC;

|DEFBUCLE LeeCatw0012;
    #312#1;
    ;
    "               ", 10, "            ";
    "zzzzzzzzzzzzzzz", 10, "zzzzzzzzzzzz";
    e;
    NULL, LeeCatw0012;
|FIN;

|PROCESO ImporteAplicar;
     enCooperativa = 0;

     |ABRE #201;
     #201#0 = enEmpresa;
     #201#1 = enAnyo;
     #201#2 = 17;
     |LEE 000#201=;
     |SI FSalida = 0;  enCooperativa = 1;  |FINSI;

     #201#0 = enEmpresa;
     #201#1 = enAnyo;
     #201#2 = 18;
     |LEE 000#201=;
     |SI FSalida = 0;  enCooperativa = 1;  |FINSI;

     #201#0 = enEmpresa;
     #201#1 = enAnyo;
     #201#2 = 19;
     |LEE 000#201=;
     |SI FSalida = 0;  enCooperativa = 1;  |FINSI;

     |CIERRA #201;

     |HAZ MiroContabilidad;
     |SI nConta = 1;        ||Si nConta = 0 no hay contabilidad, no hay pase
         |PATH_DAT #312 aPath;

         nAnyo  = enAnyo - 1;
         aAnyo  = enAnyo;
         |SI enCooperativa = 0;
             nDesde = 0;
             nHasta = 999;

             |ABRE #109;
             |SELECT #1#109;
             |BUCLE isom0507Pase7;

             |SELECT #3#109;
             |BUCLE LeeCatw0012;

             |HAZ TotalFich109;

             |SELECT #1#109;
             |CIERRA #109;

             |BUCLE isom0009BPase7;
         |SINO;
             nDesde = 1000;
             nHasta = 9999;

             |ABRE #110;
             |SELECT #1#110;
             |BUCLE isom0507Pase7;

             |BUCLE LeeCatw0012;

             |HAZ TotalFich110;

             |SELECT #1#110;
             |CIERRA #110;

             |BUCLE isom0010BPase7;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO Pase415;
     |ABRE #108;
     #108#0  = enEmpresa;
     #108#1  = enAnyo - 1;
     #108#2  = 1065;
     |LEE 000#108=;
     |SI FSalida ! 0;  |DDEFECTO #108;  |FINSI;
     |CIERRA #108;

     |SI #108#5 = 0;  |Y #108#7 = 0;
         |ACABA;
     |FINSI;

     |ABRET #106;
     |HAZ MontaEstructura505;

     |SELECT #1#106;
     #106#0 = enEmpresa;
     #106#1 = enAnyo;
     #106#2 = 285;
     |LEE 101#106=;
     |SI FSalida = 0;
         #106#5 = #108#5;
         #106#7 = #108#7;
         |GRABA 020#106a;
         |LIBERA #106;
     |FINSI;

     |ABRE #101;
     |HAZ TotalFich106;
     |CIERRA #101;
     |CIERRAT #106;

     |BUCLE isom0006;
|FPRC;

|PROCESO PaseTipoGravamen;
    nTope = 120202.41;

    |HAZ MiroContabilidad;
    |SI nConta ! 1;
        |ACABA;
    |FINSI;
    |PATH_DAT #310 aPath;

    |ABRE #310;
    |LEE 001#310p;
    |SI FSalida = 0;
        |ABRE #101;
        |SELECT #2#101;
        #101#0 = enEmpresa;
        #101#1 = enAnyo;
        #101#3 = "558";
        |LEE 101#101=;
        |SI FSalida = 0;
            #101#5 = #310#5;
            |GRABA 020#101a;
        |FINSI;
        |LIBERA #101;

        |SI #310#3 > nTope;
            #101#0 = enEmpresa;
            #101#1 = enAnyo;
            #101#3 = "901";
            |LEE 101#101=;
            |SI FSalida = 0;
                #101#5 = #310#6;
                |GRABA 020#101a;
            |FINSI;
            |LIBERA #101;
        |FINSI;
        |SELECT #1#101;
        |CIERRA #101;
    |FINSI;
|FPRC;

|PROCESO LeeCatw0015;
     |PARA nPara1 = 0; |SI nPara1 [ 21; |HACIENDO nPara1 = nPara1 + 1;
           nCampo = nPara1 + 84;
           #100nCampo = #100nCampo + #315nPara1;
           |SI #315nPara1 ! 0; nHay = 1; |FINSI;
     |SIGUE;
     #100#125 = #100#125 + #315#26;
     #100#126 = #100#126 + #315#27;
     |SI #315#26 ! 0; |O #315#27 ! 0; nHay = 1; |FINSI;
|FPRC;

|DEFBUCLE LeeCatw0015;
    #315#1;
    ;
    "               ";
    "zzzzzzzzzzzzzzz";
    e;
    NULL, LeeCatw0015;
|FIN;

|PROCESO ImporteDistribuir;

     |HAZ MiroContabilidad;
     |SI nConta = 1;        ||Si nConta = 0 no hay contabilidad, no hay pase
         |PATH_DAT #315 aPath;
         |ABRE #100;
         #100#0 = enEmpresa;
         #100#1 = enAnyo;
         |LEE 101#100=;
         |SI FSalida ! 0;
             |DDEFECTO #100;
             #100#0 = enEmpresa;
             #100#1 = enAnyo;
             |GRABA 020#100b;
         |FINSI;

         |PDEFECTO #100, 84, 106;
         #100#125 = 0;
         #100#126 = 0;
         nHay = 0;
         |BUCLE LeeCatw0015;

         |SI nHay = 1;
             |GRABA 020#100a;
             enSwDis = 1;

             |HAZ GrabaRegdocu;
         |FINSI;
         |LIBERA #100;
         |CIERRA #100;
     |FINSI;
|FPRC;

|PROCESO ImpDist199;
     |ABRE #100;
     #100#0 = enEmpresa;
     #100#1 = enAnyo;
     |LEE 101#100=;
     |SI FSalida ! 0;
         |DDEFECTO #100;
         #100#0 = enEmpresa;
         #100#1 = enAnyo;
         |GRABA 020#100b;
     |FINSI;

     |ABRE #isom0002;
     |SELECT #2#isom0002;
     #isom0002#0  = enEmpresa;
     #isom0002#1  = enAnyo;
     #isom0002#3  = "199";
     |LEE 040#isom0002.=;
     |SI FSalida ! 0;  |DDEFECTO #isom0002;  |FINSI;
     |SELECT #1#isom0002;
     |CIERRA #isom0002;
     |SI #isom0002#5 < 0; #isom0002#5 = 0; |FINSI;

     #isom0000#84 = #isom0002#5;
     #isom0000#88 = #isom0000#84 + #isom0000#85 + #isom0000#86 + #isom0000#87;
     |HAZ GrabaRegdocu;

     |GRABA 020#100a;
     |LIBERA #100;
     |CIERRA #100;
|FPRC;

|PROCESO GrabaRegdocu;
         |ABRE #600;
         #600#0 = enEmpresa;
         #600#1 = enAnyo;
         |LEE 101#600=;
         |SI FSalida = 0;
             #600#16 = #100#84;
             #600#17 = #100#85;
             #600#18 = #100#86;
             #600#19 = #100#87;
             #600#20 = #100#88;

             #600#21 = #100#89;
             #600#22 = #100#90;
             #600#23 = #100#91;
             #600#24 = #100#92;
             #600#25 = #100#94 + #100#95;
             #600#26 = #100#104;
             #600#27 = #100#102;
             #600#28 = #100#93 + #100#96  + #100#97  + #100#98  + #100#99;
             #600#28 = #600#28 + #100#100 + #100#101 + #100#103 + #100#125 + #100#126;
             #600#29 = #100#105;

             |GRABA 020#600a;
         |FINSI;
         |LIBERA #600;
         |CIERRA #600;
|FPRC;
