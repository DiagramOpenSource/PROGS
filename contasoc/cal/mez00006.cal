|FICHEROS;
     mez00006 #0;
     maacceso #1;
     maempr10 #10;
     maempr17 #17,MANTE;
     mew00013;

     lablitip@ #900;
|FIN;

|VARIABLES;
     nHastaAnyo      = 0;
     nAnyo           = 0;
     nDEmpresa       = 0;
     nHEmpresa       = 0;
     nTotalAnyo      = 0;
     nSwLee          = 0;
     nPase           = 0;
     nTrabaFijo      = 0;
     nTrabaNoFijo    = 0;

     nEstado         = 0;
     nEmpresa        = 0;
     nEjer           = 0;
     nOk             = 0;
     nOpera          = 0;
     nInkey          = 0;
     aAlfa           = "";
     aFichero        = "";
     aFich           = "";
     aMas            = "";
     aBase           = "";

     fFecha          = @;

     &enDiasBisiesto = 0;
     &eaTipo         = "";
     &enEmpresa      = 0;
     &enAnyo         = 0;

     nNume = 0;
     &en4001_1 = 0;
     &en4001_2 = 0;
     &en4002_1 = 0;
     &en4002_2 = 0;
     &en4010_1 = 0;
     &en4010_2 = 0;

     &en4120_1 = 0;
     &en4121_1 = 0;
     &en4121_2 = 0;
     &en4121_3 = 0;
     &en4122_1 = 0;
     &en4123_1 = 0;
     &en4123_2 = 0;
     &en4123_3 = 0;
     &en4120_2 = 0;
     &en4122_2 = 0;

     fFinal = @;
     nAnyoMenosUno = 0;
     &enSwMensajes = 0;
     nCampo = 0;
     nCampo1 = 0;
|FIN;

|| PROCESO DESDE LABORAL

|PROCESO PonACero;
     en4001_1 = 0;
     en4001_2 = 0;
     en4002_1 = 0;
     en4002_2 = 0;
     en4010_1 = 0;
     en4010_2 = 0;
     en4120_1 = 0;
     en4121_1 = 0;
     en4121_2 = 0;
     en4121_3 = 0;
     en4122_1 = 0;
     en4123_1 = 0;
     en4123_2 = 0;
     en4123_3 = 0;

     en4120_2 = 0;
     en4122_2 = 0;
|FPRC;

|PROCESO lablitip;
          |SI #900#1 = nAnyo;                          || periodo actual
               aAlfa = #0#0;
               aAlfa = "31.12." + aAlfa;
               fFinal = aAlfa;
               |SI fFinal [ #900#7;                 || de alta el 31/12
                    |SI #900#12 = "S";              || fijos
                         |SI #900#14 = "H";         || hombre
                              en4120_1 = en4120_1 + 1;
                         |FINSI;
                         |SI #900#14 = "M";         || mujer
                              en4121_1 = en4121_1 + 1;
                         |FINSI;
                    |FINSI;
                    |SI #900#12 = "N";              || eventuales
                         |SI #900#14 = "H";         || hombre
                              en4122_1 = en4122_1 + 1;
                         |FINSI;
                         |SI #900#14 = "M";         || mujer
                              en4123_1 = en4123_1 + 1;
                         |FINSI;
                    |FINSI;
               |FINSI;
               |SI #900#15 = "S";
                     en4010_1 =  en4010_1 + #900#10;
               |FINSI;
                                             || media total
               |SI #900#12 = "S";              || fijos ejercicio 1
                    en4001_1 = en4001_1 + #900#10;
               |FINSI;
               |SI #900#12 = "N";              || eventuales ejercicio 1
                    en4002_1 = en4002_1 + #900#10;
               |FINSI;
          |FINSI;

          |SI #900#1 = nAnyoMenosUno;
               nNume = #0#0 - 1;
               aAlfa = nNume;
               aAlfa = "31.12." + aAlfa;
               fFinal = aAlfa;
               |SI fFinal [ #900#7;                 || de alta el 31/12
                    |SI #900#12 = "S";              || fijos
                         |SI #900#14 = "H";         || hombre
                              en4120_2 = en4120_2 + 1;
                         |FINSI;
                         |SI #900#14 = "M";         || mujer
                              en4121_2 = en4121_2 + 1;
                         |FINSI;
                    |FINSI;
                    |SI #900#12 = "N";              || eventuales
                         |SI #900#14 = "H";         || hombre
                              en4122_2 = en4122_2 + 1;
                         |FINSI;
                         |SI #900#14 = "M";         || mujer
                              en4123_2 = en4123_2 + 1;
                         |FINSI;
                    |FINSI;
               |FINSI;
               |SI #900#15 = "S";
                    en4010_2 = en4010_2 + #900#10;
               |FINSI;
                                             || media total
               |SI #900#12 = "S";              || fijos ejercicio 1
                    en4001_2 = en4001_2 + #900#10;
               |FINSI;
               |SI #900#12 = "N";              || eventuales ejercicio 1
                    en4002_2 = en4002_2 + #900#10;
               |FINSI;
          |FINSI;
|FPRC;

|DEFBUCLE lablitip;
     #900#1003;
     ;
     nDEmpresa, nAnyoMenosUno, 0001;
     nHEmpresa, nAnyo, 9999;
     ;
     NULL, lablitip;
|FIN;

|| /////////////////////////////////////////////////////////////////
|PROCESO GrabaAsalariados;
     |ABRE #10;
     #10#0 = #17#1;
     #10#1 = #17#0;
     |LEE 101#10=;
     |SI FSalida = 0;
         #10#75 = #17#3;
         #10#76 = #17#4;
         #10#86 = #17#3;
         #10#88 = #17#4;

         #10#87 = en4001_2;
         #10#89 = en4002_2;

          #10#103 = en4010_1;
          #10#104 = en4010_2;

          #10#105 = en4120_1;
          #10#107 = en4121_1;
          #10#109 = en4122_1;
          #10#111 = en4123_1;

          #10#106 = en4120_2;
          #10#108 = en4121_2;
          #10#110 = en4122_2;
          #10#112 = en4123_2;

          |GRABA 020#10a;
          |LIBERA #10;
     |FINSI;

     |CIERRA #10;
|FPRC;
|| /////////////////////////////////////////////////////////////////

|PROCESO Mensaje;
     |PUSHV 11231663;
     |ATRI R;
     |PINTA 1224, "                                     ";
     |PINTA 1324, " Calculando relación de trabajadores ";
     |PINTA 1424, " de la empresa XXXXX desde  laboral. ";
     |PINTA 1524, "                                     ";
     aAlfa = #1#0;
     |QBF aAlfa;
     aAlfa = ("00000" + aAlfa) % -105;
     |PINTA 1439, aAlfa;
     |ATRI 0;
|FPRC;

|PROCESO maacceso;
     |SI nOpera = 0;
         |SI nPase = 1;
              |HAZ Mensaje;

              enEmpresa = #1#0;
              aAlfa     = #1#1;
              aAlfa     = aAlfa % -102;
              enAnyo    = aAlfa;
              enSwMensajes = 0;
              |SUB_EJECUTA ":laboral/labpainc;SOCIEDAD";

              enEmpresa = #1#0;
              aAlfa     = #1#1;
              aAlfa     = aAlfa % -102;
              enAnyo    = aAlfa;
              enAnyo    = enAnyo - 1;
              enSwMensajes = 1;
              |SUB_EJECUTA ":laboral/labpainc;SOCIEDAD";
         |FINSI;

         #17#0 = #1#1;
         #17#1 = #1#0;
         |LEE 101#17=;
         |SI FSalida ! 0;
             |DDEFECTO #17;
             #17#0 = #1#1;
             #17#1 = #1#0;
             |GRABA 020#17b;
         |FINSI;

         aAlfa     = #0#0;
         aAlfa     = aAlfa % -102;
         nAnyo     = aAlfa;
         nAnyoMenosUno = nAnyo - 1;
         nDEmpresa = #1#0;
         nHEmpresa = #1#0;

         nSwLee = 0;  |BUCLE lablitip;

         |SI nSwLee = 0;
             #17#2 = #1#2;
             |GRABA 020#17a;
             |LIBERA #17;
             ||ACABA;
         |FINSI;

         #17#2 = #1#2;
         #17#3 = 0;
         #17#4 = 0;

         |HAZ PonACero;
         |BUCLE lablitip;

         #17#3 = en4001_1;        || media fijos periodo actual
         #17#4 = en4002_1;        || media event periodo anterior

         |GRABA 020#17a;
         |LIBERA #17;

         |HAZ GrabaAsalariados;

         |SI nPase = 1;
              |POPV;
         |FINSI;
    |FINSI;

    |SI nOpera = 1;

        nOk          = 0;
        nTrabaFijo   = 0;
        nTrabaNoFijo = 0;
        #17#0 = #1#1 - 1;
        #17#1 = #1#0;
        |LEE 041#17=;
        |SI FSalida = 0;
            nOk          = 1;
            nTrabaFijo   = #17#3;
            nTrabaNoFijo = #17#4;
        |FINSI;

        nEmpresa = #1#0;
        nEjer    = #1#1 - 1;

        |HAZ LeoEl10;

        |DDEFECTO #17;
        #17#0 = #1#1;
        #17#1 = #1#0;
        |LEE 141#17=;
        nEstado = FSalida;
        |SI nEstado ! 0;
            |DDEFECTO #17;
            #17#0 = #1#1;
            #17#1 = #1#0;
            #17#2 = #1#2;
        |FINSI;

        |SI nPase = 1;
            #17#3 = nTrabaFijo;
            #17#4 = nTrabaNoFijo;
            |SI nEstado = 0;             |GRABA 020#17a; |FINSI;
            |SI nEstado ! 0; |Y nOk = 1; |GRABA 020#17b; |FINSI;
        |FINSI;
        |LIBERA #17;

        nTrabaFijo   = #17#3;
        nTrabaNoFijo = #17#4;

        nEmpresa = #1#0;
        nEjer    = #1#1;
        |HAZ PaseAsalAnter;
    |FINSI;
|FPRC;

|PROCESO LeoEl10;
    |HAZ PonACero;

    #10#0 = nEmpresa;
    #10#1 = nEjer;
    |LEE 041#10=;
    |SI FSalida = 0;
        en4001_2 = #10#86;
        en4002_2 = #10#88;
        en4010_2 = #10#103;
        en4120_2 = #10#105;
        en4121_2 = #10#107;
        en4122_2 = #10#109;
        en4123_2 = #10#111;
        |SI nOk = 0;
            nTrabaFijo   = #10#75;
            nTrabaNoFijo = #10#76;
        |FINSI;
        nOk = 1;
    |FINSI;
|FPRC;

|PROCESO PaseAsalAnter;

    #10#0 = nEmpresa;
    #10#1 = nEjer;
    |LEE 141#10=;
    |SI FSalida = 0;

        |SI nPase = 1;
            #10#75 = nTrabaFijo;
            #10#76 = nTrabaNoFijo;
            #10#86 = nTrabaFijo;
            #10#88 = nTrabaNoFijo;

            #10#103 = en4010_2;
            #10#105 = en4120_2;
            #10#107 = en4121_2;
            #10#109 = en4122_2;
            #10#111 = en4123_2;
        |FINSI;

        #10#87  = en4001_2;
        #10#89  = en4002_2;
        #10#104 = en4010_2;
        #10#106 = en4120_2;
        #10#108 = en4121_2;
        #10#110 = en4122_2;
        #10#112 = en4123_2;

        |GRABA 020#10a;
        |LIBERA #10;
    |FINSI;
|FPRC;

|DEFBUCLE maacceso;
     #1#1;
     ;
     00001, #0#0;
     99999, #0#0;
     ;
     NULL, maacceso;
|FIN;

|PROCESO PideConf;
     enDiasBisiesto = 366;
     eaTipo         = "T";
|FPRC;

|| PROCESOS MAEMPR17

|PROCESO Modif;
     |ABRE #10;
     #10#0 = #17#1;
     #10#1 = #17#0;
     |LEE 101#10=;
     |SI FSalida = 0;
          |PUSHV 05012380;
          |CLS;

          |PARA nCampo = 0; |SI nCampo [ 13; |HACIENDO nCampo = nCampo + 1;
               |SI nCampo ] 0; |Y nCampo [ 3;
                    nCampo1 = nCampo + 86;
               |SINO;
                    nCampo1 = nCampo + 99;
               |FINSI;
               #mew00013#nCampo# = #10nCampo1;
          |SIGUE;

          |PINPA #0#mew00013;
          |PINDA #0#mew00013;
          |ATRI I;
          |PINTA 0622, #10#0;
          |PINTA 0628, #10#2;
          |ATRI I;
          |ENDATOS #1#mew00013;
          |SI FSalida = 0;
               |PARA nCampo = 0; |SI nCampo [ 13; |HACIENDO nCampo = nCampo + 1;
                    |SI nCampo ] 0; |Y nCampo [ 3;
                         nCampo1 = nCampo + 86;
                    |SINO;
                         nCampo1 = nCampo + 99;
                    |FINSI;
                    #10nCampo1 = #mew00013#nCampo#;
                    #10#75 = #10#86;
                    #10#76 = #10#88;
               |SIGUE;

               |GRABA 020#10.a;
          |FINSI;
          |POPV;

          #17#3 = #10#75;
          #17#4 = #10#76;

          |PONCONTROL 23, "si";
     |FINSI;
     |CIERRA #10;
     |LIBERA #10;
|FPRC;

|PROCESO Baja1;
     #1#0 = 1;
     #1#1 = #0#0;
|FPRC;

|PROCESO Alta1;
     #1#0 = 99999;
     #1#1 = #0#0;
|FPRC;

|PROCESO Tipo66F17;  |TIPO 66;
     |ABRE #1;
     |SELECT #5#1;
     #1#0 = #17#1;
     #1#1 = #17#0;
     |LEE 000#1=;

     |CONSULTA_F_CLAVE #5#1, Baja1, Alta1;
     |SI FSalida = 0;
         #17#1 = #1#0;
         |PINTA #17#1;
     |FINSI;
     |CIERRA #1;

     |SELECT #1#1;

     |ERROR;
|FPRC;

|PROCESO Tipo11F17;  |TIPO 11;

     |SI FSalida = 10;

         |CONFI "0000S¿Recoger datos de Laboral de la empresa?";
         |SI FSalida ! 0;  |ACABA;  |FINSI;

         nPase = 0;
         |CONFI "0000S¿Recalcular el mantenimiento de trabajadores IAE?";
         |SI FSalida = 0;  nPase = 1;  |FINSI;

         |SI nPase = 1;  |HAZ PideConf;  |FINSI;

         nOpera = 0;
         |DBASS aBase;  |QBF aBase;
         aMas = aBase + "laboral/def/lablitip.mas";
         |CARGA_DEF aMas, lablitip@;
         |SI FSalida ! 0;
             |MENAV "0000 No tiene instalada la aplicacion de laboral";
             |ACABA;
         |FINSI;

         aFich = aBase + "laboral/fich/";
         |PATH_DAT #900 aFich;

         |ABRE #1;
         #1#0 = #17#1;
         #1#1 = #17#0;
         |LEE 000#1=;
         |SI FSalida = 0;
             nOpera = 0;
             |HAZ maacceso;
         |FINSI;

         |DESCARGA_DEF #lablitip@;

         |PINTA #17#2;
         |PINTA #17#3;
         |PINTA #17#4;

         |ERROR;
     |FINSI;

     |SI FSalida = 11;
         |CONFI "0000NTraerse datos del ejercicio anterior";
         |SI FSalida ! 0;  |ACABA;  |FINSI;
         nPase = 0;
         |CONFI "0000NPasar tambien Datos Anteriores como Datos Actuales";
         |SI FSalida = 0;  nPase = 1;  |FINSI;

         |SALVA_DATOS #17;
         nOk          = 0;
         nTrabaFijo   = 0;
         nTrabaNoFijo = 0;
         #17#0 = #17#0 - 1;
         |LEE 041#17=;
         |SI FSalida = 0;
             nOk          = 1;
             nTrabaFijo   = #17#3;
             nTrabaNoFijo = #17#4;
         |FINSI;
         |REPON_DATOS #17;

         nEmpresa = #17#1;
         nEjer    = #17#0 - 1;

         |ABRE #10;
         |HAZ LeoEl10;

         |LEE 101#17=;
         |SI nPase = 1; |Y FSalida = 0;
             #17#3 = nTrabaFijo;
             #17#4 = nTrabaNoFijo;
             |GRABA 020#17a;
         |FINSI;
         |LIBERA #17;

         nTrabaFijo   = #17#3;
         nTrabaNoFijo = #17#4;

         nEjer    = #17#0;
         |HAZ PaseAsalAnter;
         |CIERRA #10;

         |PINTA #17#3;
         |PINTA #17#4;

         |ERROR;
     |FINSI;
|FPRC;

|| ////////////////////////////////////////////////////////////////////

|| PROCESOS MEZ00006

|PROCESO Baja17;
     #17#0 = #0#0;
     #17#1 = 1;
|FPRC;

|PROCESO Alta17;
     #17#0 = #0#0;
     #17#1 = 99999;
|FPRC;

|PROCESO Tipo11F0;  |TIPO 11;
     nInkey = FSalida;
     |SI nInkey ! 10;  |Y nInkey ! 11; |ACABA;  |FINSI;

     |SI nInkey = 10;

        |CONFI "0000S¿Recoger datos de Laboral de todas las empresas?";
        |SI FSalida ! 0;  |ACABA;  |FINSI;

        nPase = 0;
        |CONFI "0000S¿Realizar la carga automática del mantenimiento de Altas/Bajas IAE?";
        |SI FSalida = 0;  nPase = 1;  |FINSI;

        |SI nPase = 1;  |HAZ PideConf;  |FINSI;

        |DBASS aBase;  |QBF aBase;
        aMas = aBase + "laboral/def/lablitip.mas";
        |CARGA_DEF aMas, lablitip@;
        |SI FSalida ! 0;
            |MENAV "0000 No tiene instalada la aplicacion de laboral";
            |ACABA;
        |FINSI;

        aFich = aBase + "laboral/fich/";
        |PATH_DAT #900 aFich;

        nOpera = 0;
        |ABRE #17;
        |BUCLE maacceso;
        |CIERRA #17;

        |DESCARGA_DEF #lablitip@;

        |ENTLINEAL #1#17, -2, 7, 22, 0, Baja17, Alta17;

        |MENAV "0000 Pase Realizado.";

        |ERROR;
     |FINSI;

     |SI nInkey = 11;

         |CONFI "0000NTraerse datos del ejercicio anterior de todas las empresas";
         |SI FSalida ! 0;  |ACABA;  |FINSI;

         nPase = 0;
         |CONFI "0000NPasar tambien Datos Anteriores como Datos Actuales";
         |SI FSalida = 0;  nPase = 1;  |FINSI;

         nOpera = 1;
         |ABRE #17;
         |ABRE #10;
         |BUCLE maacceso;
         |CIERRA #10;
         |CIERRA #17;

         |ENTLINEAL #1#17, -2, 7, 22, 0, Baja17, Alta17;

         |MENAV "0000 Recogidos Datos Ejercicio Anterior";

         |ERROR;
     |FINSI;
|FPRC;

|PROCESO Tipo7C0F0;  |TIPO 7;
     |ENTLINEAL #1#17, -2, 7, 22, 0, Baja17, Alta17;
|FPRC;

|PROCESO Tipo0C0F0;  |TIPO 0;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |ENTLINEAL #1#17, -2, 2, 22, 0, Baja17, Alta17;
|FPRC;


|PROCESO Baja0;
     |SI FSalida = "POSICION";
         #0#0 = nHastaAnyo;
     |SINO;
         #0#0 = 2000;
     |FINSI;
|FPRC;

|PROCESO Alta0;
     #0#0 = nHastaAnyo;
|FPRC;

|PROGRAMA;
     |CLS;
     |CABEZA "Cifra Media";

     fFecha     = ~;
     aAlfa      = fFecha % -104;
     nHastaAnyo = aAlfa;

     aFichero   = ("M6" + Usuario + "zzzzzzzz") % 108;
     |ABRE #0;  |CIERRA #0;  |DELINDEX #0;

     |ABRE #0;
     |PARA nAnyo = 2000;  |SI nAnyo [ nHastaAnyo;  |HACIENDO nAnyo = nAnyo + 1;
           #0#0 = nAnyo;
           |GRABA 020#0n;
           |LIBERA #0;
     |SIGUE;
     |CIERRA #0;

     |ENTLINEAL #1#0, -1, 4, 22, 1, Baja0, Alta0;

     |DELINDEX #0;
|FPRO;
