|FICHEROS;
     isacce09 #0;
     maempr10 #200;
     isom0001 #101;

     maacceso #100;
|FIN;

|VARIABLES;
   nVentana    = 0;
   nVent       = 0;
   nError      = 0;
   nValor      = 0;
   nHandle     = 0;
   nLabel      = 0;

   aPathSociedad = "";
   aDef          = "";
   aAlfa         = "";
   aTecla        = "";
   aError        = "";
   aF2           = "";
   aOrigen       = "";
   aDestino      = "";
   aIPRemoto     = "";
   aDocRemoto    = "";
   aDocServidor  = "";
   aAbre         = "";
   aBase         = "";
   aContenido    = "";
   aTexto        = "";

   nBoton1       = 0;
   nBoton2       = 0;
   nBoton3       = 0;
   nBoton4       = 0;
   nBoton5       = 0;
   nBoton6       = 0;
   nBoton7       = 0;
   nBoton8       = 0;
   nGrid         = 0;
   nPanta        = 0;
   nRango        = 0;
   nHandleTgz    = 0;
   nTree         = 0;

   aEsc1         = "";
   aEsc2         = "";
   aRetu         = "";

     {-1}sTree      = 0;
     sT_nID         = 0;
     sT_nOper       = 0;
     sT_aData       = "";

     {-1}aVent      = "";
         aVent1     = 0;
         aVent2     = 0;
         aVent3     = 0;
         aVent4     = 0;
         aVent5     = "";

     &empresa     = 0;
     &anyo        = 0;
     &nombre      = "";
     &nif         = "";
     &tipo        = "";
     &modo        = 0;
     &sw_mod      = 0;
     &SwAdic      = 0;
     &eParametro  = "FUERA09";
     &enCodCli    = 0;
     &enSwDeDonde = 0;
     &eaEntidad   = "";
     &eaOficina   = "";
     &eaDC        = "";
     &eaCuenta    = "";
     &enModelo    = 0;
     &enBanco     = 0;
     &enForzar    = 0;
     &eaPathAcrobat = "";
     &eaModelo      = "";
     &enAnyo        = 0;
     &enEmpresa     = 0;
     &fFechaChequeo = @;
     &enError       = 0;
     &fFechaDisol   = @;
     &eaDirTrabajo   = "";
     &eaRutaOrigen   = "";
     &eaRutaDestino  = "";
     &eaFicheroTra   = "";
     &eaFicheroTxt   = "";
     &eaFicheroTgz   = "";
     &eaFicheroTar   = "";
     &eaOrigen       = "";
     &eaDestino      = "";
     &eaAlfa         = "";
     &eaPathExplorador = "";
     &eaForzExplorador = "";
     &eaImpre          = "";
     &eaJusti          = "";
     &eaUnidadBasico   = "";
     &enEjercicio      = 0;
     &enModoConsulta   = 0;
|FIN;

|PROCESO EventoGrid;
     |SI FSalida = 1;  |O FSalida = 2;
         |ESTADO_CONTROL nBoton2, "DISABLE";
         |ESTADO_CONTROL nBoton3, "DISABLE";
         |ESTADO_CONTROL nBoton4, "DISABLE";
         |ESTADO_CONTROL nBoton5, "DISABLE";
         |ESTADO_CONTROL nBoton7, "DISABLE";
         |LEE 000#0=;
         |SI FSalida = 0;
             enEmpresa = #0#0;
             enAnyo    = #0#1;
             |ESTADO_CONTROL nBoton2, "ENABLE";
             |ESTADO_CONTROL nBoton3, "ENABLE";
             |ESTADO_CONTROL nBoton4, "ENABLE";
             |ESTADO_CONTROL nBoton5, "ENABLE";

             |SI #0#6 = "S";
                 |ESTADO_CONTROL nBoton7, "ENABLE";
             |FINSI;
         |FINSI;

         |SI nLabel ! -1;
              |FIN_CONTROL_WINDOWS nLabel;
         |FINSI;

         aAlfa = "LABEL,{{2}}" + (("00000" + #0#0) % -105) + " - " + #0#2;  |QBF aAlfa;
         |SI #0#6 = "N";
             aAlfa = aAlfa + " - NO IMPRESA";
         |SINO;
             aAlfa = aAlfa + " - IMPRESA";
         |FINSI;

         |CONTROL_SIMPLE nPanta, aAlfa, 2702, 2765, NULL;
         nLabel = FSalida;
     |FINSI;
|FPRC;

|PROCESO Boton2;
     |CONFI "0000NSeguro que quiere borrar el registro del impuesto de sociedades";
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     empresa = #0#0;
     anyo    = #0#1;

     |LEE 101#0=;
     |SI FSalida = 0;
         |SUB_EJECUTA "z0000001;IMPUESTO";

         |BORRA 020#0a;
         |LIBERA #0;
     |FINSI;

     |LEE 000#0];

     |REFRESCACONTROL nGrid;
|FPRC;

|PROCESO Boton4;
     enEmpresa = #0#0;
     enAnyo    = #0#1;
     |SUB_EJECUTA "isoy0001";
|FPRC;

|PROCESO Boton3;
     |SI #0#6 = "S";
         |CONFI "0000NEl impuesto est  impreso. Quiere dejarlo como no impreso y volverlo a emitir";
         |SI FSalida ! 0; |ACABA;  |FINSI;
     |FINSI;

     eaImpre   = "N";
     eaJusti   = "";
     enEmpresa = #0#0;
     enAnyo    = #0#1;
     |SUB_EJECUTA "isoy2009";

     |LEE 101#0=;
     |SI FSalida = 0;
         #0#6 = eaImpre;
         #0#7 = eaJusti;
         |GRABA 020#0a;
         |LIBERA #0;

         |REFRESCACONTROL nGrid;
     |FINSI;
|FPRC;

|PROCESO Boton5;
     enEmpresa = #0#0;
     enAnyo    = #0#1;
     |SUB_EJECUTA "isoy2009;BORRADOR";
|FPRC;

|PROCESO Boton6;
     eaForzExplorador = "S";
     enEjercicio      = #0#1;
     |HAZ Explorador;
|FPRC;

|PROCESO Boton7;
     |SI #0#6 ! "S";  |ACABA;  |FINSI;

     |LEE 101#0=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |VENTANA 0501, 1350, -1, 16, "N£mero de justificante obtenido";
     nVent = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVent;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     aAlfa = "LABEL,{{2}}N£mero de justificante ";
     |CONTROL_SIMPLE 0, aAlfa, 0902, 0920, NULL;

     eaJusti = #0#7;
     E_inf   = "";
     E_sup   = "16";

     |PARA;  |SI;  |HACIENDO;
          eaJusti = 0922 ? 1;
          |CONFI "0000SEl n£mero introducido es correcto";
          |SI FSalida = 0;  |SAL;  |FINSI;
     |SIGUE;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVent;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;
     |FINVENTANA nVent;

     |PULSATECLA;

     #0#7 = eaJusti;
     |GRABA 020#0a;
     |LIBERA #0;

     |REFRESCACONTROL nGrid;
|FPRC;

|PROCESO Boton8;
     aAlfa = (("00000" + #0#0) % -105) + #0#1 + "99" + "200" + "00";
     aAlfa = aAlfa + #0#2;

     aAlfa = "|:dsarchi/dsarz017;" + aAlfa;
     |SUB_EJECUTA aAlfa;
|FPRC;

|PROCESO Baja100;
     #100#0 = 1;
     #100#1 = 2009;
|FPRC;

|PROCESO Alta100;
     #100#0 = 99999;
     #100#1 = 2009;
|FPRC;

|PROCESO Boton1;
     |ABRE #100;
     #100#0 = #0#0;
     #100#1 = #0#1;
     |LEE 000#100=;
     |CONSULTA_F_CLAVE #5#100, Baja100, Alta100;
     |SI FSalida ! 0;  |CIERRA #100; |ACABA;  |FINSI;
     |CIERRA #100;

     #0#0   = #100#0;
     #0#1   = #100#1;
     |LEE 000#0=;
     |SI FSalida = 0;
         |MENAV  "0000La empresa ya ha sido abierta.";
         |ACABA;
     |FINSI;

     enEmpresa = #0#0;
     aAlfa         = "01.01." + #0#1;
     fFechaChequeo = aAlfa;
     |HAZ ChequeaEmpresa;
     |SI enError = 2;
         aAlfa = "0000NEmpresa con fecha disolucion " + fFechaDisol + ". Continuar.";
         |CONFI aAlfa;
         |SI FSalida ! 0;
             |ERROR;
             |ACABA;
         |FINSI;
      |FINSI;


     |DDEFECTO #0;
     #0#0 = #100#0;
     #0#1 = #100#1;
     #0#2 = #100#2;
     #0#3 = #100#3;
     |GRABA 020#0n;
     |LIBERA #0;

     enEmpresa = #0#0;
     enAnyo    = #0#1;

     |SUB_EJECUTA "isoz0001";

     |LEE 000#0=;

     |REFRESCACONTROL nGrid;
|FPRC;

|PROCESO isom0001;
     aAlfa = "";
     |SI enModoConsulta = 1;
         aAlfa = "(MODO CONSULTA)   ";
     |FINSI;

     aAlfa =  aAlfa + "Liquidaci¢n (" + (("00000" + #0#0) % -105) + " / " + #0#1 + " " + #0#2;
     |QBF aAlfa;
     aAlfa = aAlfa + ")";

     |VENTANA 0501, 3399, -1, 16, aAlfa;
     nVentana = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVentana;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |SUB_EJECUTA "isom0001";

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVentana;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |PULSATECLA;

     |FINVENTANA nVentana;
|FPRC;

|PROCESO Ejecucion;
     enEmpresa = #0#0;
     enAnyo    = #0#1;

     |SI aContenido = "isom0001";  |HAZ isom0001;            |FINSI;
     |SI aContenido = "isom0016";  |SUB_EJECUTA "isom0016";  |FINSI;
     |SI aContenido = "isow0010";  |SUB_EJECUTA "isow0010";  |FINSI;
     |SI aContenido = "isow0006";  |SUB_EJECUTA "isow0006";  |FINSI;
     |SI aContenido = "isow0007";  |SUB_EJECUTA "isow0007";  |FINSI;
     |SI aContenido = "isow0008";  |SUB_EJECUTA "isow0008";  |FINSI;
     |SI aContenido = "isow0009";  |SUB_EJECUTA "isow0009";  |FINSI;
     |SI aContenido = "isoz0001";  |SUB_EJECUTA "isoz0001";  |FINSI;
     |SI aContenido = "isoz0002";  |SUB_EJECUTA "isoz0002;PANTALLA";  |FINSI;

     |LEE 000#isacce09.=;
|FPRC;

|PROCESO EventoTree;
     aContenido = Contenido % -108;

     |SI FEntrada = 0;
         aTexto  = Contenido;
         |ACABA;
     |FINSI;

     enModoConsulta = 0;

     |SI #0#6 = "S";
         |MENAV "0000El impuesto est  impreso. Se entrar  en MODO CONSULTA";

         enModoConsulta = 1;
     |FINSI;

     |LEE 000#isacce09.=;

     |HAZ Ejecucion;

     |REFRESCACONTROL nGrid;  |PULSATECLA;

     |FOCOTECLADO nTree;
|FPRC;

|PROCESO CargaArbol;
     eaAlfa = eaAlfa + &13 + &10;
     |XGRABA nHandle, eaAlfa;
|FPRC;

|PROCESO MontaArbol;
     |PTEC 802;  |PAUSA;

     |HAZ LeeUnidadBasico;

     |DBASE aBase;  |QBF aBase;
     aAlfa = aBase + "tmp";      |MKDIR aAlfa;

     eaRutaOrigen  = aBase + "tmp/";
     eaRutaDestino = eaUnidadBasico + "\MODELOS";            |RMKDIR eaRutaDestino;
     eaRutaDestino = eaUnidadBasico + "\MODELOS\TMP";        |RMKDIR eaRutaDestino;
     eaRutaDestino = eaUnidadBasico + "\MODELOS\TMP\";
     eaFicheroTxt  = "isacce09" + Usuario + ".txt";
     eaFicheroTgz  = "isacce09" + Usuario + ".tgz";
     eaFicheroTar  = "isacce09" + Usuario + ".tar";

     sT_nID   = nTree;
     sT_nOper = 0;

     aAbre = eaRutaOrigen + eaFicheroTxt;
     |XABRE "WB", aAbre, nHandle;

     eaAlfa = "1:Liquidaci¢n {{isom0001}}";
     |HAZ CargaArbol;

     eaAlfa = "2:Apartados no contemplados en la liquidaci¢n {{}}";
     |HAZ CargaArbol;

     eaAlfa = "2,1:Presentaci¢n de documentaci¢n previa por Registro electr¢nico {{isom0016}}";
     |HAZ CargaArbol;

     eaAlfa = "2,2:R‚gimen especial de la reserva para inversiones en Canarias {{isow0010}}";
     |HAZ CargaArbol;

     eaAlfa = "2,3:Comunicaci¢n del importe neto de la cifra de negocio {{}}";
     |HAZ CargaArbol;

     eaAlfa = "2,3,1:Grupos de sociedades {{isow0006}}";
     |HAZ CargaArbol;

     eaAlfa = "2,3,2:No residentes de un establecimiento permanente {{isow0007}}";
     |HAZ CargaArbol;

     eaAlfa = "2,3,3:Entidades de cr‚dito, aseguradoras e instituciones inversi¢n colectiva {{isow0008}}";
     |HAZ CargaArbol;

     eaAlfa = "2,4:Operaciones de fusi¢n, escisi¢n y canje de valores {{isow0009}}";
     |HAZ CargaArbol;

     eaAlfa = "3:Utilidades {{}}";
     |HAZ CargaArbol;

     eaAlfa = "3,1:Carga de datos de contabilidad y del impuesto del ejercicio anterior {{isoz0001}}";
     |HAZ CargaArbol;

     eaAlfa = "3,2:Chequeo de totales {{isoz0002}}";
     |HAZ CargaArbol;

     |XCIERRA nHandle;

     |HAZ TraeTxtComprimido;
     aAlfa = eaRutaDestino + eaFicheroTxt;

     eaAlfa = "TREECTRL, Tabla de contenidos de la empresa seleccionada{{" + aAlfa + "}}";
     |CONTROL_SIMPLE nPanta, eaAlfa, 2802, 3598, EventoTree;
     nTree = FSalida;

     eaFicheroTxt  = "isacce09" + Usuario + ".txt";
     aAlfa         = eaRutaDestino + eaFicheroTxt;
     |RFBORRA aAlfa;

     sT_nID   = nTree;
     sT_nOper = 2;
     sT_aData = "1:";

     |ESPECIFICA "COMMANDCONTROL", sTree;

     |FOCOTECLADO nTree;
|FPRC;

|PROCESO Baja0;
     #0#0 = 1;
     #0#1 = 2009;
|FPRC;

|PROCESO Alta0;
     #0#0 = 99999;
     #0#1 = 2009;
|FPRC;

|PROCESO Tipo80;   |TIPO 80;
     |ABRET #0;

     FSalida = 3499;
|FPRC;

|PROCESO Tipo90;   |TIPO 90;
     |CIERRAT #0;
|FPRC;

|PROGRAMA;
     |PINPA #0#0;
     nPanta = FSalida;

     |HAZ MontaArbol;

     |CONTROL_SIMPLE nPanta, "BOTON,{{193}}Nuevo registro", 2667, 2681, Boton1;
     nBoton1 = FSalida;

     |CONTROL_SIMPLE nPanta, "BOTON,{{207}}Borrar registro", 2684, 2698, Boton2;
     nBoton2 = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,{{205}}Borrador sin validaci¢n", 3302, 3414, Boton5;
     nBoton5 = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,{{205}}Emisi¢n impuesto", 3316, 3427, Boton3;
     nBoton3 = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,{{205}}Informe comprobaci¢n", 3329, 3441, Boton4;
     nBoton4 = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,{{197}}Justificante declaraci¢n", 3343, 3455, Boton7;
     nBoton7 = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,{{197}}Explorador predeterminado", 3357, 3471, Boton6;
     nBoton6 = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,{{197}}Consultar DSARCHI", 3373, 3483, Boton8;
     nBoton8 = FSalida;

     |ESTADO_CONTROL nBoton2, "DISABLE";
     |ESTADO_CONTROL nBoton3, "DISABLE";
     |ESTADO_CONTROL nBoton4, "DISABLE";
     |ESTADO_CONTROL nBoton5, "DISABLE";
     |ESTADO_CONTROL nBoton7, "DISABLE";
     |ESTADO_CONTROL nBoton8, "DISABLE";

     |DBASS aBase;  |QBF aBase;
     aAlfa = aBase + "dsarchi/def/dsarz017.mas";
     |XABRE "E", aAlfa, nHandle;
     |SI FSalida ] 0;
         |ESTADO_CONTROL nBoton8, "ENABLE";
     |FINSI;

     nRango = 4 + 8 + 16 + 32 + 128;

     |LINEAL_SIMPLE #1#0, nRango, 0602, 2598, Baja0, Alta0, EventoGrid;
     nGrid = FSalida;

     aEsc1  = &255 + "806";
     aEsc2  = &255 + "807";
     aRetu  = &255 + "802";
     |PARA; |SI;  |HACIENDO;
         FSalida = "::{{001}}Salir," + nGrid;
         |LEETECLA aTecla;
         |SI aTecla = aEsc1; |SAL;  |FINSI;
         |SI aTecla = aEsc2; |SAL;  |FINSI;
         |SI aTecla = aRetu; |SAL;  |FINSI;
     |SIGUE;

     |FIN_CONTROL_WINDOWS nBoton1;
     |FIN_CONTROL_WINDOWS nBoton2;
     |FIN_CONTROL_WINDOWS nBoton3;
     |FIN_CONTROL_WINDOWS nBoton4;
     |FIN_CONTROL_WINDOWS nBoton5;
     |FIN_CONTROL_WINDOWS nBoton6;
     |FIN_CONTROL_WINDOWS nBoton7;
     |FIN_CONTROL_WINDOWS nBoton8;
|FPRO;
