|FICHEROS;
     zhazeld2 #0;
     cuacceso #1;
|FIN;

|VARIABLES;
     aFichOrigen  = "";
     aFichDestin  = "";
     aFichero     = "";
     aDirAdhoc    = "";
     aDirEmpresa  = "";
     aDirDeposito = "";
     aAbre        = "";
     aBase        = "";
     aAlfa        = "";
     aAlfa1       = "";
     aOrigen      = "";
     aDestino     = "";
     aIPRemoto    = "";

     nHandle      = 0;
     nBoton1      = 0;
     nBoton2      = 0;
     nCampo       = 0;

     &eaMoneda    = "";
     &eaUnidadBasico = "";
|FIN;

|PROCESO Enabla;  |TIPO 7;
     |DBASE aBase;  |QBT aBase;
     aDirDeposito = aBase + "depositos";
     aDirEmpresa = aDirDeposito + "/" + (("00000" + #0#1) % -105) + "/" + #0#2 + "/";

     aAbre       = aDirEmpresa + "MEMORIA.TXT";
     |XABRE "E", aAbre, nHandle;
     |SI FSalida ] 0;
         |ESTADO_CONTROL nBoton1, "ENABLE";
     |FINSI;

     aAbre       = aDirEmpresa + "GESTION.TXT";
     |SI FSalida ] 0;
         |ESTADO_CONTROL nBoton2, "ENABLE";
     |FINSI;
|FPRC;

|PROCESO Browse;  |TIPO 0;
     |SI FSalida ! 10;  |ACABA;  |FINSI;

     aAlfa = "D" + #0#4;
     |BROWSE aAlfa;
     aAlfa = (aAlfa % 270) + "\";  |QBF aAlfa;
     #0#4  = aAlfa;
     |PINTA #0#4;
     |ERROR;
|FPRC;

|PROCESO MiraVarMoneda;  |TIPO 7;
     |HAZ PintaMoneda;
     #0#5 = eaMoneda;

     |PINTA #0#5;
|FPRC;

|PROCESO EnviaFichero;
     aFichOrigen = aDirEmpresa + aFichero;
     aFichDestin = aDirAdhoc   + aFichero;   |RFBORRA aFichDestin;

     |IP_REMOTO aAlfa1; |QBT aAlfa1;
     |SI aAlfa1 ! "";
         |ENVIA_FICHERO aFichOrigen, aFichDestin;
     |SINO;
         |COPIA_FICHERO aFichOrigen, aFichDestin;
     |FINSI;
|FPRC;

|PROCESO Tipo3;  |TIPO 3;
     |GRABA 020#0a;
     |LIBERA #0;

     |DBASE aBase;  |QBT aBase;
     aDirDeposito = aBase + "depositos";

     aDirEmpresa = aDirDeposito + "/" + (("00000" + #0#1) % -105) + "/" + #0#2 + "/";

     aAbre       = aDirEmpresa + "FICHERO.TXT";
     |XABRE "E", aAbre, nHandle;
     |SI FSalida < 0;
         |MENAV "       No se ha creado el Formato en este empresa.";
         |ACABA;
     |FINSI;

     |ABRE #1;
     #1#0 = #0#1;
     #1#1 = #0#2;
     |LEE 040#1=;
     |SI FSalida ! 0;  |DDEFECTO #1;  |FINSI;
     |CIERRA #1;

     |SI #1#5 = "A";
         |SI #0#5 = "PESETAS";
             aAlfa = (("00000" + #0#1) % -105) + ".ap";
         |SINO;
             aAlfa = (("00000" + #0#1) % -105) + ".ae";
         |FINSI;
     |SINO;
         |SI #0#5 = "PESETAS";
             aAlfa = (("00000" + #0#1) % -105) + ".np";
         |SINO;
             |SI #1#12 = "P";
                 aAlfa = (("00000" + #0#1) % -105) + ".me";
             |SINO;
                 aAlfa = (("00000" + #0#1) % -105) + ".ne";
             |FINSI;
         |FINSI;
     |FINSI;

     aAlfa = (("0000" + #0#2) % -104) + "_" + aAlfa;

     aDirAdhoc = #0#4;  |QBF aDirAdhoc;
     aDirAdhoc = aDirAdhoc + aAlfa;
     |QBF aDirAdhoc;
     |RMKDIR aDirAdhoc;

     aDirAdhoc = aDirAdhoc + "\"

     aFichero  = "ACCIONES.ASC";   |HAZ EnviaFichero;
     aFichero  = "ACCIONES.TXT";   |HAZ EnviaFichero;
     aFichero  = "CERTIF.ASC";     |HAZ EnviaFichero;
     aFichero  = "DATOS.ASC";      |HAZ EnviaFichero;
     aFichero  = "FICHERO.TXT";    |HAZ EnviaFichero;
     aFichero  = "GESTION.TXT";    |HAZ EnviaFichero;
     aFichero  = "INSTANCIA.ASC";  |HAZ EnviaFichero;
     aFichero  = "MEMORIA.TXT";    |HAZ EnviaFichero;
     aFichero  = "AMBIENTA.TXT";   |HAZ EnviaFichero;

     |PARA nCampo = 1;  |SI nCampo [ 99;  |HACIENDO nCampo = nCampo + 1;
           aAbre = aDirAdhoc + "MEMORIA.M" + (("00" + nCampo) % -102);
           |XABRE "EC", aAbre, nHandle;
           |SI FSalida ] 0;
               |RFBORRA aAbre;
           |SINO;
               |SAL;
           |FINSI;
     |SIGUE;

     |PARA nCampo = 1;  |SI nCampo [ 99;  |HACIENDO nCampo = nCampo + 1;
           aAbre = aDirEmpresa + "MEMORIA.M" + (("00" + nCampo) % -102);
           |XABRE "E", aAbre, nHandle;
           |SI FSalida ] 0;
               aFichero  = "MEMORIA.M" + (("00" + nCampo) % -102);
               |HAZ EnviaFichero;
           |SINO;
               |SAL;
           |FINSI;
     |SIGUE;

     |PARA nCampo = 1;  |SI nCampo [ 99;  |HACIENDO nCampo = nCampo + 1;
           aAbre = aDirAdhoc + "GESTION.G" + (("00" + nCampo) % -102);
           |XABRE "EC", aAbre, nHandle;
           |SI FSalida ] 0;
               |RFBORRA aAbre;
           |SINO;
               |SAL;
           |FINSI;
     |SIGUE;

     |PARA nCampo = 1;  |SI nCampo [ 99;  |HACIENDO nCampo = nCampo + 1;
           aAbre = aDirEmpresa + "GESTION.G" + (("00" + nCampo) % -102);
           |XABRE "E", aAbre, nHandle;
           |SI FSalida ] 0;
               aFichero  = "GESTION.G" + (("00" + nCampo) % -102);
               |HAZ EnviaFichero;
           |SINO;
               |SAL;
           |FINSI;
     |SIGUE;

     aAbre       = aDirAdhoc + "Desc.txt";
     |XABRE "CWB", aAbre, nHandle;
     |SI FSalida ] 0;
         aAlfa = #1#2;
         |SI #0#6 = "S"; aAlfa = aAlfa + &13 + &10; |FINSI;
         |SI #0#6 = "N"; aAlfa = aAlfa + &0; |FINSI;
         |XGRABA nHandle, aAlfa;
     |FINSI;
     |XCIERRA nHandle;

     aAlfa = #0#4;  |QBF aAlfa;
     aAlfa = aAlfa > aAlfa;
     aAlfa = aAlfa - "DATA\";
     aAlfa = aAlfa + "D2.EXE";
     |RSYSTEM aAlfa;
|FPRC;

|PROCESO Boton1;
     aOrigen  = eaUnidadBasico + "\MODELOS";              |RMKDIR aOrigen;
     aOrigen  = eaUnidadBasico + "\MODELOS\DOCUMENTOS";   |RMKDIR aOrigen;

     aIPRemoto = "";
     |IP_REMOTO aIPRemoto;  |QBF aIPRemoto;

     |DBASE aBase;  |QBT aBase;
     aDirDeposito = aBase + "depositos";
     aDirEmpresa  = aDirDeposito + "/" + (("00000" + #0#1) % -105) + "/" + #0#2 + "/";

     aDestino = aOrigen + "/MEMORIA.TXT";
     aOrigen  = aDirEmpresa + "MEMORIA.TXT";
     |SI aIPRemoto ! "";
         |ENVIA_FICHERO aOrigen, aDestino;
     |SINO;
         |COPIA_FICHERO aOrigen, aDestino;
     |FINSI;
     |SI FSalida = 0;
         |RSYSTEM aDestino;
         |SI aIPRemoto ! "";
             |RECIBE_FICHERO aDestino, aOrigen;
         |SINO;
             |COPIA_FICHERO aDestino, aOrigen;
         |FINSI;
         |RFBORRA aDestino;
     |FINSI;
|FPRC;

|PROCESO Boton2;
     aOrigen  = eaUnidadBasico + "\MODELOS";              |RMKDIR aOrigen;
     aOrigen  = eaUnidadBasico + "\MODELOS\DOCUMENTOS";   |RMKDIR aOrigen;

     aIPRemoto = "";
     |IP_REMOTO aIPRemoto;  |QBF aIPRemoto;

     |DBASE aBase;  |QBT aBase;
     aDirDeposito = aBase + "depositos";
     aDirEmpresa  = aDirDeposito + "/" + (("00000" + #0#1) % -105) + "/" + #0#2 + "/";

     aDestino = aOrigen + "/GESTION.TXT";
     aOrigen  = aDirEmpresa + "GESTION.TXT";
     |SI aIPRemoto ! "";
         |ENVIA_FICHERO aOrigen, aDestino;
     |SINO;
         |COPIA_FICHERO aOrigen, aDestino;
     |FINSI;
     |SI FSalida = 0;
         |RSYSTEM aDestino;
         |SI aIPRemoto ! "";
             |RECIBE_FICHERO aDestino, aOrigen;
         |SINO;
             |COPIA_FICHERO aDestino, aOrigen;
         |FINSI;
         |RFBORRA aDestino;
     |FINSI;
|FPRC;

|PROGRAMA;
     |HAZ LeeUnidadBasico;

     |CLS;
     |CABEZA "Importacion Adhoc";
     |ABRET #0;
     |LEE 101#0p;
     |SI FSalida ! 0;
         |DDEFECTO #0;
         |GRABA 020#0b;
     |FINSI;
     |LIBERA #0;
     |PDEFECTO #0, 1, 4;

     |CONTROL_SIMPLE 0, "LBOTON, Ver o Modificar Documento de la Memoria", 1820, 1860, Boton1;
     nBoton1 = FSalida;

     |CONTROL_SIMPLE 0, "LBOTON, Ver o Modificar Documento del Informe de Gestion", 1920, 1960, Boton2;
     nBoton2 = FSalida;

     |ESTADO_CONTROL nBoton1, "DISABLE";
     |ESTADO_CONTROL nBoton2, "DISABLE";

     |PINPA #0#0;
     |PINDA #0#0;
     |ENDATOS #1#0;
     |CIERRAT #0;

     |FIN_CONTROL_WINDOWS nBoton1;
     |FIN_CONTROL_WINDOWS nBoton2;
     |PULSATECLA;
|FPRO;
