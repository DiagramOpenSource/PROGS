|FICHEROS;
     zhazetiq #0;
     maacceso #1;
|FIN;

|VARIABLES;
     nAnyo     = 0;
     nSwHay    = 0;
     nHandle   = 0;
     nBoton1   = 0;
     nBoton2   = 0;
     nBoton3   = 0;
     nBoton4   = 0;
     nWord     = 0;
     nContador = 0;

     aFichero  = "";
     aAlfa     = "";
     aPathDocu = "";
     aOrigen   = "";
     aDestino  = "";
     aIPRemoto = "";
     aAbre     = "";
     aAsigna   = "";
     aIdenti   = "";

     &eaOrigen   = "";
     &eaDestino  = "";
     &eaAlfa     = "";
     &eaPath     = "";
     &eaUnidadBasico = "";
|FIN;

|PROCESO GrabaTempo;
     #0#0 = #1#0;
     #0#1 = #1#2;
     #0#2 = #1#1;
     #0#3 = "N";
     #0#4 = #1#3;
     |GRABA 020#0n;
     |LIBERA #0;

     nSwHay = 1;
|FPRC;

|DEFBUCLE maacceso;
     #1#1;
     ;
     00000, nAnyo;
     99999, nAnyo;
     ;
     NULL, GrabaTempo;
|FIN;

|RUTINA ClaveBaja0;
     #0#0 = 1;
|FRUT;

|RUTINA ClaveAlta0;
     #0#0 = 99999;
|FRUT;

|PROCESO PonActivado;
     #0#3 = aAlfa;
     |GRABA 020#0a;
     |LIBERA #0;
|FPRC;

|DEFBUCLE zhazetiqP;
     #0#1;
     ;
     00000;
     99999;
     be;
     NULL, PonActivado;
|FIN;

|PROCESO Tipo11;  |TIPO 11;
     |SI FSalida = 10;
         aAlfa = "S";
         |BUCLE zhazetiqP;
         |PONCONTROL  23, "SI";
         |PTEC 812;
     |FINSI;

     |SI FSalida = 11;
         aAlfa = "N";
         |BUCLE zhazetiqP;
         |PONCONTROL  23, "SI";
         |PTEC 812;
     |FINSI;

     |SI FSalida = 12;
         |LEE 101#0=;

         #0#3 = "S";
         |GRABA 020#0a;
         |LIBERA #0;
         |PINTA #0#3;
         |PTEC 809;
     |FINSI;

     |SI FSalida = 13;
         |LEE 101#0=;

         #0#3 = "N";
         |GRABA 020#0a;
         |LIBERA #0;
         |PINTA #0#3;
         |PTEC 809;
     |FINSI;
|FPRC;

|PROCESO Boton141;
     |PTEC 824;
|FPRC;

|PROCESO Boton142;
     |PTEC 825;
|FPRC;

|PROCESO Boton143;
     |PTEC 826;
|FPRC;

|PROCESO Boton144;
     |PTEC 827;
|FPRC;

|PROCESO PonBoton;
     |CONTROL_SIMPLE 0, "BOTON,F2 Activo Todos", 0805, 0820, Boton141;
     nBoton1 = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,F3 Desactivo Todos", 0822, 0839, Boton142;
     nBoton2 = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,F4 Activo Linea", 0841, 0856, Boton143;
     nBoton3 = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,F5 Desactivo Linea", 0858, 0876, Boton144;
     nBoton4 = FSalida;
|FPRC;

|PROCESO QuitaBoton;
     |FIN_CONTROL_WINDOWS nBoton1;
     |FIN_CONTROL_WINDOWS nBoton2;
     |FIN_CONTROL_WINDOWS nBoton3;
     |FIN_CONTROL_WINDOWS nBoton4;
|FPRC;

|PROCESO Abre;
     |DBASE aPathDocu;
     |QBF aPathDocu;
     aPathDocu = aPathDocu + "word/etiqueta/etiquet.doc";

     aAlfa = eaUnidadBasico + "/MODELOS";                   |RMKDIR aAlfa;
     aAlfa = eaUnidadBasico + "/MODELOS/DOCUMENTOS";        |RMKDIR aAlfa;

     aOrigen   = aPathDocu;
     aDestino  = eaUnidadBasico + "/MODELOS/DOCUMENTOS/etiquet.doc";

     eaOrigen  = aOrigen;
     eaDestino = aDestino;
     |HAZ EnviaFichero;

     aAbre  = aDestino;

     |MENSA "      Abriendo el Documento, espere un Momento.";

     eaAlfa = "Abro el documento " + aAbre;
     |HAZ GeneraLog;

     |WORD_ABRE nWord, aAbre;
     |WORD_PROPIEDADES nWord, "Visible", "NO";

     eaAlfa = "Asigno variables en el documento";
     |HAZ GeneraLog;

     |MENSA "      Asignando Campos, espere un Momento.";
|FPRC;

|PROCESO Cierra;
     nContador = nContador + 1;
     |PARA; |SI nContador [ 15;  |HACIENDO nContador = nContador + 1;
             aAsigna = "";
             aIdenti = "!#"+ (("00" + nContador) % -102) + "1#";
             |WORD_ASIGNA_TEXTO nWord, aIdenti, aAsigna;

             aAsigna = "";
             aIdenti = "!#"+ (("00" + nContador) % -102) + "2#";
             |WORD_ASIGNA_TEXTO nWord, aIdenti, aAsigna;

             aAsigna = "";
             aIdenti = "!#"+ (("00" + nContador) % -102) + "3#";
             |WORD_ASIGNA_TEXTO nWord, aIdenti, aAsigna;
     |SIGUE;

     |WORD_SALVA nWord, "";

     eaAlfa = "Salvo el documento " + aDestino;
     |HAZ GeneraLog;

     eaPath = eaUnidadBasico + "/MODELOS/DOCUMENTOS/";
     |HAZ TraeDsPrintWord;

     eaAlfa = "Imprimo el documento " + aDestino;
     |HAZ GeneraLog;

     aAlfa        = eaUnidadBasico + "\MODELOS\DOCUMENTOS\wait dsprintword " + aAbre + " 0";
     |RSYSTEM aAlfa;

     |RFBORRA aAbre;

     eaAlfa = "Borro el documento " + aAbre;
     |HAZ GeneraLog;
|FPRC;

|PROCESO CargaEtiqueta;
     nContador = nContador + 1;
     |SI nContador = 1;
         |HAZ Abre;

         aAsigna = #0#2;
         aIdenti = "!#0#";
         |PARA;  |SI FSalida = 0;  |HACIENDO;
              |WORD_ASIGNA_TEXTO nWord, aIdenti, aAsigna;
         |SIGUE;
     |FINSI;

     aAsigna = ("00000" + #0#0) % -105;
     aIdenti = "!#"+ (("00" + nContador) % -102) + "1#";
     |WORD_ASIGNA_TEXTO nWord, aIdenti, aAsigna;

     aAsigna = #0#4;
     aIdenti = "!#"+ (("00" + nContador) % -102) + "2#";
     |WORD_ASIGNA_TEXTO nWord, aIdenti, aAsigna;

     aAsigna = #0#1;
     aIdenti = "!#"+ (("00" + nContador) % -102) + "3#";
     |WORD_ASIGNA_TEXTO nWord, aIdenti, aAsigna;

     |SI nContador = 15;
         |HAZ Cierra;
         nContador = 0;
     |FINSI;
|FPRC;

|DEFBUCLE zhazetiq;
     #0#1;
     3;
     00001, "S";
     99999, "S";
     ;
     NULL, CargaEtiqueta;
|FIN;

|PROGRAMA;
     |HAZ LeeUnidadBasico;

     |DBASE aAlfa;  |QBF aAlfa;
     aAlfa = aAlfa + "word/etiqueta/etiquet.doc";
     |XABRE "E", aAlfa, nHandle;
     |SI FSalida < 0;
         |MENAV "     No tiene instalado las Etiquetas en word.";
         |ACABA;
     |FINSI;

     |CLS;
     |CABEZA "Emision de Etiquetas";
     |PINPA #0#0;
     E_inf = "1900";
     E_sup = "2999";
     nAnyo = 0609 ? 1;

     aFichero = ("eti" + Usuario + "zzzzzzzz") % 108;
     |NOME_DAT #0 aFichero;
     |ABRE #0;  |CIERRA #0;  |DELINDEX #0;

     |ABRE #0;
     |BUCLE maacceso;
     |CIERRA #0;

     |SI nSwHay = 0;
         |MENAV "      Seleccion Vacia ... ";
         |ABRE #0;  |CIERRA #0;  |DELINDEX #0;
         |ACABA;
     |FINSI;

     |HAZ PonBoton;
     |ENTLINEAL #1#0, -1, 4, 22, 0, ClaveBaja0, ClaveAlta0;
     |HAZ QuitaBoton;

     nContador = 0;
     |BUCLE zhazetiq;
     |SI nContador < 15;  |Y nContador > 0;
         |HAZ Cierra;
     |FINSI;
|FPRO;
