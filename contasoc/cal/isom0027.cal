|FICHEROS;
     isom0000;
     isom0001;
     isom0006;
     isom0027;
     isom0516;

     maempr10;
|FIN;

|VARIABLES;
      nRango         = 0;
      nBotonSal      = 0;
      nBotonAnota    = -1;
      nBotonPorce    = -1;
      nTree          = 0;
      nHandle        = 0;
      nPanta         = 0;
      nBotonLabel    = 0;
      nVentana       = 0;
      nVent          = 0;
      nGrid          = 0;
      nModo          = 0;
      nLinea         = 0;
      nBase          = 0;
      nBaseApli      = 0;
      nBase1         = 0;
      nCasilla552    = 0;
      nPorce         = 0;
      nOk            = 0;

      aContenido     = "";
      aTexto         = "";
      aBase          = "";
      aAlfa          = "";
      aAbre          = "";
      aEsc1          = "";
      aEsc2          = "";
      aRetu          = "";
      aTecla         = "";
      aCas           = "";

     {-1}sTree       = 0;
     sT_nID          = 0;
     sT_nOper        = 0;
     sT_aData        = "";

     {-1}aVent       = "";
         aVent1      = 0;
         aVent2      = 0;
         aVent3      = 0;
         aVent4      = 0;
         aVent5      = "";

     &enEmpresa      = 0;
     &enAnyo         = 0;
     &eaCasilla      = "";
     &eaTitCasilla   = "";
     &eaRutaOrigen   = "";
     &eaRutaDestino  = "";
     &eaFicheroTra   = "";
     &eaFicheroTxt   = "";
     &eaFicheroTgz   = "";
     &eaFicheroTar   = "";
     &eaOrigen       = "";
     &eaDestino      = "";
     &eaAlfa         = "";
     &eaTipoColor    = "";
     &eaColor        = "";
     &enModoConsulta = 0;

     nDLinea   = 0;
     nHLinea   = 0;
     nLineaAny = 0;
     nLineaTot = 0;
|FIN;

|PROCESO Tipo12;  |TIPO 12;
|FPRC;

|PROCESO Tipo1;  |TIPO 1;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo = 1;  |O nModo = 3;
         |ERROR;
     |FINSI;
|FPRC;

|PROCESO Tipo7C7m27;  |TIPO 7;
     |SI #isom0027#2 > nLineaTot;  |ACABA;  |FINSI;

     |SI #isom0027#2 = nLineaAny;
         |SI #isom0027#5 > nBase;
             #isom0027#5 = nBase;
         |FINSI;
     |FINSI;

     |SI #isom0027#7 > #isom0027#5;
         #isom0027#7 = #isom0027#5;
     |FINSI;

     |PINTA #isom0027#5;
     |PINTA #isom0027#7;
|FPRC;

|PROCESO Tipo0C7m27;  |TIPO 0;
     |SI #isom0027#2 > nLineaTot;  |ACABA;  |FINSI;

     nOk = 0;
     |SI #isom0027#6 = "1142 ";  nOk = 1;  |FINSI;
     |SI #isom0027#6 = "1145 ";  nOk = 1;  |FINSI;
     |SI #isom0027#6 = "1456 ";  nOk = 1;  |FINSI;
     |SI #isom0027#6 = "1730 ";  nOk = 1;  |FINSI;

     |SI nOk = 1;
         |SI #isom0027#7 > nBaseApli;
             #isom0027#7 = nBaseApli;

             |MENAV "0000 Lo aplicado supera a la casilla 552 en negativa";
             |ERROR;
         |FINSI;
     |FINSI;

     |SI #isom0027#2 = nLineaAny;
         |SI #isom0027#5 > nBase;
             |MENAV "0000 Lo aplicado supera el 10% de la casilla 552 (con el limite de un 1.000.000)";
             |ERROR;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO Totalm27;
     #isom0027#9 = 0;

     |SI #isom0027#7 > #isom0027#5;  #isom0027#7 = #isom0027#5;   |FINSI;

     |SI #isom0027#8 ! "     ";
         #isom0027#9 = #isom0027#5 - #isom0027#7;
     |FINSI;

     |PINTA #isom0027#7;
     |PINTA #isom0027#9;
|FPRC;

|PROCESO Tipo2;  |TIPO 2;
     |HAZ Tipo1;

     |SI #isom0027#2 > nLineaTot;  |ACABA;  |FINSI;

     nOk = 0;
     |SI #isom0027#6 = "1142 ";  nOk = 1;  |FINSI;
     |SI #isom0027#6 = "1145 ";  nOk = 1;  |FINSI;
     |SI #isom0027#6 = "1456 ";  nOk = 1;  |FINSI;
     |SI #isom0027#6 = "1730 ";  nOk = 1;  |FINSI;

     |SI nOk = 1;
         nBaseApli = (nBaseApli -. #isom0027#7) ! 2;
     |FINSI;

     |SI #isom0027#2 = nLineaAny;
         nBase = (nBase -. #isom0027#5) ! 2;
     |FINSI;
|FPRC;

|PROCESO Tipo3;  |TIPO 3;
     #isom0516#0 = #isom0027#1;
     #isom0516#1 = #isom0027#2;
     |LEE 000#isom0516.=;
     |SI FSalida ! 0;  |DDEFECTO #isom0516;  |FINSI;

     |SI #isom0516#7 ! "I";  |ACABA;  |FINSI;

     |GRABA 020#isom0027.a;
     |LIBERA #isom0027;

     nLinea = #isom0027#2;

     |HAZ TotalFich127;

     #isom0027#0 = #maempr10#0;
     #isom0027#1 = #maempr10#1;
     #isom0027#2 = nLinea;
     |LEE 101#isom0027.=;

     aAlfa = "|IDGRID";
     aAlfa = #isom0027#aAlfa#;
     nGrid = aAlfa;
     |REFRESCACONTROL nGrid;
|FPRC;

|PROCESO Tipo0;  |TIPO 0;
     |C_M #isom0027#5,  1, "N";
     |C_M #isom0027#7,  1, "N";
     |C_M #isom0027#9,  1, "N";
     |C_M #isom0027#11, 1, "N";

     #isom0516#0 = #isom0027#1;
     #isom0516#1 = #isom0027#2;
     |LEE 000#isom0516.=;
     |SI FSalida ! 0;  |DDEFECTO #isom0516;  |FINSI;

     |SI #isom0516#7 ! "I";  |ACABA;  |FINSI;

     |SI #isom0027#4  ! "     ";  |C_M #isom0027#5,   1, "S";  |FINSI;
     |SI #isom0027#6  ! "     ";  |C_M #isom0027#7,   1, "S";  |FINSI;
     |SI #isom0027#8  ! "     ";  |C_M #isom0027#9,   1, "S";  |FINSI;
     |SI #isom0027#10 ! "     ";  |C_M #isom0027#11,  1, "S";  |FINSI;

     |SI #isom0027#2 > nLineaTot;
         |C_M #isom0027#5,  1, "N";
     |FINSI;
|FPRC;

|PROCESO Evento127;
     |SI FSalida = 1;  |O FSalida = 2;
         |LEE 000#isom0027.=;
     |FINSI;
|FPRC;

|PROCESO isom0516;
     #isom0027#0  = enEmpresa;
     #isom0027#1  = #isom0516#0;
     #isom0027#2  = #isom0516#1;
     |LEE 101#isom0027.=;
     |SI FSalida ! 0;
         |DDEFECTO #isom0027;
         #isom0027#0  = enEmpresa;
         #isom0027#1  = #isom0516#0;
         #isom0027#2  = #isom0516#1;
         |GRABA 020#isom0027.b;
     |FINSI;

     #isom0027#3  = #isom0516#2;
     #isom0027#4  = #isom0516#3;
     #isom0027#6  = #isom0516#4;
     #isom0027#8  = #isom0516#5;
     #isom0027#10 = #isom0516#6;

     eaTipoColor  = #isom0516#8;
     |HAZ CogeColor;
     #isom0027#12 = eaColor;

     |GRABA 020#isom0027.a;
     |LIBERA #isom0027;
|FPRC;

|DEFBUCLE isom0516;
     #isom0516#1;
     ;
     enAnyo, nDLinea;
     enAnyo, nHLinea;      || Del/hasta  es el isom0027
     ;
     NULL, isom0516;
|FIN;

|PROCESO MontaEstructura516;
     |SI enAnyo = 2015;
         nDLinea   = 0045;
         nHLinea   = 0085;
         nLineaAny = 55;
         nLineaTot = 60;
     |FINSI;

     |SI enAnyo = 2016;
         nDLinea   = 0045;
         nHLinea   = 0105;
         nLineaAny = 65;
         nLineaTot = 70;
     |FINSI;

     |SI enAnyo = 2017;
         nDLinea   = 0055;
         nHLinea   = 0115;
         nLineaAny = 75;
         nLineaTot = 80;
     |FINSI;

     |SI enAnyo = 2018;
         nDLinea   = 0055;
         nHLinea   = 0125;
         nLineaAny = 80;
         nLineaTot = 85;
     |FINSI;

     |SI enAnyo = 2019;
         nDLinea   = 0055;
         nHLinea   = 0135;
         nLineaAny = 85;
         nLineaTot = 90;
     |FINSI;

     |BUCLE isom0516;
|FPRC;

|PROCESO BotonSalir;
     |PTEC 807;
|FPRC;

|PROCESO BotonAnota;
     eaCasilla    = #isom0027#4;     |QBF eaCasilla;
     eaTitCasilla = #isom0027#3;     |QBF eaTitCasilla;

     |SUB_EJECUTA "isom0499";

     aAlfa = "|IDGRID";
     aAlfa = #isom0027#aAlfa#;
     nGrid = aAlfa;
     |FOCOTECLADO nGrid;
|FPRC;

|PROCESO Baja127;
     #isom0027#0  = enEmpresa;
     #isom0027#1  = enAnyo;
     #isom0027#2  = 1;
|FPRC;

|PROCESO Alta127;
     #isom0027#0  = enEmpresa;
     #isom0027#1  = enAnyo;
     #isom0027#2  = 9999;
|FPRC;

|PROCESO isom0027R;
     |SI #isom0027#5  = 0;  |ACABA;  |FINSI;

     |SI nBase = 0;
         #isom0027#5  = 0;
         #isom0027#7  = 0;
         #isom0027#9  = 0;

         |GRABA 020#isom0027.a;
         |LIBERA #isom0027;

         |ACABA;
     |FINSI;

     nBase1 = nBase1 + #isom0027#5;

     |SI nBase1 > nBase;
         nBase1 = nBase1 - #isom0027#5;
         #isom0027#5 = nBase - nBase1;
         nBase1 = nBase1 + #isom0027#5;

         |SI #isom0027#7 > #isom0027#5;
             #isom0027#7 = #isom0027#5;
         |FINSI;

         #isom0027#9 = 0;

         |SI #isom0027#8 ! "     ";
             #isom0027#9 = #isom0027#5 - #isom0027#7;
         |FINSI;

         |GRABA 020#isom0027.a;
         |LIBERA #isom0027;
     |FINSI;
|FPRC;

|DEFBUCLE isom0027R;
     #isom0027#1;
     ;
     enEmpresa, enAnyo, nLineaAny;
     enEmpresa, enAnyo, nLineaAny;
     be;
     NULL, isom0027R;
|FIN;

|PROCESO RecogeBase;
     nBase = 0;

     |ABRE #isom0001;
     |SELECT #2#isom0001;
     #isom0001#0 = #maempr10#0;
     #isom0001#1 = #maempr10#1;
     #isom0001#3 = "552";
     |LEE 000#isom0001.=;
     |SI FSalida = 0;
         nBase     = #isom0001#5;
         nBaseApli = -#isom0001#5;
     |FINSI;

     |SI nBaseApli < 0;
         nBaseApli = 0;
     |FINSI;

     |SELECT #1#isom0001;
     |CIERRA #isom0001;

     nBase = (nBase % 10) ! 2;
     |SI nBase > 1000000; nBase = 1000000;  |FINSI;
|FPRC;

|PROCESO TopeCompensa;
     |ABRE #isom0000;
     #isom0000#0 = enEmpresa;
     #isom0000#1 = enAnyo;
     |LEE 000#isom0000.=;
     |SI FSalida ! 0;  |DDEFECTO #isom0000;  |FINSI;
     |CIERRA #isom0000;

     |HAZ RecogeBase;
     nCasilla552 = nBase;

     aCas = "1455";
     |SI enAnyo ] 2017;  aCas = "1034";  |FINSI;

     |SELECT #2#isom0027;
     #isom0027#0 = enEmpresa;
     #isom0027#1 = enAnyo;
     #isom0027#4 = aCas;
     |LEE 000#isom0027.=;
     |SI FSalida = 0;
         nBase     = (nBase - #isom0027#5) ! 2;
     |FINSI;
     |LIBERA #isom0027;
     |SELECT #1#isom0027;

     |SI nBase < 0;  |O nCasilla552 [ 0;
         |HAZ RecogeBase;
         |SI nBase < 0;  nBase = 0;  |FINSI;

         nBase1 = 0;  |BUCLE isom0027R;
         |HAZ TotalFich127;

         nBase = 0;
     |FINSI;

     |SELECT #2#isom0027;
     #isom0027#0 = enEmpresa;
     #isom0027#1 = enAnyo;
     #isom0027#4 = "1147";
     |LEE 000#isom0027.=;
     |SI FSalida = 0;
         nBaseApli = (nBaseApli - #isom0027#7) ! 2;
     |FINSI;
|FPRC;

|PROCESO isom0027;
     |SI #isom0027#5  ! 0;  |ACABA;  |FINSI;
     |SI #isom0027#7  ! 0;  |ACABA;  |FINSI;
     |SI #isom0027#9  ! 0;  |ACABA;  |FINSI;
     |SI #isom0027#11 ! 0;  |ACABA;  |FINSI;

     |BORRA 020#isom0027.a;
     |LIBERA #isom0027;
|FPRC;

|DEFBUCLE isom0027;
     #isom0027#1;
     ;
     enEmpresa, enAnyo, 0000;
     enEmpresa, enAnyo, 9999;
     be;
     NULL, isom0027;
|FIN;

|PROGRAMA;
     |ABRE #maempr10;
     #maempr10#0 = enEmpresa;
     #maempr10#1 = enAnyo;
     |LEE 000#maempr10.=;
     |SI FSalida ! 0;  |DDEFECTO #maempr10;  |FINSI;
     |CIERRA #maempr10;

     |HAZ MontaEstructura516;

     |HAZ TopeCompensa;

     #isom0027#0 = enEmpresa;
     #isom0027#1 = enAnyo;
     #isom0027#2 = 5;
     |LEE 000#isom0027.];

     |PINPA #0#isom0027;

     |CONTROL_SIMPLE 0, "BOTON,{{197}} sali&R", 3488, 3498, BotonSalir;
     nBotonSal = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,{{197}} a&Notaciones de la compesaci¢n", 3302, 3425, BotonAnota;
     nBotonAnota = FSalida;

     |SI enModoConsulta = 0;
         nRango = 1 + 2 + 4 + 8 + 16 + 128 + 256;
     |SINO;
         nRango = 4 + 8 + 16 + 128 + 256;
     |FINSI;

     |LINEAL_SIMPLE #1#isom0027, nRango, 0502, 3498, Baja127, Alta127, Evento127;

     |FIN_CONTROL_WINDOWS nBotonSal;
     |FIN_CONTROL_WINDOWS nBotonAnota;

     |BUCLE isom0027;

     |PULSATECLA;
|FPRO;

|PROCESO Tipo80m27;  |TIPO 80;
     |ABRET #isom0027;
     |ABRET #isom0516;

     FSalida = 3499;
|FPRC;

|PROCESO Tipo90m27;  |TIPO 90;
     |CIERRAT #isom0027;
     |CIERRAT #isom0516;

     |SELECT #1#isom0001;
|FPRC;
