|FICHEROS;
  soagico1 #0;
  sodacoca #1;
  sodacoli #2;
  soplanca #5;
  soplanli #6;
  maacceso #7;
  sodacuen #8;
  soda8m00 #800;
  soda8m01 #801;
  soda8m11 #811;
  soda8m12 #812;

  :/contagen/def/canempre #3;
  :/contagen/def/cacuenta #4;
  :/contagen/def/camoneda #100;
  :/contagen/def/ca8ctrle #400;
  :/contagen/def/ca8m0003 #403;
  :/contagen/def/ca8m0011 #411;
  :/contagen/def/ca8m0012 #412;
|FIN;

|VARIABLES;
  aAlfa1   = "";
  Cuen2008 = "";
  Ejercicio = 0;
  linea = 0;
  longi = 0;
  d_digi = 0;
  h_digi = 0;
  d_cuen = "";
  h_cuen = "";
  d_pase = "";
  h_pase = "";
  path = "";
  empresa = "";
  anyo = "";
  vari = "";
  aste = "";
  long = "";
  numero = "";
  dig1 = 0;
  dig3 = 0;
  dig4 = 0;
  dig5 = 0;
  dig6 = 0;
  dig7 = 0;
  dig8 = 0;
  dig9 = 0;
  activo = 0;
  pasivo = 0;
  beneficio = 0;
  CampoNivel = 0;
  Posicion   = 0;
  HayDiario  = 0;
  Linea      = 0;
  Cuenta     = "";
  EjeAnt     = 0;
  SaldoFinal = 0;
  Primer     = "";
  QueEs      = 0;
  LineaAnt   = 0;
  nCanti     = 0;
  nCanti1    = 0;
  nCanti2    = 0;
  nCanti3    = 0;
  aPregunta  = "";

  swchequeo  = 0;
  para1      = 0;
  para2      = 0;
  &sibloq    = "";
  &empre     = 0;
  &perio     = 0;
  &plani     = 0;
  &siono     = "";
  &cuent     = "";
  &empAlta   = 0;
  &acabalo   = 0;
  &eaPathContagen = "";
  &enNoPanta      = 0;
  &EURODB         = 0;
  &eaMonConta     = "";
  &nivel = 0;
  &enPlan  = 0;
  nLinea = 0;
  &enEjerS = 0;
  &eaPlanillaS = "";
  nSw = 0;
  nError     = 0;
  nNume1     = 0;

   &aTitulo  = "";
   &aAPR     = "";
   &aCuenta  = "";
|FIN;

|INCLUYE varbal_i;

|| ************************ CALCULOS DE PANTALLA *************************
|PROCESO inicio;
  empresa = #0#0;
  empresa = ("00000" + empresa) % -105;

  anyo    = #0#1;
  anyo    = "0" + anyo;
  anyo    = anyo % -101;
  empresa = empresa + anyo;
  nError  = 0;
  |ABRE #3;
  #3#2 = #0#0;
  #3#3 = #0#1;
  |LEE 040#3=;
  |SI FSalida ! 0;
      |CIERRA #3;
      |SI enNoPanta = 0; |MENAV "      No existe Empresa Contable"; |FINSI;
      nError = 1;
      |ACABA;
  |FINSI;
  |CIERRA #3;

  |ABRE #400;
  |DDEFECTO #400;
  #400#0 = #0#0;
  #400#1 = #0#1;
  |LEE 040#400=;
  |CIERRA #400;
  eNum2008 = #400#2;

  |ABRE #100;
  #100#0 = #3#2;
  #100#1 = #3#3;
  |LEE 040#100=;
  |SI FSalida ! 0;  |DDEFECTO #100;  |FINSI;
  |CIERRA #100;
  eaMonConta = #100#2;

  |SI #3#26 < 80; Ejercicio = 2000 + #3#26; |SINO;  Ejercicio = 1900 + #3#26; |FINSI;

  |ABRE #7;
  #7#0 = empAlta;
  #7#1 = Ejercicio;
  |LEE 040#7=;
  |SI FSalida ! 0;
      |CIERRA #7;
      |SI enNoPanta = 0; |MENAV "     No existe Cliente."; |FINSI;
      nError = 1;
      |ACABA;
  |FINSI;
  |CIERRA #7;

  |HAZ BusquedaApli;
  |SI eaPathContagen = "";
      |SI enNoPanta = 0; |MENAV "      No existe Aplicacion de Contabilidad"; |FINSI;
      nError = 1;
      |ACABA;
  |FINSI;

  path = eaPathContagen + "fich/" + empresa +  "/";
  |PATH_DAT #4path;
  |PATH_DAT #403path;
  |PATH_DAT #411path;
  |PATH_DAT #412path;

  dig1 = #3#11;
  dig3 = #3#13;
  dig4 = #3#14;
  dig5 = #3#15;
  dig6 = #3#16;
  dig7 = #3#17;
  dig8 = #3#18;
  dig9 = #3#19;

  |SI cuent = "";  cuent = "129000000000";  |FINSI;
  CampoNivel = 13 + dig3;
  Posicion   = 100 + #3CampoNivel;
  #0#5       = cuent % Posicion;
  |SI enNoPanta = 0;  |PINTA #0#5;  |FINSI;
  #0#6 = dig3;
  |SI enNoPanta = 0;  |PINTA #0#6; |FINSI;
|FPRC;

|PROCESO planilla;
  |ABRE #5;
  #5#0 = #0#2;
  |LEE 040#5=;
  |SI FSalida ! 0;
      |SI enNoPanta = 0; |MENAV "      No existe Planilla."; |FINSI;
      nError = 1;
      |ERROR;
  |SINO;
      #0#3 = #5#1;
      |SI enNoPanta = 0;  |PINTA #0#3;  |FINSI;
  |FINSI;
  |CIERRA #5;
|FPRC;

|| **************************************************************************
|| *********************  CALCULO PASES *************************************
|| **************************************************************************
|PROCESO BorraTabla12;
  |BORRA 020#812a;
  |LIBERA #812;
|FPRC;

|DEFBUCLE BorraTabla12;
  #812#1;
  ;
  #0#0, Ejercicio, "            ", 01;
  #0#0, Ejercicio, "zzzzzzzzzzzz", 99;
  be;
  NULL, BorraTabla12;
|FIN;

|PROCESO BorraTabla11;
  |BORRA 020#811a;
  |LIBERA #811;
|FPRC;

|DEFBUCLE BorraTabla11;
  #811#1;
  ;
  #0#0, Ejercicio, "            ";
  #0#0, Ejercicio, "zzzzzzzzzzzz";
  be;
  NULL, BorraTabla11;
|FIN;

|PROCESO BorraTabla;
  |BORRA 020#801a;
  |LIBERA #801;
|FPRC;

|DEFBUCLE BorraTabla;
  #801#1;
  ;
  #0#0, 0001;
  #0#0, 9999;
  be;
  NULL, BorraTabla;
|FIN;

|PROCESO PasarTabla11;
  |DDEFECTO #811;
  #811#0 = #411#0;
  #811#9  = #0#0;
  #811#10 = Ejercicio;
  #811#1 = #411#1;
  #811#2 = #411#2;
  #811#3 = #411#3;
  #811#4 = #411#4;
  #811#5 = #411#5;
  #811#6 = #411#6;
  #811#7 = #411#7;
  #811#8 = #411#8;
  #811#11 = eNum2008;
  |GRABA 040#811n;
|FPRC;

|DEFBUCLE PasarTabla11;
  #411#1;
  ;
  "            ";
  "zzzzzzzzzzzz";
  e;
  NULL, PasarTabla11;
|FIN;

|PROCESO PasarTabla12;
  |DDEFECTO #812;
  #812#0 = #412#0;
  #812#7 = #0#0;
  #812#8 = Ejercicio;
  #812#1 = #412#1;
  #812#2 = #412#2;
  #812#3 = #412#3;
  #812#4 = #412#4;
  #812#5 = #412#5;
  #812#6 = #412#6;
  |GRABA 040#812n;
|FPRC;

|DEFBUCLE PasarTabla12;
  #412#1;
  ;
  "            ", 001;
  "zzzzzzzzzzzz", 999;
  e;
  NULL, PasarTabla12;
|FIN;

|PROCESO PasarTabla;
 |SI #403#0 ! #403#2;
     #801#0 = #0#0;
     #801#1 = nLinea;
     |LEE 001#801=;
     |SI FSalida ! 0;
          |DDEFECTO #801;
          #801#0 = #0#0;
          #801#1 = nLinea;
          #801#2 = #403#0;
          #801#3 = #403#1;
          #801#4 = #403#2;
          #801#5 = #403#3;
          |GRABA 020#801n;
     |FINSI;
     nLinea = nLinea + 1;
 |FINSI;
|FPRC;

|DEFBUCLE PasarTabla;
  #403#1;
  ;
  "            ";
  "zzzzzzzzzzzz";
  e;
  NULL, PasarTabla;
|FIN;

|PROCESO CambiaCanti;
     |SI #100#2 = "P";
         |SI EURODB ! 0;
             nCanti = (nCanti / 166.386) ! 2;
         |FINSI;
     |SINO;
         |SI EURODB = 0;
             nCanti = nCanti * 166.386;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO borralin;
  |BORRA 020#2c;
|FPRC;

|PROCESO grabar;
   #1#0 = empAlta;
   #1#1 = Ejercicio;
   |LIBERA #1;
   |LEE 101#1=;
   |SI FSalida ! 0;
       |DDEFECTO #1;
       #1#0 = empAlta;
       #1#1 = Ejercicio;
       #1#2 = #7#2;
       #1#3 = 0;
       #1#4 = 0;
       #1#5 = 0;
       #1#6 = 0;
       #1#7 = 0;
       #1#8 = eNum2008;
       |GRABA 020#1c;
       |LIBERA #1;
       linea = 0;
   |FINSI;

   aCuenta = #4#0;
   |HAZ MSigno08_s;

   #2#0 = #1#0;
   #2#1 = Ejercicio;
   #2#3 = #4#0;
   |LIBERA #2;
   |LEE 110#2=;
   |SI FSalida ! 0;

       |SI enNoPanta = 0;
           |ATRI I;
           |PINTA 1856, #4#1;
           |PINTA 1833, #4#0;
           |ATRI 0;
       |FINSI;

       |DDEFECTO #2;

       |SI #4#0 ! #0#5;
           || ........ |SI #4#51 ! 0; |O #4#9 ! 0; |O #4#52 ! 0; |O #4#10 ! 0;
           |HAZ chequeoMov;
           |SI swchequeo = 1;
             linea  = linea + 1;
             #2#0   = #1#0;
             #2#1   = Ejercicio;
             #2#2   = linea;
             #2#3   = #4#0;
             #2#4   = #4#2;
             nCanti = #4#11;                 |HAZ CambiaCanti;  #2#9 = nCanti;
             #2#5   = nCanti;
             nCanti = #4#51 - #4#48 - #4#9;  |HAZ CambiaCanti;  #2#6 = nCanti;
             nCanti = #4#52 - #4#49 - #4#10; |HAZ CambiaCanti;  #2#7 = nCanti;
             #2#8   = #2#5  + #2#6  - #2#7;
             |SI HayDiario = 1;
                 #2#9 = 0;
                 #2#5 = 0;
                 #2#6 = 0;
                 #2#7 = 0;
             |FINSI;
             |GRABA 020#2n;

             #1#3 = #1#3 + #2#5;
             #1#4 = #1#4 + #2#6;
             #1#5 = #1#5 + #2#7;
             #1#6 = #1#6 + #2#8;
             #1#7 = #1#7 + #2#9;
             |GRABA 020#1a;

             || #8#2 = enPlan;  || No grabar sodacuen tique 2023 _ 1891
             || #8#0 = #2#3;
             || |LEE 101#8=;
             || |SI FSalida ! 0;
             ||    #8#0 = #2#3;
             ||    #8#1 = #2#4;
             ||    #8#2 = enPlan;
             ||   |GRABA 020#8n;
             || |FINSI;
             || |LIBERA #8;
           |FINSI;

           |SI Ejercicio < 2008;
               |SI #4#4 = 0;  |Y #4#5 = "A"; |Y #4#3 = "S";
                   activo = activo + #2#8;
               |FINSI;
               |SI #4#4 = 0; |Y #4#5 = "P"; |Y #4#3 = "S";
                   pasivo = pasivo + #2#8;
               |FINSI;
           |SINO;
               |SI aAPR = "A"; |Y #4#3 = "S";
                   activo = activo + #2#8;
               |FINSI;
               |SI aAPR = "I"; |Y #4#3 = "S";
                   activo = activo + #2#8;
               |FINSI;
               |SI aAPR = "P"; |Y #4#3 = "S";
                   pasivo = pasivo + #2#8;
               |FINSI;
               |SI aAPR = "G"; |Y #4#3 = "S";
                   pasivo = pasivo + #2#8;
               |FINSI;
           |FINSI;
       |FINSI;
   |FINSI;
   |LIBERA #4;
|FPRC;

|PROCESO chequeoMov;
 swchequeo = 0;
 |PARA para1 = 9; |SI para1 [ 45; |HACIENDO para1 = para1 + 3;
       para2 = para1 + 1;
       |SI #4para1 ! 0; |O #4para2 ! 0;
           swchequeo = 1;
           |SAL;
       |FINSI;
 |SIGUE;
|FPRC;

|PROCESO grabar1;
   #1#0 = empAlta;
   #1#1 = Ejercicio;
   |LIBERA #1;
   |LEE 101#1=;
   |SI FSalida ! 0;
       |DDEFECTO #1;
       #1#0 = empAlta;
       #1#1 = Ejercicio;
       #1#2 = #3#1;
       #1#3 = 0;
       #1#4 = 0;
       #1#5 = 0;
       #1#6 = 0;
       #1#7 = 0;
       #1#8 = eNum2008;
       |GRABA 020#1c;
       |LIBERA #1;
       linea = 0;
   |FINSI;

   #2#0 = #1#0;
   #2#1 = Ejercicio;
   #2#3 = #4#0;
   |LIBERA #2;
   |LEE 110#2=;
   |SI FSalida ! 0;
       linea = linea + 1;

       |SI enNoPanta = 0;
           |ATRI I;
           |PINTA 1856, #4#1;
           |PINTA 1833, #4#0;
           |ATRI 0;
       |FINSI;

       |DDEFECTO #2;
       beneficio = activo + pasivo;

       |SI beneficio > 0;
           nCanti = #4#52; |HAZ CambiaCanti;  nCanti1 = nCanti;
           nCanti = #4#49; |HAZ CambiaCanti;  nCanti2 = nCanti;
           nCanti = #4#10; |HAZ CambiaCanti;  nCanti3 = nCanti;
           #2#7   = nCanti1 - (nCanti2 - beneficio) - nCanti3;
           nCanti = #4#51 - #4#48 - #4#9;                 |HAZ CambiaCanti;
           #2#6   = nCanti;
       |SINO;
           nCanti = #4#51; |HAZ CambiaCanti;  nCanti1 = nCanti;
           nCanti = #4#48; |HAZ CambiaCanti;  nCanti2 = nCanti;
           nCanti = #4#9;  |HAZ CambiaCanti;  nCanti3 = nCanti;
           #2#6   = nCanti1 - (nCanti2 + beneficio) - nCanti3;
           nCanti = #4#52 - #4#49 - #4#10;                |HAZ CambiaCanti;
           #2#7   = nCanti;
       |FINSI;

       #2#0 = #1#0;
       #2#1 = Ejercicio;
       #2#2 = linea;
       #2#3 = #4#0;
       #2#4 = #4#2;
       nCanti = #4#11;  |HAZ CambiaCanti;  #2#5 = nCanti;
       #2#8   = #2#5 + #2#6 - #2#7;
       nCanti = #4#11;  |HAZ CambiaCanti;  #2#9 = nCanti;

       |SI HayDiario = 1;
           #2#9 = 0;
           #2#5 = 0;
           #2#6 = 0;
           #2#7 = 0;
       |FINSI;
       |GRABA 020#2n;

       ||   #1#3 = #1#3 + #2#5;
       #1#4 = #1#4 + #2#6;
       #1#5 = #1#5 + #2#7;
       #1#6 = #1#6 + #2#8;
       |GRABA 020#1a;
   |FINSI;
|FPRC;

|DEFBUCLE soagicon1;
   #4#1;
   3;
   d_cuen, d_pase;
   h_cuen, h_pase;
   be;
   NULL,grabar;
|FIN;

|DEFBUCLE beneficio;
    #4#1;
    3;
    #0#5, d_pase;
    #0#5, h_pase;
    be;
    NULL,grabar1;
|FIN;

|PROCESO montarlo;
vari = #6#2;
|QBF vari;
aste = vari % -101;
long = vari % 0;
longi = long - 1;
longi = 100 + longi;
numero = vari % longi;
|SI aste = "*";
    d_cuen = numero + "            ";
    d_cuen = d_cuen % 112;
    h_cuen = numero + "zzzzzzzzzzzz";
    h_cuen = h_cuen % 112;
    d_digi = 1;
    h_digi = 6;
    d_pase = "S";
    h_pase = "S";
    |BUCLE soagicon1;
|SINO;
    d_cuen = #6#2;
    h_cuen = #6#2;
    |SI long = dig4; d_digi = 1; |FINSI;
    |SI long = dig5; d_digi = 2; |FINSI;
    |SI long = dig6; d_digi = 3; |FINSI;
    |SI long = dig7; d_digi = 4; |FINSI;
    |SI long = dig8; d_digi = 5; |FINSI;
    |SI long = dig9; d_digi = 6; |FINSI;
    h_digi = d_digi;
    d_pase = "S";
    h_pase = "S";
    |BUCLE soagicon1;
|FINSI;
|LIBERA #6;
|FPRC;

|DEFBUCLE soagicon0;
  #6#1;
  ;
  #0#2, 001;
  #0#2, 999;
  e;
  NULL, montarlo;
|FIN;

|PROCESO LeoCuenta;
 |SI nSw = 0;
     #801#0 = #0#0;
     #801#4 = Cuen2008;
     |LEE 001#801=;
     |SI FSalida = 0;
         Cuenta = #801#2;
     |FINSI;
 |SINO;
     #801#0 = #0#0;
     #801#1 = Cuen2008;
     |LEE 001#801=;
     |SI FSalida = 0;
         Cuenta = #801#4;
     |FINSI;
 |FINSI;
|FPRC;

|PROCESO CalculaApu;
Primer     = #2#3 % 101;

Linea      = #2#2;
Cuenta     = #2#3;
SaldoFinal = 0;
Cuen2008   = Cuenta;

|SI Ejercicio = 2008;  |HAZ LeoCuenta; |FINSI;

|SELECT #2#2;
#2#0 = empAlta;
#2#1 = EjeAnt;
#2#3 = Cuenta;
|LEE 040#2=;
|SI #2#0 = empAlta; |Y #2#1 = EjeAnt; |Y #2#3 = Cuenta; |Y FSalida = 0;
    SaldoFinal = #2#8;
|FINSI;

Cuenta = Cuen2008;
|SELECT #1#2;
#2#0 = empAlta;
#2#1 = Ejercicio;
#2#2 = Linea;
|LIBERA #2;
|LEE 140#2=;
|SI FSalida = 0;
    #2#9 = SaldoFinal;
    #2#5 = SaldoFinal;
    |SI Primer = "6"; #2#5 = 0; |FINSI;
    |SI Primer = "7"; #2#5 = 0; |FINSI;

    QueEs = #2#8 - #2#5; || antes ... #2#5 - #2#8;
    |SI QueEs > 0;
        #2#6 = QueEs;
    |SINO;
        #2#7 = -QueEs;
    |FINSI;

    |GRABA 140#2a;
|SINO;
    |DDEFECTO #2;
|FINSI;

#1#3 = #1#3 + #2#5;
#1#4 = #1#4 + #2#6;
#1#5 = #1#5 + #2#7;
#1#7 = #1#7 + #2#9;
|FPRC;

|DEFBUCLE sodacoli_1;
#2#1;
;
empAlta, Ejercicio, 0000;
empAlta, Ejercicio, 9999;
be;
NULL, CalculaApu;
|FIN;

|PROCESO MiraAnterior;
Primer     = #2#3 % 101;
Cuenta     = #2#3;
SaldoFinal = #2#8;
LineaAnt   = #2#2;

Cuen2008   = Cuenta;
|SI Ejercicio = 2008;  |HAZ LeoCuenta; |FINSI;

|| .. |SI Primer = "6"; |ACABA; |FINSI;
|| .. |SI Primer = "7"; |ACABA; |FINSI;

|SELECT #2#2;
#2#0 = empAlta;
#2#1 = Ejercicio;
#2#3 = Cuenta;
|LEE 040#2=;
|SI #2#0 = empAlta; |Y #2#1 = Ejercicio; |Y #2#3 = Cuenta; |Y FSalida = 0;
    |ACABA;
|FINSI;

Linea = Linea + 1;

|SELECT #1#2;
#2#0 = empAlta;
#2#1 = Ejercicio;
#2#2 = Linea;
|LIBERA #2;
|LEE 140#2=;
|SI FSalida = 0; |ACABA; |FINSI;

#2#1 = Ejercicio;
#2#2 = Linea;
#2#9 = SaldoFinal;
#2#5 = SaldoFinal;
    |SI Primer = "6"; #2#5 = 0; |FINSI;
    |SI Primer = "7"; #2#5 = 0; |FINSI;
#2#6 = 0;
#2#7 = 0;
 QueEs = #2#5;
 |SI QueEs < 0;
    #2#6 = -QueEs;
 |SINO;
    #2#7 = QueEs;
 |FINSI;
#2#8 = 0;
|GRABA 140#2n;

#1#3 = #1#3 + #2#5;
#1#4 = #1#4 + #2#6;
#1#5 = #1#5 + #2#7;
#1#7 = #1#7 + #2#9;

#2#0 = empAlta;
#2#1 = EjeAnt;
#2#2 = LineaAnt;
|LIBERA #2;
|LEE 140#2=;
|FPRC;

|DEFBUCLE sodacoli_2;
#2#1;
;
empAlta, EjeAnt, 0000;
empAlta, EjeAnt, 9999;
be;
NULL, MiraAnterior;
|FIN;

|PROCESO VerCta129;
   nError   = 0;
   enHay129 = 0;
   |ABRE #4;
   |DDEFECTO #4;
   #4#0 = #0#5;
   |LEE 041#4=;
   |CIERRA #4;

   nNume1 = #4#53 - #4#50;
   |SI nNume1 ! 0;
       enHay129 = 1;
   |FINSI;

   |SI enHay129 ! 0;
       nError = 1;
       |SI enNoPanta = 0; |O enMen129 = 1;
           |CONFI "0000NLa Cta 129 tiene saldo. Haga la Distribucion Resultado antes del pase. Continuar"
           |SI FSalida = 0;
               nError = 0;
           |FINSI;
       |FINSI;
   |FINSI;
|FPRC;

|PROCESO pase; |TIPO 3;
   |SI enNoPanta = 0;
        |PUSHV 0501 2380;

        |D_CUADRO 811, 1471;
        |ATRI R;
        |PINTA 0912, " Si no tuvieramos el diario de apertura introducido en la  "
        |PINTA 1012, " contabilidad introducir una <N> a la siguiente pregunta,  "
        |PINTA 1112, " para que lea la introduccion de apuntes del a¤o anterior y"
        |PINTA 1212, " introduzca el saldo final como saldo anterior y apertura  "
        |PINTA 1312, " en el ejercicio actual.       Hay Diario de Apertura :    "
        |SI acabalo = 0;
            aPregunta = "S";
        |SINO;
            aPregunta = "S";
        |FINSI;
        |ATRI r;

        E_inf = "SNsn";
        E_sup = "1";
        aPregunta = 1368 ? 1;
        aPregunta = aPregunta > "";
        |PINTA 1368, aPregunta;
   |SINO;
        aPregunta = "S";
   |FINSI;

   HayDiario = 0;
   |SI aPregunta = "N"; HayDiario = 1; |FINSI;

   |SI enNoPanta = 0;
       |POPV;
   |FINSI;

   |HAZ VerCta129;
   |SI nError = 1; |ACABA; |FINSI;

   linea = 0;
   |ABRE #1;
   |SI #3#26 < 80;
       Ejercicio = 2000 + #3#26;
   |SINO;
       Ejercicio = 1900 + #3#26;
   |FINSI;

   #1#0 = empAlta;
   #1#1 = Ejercicio;
   |LEE 040#1=;
   |SI FSalida = 0;
      |BUCLELIN 0borralin#1;
      |BORRA 020#1c;
   |FINSI;
   |CIERRA #1;

   enPlan = 2008;  |SI Ejercicio < 2008; enPlan = 1990; |FINSI;
   |ABRE #1;
   |ABRE #2;
   |ABRE #8;
   |SELECT #2#2;
   |BUCLE soagicon0;

   d_digi  = 1;
   h_digi  = 6;
   d_pase  = "S";
   h_pase  = "S";
   |BUCLE beneficio;
   |CIERRA #8;
   |CIERRA #2;


   |SI Ejercicio = 2008;
       |ABRE #800;
       #800#0 = #0#0;
       |LEE 001#800=;
       |SI FSalida = 0;
           |BUCLE BorraTabla;
       |SINO;
            #800#0 = #0#0;
            #800#1 = 2008;
            #800#2 = #0#3;
            #800#3 = eNum2008;
            |GRABA 020#800n;
       |FINSI;
       |CIERRA #800;
       nLinea = 1;
       |ABRE #801;
       |BUCLE PasarTabla;
       |CIERRA #801;
       |CIERRA #800;
   |FINSI;

   |SI Ejercicio ] 2008;
       |ABRE #811;
       #811#9  = #0#0;
       #811#10 = Ejercicio;
       #811#0  = "             ";
       |LEE 001#811];
       |SI FSalida = 0; |Y #811#9 = #0#0; |Y #811#10 = Ejercicio;
           |BUCLE BorraTabla11;
           |BUCLE BorraTabla12;
       |FINSI;
       nLinea = 1;
       |ABRE #811;
       |ABRE #812;
       |BUCLE PasarTabla11;
       |BUCLE PasarTabla12;
       |CIERRA #812;
       |CIERRA #811;
   |FINSI;

   |SI HayDiario = 1;
       #1#0 = empAlta;
       #1#1 = Ejercicio;
       |LIBERA #1;
       |LEE 140#1=;
       |SI FSalida = 0;
           nSw = 0;
           |ABRE #801;
           |SELECT #2#801;
           EjeAnt = Ejercicio - 1;
           |BUCLE sodacoli_1;
           nSw = 1;
           |SELECT #3#801;
           |BUCLE sodacoli_2;
           |GRABA 140#1a;
           |SELECT #1#801;
           |CIERRA #801;
       |FINSI;
   |FINSI;

   |CIERRA #1;

   |SI #0#7 ! "N";
       |ABRE #3;
       #3#2 = #0#0;
       #3#3 = #0#1;
       |LEE 141#3=;
       |SI FSalida =0;
           aAlfa1 = Ejercicio;
           aAlfa1 = "31.12." + aAlfa1;
           #3#34 = aAlfa1;
           |GRABA 020#3a;
       |FINSI;
       |LIBERA #3;
   |FINSI;
   |CIERRA #3;

|FPRC;

|| ************************ CALCULO PRINCIPAL *************************
|PROGRAMA;
    |SI PARAMETRO ! "";  enNoPanta = 1;  sibloq = "S"; |FINSI;

    |HAZ LeeEURODB;
    enEjerS = 2008; eaPlanillaS = "sodacuen";  |HAZ PlanDePlanillasS;
    enEjerS = 2008; eaPlanillaS = "soplanli";  |HAZ PlanDePlanillasS;

    #0#0 = empre;
    #0#1 = perio;
    #0#2 = plani;   |HAZ planilla;
    #0#4 = siono;
    #0#5 = cuent;
    #0#7 = sibloq;
    |SI empAlta = 0;
        empAlta = empre;
    |FINSI;

    |SI enNoPanta = 0;
        |PUSHV 05012380;
        |PINPA #0#0;
        |PINDA #0#0;
    |FINSI;

    |HAZ inicio;
    |SI nError = 0;
        |HAZ pase;
    |FINSI;

    |SI enNoPanta = 0;
        |POPV;
    |FINSI;
|FPRO;
