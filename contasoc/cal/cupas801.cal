|FICHEROS;
    cubal800 #0;

    sodacoli #1;         || cuentas
    cubal801 #801;       || Total

    soda8w02 #2;         || temporal Cuentas
    soda8w03 #3;         || temporal para < 2008

    soda8m11 #431;  || Reparto 8 y 9 cabera
    soda8m12 #432;  || Reparto 8 y 9 lineas

    soda8w07 #890;

    :contagen/ca8cumay #410;  || Control de Balances
    :contagen/ca8m0000 #420;  || Descripcion Numero de Balance
    :contagen/ca8m0001 #421;  || Def Balance Textos
    :contagen/ca8m0002 #422;  || Def Balance Linea Cuentas
|FIN;

|VARIABLES;
     &empresa = 0;
     &anyo    = 0;
     &nombre  = "";
     &nif     = "";
     &unid    = "";
     &tipo    = "";
     &acabalo = 0;

   aFichero  = "";
   nImporte  = 0;
   nCantidad = 0;
   nLong     = 0;
   &aTitulo  = "";
   &aAPR     = "";
   &aCuenta  = "";
   aAlfa1   = "";
   aAlfa2   = "";
   acumula  = 0;
   acumul1  = 0;

   nCampo  = 0;
   nCampo2 = 0;
   aCampo3 = "";
   nCampo4 = 0;
   nCampo5 = 0;
   nCampo6 = 0;
   nCampo7 = 0;
   nCampo8 = 0;
   nCampo16 = 0;
   nSI     = 0;
   aCtaM   = "";
   aDCta   = "";
   aHCta   = "";
   nNivel  = 0;
    activo = 0;
    activ1 = 0;
    pasivo = 0;
    pasiv1 = 0;

   &d_mes = 0;
   &h_mes = 0;
   nSeguir   = 0;
   aSigno    = "";

   nEjer     = 0;
   nHay      = 0;
   &eaICtas  = "";
   nNumero   = 0;
|FIN;

|INCLUYE varcon_i;
|INCLUYE varbal_i;

|PROCESO MiroSiHay;
  |SI nHay = -1; nHay = 0; |FINSI;
  |SI #1#5 ! 0; |O #1#6 ! 0; |O #1#7 ! 0; |O #1#8 ! 0; |O #1#9 ! 0;
      nHay = 1;
  |FINSI;
|FPRC;

|DEFBUCLE MiroSiHay;
   #1#2;
   ;
   empresa, nEjer, "8           ";
   empresa, nEjer, "999999999999";
   ;
   NULL, MiroSiHay;
|FIN;

|PROCESO Grabaw3;
 |DDEFECTO #2;
 #2#0 = aCuenta;
 #2#5 = anyo;
 |LEE 001#2=;
 |SI FSalida ! 0;
     #2#0 = aCuenta;
     #2#5 = anyo;
     |GRABA 020#2c;
 |FINSI;
 |SI aSigno = "D";
     #2acumula = #2acumula + nImporte;
 |SINO;
     #2acumul1 = #2acumul1 + nImporte;
 |FINSI;
 |GRABA 020#2a;
|FPRC;

|PROCESO reparto_lin;
 aAlfa1 = #432#2; |QBF aAlfa1;
 |SI aAlfa1 ! "";
     aCuenta  = #432#2;
     aSigno   = #431#8;
     nImporte = #432#4;
     |HAZ Grabaw3;
 |FINSI;
|FPRC;

|DEFBUCLE reparto_lin;
 #432#1;
 ;
 #431#9, #431#10, #431#0, 01;
 #431#9, #431#10, #431#0, 99;
 e;
 NULL, reparto_lin;
|FIN;

|PROCESO reparto;
 aAlfa1 = #431#2; |QBF aAlfa1;
 |SI aAlfa1 ! "";
     aCuenta  = #431#2;
     nImporte = #431#4;
     aSigno   = #431#8;
     |HAZ Grabaw3;
 |SINO;
     |BUCLE reparto_lin;
 |FINSI;
|FPRC;

|DEFBUCLE reparto;
 #431#1;
 ;
 empresa, nEjer, "            ";
 empresa, nEjer, "zzzzzzzzzzzz";
 e;
 NULL, reparto;
|FIN;

|PROCESO pide8y9;
  |ABRE #2;
  nEjer = anyo;
  acumula = 1;
  acumul1 = 2;
  |BUCLE reparto;

  nEjer = anyo - 1;
  |SI nEjer ] 2008;
      acumula = 3;
      acumul1 = 4;
      |BUCLE reparto;
  |FINSI;
  |CIERRA #2;
|FPRC;

|PROCESO conexi8y9;
  enErrorW17 = 0;
  nHay = -1;
  nEjer = anyo;
  |BUCLE MiroSiHay;
  |SI nHay = 1;
      enEmpresa = empresa;
      enEjer    = anyo;
      |SI PARAMETRO = "";
          |SUB_EJECUTA "soda8z03;BALANCES";
      |SINO;
          |SUB_EJECUTA "soda8z03;BALANCEN";
      |FINSI;
      |SI enErrorW17 = 1; |ACABA; |FINSI;
  |FINSI;
  |HAZ pide8y9;
|FPRC;


|| ***************************************************************************
|| *********************  GRABACION y CALCULOS *******************************
|| ***************************************************************************
|PROCESO DetalleCta;  ||Carga Trabajo de Cuentas Impresion Detalle
  #890#0  = empresa;
  #890#1  = anyo;
  #890#2  = nBalance;
  #890#3  = nNumero;
  #890#4  = aCuenta;
  |LEE 101#890=;
  |SI FSalida ! 0;
      |DDEFECTO #890;
      #890#0  = empresa;
      #890#1  = anyo;
      #890#2  = nBalance;
      #890#3  = nNumero;
      #890#4  = aCuenta;
      #890#5  = aTitulo;
      |GRABA 020#890b;
  |FINSI;
  #890acumula = #890acumula + nImporte;
  |GRABA 020#890a;
  |LIBERA #890;
|FPRC;

|PROCESO cargatra_3;  ||Carga Trabajo de Cuentas
  nNumero = 0;
  #801#0  = empresa;
  #801#1  = anyo;
  #801#9  = nCampo2;
  #801#10 = aCampo3;
  #801#11 = nCampo4;
  #801#12 = nCampo5;
  #801#13 = nCampo6;
  #801#14 = nCampo7;
  #801#15 = nCampo8;
  |LEE 101#801=;
  |SI FSalida = 0; |Y #801#8 ! "A";
      #801acumula = #801acumula + nImporte;
      |GRABA 020#801a;
      nNumero = #801#2;
  |FINSI;
  |LIBERA #801;
|FPRC;

|PROCESO LosTotales;
  #0#6 = nEjer;   |PINTA #0#6;
  #0#7 = aCuenta; |PINTA #0#7;
  nCampo2 = #422#2;
  aCampo3 = #422#3;
  nCampo4 = #422#4;
  nCampo5 = #422#5;
  nCampo6 = #422#6;
  nCampo7 = #422#7;
  nCampo8 = #422#12;
  nCampo16 = #421#11;

  |HAZ cargatra_3;
  |SI eaICtas = "S"; |Y nNumero ! 0;
      |HAZ DetalleCta;
  |FINSI;

  |SI nCampo8 ! 0;    ||Subpartida (A/P = 0)
      nCampo8 = 0;
      |HAZ cargatra_3;
  |FINSI;

  |SI nCampo7 ! 0;   ||Partida
      nCampo8 = 0;
      nCampo7 = 0;
      |HAZ cargatra_3;
  |FINSI;

  |SI nCampo6 ! 0;   ||Epigrafe
      nCampo8 = 0;
      nCampo7 = 0;
      nCampo6 = 0;
      |HAZ cargatra_3;
  |FINSI;

  |SI nCampo5 ! 0;  ||Subagrupacion
      nCampo8 = 0;
      nCampo7 = 0;
      nCampo6 = 0;
      nCampo5 = 0;
      |HAZ cargatra_3;
  |FINSI;

  |SI nCampo4 ! 0;  ||Agrupacion
      nCampo8 = 0;
      nCampo7 = 0;
      nCampo6 = 0;
      nCampo5 = 0;
      nCampo4 = 0;
      |HAZ cargatra_3;
  |FINSI;

  |SI aCampo3 = "A";
      |SI acumula = 6;
          activ1 = (activ1 + nImporte) ! 2;
      |SINO;
          activo = (activo + nImporte) ! 2;
      |FINSI;
  |SINO;
      |SI acumula = 6;
          pasiv1 = (pasiv1 + nImporte) ! 2;
      |SINO;
          pasivo = (pasivo + nImporte) ! 2;
      |FINSI;
  |FINSI;
|FPRC;

|PROCESO Final;
  || Total Activo
  #801#0  = empresa;
  #801#1  = anyo;
  #801#9  = 0;
  #801#10 = "A";
  #801#11 = 99;
  #801#12 = 0;
  #801#13 = 0;
  #801#14 = 0;
  #801#15 = 0;
  |LEE 101#801=;
  |SI FSalida = 0;
      |SI #0#3 = "SI"; #801#6 = activ1; |FINSI;
      |SI #0#4 = "SI"; #801#7 = activo; |FINSI;
      |GRABA 020#801a;
  |FINSI;
  |LIBERA #801;

  || Total Activo
  #801#0  = empresa;
  #801#1  = anyo;
  #801#9  = 0;
  #801#10 = "P";
  #801#11 = 99;
  #801#12 = 0;
  #801#13 = 0;
  #801#14 = 0;
  #801#15 = 0;
  |LEE 101#801=;
  |SI FSalida = 0;
      |SI #0#3 = "SI"; #801#6 = pasiv1; |FINSI;
      |SI #0#4 = "SI"; #801#7 = pasivo; |FINSI;
      |GRABA 020#801a;
  |FINSI;
  |LIBERA #801;
|FPRC;

|PROCESO LaCondicion801;
  nImporte = nCantidad;
  |SI nImporte ! 0;
      |SI #422#11 ! " ";
          |SI #422#11 = "P"; |Y nImporte < 0; nImporte = 0; |FINSI;
          |SI #422#11 = "N"; |Y nImporte > 0; nImporte = 0; |FINSI;
      |FINSI;
  |FINSI;
  |SI #422#10 = "-"; nImporte = - nImporte; |FINSI;
  |SI nImporte !  0;
      |HAZ LosTotales;
  |FINSI;
|FPRC;

|PROCESO calculit;

  aCuenta = #1#3;
  aTitulo = #1#4;
  |HAZ MSigno08_s;

  aAlfa1 = #1#3; |QBF aAlfa1;
  aAlfa1 = aAlfa1 % 0;
  nLong  = aAlfa1;
  aCuenta = #1#3;
  aTitulo = #1#4;

  |SI #0#2 = "A";
      nCantidad = #1#8;
      |HAZ LaCondicion801;
  |SINO;
      |SI #0#4 = "SI";
          nCantidad = #1#8;
          acumula = 7;
          |HAZ LaCondicion801;
      |FINSI;

      |SI #0#3 = "SI";
          nCantidad = #1#9;
          acumula = 6;
          |HAZ LaCondicion801;
      |FINSI;
  |FINSI;
|FPRC;

|DEFBUCLE cabasitu0;
 #1#2;
 ;
 empresa, nEjer, aDCta;
 empresa, nEjer, aHCta;
 e;
 NULL,calculit;
|FIN;

|PROCESO calculit1;
  aCuenta = #2#0;
  |HAZ MSigno08_s;

  aAlfa1 = #2#0; |QBF aAlfa1;
  aAlfa1 = aAlfa1 % 0;
  nLong  = aAlfa1;
  aCuenta = #2#0;

  |SI #0#4 = "SI";
      nCantidad = #2#1 - #2#2; ||debe menos haber
      acumula = 7;
      |HAZ LaCondicion801;
  |FINSI;

  |SI #0#3 = "SI";
      nCantidad = #2#3 - #2#4; ||debe menos haber
      acumula = 6;
      |HAZ LaCondicion801;
  |FINSI;
|FPRC;

|DEFBUCLE elauxiliar;
 #2#1;
 ;
 anyo, aDCta;
 anyo, aHCta;
 e;
 NULL,calculit1;
|FIN;

|PROCESO calculit0;
  aCuenta = #3#0;
  |HAZ MSigno08_s;

  aAlfa1 = #3#0; |QBF aAlfa1;
  aAlfa1 = aAlfa1 % 0;
  nLong  = aAlfa1;
  aCuenta = #3#0;

  nCantidad = #3#2;
  acumula = 6;
  |HAZ LaCondicion801;
|FPRC;

|DEFBUCLE AuxEquival0;  ||para el ejer. 2008 equivalencia
 #3#1;
 ;
 anyo, aDCta;
 anyo, aHCta;
 e;
 NULL,calculit0;
|FIN;

|PROCESO calculit_4;
 aAlfa1 = #422#9; |QBF aAlfa1;
 aDCta = (aAlfa1 + "            ") % 112;
 aHCta = (aAlfa1 + "zzzzzzzzzzzz") % 112;

 |SI #0#2 = "A";
     |SI #0#3 = "SI";
         acumula = 6;
         |SI anyo ! 2008;    || lee el balance del ejercicio anterior
             nEjer   = anyo - 1;
             |BUCLE cabasitu0;
         |SINO;
             |BUCLE AuxEquival0;  ||para el ejer. 2008 equivalencia
         |FINSI;
     |FINSI;
     |SI #0#4 = "SI";        || lee balance del ejercicio
         acumula = 7;
         nEjer = anyo;
         |BUCLE cabasitu0;
     |FINSI;
 |SINO;
     nEjer = anyo;      ||solo lee el balance para los dos ejercicios
     |BUCLE cabasitu0;
 |FINSI;

 |BUCLE elauxiliar;
|FPRC;

|DEFBUCLE cabasitu4;
 #422#1;
 ;
 enEjerBal, nBalNum, nBalance, 0, "A", 00, 0, 00, 00, 0, 001;
 enEjerBal, nBalNum, nBalance, 0, "P", 99, 9, 99, 99, 9, 999;
 e;
 NULL,calculit_4;
|FIN;

|PROCESO PaseSituacion;

  aFichero = "x" + Usuario;
  |NOME_DAT #2 aFichero;|DELINDEX #2;

  aFichero = "y" + Usuario;
  |NOME_DAT #3 aFichero;

  |SI PARAMETRO = "";
      |ATRI I; |PINTA 1847, "Balance de Situacion    "; |ATRI 0;
  |FINSI;

  activo = 0; activ1 = 0;
  pasivo = 0; pasiv1 = 0;

  |HAZ conexi8y9;
  |SI enErrorW17 = 1; |ACABA; |FINSI;

  |ABRE #801;
  |ABRE #890;
  |SELECT #2#801;
  |ABRE #2;
  |BUCLE cabasitu4;
  |CIERRA #2;

  |HAZ Final;
  |SELECT #1#801;
  |CIERRA #890;
  |CIERRA #801;
|FPRC;
