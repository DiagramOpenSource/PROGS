|FICHEROS;
    mem11fic #0;
    mem11fil #1,MANTE;
    mem11fi3 #11,MANTE;

    mem00002 #12;
    mem00003 #13;
    mem00004 #14;

    espejo@  #911;
|FIN;

|VARIABLES;
     &enCalcCif   = 0;
     &eaVarDni    = "";

     &eaExtension = "";
     &eaPrograma  = "";

     &enEmpresa = 0;
     &enEjer    = 0;
     nApar      = 0;
     nSApa      = 0;
     nAgru      = 0;
     nSAgr      = 0;
     nDApar     = 0;
     nHApar     = 0;
     nD1        = 0;
     nD2        = 0;
     nD3        = 0;
     nD4        = 0;
     nH1        = 0;
     nH2        = 0;
     nH3        = 0;
     nH4        = 0;
     nD11       = 0;
     nD21       = 0;
     nD31       = 0;
     nD41       = 0;
     nH11       = 0;
     nH21       = 0;
     nH31       = 0;
     nH41       = 0;
     nSw        = 0;
     nValor1    = 0;
     nValor2    = 0;
     nValor3    = 0;
     nValor4    = 0;
     nNume1     = 0;
     nNume2     = 0;

     nModo        = 0;
     nModoEntrada = 0;
     aAlfa1       = "";
     aDef911      = "";
     aBase        = "";
|FIN;


|CALCULO Tipo8_11; |TIPO 8;

   |ABRE #12;
   |LEE 040#12p;
   |SI FSalida ! 0;
       |CIERRA #12;
       eaExtension = "soc";
       eaPrograma  = "mem00002";
       |SUB_EJECUTA_NP ":basica/dspatron";
   |SINO;
       |CIERRA #12;
   |FINSI;

   |ABRE #13;
   |LEE 040#13p;
   |SI FSalida ! 0;
       |CIERRA #13;
       eaExtension = "soc";
       eaPrograma  = "mem00003";
       |SUB_EJECUTA_NP ":basica/dspatron";
   |SINO;
       |CIERRA #13;
   |FINSI;

   |ABRE #14;
   |LEE 040#14p;
   |SI FSalida ! 0;
       |CIERRA #14;
       eaExtension = "soc";
       eaPrograma  = "mem00004";
       |SUB_EJECUTA_NP ":basica/dspatron";
   |SINO;
       |CIERRA #14;
   |FINSI;

   |DBASE aBase;
   aDef911 = aBase + "def/mem11fil";
   |CARGA_DEF aDef911, espejo@;
   |SI FSalida ! 0;
       aDef911 = "";
       |MENAV "    Error en Defs. Proceso Interumpido";
       |PTEC 807;
       |ACABA;
   |FINSI;

|FCAL;

|CALCULO Tipo9; |TIPO 9;
 |SI aDef911 ! ""; |DESCARGA_DEF #911; |FINSI;
|FCAL;

|PROCESO Elanyo; |TIPO 0;
  #0#5 = #0#1 - 1;
  |HAZ PintaAny;
|FPRC;

|PROCESO  PintaAny;
  |ATRI R;
  aAlfa1 = #0#1;
  |PINTA 0861, aAlfa1;
  aAlfa1 = #0#5;
  |PINTA 0875, aAlfa1;

  aAlfa1 = "    Ejercicio " + #0#1 + "     ";
  |PINTA 1633, aAlfa1;
  aAlfa1 = "    Ejercicio " + #0#5 + "     ";
  |PINTA 1657, aAlfa1;
  |ATRI 0;
|FPRC;

|PROCESO LetraNif;  |TIPO 6;
     |SI FSalida = 10;
         enCalcCif = 1;
         eaVarDni  =  #0Campo;
         |HAZ CalculoDNI;
         #0Campo = eaVarDni;
         |PINTA #0Campo;
         |ERROR;
         |ACABA;
     |FINSI;

     eaVarDni  = #0Campo;
     enCalcCif = 0;
     |HAZ CalculoDNI;
     eaVarDni = (eaVarDni + "            ") % 112;
     |SI #0Campo ! eaVarDni;
         |CONFI "0000SEl Nif puede ser Erroneo. Desea Calcularlo.";
         |SI FSalida = 0;
             #0Campo = eaVarDni;  |PINTA #0Campo;
             |ERROR;
             |ACABA;
         |FINSI;
     |FINSI;
|FPRC;
|| **********************************************************************


|PROCESO LeoTexto;
     |ABRE #12;
     #12#0  = nApar;
     #12#1  = nSApa;
     #12#4  = nAgru;
     #12#6  = nSAgr;
     |LEE 040#12=;
     |SI FSalida ! 0;  |DDEFECTO #12;  |FINSI;
     |CIERRA #12;
|FPRC;


|PROCESO GrabaRegistros;
     |DDEFECTO #1;
     #1#0  = enEmpresa;
     #1#1  = enEjer;
     #1#2  = #12#0;
     #1#3  = #12#1;
     #1#4  = #12#4;
     #1#5  = #12#6;
     #1#6  = #12#2;
     #1#11 = #12#3;
     #1#12 = #12#5;
     #1#13 = #12#7;

     |GRABA 020#1n;
     |LIBERA #1;
|FPRC;

|DEFBUCLE mem00002;
     #12#1;
     ;
     1, 00, 00, 00;
     9, 99, 99, 99;
     ;
     NULL, GrabaRegistros;
|FIN;

|PROCESO CreoelFich;
     |ABRE #1;
     #1#0 = enEmpresa;
     #1#1 = enEjer;
     #1#2 = 1;
     #1#3 = 00;
     #1#4 = 00;
     #1#5 = 00;
     |LEE 040#1];
     |SI FSalida ! 0;  |O #1#0 ! enEmpresa;  |O #1#1 ! enEjer;
         |BUCLE mem00002;
     |FINSI;
     |CIERRA #1;
|FPRC;

|PROCESO ClaveAlta;
 #1#0 = #0#0;
 #1#1 = #0#1;
 #1#2 = nDApar;
 #1#3 = 01;
 #1#4 = 00;
 #1#5 = 00;
|FPRC;

|PROCESO ClaveBaja;
 #1#0 = #0#0;
 #1#1 = #0#1;
 #1#2 = nHApar;
 #1#3 = 99;
 #1#4 = 99;
 #1#5 = 99;
|FPRC;

|PROCESO ClaveAlta11;
 #11#0 = #0#0;
 #11#1 = #0#1;
 #11#2 = nDApar;
 #11#3 = 01;
 #11#4 = 00;
 #11#5 = 00;
|FPRC;

|PROCESO ClaveBaja11;
 #11#0 = #0#0;
 #11#1 = #0#1;
 #11#2 = nHApar;
 #11#3 = 99;
 #11#4 = 00;
 #11#5 = 00;
|FPRC;

|PROCESO BorroLineas;
 |BORRA 020#1a;
 |LIBERA #1;
|FPRC;

|DEFBUCLE BorroLineas;
 #1#1;
 ;
 #0#0, #0#1, 0, 00, 00, 00;
 #0#0, #0#1, 9, 99, 99, 99;
 be;
 NULL, BorroLineas;
|FIN;

|PROCESO Tipo1_11; |TIPO 1;
 nModoEntrada = (FEntrada / 100) ! 0;
 |SI nModoEntrada = 3;
     |BUCLE BorroLineas;
 |FINSI;
|FPRC;

|PROCESO VerTablas;
   enEmpresa = #0#0;
   enEjer    = #0#1;

   nDApar = 1;
   nHApar = 2;
   |ENTLINEAL #1#1, -6, nModo, 15, 0, ClaveAlta, ClaveBaja;

   nDApar = 3; nHApar = 3;
   |ENTLINEAL #1#11, -4, nModo, 22, 0, ClaveAlta11, ClaveBaja11;
|FPRC;

|PROCESO Tipo13_11; |TIPO 13;
   nModoEntrada = (FEntrada / 100) ! 0;
   |SI nModoEntrada = 0; |ACABA; |FINSI;

   |HAZ PintaAny;
   nModo = 7;
   |HAZ VerTablas;
|FPRC;

|PROCESO Tipo3_11; |TIPO 12;

   enEmpresa = #0#0;
   enEjer    = #0#1;
   |HAZ CreoelFich;

   nModo = 4;
   |HAZ VerTablas;
|FPRC;

|| ************************************************************************
|PROCESO Tipo5;  |TIPO 5;
     #1#0  = enEmpresa;
     #1#1  = enEjer;
     #1#2  = 1;
     #1#3  = 01;
     #1#4  = 00;
     #1#5  = 00;
     |LEE 040#1=;
     |LINEAS_BI_ATRI #1#11, N, S, -1;
|FPRC;

|PROCESO Tipo7;  |TIPO 7;
     |SI #1#11 = "T";
         |C_M #1#7, 1,"N";
         |C_M #1#8, 1,"N";
     |FINSI;

     |SI #1#11 = "D";
         |C_M #1#7, 1, "N";
         |C_M #1#8, 1, "N";
         |SI #1#12 = 0;
             |C_M #1#7, 1,"S";
             |C_M #1#8, 1,"S";
         |FINSI;
     |FINSI;

     |SI #1#12 = 0; |Y #1#11 = "D";
         |MENSA "     Pulse <F2> para Introduccion del Importe";
     |FINSI;

     nApar = #1#2;
     nSApa = 0;
     nAgru = 0;
     nSAgr = 0;
     |HAZ LeoTexto;
     aAlfa1=  #12#2 % 141;
     |ATRI R; |PINTA 0811, aAlfa1; |ATRI 0;
|FPRC;

|PROCESO Refresco;
     |PONCONTROL 23, "SI";
|FPRC;

|PROCESO Totales;
     |HAZ Total;
     |HAZ Refresco;
|FPRC;

|PROCESO Introduccion;  |TIPO 11;
     |SI FSalida ! 10; |ACABA; |FINSI;

     |LEE 101#1=;

     |SI #1#12 = 1; |O #1#11 = "T";
         |LIBERA #1;
         |ACABA;
     |FINSI;

     |ET Camp7_1;
     |ENCAMPO #7#1;

     |ENCAMPO #8#1;
     |SI FSalida = 2; |VETE Camp7_1; |FINSI;

     |GRABA 020#1a;
     |LIBERA #1;

     |SALVA_DATOS #1;

     |HAZ Totales;

     |REPON_DATOS #1;
|FPRC;

|PROCESO Tipo5_3;  |TIPO 5;

     #11#0  = enEmpresa;
     #11#1  = enEjer;
     #11#2  = 3;
     #11#3  = 01;
     #11#4  = 00;
     #11#5  = 00;
     |LEE 040#11=;
     |LINEAS_BI_ATRI #11#11, D, S, -1;
|FPRC;

|PROCESO Tipo7_3;  |TIPO 7;
     |SI #11#11 = "T";
         |C_M #11#7, 1,"N";
         |C_M #11#8, 1,"N";
         |C_M #11#9, 1,"N";
         |C_M #11#10, 1,"N";
     |FINSI;

     |SI #11#11 = "D";
         |C_M #11#7, 1, "N";
         |C_M #11#8, 1, "N";
         |C_M #11#9, 1, "N";
         |C_M #11#10, 1, "N";
         |SI #11#12 = 0;
             |C_M #11#7, 1,"S";
             |C_M #11#8, 1,"S";
             |C_M #11#9, 1,"S";
             |C_M #11#10, 1,"S";
         |FINSI;
     |FINSI;

     |SI #11#12 = 0; |Y #11#11 = "D";
         |MENSA "     Pulse <F2> para Introduccion del Importe";
     |FINSI;

     nApar = #11#2;
     nSApa = 0;
     nAgru = 0;
     nSAgr = 0;
     |HAZ LeoTexto;
     aAlfa1 = #12#2 % 127;
     |ATRI R; |PINTA 1705, aAlfa1; |ATRI 0;
|FPRC;

|PROCESO Totales3;
     |HAZ Total;
     |HAZ Refresco;
|FPRC;

|PROCESO Introduccion3;  |TIPO 11;
     |SI FSalida ! 10; |ACABA; |FINSI;

     |LEE 101#11=;
     |SI #11#12 = 1; |O #11#11 = "T";
         |LIBERA #11;
         |ACABA;
     |FINSI;

     |ET Camp7;
     |ENCAMPO #7#11;

     |ET Camp8;
     |ENCAMPO #8#11;
     |SI FSalida = 2; |VETE Camp7; |FINSI;

     |ET Camp9;
     |ENCAMPO #9#11;
     |SI FSalida = 2; |VETE Camp8; |FINSI;

     |ENCAMPO #10#11;
     |SI FSalida = 2; |VETE Camp9; |FINSI;

     |GRABA 020#11a;
     |LIBERA #11;

     |SALVA_DATOS #11;

     |HAZ Totales3;

     |REPON_DATOS #11;
|FPRC;

|| ***********************************************************************
|PROCESO LeeCasilla;
     #911#0  = enEmpresa;
     #911#1  = enEjer;
     #911#2  = nApar;
     #911#3  = nSApa;
     #911#4  = nAgru;
     #911#5  = nSAgr;
     |LEE 040#911=;
     |SI FSalida ! 0;  |DDEFECTO #911;  |FINSI;
|FPRC;

|PROCESO GraboCasilla;
     |ABRE #911;
     #911#0  = enEmpresa;
     #911#1  = enEjer;
     #911#2  = nApar;
     #911#3  = nSApa;
     #911#4  = nAgru;
     #911#5  = nSAgr;
     |LEE 040#911=;
     |SI FSalida ! 0;
         |DDEFECTO #911;
         #911#0  = enEmpresa;
         #911#1  = enEjer;
         #911#2  = nApar;
         #911#3  = nSApa;
         #911#4  = nAgru;
         #911#5  = nSAgr;
         #911#6  = #12#2;
         #911#11 = #12#3;
         #911#12 = #12#5;
         #911#13 = #12#7;
         |GRABA 020#911b;
      |FINSI;
      #911#7  = nValor1;
      #911#8  = nValor2;
      #911#9  = nValor3;
      #911#10 = nValor4;
      |GRABA 020#911a;
      |LIBERA #911;
      |CIERRA #911;
|FPRC;

|PROCESO LeoTotal1;
    nValor1   = nValor1 + #911#7;
    nValor2   = nValor2 + #911#8;
    nValor3   = nValor3 + #911#9;
    nValor4   = nValor4 + #911#10;
|FPRC;

|DEFBUCLE LeoTotal1;
 #911#1006;
 ;
 enEmpresa, enEjer, nD11, nD21, nD31, nD41;
 enEmpresa, enEjer, nH11, nH21, nH31, nH41;
 e;
 NULL, LeoTotal1;
|FIN;

|PROCESO LeoTabla;
  |SI #12#5 ! 1;
      |SI #12#3 ! "T"; |ACABA; |FINSI;
  |FINSI;

  |SI nSw = 3;
      nValor1 = 0; nValor2 = 0;
      nValor3 = 0; nValor4 = 0;
      nD11 = #12#0; nH11 = #12#0;
      nD21 = #12#1; nH21 = #12#1;
      nD31 = #12#4; nH31 = #12#4;
      nD41 = 01;    nH41 = 99;
      |BUCLE LeoTotal1;
      nApar = #12#0;
      nSApa = #12#1;
      nAgru = #12#4;
      nSAgr = 00;
      |HAZ GraboCasilla;
  |FINSI;
  |SI nSw = 2;
      nValor1 = 0; nValor2 = 0;
      nValor3 = 0; nValor4 = 0;
      nD11 = #12#0; nH11 = #12#0;
      nD21 = #12#1; nH21 = #12#1;
      nD31 = 01;    nH31 = 99;
      nD41 = 00;    nH41 = 00;
      |BUCLE LeoTotal1;
      nApar = #12#0;
      nSApa = #12#1;
      nAgru = 00;
      nSAgr = 00;
      |HAZ GraboCasilla;
  |FINSI;
  |SI nSw = 1;
      nValor1 = 0; nValor2 = 0;
      nValor3 = 0; nValor4 = 0;
      nD11 = #12#0; nH11 = #12#0;
      nD21 = 01;    nH21 = 89;
      nD31 = 00;    nH31 = 00;
      nD41 = 00;    nH41 = 00;
      |BUCLE LeoTotal1;
      nApar = #12#0;
      nSApa = 90;
      nAgru = 00;
      nSAgr = 00;
      |HAZ GraboCasilla;
  |FINSI;
|FPRC;

|DEFBUCLE leom00002;
 #12#1;
 ;
 nD1, nD2, nD3, nD4;
 nH1, nH2, nH3, nH4;
 e;
 NULL, LeoTabla;
|FIN;

|PROCESO Total;

  nD1 = nDApar; nH1 = nHApar;

  nSw = 3;
  nD2 = 1; nH2 = 89;
  nD3 = 1; nH3 = 99;
  nD4 = 0; nH4 = 0;
  |BUCLE leom00002;

  nSw = 2;
  nD2 = 1; nH2 = 89;
  nD3 = 0; nH3 = 0;
  nD4 = 0; nH4 = 0;
  |BUCLE leom00002;

  nSw = 1;
  nD2 = 0; nH2 = 0;
  nD3 = 0; nH3 = 0;
  nD4 = 0; nH4 = 0;
  |BUCLE leom00002;


  |SI nDApar ! 3;
      nValor3 = 0;
      nValor4 = 0;

      |ABRE #911;
      nApar = 1;  nSApa = 90;
      nAgru = 00; nSAgr = 00;
      |HAZ LeeCasilla;
      nNume1 = #911#7;
      nNume2 = #911#8;

      nApar = 2;  nSApa = 90;
      nAgru = 00; nSAgr = 00;
      |HAZ LeeCasilla;
      nNume1 = nNume1 - #911#7;
      nNume2 = nNume2 - #911#8;
      |CIERRA #911;

      nValor1 = 0; |SI nNume1 > 0; nValor1 = nNume1; |FINSI;
      nValor2 = 0; |SI nNume2 > 0; nValor2 = nNume2; |FINSI;
      nApar = 1;
      nSApa = 91;
      nAgru = 00;
      nSAgr = 00;
      |HAZ GraboCasilla;

      nValor1 = 0; |SI nNume1 < 0; nValor1 = -nNume1; |FINSI;
      nValor2 = 0; |SI nNume2 < 0; nValor2 = -nNume2; |FINSI;
      nApar = 2;
      nSApa = 91;
      nAgru = 00;
      nSAgr = 00;
      |HAZ GraboCasilla;

  |FINSI;
|FPRC;

|| *******************************************************************
