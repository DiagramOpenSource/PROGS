|FICHEROS;
     zusuario #998;
|FIN;

|VARIABLES;
     nHandle        = 0;
     nSleep         = 0;

     aBase          = "";
     aPathLog       = "";
     aLog           = "";
     aAbre          = "";
     aAlfa          = "";
     aHora          = "";
     aIPRemoto      = "";

     fFecha         = @;

     &eaAlfa        = "";
     &eaOrigen      = "";
     &eaDestino     = "";
     &eaPath        = "";
|FIN;

|PROCESO GeneraLog;
     |DBASE aBase;  |QBF aBase;

     aPathLog = aBase + "logs";  |MKDIR aPathLog;
     aPathLog = aPathLog + "/";

     fFecha   = ~;
     aLog     = Usuario % 810;  |QBF aLog;
     aLog     = aLog + ((fFecha % 704) + (fFecha % 402) + (fFecha % 102)) + ".txt";

     |QBF eaAlfa;
     |HORA aHora;

     aAlfa    =  aHora + " " + eaAlfa + &13 + &10;
     aAbre    = aPathLog + aLog;

     |XABRE "AB", aAbre, nHandle;
     |XGRABA nHandle, aAlfa;
     |XCIERRA nHandle;
|FPRC;

|PROCESO EnviaFichero;
     |ABRE #998;
     #998#0 = Usuario % 810;
     |LEE 000#998=;
     |SI FSalida ! 0;  |DDEFECTO #998;  |FINSI;
     |CIERRA #998;

     aIPRemoto = "";
     |IP_REMOTO aIPRemoto;  |QBT aIPRemoto;

     eaAlfa = "Envio documento de " + eaOrigen + " a " + eaDestino;
     |HAZ GeneraLog;

     |PULSATECLA;
     |SI aIPRemoto ! "";
         |ENVIA_FICHERO eaOrigen, eaDestino;
     |SINO;
         |COPIA_FICHERO eaOrigen, eaDestino;
     |FINSI;
     |PULSATECLA;

     eaAlfa = "Espero "  + #998#1 + " segundos ";
     |HAZ GeneraLog;

     nSleep = #998#1;
     |SLEEP nSleep;
|FPRC;

|PROCESO RecibeFichero;
     |ABRE #998;
     #998#0 = Usuario % 810;
     |LEE 000#998=;
     |SI FSalida ! 0;  |DDEFECTO #998;  |FINSI;
     |CIERRA #998;

     aIPRemoto = "";
     |IP_REMOTO aIPRemoto;  |QBT aIPRemoto;

     eaAlfa = "Envio documento de " + eaOrigen + " a " + eaDestino;
     |HAZ GeneraLog;

     |PULSATECLA;
     |SI aIPRemoto ! "";
         |RECIBE_FICHERO eaOrigen, eaDestino;
     |SINO;
         |COPIA_FICHERO eaOrigen, eaDestino;
     |FINSI;
     |PULSATECLA;

     eaAlfa = "Espero "  + #998#1 + " segundos ";
     |HAZ GeneraLog;

     nSleep = #998#1;
     |SLEEP nSleep;
|FPRC;

|PROCESO TraeDsPrintWord;
     |DBASE aBase;  |QBF aBase;

     aAlfa = eaPath + "wait.exe";
     |XABRE "EC", aAlfa, nHandle;
     |SI FSalida < 0;
         eaOrigen  = aBase + "planilla/wait.exe";
         eaDestino = eaPath + "wait.exe";
         |HAZ EnviaFichero;
     |FINSI;

     aAlfa = eaPath + "dsprintword.exe";
     |XABRE "EC", aAlfa, nHandle;
     |SI FSalida < 0;
         eaOrigen  = aBase + "planilla/dsprintword.exe";
         eaDestino = eaPath + "dsprintword.exe";
         |HAZ EnviaFichero;
     |FINSI;
|FPRC;
