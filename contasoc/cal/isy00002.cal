|FICHEROS;
     isy00002 #0;
     isacceso #100;
     isocie59 #159;
     isocie91 #191;

     :/integral/def/dsam0001 #1;
     :/integral/def/dsam0054 #54;

     &clientes@ #715;
|FIN;

|VARIABLES;
     aPathDoc         = "";
     aAbre            = "";
     aAlfa            = "";
     aAlfa1           = "";
     aAlfa2           = "";
     aOrigen          = "";
     aDestino         = "";
     aDescri          = "";
     aIPRemoto        = "";
     aCadena          = "";
     aLen             = "";
     aAsigna          = "";
     aCaracter        = "";
     aIdenti          = "";

     nHandle          = 0;
     nPara            = 0;
     nResultado       = 0;
     nWord            = 0;
     nLinea           = 0;
     nLen             = 0;
     nPos             = 0;
     nCampo           = 0;

     &empresa         = 0;
     &anyo            = 0;
     &enSwDeDonde     = 0;
     &enModelo        = 0;
     &enForzar        = 0;
     &enCodCli        = 0;

     &eaEntidad       = "";
     &eaOficina       = "";
     &eaDC            = "";
     &eaCuenta        = "";
     &eaNomEnt        = "";
     &eaNomOfi        = "";
     &eaEfectivo      = "";
     &eaRutaDocumento = "";
     &enFSalida       = 0;
     &enCodUbica      = 0;
     &eaNomUbicacion  = "";
     &eaAlfa          = "";
     &eaImporte       = "";
     &eaOrigen        = "";
     &eaDestino       = "";
     &eaPath          = "";
     &eaUnidadBasico  = "";
|FIN;

|PROCESO Seleccion; |TIPO 0;
     |SI #0#0 = "S";
         |CAMPO_MODIFICA #0#01, 1, "S";
         |CAMPO_MODIFICA #0#02, 1, "S";

         |PARA nPara = 3; |SI nPara [ 32; |HACIENDO nPara = nPara + 1;
               #0nPara = 0; |PINTA #0nPara;
               |CAMPO_MODIFICA #0nPara, 1, "N";
         |SIGUE;
     |SINO;
         #0#1 = 00001; |PINTA #0#1;    |CAMPO_MODIFICA #0#01, 1, "N";
         #0#2 = 99999; |PINTA #0#2;    |CAMPO_MODIFICA #0#02, 1, "N";

         |PARA nPara = 3; |SI nPara [ 32; |HACIENDO nPara = nPara + 1;
               |CAMPO_MODIFICA #0nPara, 1, "S";
         |SIGUE;
     |FINSI;
|FPRC;

|PROCESO Gestion;
     |C_M #0#37, 1, "S";
     |SI #0#36 = "N";
         |C_M #0#37, 1, "N";  #0#37 = 0;
                              #0#38 = "";
     |FINSI;

     |PINTA #0#37;
     |PINTA #0#38;
|FPRC;

|PROCESO BuscaUbicacion;
     |SI #0#36 = "N";  |ACABA;  |FINSI;

     enCodUbica = #0#37;
     |HAZ LeeUbicaciones;
     |SI enFSalida ! 0;  |ERROR;  |ACABA;  |FINSI;

     #0#37 = enCodUbica;      |PINTA #0#37;
     #0#38 = eaNomUbicacion;  |PINTA #0#38;
|FPRC;

|PROCESO CargaVarBanco;
     |SI enSwDeDonde = 1;
         enModelo = 201;
         |SI #100#5 = 200;
             enModelo = 200;
         |FINSI;
         enForzar = 1;  |HAZ CogeDatosBancos;
     |SINO;
         aAlfa1  = "";
         aAlfa   = #715#81;       |QBF aAlfa; eaEntidad = ("0000" + aAlfa) % -104;
         aAlfa   = #715#82;       |QBF aAlfa; eaOficina = ("0000" + aAlfa) % -104;
         aAlfa   = #715#83;       |QBF aAlfa; eaDC      = ("00"   + aAlfa) % -102;
         aAlfa   = #715#84 % 110; |QBF aAlfa; eaCuenta  = ("0000000000" + aAlfa) % -110;
                                              eaNomEnt  = #715#80;
                                              eaNomOfi  = "";
     |FINSI;
|FPRC;

|PROCESO AbreWord;
     aAlfa = eaUnidadBasico + "/MODELOS";                   |RMKDIR aAlfa;
     aAlfa = eaUnidadBasico + "/MODELOS/DOCUMENTOS";        |RMKDIR aAlfa;

     aOrigen   = aPathDoc + "word0065.doc";
     aDestino  = eaUnidadBasico + "/MODELOS/DOCUMENTOS/word0065.doc";
     aDescri   = "CARTA PAGO DECLARACION IMPUESTO SOCIEDADES EJERCICIO " + #0#33;

     eaOrigen  = aOrigen;
     eaDestino = aDestino;
     |HAZ EnviaFichero;

     nWord  = 1;
     aAbre  = aDestino;

     |MENSA "      Abriendo el Documento, espere un Momento.";

     eaAlfa = "Abro el documento " + aAbre;
     |HAZ GeneraLog;

     |WORD_ABRE nWord, aAbre;
     |WORD_PROPIEDADES nWord, "Visible", "NO";

     eaAlfa = "Asigno variables en el documento";
     |HAZ GeneraLog;
|FPRC;

|PROCESO GuardaDocumento;
     |ABRE #54;
     #54#0  = #100#0;
     #54#1  = 9999;
     |LEE 040#54];
     |SI FSalida = 0;
         |LEE 040#54a;
     |SINO;
         |LEE 040#54u;
     |FINSI;
     |SI FSalida = 0;  |Y #54#0 = #100#0;
         nLinea = #54#1 + 1;
     |SINO;
         nLinea = 1;
     |FINSI;

     |DDEFECTO #54;
     |HORA aAlfa;
     aAlfa1 = aAlfa % 102;
     aAlfa2 = aAlfa % 402;

     #54#0  = #100#0;
     #54#1  = nLinea;
     #54#2  = aDescri;
     #54#3  = "DOC" + (("00000" + #54#0) % -105) + (("0000" + #54#1) % -104) + ".DOC";
     #54#4  = ~;
     #54#5  = aAlfa1;
     #54#6  = aAlfa2;
     #54#7  = ~;
     #54#8  = aAlfa1;
     #54#9  = aAlfa2;
     #54#10 = #0#37;
     #54#11 = "";
     #54#12 = "";
     |GRABA 020#54n;
     |LIBERA #54;
     |CIERRA #54;

     aOrigen  = aAbre;
     aDestino = eaRutaDocumento + "/" + "DOC" + (("00000" + #100#0) % -105) + (("0000" + nLinea) % -104) + ".DOC";
     |QBT aOrigen;
     |QBT aDestino;

     |RCOPIA_FICHERO aOrigen, aDestino;
     |SI FSalida ! 0;
         |MENAV "     No se ha podido copia el Documento al Cliente.";
     |FINSI;
|FPRC;

|PROCESO Asigna;
     aCadena = "";
     aLen    = aAsigna % 0;
     nLen    = aLen;
     |PARA nCampo = 1;  |SI nCampo [ nLen;  |HACIENDO nCampo = nCampo + 1;
           nPos = (100 * nCampo) + 1;
           aCaracter = aAsigna % nPos;
           |SI aCaracter = "¥";  aCaracter = &209; |FINSI;
           |SI aCaracter = "¤";  aCaracter = &241; |FINSI;
           |SI aCaracter = " ";  aCaracter = &225; |FINSI;
           |SI aCaracter = "‚";  aCaracter = &233; |FINSI;
           |SI aCaracter = "¡";  aCaracter = &237; |FINSI;
           |SI aCaracter = "¢";  aCaracter = &243; |FINSI;
           |SI aCaracter = "£";  aCaracter = &250; |FINSI;
           |SI aCaracter = "§";  aCaracter = &176; |FINSI;
           |SI aCaracter = "¦";  aCaracter = &170; |FINSI;
           aCadena = aCadena + aCaracter;
     |SIGUE;
     aAsigna = aCadena;

     |PARA;  |SI;  |HACIENDO;
          |WORD_ASIGNA_TEXTO nWord, aIdenti, aAsigna;
          |SI FSalida ! 0;  |SAL;  |FINSI;
     |SIGUE;
|FPRC;

|PROCESO CargaVariables;
     aAsigna = #715#1;  |HAZ QuitaRaros;
     aIdenti = "!#1#";
     |HAZ Asigna;

     |SI enSwDeDonde = 1;
         aAsigna = "";
         |SI #715#39 ! "  ";  aAsigna = #715#39;  |FINSI;

         aAsigna = aAsigna + " " + #715#40;  |QBF aAsigna;

         |SI #715#41 ! "     ";  aAsigna = aAsigna + ", " + #715#41;  |QBF aAsigna;  |FINSI;
         |SI #715#42 ! "   ";    aAsigna = aAsigna + " "  + #715#42;  |QBF aAsigna;  |FINSI;
         |SI #715#43 ! "  ";     aAsigna = aAsigna + " "  + #715#43;  |QBF aAsigna;  |FINSI;
         |SI #715#44 ! "  ";     aAsigna = aAsigna + " "  + #715#44;  |QBF aAsigna;  |FINSI;

         aIdenti = "!#2#";
         |HAZ Asigna;

         aAsigna = "";
         aAsigna = (("00" + #715#45) % -102) + (("000" + #715#46) % -103);
         aAsigna = aAsigna + " " + #715#47;  |QBF aAsigna;

         aIdenti = "!#3#";
         |HAZ Asigna;

         aAsigna = "(" + #715#48;                  |QBF aAsigna;
         aAsigna = aAsigna + ")";
         |SI aAsigna = "()";  aAsigna = "";  |FINSI;
         aIdenti = "!#4#";
         |HAZ Asigna;
     |SINO;
         aAsigna = "";
         |SI #715#51 ! "  ";  aAsigna = #715#51;  |FINSI;

         aAsigna = aAsigna + " " + #715#52;  |QBF aAsigna;

         |SI #715#53 ! "     ";  aAsigna = aAsigna + ", " + #715#53;  |QBF aAsigna;  |FINSI;
         |SI #715#54 ! "   ";    aAsigna = aAsigna + " "  + #715#54;  |QBF aAsigna;  |FINSI;
         |SI #715#55 ! "  ";     aAsigna = aAsigna + " "  + #715#55;  |QBF aAsigna;  |FINSI;
         |SI #715#56 ! "  ";     aAsigna = aAsigna + " "  + #715#56;  |QBF aAsigna;  |FINSI;

         aIdenti = "!#2#";
         |HAZ Asigna;

         aAsigna = "";
         aAsigna = (("00" + #715#57) % -102) + (("000" + #715#58) % -103);
         aAsigna = aAsigna + " " + #715#59;  |QBF aAsigna;

         aIdenti = "!#3#";
         |HAZ Asigna;

         aAsigna = "(" + #715#60;                  |QBF aAsigna;
         aAsigna = aAsigna + ")";
         |SI aAsigna = "()";  aAsigna = "";  |FINSI;
         aIdenti = "!#4#";
         |HAZ Asigna;
     |FINSI;

     aAsigna = #1#4;  |QBF aAsigna;
     eaAlfa  = #0#34;  |HAZ SacaFecha;
     aAsigna = aAsigna + ", " + eaAlfa;

     aIdenti = "!#5#";
     |HAZ Asigna;

     aAsigna = #1#1;  |QBF aAsigna;
     aIdenti = "!#NOMASESORIA#";
     |HAZ Asigna;

     aAsigna = #1#2;  |QBF aAsigna;
     aIdenti = "!#DESASESORIA#";
     |HAZ Asigna;

     aAsigna = #0#33;
     aIdenti = "!#EJERCICIO#";
     |HAZ Asigna;

     aAsigna = #0#35;
     aIdenti = "!#DIAFIN#";
     |HAZ Asigna;

     eaImporte = nResultado;  |HAZ Importe;
     aAsigna   = eaImporte;
     aIdenti = "!#IMPORTE#";
     |HAZ Asigna;

     aAsigna   = "";
     |SI eaNomEnt ! "";
         aAsigna   = "ENTIDAD..:   " + eaNomEnt;
     |FINSI;
     aIdenti = "!#ENTIDAD#";
     |HAZ Asigna;

     aAsigna   = "";
     |SI eaNomOfi ! "";
         aAsigna   = "SUCURSAL:   " + eaNomOfi;
     |FINSI;
     aIdenti = "!#SUCURSAL#";
     |HAZ Asigna;

     aAsigna   = "";
     |SI eaCuenta ! "";
         aAsigna   = "CUENTA...:   " + eaCuenta;
     |FINSI;
     aIdenti = "!#CUENTA#";
     |HAZ Asigna;
|FPRC;

|PROCESO isacceso;
     |SI #100#5 ! "225";
         #159#0 = #100#0;
         #159#1 = #100#1;
         |LEE 040#159=;
         |SI FSalida ! 0;  |DDEFECTO #159;  |FINSI;

         nResultado = #159#105;
     |SINO;
         #191#0 = #100#0;
         #191#1 = #100#1;
         |LEE 040#191=;
         |SI FSalida ! 0;  |DDEFECTO #191;  |FINSI;

         nResultado = #191#123;
     |FINSI;

     |SI nResultado [ 0;  |ACABA;  |FINSI;

     enCodCli = #100#0;
     |HAZ LeePersona;

     |HAZ CargaVarBanco;

     |QBF eaCuenta;

     |SI eaCuenta = "";  |ACABA;  |FINSI;
     |HAZ AbreWord;

     |MENSA "      Asignando Campos, espere un Momento.";

     |HAZ CargaVariables;

     |MENSA "      Imprimiendo, espere un Momento.";

     |WORD_SALVA nWord, "";

     |MENSA "      Cerrando Documento";

     eaAlfa = "Salvo el documento " + aAbre;
     |HAZ GeneraLog;

     |MENSA "      Imprimiendo Documento";

     eaPath = eaUnidadBasico + "/MODELOS/DOCUMENTOS/";
     |HAZ TraeDsPrintWord;

     eaAlfa = "Imprimo el documento " + aAbre;
     |HAZ GeneraLog;

     aAlfa        = eaUnidadBasico + "\MODELOS\DOCUMENTOS\wait dsprintword " + aAbre + " 0";
     |RSYSTEM aAlfa;

     |SI #0#36 = "S";  |HAZ GuardaDocumento;  |FINSI;

     |RFBORRA aAbre;

     eaAlfa = "Borro el documento " + aAbre;
     |HAZ GeneraLog;
|FPRC;

|DEFBUCLE isacceso;
     #100#1;
     6;
     anyo, #0#1, "S";
     anyo, #0#2, "S";
     ;
     NULL, isacceso;
|FIN;

|PROCESO Tipo3;  |TIPO 3;
     |ABRE #1;
     |LEE 000#1p;
     |SI FSalida ! 0;  |DDEFECTO #1;  |FINSI;
     |CIERRA #1;

     |ABRE #159;
     |ABRE #191;

     |SI #0#0 = "S";
         |BUCLE isacceso;
     |SINO;
         |PARA nPara = 3; |SI nPara [ 32; |HACIENDO nPara = nPara + 1;
               |SI #0nPara ! 0;
                   #0#1 = #0nPara;
                   #0#2 = #0nPara;
                   |BUCLE isacceso;
               |FINSI;
         |SIGUE;
     |FINSI;

     |CIERRA #159;
     |CIERRA #191;
|FPRC;


|PROGRAMA;
     |HAZ LeeUnidadBasico;

     |CLS;
     |CABEZA "Carta Pago declaracion Impuesto de Sociedades";

     |SUB_EJECUTA_NP ":basica/wordm000;CREALOS";

     |DBASS aPathDoc; |QBF aPathDoc;
     aPathDoc = aPathDoc + "basica/word/";

     aAbre = aPathDoc + "word0065.doc";
     |XABRE "E", aAbre, nHandle;
     |SI FSalida < 0;
         |MENAV "     No tiene instalado la plantilla de la Carta, instale ipbasica";
         |ACABA;
     |FINSI;

     |DDEFECTO #0;
     #0#0  = "S";
     #0#1  = empresa;
     #0#2  = empresa;
     #0#33 = anyo;
     aAlfa = "20.07." + #0#33;
     #0#35 = aAlfa;

     |PINPA #0#0;
     |PINDA #0#0;
     |ENDATOS #1#0;
|FPRO;
