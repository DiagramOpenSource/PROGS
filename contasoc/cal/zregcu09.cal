|FICHEROS;
     zregcu09;
|FIN;

|VARIABLES;
     nCampo           = 0;
     nGrabo            = 0;
     nVentana         = 0;
     nSubPanta        = 0;
     nNume            = 0;

     aAlfa1           = "";
     aAlfa2           = "";

     {-1}aVent        = "";
         aVent1       = 0;
         aVent2       = 0;
         aVent3       = 0;
         aVent4       = 0;
         aVent5       = "";

     &enEmpresa       = 0;
     &enEjerCuadro    = 0;
|FIN;

|PROCESO Tipo12Cuadros;  |TIPO 12;
|FPRC;

|PROGRAMA;
     |VENTANA 0501, 1699, -1, 16, "Detalle de subvenciones, donaciones y legados";
     nVentana = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVentana;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |ABRE #zregcu09;
     #zregcu09#0 = enEmpresa;
     #zregcu09#1 = enEjerCuadro;
     |LEE 101#zregcu09.=;
     |SI FSalida ! 0;
         |DDEFECTO #zregcu09;
         #zregcu09#0 = enEmpresa;
         #zregcu09#1 = enEjerCuadro;
         |GRABA 020#zregcu09.b;
     |FINSI;

     |PINPA #0#zregcu09;
     nSubPanta = FSalida;

     nNume  = enEjerCuadro - 1;
     aAlfa1 = enEjerCuadro;
     aAlfa2 = nNume;

     aAlfa1 = "LABEL,{{2}}" + aAlfa1;
     aAlfa2 = "LABEL,{{2}}" + aAlfa2;

     |CONTROL_SIMPLE nSubPanta, aAlfa1, 0575, 0779, NULL;
     |CONTROL_SIMPLE nSubPanta, aAlfa2, 0591, 0995, NULL;

     |PINDA #0#zregcu09;
     |ENDATOS #1#zregcu09;
     |SI FSalida = 0;
         nGrabo = 0;
         |PARA nCampo = 2;  |SI nCampo [ 13;  |HACIENDO nCampo = nCampo + 1;
               |SI #zregcu09#nCampo# ! 0;
                   nGrabo = 1;  |SAL;
               |FINSI;
         |SIGUE;

         |SI nGrabo ! 0;
             |GRABA 020#zregcu09.a;
         |SINO;
             |BORRA 020#zregcu09.a;
         |FINSI;
     |FINSI;
     |LIBERA #zregcu09;
     |CIERRA #zregcu09;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVentana;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |PULSATECLA;

     |FINVENTANA nVentana;
|FPRO;
