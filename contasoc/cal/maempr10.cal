|FICHEROS;
     maempr10 #0;

     :/basica/def/agim0035 #135;
     :/basica/def/agim0135 #136;

     :/contagen/def/canempre #30;
     :/contagen/def/ca8ctrle #400;
     :/contagen/def/ca8comay #401;
     :/contagen/def/ca8m0000 #420;

     maempr17;
|FIN;

|VARIABLES;
   &empresa = 0;
   &anyo    = 0;
   &nombre  = "";
   &nif     = "";
   &modo    = 0;

   nEjer    = 0;
   nConta   = 0;

   FEstado  = 0;
   alfa1    = "";
   alfa2    = "";
   tecla    = "";
   avance   = "815";
   atras    = "814";

   aAlfa    = "";
   aAlfa1   = "";
   aAlfa2   = "";
   aAlfa3   = "";
   aAlfa4   = "";
   aAlfa5   = "";

   &enCalcCif    = 0;
   &eaVarDni     = "";
|FIN;

|INCLUYE verifech;
|INCLUYE varbal_i;

|PROGRAMA;
  |ABRE #0;
  |PINPA #0#0;
  |SI modo = 1; |O modo = 2; |HAZ pidedato; |FINSI;
  |SI modo = 4; |HAZ consulta; |FINSI;
  |CIERRA #0;
|FPRO;

|PROCESO Tipo66;  |TIPO 66;
     |SI #0#1 [ 2007;
         |ABRE #135;
         #135#0 = #0#17;
         |LEE 000#135=;

         |CONSULTA_CLAVE #1#135;
         |SI FSalida = 0;
             #0#17 = #135#0;
         |FINSI;

         |CIERRA #135;
     |SINO;
         |ABRE #136;
         #136#0 = #0#17;
         |LEE 000#136=;

         |CONSULTA_CLAVE #1#136;
         |SI FSalida = 0;
             #0#17 = #136#0;
         |FINSI;

         |CIERRA #136;
     |FINSI;

     |PINTA #0#17;
|FPRC;

|PROCESO LetraNif;  |TIPO 6;
     |SI FSalida = 10;
         enCalcCif = 1;
         eaVarDni  =  #0Campo;
         |HAZ CalculoDNI;
         #0Campo = eaVarDni;
         |PINTA #0Campo;
         |ERROR;
         |ACABA;
     |FINSI;

     eaVarDni  = #0Campo;
     enCalcCif = 0;
     |HAZ CalculoDNI;
     eaVarDni = (eaVarDni + "            ") % 112;
     |SI #0Campo ! eaVarDni;
         |CONFI "0000SEl Nif puede ser Erroneo. Desea Calcularlo.";
         |SI FSalida = 0;
             #0Campo = eaVarDni;  |PINTA #0Campo;
             |ERROR;
             |ACABA;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO maempr17;
     |ABRE #maempr17;
     #maempr17#1 = #0#0;
     #maempr17#0 = #0#1;
     |LEE 101#maempr17.=;
     |SI FSalida = 0;
          #maempr17#3 = #0#75;
          #maempr17#4 = #0#76;
          |GRABA 020#maempr17.a;
     |SINO;
          #maempr17#2 = #0#2;
          #maempr17#3 = #0#75;
          #maempr17#4 = #0#76;
          |GRABA 020#maempr17.n;
     |FINSI;
     |CIERRA #maempr17;
     |LIBERA #maempr17;
|FPRC;

|PROCESO pidedato;
  #0#0 = empresa;
  #0#1 = anyo;
  |LEE 110#0=;
  FEstado = FSalida;
  |PINDA #0#0;
  |SI anyo ] 2008; |HAZ LeeEmpresa; |FINSI;

  |SI modo = 1; |ENDATOS #1#0;  |FINSI;
  |SI modo = 2; |ENDATOS #2#0;  |FINSI;
  |SI FSalida = 0;
      #0#0 = empresa;
      #0#1 = anyo;
      #0#2 = nombre;
      #0#3 = nif;
      |SI FEstado = 0; |GRABA 020#0a; |SINO; |GRABA 020#0n; |FINSI;
      |LIBERA #0;

      |HAZ maempr17;
  |FINSI;
|FPRC;

|PROCESO consulta;
  avance = &255 + avance;
  atras = &255 + atras;
  #0#0 = empresa;
  #0#1 = anyo;
  |LEE 040#0=;
  |SI FSalida = 0; |HAZ pantalla1; |FINSI;
|FPRC;

|PROCESO pantalla1;
  |PINPA #0#0;
  |PINDA #0#0;
  |MENSA "0411  Av.Pag.= Siguiente Pantalla  Re.Pag. = Pantalla Anterior";
  |LEETECLA tecla;
  |SI tecla = avance; |HAZ pantalla2; |ACABA; |FINSI;
|FPRC;

|PROCESO pantalla2;
  |PINPA #1#0;
  |PINDA #1#0;
  |MENSA "0411  Av.Pag.= Siguiente Pantalla  Re.Pag. = Pantalla Anterior";
  |LEETECLA tecla;
  |SI tecla = atras;  |HAZ pantalla1; |ACABA; |FINSI;
  |SI tecla = avance; |HAZ pantalla3; |ACABA; |FINSI;
|FPRC;

|PROCESO pantalla3;
  |PINPA #2#0;
  |PINDA #2#0;
  |MENSA "0411  Av.Pag.= Siguiente Pantalla  Re.Pag. = Pantalla Anterior";
  |LEETECLA tecla;
  |SI tecla = atras;  |HAZ pantalla2; |ACABA; |FINSI;
  |SI tecla = avance;  |HAZ pantalla4; |ACABA; |FINSI;
|FPRC;

|PROCESO pantalla4;
  |PINPA #3#0;
  |PINDA #3#0;
  |MENSA "0411  Av.Pag.= Siguiente Pantalla  Re.Pag. = Pantalla Anterior";
  |LEETECLA tecla;
  |SI tecla = atras;  |HAZ pantalla3; |ACABA; |FINSI;
|FPRC;

|PROCESO PintaEjercicio;  |TIPO 7;
     |PUSHV 0501 2380;
     |BLANCO 1028 1875;
     |CUADRO 1028 1875;

     |ATRI R;
     |PINTA 1129, "               EJERCICIO                      ";
     |ATRI r;

     |ATRI S;
     |PINTA 1229, "Dos primeros digitos introducir el Ejercicio  ";
     |PINTA 1429, "En el tercer digito introducir :              ";
     |ATRI s;

     |ATRI I;
     |PINTA 1529, "   <1> Ejercicio entero comenzando el 01.01   ";
     |PINTA 1629, "   <2> Ejercicio entero no comenzando el 01.01";
     |PINTA 1729, "   <3> Ejercicio incompleto.                  ";
     |ATRI i;

     |LEETECLA alfa1;
     |POPV;
|FPRC;

|PROCESO Ant_Modelo; |TIPO 7;
  |C_M #0#18,1,"S";
  |SI #0#1 ] 2008;
      |C_M #0#18,1,"N";
      #0#18 = "N"; |PINTA #0#18;
  |FINSI;
|FPRC;

|PROCESO Modelo;
     |SI #0#18 ! "P";  |ACABA;  |FINSI;

     |SI #0#1 < 2003;
         |MENAV "0000 Activado en Ejercicios igual o superior al 2003";
         |ERROR;
     |FINSI;
|FPRC;

|PROCESO copostal; |TIPO 0;
  alfa1 = ("00"  + #0#10) % -102;
  alfa2 = ("000" + #0#11) % -103;
  #0#12 = alfa1 + alfa2;
|FPRC;

|PROCESO Complementaria;
  |C_M #0#99,  1, "S";
  |C_M #0#100, 1, "S";
  |SI #0#98  = "N";
      #0#99  = "";  |PINTA #0#99;
      #0#100 = "";  |PINTA #0#100;
      |C_M #0#99,  1, "N";
      |C_M #0#100, 1, "N";
  |FINSI;
|FPRC;

|PROCESO EjercicioIni;
     aAlfa = #0#16 % -101;

     |SI aAlfa = "1";
         aAlfa1 = "01.01." + #0#1;
         aAlfa2 = #0#31;
         |SI aAlfa1 ! aAlfa2;
             aAlfa3 = "0000 Fecha Inicio Ejercicio Incorrecta. Debe ser: " + aAlfa1;
             |MENAV aAlfa3;
             |ERROR;
         |FINSI;
     |FINSI;

     |SI aAlfa = "2";
         aAlfa1 = "01.01." + #0#1;
         aAlfa2 = #0#31;
         |SI aAlfa1 = aAlfa2;
             aAlfa3 = "0000 Fecha Inicio Ejercicio Incorrecta. Debe ser distinta a: " + aAlfa1;
             |MENAV aAlfa3;
             |ERROR;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO EjercicioFin;
     aAlfa = #0#16 % -101;

     |SI aAlfa = "1";
         aAlfa1 = "31.12." + #0#1;
         aAlfa2 = #0#32;
         |SI aAlfa1 ! aAlfa2;
             aAlfa3 = "0000 Fecha Fin Ejercicio Incorrecta. Debe ser: " + aAlfa1;
             |MENAV aAlfa3;
             |ERROR;
         |FINSI;
     |FINSI;

     |SI aAlfa = "2";
         aAlfa1 = "31.12." + #0#1;
         aAlfa2 = #0#32;
         |SI aAlfa1 = aAlfa2;
             aAlfa3 = "0000 Fecha Fin Ejercicio Incorrecta. Debe ser distinta a: " + aAlfa1;
             |MENAV aAlfa3;
             |ERROR;
         |FINSI;
     |FINSI;

     |SI aAlfa = "3";
         aAlfa1 = #0#31;
         aAlfa2 = #0#32;
         aAlfa3 = "01.01." + #0#1;
         aAlfa4 = "31.12." + #0#1;

         |SI aAlfa1 = aAlfa3;  |Y aAlfa2 = aAlfa4;
             aAlfa5 = "0000 El Ejercicio Economico debe ser incompleto.";
             |MENAV aAlfa5;
             |ERROR;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO LeeEmpresa;
     aAlfa1 = #0#1;
     aAlfa1 = aAlfa1 % -102;
     nEjer = aAlfa1;

     nConta = 0;
     |ABRE #30;
     |SELECT #4#30;
     #30#2  = #0#0;
     #30#26 = nEjer;
     |LEE 040#30=;
     |SI FSalida = 0;
         |ABRE #400;
         |DDEFECTO #400;
         #400#0 = #30#2;
         #400#1 = #30#3;
         |LEE 001#400=;
         |SI FSalida = 0;
             eNum2008 = #400#2;
             |ABRE #401;
             #401#0 = eNum2008;
             |LEE 041#401=;
             |SI FSalida = 0;
                 eTip2008 = #401#3;
                 nConta   = 1;
             |FINSI;
             |CIERRA #401;
         |FINSI;
         |CIERRA #400;
    |FINSI;
    |CIERRA #30;
|FPRC;


|PROCESO LeeBal; |TIPO 6;
 |SI #0#1 < 2008; |ACABA; |FINSI;

 |ABRE #420;
 |DDEFECTO #420;
 #420#0 = #0Campo;
 |LEE 001#420=;
 |SI FSalida ! 0;
     |CIERRA #420;
     |MENAV  "    No existe Estructura de Balance";
     |ERROR;
     |ACABA;
 |FINSI;
 |CIERRA #420;

 |SI nConta = 0; |ACABA; |FINSI;
 |SI eTip2008 = 2;
      |SI #420#2 = 1; |O #420#2 = 2;
          |MENAV "    Error. Plan Contable de Empresa PYME, NO puede realizar estos Balances";
          |ERROR;
      |FINSI;
 |FINSI;
|FPRC;

|PROCESO LeeRes; |TIPO 6;
 |SI #0#1 < 2008; |ACABA; |FINSI;

 |ABRE #420;
 |DDEFECTO #420;
 #420#0 = #0Campo;
 |LEE 001#420=;
 |SI FSalida ! 0;
     |CIERRA #420;
     |MENAV  "    No existe Estructura de Cuenta de Resultados";
     |ERROR;
     |ACABA;
 |FINSI;
 |CIERRA #420;

 |SI nConta = 0; |ACABA; |FINSI;

 |SI eTip2008 = 2;
      |SI #420#2 = 1; |O #420#2 = 2;
          |MENAV "    Error. Plan Contable de Empresa PYME, NO puede realizar esta Cta de Resultados";
          |ERROR;
      |FINSI;
  |FINSI;
|FPRC;

|PROCESO RefrescaCampos;
     |SI #0Campo ! Contenido;
          |SI Campo = 75;
               #0#86 = #0#75;
          |FINSI;
          |SI Campo = 76;
               #0#88 = #0#76;
          |FINSI;
          |SI Campo = 86;
               #0#75 = #0#86;
          |FINSI;
          |SI Campo = 88;
               #0#76 = #0#88;
          |FINSI;
     |FINSI;
|FPRC;
