|FICHEROS;
     isom0200;
     isom0201;
     isom0202,MANTE;
     isom0203;
     isom0204;
     isom0205;

     isoz0003;

     trabajo@;
|FIN;

|VARIABLES;
     nID            = 0;
     nBoton         = -1;
     nVentTrab      = -1;
     nVentRev       = -1;
     nVentana003    = -1;
     nPanta003      = -1;
     nBtnAlta       = -1;
     nBtnModi       = -1;
     nBtnBaja       = -1;
     nBtnReva       = -1;
     nBtnSal        = -1;
     nCampo         = -1;
     nRango         = -1;
     nLinea         = -1;
     nGrid201       = -1;
     nIdSalir       = -1;
     nCoef          = -1;
     nIDE           = -1;
     nHay           = 0;

     aAlfa          = "";
     aID            = "";
     aQueBoton      = "";
     aEsc1          = "";
     aEsc2          = "";
     aRetu          = "";
     aTecla         = "";
     aMas           = "";

  {2}nNivel         = 0;

 {-1}aVent          = "";
     aVent1         = 0;
     aVent2         = 0;
     aVent3         = 0;
     aVent4         = 0;
     aVent5         = "";

  {20}aPopup        = "";

     &enEmpresa     = 0;
     &enAnyo        = 0;
     &enModoReg     = 0;
|FIN;

|PROCESO QuitaBotonSalir;
     nIdSalir = 0;
     InNivel   = nNivel1<-;
     nNivel1   = 0;
     |ESPECIFICA "IDBOTONTECLA", nNivel;

     nIdSalir = nNivel2;
     |ESTADO_CONTROL nIdSalir, "HIDE";
|FPRC;

|PROCESO PonBotonSalir;
     |ESTADO_CONTROL nIdSalir, "SHOW";
|FPRC;

|PROCESO FinVentana003;
     |SI nVentana003 ! -1;
         |FINVENTANA nVentana003;
         nVentana003 = -1;
     |FINSI;

     |PULSATECLA;
|FPRC;

|PROCESO Tipo12Fw003;  |TIPO 12;
     |SI FSalida = 7;
         |ERROR;
         |ACABA;
     |FINSI;

     |SI #isoz0003#12 < 0;
         |MENAV "0000Casilla (324 - Diferencia) tiene importe negativo. Por favor revise los datos";
         |ERROR;
         |ACABA;
     |FINSI;

     |HAZ TotalFw003;
     nBoton    = -1;
|FPRC;

|PROCESO TotalFw003;
     #isom0202#0 = #isom0201#0;
     #isom0202#1 = #isom0201#1;
     #isom0202#2 = #isom0201#2;
     #isom0202#3 = 31;
     |LEE 000#isom0202.=;
     |SI FSalida ! 0;  |DDEFECTO #isom0202;  |FINSI;

     #isoz0003#12 = (#isom0202#8 - #isom0202#9) - (#isom0202#5 - #isom0202#6);

     #isoz0003#14 = #isoz0003#12 * #isoz0003#13;

     #isoz0003#16 = #isoz0003#14 - #isoz0003#15;
     |SI #isoz0003#16 < 0; #isoz0003#16 = 0;  |FINSI;

     #isoz0003#19 = (#isoz0003#16 - #isoz0003#17) % #isoz0003#18;
     |SI #isoz0003#19 < 0; #isoz0003#19 = 0;  |FINSI;

     |PINTA #isoz0003#12;
     |PINTA #isoz0003#14;
     |PINTA #isoz0003#16;
     |PINTA #isoz0003#19;
|FPRC;

|PROCESO Tipo0C18Fw003;  |TIPO 0;
     |POSICIONATE #isoz0003#3;
|FPRC;

|PROCESO Tipo66C3Fw003;  |TIPO 66;
     aAlfa = "|C004WID";
     aID   = #isoz0003#aAlfa#;
     nID   = aID;

     aPopup1  = nID;  || Si fuera -1 en la posicion
     aPopup2  = 11;
     aPopup3  = "10 Inmovilizado material. Terrenos y bienes naturales.";
     aPopup4  = "11 Inmovilizado material. Terrenos y bienes naturales en regimen de arrendamiento financiero.";
     aPopup5  = "20 Inmovilizado material. Construcciones.";
     aPopup6  = "21 Inmovilizado material. Construcciones en regimen de arrendamiento financiero.";
     aPopup7  = "30 Resto del inmovilizado material.";
     aPopup8  = "31 Resto del inmovilizado material en regimen de arrendamiento financiero.";
     aPopup9  = "40 Inversiones inmobiliarias. Terrenos y bienes naturales.";
     aPopup10 = "41 Inversiones inmobiliarias. Terrenos y bienes naturales en regimen de arrendamiento financiero.";
     aPopup11 = "50 Inversiones inmobiliarias. Construcciones.";
     aPopup12 = "51 Inversiones inmobiliarias. Construcciones en regimen de arrendamiento financiero.";
     aPopup13 = "60 Inmovilizado intangible (acuerdos de concesion de las empresas concesionarias).";

     IaPopup  = aPopup1  <-;
     |ESPECIFICA "TRACKPOPUP", aPopup;
     |SI FSalida ! 0;
         IaPopup  = aPopup3  <-;
         IaPopup  = IaPopup + (FSalida - 1);

         aAlfa = aPopup % 102;  #isoz0003#4 = aAlfa;
     |FINSI;

     |PINTA #isoz0003#4;
|FPRC;

|PROCESO Tipo0C3Fw003;  |TIPO 0;
     #isoz0003#5 = "";

     |SI #isoz0003#4 = 10;
         #isoz0003#5 = "Inmovilizado material. Terrenos y bienes naturales.";
     |FINSI;

     |SI #isoz0003#4 = 11;
         #isoz0003#5 = "Inmovilizado material. Terrenos y bienes naturales en r‚gimen de arrendamiento financiero.";
     |FINSI;

     |SI #isoz0003#4 = 20;
         #isoz0003#5 = "Inmovilizado material. Construcciones.";
     |FINSI;

     |SI #isoz0003#4 = 21;
         #isoz0003#5 = "Inmovilizado material. Construcciones en r‚gimen de arrendamiento financiero.";
     |FINSI;

     |SI #isoz0003#4 = 30;
         #isoz0003#5 = "Resto del inmovilizado material.";
     |FINSI;

     |SI #isoz0003#4 = 31;
         #isoz0003#5 = "Resto del inmovilizado material en r‚gimen de arrendamiento financiero.";
     |FINSI;

     |SI #isoz0003#4 = 40;
         #isoz0003#5 = "Inversiones inmobiliarias. Terrenos y bienes naturales.";
     |FINSI;

     |SI #isoz0003#4 = 41;
         #isoz0003#5 = "Inversiones inmobiliarias. Terrenos y bienes naturales en r‚gimen de arrendamiento financiero.";
     |FINSI;

     |SI #isoz0003#4 = 50;
         #isoz0003#5 = "Inversiones inmobiliarias. Construcciones.";
     |FINSI;

     |SI #isoz0003#4 = 51;
         #isoz0003#5 = "Inversiones inmobiliarias. Construcciones en r‚gimen de arrendamiento financiero.";
     |FINSI;

     |SI #isoz0003#4 = 60;
         #isoz0003#5 = "Resto del inmovilizado material en r‚gimen de arrendamiento financiero.";
     |FINSI;

     |SI #isoz0003#4 = 31;
         #isoz0003#5 = "Inmovilizado intangible (acuerdos de concesi¢n de las empresas concesionarias).";
     |FINSI;

     aAlfa = #isoz0003#5;  |QBF aAlfa;
     |SI aAlfa = "";
         |MENAV "0000Clave inexistente.";
         |ERROR;
         |ACABA;
     |FINSI;

     |PINTA #isoz0003#5;

     |C_M #isoz0003#8,  1, "S";
     |C_M #isoz0003#9,  1, "S";
     |C_M #isoz0003#10, 1, "S";
     |C_M #isoz0003#11, 1, "S";
     |C_M #isoz0003#20, 1, "S";

     |SI #isoz0003#4 = 30;  |O #isoz0003#4 = 31;  |O #isoz0003#4 = 60;
         |C_M #isoz0003#8,  1, "N";  #isoz0003#8  = "";
         |C_M #isoz0003#9,  1, "N";  #isoz0003#9  = "";
         |C_M #isoz0003#10, 1, "N";  #isoz0003#10 = 0;
         |C_M #isoz0003#11, 1, "N";  #isoz0003#11 = 0;
         |C_M #isoz0003#20, 1, "N";  #isoz0003#20 = "0";
     |FINSI;

     |PINTA #isoz0003#8;
     |PINTA #isoz0003#9;
     |PINTA #isoz0003#10;
     |PINTA #isoz0003#11;
     |PINTA #isoz0003#20;
|FPRC;

|PROCESO Tipo7C3Fw003;  |TIPO 7;
     |SI nBoton ! -1;  |ACABA;  |FINSI;

     aQueBoton = "|X001";
     aID       = #isoz0003#aQueBoton#;
     nBoton    = aID;
     |ESTADO_CONTROL nBoton, "HIDE";
|FPRC;

|PROCESO Tipo7C20Fw003;  |TIPO 7;
     |VENTANA 2220, 2889, -1, 1, "";
     nVentana003 = FSalida;

     |CONTROL_SIMPLE 0, "LABEL,{{15}}SITUACION", 2222, 2269, NULL;
     |CONTROL_SIMPLE 0, "LABEL,{{16}}1 .- Vivienda o plaza de garaje situada en cualquier punto del territorio espa¤ol,", 2323, 2389, NULL;
     |CONTROL_SIMPLE 0, "LABEL,{{16}}      con excepcion del Pais Vasco y Navarra", 2423, 2489, NULL;
     |CONTROL_SIMPLE 0, "LABEL,{{16}}2 .- Vivienda o plaza de garaje situada en el Pais Vasco y Navarra", 2523, 2589, NULL;
     |CONTROL_SIMPLE 0, "LABEL,{{16}}3 .- Vivienda o plaza de garaje situada en cualquier punto del territorio espa¤ol,", 2623, 2689, NULL;
     |CONTROL_SIMPLE 0, "LABEL,{{16}}     pero sin tener asignada referencia catastral.", 2723, 2789, NULL;
     |CONTROL_SIMPLE 0, "LABEL,{{16}}4 .- Vivienda o plaza de garaje situada en el extranjero. ", 2823, 2889, NULL;

     |PTEC 802;  |PAUSA;
|FPRC;

|PROCESO Tipo0C20Fw003;  |TIPO 0;
     |HAZ FinVentana003;
|FPRC;

|PROCESO Baja200;
     #isom0200#0 = enEmpresa;
     #isom0200#1 = enAnyo;
     #isom0200#2 = "";
|FPRC;

|PROCESO Alta200;
     #isom0200#0 = enEmpresa;
     #isom0200#1 = enAnyo;
     #isom0200#3 = "zzzzzzzzz";
|FPRC;

|PROCESO Tipo66C0Fw003;  |TIPO 66;
     |ABRE #isom0200;
     |CONSULTA_F_CLAVE #2#isom0200, Baja200, Alta200;
     |SI FSalida = 0;
         #isoz0003#0  = #isom0200#3;
         #isoz0003#1  = #isom0200#4;
         #isoz0003#18 = #isom0200#5;

         |PINTA #isoz0003#0;
         |PINTA #isoz0003#1;
         |PINTA #isoz0003#18;
     |FINSI;
     |CIERRA #isom0200;
|FPRC;

|PROCESO Tipo0C0Fw003;  |TIPO 0;
     |SI #isoz0003#0 = "         ";
         #isoz0003#1  = "";
         #isoz0003#18 = 100;

         |PINTA #isoz0003#1;
         |PINTA #isoz0003#18;

         |HAZ TotalFw003;

         |ACABA;
     |FINSI;

     |ABRE #isom0200;
     |SELECT #2#isom0200;
     #isom0200#0 = enEmpresa;
     #isom0200#1 = enAnyo;
     #isom0200#3 = #isoz0003#0;
     |LEE 000#isom0200.=;
     |SI FSalida ! 0;
         |CIERRA #isom0200;
         |MENAV "0000La entidad no ha sido dada de alta en el punto anterior.";
         |ERROR;
         |ACABA;
     |FINSI;
     |CIERRA #isom0200;

     #isoz0003#1  = #isom0200#4;
     #isoz0003#18 = #isom0200#5;

     |PINTA #isoz0003#1;
     |PINTA #isoz0003#18;

     |HAZ TotalFw003;
|FPRC;

|PROCESO isom0202Calc;
     #trabajo@#5  = #trabajo@#5  + #isom0202#5;
     #trabajo@#6  = #trabajo@#6  + #isom0202#6;
     #trabajo@#8  = #trabajo@#8  + #isom0202#8;
     #trabajo@#9  = #trabajo@#9  + #isom0202#9;
|FPRC;

|DEFBUCLE isom0202Calc;
     #isom0202#1;
     ;
     #isom0201#0, #isom0201#1, #isom0201#2, 01;
     #isom0201#0, #isom0201#1, #isom0201#2, 30;
     be;
     NULL, isom0202Calc;
|FIN;

|PROCESO Calculo;
     |DDEF aMas;
     aMas = aMas + "isom0202.mas";
     |CARGA_DEF aMas, trabajo@;

     |ABRE #trabajo@;
     #trabajo@#0 = #isom0202#0;
     #trabajo@#1 = #isom0202#1;
     #trabajo@#2 = #isom0202#2;
     #trabajo@#3 = 31;
     |LEE 101#trabajo@.=;
     |SI FSalida = 0;
         #trabajo@#5  = 0;
         #trabajo@#6  = 0;
         #trabajo@#8  = 0;
         #trabajo@#9  = 0;

         |BUCLE isom0202Calc;

         |GRABA 020#trabajo@.a;
         |LIBERA #trabajo@;

         #isoz0003#12 = #trabajo@#8 - #trabajo@#9 - #trabajo@#5 - #trabajo@#6;
         |PINTA #isoz0003#16;
     |FINSI;
     |CIERRA #trabajo@;

     |DESCARGA_DEF #trabajo@;
|FPRC;

|PROCESO PintaDatosPantalla;
     #isoz0003#0  = #isom0201#21;
     #isoz0003#1  = #isom0201#22;

     |PARA nCampo = 3;  |SI nCampo [ 20;  |HACIENDO nCampo = nCampo + 1;
           #isoz0003#nCampo# = #isom0201#nCampo#;

           |C_V #isoz0003#nCampo#, 0, aAlfa;
           |SI aAlfa = "S";  |PINTA #isoz0003#nCampo#;  |FINSI;

           |C_M #isoz0003#nCampo#, 1, "N";
     |SIGUE;

     |PINTA #isoz0003#0;
     |PINTA #isoz0003#1;
|FPRC;

|PROCESO CargaFantasma;
     |C_M #isoz0003#0,  1, "S";
     |C_M #isoz0003#3,  1, "S";
     |C_M #isoz0003#4,  1, "S";
     |C_M #isoz0003#6,  1, "S";
     |C_M #isoz0003#7,  1, "S";
     |C_M #isoz0003#8,  1, "S";
     |C_M #isoz0003#9,  1, "S";
     |C_M #isoz0003#10, 1, "S";
     |C_M #isoz0003#11, 1, "S";
     |C_M #isoz0003#13, 1, "S";
     |C_M #isoz0003#15, 1, "S";
     |C_M #isoz0003#17, 1, "S";
     |C_M #isoz0003#20, 1, "S";

     #isoz0003#0  = #isom0201#21;
     #isoz0003#1  = #isom0201#22;

     |PARA nCampo = 3;  |SI nCampo [ 20;  |HACIENDO nCampo = nCampo + 1;
           #isoz0003#nCampo# = #isom0201#nCampo#;

           |C_V #isoz0003#nCampo#, 0, aAlfa;
           |SI aAlfa = "S";  |PINTA #isoz0003#nCampo#;  |FINSI;
     |SIGUE;
|FPRC;

|PROCESO DescargaFantasma;
     |PARA nCampo = 3;  |SI nCampo [ 20;  |HACIENDO nCampo = nCampo + 1;
           #isom0201#nCampo# = #isoz0003#nCampo#;
     |SIGUE;

     #isom0201#21 = #isoz0003#0;
     #isom0201#22 = #isoz0003#1;

     |HAZ PintaDatosPantalla;
|FPRC;

|PROCESO Graba202;
     nIDE = nIDE + 1;

     #isom0202#0 = #isom0201#0;
     #isom0202#1 = #isom0201#1;
     #isom0202#2 = #isom0201#3;
     #isom0202#3 = nIDE;
     |LEE 101#isom0202.=;
     |SI FSalida ! 0;
         |DDEFECTO #isom0202;
         #isom0202#0 = #isom0201#0;
         #isom0202#1 = #isom0201#1;
         #isom0202#2 = #isom0201#2;
         #isom0202#3 = nIDE;
         |GRABA 020#isom0202.b;
     |FINSI;

     #isom0202#4 = aAlfa;
     #isom0202#7 = nCoef;
     |GRABA 020#isom0202.a;
     |LIBERA #isom0202;
|FPRC;

|PROCESO CargaRevalorizacion;
     nIDE   = 0;

     aAlfa = "1983 y anteriores";  nCoef = 2.2946;  |HAZ Graba202;
     aAlfa = "      1984       ";  nCoef = 2.0836;  |HAZ Graba202;
     aAlfa = "      1985       ";  nCoef = 1.9243;  |HAZ Graba202;
     aAlfa = "      1986       ";  nCoef = 1.8116;  |HAZ Graba202;
     aAlfa = "      1987       ";  nCoef = 1.7258;  |HAZ Graba202;
     aAlfa = "      1988       ";  nCoef = 1.6487;  |HAZ Graba202;
     aAlfa = "      1989       ";  nCoef = 1.5768;  |HAZ Graba202;
     aAlfa = "      1990       ";  nCoef = 1.5151;  |HAZ Graba202;
     aAlfa = "      1991       ";  nCoef = 1.4633;  |HAZ Graba202;
     aAlfa = "      1992       ";  nCoef = 1.4309;  |HAZ Graba202;
     aAlfa = "      1993       ";  nCoef = 1.4122;  |HAZ Graba202;
     aAlfa = "      1994       ";  nCoef = 1.3867;  |HAZ Graba202;
     aAlfa = "      1995       ";  nCoef = 1.3312;  |HAZ Graba202;
     aAlfa = "      1996       ";  nCoef = 1.2679;  |HAZ Graba202;
     aAlfa = "      1997       ";  nCoef = 1.2396;  |HAZ Graba202;
     aAlfa = "      1998       ";  nCoef = 1.2235;  |HAZ Graba202;
     aAlfa = "      1999       ";  nCoef = 1.2150;  |HAZ Graba202;
     aAlfa = "      2000       ";  nCoef = 1.2089;  |HAZ Graba202;
     aAlfa = "      2001       ";  nCoef = 1.1839;  |HAZ Graba202;
     aAlfa = "      2002       ";  nCoef = 1.1696;  |HAZ Graba202;
     aAlfa = "      2003       ";  nCoef = 1.1499;  |HAZ Graba202;
     aAlfa = "      2004       ";  nCoef = 1.1389;  |HAZ Graba202;
     aAlfa = "      2005       ";  nCoef = 1.1238;  |HAZ Graba202;
     aAlfa = "      2006       ";  nCoef = 1.1017;  |HAZ Graba202;
     aAlfa = "      2007       ";  nCoef = 1.0781;  |HAZ Graba202;
     aAlfa = "      2008       ";  nCoef = 1.0446;  |HAZ Graba202;
     aAlfa = "      2009       ";  nCoef = 1.0221;  |HAZ Graba202;
     aAlfa = "      2010       ";  nCoef = 1.0100;  |HAZ Graba202;
     aAlfa = "      2011       ";  nCoef = 1.0100;  |HAZ Graba202;
     aAlfa = "      2012       ";  nCoef = 1.0000;  |HAZ Graba202;
     aAlfa = "    Totales      ";  nCoef = 0;       |HAZ Graba202;
|FPRC;

|PROCESO Alta;
     |ESTADO_CONTROL nBtnAlta, "DISABLE";
     |ESTADO_CONTROL nBtnModi, "DISABLE";
     |ESTADO_CONTROL nBtnBaja, "DISABLE";
     |ESTADO_CONTROL nBtnReva, "ENABLE";

     |HAZ QuitaBotonSalir;

     |ESTADO_CONTROL nGrid201, "DISABLE";

     |PULSATECLA;

     #isom0201#0 = enEmpresa;
     #isom0201#1 = enAnyo;
     #isom0201#2 = 9999;
     |LEE 000#isom0201.];
     |SI FSalida = 0;
         |LEE 000#isom0201.a;
     |SINO;
         |LEE 000#isom0201.u;
     |FINSI;

     nLinea = 1;
     |SI FSalida = 0;  |Y #isom0201#0 = enEmpresa;
                       |Y #isom0201#1 = enAnyo;
         nLinea = #isom0201#2 + 1;
     |FINSI;

     |DDEFECTO #isom0201;

     #isom0201#0 = enEmpresa;
     #isom0201#1 = enAnyo;
     #isom0201#2 = nLinea;

     enModoReg = 1;
     |HAZ CargaFantasma;
     |HAZ CargaRevalorizacion;

     |ENDATOS #1#isoz0003;
     |SI FSalida = 0;
         |HAZ DescargaFantasma;

         |GRABA 020#isom0201.n;
     |FINSI;
     |LIBERA #isom0201;

     |ESTADO_CONTROL nBtnAlta, "ENABLE";

     |ESTADO_CONTROL nBtnReva, "DISABLE";

     |HAZ PonBotonSalir;

     |ESTADO_CONTROL nGrid201, "ENABLE";
     |REFRESCACONTROL nGrid201;

     FSalida = 1;  |HAZ Evento201;

     |FOCOTECLADO nGrid201;
|FPRC;

|PROCESO Modificar;
     |LEE 101#isom0201.=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |ESTADO_CONTROL nBtnAlta,  "DISABLE";
     |ESTADO_CONTROL nBtnModi,  "DISABLE";
     |ESTADO_CONTROL nBtnBaja,  "DISABLE";
     |ESTADO_CONTROL nBtnReva,  "ENABLE";

     |HAZ QuitaBotonSalir;

     |ESTADO_CONTROL nGrid201, "DISABLE";

     |HAZ CargaFantasma;
     |ENDATOS #2#isoz0003;
     |SI FSalida = 0;
         |HAZ DescargaFantasma;

         |GRABA 020#isom0201.a;
     |FINSI;
     |LIBERA #isom0201;

     |ESTADO_CONTROL nBtnAlta, "ENABLE";
     |ESTADO_CONTROL nBtnModi, "ENABLE";
     |ESTADO_CONTROL nBtnBaja, "ENABLE";

     |ESTADO_CONTROL nBtnReva, "DISABLE";

     |HAZ PonBotonSalir;

     |ESTADO_CONTROL nGrid201, "ENABLE";
     |REFRESCACONTROL nGrid201;
     |FOCOTECLADO nGrid201;
|FPRC;

|PROCESO isom0202BAct;
     |BORRA 020#isom0202.a;
     |LIBERA #isom0202;
|FPRC;

|DEFBUCLE isom0202BAct;
     #isom0202#1;
     ;
     #isom0201#0, #isom0201#1, #isom0201#2, 01;
     #isom0201#0, #isom0201#1, #isom0201#2, 99;
     be;
     NULL, isom0202BAct;
|FIN;

|PROCESO Baja;
     |CONFI "0000 Confirma la baja del registro seleccionado";
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |LEE 101#isom0201.=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |BORRA 020#isom0201.a;
     |LIBERA #isom0201;

     |BUCLE isom0202BAct;

     |ESTADO_CONTROL nBtnModi,  "DISABLE";
     |ESTADO_CONTROL nBtnBaja,  "DISABLE";

     |REFRESCACONTROL nGrid201;
     |FOCOTECLADO nGrid201;
|FPRC;

|PROCESO BotonSalir;
     |PTEC 806;
|FPRC;

|PROCESO Baja202;
     #isom0202#0 = #isom0201#0;
     #isom0202#1 = #isom0201#1;
     #isom0202#2 = #isom0201#2;
     #isom0202#3 = 1;
|FPRC;

|PROCESO Alta202;
     #isom0202#0 = #isom0201#0;
     #isom0202#1 = #isom0201#1;
     #isom0202#2 = #isom0201#2;
     #isom0202#3 = 99;
|FPRC;

|PROCESO Evento202;
     |SI FSalida = 1;  |O FSalida = 2;
         |LEE 000#isom0202.=;
     |FINSI;
|FPRC;

|PROCESO Revalorizar;
     |VENTANA 0501, 3599, -1, 16, "Revalorizacion";
     nVentRev = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVentRev;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |PTEC 802;  |PAUSA;

     |CONTROL_SIMPLE 0, "BOTON,{{197}}Salir", 3588, 3598, BotonSalir;
     nBtnSal = FSalida;

     nRango = 1 + 4 + 8 + 16 + 128 + 256;

     |LINEAL_SIMPLE #1#isom0202, nRango, 0502, 3398, Baja202, Alta202, Evento202;

     |FIN_CONTROL_WINDOWS nBtnSal;

     |HAZ Calculo;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVentRev;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA nVentRev;

     |POSICIONATE #isoz0003#12;
     |PTEC 808;
     |PTEC 809;
     |PTEC 809;
|FPRC;

|PROCESO Evento201;
     |SI FSalida = 1;  |O FSalida = 2;
         |LEE 000#isom0201.=;

         |ESTADO_CONTROL nBtnModi, "ENABLE";
         |ESTADO_CONTROL nBtnBaja, "ENABLE";

         |PULSATECLA;

         enModoReg = 4;
         |HAZ PintaDatosPantalla;
     |FINSI;

     |SI FSalida = 4;
         |HAZ Modificar;
     |FINSI;
|FPRC;

|PROCESO BotonesPanta003;
     |CONTROL_SIMPLE nPanta003, "BOTON,{{193}}&Alta ", 1564, 1574, Alta;
     nBtnAlta = FSalida;

     |CONTROL_SIMPLE nPanta003, "BOTON,{{197}}&Modificar ", 1576, 1586, Modificar;
     nBtnModi = FSalida;

     |CONTROL_SIMPLE nPanta003, "BOTON,{{199}}&Borrar", 1588, 1598, Baja;
     nBtnBaja = FSalida;

     |CONTROL_SIMPLE nPanta003, "BOTON,{{197}}&Revalorizaci¢n", 2883, 2898, Revalorizar;
     nBtnReva = FSalida;

     |ESTADO_CONTROL nBtnModi,  "DISABLE";
     |ESTADO_CONTROL nBtnBaja,  "DISABLE";
     |ESTADO_CONTROL nBtnReva,  "DISABLE";
|FPRC;

|PROCESO Baja201;
     #isom0201#0 = enEmpresa;
     #isom0201#1 = enAnyo;
     #isom0201#2 = 1;
|FPRC;

|PROCESO Alta201;
     #isom0201#0 = enEmpresa;
     #isom0201#1 = enAnyo;
     #isom0201#2 = 9999;
|FPRC;

|PROCESO isom0202B;
     |BORRA 020#isom0202.a;
     |LIBERA #isom0202;
|FPRC;

|DEFBUCLE isom0202B;
     #isom0202#1;
     ;
     enEmpresa, enAnyo, 0000, 01;
     enEmpresa, enAnyo, 9999, 99;
     be;
     NULL, isom0202B;
|FIN;

|PROCESO isom0203B;
     |BORRA 020#isom0203.a;
     |LIBERA #isom0203;
|FPRC;

|DEFBUCLE isom0203B;
     #isom0203#1;
     ;
     enEmpresa, enAnyo, 0000;
     enEmpresa, enAnyo, 9999;
     be;
     NULL, isom0203B;
|FIN;

|PROCESO isom0204B;
     |BORRA 020#isom0204.a;
     |LIBERA #isom0204;
|FPRC;

|DEFBUCLE isom0204B
     #isom0204#1;
     ;
     enEmpresa, enAnyo, 00;
     enEmpresa, enAnyo, 99;
     be;
     NULL, isom0204B;
|FIN;

|PROGRAMA;
     |VENTANA 0501, 3599, -1, 16, "Activos";
     nVentTrab = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVentTrab;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |PTEC 802;  |PAUSA;

     |ABRE #isom0201;
     |ABRE #isom0202;

     |PINPA #0#isoz0003;  nPanta003 = FSalida;
     |PINDA #0#isoz0003;

     |HAZ BotonesPanta003;

     nRango = 4 + 8 + 16 + 32 + 128;

     |LINEAL_SIMPLE #1#isom0201, nRango, 0602, 1498, Baja201, Alta201, Evento201;
     nGrid201 = FSalida;

     aEsc1  = &255 + "806";
     aEsc2  = &255 + "807";
     aRetu  = &255 + "802";
     |PARA; |SI;  |HACIENDO;
            FSalida = "::{{001}}Salir," + nGrid201;
            |LEETECLA aTecla;
            |SI aTecla = aEsc1; |SAL;  |FINSI;
            |SI aTecla = aEsc2; |SAL;  |FINSI;
            |SI aTecla = aRetu; |SAL;  |FINSI;
     |SIGUE;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVentTrab;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA nVentTrab;

     |CIERRA #isom0202;

     nHay = 0;
     #isom0201#0 = enEmpresa;
     #isom0201#1 = enAnyo;
     #isom0201#2 = 1;
     |LEE 000#isom0201.];
     |SI FSalida = 0;  |Y #isom0201#0 = enEmpresa;  |Y  #isom0201#1 = enAnyo;
         nHay = 1;
     |FINSI;

     |SI nHay = 0;
         |BUCLE isom0202B;
         |BUCLE isom0203B;
         |BUCLE isom0204B;

         |ABRE #isom0205;
         #isom0205#0 = enEmpresa;
         #isom0205#1 = enAnyo;
         |LEE 101#isom0205.=;
         |SI FSalida = 0;
             |BORRA 020#isom0205.a;
         |FINSI;
         |LIBERA #isom0205;
         |CIERRA #isom0205;
     |FINSI;

     |CIERRA #isom0201;
|FPRO;
