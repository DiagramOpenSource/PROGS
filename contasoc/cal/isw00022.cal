|FICHEROS;
     isw00022 #500,MANTE;
     isw00030 #501,MANTE;
|FIN;

|VARIABLES;
     aFichOrigen  = "";
     aFichDestin  = "";
     aDirectorio  = "";
     aDirectorioD = "";
     aFichero     = "";
     aAlfa        = "";
     aAlfa1       = "";
     aAlfan       = "";
     aSesion      = "";
     aRaiz        = "";
     aPathBass    = "";
     aHandle      = "";
     aVariable    = "";
     aCaract      = "";
     aVersion     = "";
     aLongi       = "";
     aPath        = "";
     aAlfaa       = "";
     aEjecutable  = "";
     aEjecutable1 = "";

     nEsBasico    = 0;
     nPosic       = 0;
     nVersion     = 0;
     nPara        = 0;
     nLongi       = 0;
     nCanal       = 0;
     nSwDirecta   = 0;

     &eaTipoSec   = "";
     &enAnyo      = 0;

{800}&NEmpresas   = "";
|FIN;

|RUTINA ClaveBaja501;
     #501#0 = 1;
|FRUT;

|RUTINA ClaveAlta501;
     #501#0 = 99;
|FRUT;

|PROCESO SelecImpresora;  |TIPO 11;
     |SI FSalida ! 10;  |ACABA;  |FINSI;

     aAlfa = #501#1;
     |PTEC 807;
|FPRC;

|PROCESO PedirImpresora;
     |SI #500#2 = "N";  |ACABA;  |FINSI;

     |PUSHV 0501 2380;
     |ENTLINEAL #1#501, -1, 2, 21, 1, ClaveBaja501, ClaveAlta501;
     |POPV;

     |QBF aAlfa;
     |SI aAlfa = "";
         |MENAV "     Seleccione Impresora";
         |ERROR;
     |FINSI;

     #500#5 = aAlfa;
     |PINTA #500#5;
|FPRC;

|PROCESO CopiaFichero;
     FSalida = 0;
     aFichOrigen  = aDirectorio  + aFichero;
     aFichDestin  = aDirectorioD + aFichero;

     |COPIA_FICHERO aFichOrigen, aFichDestin;
     |SI FSalida ! 0;
         aAlfa = "Error [" + FSalida + "] en copia de ficheros ...";
         |MENAV aAlfa;
     |FINSI;

     |SI nEsBasico = 0;
         aAlfa = aDirectorio + aFichero;
         |FBORRA aAlfa;
     |FINSI;
|FPRC;

|PROCESO Sesion; |TIPO 0;
     aRaiz       = "";  |DIRECTORIO aRaiz;
     aDirectorio = "";  |DBASE aDirectorio;
     aDirectorio = aDirectorio + "hacienda/";
     aSesion     = #500#0; |QBF aSesion;
     aDirectorio = aDirectorio + aSesion + "/" + "imprime.bat";

     |SI nSwDirecta = 0;
         |XABRE "E", aDirectorio, nCanal;
         |SI FSalida ] 0;
             |CONFI "2400SLa Sesion ya existe, desea otra sesion : "
             |SI FSalida = 0;  |ERROR;  |FINSI;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO QueVersion;
     nSwDirecta = 0;

     |VERSIONRTME aVersion;
     |SI aVersion ] "9.09Q";  nSwDirecta = 1;  |ACABA;  |FINSI;

     aLongi = aVersion % 0;
     nLongi = aLongi;
     aAlfan = "";
     |PARA nPara = 1;  |SI nPara [ nLongi;  |HACIENDO nPara = nPara + 1;
           nPosic  = (100 * nPara) + 1;
           aCaract = aAlfan % nPosic;
           |SI aCaract = ".";  |SAL;  |FINSI;

           aVersion = aVersion + aCaract;
     |SIGUE;

     nVersion = aVersion;

     |SI nVersion ] 10;  nSwDirecta = 1; |FINSI;
|FPRC;

|PROCESO EnviaFichero;
     |IP_REMOTO aAlfa1; |QBT aAlfa1;
     |SI aAlfa1 ! "";
         |ENVIA_FICHERO aFichOrigen, aFichDestin;
     |SINO;
         |COPIA_FICHERO aFichOrigen, aFichDestin;
     |FINSI;
|FPRC;

|PROCESO Directa;
     aAlfa = "/MODELOS";                                       |RMKDIR aAlfa;
     aAlfa = "/MODELOS/CONTASOC";                              |RMKDIR aAlfa;
     aAlfa = "/MODELOS/CONTASOC/" + eaTipoSec;                 |RMKDIR aAlfa;
     aAlfa = "/MODELOS/CONTASOC/" + eaTipoSec + "/" + enAnyo;  |RMKDIR aAlfa;

     aDirectorio  = "";  |DBASE aDirectorio;
     aFichOrigen = aDirectorio + "hacienda/DIRECTA/" + aEjecutable;
     aFichDestin = "/MODELOS/CONTASOC/" + eaTipoSec + "/" + enAnyo + "/" + aEjecutable;
     |HAZ EnviaFichero;

     INEmpresas = NEmpresas1 <-;
     |PARA nPara = 1; |SI nPara [ 500; |HACIENDO nPara = nPara + 1;
           |SI NEmpresas = ""; |SAL; |FINSI;

           aFichOrigen = aDirectorio + "hacienda/DIRECTA/" + (NEmpresas % -108);
           aFichDestin = "/MODELOS/CONTASOC/" + eaTipoSec + "/" + enAnyo + "/" + (NEmpresas % -108);
           |HAZ EnviaFichero;

           INEmpresas = INEmpresas + 1;
     |SIGUE;

     aFichOrigen = aDirectorio + "hacienda/DIRECTA/imprime.bat";
     aFichDestin = "/MODELOS/CONTASOC/" + eaTipoSec + "/" + enAnyo + "/imprime.bat";
     |HAZ EnviaFichero;

     aAlfa1 = "/MODELOS/CONTASOC/" + eaTipoSec + "/" + enAnyo + "/imprime";
     |RSYSTEM aAlfa1;

     aAlfa1 = "/MODELOS/CONTASOC/" + eaTipoSec + "/" + enAnyo + "/imprime.bat";
     |RFBORRA aAlfa1;
|FPRC;

|PROCESO CopiaBasico;
     nEsBasico = 1;
     aFichero  = aEjecutable; |HAZ CopiaFichero;
|FPRC;

|PROCESO CopiaBueno;
     Pos = -1;
     INEmpresas = NEmpresas1 <-;

     aPathBass = "";
     |DBASE aPathBass;
     aPath   = "wt  " + aPathBass + "hacienda/imprime.bat";

     Pos = -1;
     |FABRE aPath;
     aHandle = FSalida;
     |SI FSalida ] 0;
         |SI nSwDirecta = 1;
             aVariable = aHandle + " CD \";
             |FGRABA aVariable;

             aVariable = aHandle + " CD MODELOS";
             |FGRABA aVariable;

             aVariable = aHandle + " CD CONTASOC";
             |FGRABA aVariable;

             aVariable = aHandle + " CD " + eaTipoSec;
             |FGRABA aVariable;

             aVariable = aHandle + " CD " + enAnyo;
             |FGRABA aVariable;
         |FINSI;

         aVariable = aHandle + " " + aEjecutable1 + " -y";
         |FGRABA aVariable;

         |SI enAnyo ] 1999;
             aVariable = aHandle + " MOVE EAN128~1.DLL EAN128_32.DLL";
             |FGRABA aVariable;
         |FINSI;

         |PARA nPara = 1; |SI nPara [ 500; |HACIENDO nPara = nPara + 1;
             |SI NEmpresas = ""; |SAL; |FINSI;

             nEsBasico = 0;
             aFichero  = NEmpresas % -108;  |HAZ CopiaFichero;

             |SI enAnyo ] 1999;
                 aAlfa = #500#5;  |QBF aAlfa;
                 aVariable = aHandle + " wait 201pdf " + "/E:" + aFichero + " /R:" + aFichero + ".txt";
                 |SI #500#2 = "S"; aVariable = aVariable + " /I: " + aAlfa;  |FINSI;
                 |SI #500#1 = "N"; aVariable = aVariable + " /C: N"; |FINSI;
                 |SI #500#1 = "S"; aVariable = aVariable + " /C: S"; |FINSI;
             |SINO;
                 aVariable = aHandle + " 201pdf " + aFichero;
                 |SI #500#4 = "N"; aVariable = aVariable + "  s";  |FINSI;
                 |SI #500#1 = "S"; aVariable = aVariable + "  a";  |FINSI;
                 |SI #500#3 = "1"; aVariable = aVariable + "  P1"; |FINSI;
                 |SI #500#3 = "2"; aVariable = aVariable + "  P2"; |FINSI;
                 |SI #500#3 = "3"; aVariable = aVariable + "  P3"; |FINSI;
             |FINSI;

             |FGRABA aVariable;

             |SI enAnyo [ 1998;
                 aVariable = aHandle + " move errores.txt " + aFichero + ".txt";
                 |FGRABA aVariable;
             |FINSI;

             aVariable  = aHandle + " DEL " + aFichero;
             |FGRABA aVariable;

             INEmpresas = INEmpresas + 1;
         |SIGUE;

         |SI enAnyo ] 1999;
             aVariable  = aHandle + " DEL *.EXE";   |FGRABA aVariable;
             aVariable  = aHandle + " DEL *.DLL";   |FGRABA aVariable;
             aVariable  = aHandle + " DEL A*.*";    |FGRABA aVariable;
             aVariable  = aHandle + " DEL C*.*";    |FGRABA aVariable;
             aVariable  = aHandle + " DEL D*.*";    |FGRABA aVariable;
             aVariable  = aHandle + " DEL P*.*";    |FGRABA aVariable;
             aVariable  = aHandle + " DEL V*.*";    |FGRABA aVariable;
         |SINO;
             aVariable  = aHandle + " DEL *.EXE";   |FGRABA aVariable;
             aVariable  = aHandle + " DEL *.TAB";   |FGRABA aVariable;
             aVariable  = aHandle + " DEL *.IMP";   |FGRABA aVariable;
             aVariable  = aHandle + " DEL DIE*.*";  |FGRABA aVariable;
         |FINSI;

         FSalida = aHandle;
         |FCIERRA FSalida;

         nEsBasico = 0;
         aFichero = "imprime.bat"; |HAZ CopiaFichero;
     |FINSI;

     |SI nSwDirecta = 0;
         |MENAV " FICHEROS COPIADOS !!!. SESION COMPLETADA CON EXITO";
     |FINSI;

     |POPV;
|FPRC;

|PROCESO CopiaDisco;
     |HAZ QueVersion;

     eaTipoSec  = "201";
     |SI enAnyo = 1997;  aEjecutable = "i201ls97.exe";  |FINSI;
     |SI enAnyo = 1998;  aEjecutable = "i201ls98.exe";  |FINSI;
     |SI enAnyo = 1999;  aEjecutable = "i201ls99.exe";  |FINSI;

     |SI enAnyo = 1997;  aEjecutable1 = "i201ls97";     |FINSI;
     |SI enAnyo = 1998;  aEjecutable1 = "i201ls98";     |FINSI;
     |SI enAnyo = 1999;  aEjecutable1 = "i201ls99";     |FINSI;

     |DDEFECTO #500;

     |SI nSwDirecta = 1;
         #500#0 = "DIRECTA";
         |C_M #500#0, 1, "N";
     |FINSI;

     |PUSHV 0501 2380;

     |SI enAnyo ] 1999;
         |C_M #500#3, 1, "N";  |C_V #500#3, 1, "N";
         |C_M #500#4, 1, "N";  |C_V #500#4, 1, "N";
     |SINO;
         |C_M #500#2,  1, "N";  |C_V #500#2,  1, "N";
     |FINSI;

     Pos = -1;
     |PINPA #0#500;
     |PINDA #0#500;
     |ENDATOS #2#500;
     |SI FSalida ! 0;  |POPV;  |ACABA;  |FINSI;

     aRaiz       = "";  |DIRECTORIO aRaiz;
     aDirectorio = "";  |DBASE aDirectorio;
     aDirectorio = aDirectorio + "hacienda/";

     aSesion = #500#0; |QBF aSesion;
     aDirectorioD = aDirectorio + aSesion;
     |MKDIR aDirectorioD;
     aDirectorioD = aDirectorioD + "/";
     aAlfa = aRaiz + "bin/agichmod 777 " + aDirectorioD;  |SYSTEM aAlfa;

     |HAZ CopiaBasico;
     |HAZ CopiaBueno;

     |POPV;

     |SI nSwDirecta = 1;  |HAZ Directa;  |FINSI;
|FPRC;
