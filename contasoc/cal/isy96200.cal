|FICHEROS;
     isy96200 #0;               || Este DEf
     isacceso #1;               || Acceso al impuesto
     maempr01 #2;               || Datos Empresas
     maempr11 #4;               || Caracteres de la Declaracion
     maempr04 #5;               || Representantes
     maempr03 #6;               || Administradores
     maempr06 #7;               || Otras Empresas
     maempr02 #8;               || Relacion de Socios
     maempr14 #9;               || Operaciones Paraisos Fiscales
     maempr15 #10;              || Tenencias   Paraisos Fiscales
     isocie61 #11;              || Transparencia Fiscal
     maempr05 #12;              || Socios Transparencia Fiscal
     isocie57 #13;              || Balances
     isocie59 #14;              || Liquidacion
     isocie58 #15;              || Distribucion de Resultados
     isocie55 #16;              || Resumen de Deducciones
     isocie60 #17;              || Cooperativas
     isocie63 #18;              || Por Doble imposicion
     isocie62 #19;              || Compensaciones
     maempr12 #20;              || Transparancia Fiscal
     maempr13 #21;              || Transparancia Fiscal

     isw00007 #90;
     isw00008 #99,MANTE;
     isw00009 #100;
     isw00010 #101;
     isw00011 #102;
     isw00012 #103;

     &haciend@ #706;
|FIN;

|VARIABLES;
     ZPara       = 0;
     ZPara1      = 0;
     XPara       = 0;
     Dia         = "";
     Mes         = "";
     _Anyo       = "";
     SwHay       = 0;
     DEmpre      = 0;
     HEmpre      = 0;
     Fichero     = "";
     VAlfa1      = "";
     VAlfa2      = "";
     _Informe    = "";
     ContaBucle  = 0;
     ContaBucleL = 0;
     Puntero     = 0;

     &enCodProv = 0;
     &enCodPobl = 0;

 {-1} Hojas      = "";
      Hojas1     = "2400";
      Hojas2     = "1";
      Hojas3     = "Elija impresion : ";
      Hojas4     = "CH";
      Hojas5     = "  papel Continuo  ";
      Hojas6     = "  papel Hoja a Hoja  ";
|FIN;


|| **************************************************************************
||                           PROCESOS DEL PROGRAMA
|| **************************************************************************

|PROCESO Seleccion; |TIPO 0;
  |SI #0#0 = "S";
      |CAMPO_MODIFICA #0#01, 1, "S";
      |CAMPO_MODIFICA #0#02, 1, "S";

      |PARA XPara = 3; |SI XPara [ 32; |HACIENDO XPara = XPara + 1;
            #0XPara = 0; |PINTA #0XPara;
            |CAMPO_MODIFICA #0XPara, 1, "N";
      |SIGUE;
  |SINO;
      #0#1 = 00001; |PINTA #0#1;    |CAMPO_MODIFICA #0#01, 1, "N";
      #0#2 = 99999; |PINTA #0#2;    |CAMPO_MODIFICA #0#02, 1, "N";

      |PARA XPara = 3; |SI XPara [ 32; |HACIENDO XPara = XPara + 1;
            |CAMPO_MODIFICA #0XPara, 1, "S";
      |SIGUE;
  |FINSI;
|FPRC;

|PROCESO Mayus;
  #0Campo = #0Campo > #0Campo;  |PINTA #0Campo;
|FPRC;

|| **************************************************************************
||                           PROCESOS LECTURAS DE FICHEROS
|| **************************************************************************

|PROCESO LeeFich;
  |ABRE #1;
  #1#0 = #99#0;
  #1#1 = 1996;
  |LEE 040#1=;
  |SI FSalida ! 0; |DDEFECTO #1; |FINSI;
  |CIERRA #1;

  |ABRE #2;
  #2#0 = #1#0;
  #2#1 = #1#1;
  |LEE 040#2=;
  |SI FSalida ! 0; |DDEFECTO #2; |FINSI;
  |CIERRA #2;

  enCodProv = #2#29;
  enCodPobl = #2#30;
  |HAZ LeeHacienda;


  |ABRE #13;
  #13#0 = #1#0;
  #13#1 = #1#1;
  |LEE 040#13=;
  |SI FSalida ! 0; |DDEFECTO #13; |FINSI;
  |CIERRA #13;

  |ABRE #11;
  #11#0 = #1#0;
  #11#1 = #1#1;
  |LEE 040#11=;
  |SI FSalida ! 0; |DDEFECTO #11; |FINSI;
  |CIERRA #11;

  |ABRE #14;
  #14#0 = #1#0;
  #14#1 = #1#1;
  |LEE 040#14=;
  |SI FSalida ! 0; |DDEFECTO #14; |FINSI;
  |CIERRA #14;

  |ABRE #15;
  #15#0 = #1#0;
  #15#1 = #1#1;
  |LEE 040#15=;
  |SI FSalida ! 0; |DDEFECTO #15; |FINSI;
  |CIERRA #15;

  |ABRE #16;
  #16#0 = #1#0;
  #16#1 = #1#1;
  |LEE 040#16=;
  |SI FSalida ! 0; |DDEFECTO #16; |FINSI;
  |CIERRA #16;

  |ABRE #17;
  #17#0 = #1#0;
  #17#1 = #1#1;
  |LEE 040#17=;
  |SI FSalida ! 0; |DDEFECTO #17; |FINSI;
  |CIERRA #17;

  |ABRE #18;
  #18#0 = #1#0;
  #18#1 = #1#1;
  |LEE 040#18=;
  |SI FSalida ! 0; |DDEFECTO #18; |FINSI;
  |CIERRA #18;
|FPRC;

|PROCESO Defectos;
  |DDEFECTO #100;
  |DDEFECTO #101;
  |DDEFECTO #102;
  |DDEFECTO #103;
|FPRC;

|PROCESO Representante;
  ContaBucle = ContaBucle + 1;
  |SI ContaBucle = 1;
      #100#66 = #5#3 %  120;
      #100#67 = #5#3 % -120;
      #100#68 = #5#4;
      #100#69 = #5#5;
      #100#70 = #5#6;
  |FINSI;

  |SI ContaBucle = 2;
      #100#71 = #5#3 %  120;
      #100#72 = #5#3 % -120;
      #100#73 = #5#4;
      #100#74 = #5#5;
      #100#75 = #5#6;
  |FINSI;

  |SI ContaBucle = 3;
      #100#76 = #5#3 %  120;
      #100#77 = #5#3 % -120;
      #100#78 = #5#4;
      #100#79 = #5#5;
      #100#80 = #5#6;
  |FINSI;
|FPRC;

|DEFBUCLE maempr04;
  #5#1;
  ;
  #1#0, #1#1, 00;
  #1#0, #1#1, 99;
  ;
  NULL, Representante;
|FIN;

|PROCESO PuntoA;
  |SI ContaBucle ] 6;
      #101#27 = "";
      #101#28 = "";
      #101#29 = "Segun relaci¢n adjunta.";
      #101#30 = "";
      #101#31 = 0;
      |ERROR10;
      |ACABA;
  |FINSI;

  Puntero = (ContaBucle * 5) + 2;  #101Puntero = #6#3;
  Puntero = Puntero + 1;           #101Puntero = #6#4;
  Puntero = Puntero + 1;           #101Puntero = #6#5;
  Puntero = Puntero + 1;           #101Puntero = #6#6;
  Puntero = Puntero + 1;           #101Puntero = #6#7;

  ContaBucle = ContaBucle + 1;
|FPRC;

|DEFBUCLE maempr03;
  #6#1;
  ;
  #1#0, #1#1, 00;
  #1#0, #1#1, 99;
  ;
  NULL, PuntoA;
|FIN;

|PROCESO PuntoB1;
  |SI ContaBucle ] 6;
      #101#57 = "";
      #101#58 = "Segun relaci¢n adjunta.";
      #101#59 = 0;
      #101#60 = 0;
      #101#61 = 0;
      |ERROR10;
      |ACABA;
  |FINSI;

  Puntero = (ContaBucle * 5) + 32; #101Puntero = #7#3;
  Puntero = Puntero + 1;           #101Puntero = #7#4;
  Puntero = Puntero + 1;           #101Puntero = #7#5;
  Puntero = Puntero + 1;           #101Puntero = #7#6;
  Puntero = Puntero + 1;           #101Puntero = #7#7;

  ContaBucle = ContaBucle + 1;
|FPRC;

|DEFBUCLE maempr06;
  #7#1;
  ;
  #1#0, #1#1, 00;
  #1#0, #1#1, 99;
  ;
  NULL, PuntoB1;
|FIN;

|PROCESO PuntoB2;
  |SI ContaBucle ] 6;
      #101#92 = "";
      #101#93 = "";
      #101#94 = "Segun relaci¢n adjunta.";
      #101#95 = 0;
      #101#96 = 0;
      #101#97 = 0;
      |ERROR10;
      |ACABA;
  |FINSI;

  Puntero = (ContaBucle * 6) + 62; #101Puntero = #8#3;
  Puntero = Puntero + 1;           #101Puntero = #8#4;
  Puntero = Puntero + 1;           #101Puntero = #8#5;
  Puntero = Puntero + 1;           #101Puntero = #8#6;
  Puntero = Puntero + 1;           #101Puntero = #8#7;
  Puntero = Puntero + 1;           #101Puntero = #8#8;

  ContaBucle = ContaBucle + 1;
|FPRC;

|DEFBUCLE maempr02;
  #8#1;
  ;
  #1#0, #1#1, 000;
  #1#0, #1#1, 999;
  ;
  NULL, PuntoB2;
|FIN;

|PROCESO PuntoC1;
  |SI ContaBucle ] 6;
      #101#128 = "";
      #101#129 = "Segun relaci¢n adjunta.";
      #101#130 = "";
      #101#131 = "";
      #101#132 = "";
      #101#133 = 0;
      |ERROR10;
      |ACABA;
  |FINSI;

  Puntero = (ContaBucle * 6) + 98; #101Puntero = #9#3;
  Puntero = Puntero + 1;           #101Puntero = #9#4;
  Puntero = Puntero + 1;           #101Puntero = #9#5;
  Puntero = Puntero + 1;           #101Puntero = #9#6;
  Puntero = Puntero + 1;           #101Puntero = #9#7;
  Puntero = Puntero + 1;           #101Puntero = #9#8;

  ContaBucle = ContaBucle + 1;
|FPRC;

|DEFBUCLE maempr14;
  #9#1;
  ;
  #1#0, #1#1, 00, 00;
  #1#0, #1#1, 00, 99;
  ;
  NULL, PuntoC1;
|FIN;

|PROCESO PuntoC2;
  |SI ContaBucle ] 6;
      #101#164 = "";
      #101#165 = "Segun relaci¢n adjunta.";
      #101#166 = "";
      #101#167 = "";
      #101#168 = 0;
      #101#169 = 0;
      |ERROR10;
      |ACABA;
  |FINSI;

  Puntero = (ContaBucle * 6) + 134; #101Puntero = #10#3;
  Puntero = Puntero + 1;            #101Puntero = #10#4;
  Puntero = Puntero + 1;            #101Puntero = #10#6;
  Puntero = Puntero + 1;            #101Puntero = #10#5;
  Puntero = Puntero + 1;            #101Puntero = #10#7;
  Puntero = Puntero + 1;            #101Puntero = #10#8;

  ContaBucle = ContaBucle + 1;
|FPRC;

|DEFBUCLE maempr15;
  #10#1;
  ;
  #1#0, #1#1, 00, 00;
  #1#0, #1#1, 00, 99;
  ;
  NULL, PuntoC2;
|FIN;

|PROCESO PuntoE;
  |SI ContaBucle ] 16;
      #102#130 = "";
      #102#131 = "";
      #102#132 = "";
      #102#133 = "Segun relaci¢n adjunta.";
      #102#134 = 0;
      #102#135 = 0;
      #102#136 = 0;
      |ERROR10;
      |ACABA;
  |FINSI;

  Puntero = (ContaBucle * 7) + 25;  #102Puntero = #12#3;
  Puntero = Puntero + 1;            #102Puntero = #12#4;
  Puntero = Puntero + 1;            #102Puntero = #12#5;
  Puntero = Puntero + 1;            #102Puntero = #12#6;
  Puntero = Puntero + 1;            #102Puntero = #12#7;
  Puntero = Puntero + 1;            #102Puntero = #12#8;
  Puntero = Puntero + 1;            #102Puntero = #12#9;

  ContaBucle = ContaBucle + 1;
|FPRC;

|DEFBUCLE maempr05;
  #12#2;
  ;
  #1#0, #1#1, 999.99, 00;
  #1#0, #1#1,      0, 99;
  ;
  NULL, PuntoE;
|FIN;

|PROCESO El21;
  |SI ContaBucleL ] 5;
      Puntero = (ContaBucle * 24) - 2;  #103Puntero = "Segun relaci¢n adjunta.";
      Puntero = Puntero + 1;            #103Puntero = "";
      Puntero = Puntero + 1;            #103Puntero = "";
      Puntero = Puntero + 1;            #103Puntero = #103Puntero + #21#7;
      |ACABA;
  |FINSI;

  Puntero = (((ContaBucle * 24) - 2) - 16) + (ContaBucleL * 4);
                                    #103Puntero = #21#4;
  Puntero = Puntero + 1;            #103Puntero = #21#5;
  Puntero = Puntero + 1;            #103Puntero = #21#6;
  Puntero = Puntero + 1;            #103Puntero = #21#7;

  ContaBucleL = ContaBucleL + 1;
|FPRC;

|DEFBUCLE maempr13;
  #21#1;
  ;
  #20#0, #20#1, #20#2, 00;
  #20#0, #20#1, #20#2, 99;
  ;
  NULL, El21;
|FIN;

|PROCESO El20;
  #103#146 = #103#146 + #20#6;
  |SI ContaBucle ] 6;
      #103#122 = "Segun relaci¢n adjunta.";
      #103#123 = "";
      #103#124 = "";
      #103#125 = #103#125 + #20#6;

      |PARA ZPara = 125; |SI ZPara [ 142; |HACIENDO;
            ZPara = ZPara + 1;  #103ZPara = "";
            ZPara = ZPara + 1;  #103ZPara = "";
            ZPara = ZPara + 1;  #103ZPara = "";
            ZPara = ZPara + 1;  #103ZPara = 0;
      |SIGUE;
      |ACABA;
  |FINSI;

  Puntero = (ContaBucle * 24) + 2;  #103Puntero = #20#3;
  Puntero = Puntero + 1;            #103Puntero = #20#4;
  Puntero = Puntero + 1;            #103Puntero = #20#5;
  Puntero = Puntero + 1;            #103Puntero = #20#6;

  ContaBucle = ContaBucle + 1;

  ContaBucleL = 0;  |BUCLE maempr13;
|FPRC;

|DEFBUCLE maempr12;
  #20#1;
  ;
  #1#0, #1#1, 00;
  #1#0, #1#1, 99;
  ;
  NULL, El20;
|FIN;

|| **************************************************************************
||                           PROCESOS DE IMPRESION
|| **************************************************************************

|PROCESO Impresion;
  |SI Hojas2 = 1;
      _Informe = "h" + _Informe;
      |INFOR _Informe;
      |SI FSalida ! 0;  |ACABA;  |FINSI;

      |PRINT;   |PIEINF;   |FININF;
      |ACABA;
  |FINSI;

  |ET Comienzo;
      VAlfa1 = _Informe % -102;

      |SI VAlfa1 ! "14";
          #90#2 = "COLOQUE HOJA " + VAlfa1 + " DEL MODELO 200, <INTRO PARA IMPRIMIR>";
      |SINO;
          #90#2 = "COLOQUE HOJA INGRESO O DEVOLUCION DEL MODELO 200, <INTRO PARA IMPRIMIR>";
      |FINSI;

      |ATRI P; |PINTA #90#2; |ATRI 0;
      |PAUSA;

      _Informe = "m" + _Informe;
      |INFOR _Informe;
      |SI FSalida = 0;
          |PRINT; |PIEINF; |FININF;
          |CONFI "2400NDesea repetir la hoja : ";
          |SI FSalida = 0;  |VETE Comienzo;  |FINSI;
      |FINSI;
|FPRC;


|| **************************************************************************
||                           PROCESOS CARGA DE HOJAS
|| **************************************************************************

|PROCESO Hoja1;
  #100#0  = #706#2;                   || Delegacion
  #100#1  = #706#3;                   || Administracion
  VAlfa1  = #706#0;
  VAlfa2  = #706#1;
  #100#2  = (("00" + VAlfa1) % -102) + (("000" + VAlfa2) % -103);  || Codigo
  #100#3  = #2#31 % 102;            || Del Dia
  #100#4  = #2#31 % 402;            || Del Mes
  #100#5  = #2#31 % 902;            || Del A¤o
  #100#6  = #2#32 % 102;            || Al Dia
  #100#7  = #2#32 % 402;            || Al Mes
  #100#8  = #2#32 % 902;            || Al A¤o
  #100#9  = #2#16;                  || Ejercicio
  #100#10 = #2#17;                  || CNAE
  #100#11 = #2#3;                   || NIF
  #100#12 = #2#2;                   || Nombre
  #100#13 = #2#4;                   || SG
  #100#14 = #2#5;                   || Domicilio
  #100#15 = #2#6;                   || Numero
  #100#16 = #2#7;                   || Escalera
  #100#17 = #2#8;                   || Piso
  #100#18 = #2#9;                   || Puerta
  #100#19 = #2#15;                  || Telefono
  #100#20 = #2#13;                  || Municipio
  #100#21 = #2#10;                  || Codigo Provincia
  #100#22 = #2#14;                  || Provincia
  VAlfa1  = #2#10;
  VAlfa2  = #2#11;
  #100#23 = (("00" + VAlfa1) % -102) + (("000" + VAlfa2) % -103);  || Codigo
  |SI #2#98 = "S";  #100#24 = "X";  |FINSI;   || Es Complementaria
  #100#25 = #2#99;                  || N§ Justificante
  #100#26 = #2#100;                 || Digito N§ Justificante

  |ABRE #4;
  |PARA ZPara = 27;  |SI ZPara [ 54;  |HACIENDO ZPara = ZPara + 1;
        ZPara1 = ZPara - 26;
        #4#0 = #1#0;
        #4#1 = #1#1;
        #4#2 = ZPara1;
        |LEE 040#4=;
        |SI FSalida = 0;
            #100ZPara = "X";
            |SI ZPara1 = 9;  |O ZPara1 = 10;
                |SI #100#34 = 0;  #100#55 = #4#4;  |FINSI;
            |FINSI;
        |FINSI;
  |SIGUE;
  |CIERRA #4;

  #100#56 = #2#75;                         || Empleados no fijos
  #100#57 = #2#76;                         || Empleados si fijos
  #100#58 = " ";
  |SI #2#34 = "S";  #100#58 = "X";  |FINSI;   || Renuncia
  #100#59 = #2#77;                            || Nombre Secretario
  #100#60 = #2#78;                            || NIF    Secretario
  #100#61 = #2#77;                            || Nombre Secretario
  #100#62 = #2#13;                            || Poblacion firma
  #100#63 = Dia;                              || Dia
  #100#64 = Mes;                              || Mes
  #100#65 = _Anyo;                            || A¤o

  ContaBucle = 0;    |BUCLE maempr04;

  _Informe = "9620001";  |HAZ Impresion;
|FPRC;

|PROCESO Hoja2;
  #101#0 = #1#3;
  #101#1 = #1#2;

  ContaBucle = 0;    |BUCLE maempr03;
  ContaBucle = 0;    |BUCLE maempr06;
  ContaBucle = 0;    |BUCLE maempr02;
  ContaBucle = 0;    |BUCLE maempr14;
  ContaBucle = 0;    |BUCLE maempr15;

  _Informe = "9620002";  |HAZ Impresion;
|FPRC;

|PROCESO Hoja3;
  #102#0 = #1#3;
  #102#1 = #1#2;

  |PARA ZPara = 2;  |SI ZPara [ 24;  |HACIENDO ZPara = ZPara + 1;
        #102ZPara = #11ZPara;
  |SIGUE;

  ContaBucle = 0;    |BUCLE maempr05;

  _Informe = "9620003";  |HAZ Impresion;
|FPRC;

|PROCESO Hoja4;
  _Informe = "9620004";  |HAZ Impresion;
|FPRC;

|PROCESO Hoja5;
  _Informe = "9620005";  |HAZ Impresion;
|FPRC;

|PROCESO Hoja6;
  _Informe = "9620006";  |HAZ Impresion;
|FPRC;

|PROCESO Hoja7;
  _Informe = "9620007";  |HAZ Impresion;
|FPRC;

|PROCESO Hoja8;
  _Informe = "9620008";  |HAZ Impresion;
|FPRC;

|PROCESO Hoja9;
  _Informe = "9620009";  |HAZ Impresion;
|FPRC;

|PROCESO Hoja10;
  |ABRE #19;
  #19#0 = #1#0;
  #19#1 = #1#1;
  #19#2 = 547;
  |LEE 040#19=;
  |SI FSalida = 0;
      |PARA ZPara = 4; |SI ZPara [ 26; |HACIENDO ZPara = ZPara + 1;
            ZPara1 = ZPara + 133;
            #102ZPara1 = #19ZPara;
      |SIGUE;
  |FINSI;

  #19#0 = #1#0;
  #19#1 = #1#1;
  #19#2 = 561;
  |LEE 040#19=;
  |SI FSalida = 0;
      |PARA ZPara = 4; |SI ZPara [ 26; |HACIENDO ZPara = ZPara + 1;
            ZPara1 = ZPara + 156;
            #102ZPara1 = #19ZPara;
      |SIGUE;
  |FINSI;
  |CIERRA #19;

  _Informe = "9620010";  |HAZ Impresion;
|FPRC;

|PROCESO Hoja11;
  _Informe = "9620011";  |HAZ Impresion;
|FPRC;

|PROCESO Hoja12;
  _Informe = "9620012";  |HAZ Impresion;
|FPRC;

|PROCESO Hoja13;
  #103#0 = #1#3;
  #103#1 = #1#2;
  ContaBucle = 0;  |BUCLE maempr12;
  _Informe = "9620013";  |HAZ Impresion;
|FPRC;

|PROCESO Hoja14;
  #100#0  = #706#2;                   || Delegacion
  #100#1  = #706#3;                   || Administracion
  VAlfa1  = #706#0;
  VAlfa2  = #706#1;
  #100#2  = (("00" + VAlfa1) % -102) + (("000" + VAlfa2) % -103);  || Codigo
  #100#3  = #2#31 % 102;            || Del Dia
  #100#4  = #2#31 % 402;            || Del Mes
  #100#5  = #2#31 % 902;            || Del A¤o
  #100#6  = #2#32 % 102;            || Al Dia
  #100#7  = #2#32 % 402;            || Al Mes
  #100#8  = #2#32 % 902;            || Al A¤o
  #100#9  = #2#16;                  || Ejercicio
  #100#10 = #2#17;                  || CNAE
  #100#11 = #2#3;                   || NIF
  #100#12 = #2#2;                   || Nombre
  #100#13 = #2#4;                   || SG
  #100#14 = #2#5;                   || Domicilio
  #100#15 = #2#6;                   || Numero
  #100#16 = #2#7;                   || Escalera
  #100#17 = #2#8;                   || Piso
  #100#18 = #2#9;                   || Puerta
  #100#19 = #2#15;                  || Telefono
  #100#20 = #2#13;                  || Municipio
  #100#21 = #2#10;                  || Codigo Provincia
  #100#22 = #2#14;                  || Provincia
  #100#62 = #2#13;                         || Poblacion firma
  #100#63 = Dia;                           || Dia
  #100#64 = Mes;                           || Mes
  #100#65 = _Anyo;                         || A¤o
  _Informe = "9620014";  |HAZ Impresion;
|FPRC;

|PROCESO Hoja15;
  |SI Hojas2 ! 1;  |ACABA;  |FINSI;
  _Informe = "9620099";  |HAZ Impresion;
|FPRC;

|PROCESO ImprimeLos;
  |PINPA #0#90;
  #90#0 = #99#0;  |PINTA #90#0;
  #90#1 = #99#3;  |PINTA #90#1;

  |PARA XPara = 1; |SI XPara [ #99#4; |HACIENDO XPara = XPara + 1;
        |HAZ Defectos;
        #90#3 = XPara;  |PINTA #90#3;
        |HAZ LeeFich;
        |SI #0#38 = "S"; |HAZ Hoja1;  |FINSI;   || h9620001
        |SI #0#39 = "S"; |HAZ Hoja2;  |FINSI;   || h9620002
        |SI #0#40 = "S"; |HAZ Hoja3;  |FINSI;   || h9620003
        |SI #0#41 = "S"; |HAZ Hoja4;  |FINSI;   || h9620004
        |SI #0#42 = "S"; |HAZ Hoja5;  |FINSI;   || h9620005
        |SI #0#43 = "S"; |HAZ Hoja6;  |FINSI;   || h9620006
        |SI #0#44 = "S"; |HAZ Hoja7;  |FINSI;   || h9620007
        |SI #0#45 = "S"; |HAZ Hoja8;  |FINSI;   || h9620008
        |SI #0#46 = "S"; |HAZ Hoja9;  |FINSI;   || h9620009
        |SI #0#47 = "S"; |HAZ Hoja10; |FINSI;   || h9620010
        |SI #0#48 = "S"; |HAZ Hoja11; |FINSI;   || h9620011
        |SI #0#49 = "S"; |HAZ Hoja12; |FINSI;   || h9620012
        |SI #0#50 = "S"; |HAZ Hoja13; |FINSI;   || h9620013
        |SI #0#51 = "S"; |HAZ Hoja14; |FINSI;   || h9620014
        |SI #0#52 = "S"; |HAZ Hoja15; |FINSI;   || h9620099
  |SIGUE;
|FPRC;

|DEFBUCLE rew00001;
  #99#1;
  5;
  00000, "S";
  99999, "S";
  ;
  NULL, ImprimeLos;
|FIN;

******************** Tipo de Impresion

|PROCESO TipoImpre;
  |MENU Hojas;
  Hojas2 = FSalida;
  |SI Hojas2 ! 1; |Y Hojas2 ! 2; |ACABA; |FINSI;

  Dia   = #0#33 % 102;
  Mes   = #0#33 % 402;
  _Anyo = #0#33 % -101;

  |SI Mes = "01"; Mes = "Enero     "; |FINSI;
  |SI Mes = "02"; Mes = "Febrero   "; |FINSI;
  |SI Mes = "03"; Mes = "Marzo     "; |FINSI;
  |SI Mes = "04"; Mes = "Abril     "; |FINSI;
  |SI Mes = "05"; Mes = "Mayo      "; |FINSI;
  |SI Mes = "06"; Mes = "Junio     "; |FINSI;
  |SI Mes = "07"; Mes = "Julio     "; |FINSI;
  |SI Mes = "08"; Mes = "Agosto    "; |FINSI;
  |SI Mes = "09"; Mes = "Septiembre"; |FINSI;
  |SI Mes = "10"; Mes = "Octubre   "; |FINSI;
  |SI Mes = "11"; Mes = "Noviembre "; |FINSI;
  |SI Mes = "12"; Mes = "Diciembre "; |FINSI;
|FPRC;

******************** Grabacion del Indexado a Imprimir

|PROCESO GrabaTempo;
  #99#0 = #1#0;
  #99#1 = "";
  #99#2 = "";
  #99#3 = #1#2;
  #99#4 = #0#37;
  #99#5 = "S";
  #99#6 = 1;
  #99#7 = #1#3;
  |GRABA 040#99n;

  SwHay = 1;
|FPRC;

|DEFBUCLE isacceso;
  #1#1;
  4;
  1996, DEmpre, "N";
  1996, HEmpre, "N";
  ;
  NULL, GrabaTempo;
|FIN;

|RUTINA inferior;
  #99#0 = 1;
|FRUT;

|RUTINA superior;
  #99#0 = 99999;
|FRUT;

|PROCESO CargaTempo;
  Fichero = Usuario % 108;  |QBT Fichero;
  Fichero = ("01" + Fichero + "zzzzzzzz") % 108;
  |NOME_DAT #99 Fichero;
  |ABRE #99; |CIERRA #99; |DELINDEX #99;

  |ABRE #99;
  SwHay = 0;
  |SI #0#0 = "S";
      DEmpre = #0#1;
      HEmpre = #0#2;
      |BUCLE isacceso;
  |SINO;
      |PARA XPara = 3; |SI XPara [ 32; |HACIENDO XPara = XPara + 1;
            |SI #0XPara ! 0;
                DEmpre = #0XPara;
                HEmpre = #0XPara;
                |BUCLE isacceso;
            |FINSI;
      |SIGUE;
  |FINSI;
  |CIERRA #99;

  |SI SwHay = 0; |ACABA; |FINSI;

  |SI #0#35 = "N"; |ACABA; |FINSI;

  |ENTLINEAL #1#99, 1, 4, 19, 1, inferior, superior;
|FPRC;

******************** Proceso Marcar y Desmarcar el Entlineal

|PROCESO funcion1; |TIPO 11;
  |SI FSalida ! 9; |ACABA; |FINSI;

  |LIBERA #99;
  |LEE 140#99=;

  |SI #99#5 = "S"; #99#5 = "N"; |SINO; #99#5 = "S"; |FINSI;
  |PINTA #99#5;
  |GRABA 140#99a;
|FPRC;

|PROCESO Tipo3; |TIPO 3;
  |HAZ PreparaInformes;

  |IMPRE 0;
  |SI FSalida ! 0; |ACABA; |FINSI;

  |HAZ TipoImpre;     |SI Hojas2 ! 1; |Y Hojas2 ! 2; |ACABA; |FINSI;
  |HAZ CargaTempo;

  |SI SwHay = 0; |ACABA; |FINSI;

  |BUCLE rew00001;

  |FINIMP;
  |ABRE #99; |CIERRA #99; |DELINDEX #99;
|FPRC;
