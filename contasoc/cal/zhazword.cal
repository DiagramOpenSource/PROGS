|FICHEROS;
     maacceso #1;

     :/contagen/def/canempre #5;

     :/basica/def/agiconpa #4;
     :/basica/def/agim0028 #7;

     meempmem #100;
     sodacoca #101;
     cuacceso #102;
     meempme1 #103;
     maempr11 #111;

     zusuario #98;
     zhazword #99;

     megwordc #500;
|FIN;

|VARIABLES;
     aFichero    = "";
     aAlfa       = "";
     aAlfa1      = "";
     aAlfa2      = "";
     aAlfa3      = "";
     aAlfa4      = "";
     aAlfa5      = "";
     aAlfa6      = "";
     aAlfa7      = "";
     aAlfa8      = "";
     aCampo2     = "";
     aCampo3     = "";
     aCampo4     = "";
     aCampo5     = "";
     aCampo6     = "";
     aCampo7     = "";
     aCampo8     = "";
     aCampo9     = "";
     aCampo10    = "";
     aCampo11    = "";
     aCampo14    = "";
     aCampo15    = "";
     aAutoReturn = "";
     aCadena     = "";
     aDirec      = "";
     aCoopera    = "";

     nNume       = 0;
     nModo       = 0;
     nLong       = 0;
     nCaracter   = 0;
     nPosi       = 0;
     nPtec       = 0;
     nForma      = 0;
     nCanal      = 0;
     nNumer1     = 0;
     nNumer2     = 0;

  {-1}aMenu  = "";
      aMenu1  = "2400";
      aMenu2  = "1";
      aMenu3  = "";
      aMenu4  = "UMGCS"
      aMenu5  = " conf.Usuario ";
      aMenu6  = " capitulos Memorias ";
      aMenu7  = " capitulos Gestion ";
      aMenu8  = " Creacion ";
      aMenu9  = " Salir";

  {-1}aMenuDup  = "";
      aMenuDup1  = "2400";
      aMenuDup2  = "1";
      aMenuDup3  = "";
      aMenuDup4  = "DF"
      aMenuDup5  = " duplicar del ejercicio anterior";
      aMenuDup6  = " de la Ficha sociedades";

     &efFecha    = @;
     &enEmpresa  = 0;
     &enAnyo     = 0;
     &empresa    = 0;
     &anyo       = 0;
     &siono      = "";
     &empAlta    = 0;
     &empre      = 0;
     &perio      = 0;
     &plani      = 0;
     &cuent      = "";
     &eaMoneda   = "";
     &eaPrograma = "";

     &fFechaChequeo = @;
     &enError       = 0;
     &fFechaDisol   = @;

     &enCodCli    = 0;
     &enSwDeDonde = 0;
     nEjerTex     = 0;
     &enDeHaz     = 0;
     &eaUnidadBasico = "";

|FIN;

|| ****************************************
|| PROCESOS DEL LINEAL
|| ****************************************

|PROCESO Tipo2;  |TIPO 2;
     |SI nModo ! 1;  |ACABA; |FINSI;

     aCampo2  = #99#2;
     aCampo3  = #99#3;
     aCampo4  = #99#4;
     aCampo5  = #99#5;
     aCampo6  = #99#6;
     aCampo7  = #99#7;
     aCampo8  = #99#8;
     aCampo9  = #99#9;
     aCampo10 = #99#10;
     aCampo11 = #99#11;
     aCampo14 = #99#14;
     aCampo15 = #99#15;
|FPRC;

|PROCESO MiraEmpresa;
     |SI #99#3 = "N";
         |ABRE #100;
         #100#0 = #99#0;
         |LEE 040#100=;
         |SI FSalida ! 0;
             |MENAV "     No existe Empresa de Memoria.";
             |PTEC 807;
         |FINSI;

         |SI #100#5 ! #99#2;
             |MENAV "     El a¤o no coincide con el de la Empresa de Memoria";
             |PTEC 807;
         |FINSI;
         |CIERRA #100;
     |FINSI;
|FPRC;

|PROCESO LeeEmpresaFicha;
     |ABRE #7;
     #7#0 = #99#0;
     |LEE 040#7=;
     |SI FSalida ! 0;
         |MENAV  "      No existe Empresa en la Ficha de Sociedades";
         |ERROR;
         |CIERRA #7;
         |ACABA;
     |FINSI;
     |CIERRA #7;
|FPRC;

|PROCESO MiraVarMoneda;  |TIPO 7;
     eaPrograma = "zhazword";
     |HAZ PintaMoneda;
     #99#18 = eaMoneda;

     |PINTA #99#18;
|FPRC;

|PROCESO PonEmpresa;  |TIPO 7;
     #99#1 = #7#1;
     |PINTA #99#1;

     nModo = (FEntrada / 100) ! 0;
     |SI nModo ! 1;  |ACABA; |FINSI;

     |SI aCampo2 ! "";
         #99#2   = aCampo2;   |PINTA #99#2;
         #99#3   = aCampo3;   |PINTA #99#3;
         #99#4   = aCampo4;   |PINTA #99#4;
         #99#5   = aCampo5;   |PINTA #99#5;
         #99#6   = aCampo6;   |PINTA #99#6;
         #99#7   = aCampo7;   |PINTA #99#7;
         #99#8   = aCampo8;   |PINTA #99#8;
         #99#9   = aCampo9;   |PINTA #99#9;
         #99#10  = aCampo10;  |PINTA #99#10;
         #99#11  = aCampo11;  |PINTA #99#11;
         #99#14  = aCampo14;  |PINTA #99#14;
         #99#15  = aCampo15;  |PINTA #99#15;
     |FINSI;
|FPRC;

|PROCESO DuplicaEmpresa;
     enEmpresa = #99#0;
     enAnyo    = #99#2;

     |MENU aMenuDup;
     aMenuDup2 = FSalida;
     |SI aMenuDup2 = "1";  |SUB_EJECUTA "maempr08";  |FINSI;
     |SI aMenuDup2 = "2";  |SUB_EJECUTA "z0000010";  |FINSI;
     #99#3 = "S";  |PINTA #99#3;
|FPRC;

|PROCESO LeeEmpresaSoc;
     enEmpresa     = #99#0;
     aAlfa         = "01.01." + #99#2;
     fFechaChequeo = aAlfa;
     |HAZ ChequeaEmpresa;

     |SI enError = 1;
         |CONFI "0000NEmpresa INACTIVA. Continuar.";
         |SI FSalida ! 0;
             |ERROR;
             |ACABA;
         |FINSI;
     |FINSI;

     |SI enError = 2;
         aAlfa = "0000NEmpresa con fecha disolucion " + fFechaDisol + ". Continuar.";
         |CONFI aAlfa;
         |SI FSalida ! 0;
             |ERROR;
             |ACABA;
         |FINSI;
     |FINSI;

     |ABRE #1;
     #1#0 = #99#0;
     #1#1 = #99#2;
     |LEE 040#1=;
     |SI FSalida ! 0;
         |MENAV  "      No existe Empresa con el Ejercicio seleccionado";
         |CONFI "2400NDesea crearla";
         |SI FSalida = 0;
             |HAZ DuplicaEmpresa;
         |FINSI;
         |ERROR;
         |CIERRA #1;
         |ACABA;
     |FINSI;
     |CIERRA #1;

     nEjerTex = #99#2;
     aCoopera = "N";

     |ABRE #111;
     #111#0 = #99#0;
     #111#1 = #99#2;
     #111#2 = 17;
     |LEE 000#111=;
     |SI FSalida = 0;   aCoopera = "S";   |FINSI;

     #111#0 = #99#0;
     #111#1 = #99#2;
     #111#2 = 18;
     |LEE 000#111=;
     |SI FSalida = 0;   aCoopera = "S";   |FINSI;

     #111#0 = #99#0;
     #111#1 = #99#2;
     #111#2 = 19;
     |LEE 000#111=;
     |SI FSalida = 0;   aCoopera = "S";   |FINSI;
     |CIERRA #111;

|FPRC;

|PROCESO Modifica19;  |TIPO 7;
     |C_M #99#19, 1, "N";
     |SI #99#16 = "E";
         |C_M #99#19, 1, "S";
     |FINSI;
|FPRC;

|PROCESO DeDonde;
     |C_M #99#16, 1, "S";
     |C_M #99#19, 1, "S";
     |SI #99#4 = "N";
         |C_M #99#16, 1, "N";
         |C_M #99#19, 1, "N";
         #99#16 = "";
         #99#19 = "";
         |PINTA #99#16;
         |PINTA #99#19;
     |FINSI;
|FPRC;

|PROCESO Campo16;
     |SI aCoopera = "S";  |Y #99#16 = "I";
         |MENAV "0000 Cuentas Anuales Cooperativas, no se pasan por impuesto.";
         |ERROR;
     |FINSI;
|FPRC;

|PROCESO Memoria;
     |SI #99#5 = "N";
         #99#6 = 0;             |PINTA #99#6;
         #99#7 = "01.01.0000";  |PINTA #99#7;
         |C_M #99#6, 1, "N";
         |C_M #99#7, 1, "N";
         |ACABA;
     |FINSI;

     |C_M #99#6, 1, "S";
     |C_M #99#7, 1, "S";

     |ABRE #500;
     #500#0 = #99#0;
     #500#1 = #99#2;
     |LEE 040#500=;
     |SI FSalida = 0;
         #99#6 = #500#3;  |PINTA #99#6;
     |FINSI;
     |CIERRA #500;

     |SI aCampo7 = "";
         nNume = #99#2 + 1;
         aAlfa = "30.03." + nNume;
         #99#7 = aAlfa;      |PINTA #99#7;
     |FINSI;
|FPRC;

|PROCESO Gestion;
     |SI #99#8 = "N";
         #99#9 = 0;             |PINTA #99#9;
         |C_M #99#9, 1, "N";
         |ACABA;
     |FINSI;

     |ABRE #500;
     #500#0 = #99#0;
     #500#1 = #99#2;
     |LEE 040#500=;
     |SI FSalida = 0;  |Y #500#7 = "N";
         #99#9 = #500#5;
     |SINO;
         #99#9 = -1;
     |FINSI;
     |CIERRA #500;

     |PINTA #99#9;
     |C_M #99#9, 1, "S";
|FPRC;

|PROCESO Certificado;
     |SI #99#10 = "N";
         #99#11 = "01.01.0000";  |PINTA #99#11;
         |C_M #99#11, 1, "N";
         |C_M #99#12, 1, "N";
         |C_M #99#13, 1, "N";
         |C_M #99#17, 1, "N";
         |ACABA;
     |FINSI;

     |C_M #99#11, 1, "S";
     |C_M #99#12, 1, "S";
     |C_M #99#13, 1, "S";
     |C_M #99#17, 1, "S";

     |ABRE #100;
     #100#0 = #99#0;
     |LEE 040#100=;
     |SI FSalida = 0;
         #99#12 = #100#123;  |PINTA #99#12;
         #99#13 = #100#124;  |PINTA #99#13;
         #99#17 = #100#152;  |PINTA #99#17;
     |FINSI;
     |CIERRA #100;

     #99#11 = ~;             |PINTA #99#11;
|FPRC;

|PROCESO CodigoNotario;
     |C_M #99#17, 1, "S";
     |SI #99#13 = "N";
         |C_M #99#17, 1, "N";
         #99#17 = 0;  |PINTA #99#17;
     |FINSI;
|FPRC;


|PROCESO Instancia;
     |SI #99#14 = "N";
         #99#15 = "01.01.0000";  |PINTA #99#11;
         |C_M #99#15, 1, "N";
         |ACABA;
     |FINSI;

     |C_M #99#15, 1, "S";
     #99#15 = ~;
|FPRC;

|PROCESO TipoActa;  |TIPO 7;
     |PUSHV 0501 2380;
     |CUADRO 1201 2357;
     |BLANCO 1302 2256;
     |ATRI R;
     |PINTA 1302, "-1- JUNTA UNIVERSAL CUENTAS ABREVIADAS                 ";
     |PINTA 1402, "-2- JUNTA UNIVERSAL CUENTAS ORDINARIAS                 ";
     |PINTA 1502, "-3- JUNTA UNIVERSAL (TRANSPARENCIA)                    ";
     |PINTA 1602, "-4- JUNTA UNIVERSAL (COOPERATIVAS)                     ";
     |PINTA 1702, "-5- JUNTA UNIVERSAL (UNIPERSONAL)                      ";
     |PINTA 1802, "-6- JUNTA UNIVERSAL (NO APROBACION DE CUENTAS)         ";
     |PINTA 1902, "-7- JUNTA NO UNIVERSAL CUENTAS ABREVIADAS              ";
     |PINTA 2002, "-8- JUNTA NO UNIVERSAL CUENTAS ORDINARIAS              ";
     |PINTA 2102, "-9- JUNTA NO UNIVERSAL (NO APROBACION DE CTAS.)        ";
     |PINTA 2202, "-0- COOPERATIVAS (CERTIFICADO Y SOLICITUD DE DEPOSITO) ";
     |ATRI r;
|FPRC;

|PROCESO Recupera;
     |POPV;
     |PINTA #99#12;
|FPRC;

|RUTINA ClaveBaja99;
     #99#0 = 1;
|FRUT;

|RUTINA ClaveAlta99;
     #99#0 = 99999;
|FRUT;

|| ****************************************
|| PROCESOS DE LA CREACION
|| ****************************************

|PROCESO PonPtecs;
    |SI aAutoReturn = "0";  |PTEC 802;  |FINSI;

    aAlfa = aCadena % 0;
    nLong  = aAlfa;
    |PARA nCaracter = nLong;  |SI nCaracter ] 1;  |HACIENDO nCaracter = nCaracter - 1;
          nPosi  = (100 * nCaracter) + 1;
          aAlfa  = aCadena % nPosi;
          nPtec  = &aAlfa;
          |PTEC nPtec;
    |SIGUE;
|FPRC;

|| ****************************************************************
|| GRABA APUNTES DE CONTABILIDAD
|| ****************************************************************

|PROCESO canempre1;
     nNumer1 = #99#2 $ 100;
     |SI nNumer1 = #5#26;
         perio = #5#3;
         |ERROR10;
     |FINSI;
|FPRC;

|DEFBUCLE canempre;
     #5#1;
     ;
     nNumer2, 0;
     nNumer2, 9;
     ;
     NULL, canempre1;
|FIN;

|PROCESO BuscaPeriodo;
     perio = -1;
     nNumer2 = #4#5;
     |BUCLE canempre;
|FPRC;

|PROCESO agiconpa1;
     |SI #4#2 ! "I1";  |ACABA;  |FINSI;

     |HAZ BuscaPeriodo;
     |SI perio = -1;  |ACABA;  |FINSI;

     empAlta = #99#0;
     empre   = #4#5;
     plani   = #4#17;
     cuent   = #4#18;
     siono   = "SI";
     |PTEC 802;
     |SUB_EJECUTA "soagico1";
|FPRC;

|DEFBUCLE agiconpa;
     #4#1;
     ;
     #99#0,  1;
     #99#0, 99;
     ;
     NULL, agiconpa1;
|FIN;

|PROCESO MiraApuntes;
     |ABRE #101;
     #101#0 = #99#0;
     #101#1 = #99#2;
     |LEE 040#101=;
     |SI FSalida ! 0;
         |BUCLE agiconpa;
     |FINSI;
     |CIERRA #101;
|FPRC;

|| ****************************************************************
|| CUENTAS ANUALES;
|| ****************************************************************

|PROCESO GrabaCuentas;
     |PTEC 807;                                       || Salida 3
     |PTEC 807;                                       || Salida 2
     |PTEC 807;                                       || Salida 1

     |PTEC 802;                                       || Intro 7¦ Opcion
     aCadena = "7";                  |HAZ PonPtecs;   || A¤o

     |SI #99#16 = "E";
         |PTEC 802;                                       || Intro 2¦ Opcion
         aCadena = "SI";                 |HAZ PonPtecs;   || Es correcto "SI";
         aCadena = "SI";                 |HAZ PonPtecs;   || Es correcto "SI";

         |SI #99#19 = "S";
             aCadena = "SI";             |HAZ PonPtecs;   || Es correcto "SI";
             aCadena = "M";              |HAZ PonPtecs;   || Manual o Automatico;
         |SINO;
             |PTEC 809;                                   || Flecha Abajo
             |PTEC 809;                                   || Flecha Abajo
         |FINSI;

         |PTEC 809;                                       || Flecha Abajo
         |PTEC 802;                                       || Intro 2¦ Opcion
         |PTEC 809;                                       || Flecha Abajo
     |SINO;
         |PTEC 802;                                       || Intro 2¦ Opcion
         aCadena = "SI";                 |HAZ PonPtecs;   || Es correcto "SI";
         |PTEC 802;                                       || Intro 2¦ Opcion
         |PTEC 809;                                       || Flecha Abajo
         |PTEC 809;                                       || Flecha Abajo
     |FINSI;

     |SI #99#19 = "N";
         |PTEC 802;                                       || Intro 2¦ Opcion
         aCadena = "SI";                 |HAZ PonPtecs;   || Es correcto "SI";
         |PTEC 802;                                       || Intro Primera Opcion
     |FINSI;

     |PTEC 802;                                       || Intro a la pregunta
     |PTEC 802;                                       || Valida el Correcto
     |PTEC 815;                                       || Avanza Correcto S/N

     aCadena = ("0000"  + #99#2) % -104;   |HAZ PonPtecs;  || Pone el A¤o
     aCadena = ("00000" + #99#0)  % -105;  |HAZ PonPtecs;  || Pone Hasta Empresa

     |PTEC 802;                 || Pulsa intro en Alta
     |PTEC 810;                 || Se Posiciona en el Alta

     empresa = #99#0;
     anyo    = #99#2;
     |SUB_EJECUTA "cuacceso";
|FPRC;

|PROCESO MiraCuentas;
     |ABRE #102;
     #102#0 = #99#0;
     #102#1 = #99#2;
     |LEE 040#102=;
     |SI FSalida ! 0;
         |CIERRA #102;
         |HAZ GrabaCuentas;
     |FINSI;
|FPRC;

|| ****************************************************************
|| EMPRESA DE MEMORIA
|| ****************************************************************

|PROCESO CreaMeempmem;
     |PTEC 807;                            || Pulsa Escape
     |PTEC 807;                            || Pulsa Escape
     |PTEC 807;                            || Pulsa Escape
     |PTEC 807;                            || Pulsa Escape

     |PTEC 807;                            || Pulsa Escape
     |PTEC 802;                            || Pulsa intro
     aCadena = "1";        |HAZ PonPtecs;  || Pone el Acta
     |PTEC 802;                            || Pulsa intro
     |PTEC 802;                            || Pulsa intro
     aCadena = "4";        |HAZ PonPtecs;  || Pone el Acta

     |PTEC 807;                            || Pulsa Escape
     |PTEC 802;                            || Pulsa intro
     aCadena = "3";        |HAZ PonPtecs;  || Pone el Acta

     |PTEC 807;                            || Pulsa Escape
     |PTEC 802;                            || Pulsa intro
     aCadena = "2";        |HAZ PonPtecs;  || Pone el Acta

     |PTEC 807;                            || Pulsa Escape
     |PTEC 802;                            || Pulsa intro

     |PTEC 802;                            || Pulsa intro
     |PTEC 802;                            || Pulsa intro

     aCadena = ("000" + #99#17) % -103;    |HAZ PonPtecs;  || Pone Codigo del Notario
     aCadena = #99#13;                     |HAZ PonPtecs;  || Pone el Notario
     aCadena = ("0" + #99#12)  % -101;     |HAZ PonPtecs;  || Pone el Acta

     |PTEC 802;                            || Pulsa intro
     |PTEC 802;                            || Pulsa intro

     aCadena = ("0000"  + #99#2) % -104;   |HAZ PonPtecs;  || Pone el A¤o
     aCadena = ("00000" + #99#0)  % -105;  |HAZ PonPtecs;  || Pone Hasta Empresa

     |PTEC 802;                            || Pulsa intro

     |SI nForma = 1;  |PTEC 810;  |FINSI;  || Se Posiciona en el Alta
     |SI nForma = 2;  |PTEC 811;  |FINSI;  || Se Posiciona en la Modificacion

     empresa = #99#0;
     anyo    = #99#2;
     enDeHaz = 1;
     |SUB_EJECUTA "meempmem";
     enDeHaz = 0;

     |PTEC 802;                            || Pulsa intro faltaba uno
     |PTEC 802;                            || Pulsa intro
     |PTEC 802;                            || Pulsa intro
     aCadena = ("00000" + #99#0)  % -105;  |HAZ PonPtecs;  || Pone Hasta Empresa
     aCadena = ("00000" + #99#0)  % -105;  |HAZ PonPtecs;  || Pone Desde Empresa
     aCadena = ("0000"  + #99#2) % -104;   |HAZ PonPtecs;  || Pone el A¤o
     |PTEC 802;                            || Pulsa intro

     |SUB_EJECUTA "pasecuad";
|FPRC;

|PROCESO MiraEmpMemoria;
     nForma = 0;
     |ABRE #100;
     #100#0 = #99#0;
     |LEE 040#100=;
     |SI FSalida ! 0;
         nForma = 1;
     |SINO;
         |ABRE #103;
         #103#0 = #99#0;
         #103#1 = 1;
         |LEE 040#103];
         |SI FSalida ! 0;  |O  #103#0 ! #99#0;
             nForma = 2;
         |FINSI;
         |CIERRA #103;

         |SI #100#5 ! #99#2;
             nForma = 2;
         |FINSI;
     |FINSI;
     |CIERRA #100;

     |SI nForma ! 0;  |HAZ CreaMeempmem;  |FINSI;

     |ABRE #100;
     #100#0 = #99#0;
     |LEE 101#100=;
     |SI FSalida = 0;
         #100#1   = #99#1;
         #100#123 = #99#12;
         #100#124 = #99#13;
         #100#151 = #99#11;
         #100#152 = #99#17;
         |GRABA 020#100a;
     |FINSI;
     |LIBERA #100;
     |CIERRA #100;
|FPRC;

|PROCESO Creacion;
     |IP_LOCAL aAlfa;
     |NOMBRE_IP aAlfa, aAlfa1;    |QBT aAlfa1;

     aAutoReturn = "0";

     |SI aAlfa1 ! "";
         |HAZ LeeUnidadBasico;

         aAlfa = eaUnidadBasico + "\ds\ds.ini";
         |XABRE "C", aAlfa, nCanal;

         |PARA;  |SI  FSalida ] 0;  |HACIENDO;
                 |XLEE nCanal, 254, aAlfa;
                 |SI FSalida < 0;  |SAL;  |FINSI;
                 aAlfa  = aAlfa > aAlfa;
                 aAlfa1 = aAlfa - "AUTORETURN";
                 |SI aAlfa1 ! aAlfa;
                     aAutoReturn = aAlfa % 1201;
                     |SAL;
                 |FINSI;
         |SIGUE;
         |XCIERRA  nCanal;
     |FINSI;

     |SI aAutoReturn ! "0";
         aAutoReturn = "1";
     |FINSI;

     |HAZ MiraApuntes;
     |HAZ MiraCuentas;
     |HAZ MiraEmpMemoria;
|FPRC;

|PROCESO EjecutaEmpresa;
       |SI #99#3 = "S";
           |HAZ Creacion;
       |SINO;
          |ABRE #100;
          #100#0 = #99#0;
          |LEE 040#100=;
          |SI FSalida ! 0; |CIERRA #100;  |ACABA;  |FINSI;
          |CIERRA #100;

          |SI #100#5 ! #99#2;  |ACABA;  |FINSI;
       |FINSI;

       |ABRE #100;
       #100#0 = #99#0;
       |LEE 101#100=;
       |SI FSalida = 0;
           #100#1   = #99#1;
           #100#123 = #99#12;
           #100#124 = #99#13;
           #100#151 = #99#11;
           #100#152 = #99#17;
           |GRABA 020#100a;
       |FINSI;
       |LIBERA #100;
       |CIERRA #100;

       eaMoneda = #99#18;

       |SI #99#5 = "S";
           efFecha = #99#7;
           aAlfa1  = "zmenumem;";
           aAlfa2  = "000000";
           aAlfa3  = (("00000" + #99#0) % -105);
           aAlfa4  =  "5";
           aAlfa5  = "0";
           aAlfa6  = "0";
           aAlfa7  = "0000";
           aAlfa8  = "=" + ((000 + #99#6) % -103);
           aAlfa   = aAlfa1 + aAlfa2 + aAlfa3 + aAlfa4 + aAlfa5 + aAlfa6 + aAlfa7 + aAlfa8;
           |SUB_EJECUTA aAlfa;
       |FINSI;

       |SI #99#8 = "S";
           aAlfa8 = "=" + ((000 + #99#9) % -103);

           |ABRE #500;
           #500#0 = #99#0;
           #500#1 = #99#2;
           |LEE 101#500=;
           |SI FSalida ! 0;
               |DDEFECTO #500;
               #500#0 = #99#0;
               #500#1 = #99#2;
               #500#2 = #99#1;
               |GRABA 020#500b;
           |FINSI;

           |SI #99#9 = -1;
               #500#7 = "S";
               nNume = #500#4 + 1;
               aAlfa8 = "=" + ((000 + nNume) % -103);
           |SINO;
               #500#7 = "N";
           |FINSI;

           |GRABA 020#500a;
           |LIBERA #500;
           |CIERRA #500;

           aAlfa1 = "zmenumem;";
           aAlfa2 = "000000";
           aAlfa3 = (("00000" + #99#0) % -105);
           aAlfa4 =  "7";
           aAlfa5 = "0";
           aAlfa6 = "0";
           aAlfa7 = "0000";
           aAlfa  = aAlfa1 + aAlfa2 + aAlfa3 + aAlfa4 + aAlfa5 + aAlfa6 + aAlfa7 + aAlfa8;
           |SUB_EJECUTA aAlfa;
       |FINSI;

       |SI #99#10 = "S";
           aAlfa1 = "zmenumem;";
           aAlfa2 = "000000";
           aAlfa3 = (("00000" + #99#0) % -105);
           aAlfa4 =  "9";
           aAlfa5 = "0";
           aAlfa6 = "0";
           aAlfa7 = "    ";
           aAlfa  = aAlfa1 + aAlfa2 + aAlfa3 + aAlfa4 + aAlfa5 + aAlfa6 + aAlfa7;
           |SUB_EJECUTA aAlfa;
       |FINSI;

       |SI #99#14 = "S";
           efFecha = #99#15;
           aAlfa1 = "zmenumem;";
           aAlfa2 = "000000";
           aAlfa3 = (("00000" + #99#0) % -105);
           aAlfa4 =  "0";
           aAlfa5 = "0";
           aAlfa6 = "0";
           aAlfa7 = "    ";
           aAlfa  = aAlfa1 + aAlfa2 + aAlfa3 + aAlfa4 + aAlfa5 + aAlfa6 + aAlfa7;
           |SUB_EJECUTA aAlfa;
       |FINSI;

       |SI #99#4 = "S";
           aAlfa1  = "zmenumem;";
           aAlfa2  = "000000";
           aAlfa3  = (("00000" + #99#0) % -105);
           aAlfa4  =  "4";
           aAlfa5  = "0";
           aAlfa6  = "0";
           aAlfa7  = #99#2;
           aAlfa   = aAlfa1 + aAlfa2 + aAlfa3 + aAlfa4 + aAlfa5 + aAlfa6 + aAlfa7;
           |SUB_EJECUTA aAlfa;
       |FINSI;
|FPRC;

|DEFBUCLE zhazword;
     #99#1;
     ;
     00001;
     99999;
     ;
     NULL, EjecutaEmpresa;
|FIN;

|PROCESO ConfUsuario;
     aAlfa = Usuario % 810;  |QBF Usuario;

     |ABRE #98;
     #98#0 = Usuario % 810;
     |LEE 101#98=;
     |SI FSalida ! 0;
         |DDEFECTO #98;
         #98#0 = Usuario % 810;
         |GRABA 020#98b;
     |FINSI;

     |PUSHV 0501 2380;

     |PINPA #0#98;
     |PINDA #0#98;
     |ENDATOS #1#98;
     |SI FSalida = 0;
         |GRABA 020#98a;
     |FINSI;
     |LIBERA #98;
     |CIERRA #98;

     |POPV;
|FPRC;

|PROCESO Menus;
     #99#2 = nEjerTex;
     |HAZ CreaMemoria;
     |HAZ CreaGestion;

     |PARA; |SI; |HACIENDO;
          |MENU aMenu;
          aMenu2 = FSalida;
          |SI aMenu2 = "1";  |HAZ ConfUsuario;    |FINSI;
          |SI aMenu2 = "2";  |HAZ LinealMemoria;  |FINSI;
          |SI aMenu2 = "3";  |HAZ LinealGestion;  |FINSI;
          |SI aMenu2 = "4";  |SAL;  |FINSI;
          |SI aMenu2 = "5";  |SAL;  |FINSI;
     |SIGUE;
|FPRC;

|PROGRAMA;
     aFichero = (Usuario + "zzzzzzzz") % 108;
     |NOME_DAT #99 aFichero;
     |ABRE #99;  |CIERRA #99;  |DELINDEX #99;

     |PINPA #0#99;
     |ENTLINEAL #1#99, -1, 1, 22, 0, ClaveBaja99, ClaveAlta99;

     |HAZ Menus;
     |SI aMenu2 = "4";
         |BUCLE zhazword;
     |FINSI;

     |DELINDEX #99;
|FPRO;
