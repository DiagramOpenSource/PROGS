|FICHEROS;
    soanteri #0;
    sodacoca #1;    || datos cont. cabs.
    sodacoli #2;    || datos cont. lins.
    maacceso #3;    || datos empresa
    sodacuen #4;    || descripciones cuentas

    soda8m01 #801;
   :/contagen/def/canempre #30;
|FIN;

|VARIABLES;
    nNoCrea = 0;
    alfa1 = "";
    alfa2 = "";
    alfa3 = "";
    alfa4 = "";
    nume1 = 0;
    nume2 = 0;
    nume3 = 0;
    nume4 = 0;
    para1 = 0;
    para2 = 0;
    para3 = 0;
    para4 = 0;
    fech1 = @;
    fech2 = @;
    fech3 = @;
    fech4 = @;
    FEstado = 0;
    seleccio = 0;
    cuenta  = "";
    saldo   = 0;
    t_aper  = 0;
    t_sald  = 0;
    t_actu  = 0;
    ult_lin = 0;
    explo   = 0;
    linea_129 = 0;

    aCuenta = "";
    aNomCta = "";
    aCtaPer = "";

    nCan5 = 0;
    nCan6 = 0;
    nCan7 = 0;
    nCan8 = 0;
    nCan9 = 0;
    nEstado = 0;
    nLinea = 0;
    aDef = "";

    aBase  = "";
    nMira  = 0;
    nSwExi = 0;
    sw     = 0;
    &aPathDsmodelo  = "";
    CampoNivel = 0;
    &cuent   = "";
    Posicion = 0;
    &eaPlanillaS = "";
    &enEjerS     = 0;

    nInkey    = 0;
    eNum2008A = 0;
    &empresa  = 0;
    &anyo     = 0;
|FIN;

|INCLUYE varcon_i;
|INCLUYE varbal_i;

____________________________________/ CALCULOS DESDE PANTALLA
|PROCESO inicio; |TIPO 0;
  |SI FSalida = 2; |ACABA; |FINSI;

  |HAZ BusquedaApli;

  eNum2008 = -1;
  Posicion   = 112;
  |SI eaPathContagen = "";
      dig1 = 1;
      dig3 = 5;
      dig4 = 1;
      dig5 = 2;
      dig6 = 3;
      dig7 = 4;
      dig8 = 12;
      dig9 = 0;
      Posicion   = 112;
  |SINO;
      enEmpresa  = #0#0;
      enPerConta = #0#1;
      enSwSinMen = 1;
      |HAZ LeeEmpConta;
      |SI swerror = 1;
          |HAZ SituaMaestra;
      |FINSI;
      CampoNivel = 13 + dig3;
      Posicion   = 100 + #30CampoNivel;
  |FINSI;
  |SI cuent = "";  cuent = "129000000000";  |FINSI;
  #0#5       = cuent % Posicion;
  |PINTA #0#5;
|FPRC;

|PROCESO min4;
 #4#0 = "            ";
 #4#2 = enPlan;
|FPRC;

|PROCESO max4;
 #4#0 = "zzzzzzzzzzzz";
 #4#2 = enPlan;
|FPRC;

|PROCESO cuen66; |TIPO 66;
|FPRC;

|PROCESO cuenta; |TIPO 6;
   nInkey = FSalida;
   enPlan = 2008; |SI #0#1 < 2008; enPlan = 1990; |FINSI;

   |SI nInkey = 10;
       enPlan = 2008; |SI #0#1 < 2008; enPlan = 1990; |FINSI;
       |ABRE #4;
       #4#0 = #0#5;
       |CONSULTA_F_CLAVE #1#4, min4, max4;
       #0#5    = #4#0;
       |CIERRA #4;
   |FINSI;

    emCta = 1;        || Dar mensajes de error
    eaCuenta = #0Campo;
    salidas  = FSalida;
    |HAZ FiltraPuntos;
    #0Campo = eaCuenta;
    |PINTA #0Campo;
    |HAZ SacaNivel;

    |ABRE #4;
    |DDEFECTO #4;
    #4#0 = #0#5;
    #4#2 = enPlan;
    |LEE 001#4=;
    #0#5    = #4#0; |PINTA #0#5;
    #0#6    = #4#1; |PINTA #0#6;
    |CIERRA #4;
|FPRC;

|PROCESO MiraNivel;
 |SI #0Campo > dig3;
     |MENAV "    Error. No existe Nivel en la Empresa";
     |ERROR;
 |FINSI;
|FPRC;

|CALCULO Alentrar; |TIPO 7;
 fech1 = ~;
 alfa1 = fech1;
 alfa1 = alfa1 % -104;
 #0#1 = alfa1;
 #0#1 = #0#1 - 1;
 #0#3 = #0#1 - 1;
 |PINTA #0#1;
 |PINTA #0#3;
|FCAL;

|CALCULO anterior; |TIPO 0;
    #0#2 = #0#0;
    #0#3 = #0#1 - 1;
    |PINTA #0#2;
    |PINTA #0#3;
|FCAL;

|CALCULO siesta_act; |TIPO 0;
    |SI FSalida ! 2;
        |ABRE #1;
        #1#0 = #0#0;
        #1#1 = #0#1;
        |LEE 001#1=;
        |SI FSalida = 0;
            |AVISO;
            |CONFI "    NEmpresa con datos en este a¤o; dar de baja?: ";
            |SI FSalida = 0;
                |ATRI P;
                |MENSA "    Borrando ...";
                |ATRI 0;
                |BORRA 020#1a;
                |BUCLELIN 1NULL#1;
                |BLIN 4;
            |SINO;
                |ERROR;
            |FINSI;
        |FINSI;
        |CIERRA #1;
    |FINSI;
|FCAL;

|CALCULO siesta_ant; |TIPO 0;
    |ABRE #1;
    #1#0 = #0#2;
    #1#1 = #0#3;
    |LEE 001#1=;
    |SI FSalida ! 0;
        |MENAV "    Empresa sin datos en este a¤o ...";
        |ERROR;
    |SINO;
        eNum2008A = #1#8;
    |FINSI;
    |CIERRA #1;
|FCAL;
____________________________________/ CALCULOS DE CAPTURA
|CALCULO lee_datos;
    |DDEFECTO #3;
    |ABRE #3;
    #3#0 = #1#0;
    #3#1 = #1#1;
    |LEE 021#3=;
    |CIERRA #3;
|FCAL;

|CALCULO lee_cuenta;
  |SI #0#1 = 2008; |Y #0#3 < 2008;
      |SELECT #3#801;
      #801#0 = #0#0;
      #801#2 = cuenta;
      |LEE 001#801=;
      |SI FSalida = 0;
          cuenta = #801#4;
          #2#4   = #801#5;
      |FINSI;
      |SELECT #1#801;
  |FINSI;

  |DDEFECTO #4;
  #4#0 = cuenta;
  #4#2 = enPlan;
  #4#1 = #2#4;
|FCAL;

|CALCULO cabecera;
    |DDEFECTO #1;
    |ABRE #1;
    #1#0 = #0#0;
    #1#1 = #0#1;
    |HAZ lee_datos;
    #1#2 = #3#2;

    |SI eNum2008A = -1;
        empresa = #0#0;
        anyo    = #0#1;
        |SI eNum2008 = -1;
            enDesde = 0;
            |HAZ eLeePCGCCAA;
            |SI enDesde = 0;   |HAZ eLeePCGFicha; |FINSI;
        |FINSI;
        eNum2008A = eNum2008;
    |FINSI;

    #1#8 = eNum2008A;
    |GRABA 020#1n;
    |CIERRA #1;

|SI #0#1 = 2008; |Y #0#3 < 2008;
    nNoCrea = 1;
    |ABRE #801;
    #801#0 = #0#0;
    #801#1 = 1;
    |LEE 001#801];
    |SI FSalida = 0; |Y #801#0 = #0#0;
        nNoCrea = 0;
    |FINSI;
    |CIERRA #801;
    |SI nNoCrea = 1;
        enEmpresa   = #0#0;
        enEjer      = #0#1;
        eaNombreEmp = #1#2;
        |SUB_EJECUTA_NP "soda8m00;Pase";
   |FINSI;
|FINSI;
|FCAL;

|CALCULO lineas3;
    #1#0 = #0#0;
    #1#1 = #0#1;
    |LEE 121#1=;
    |SI FSalida = 0;
        #1#3 = t_aper;        || total apertura
        #1#6 = t_actu;        || total saldo Actual
        #1#7 = t_sald;        || total saldo
        |GRABA 020#1a;
    |FINSI;
    |LIBERA #1;
|FCAL;

|CALCULO lineas2;
       |SI linea_129 ! 0;
           ult_lin = linea_129;
       |SINO;
           ult_lin = ult_lin + 1;
       |FINSI;
       |ABRE #2;
       |DDEFECTO #2;
       #2#0 = #0#0;         || empresa
       #2#1 = #0#1;         || a¤o
       #2#2 = ult_lin;      || registro
       #2#3 = #0#5;         || cuenta
       #2#4 = #0#6;         || titulo
       |LEE 141#2=;
       |SI FSalida ! 0;
           #2#0 = #0#0;         || empresa
           #2#1 = #0#1;         || a¤o
           #2#2 = ult_lin;      || registro
           #2#3 = #0#5;         || cuenta
           #2#4 = #0#6;         || titulo
           |GRABA 020#2b;
       |FINSI;
       #2#5 = explo;        || apertura explotacion
       |SI #0#8 = "S"; #2#8 = #2#5; |FINSI;
       t_actu = t_actu + #2#8;         || total saldo Actual
       |GRABA 020#2a;
       |LIBERA #2;
       |CIERRA #2;
|FCAL;

|CALCULO lineas1;
    saldo = #2#8;
    #2#0  = #0#0;         || empresa
    #2#1  = #0#1;         || a¤o
    enPlan = 2008; |SI #2#1 < 2008; enPlan = 1990; |FINSI;
    cuenta = #2#3;
    |HAZ lee_cuenta;
    #2#3  = cuenta;
    #2#4  = #4#1;         || titulo cuenta
    #2#5  = 0;            || Apertura
    #2#6  = 0;            || debe a cero
    #2#7  = 0;            || haber a cero
    #2#8  = 0;            || saldo a cero
    #2#9  = 0;            || Saldo Ejercicio Anterior

    alfa1 = #2#3;
    alfa1 = alfa1 % 101;
    |SI alfa1 ! "6"; |Y alfa1 ! "7";
        #2#5 = saldo;        || apertura (menos en los grupos 6 y 7)
    |SINO;
        explo = explo + saldo;          || acumula para perd. y ganan.
    |FINSI;
    |SI alfa1 ! "6"; |Y alfa1 ! "7";
        #2#9 = saldo;         || saldo a¤o anterior (resto)

        |SI #0#8 = "S";  #2#8 = #2#5; |FINSI;
    |SINO;
        |SI #0#7 = "S";
            #2#9 = saldo;      || saldo a¤o anterior (explotacion)
        |FINSI;
    |FINSI;
    |GRABA 020#2n;
    |SI FSalida = 0;
        |SI #2#3 = #0#5;
            linea_129 = #2#2;
        |SINO;
            t_aper = t_aper + #2#5;         || total apertura
            t_sald = t_sald + #2#9;         || total saldo a¤o anterior
            t_actu = t_actu + #2#8;         || total saldo Actual
        |FINSI;
        ult_lin = #2#2;                 || guarda ultima linea
    |FINSI;
|FCAL;

|CALCULO lineas;
    t_aper    = 0;
    t_sald    = 0;
    t_actu    = 0;
    ult_lin   = 0;
    linea_129 = 0;

    |ABRE #801;
    |ABRE #4;
    |ABRE #1;
    #1#0 = #0#2;
    #1#1 = #0#3;
    |LEE 021#1=;
    |SI FSalida = 0;
        |BUCLELIN 2lineas1#1;          || graba lineas
    |FINSI;
    |HAZ lineas2;       || graba cuenta explotacion
    |HAZ lineas3;       || regraba totales
    |CIERRA #1;
    |CIERRA #4;
    |CIERRA #801;
|FCAL;
____________________________________/ CALCULO PRINCIPAL
|CALCULO tipo3; |TIPO 3;
    enEjerS = 2008; eaPlanillaS = "sodacuen";  |HAZ PlanDePlanillasS;
    |SI #0#4 = "SI";
        |ATRI P;
        |MENSA "    Capturando ...";
        |ATRI 0;
        |HAZ cabecera;
        |HAZ lineas;
        |BLIN 4;
    |FINSI;
|FCAL;
