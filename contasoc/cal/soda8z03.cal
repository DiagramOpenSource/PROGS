|FICHEROS;
  soda8m11 #0,MANTE;
  soda8m12 #12,MANTE;
  sodacoli #1;
  sodacoca #2;

  soda8z03 #10;
  maacceso #31;

  :/contagen/def/ca8ctrle #400;  ||Control Empresa
  :/contagen/def/ca8comay #401;  ||Control Cuentas de mayor
  :/contagen/def/ca8m0010 #430;  ||Tabla de Conf.Cierre
  :/contagen/def/ca8m0011 #411;  ||Tabla de Conf.Cierre
  :/contagen/def/ca8m0012 #412;  ||Tabla de Conf.Cierre
  :/contagen/def/ca8m0001 #421;
  :/contagen/def/canempre #30;

  sodacoli@ #3;
   :/basica/def/agiconpa #4;     || control pases (basica)
|FIN;

|VARIABLES;

   f1    = "823";
   tecla = "";
   opcion = 0;
 nCrear    = 0;
 nInkey    = 0;
 aAlfa1    = "";
 aAlfa2    = "";
 nNumero   = 0;
 nNume1    = 0;
 nNume2    = 0;
 &enEjerS     = 0;
 &eaPlanillaS = "";
 &enAnyo      = 0;
 nHay      = 0;
 nEmpresa  = 0;
 nAny3     = 0;
 nDEmpresa = 0;
 nHEmpresa = 0;
 nDEjer    = 0;
 nHEjer    = 0;

 esalta    = 0;
 aParam    = "";
 aFichero  = "";
 aCuenta   = "";
 nEstado   = 0;
 nAlgun    = 0;
 nLinea    = 0;
 nError    = 0;
 aMas      = "";
 aDef      = "";
 aCtaPos   = "";
 nSwOp     = 0;
 nOper     = 0;
|FIN;

|INCLUYE varcon_i;
|INCLUYE varbal_i;
|INCLUYE lospat_i;

|| /////////////////////// PROCESOS DE TRABAJO \\\\\\\\\\\\\\\\\\\\

|PROCESO MiroSiHay;
  |SI nHay = -1; nHay = 0; |FINSI;
  nNume1 = 0;
  |SI #1#5 ! 0; |O #1#6 ! 0; |O #1#7 ! 0;
     |O #1#8 ! 0; |O #1#9 ! 0;
      nNume1 = 1;
  |FINSI;
  |SI nNume1 ! 0; nHay = 1; |FINSI;
|FPRC;

|DEFBUCLE MiroSiHay;
   #1#1;
   3;
   nEmpresa, nAny3, 0001, "8           ";
   nEmpresa, nAny3, 9999, "999999999999";
   ;
   NULL, MiroSiHay;
|FIN;

|CALCULO pintanom; |TIPO 6;
 |ABRE #31;
 |SELECT #1#31;
 #31#0 = #10#0;
 #31#1 = #10#1;
 |LEE 040#31=;
 |SI FSalida = 0;
     #10#2 = #31#2;
     |PINTA #10#2;
 |SINO;
    |MENAV "     No existe Empresa en el Ejercicio ";
    |ERROR;
    |CIERRA #31;
    |ACABA;
 |FINSI;
 |CIERRA #31;

 |ABRE #2;
 #2#0 = #10#0;
 #2#1 = #10#1;
 |LEE 040#2=;
 |SI FSalida ! 0;
     |CONFI "    NNo existen datos del Balance de Contabilidad. Continuar ";
     |SI FSalida ! 0;
         |CIERRA #2;
         |ACABA;
     |FINSI;
 |FINSI;
 |CIERRA #2;
  nHay = -1;
  nEmpresa = #10#0;
  nAny3    = #10#1;
  |BUCLE MiroSiHay;
  |SI nHay [ 0;
      aAlfa1 = "    NEsta Empresa no tiene Cuentas del Grupo 8 y 9. Continuar";
      |SI nHay = 0;
          aAlfa1 = "    SLas Cuentas del Grupo 8 y 9 no tienen saldo. Continuar";
      |FINSI;
      |CONFI aAlfa1;
      |SI FSalida ! 0;  |ERROR; |ACABA; |FINSI;
  |FINSI;
  |HAZ traspaso8;
|FCAL;

|PROCESO Borram12;
 |BORRA 020#12a;
 |LIBERA #12;
|FPRC;

|DEFBUCLE borrarm12;
 #12#1;
 ;
 #10#0, #10#1, "            ", 00;
 #10#0, #10#1, "zzzzzzzzzzzz", 99;
 be;
 NULL, Borram12;
|FIN;

|PROCESO Borram11;
 |BORRA 020#0a;
 |LIBERA #0;
|FPRC;

|DEFBUCLE borrarm11;
 #0#1;
 ;
 #10#0, #10#1, "            ";
 #10#0, #10#1, "zzzzzzzzzzzz";
 be;
 NULL, Borram11;
|FIN;

|PROCESO Tipo1_c; |TIPO 1;
 esalta = (FEntrada / 100) ! 0;
 |SI esalta = 3;
     |CONFI "    NEsta Seguro de Borrar la configuracion Cierre grupo 8 y 9 de esta Empresa ";
     |SI FSalida ! 0;
         |ERROR;
         |ACABA;
     |FINSI;
     |BUCLE borrarm11;
     |BUCLE borrarm12;
 |FINSI;
|FPRC;

|PROCESO leeElemento;
 nNume1 = 1;
 |ERROR10;
|FPRC;

|DEFBUCLE LeeElementos;
 #0#1;
 ;
 #10#0,#10#1, "            ";
 #10#0,#10#1, "zzzzzzzzzzzz";
 ;
 NULL, leeElemento;
|FIN;

|PROCESO ComprueboLinea;
 nNume1 = 0;
 |BUCLE LeeElementos;
 |SI nNume1 = 0;
     |BORRA 041#10c;
 |FINSI;
 |LIBERA #10;
|FPRC;

|| **********************************************************************
|RUTINA ClaveBaja0;
  #0#9  = #10#0;
  #0#10 = #10#1;
  |SI FSalida = "POSICION";
      #0#0  = aCtaPos;
  |SINO;
      #0#0  = "8           ";
  |FINSI;
|FRUT;

|RUTINA ClaveAlta0;
     #0#9  = #10#0;
     #0#10 = #10#1;
     #0#0  = "999999999999";
|FRUT;

|PROCESO Tipo7_c;  |TIPO 13;
     enEmpresa = #10#0;
     enEjer    = #10#1;
     |ENTLINEAL #1#0, -3, 7, 22, 0, ClaveBaja0, ClaveAlta0;
|FPRC;

|PROCESO Tipo3_c;  |TIPO 12;
        |HAZ VerContagen;
        enEmpresa   = #10#0;
        enEjer      = #10#1;
||        |ENTLINEAL #1#0, -3, 2, 22, 0, ClaveBaja0, ClaveAlta0;
||        || |HAZ ComprueboLinea;

   nError = 1;
   |PARA; |SI nError ! 0; |HACIENDO;
          |ENTLINEAL #1#0, -3, 2, 22, 0, ClaveBaja0, ClaveAlta0;
          |HAZ MiroError;
          |SI nError ! 0;
              aAlfa1 = "    NExiste contrapartidas y/o reparto mal informados. Salir S/N";
              |SI aParam ! "";
                  aAlfa1 = "    NExiste contrapartidas y/o reparto mal informados. Interrumpir Proceso";
              |FINSI;
              |CONFI aAlfa1;
              |SI FSalida = 0;
                  nError = 0;
                  enErrorW17 = 1;
              |FINSI;
          |FINSI;
   |SIGUE;
|FPRC;

|PROCESO AltaEmpresa;
 #0#9  = #2#0;
 #0#10 = #2#1;
 #0#0  = "            ";
 |LEE 001#0];
 |SI FSalida = 0;
     #10#0 = #2#0;
     #10#1 = #2#1;
     #10#2 = #2#2;
     #10#3 = #0#11;
     |GRABA 040#10n;
 |FINSI;
|FPRC;

|DEFBUCLE AltaEmpresas;
 #2#1;
 ;
 nDEmpresa, nDEjer;
 nHEmpresa, nHEjer;
 e;
 NULL, AltaEmpresa;
|FIN;

|CALCULO traspaso8; |TIPO 0;
    opcion    = (FEntrada / 100) ! 0;
    |SI opcion ! 1; |ACABA; |FINSI;

    enEmpresa = #10#0;
    enEjer    = #10#1;
    |MENSA "0415Pulse <F1> calculo tabla. (si existe la pasa de contabilidad)";
    |LEETECLA tecla;
    f1 = &255 + "823";
    |BLIN 04;

    nInkey = FSalida;
    |SI tecla = f1;
        |HAZ ElPase;
    |FINSI;
|FCAL;
|| ---------------------------------------------------------------------

|PROCESO Leosodacoli;
    #0#9  = #1#0;
    #0#10 = #1#1;
    #0#0  = #1#3;
    |LEE 101#0=;
    nEstado = FSalida;

    |SI #1#8 ! 0;
       eaCuenta = #1#3;
       |HAZ aContra8y9;
       |SI eSwT8y9 = 1; |LIBERA #0; |ACABA; |FINSI;

       #0#0 = #1#3;
       #0#1 = #1#4;
       #0#2 = eaCon8y9;
       #0#3 = eaTit8y9;
       #0#6 = enNivel;
       |SI #1#8 ] 0;
            #0#7 = #1#8;
            #0#8 = "D";
       |SINO;
            #0#7 = -#1#8;
            #0#8 = "H";
       |FINSI;
       aCuenta = #0#2; |QBF aCuenta;
       |SI aCuenta ! "";
            #0#4 = #0#7;  || Todo Repartido
       |FINSI;
       |SI nEstado = 0; |GRABA 020#0n; |FINSI;
       |SI nEstado ! 0; |GRABA 020#0b; |FINSI;
   |FINSI;
|FPRC;

|DEFBUCLE Leosodacoli;
 #1#1;
 3;
 #10#0, #10#1, 0001, "8           ";
 #10#0, #10#1, 9999, "999999999999";
 ;
 NULL, Leosodacoli;
|FIN;

|PROCESO PasoUnoaUno;
 |ABRE #0;
 |BUCLE Leosodacoli;
 |CIERRA #0;
|FPRC;

|| /////////////////////////////////////////////////////////////////////
|PROCESO PasarTabla11;
  |DDEFECTO #0;
  #0#0 = #411#0;
  #0#9 = #10#0;
  #0#10 = #10#1;
  #0#1 = #411#1;
  #0#2 = #411#2;
  #0#3 = #411#3;
  #0#4 = #411#4;
  #0#5 = #411#5;
  #0#6 = #411#6;
  #0#7 = #411#7;
  #0#8 = #411#8;
  #0#11 = eNum2008;
  |GRABA 040#0n;
|FPRC;

|DEFBUCLE PasarTabla11;
 #411#1;
 ;
 "            ";
 "zzzzzzzzzzzz";
 e;
 NULL, PasarTabla11;
|FIN;

|PROCESO PasarTabla12;
  |DDEFECTO #12;
  #12#0 = #412#0;
  #12#7 = #10#0;
  #12#8 = #10#1;
  #12#1 = #412#1;
  #12#2 = #412#2;
  #12#3 = #412#3;
  #12#4 = #412#4;
  #12#5 = #412#5;
  #12#6 = #412#6;
  |GRABA 040#12n;
|FPRC;

|DEFBUCLE PasarTabla12;
 #412#1;
 ;
 "            ", 001;
 "zzzzzzzzzzzz", 999;
 e;
 NULL, PasarTabla12;
|FIN;

|PROCESO Elpasetabla;
  nAlgun = 0;
  |SI aParam ! "";
      #0#9  = #10#0;
      #0#10 = #10#1;
      #0#0  = "            ";
      |LEE 101#0];
      |SI FSalida = 0; |Y #0#9 = #10#0; |Y #0#10 = #10#1;
          |BUCLE borrarm11;
          |BUCLE borrarm12;
      |FINSI;
  |FINSI;
  |SI nCrear = 1;
      nLinea = 1;
     |PATH_DAT #411 aPathConta;
     |PATH_DAT #412 aPathConta;
     |ABRE #0;
     |ABRE #12;
     |BUCLE PasarTabla11;
     |BUCLE PasarTabla12;
     |CIERRA #12;
     |CIERRA #0;
  |SINO;
     |HAZ PasoUnoaUno;
  |FINSI;
|FPRC;

|PROCESO ElPase;
     nCrear = 0;
     |HAZ VerContagen;
     |SI enEnContagen = 1; nCrear = 1; |FINSI;
     |HAZ Elpasetabla;
|FPRC;

|PROGRAMA;

 |HAZ BusquedaApli;
 || ... Tablas estandar que necesita
 enEjerS = 2008; eaPlanillaS = "sodacuen";  |HAZ PlanDePlanillasS;
 |HAZ VerPatrones;

 aParam = PARAMETRO; |QBF aParam;
 || ......................................................................

 nDEmpresa = 00000; nHEmpresa = 99999;
 nDEjer    = 2008;  nHEjer    = 2999;
 |SI aParam ! "";
     nDEmpresa = enEmpresa; nHEmpresa = enEmpresa;
     nDEjer    = enEjer;    nHEjer    = enEjer;
 |FINSI;

 aFichero = ("4" + Usuario) % 108;
 |NOME_DAT #10 aFichero;
 |ABRE #10;  |CIERRA #10;  |DELINDEX #10;

 |ABRE #10;
 |ABRE #0;
 |BUCLE AltaEmpresas;
 |CIERRA #0;
 |CIERRA #10;

 |DBASE aMas;
 aDef = aMas + "def/sodacoli";
 |CARGA_DEF aDef, sodacoli@;
 |SI FSalida ! 0;
     |MENAV "    Error al Carga fichero de Cuentas";
     |ACABA;
  |FINSI;

  |SI aParam = "";
      |MANTE #10;
  |SINO;
      |HAZ LosParametros;
  |FINSI;

  |DELINDEX #10;
  |DESCARGA_DEF #3;

|FPRO;

|PROCESO VerContagen;
        enEnContagen = 0;
        |SI eaPathContagen ! "";
            |ABRE #30;
            |SELECT #4#30;
            aAlfa1 = #10#1;
            aAlfa1 = aAlfa1 % -102;
            nAny3  = aAlfa1;
            #30#2  = #10#0;
            #30#26 = nAny3;
            |LEE 001#30=;
            |SI FSalida = 0;
                aAlfa1 = #30#2; aAlfa1 = ("00000" + aAlfa1) % -105;
                aAlfa2 = #30#3; aAlfa2 = ("0"     + aAlfa2) % -101;
                aAlfa1 = aAlfa1 + aAlfa2;
                aPathConta = eaPathContagen + "fich/" + aAlfa1 +  "/";
                enEnContagen = 1;
                enPerConta   = #30#3;
            |FINSI;
            |CIERRA #30;
            |ABRE #400;
            |DDEFECTO #400;
            #400#0 = #30#2;
            #400#1 = #30#3;
            |LEE 040#400=;
            |CIERRA #400;
            eNum2008 = #400#2;
            enAnyo = 2000 + nAny3;
            |HAZ eHazEjerCCAA;
        |FINSI;
|FPRC;

|PROCESO LosParametros;
  #10#0 = enEmpresa;
  #10#1 = enEjer;
  nEmpresa = #10#0;
  nAny3    = #10#1;

  nHay      = -1;
  |BUCLE MiroSiHay;
  |SI nHay < 0;
      |BUCLE borrarm11;
      |BUCLE borrarm12;
      |ACABA;
  |FINSI;
  |HAZ VerContagen;

   nHay = -1;
   |HAZ CreoTabla;
   |SI nHay ! 1; |ACABA; |FINSI;

   nError = 0;
   |HAZ MiroError;
   |SI nError ! 0;
       enErrorW17 = 1;
       |SI aParam ! "BALANCEN";
           |PINPA #0#10;
           |PINDA #0#10;
           |HAZ Tipo3_c;
       |FINSI;
   |FINSI;
|FPRC;


|| /////////////////////// PROCESOS DE TRABAJO \\\\\\\\\\\\\\\\\\\\
|PROCESO LasLineas;
 aCuenta = #12#2;
 |QBF aCuenta; |SI aCuenta = "";  nError = 1; |FINSI;
 #0#4 = #0#4 + #12#4;
|FPRC;

|DEFBUCLE LasLineas;
 #12#1;
 ;
 #0#9, #0#10, #0#0,01;
 #0#9, #0#10, #0#0,99;
 ;
 NULL, LasLineas;
|FIN;


|| ///////////////////////////////////////////////////////////////
|PROCESO BorroLineas_11;
 |SI nOper = 0;
     |BORRA 020#12a;
 |SINO;
     #0#4  = #0#4 - #12#4;
     #12#4 = 0;
     |GRABA 020#12a;
 |FINSI;
 |LIBERA #12;
|FPRC;

|DEFBUCLE BorroLineas_11;
 #12#1;
 ;
 #0#9, #0#10, #0#0, 01;
 #0#9, #0#10, #0#0, 99;
 be;
 NULL, BorroLineas_11;
|FIN;
|| ///////////////////////////

|PROCESO PrimeraPasada;

  |SI nHay = -1; nHay = 0; |FINSI;  || para ver si existen ctas 8 y 9

  || |SI aParam = "BALANCES"; |O aParam = "BALANCEN"; |HAZ CalSaldo; |FINSI;

  #0#9  = #1#0;
  #0#10 = #1#1;
  #0#0  = #1#3;
  |LEE 101#0=;
  |SI FSalida ! 0;
      |SI #1#8 = 0;
          |ACABA;
      |SINO;
          |SI nSwOp = 0;
              |DDEFECTO #0;
              #0#9  = #1#0;
              #0#10 = #1#1;
              #0#0  = #1#3;
              #0#1  = #1#4;
              eaCuenta = #0#0;
              |HAZ aContra8y9;
              #0#2 = eaCon8y9;
              #0#3 = eaTit8y9;
              |GRABA 020#0b;
              nHay = 1;
          |SINO;
              nError = 3;
              |ERROR10;
              |ACABA;
          |FINSI;
      |FINSI;
   |FINSI;

   |SI nSwOp = 0;
      |SI #1#8 ] 0;
          #0#7 = #1#8;
          #0#8 = "D";
      |SINO;
          #0#7 = -#1#8;
          #0#8 = "H";
      |FINSI;

      aCuenta = #0#2; |QBF aCuenta;
      |SI aCuenta ! "";
          #0#4 = #0#7;  || Todo Repartido
          nOper = 0;
          |BUCLE BorroLineas_11;
      |SINO;
          aAlfa1 = aCuenta % -101;
          |SI #1#8 = 0; |Y aAlfa1 ! "*";
              nOper = 1;
              |BUCLE BorroLineas_11;
          |FINSI;
      |FINSI;
      |GRABA 020#0a;
      |LIBERA #0;
      nHay = 1;
   |FINSI;

   |SI nSwOp = 1;
       aCuenta = #0#2; |QBF aCuenta;
       |SI aCuenta = "";
           #0#4 = 0;
           |BUCLE LasLineas;
           |SI #0#4 ! #0#7; nError = 2; |FINSI;
       |SINO;
           aAlfa1 = aCuenta % -101;
           |SI aAlfa1 = "*"; nError = 1; |FINSI;
       |FINSI;
       |SI nError ! 0; aCtaPos = #0#0; |ERROR10; |FINSI;
   |FINSI;
|FPRC;

|DEFBUCLE PrimeraPasada;
 #1#2;
 ;
 #10#0, #10#1, "8           ";
 #10#0, #10#1, "999999999999";
 e;
 NULL, PrimeraPasada;
|FIN;

|PROCESO SegundaPasada;
  #3#0 = #0#9;
  #3#1 = #0#10;
  #3#3 = #0#0;
  |LEE 001#3=;
  |SI FSalida ! 0;
      |BORRA 020#0a;
      nOper = 0;
      |BUCLE BorroLineas_11;
  |FINSI;
|FPRC;

|DEFBUCLE SegundaPasada;
 #0#1;
 ;
 #10#0, #10#1, "8           ";
 #10#0, #10#1, "999999999999";
 e;
 NULL, SegundaPasada;
|FIN;

|PROCESO CreoTabla;
  nSwOp  = 0;
  |ABRE #0;
  |BUCLE PrimeraPasada;
  |CIERRA #0;

  |ABRE #3;
  |SELECT #2#3;
  |BUCLE SegundaPasada;
  |SELECT #1#3;
  |CIERRA #3;
|FPRC;

|PROCESO MiroError;
  aCtaPos = "";
  nError = 0;
  nSwOp  = 1;
  |ABRE #3;
  |SELECT #2#3;
  |ABRE #0;
  |BUCLE PrimeraPasada;
  |CIERRA #0;

  |BUCLE SegundaPasada;
  |SELECT #1#3;
  |CIERRA #3;
|FPRC;
