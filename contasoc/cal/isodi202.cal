|FICHEROS;
     isodi202;

     :modelos/dsmox003;
     :modelos/dsmom108;
|FIN;

|VARIABLES;
     n0Vent              = 0;
     nBtnEnv             = 0;
     nBtnPre             = 0;
     nHandle             = 0;
     nCampo              = 0;
     nCar                = 0;
     nPos                = 0;
     nCmp                = 0;
     nPan                = 0;
     nEnvio              = 0;
     nIde                = 0;
     nOk                 = 0;

     aAlfa               = "";
     aAlfa1              = "";
     aAlfa2              = "";
     aDIR                = "";
     aAbre               = "";
     aFicPortal          = "";
     aFicRespHtm         = "";
     aFicRespTxt         = "";
     aName               = "";
     aOrigen             = "";
     aDestino            = "";
     aVal                = "";
     aF2                 = "";
     aCan                = "";
     aTecla              = "";
     aFic                = "";
     aPathPlugins        = "";
     aDocRemoto          = "";
     aDocServidor        = "";
     aIPRemoto           = "";
     aFicCert            = "";
     aLon                = "";
     aDato               = "";
     aReg                = "";
     aLetra              = "";
     aSep                = "";
     aPathExe            = "";
     aPath               = "";
     aTIA                = "";

     {1}aContiene        = "";
     {99}aValor          = "";
     {10}aPopup          = "";

     &eaUnidad           = "";
     &eaPathEmision      = "";
     &eaPathInternet     = "";
     &eaPathPreDecla     = "";
     &eaPathLanza        = "";
     &eaPathExplorador   = "";
     &eaNIFCerti         = "";
     &eaNomCerti         = "";
     &eaFicheroPlano     = "";
     &eaFOrigen          = "";
     &eaFDestino         = "";
     &eaDSMAC            = "";
     &eaUnidadBasico     = "";
     &enErrorPortal      = 0;
     &eaNIF              = "";
     &eaCSV              = "";
     &eaJUS              = "";
|FIN;

|PROCESO T0C1;  |TIPO 0;
     |SI #isodi202#1 = "N";
         |ACABA;
     |FINSI;

     |SI #isodi202#8 = "         ";
         |MENAV "0000Seleccione un presentador.";
         #isodi202#1 = "N";
         |PINTA #isodi202#1;
         |ACABA;
     |SINO;
         |ESTADO_CONTROL nBtnPre, "ENABLE";
     |FINSI;

     |SI #isodi202#18 = "I";
         aAlfa = #isodi202#20;  |QBF aAlfa;
         |SI aAlfa = "";
             |MENAV "0000Introduzca el c¢digo NRC";
             #isodi202#1 = "N";
             |PINTA #isodi202#1;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO GrabaPortal;
     aAlfa = aAlfa + &13 + &10;
     |XGRABA nHandle, aAlfa;
|FPRC;

|PROCESO GrabaPortalSin;
     |XGRABA nHandle, aAlfa;
|FPRC;

|PROCESO GrabaAccion;
     |QBF aVal;

     aAlfa = (&9 * 3) + "<input type=" + &34 + "hidden" + &34 + " name=" + &34;
     aAlfa = aAlfa + aName + &34 + " value=" + &34 + aVal + &34 + " />";
     |HAZ GrabaPortal;
|FPRC;

|PROCESO Mod202;
     aName = "FIRNIF";     aVal = #isodi202#8;     |HAZ GrabaAccion;
     aName = "FIRNOMBRE";  aVal = #isodi202#9;     |HAZ GrabaAccion;
     aName = "FIR";        aVal = "FirmaBasica";   |HAZ GrabaAccion;
     aName = "IDI";        aVal = #isodi202#21;    |HAZ GrabaAccion;

     aAlfa = (&9 * 3) + "<input type=" + &34 + "hidden" + &34 + " name=" + &34;
     aAlfa = aAlfa + "F01" + &34 + " value=" + &34;
     |HAZ GrabaPortalSin;

     |XCIERRA nHandle;

     aDestino   = eaPathEmision + aFicPortal;
     aOrigen    = eaPathEmision + eaFicheroPlano;
     |AFEGIR_FICHERO aOrigen, aDestino;

     aAbre      = eaPathEmision + aFicPortal;
     |XABRE "AB", aAbre, nHandle;

     aAlfa = &34 + " />";
     |HAZ GrabaPortal;

     aName = "NRC";        aVal = #isodi202#20;    |HAZ GrabaAccion;

     aAlfa = (&9 * 2) + "</form>";   |HAZ GrabaPortal;
     aAlfa = &9 + "</body>";         |HAZ GrabaPortal;
     aAlfa = "</html>";              |HAZ GrabaPortal;

     |XCIERRA nHandle;
|FPRC;

|PROCESO CopiaFic;
     aContiene1 = eaFDestino;
     |ESPECIFICA "REMOTOMD5", aContiene;
     aDocRemoto = FSalida;  |QBF aDocRemoto;

     aContiene1 = eaFOrigen;
     |ESPECIFICA "MD5", aContiene;
     aDocServidor = FSalida;  |QBF aDocServidor;

     aIPRemoto = "";
     |IP_REMOTO aIPRemoto;  |QBT aIPRemoto;
     |SI aDocRemoto ! aDocServidor;  |O aDocRemoto = "";
         |SI aIPRemoto ! "";
             |ENVIA_FICHERO eaFOrigen, eaFDestino;
         |SINO;
             |COPIA_FICHERO eaFOrigen, eaFDestino;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO Enviar;
     aFicPortal  = "Portal" + #isodi202#12 + ".html";
     aFicRespHtm = "Resp" + #isodi202#12 + ".html";
     aFicRespTxt = "Resp" + #isodi202#12 + ".txt";
     aAbre      = eaPathEmision + aFicPortal;
     |XABRE "WB", aAbre, nHandle;

     aAlfa = "<!DOCTYPE HTML PUBLIC " + &34 + "-//W3C//DTD HTML 4.01 ";
     aAlfa = aAlfa + "Transitional//EN" + &34 + " ";
     aAlfa = aAlfa + &34 + "http://www.w3.org/TR/html4/loose.dtd" + &34 + ">";
     |HAZ GrabaPortal;

     aAlfa = "<html>";
     |HAZ GrabaPortal;

     aAlfa = &9 + "<head>";
     |HAZ GrabaPortal;

     aAlfa = (&9 * 2) + "<title>Documento sin t&iacute;tulo</title>";
     |HAZ GrabaPortal;

     aAlfa = (&9 * 2) + "<meta http-equiv=" + &34 + "Content-Type" + &34;
     aAlfa = aAlfa + " content=" + &34 + "text/html; charset=iso-8859-1";
     aAlfa = aAlfa + &34 + ">";
     |HAZ GrabaPortal;

     aAlfa = &9 + "</head>";
     |HAZ GrabaPortal;

     aAlfa = "";
     |HAZ GrabaPortal;

     aAlfa = &9 + "<script language=" + &34 + "javascript" + &34 + ">";
     |HAZ GrabaPortal;

     aAlfa = (&9 * 2) + "function carga()";
     |HAZ GrabaPortal;

     aAlfa = (&9 * 2) + "{";
     |HAZ GrabaPortal;

     aAlfa = (&9 * 3) + "document.form1.submit();";
     |HAZ GrabaPortal;

     aAlfa = (&9 * 2) + "}";
     |HAZ GrabaPortal;

     aAlfa = &9 + "</script>";
     |HAZ GrabaPortal;

     aAlfa = "";
     |HAZ GrabaPortal;

     aAlfa = &9 + "<body onload=" + &34 + "javascript:carga();" + &34 + ">";
     |HAZ GrabaPortal;

     aAlfa = (&9 * 2) + "<form method=" + &34 + "POST" + &34 + " action=" + &34;
     aAlfa = aAlfa + aDIR + &34 + " name=" + &34 + "form1" + &34;
     aAlfa = aAlfa + " id=" + &34 + "form1" + &34 + ">";
     |HAZ GrabaPortal;

     |HAZ Mod202;

     eaFOrigen  = eaPathEmision + aFicPortal;
     eaFDestino = eaPathLanza   + aFicPortal;
     |HAZ CopiaFic;

     |SI eaPathExplorador ! "";
         aAlfa = eaPathExplorador + " " + eaFDestino;
         aAlfa  = "cmd " + &34 + "/c START /WAIT " + aAlfa + &34;
     |SINO;
         aAlfa  = "cmd " + &34 + "/c START /WAIT " + eaFDestino + &34;
     |FINSI;

     enErrorPortal = 0;

     |RSYSTEM aAlfa;

     aAlfa = eaPathEmision + aFicPortal;      |FBORRA aAlfa;
     aAlfa = eaPathEmision + eaFicheroPlano;  |FBORRA aAlfa;

     |SI enErrorPortal = 0;
         aAbre   = eaPathEmision + Usuario + ".ok";
         |XABRE "WB", aAbre, nHandle;
         |XGRABA nHandle, "OK";
         |XCIERRA nHandle;
     |FINSI;
|FPRC;

|PROCESO BtnEnv;
     nEnvio = 1;

     |PTEC 806;
|FPRC;

|PROCESO Cancelar;
     |PTEC 807;
|FPRC;

|PROCESO ColocaCampos;
     aAlfa = "LABEL,{{7}}Portal directo: " + aDIR;
     |CONTROL_SIMPLE nPan, aAlfa, 0602, 0699, NULL;

     |CONTROL_SIMPLE nPan, "LABEL,{{15}}Conforme ", 3402, 3413, NULL;
     aAlfa = "LABEL,{{7}}(Para finalizar el proceso marque la casilla ";

     aAlfa = aAlfa + &34 + "Conforme"  + &34 + "y pulse en " + &34 + "Firmar y enviar" + &34 + ")";
     |CONTROL_SIMPLE nPan, aAlfa, 3420, 3498, NULL;

     |SI #isodi202#18 = "I";
         |C_V #isodi202#20, 1, "S";
         |C_M #isodi202#20, 1, "S";
     |FINSI;
|FPRC;

|PROCESO Certificados;
     |CONSULTA_CLAVE #2#dsmox003;
     |SI FSalida = 0;
         #isodi202#8 = #dsmox003#1;  |PINTA #isodi202#8;
         #isodi202#9 = #dsmox003#2;  |PINTA #isodi202#9;
     |FINSI;

     |SI #isodi202#8 ! "         ";
         |ESTADO_CONTROL nBtnPre, "ENABLE";
     |FINSI;
|FPRC;

|PROCESO Predeterminar;
     aPopup1  = nBtnPre;  || Si fuera -1 en la posicion
     aPopup2  = 2;
     aPopup3  = "Grabar predeterminado";
     aPopup4  = "Quitar predeterminado";

     IaPopup  = aPopup1  <-;
     |ESPECIFICA "TRACKPOPUP", aPopup;
     |SI FSalida = 1;  |O FSalida = 2;
         |SI FSalida = 1;  aAlfa = #isodi202#8;  |FINSI;
         |SI FSalida = 2;  aAlfa = "";  |FINSI;

         |ABRE #dsmom108;
         #dsmom108#0 = eaDSMAC;
         |LEE 101#dsmom108.=;
         |SI FSalida ! 0;
             |DDEFECTO #dsmom108;
             #dsmom108#0 = eaDSMAC;
             |GRABA 020#dsmom108.b;
         |FINSI;

         #dsmom108#4 = aAlfa;
         |GRABA 020#dsmom108.a;
         |LIBERA #dsmom108;
         |CIERRA #dsmom108;
     |FINSI;
|FPRC;

|PROGRAMA;
     aDIR = "https://www7.aeat.es/wlpl/PFTW-PICW/PresBasica";  || Pruebas
     aDIR = "https://www1.agenciatributaria.gob.es/wlpl/PFTW-PICW/PresBasica";

     |DBASS aPath;  |QBT aPath;
     aAbre = aPath + "modelos/webdirecta.txt";
     |XABRE "E", aAbre, 0;
     |SI FSalida ] 0;
         |XABRE "", aAbre, nHandle;
         |XLEE nHandle, 250, aDIR;
         |XCIERRA nHandle;
         |QBF aDIR;
         |SI aDIR = "";
             |MENAV "0000Configuraci¢n incorrecta de la WEB directa.";
             |ACABA;
         |FINSI;
     |FINSI;

     |VENTANA 0501, 3599, -1, 17, "";
     n0Vent = FSalida;

     |PINPA #0#isodi202;  nPan = FSalida;

     |HAZ ColocaCampos;

     |PINDA #0#isodi202;

     |CONTROL_SIMPLE nPan, "BOTON,{{197}}", 0830, 0831, Certificados;
     |CONTROL_SIMPLE 0, "BOTON,{{207}}Cancelar", 3551, 3561, Cancelar;
     |CONTROL_SIMPLE 0, "BOTON,{{197}}Firmar y enviar", 3535, 3548, BtnEnv;
     nBtnEnv = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,{{197}}Predeterminar presentador", 3502, 3525, Predeterminar;
     nBtnPre = FSalida;

     |SI #isodi202#8 = "         ";
         |ESTADO_CONTROL nBtnPre, "DISABLE";
     |FINSI;

     nEnvio = 0;
     |PARA; |SI;  |HACIENDO;
         |SI #isodi202#1 = "S";
             |ESTADO_CONTROL nBtnEnv, "ENABLE";
         |SINO;
             |ESTADO_CONTROL nBtnEnv, "DISABLE";
         |FINSI;

         |PINTA #isodi202#20;
         |PINTA #isodi202#1;

         |SI #isodi202#18 = "I";
             |ENCAMPO #20#isodi202;
             |SI nEnvio  = 1;  |HAZ Enviar;   |SAL;  |FINSI;
             |SI FSalida = 7;  |SAL;  |FINSI;
         |FINSI;

         |ENCAMPO #1#isodi202;
         |SI nEnvio  = 1;  |HAZ Enviar;   |SAL;  |FINSI;
         |SI FSalida = 7;                 |SAL;  |FINSI;
     |SIGUE;

     |FINVENTANA n0Vent;
|FPRO;

|PROCESO TraeDslistacerti;
     |HAZ LeeUnidadBasico;

     aPathExe = eaUnidadBasico + "\MODELOS";  |RMKDIR aPathExe;
     aPathExe = aPathExe + "\DSMAC";          |RMKDIR aPathExe;
     aPathExe = aPathExe + "\";

     |DBASS aPathPlugins;  |QBT aPathPlugins;
     aPathPlugins = aPathPlugins + "modelos/plugins";
     |MKDIR aPathPlugins;
     aOrigen  = aPathPlugins + "/ds_listacerti.exe";
     aDestino = aPathExe + "ds_listacerti.exe";

     aContiene1 = aDestino;
     |ESPECIFICA "REMOTOMD5", aContiene;
     aDocRemoto = FSalida;  |QBF aDocRemoto;

     aContiene1 = aOrigen;
     |ESPECIFICA "MD5", aContiene;
     aDocServidor = FSalida;  |QBF aDocServidor;

     aIPRemoto = "";
     |IP_REMOTO aIPRemoto;  |QBT aIPRemoto;
     |SI aDocRemoto ! aDocServidor;  |O aDocRemoto = "";
         |SI aIPRemoto ! "";
             |ENVIA_FICHERO aOrigen, aDestino;
         |SINO;
             |COPIA_FICHERO aOrigen, aDestino;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO Datos;
     aValor1 = "";
     aValor2 = "";

     nCampo  = 1;
     IaValor = aValor1<-;
     aLon    = aDato % 0;
     aReg    = "";
     aSep    = ";";
     |PARA nCar = 1; |SI nCar [ aLon; |HACIENDO nCar = nCar + 1;
          nPos   = (nCar * 100) + 1;
          aLetra = aDato % nPos;
          |SI aLetra = aSep;
              aValor  = aReg;
              aReg    = "";
              nCampo  = nCampo + 1;
              IaValor = IaValor + 1;
          |SINO;
               aReg = aReg + aLetra;
          |FINSI;

          |SI nCampo > 90; |SAL; |FINSI;     || Por si.... :)
     |SIGUE;

     aValor = aReg;
|FPRC;

|PROCESO ListaCertificados;
     |HAZ TraeDslistacerti;

     aFicCert = eaPathLanza + "certifi.csv";

     aAbre = eaPathLanza + "ejecuta.bat";
     |XABRE "WBC", aAbre, nHandle;

     aAlfa = aPathExe + "ds_listacerti ds123456 " + aFicCert;
     |XGRABA nHandle, aAlfa;

     |XCIERRA nHandle;

     |RSYSTEM aAbre;

     aAbre = aFicCert;
     |XABRE "UC", aAbre, nHandle;
     |SI FSalida < 0;  |ACABA;  |FINSI;

     nIde = 0;
     |PARA; |SI; |HACIENDO;
          |XLEE nHandle, 250, aDato;
          |SI FSalida < 0;  |SAL;  |FINSI;

          |HAZ Datos;

          nIde = nIde + 1;

          #dsmox003#0 = nIde;
          #dsmox003#1 = aValor1;
          #dsmox003#2 = aValor2;
          |GRABA 020#dsmox003.n;
          |LIBERA #dsmox003;
     |SIGUE;
     |XCIERRA nHandle;

     aAbre = eaPathLanza + "ejecuta.bat";  |RFBORRA aAbre;
     aAbre = aFicCert;                     |RFBORRA aAbre;
|FPRC;

|PROCESO T80;  |TIPO 80;
     aFic = "isodi202" + Usuario;
     |NOME_DAT #isodi202#aFic#;   |ABRET #isodi202;

     aFic = "dsmox003" + Usuario;
     |NOME_DAT #dsmox003#aFic#;
     |ABRE #dsmox003;  |CIERRA #dsmox003;  |DELINDEX #dsmox003;
     |ABRET #dsmox003;

     |LEE 000#isodi202.p;

     eaPathEmision    = #isodi202#2;  |QBF eaPathEmision;
     eaPathInternet   = #isodi202#3;  |QBF eaPathInternet;
     eaPathPreDecla   = #isodi202#4;  |QBF eaPathPreDecla;
     eaPathLanza      = #isodi202#5;  |QBF eaPathLanza;
     eaPathExplorador = #isodi202#6;  |QBF eaPathExplorador;
     eaFicheroPlano   = #isodi202#7;  |QBF eaFicheroPlano;

     aFic = eaPathEmision + Usuario + ".ok";
     |FBORRA aFic;

     |HAZ ListaCertificados;

     |HAZ CogeMac;

     |ABRE #dsmom108;
     #dsmom108#0 = eaDSMAC;
     |LEE 000#dsmom108.=;
     |SI FSalida ! 0;  |DDEFECTO #dsmom108;  |FINSI;
     |CIERRA #dsmom108;

     |SELECT #2#dsmox003;
     #dsmox003#1 = #isodi202#10;
     |SI #dsmom108#4 ! "        ";
         #dsmox003#1 = #dsmom108#4;
     |FINSI;
     |LEE 000#dsmox003.=;
     |SI FSalida ! 0;
         |DDEFECTO #dsmox003;
     |FINSI;
     |SELECT #1#dsmox003;

     #isodi202#8 = #dsmox003#1;
     #isodi202#9 = #dsmox003#2;

     FSalida = 3599;
|FPRC;

|PROCESO T90;  |TIPO 90;
     |CIERRAT #isodi202;  |DELINDEX #isodi202;
     |CIERRAT #dsmox003;  |DELINDEX #dsmox003;
|FPRC;
