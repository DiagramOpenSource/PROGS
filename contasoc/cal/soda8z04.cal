|FICHEROS;
     soda8z04 #0;

     sodacoca #10;
     sodacoli #1;
     soda8w08 #2;

     cuacceso #200;
     maempr10 #210;

    :/contagen/def/ca8m0000 #420;
    :/contagen/def/ca8m0001 #421;
    :/contagen/def/ca8m0002 #422;
    :/contagen/def/ca8cumay;
    :/contagen/def/ca8ctrle;
    :/contagen/def/canempre;

     soda8m11 #431;
     soda8m12 #432;
|FIN;

|VARIABLES;
   fFecha    = @;
   que_anyo  = "";

   informe   = "soda8z04";

   aParam    = "";
   aAlfa1    = "";
   aAlfa2    = "";
   nNume1    = 0;
   nNume2    = 0;
   nCampo    = 0;

   aDCuenta  = "";
   aHCuenta  = "";
   nError    = 0;

   mascara  = "";
   uno      = "";
   dos      = "";
   nNoBal8  = 0;
   nNoPlan8 = 0;

   nNumero  = 0;
   nTipo    = 0;
   mensa    = 0;
   aFichero = "";

   nEjer      = 0;
   aCtaOri    = "";
   aCuenta    = "";
   nImporte   = "";
   aSigno     = "";
   nHay       = 0;
   nHayCCAA   = 0;
   nHayFicha  = 0;

   &empresa  = 0;
   &anyo     = 0;
   &aError   = "";
   &nSwLinea = 0;
   &enAnyo    = 0;
|FIN;

|INCLUYE varbal_i;

|CALCULO que_anyo; |TIPO 7;
   fFecha   = ~;
   que_anyo = fFecha % -104;
   #0#2     = que_anyo;
   #0#2     = #0#2 - 1;
   |PINTA #0#2;
|FCAL;

|CALCULO descri; |TIPO 6;
    |C_M #0Campo, 0, aAlfa1;
    |SI aAlfa1 = "N"; |ACABA; |FINSI;

    |SI Campo = 3;
        nBalNum = #0Campo;
        |HAZ LeeBal0;
    |SINO;
        nCtaNum = #0Campo;
        |HAZ LeeRes0;
    |FINSI;
    |SI nError = 1;
       |MENAV "   Error. No existe Numero de Balance";
       |ERROR;
    |SINO;
        nCampo = Campo + 1;
        |PINTA #0nCampo;
    |FINSI;
|FCAL;

|PROCESO LeeBal0;
 nError = 0;
 |ABRE #420;
 #420#0 = nBalNum;
 |LEE 001#420=;
 |SI FSalida ! 0;
     nError = 1;
 |SINO;
     enTBal   = #420#2;
     aLucratB = #420#3;
     #0#4     = #420#1;
     #0#3     = #420#0;
 |FINSI;
 |CIERRA #420
|FPRC;

|PROCESO LeeRes0;
 nError = 0;
 |ABRE #420;
 #420#0 = nCtaNum;
 |LEE 001#420=;
 |SI FSalida ! 0;
     nError = 1;
 |SINO;
     enTCta   = #420#2;
     aLucratP = #420#3;
     #0#6     = #420#1;
     #0#5     = #420#0;
 |FINSI;
 |CIERRA #420
|FPRC;

|PROCESO QueElijo;
  aAlfa1 = #0#7;
  |C_M #0#3, 1, aAlfa1;
  |C_M #0#5, 1, aAlfa1;
|FPRC;

||////////////////////////////////////////////////////////////////////
|PROCESO LeePCGConta;
     aAlfa1 = #0#2;
     aAlfa1 = aAlfa1 % -102;
     nEjer = aAlfa1;
     |ABRE #canempre;
     |SELECT #4#canempre;
     #canempre#2  = #10#0;
     #canempre#26 = nEjer;
     |LEE 040#canempre.=;
     |SI FSalida = 0;
         |ABRE #ca8ctrle;
         |DDEFECTO #ca8ctrle;
         #ca8ctrle#0 = #canempre#2;
         #ca8ctrle#1 = #canempre#3;
         |LEE 001#ca8ctrle.=;
         |SI FSalida = 0;
             eNum2008 = #ca8ctrle#2;
         |FINSI;
         |CIERRA #ca8ctrle;
    |SINO;
         eNum2008 = 0;
         |SI nCtaNum = 0;  |O nCtaNum = 6;
          |O nCtaNum = 7;  |O nCtaNum = 10; eNum2008 = 0;  |FINSI;
         |SI nCtaNum = 1;  |O nCtaNum = 5;  eNum2008 = 1;  |FINSI;
         |SI nCtaNum = 30; |O nCtaNum = 32; eNum2008 = 30; |FINSI;
         |SI nCtaNum = 31;                  eNum2008 = 31; |FINSI;
         |SI nCtaNum = 40; |O nCtaNum = 42; eNum2008 = 40; |FINSI;
         |SI nCtaNum = 41;                  eNum2008 = 41; |FINSI;
    |FINSI;
    |CIERRA #canempre;
|FPRC;

||////////////////////////////////////////////////////////////////////
|PROCESO MiroSiHay;
  |SI nHay = -1; nHay = 0; |FINSI;
  |SI #1#5 ! 0; |O #1#6 ! 0; |O #1#7 ! 0; |O #1#8 ! 0; |O #1#9 ! 0;
      nHay = 1;
  |FINSI;
|FPRC;

|DEFBUCLE MiroSiHay;
  #1#2;
  ;
  #10#0, #10#1, aDCuenta;
  #10#0, #10#1, aHCuenta;
  ;
  NULL, MiroSiHay;
|FIN;

|PROCESO Grabaw3;
   |DDEFECTO #2;
   #2#0 = aCuenta;
   #2#5 = #10#1;
   #2#6 = aCtaOri;
   |LEE 001#2=;
   |SI FSalida ! 0;
       #2#0 = aCuenta;
       #2#5 = #10#1;
       #2#6 = aCtaOri;
       |GRABA 020#2c;
   |FINSI;
   |SI aSigno = "D";
        #2#1 = #2#1 + nImporte;
   |SINO;
        #2#2 = #2#2 + nImporte;
  |FINSI;
  |GRABA 020#2a;
|FPRC;

|PROCESO reparto_lin;
   aAlfa1 = #432#2; |QBF aAlfa1;
   |SI aAlfa1 ! "";
       aCuenta  = #432#2;
       aSigno   = #431#8;
       nImporte = #432#4;
       |HAZ Grabaw3;
   |FINSI;
|FPRC;

|DEFBUCLE reparto_lin;
    #432#1;
    ;
    #431#9, #431#10, #431#0, 01;
    #431#9, #431#10, #431#0, 99;
    e;
    NULL, reparto_lin;
|FIN;

|PROCESO reparto;
   aCtaOri = #431#0;
   aAlfa1  = #431#2; |QBF aAlfa1;
   |SI aAlfa1 ! "";
       aCuenta  = #431#2;
       nImporte = #431#4;
       aSigno   = #431#8;
       |HAZ Grabaw3;
   |SINO;
       |BUCLE reparto_lin;
   |FINSI;
|FPRC;

|DEFBUCLE reparto;
   #431#1;
   ;
   #10#0, #10#1, "            ";
   #10#0, #10#1, "zzzzzzzzzzzz";
   e;
   NULL, reparto;
|FIN;

|PROCESO conexi8y9;
  aDCuenta = "8           ";
  aHCuenta = "999999999999";
  nHay = -1;

  |BUCLE MiroSiHay;
  |SI nHay = 1;
     |ABRE #2;
     |BUCLE reparto;
     |CIERRA #2;
     |SI enTBal = 3;  nError = 1; |FINSI;
  |FINSI;
|FPRC;
|| /////////////////////////////////////////////////////////////////

|PROCESO LeeReparto;
   #0#11 = #0#11 + (#2#1 - #2#2);

   uno   = #2#0; |QBF uno;
   |SI uno = ""; |ACABA; |FINSI;

   nNoBal8  = 1;
   nNoPlan8 = 1;
   uno  = #2#0; |QBF uno;
   dos  = uno + "000000000000";

   nOk = 0;
   uno = dos % A105;
   |HAZ Mirodefect;
   |SI nNoBal8 = 0; |Y nNoPlan8 = 0; |ACABA;  |FINSI;

   nOk = 0;
   uno = dos % A104;
   |HAZ Mirodefect;
   |SI nNoBal8 = 0; |Y nNoPlan8 = 0; |ACABA;  |FINSI;

   nOk = 0;
   uno = dos % A103;
   |HAZ Mirodefect;
   |SI nNoBal8 = 0; |Y nNoPlan8 = 0; |ACABA;  |FINSI;

   nOk = 1;
   uno = dos % A102;
   |HAZ Mirodefect;
   |SI nNoBal8 = 0; |Y nNoPlan8 = 0; |ACABA;  |FINSI;

||   uno = dos % A101;
||   |HAZ Mirodefect;
   |SI nNoBal8 = 1;
       aError = "LA CUENTA DE REPARTO " + #2#0 + " NO EXISTE EN BALANCE SITUACION";
       nError = 1;
       nSwLinea = 2; |PRINT;
   |FINSI;
   |SI nNoPlan8 = 1;
       aError = "NO EXISTE EN PLAN CONTABLE ";
       nError = 1;
       nSwLinea = 0; |PRINT;
   |FINSI;
|FPRC;

|DEFBUCLE LeeReparto;
 #2#1;
 ;
 #10#1, aDCuenta, aCtaOri;
 #10#1, aHCuenta, aCtaOri;
 e;
 NULL,LeeReparto;
|FIN;

|| /////////////////////////////////////////////////////////////////

|PROCESO saca2008;
   nNoBal8 = 0;
|FPRC;

|DEFBUCLE saca2008;
 #ca8m0002#2;
 ;
 enEjerBal, nNumero, nTipo, uno;
 enEjerBal, nNumero, nTipo, uno;
 ;
 NULL, saca2008;
|FIN;


|PROCESO Mirodefect;
  uno = (uno + "     " ) % 105;

  |SI nOk = 0;
      #ca8cumay#12 = enEjerBal;
      #ca8cumay#10 = eNum2008;
      #ca8cumay#0  = uno;
      |LEE 041#ca8cumay.=;
      |SI FSalida = 0;
          nNoPlan8 = 0;
      |FINSI;
  |FINSI;

  |BUCLE saca2008;
|FPRC;


|PROCESO LeoCuentas;
   |SI aParam = "";
       |PINTA 1856, "                   ";

       |PINTA 1852, #1#0;
       |PINTA 1858, #1#1;
       |PINTA 1863, #1#3;
   |FINSI;

   aAlfa1 = #1#3 % 101;
   aAlfa2 = #1#3 % 103;
   nCampo = 8;  |SI aAlfa1 = "6"; |O aAlfa1 = "7";  nCampo = 9; |FINSI;
   #0nCampo = #0nCampo + #1#8;
   |SI aAlfa2 = "129";
       #0#10 = #0#10 + #1#8;
   |FINSI;

   nTipo = 9;
   |SI aAlfa1 ] "0"; |Y aAlfa1 [ "5";
       nTipo = 0;
       nNumero = nBalNum;
   |FINSI;
   |SI aAlfa1 ] "6"; |Y aAlfa1 [ "7";
       nTipo = 1;
       nNumero = nCtaNum;
   |FINSI;

   |SI aAlfa1 ] "8"; |Y aAlfa1 [ "9";
       #0#11   = 0;
       aCtaOri = #1#3;
       nTipo   = 0;
       nNumero = nBalNum;
       |BUCLE LeeReparto;
       |SI #0#11 ! #1#8;
           aError = "REPARTO INCORRECTO DE LA CUENTA " + aCtaOri;
           nError = 1;
           nSwLinea = 1; |PRINT;
       |FINSI;
       |ACABA;
   |FINSI;

   uno     = #1#3; |QBF uno;
   |SI uno = ""; |ACABA; |FINSI;

   nNoBal8  = 1;
   nNoPlan8 = 1;
   uno  = #1#3; |QBF uno;
   dos  = uno + "000000000000";

   nOk = 0;
   uno = dos % A105;
   |HAZ Mirodefect;
   |SI nNoBal8 = 0; |Y nNoPlan8 = 0; |ACABA;  |FINSI;

   nOk = 0;
   uno = dos % A104;
   |HAZ Mirodefect;
   |SI nNoBal8 = 0; |Y nNoPlan8 = 0; |ACABA;  |FINSI;

   nOk = 0;
   uno = dos % A103;
   |HAZ Mirodefect;
   |SI nNoBal8 = 0; |Y nNoPlan8 = 0; |ACABA;  |FINSI;

   nOk = 1;
   uno = dos % A102;
   |HAZ Mirodefect;
   |SI nNoBal8 = 0; |Y nNoPlan8 = 0; |ACABA;  |FINSI;

||   uno = dos % A101;
||   |HAZ Mirodefect;
   |SI nNoBal8 = 1;
       |SI nTipo = 0; aAlfa1 = "SITUACION"; |FINSI;
       |SI nTipo = 1; aAlfa1 = "RESULTADOS"; |FINSI;
       aError = "NO EXISTE EN BALANCE " + aAlfa1;
       nError = 1;
       nSwLinea = 0; |PRINT;
   |FINSI;
   |SI nNoPlan8 = 1;
       aError = "NO EXISTE EN PLAN CONTABLE ";
       nError = 1;
       nSwLinea = 0; |PRINT;
   |FINSI;
|FPRC;

|DEFBUCLE LeoCuentas;
    #1#2;
    ;
    #10#0, #10#1, "            ";
    #10#0, #10#1, "zzzzzzzzzzzz";
    e;
    NULL, LeoCuentas;
|FIN;

|PROCESO Chequear;

     nHayCCAA   = 0;
     nHayFicha  = 0;

     eNum2008   = #10#8;

     |DDEFECTO #200;
     #200#0 = #10#0;
     #200#1 = #10#1;
     |LEE 041#200=;
     |SI FSalida = 0;
         |SI eNum2008 = -1;
             eNum2008 = #200#14;
         |FINSI;
         nHayCCAA = 1;
     |FINSI;

     |DDEFECTO #210;
     #210#0 = #10#0;
     #210#1 = #10#1;
     |LEE 041#210=;
     |SI FSalida = 0; nHayFicha = 1; |FINSI;

     nNume1 = #0#3;
     nNume2 = #0#5;
     |SI #0#7 = "N";   || cada uno el suyo
         nBalNum = #0#3; nCtaNum = #0#5;
         |SI nHayFicha = 1; nBalNum = #210#101; nCtaNum = #210#102; |FINSI;
         |SI nHayCCAA  = 1; nBalNum = #200#15;  nCtaNum = #200#16;  |FINSI;
         |HAZ LeeBal0;
         |HAZ LeeRes0;
     |FINSI;
     |SI eNum2008 = -1;
         |HAZ LeePCGConta;
     |FINSI;

     #0#8  = 0;
     #0#9  = 0;
     #0#10 = 0;
     nError = 0;

     |DELINDEX #2;
     |HAZ conexi8y9;
     |SI nError = 1;
         aError = "Hay cuentas grupos 8 y 9, y lanza un Balance de PYME";
         nSwLinea = 1; |PRINT;
     |FINSI;

     aDCuenta = "            ";
     aHCuenta = "zzzzzzzzzzzz";
     |ABRE #ca8cumay;
     |BUCLE LeoCuentas;
     |CIERRA #ca8cumay;
     |SI #0#8 ! 0;
         nError = 1;
         aError = "Descuadre en el Balance: " + #0#8;
         nSwLinea = 1; |PRINT;
     |FINSI;
     |SI #0#9 ! #0#10;
         nError = 1;
         aError = "El Saldo de la Cuenta 129 no es correcto";
         nSwLinea = 1; |PRINT;
     |FINSI;

     |SI nError ! 0;
         |PIEINF;
     |SINO;
         |SI mensa = 0;
             |MENAV "    No detectado Error en las Estructuras y Datos Contables";
         |FINSI;
    |FINSI;

    #0#3 = nNume1; #0#5 = nNume2;
|FPRC;

|DEFBUCLE sodacoca;
    #10#1;
    ;
    #0#0, #0#2;
    #0#1, #0#2;
    e;
    NULL, Chequear;
|FIN;

|PROCESO tipo_3; |TIPO 3;
     aFichero = "8f" + Usuario;
     |NOME_DAT #2 aFichero;

     |IMPRE 0;
     |SI FSalida ! 0; |ACABA; |FINSI;

     |INFOR informe;
     |SI FSalida ! 0; |FININF; |ACABA; |FINSI;

     |SI #0#0 ! #0#1; mensa = 1; |FINSI;

     enAnyo = #0#2;
     |HAZ eHazEjerCCAA;

     |ABRE #200;
     |ABRE #210;
     |BUCLE sodacoca;
     |CIERRA #210;
     |CIERRA #200;

     |FININF;
     |FINIMP;
|FPRC;


|PROGRAMA;
     aParam = PARAMETRO;

     |SI aParam = "";
         |MANTE #0;
     |SINO;
         |DDEFECTO #0;
         #0#0 = empresa;
         #0#1 = empresa;
         #0#2 = anyo;
         |HAZ tipo_3;
     |FINSI;
|FPRO;
