|FICHEROS;
     zregcu16;
     zw000012,MANTE;
|FIN;

|VARIABLES;
     nEstado        = 0;
     nPan           = 0;
     nBoton1        = 0;
     nBoton2        = 0;
     nBoton3        = 0;
     nGrid          = 0;
     nRango         = 0;
     nIdSalir       = 0;
     nCampo         = 0;
     nModo          = 0;
     nVentana       = 0;

     aEsc1          = "";
     aEsc2          = "";
     aRetu          = "";
     aTecla         = "";

     {-1}aVent      = "";
         aVent1     = 0;
         aVent2     = 0;
         aVent3     = 0;
         aVent4     = 0;
         aVent5     = "";

     {2}nNivel      = 0;

     &enEmpresa     = 0;
     &enEjerCuadro  = 0;
|FIN;

|PROCESO Tipo12Cuadros;  |TIPO 12;
|FPRC;

|PROCESO Bajazregcu16;
     #zregcu16#0 = enEmpresa;
     #zregcu16#1 = enEjerCuadro;
     #zregcu16#2 = 001;
|FPRC;

|PROCESO Altazregcu16;
     #zregcu16#0 = enEmpresa;
     #zregcu16#1 = enEjerCuadro;
     #zregcu16#2 = 999;
|FPRC;

|PROCESO Eventozregcu16;
     |SI nModo ! 0;  |ACABA;  |FINSI;

     |SI FSalida = 1;  |O FSalida = 2;
         |LEE 000#zregcu16.=;
         nEstado = FSalida;
         |SI FSalida ! 0;  |O #zregcu16#0 ! enEmpresa;  |O #zregcu16#1 ! enEjerCuadro;
             |DDEFECTO #zregcu16;
             nEstado = 1;
         |FINSI;

         |PARA nCampo = 0;  |SI nCampo [ 11;  |HACIENDO nCampo = nCampo + 1;
               #zw000012#nCampo# = #zregcu16#nCampo#;

               |C_M #zw000012#nCampo#, 1, "N";   |PINTA #zw000012#nCampo#;
         |SIGUE;

         FSalida = 0;

         |SI nEstado = 0;
             |ESTADO_CONTROL nBoton2, "ENABLE";
             |ESTADO_CONTROL nBoton3, "ENABLE";
         |SINO;
             |ESTADO_CONTROL nBoton2, "DISABLE";
             |ESTADO_CONTROL nBoton3, "DISABLE";
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO Alta;
     nModo     = 1;
     nIdSalir  = 0;
     InNivel   = nNivel1<-;
     nNivel1   = 0;
     |ESPECIFICA "IDBOTONTECLA", nNivel;

     nIdSalir = nNivel2;
     |ESTADO_CONTROL nIdSalir, "HIDE";

     |ESTADO_CONTROL nBoton1,   "DISABLE";
     |ESTADO_CONTROL nBoton2,   "DISABLE";
     |ESTADO_CONTROL nBoton3,   "DISABLE";
     |ESTADO_CONTROL nGrid,     "DISABLE";

     |DDEFECTO #zw000012;

     #zregcu16#0 = enEmpresa;
     #zregcu16#1 = enEjerCuadro;
     #zregcu16#2 = 999;
     |LEE 000#zregcu16.];
     |SI FSalida = 0;
         |LEE 000#zregcu16.a;
     |SINO;
         |LEE 000#zregcu16.u;
     |FINSI;

     #zw000012#0 = enEmpresa;
     #zw000012#1 = enEjerCuadro;
     #zw000012#2 = 1;
     |SI FSalida = 0;  |Y  #zregcu16#0 = enEmpresa;  |Y  #zregcu16#1 = enEjerCuadro;
          #zw000012#2 = #zregcu16#2 + 1;
     |FINSI;

     |PARA nCampo = 0;  |SI nCampo [ 11;  |HACIENDO nCampo = nCampo + 1;
           |C_M #zw000012#nCampo#, 1, "S";  |PINTA #zw000012#nCampo#;
     |SIGUE;

     |C_M #zw000012#0, 1, "N";
     |C_M #zw000012#1, 1, "N";
     |C_M #zw000012#2, 1, "N";  |PINTA #zw000012#2;

     |ENDATOS #1#zw000012;
     |SI FSalida = 0;
         |PARA nCampo = 0;  |SI nCampo [ 11;  |HACIENDO nCampo = nCampo + 1;
               #zregcu16#nCampo# = #zw000012#nCampo#;
         |SIGUE;
         |GRABA 020#zregcu16.n;
         |LIBERA #zregcu16;
     |FINSI;

     |ESTADO_CONTROL nBoton1,   "ENABLE";
     |ESTADO_CONTROL nBoton2,   "ENABLE";
     |ESTADO_CONTROL nBoton3,   "ENABLE";
     |ESTADO_CONTROL nGrid,     "ENABLE";

     |REFRESCACONTROL nGrid;  |PULSATECLA;

     nModo     = 0;

     |FOCOTECLADO nGrid;

     |ESTADO_CONTROL nIdSalir, "SHOW";

     |PULSATECLA;
|FPRC;

|PROCESO Modificar;
     |LEE 101#zregcu16.=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     nModo     = 1;
     nIdSalir  = 0;
     InNivel   = nNivel1<-;
     nNivel1   = 0;
     |ESPECIFICA "IDBOTONTECLA", nNivel;

     nIdSalir = nNivel2;
     |ESTADO_CONTROL nIdSalir, "HIDE";

     |ESTADO_CONTROL nBoton1,   "DISABLE";
     |ESTADO_CONTROL nBoton2,   "DISABLE";
     |ESTADO_CONTROL nBoton3,   "DISABLE";
     |ESTADO_CONTROL nGrid,     "DISABLE";

     |DDEFECTO #zw000012;

     |PARA nCampo = 0;  |SI nCampo [ 11;  |HACIENDO nCampo = nCampo + 1;
           #zw000012#nCampo# = #zregcu16#nCampo#;

           |C_M #zw000012#nCampo#, 1, "S";  |PINTA #zw000012#nCampo#;
     |SIGUE;

     |C_M #zw000012#0, 1, "N";
     |C_M #zw000012#1, 1, "N";
     |C_M #zw000012#2, 1, "N";  |PINTA #zw000012#2;

     |ENDATOS #1#zw000012;
     |SI FSalida = 0;
         |PARA nCampo = 0;  |SI nCampo [ 11;  |HACIENDO nCampo = nCampo + 1;
               #zregcu16#nCampo# = #zw000012#nCampo#;
         |SIGUE;
         |GRABA 020#zregcu16.a;
         |LIBERA #zregcu16;
     |FINSI;

     |ESTADO_CONTROL nBoton1,   "ENABLE";
     |ESTADO_CONTROL nBoton2,   "ENABLE";
     |ESTADO_CONTROL nBoton3,   "ENABLE";
     |ESTADO_CONTROL nGrid,     "ENABLE";

     |REFRESCACONTROL nGrid;  |PULSATECLA;

     nModo     = 0;

     |FOCOTECLADO nGrid;

     |ESTADO_CONTROL nIdSalir, "SHOW";

     |PULSATECLA;
|FPRC;

|PROCESO Baja;
     |LEE 101#zregcu16.=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |CONFI "0000NSeguro que quiere BORRAR el apunte";
     |SI FSalida ! 0;  |LIBERA #zregcu16;  |ACABA;  |FINSI;

     |BORRA 020#zregcu16.a;
     |LIBERA #zregcu16;

     |REFRESCACONTROL nGrid;  |PULSATECLA;

     FSalida = "SKIPDEFAULT";

     FSalida = 1;  |HAZ Eventozregcu16;
     |PULSATECLA;
     |FOCOTECLADO nGrid;
|FPRC;

|PROGRAMA;
     |VENTANA 0501, 2799, -1, 16, "Arrendamientos financieros";
     nVentana = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVentana;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |ABRET #zregcu16;

     |PINPA #0#zw000012;
     nPan = FSalida;

     #zregcu16#0 = enEmpresa;
     #zregcu16#1 = enEjerCuadro;
     #zregcu16#2 = 1;
     |LEE 000#zregcu16.=;

     nRango = 4 + 8 + 16 + 32 + 128;

     |LINEAL_SIMPLE #1#zregcu16, nRango, 0502, 1598, Bajazregcu16, Altazregcu16, Eventozregcu16;
     nGrid = FSalida;

     FSalida = 1;  |HAZ Eventozregcu16;

     |CONTROL_SIMPLE nPan, "BOTON,{{193}}Alta ", 1664, 1674, Alta;
     nBoton1  = FSalida;

     |CONTROL_SIMPLE nPan, "BOTON,{{197}}Modificar ", 1676, 1686, Modificar;
     nBoton2  = FSalida;

     |CONTROL_SIMPLE nPan, "BOTON,{{199}}Borrar", 1688, 1698, Baja;
     nBoton3  = FSalida;

     |ESTADO_CONTROL nBoton2, "DISABLE";
     |ESTADO_CONTROL nBoton3, "DISABLE";

     |PINDA #0#zw000012;

     aEsc1  = &255 + "806";
     aEsc2  = &255 + "807";
     aRetu  = &255 + "802";
     |PARA; |SI;  |HACIENDO;
          FSalida = "::{{001}}Salir," + nGrid;
          |LEETECLA aTecla;
          |SI aTecla = aEsc1; |SAL;  |FINSI;
          |SI aTecla = aEsc2; |SAL;  |FINSI;
          |SI aTecla = aRetu; |SAL;  |FINSI;
     |SIGUE;

     |FIN_CONTROL_WINDOWS nBoton1;
     |FIN_CONTROL_WINDOWS nBoton2;
     |FIN_CONTROL_WINDOWS nBoton3;
     |DESTRUYECONTROL nGrid;

     |PULSATECLA;

     |CIERRAT #zregcu16;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = nVentana;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |PULSATECLA;

     |FINVENTANA nVentana;
|FPRO;
