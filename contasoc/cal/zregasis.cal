|FICHEROS;
     zregasis;
     zw000018,MANTE;
|FIN;

|VARIABLES;
     n1Btn1           = 0;
     n1Btn2           = 0;
     n1Btn3           = 0;
     n1Vent           = 0;
     n2Vent           = 0;
     nRango           = -1;
     nGrid            = 0;
     nLinea           = 0;

     aEsc1            = "";
     aEsc2            = "";
     aRetu            = "";
     aTecla           = "";

     {-1}aVent        = "";
         aVent1       = 0;
         aVent2       = 0;
         aVent3       = 0;
         aVent4       = 0;
         aVent5       = "";

      &enModo         = 0;
      &enEmpresa      = 0;
      &empresa        = 0;
      &anyo           = 0;
      &enAnyo         = 0;
|FIN;

|PROCESO Baja4;
     #zregasis#0 = enEmpresa;
     #zregasis#1 = enAnyo;
     #zregasis#2 = 1;
|FPRC;

|PROCESO Alta4;
     #zregasis#0 = enEmpresa;
     #zregasis#1 = enAnyo;
     #zregasis#2 = 999;
|FPRC;

|PROCESO Alta;
     #zregasis#0 = enEmpresa;
     #zregasis#1 = enAnyo;
     #zregasis#2 = 999;
     |LEE 000#zregasis.];
     |SI FSalida = 0;
         |LEE 000#zregasis.a;
     |SINO;
         |LEE 000#zregasis.u;
     |FINSI;

     nLinea = 1;
     |SI FSalida = 0;  |Y  #zregasis#0 = enEmpresa;  |Y  #zregasis#1 = enAnyo;
         nLinea = #zregasis#2 + 1;
     |FINSI;

     |DDEFECTO #zw000018;

     |VENTANA 0501, 1599, -1, 16, "Datos firmante";
     n2Vent = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n2Vent;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |PINPA #0#zw000018;
     |PINDA #0#zw000018;
     |ENDATOS #1#zw000018;
     |SI FSalida = 0;
         #zregasis#0 = enEmpresa;
         #zregasis#1 = enAnyo;
         #zregasis#2 = nLinea;
         #zregasis#3 = #zw000018#3;
         #zregasis#4 = #zw000018#4;
         #zregasis#5 = #zw000018#5;

         |GRABA 020#zregasis.n;
         |LIBERA #zregasis;
     |FINSI;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n2Vent;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA n2Vent;

     |REFRESCACONTROL nGrid;
     |FOCOTECLADO nGrid;
|FPRC;

|PROCESO Modificar;
     |LEE 101#zregasis.=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |VENTANA 0501, 1599, -1, 16, "Datos firmante";
     n2Vent = FSalida;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n2Vent;
     aVent5 = "MODAL";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     #zw000018#3  = #zregasis#3;
     #zw000018#4  = #zregasis#4;
     #zw000018#5  = #zregasis#5;

     |PINPA #0#zw000018;
     |PINDA #0#zw000018;
     |ENDATOS #1#zw000018;
     |SI FSalida = 0;
         #zregasis#3 = #zw000018#3;
         #zregasis#4 = #zw000018#4;
         #zregasis#5 = #zw000018#5;

         |GRABA 020#zregasis.a;
         |LIBERA #zregasis;
     |FINSI;

     aVent1 = -1;
     aVent2 = -1;
     aVent3 = 0;
     aVent4 = n2Vent;
     aVent5 = "MODELESS";
     |ESPECIFICA "ESTADO_VENTANA", aVent;

     |FINVENTANA n2Vent;

     |REFRESCACONTROL nGrid;
     |FOCOTECLADO nGrid;
|FPRC;

|PROCESO Baja;
     |LEE 101#zregasis.=;
     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |CONFI "0000NSeguro que quiere borrar el firmante";
     |SI FSalida ! 0;  |LIBERA #zregasis;  |ACABA;  |FINSI;

     |BORRA 020#zregasis.a;
     |LIBERA #zregasis;

     |REFRESCACONTROL nGrid;
     |FOCOTECLADO nGrid;
|FPRC;

|PROCESO Evento4;
     |SI FSalida = 1;  |O FSalida = 2;
         |LEE 000#zregasis.=;
         |SI FSalida = 0;
             |ESTADO_CONTROL n1Btn2, "ENABLE";
             |ESTADO_CONTROL n1Btn3, "ENABLE";
         |FINSI;
     |FINSI;

     |SI FSalida = 4;
         |HAZ Modificar;
     |FINSI;
|FPRC;

|PROGRAMA;
     |VENTANA 0501, 1799, -1, 16, "Asistentes a la reuni¢n";
     n1Vent = FSalida;

     nRango = 4 + 8 + 16 + 32 + 128;

     |ABRE #zregasis;
     |LINEAL_SIMPLE #1#zregasis, nRango, 0502, 1498, Baja4, Alta4, Evento4;
     nGrid = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,{{193}}Alta ", 1564, 1574, Alta;
     n1Btn1  = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,{{197}}Modificar ", 1576, 1586, Modificar;
     n1Btn2  = FSalida;

     |CONTROL_SIMPLE 0, "BOTON,{{199}}Borrar", 1588, 1598, Baja;
     n1Btn3  = FSalida;

     |ESTADO_CONTROL n1Btn2, "DISABLE";
     |ESTADO_CONTROL n1Btn3, "DISABLE";

     aEsc1  = &255 + "806";
     aEsc2  = &255 + "807";
     aRetu  = &255 + "802";
     |PARA; |SI;  |HACIENDO;
          FSalida = "::{{001}}Salir," + nGrid;
          |LEETECLA aTecla;
          |SI aTecla = aEsc1; |SAL;  |FINSI;
          |SI aTecla = aEsc2; |SAL;  |FINSI;
          |SI aTecla = aRetu; |SAL;  |FINSI;
     |SIGUE;

     |CIERRA #zregasis;

     |FIN_CONTROL_WINDOWS n1Btn1;
     |FIN_CONTROL_WINDOWS n1Btn2;
     |FIN_CONTROL_WINDOWS n1Btn3;

     |DESTRUYECONTROL nGrid;

     |FINVENTANA n1Vent;

     |PULSATECLA;
|FPRO;
