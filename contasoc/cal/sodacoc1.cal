|FICHEROS;
   sodacoc1 #0;
   sodacol1 #1;
   sodacoli #2;

   sodacotm #90;    || tmp. ordenacion apuntes por cuenta

   :/contagen/def/canempre #30;     || empresas (agicont)
   :/contagen/def/cacuenta #5;      ||
   :/basica/def/agiconpa #4;        || control pases (basica)
   :/contagen/def/ca8comay;
   sodacuen #8;

   soda8w06 #500,MANTE;
|FIN;

|VARIABLES;
   fichdat = "sodacoli";
   opcion = 0;
   anyant = 0;
   cuenta = "";

   &perio = 0;
   &plani = 0;
   &cuent = "";
   &siono = "";
   &sibloq = "";
   dire = "";
   nume1 = 0;
   nume2 = 0;
   nume3 = 0;
   nume4 = 0;
   alfa1 = "";
   alfa2 = "";
   alfa3 = "";
   alfa4 = "";
   inkey = 0;
   f1    = "823";
   tecla = "";

    linord = 0;
    graord = 0;
    borord = 0;
    camord = 0;
   nSwOp = 0;
    nEmpresa = 0;

   &empresa  = 0;
   &anyo     = 0;
   &nombre   = "";

   &eaImp1 = "";
   &eaImp2 = "";
   &eaImp3 = "";
   &eaCeros = "";
   &eaICtas = "";

   path = "";
   nError = 0;
   sw = 0;
   aCuenta = "";
|FIN;

|INCLUYE varcon_i;
|INCLUYE varbal_i;

|PROCESO inipro; |TIPO 8;
   |NOME_DAT #1 fichdat;
   |HAZ BusquedaApli;
|FPRC;

|| /////////// Datos Lineas

|PROCESO Tipo5; |TIPO 5;
    FSalida = "SINBARRA";

    |HAZ VerConta;
    |SI enPerConta ! -1;
        alfa1 = nEmpresa; alfa1 = ("00000" + alfa1) % -105;
        alfa1 = alfa1 + enPerConta;

        path = eaPathContagen + "fich/" + alfa1 +  "/";
        |PATH_DAT #5 path;
    |FINSI;
|FPRC;

|PROCESO actualiz; |TIPO 2;
       alfa1 = #1#3 % 103;
       |SI alfa1 ! "129";
           #0#3 = #0#3 +. #1#5;
           #0#7 = #0#7 +. #1#9;
       |FINSI;
       #0#4 = #0#4 +. #1#6;
       #0#5 = #0#5 +. #1#7;
       #0#6 = #0#6 +. #1#8;
       |PINTA #0#3;
       |PINTA #0#4;
       |PINTA #0#5;
       |PINTA #0#6;
       |PINTA #0#7;
|FPRC;

|PROCESO cuen66; |TIPO 66;
|FPRC;

|PROCESO leeregi;
   #1#9 = #1#9 + #2#8;
|FPRC;

|DEFBUCLE sodacoli;
   #2#2;
   ;
   #1#0,anyant,#1#3;
   #1#0,anyant,#1#3;
   e;
   NULL,leeregi;
|FIN;

|PROCESO cuenta; |TIPO 0;
    opcion = (FEntrada / 100) ! 0;
    |SI opcion = 1;
        #1#9 = 0;
        anyant = #1#1 - 1;
        |CIERRAT #1;
        |BUCLE sodacoli;
        |PINTA #1#9;
        |ABRET #1;
    |FINSI;

    |HAZ apertura;

    alfa1 = #1#3; |QBF alfa1;
    alfa2 = alfa1 % 0;
    nume1 = alfa2;

    |ABRE #8;
    #8#2 = enPlan;
    #8#0 = #1#3;
    |LEE 001#8=;
    |SI FSalida ! 0;
        #8#2 = enPlan;
        #8#0 = #1#3;
        #8#1 = #1#4;
        |SI nume1 [ 4; |GRABA 020#8n; |FINSI;
    |FINSI;
    |CIERRA #8;
|FPRC;

|PROCESO apertura; |TIPO 0;
         opcion = (FEntrada / 100) ! 0;
         cuenta = #1#3 % 101;
         |SI cuenta = "6"; |O cuenta = "7";
             #1#5 = 0;
         |SINO;
             #1#5 = #1#9;
         |FINSI;
         |SI opcion = 1;
             #1#8 = #1#5;
             |PINTA #1#8;
         |FINSI;
         |HAZ saldo;
|FPRC;

|PROCESO saldo; |TIPO 0;
         |SI #1#8 > #1#5;
             #1#6 = #1#8 - #1#5;
             #1#7 = 0;
         |SINO;
             #1#6 = 0;
             #1#7 = #1#5 - #1#8;
         |FINSI;
|FPRC;

|PROCESO BuscaAnt;
   nError = 1;
   |ABRE #2;
   |SELECT #2#2;
   #2#0 = #1#0;
   #2#1 = #1#1 - 1;
   #2#3 = #1#3;
   |LEE 041#2=;
   |SI FSalida = 0;
       nError = 0;
       #1#4 = #2#4;
   |FINSI;
   |SELECT #1#2;
   |CIERRA #2;
|FPRC;

|PROCESO min8;
 #8#0 = "            ";
 #8#2 = enPlan;
|FPRC;

|PROCESO max8;
 #8#0 = "zzzzzzzzzzzz";
 #8#2 = enPlan;
|FPRC;

|PROCESO min5;
 #5#0 = "            ";
 #5#3 = "S";
|FPRC;

|PROCESO max5;
 #5#0 = "zzzzzzzzzzzz";
 #5#3 = "S";
|FPRC;

|PROCESO cuenta_li; |TIPO 6;
   alfa1 = Contenido;

   inkey = FSalida;
   |SI inkey = 10;
       |SI enPerConta = -1;
           |ABRE #8;
           #8#0 = #1Campo;
           |CONSULTA_F_CLAVE #1#8, min8, max8;
           #1Campo    = #8#0;
           |CIERRA #8;
       |SINO;
           |ABRE #5;
           #5#0 = #1Campo;
           |CONSULTA_F_CLAVE #6#5, min5, max5;
           #1Campo    = #5#0;
           |CIERRA #5;
       |FINSI;
   |FINSI;

    emCta = 1;        || Dar mensajes de error
    eaCuenta = #1Campo;
    salidas  = FSalida;
    |HAZ FiltraPuntos;
    #1Campo = eaCuenta;
    |PINTA #1Campo;

    |HAZ SacaNivel;

    alfa1 = #1Campo; |QBF alfa1;
    alfa2 = alfa1 % 0;
    nume1 = alfa2;

    |SI alfa1 ! #1Campo;
        |SI enPerConta = -1;
            |HAZ BuscaAnt;
            |SI nError = 1;
                |ABRE #8;
                #8#2 = enPlan;
                #8#0 = #1Campo;
                |LEE 001#8=;
                |SI FSalida = 0;
                    #1#4 = #8#1; |PINTA #1#4;
                |SINO;
                    #8#2 = enPlan;
                    #8#0 = #1Campo;
                    |SI nume1 [ 4; |GRABA 020#8n; |FINSI;
                |FINSI;
                |CIERRA #8;
            |FINSI;
        |SINO;
            |ABRE #5;
            #5#0 = #1Campo;
            |LEE 001#5=;
            |SI FSalida = 0;
                #1#4 = #5#2; |PINTA #1#4;
            |FINSI;
            |CIERRA #5;
        |FINSI;
    |FINSI;
|FPRC;

____________________________________/ TRASPASO DESDE AGICONT
|PROCESO lee_empresas1;
    nume1 = #0#1 $ 100;
    |SI nume1 = #30#26;
        perio = #30#3;
       |ERROR10;
    |FINSI;
|FPRC;

|DEFBUCLE lee_empresas;
    #30#1;
    ;
    nume2, 0;
    nume2, 9;
    ;
    NULL, lee_empresas1;
|FIN;

|PROCESO busca_perio;
    perio = -1;
    |SI eaPathContagen ! "";
        nume2 = #0#0;
        |BUCLE lee_empresas;
    |FINSI;
|FPRC;

|PROCESO agiconpa1;
    |SI #4#2 = "I1";
        |SI nSwOp = 0;
            |CONFI "    NConfirme el Traspaso desde CONTABILIDAD (S/N): ";
            |SI FSalida = 0;
                |HAZ busca_perio;
                |SI perio ! -1;
                    empre = #0#0;
                    plani = #4#17;
                    cuent = #4#18;
                    siono = "SI";
                    sibloq = "S";
                    |CIERRAT #0;
                    |SUB_EJECUTA "soagico1";
                    |ABRET #0;
                    |PTEC 802;
                |SINO;
                    alfa1 = "    A쨚 " + #0#1 + " SIN definir en CONTABILIDAD ...";
                    |MENAV alfa1;
                |FINSI;
            |SINO;
                nEmpresa = #4#5;
            |FINSI;
        |FINSI;
    |FINSI;
|FPRC;

|DEFBUCLE agiconpa;
   #4#1;
   ;
   #0#0,  1;
   #0#0, 99;
   ;
   NULL, agiconpa1;
|FIN;

|PROCESO VerConta;
    enPlan = 2008; |SI #0#1 < 2008;enPlan = 1990; |FINSI;
    nEmpresa = #0#0;
    nSwOp = 1;
    |BUCLE agiconpa;
    nume2 = nEmpresa;
    |HAZ busca_perio;
    enPerConta = perio;
|FPRC;

|CALCULO traspaso; |TIPO 0;
    opcion = (FEntrada / 100) ! 0;

    |HAZ VerConta;
    nSwOp = 0;
    |SI opcion ! 1; |ACABA; |FINSI;

    |MENSA "0415 Pulse <F1> para traspaso desde Contabilidad";
    |LEETECLA tecla;
    f1 = &255 + "823";
    |BLIN 04;

    inkey = FSalida;
    |SI tecla = f1;
        |BUCLE agiconpa;
    |FINSI;
|FCAL;

|CALCULO Tipo7C8; |TIPO 7;

    empresa = #0#0;
    anyo    = #0#1;
    enDesde = 0;
    |SI enPerConta ! -1; |HAZ eLeePCGConta; |FINSI;
    |SI enDesde = 0;     |HAZ eLeePCGCCAA;  |FINSI;
    |SI enDesde = 0;     |HAZ eLeePCGFicha; |FINSI;

    opcion = (FEntrada / 100) ! 0;
    |SI opcion = 1; |O #0#8 = -1;
        #0#8 = eNum2008; |PINTA #0#8;
    |FINSI;

    |HAZ Tipo13;

    alfa1 = "S";
    |SI opcion ! 1; |O enDesde ! 0; alfa1 = "N"; |FINSI;
    |C_M #0#8, 1, alfa1;
|FCAL;

|PROCESO Tipo13; |TIPO 13;
    opcion = (FEntrada / 100) ! 0;
    |PINTA 0770, "          ";
    |PINTA 0870, "컴컴컴컴컴";
    |SI opcion > 0; |Y #0#8 ! -1;
        eNum2008 = #0#8;
        |HAZ eLeePCGTodos;
        |ATRI S; |PINTA 0770, eaPlan1; |ATRI 0;
    |FINSI;
|FPRC;

|PROCESO inf;
  #ca8comay#0 = 0;
|FPRC;

|PROCESO sup;
  #ca8comay#0 = 99;
|FPRC;

|CALCULO Tipo0C8; |TIPO 6;
    |C_M #0#8, 0, alfa1;
    |SI alfa1 = "N"; |ACABA; |FINSI;

    |SI FSalida = 10;
       |ABRE #ca8comay;
       #ca8comay#0 = #0#8;
       |CONSULTA_F_CLAVE #1#ca8comay,inf, sup;
       #0#8 = #ca8comay#0;
       |PINTA #0#8;
       |CIERRA #ca8comay;
    |FINSI;

    |ABRE #ca8comay;
    #ca8comay#0 = #0#8;
    |LEE 041#ca8comay.=;
    |SI FSalida ! 0;
        |MENAV "0000Error, no existe codigo de Plan Contable";
        |ERROR;
    |SINO;
        eaPlan1 = #ca8comay#2;
        |ATRI S; |PINTA 0770, eaPlan1; |ATRI 0;
    |FINSI;
    |CIERRA #ca8comay;
|FCAL;

||   proceso cabecera

|PROCESO tipo4; |TIPO 4;

   |PUSHV 0548 1379;
   |BLANCO 0548 1379;

   |PINPA #0#500;

   |SI #0#1 < 2008;
       |POPV;
       |PUSHV 0548 0778;
       |CUADRO 0548 0778;
       |ATRI R;
       |PINTA 0549, "Imprimir borrador de Balances";
       |ATRI I;
       |PINTA 0649 ," Entrada Gerenal de Datos:   ";
       #500#1 = "N"; |C_M #500#1, 1, "N"; |C_V #500#1, 1, "N";
       #500#2 = "N"; |C_M #500#2, 1, "N"; |C_V #500#2, 1, "N";
       #500#3 = "N"; |C_M #500#3, 1, "N"; |C_V #500#3, 1, "N";
       #500#4 = "N"; |C_M #500#4, 1, "N"; |C_V #500#4, 1, "N";
       #500#5 = "N"; |C_M #500#5, 1, "N"; |C_V #500#5, 1, "N";
   |FINSI;

   |PINDA #0#500;
   |ENDATOS #2#500;
   |CONFI "    SCONFORME S/N";
   |SI FSalida ! 0;
       |POPV;
   |SINO;
       |POPV;

       empresa = #0#0;
       anyo    = #0#1;
       empresa = #0#0;
       anyo    = #0#1;

       |SI #500#0 = "S"; |O #500#1 = "S"; |O #500#2 = "S";
           eaImp1  = #500#0;
           eaImp2  = #500#1;
           eaImp3  = #500#2;
           eaCeros = #500#3;
           eaICtas = #500#4;
           |SUB_EJECUTA_NP "soda8y03;BORRADOR";
       |FINSI;

       |SI #500#5 = "S";
           eNum2008 = #0#8;
           |SUB_EJECUTA_NP "soda8z04;ENTRADA";
       |FINSI;
   |FINSI;
|FPRC;
____________________________________/ PROCESOS DE ORDENACION POR CUENTA
|PROCESO lee_tempo1;
    linord = linord + 1;

    #1#0 = #90#0;
    #1#1 = #90#1;
    #1#2 = linord;
    #1#3 = #90#3;
    #1#4 = #90#4;
    #1#5 = #90#5;
    #1#6 = #90#6;
    #1#7 = #90#7;
    #1#8 = #90#8;
    #1#9 = #90#9;
    |GRABA 020#1n;
    |SI FSalida = 0;
        graord = graord + 1;
    |FINSI;
    |SI aCuenta = #1#3; |Y aCuenta ! "";
        sw = 1;
    |FINSI;
    aCuenta = #1#3;
|FPRC;

|DEFBUCLE lee_tempo;
 #90#1;
 ;
 #0#0, #0#1, "            ", 0001;
 #0#0, #0#1, "zzzzzzzzzzzz", 9999;
 ;
 NULL, lee_tempo1;
|FIN;

|PROCESO gra_tempo;
    |DDEFECTO #90;
    #90#0 = #1#0;
    #90#1 = #1#1;
    #90#2 = #1#2;
    #90#3 = #1#3;
    #90#4 = #1#4;
    #90#5 = #1#5;
    #90#6 = #1#6;
    #90#7 = #1#7;
    #90#8 = #1#8;
    #90#9 = #1#9;
    |GRABA 020#90n;
    |SI FSalida = 0;
        |BORRA 020#1a;
        |SI FSalida = 0;
            borord = borord + 1;
        |FINSI;
    |FINSI;
|FPRC;

|PROCESO ordenacion; |TIPO 0;
    inkey = FSalida;

    |SI inkey ! 10; |ACABA;  |FINSI;        || pulsando F2

    opcion = (FEntrada / 100) ! 0;
    |SI opcion ! 2; |ACABA;  |FINSI;        || solo en modificacion

    |AVISO;
    |CONFI "    NOrdenar las lineas por Cuentas Contables: ";
    |SI FSalida ! 0;    |ACABA;   |FINSI;

    |ATRI P;  |MENSA "    Ordenando ...";   |ATRI 0;
    camord = Campo;
    linord = 0;
    graord = 0;
    borord = 0;
    |LEE 121#0=;                  || bloquea la cabecera
    alfa1 = Usuario;
    |QBF alfa1;
    |NOME_DAT #90 alfa1;
    |DELINDEX #90;
    |ABRE #90;
    |CIERRAT #1;
    |BUCLELIN 0gra_tempo#0;       || graba en temporal y borra en sodacoli
    |ABRET #1;
    |CIERRA #90;
    sw = 0;
    aCuenta = "";
    |BUCLE lee_tempo;             || lee de temporal y graba en sodacoli
    |DELINDEX #90;
    |SI linord = 0;
        |MENAV "    Asegurese que existe la empresa y a쨚 informados ...";
    |SINO;
        |SI borord ! graord;
            alfa1 = "    Problemas: borrados [" + borord + "], grabados [" + graord + "] ...";
            |MENAV alfa1;
        |FINSI;
        |SI sw ! 0;
            alfa1 = "    Problemas: Existen Cuentas repetidas, repase sus datos";
            |MENAV alfa1;
        |FINSI;
    |FINSI;
    |ATRI R;  |BLIN 4;   |ATRI 0;
    |LIBERA #0;
    |SI camord = 1;
        |PTEC 802;
    |FINSI;
    |PTEC 802;
|FPRC;

|CALCULO lineasli;
   nume1 = (#1#5 + #1#6 - #1#7) ! 2;
   |SI nume1 ! #1#8;
       |SI #1#8 > #1#5;
           #1#6 = #1#8 - #1#5;
           #1#7 = 0;
       |SINO;
           #1#6 = 0;
           #1#7 = #1#5 - #1#8;
       |FINSI;
       |GRABA 020#1a;
   |FINSI;

    alfa1 = #1#3; alfa1 = alfa1 % 103;
    #0#4 = #0#4 + #1#6;
    #0#5 = #0#5 + #1#7;
    #0#6 = #0#6 + #1#8;
    |SI alfa1 ! "129";
        #0#3 = #0#3 + #1#5;
        #0#7 = #0#7 + #1#9;
    |FINSI;
    |PINTA #0#3;
    |PINTA #0#4;
    |PINTA #0#5;
    |PINTA #0#6;
    |PINTA #0#7;
    |LIBERA #1;
|FCAL;

|CALCULO recalcul; |TIPO 3;
    |PDEFECTO #0, 3, 8;
    |BUCLELIN 0lineasli#0;
|FCAL;

|CALCULO tipo30; |TIPO 30;
    |CONFI "2400NImprimir borrador de Balances: ";
    |SI FSalida = 0;
        |HAZ tipo4;
    |FINSI;
|FCAL;
