|FICHEROS;
    rey00102 #0;
    rem00100 #1;    || datos contribuyentes
    rem00002 #2;    || inmuebles
    rem00003 #3;    || modelo 210
    rehacien #4;    || datos hacienda
    rem00101 #5;    || datos familiares contribuyentes

    rew00008 #10;   || tmp.modelo 210 (a)
    rew00009 #11;   || tmp.comprobacion
|FIN;

|VARIABLES;
    aAlfa    = "";
    aAlfa1   = "";
    aAlfa2   = "";
    TipoDato = "";
    Vacio1   = "";
    Vacio2   = "";
    aLong    = "";
    Cadena   = "";
    aAlfax   = "";

    nAntEmp   = 0;
    nSwError  = 0;
    Handle    = 0;
    nLong     = 0;
    nCont1    = 0;
    Long      = 0;
    LongDec   = 0;
    nNegativo = 0;
    Calc1     = 0;
    Calc2     = 0;
    nSwDatos  = 1;

    Comodin = "";
    Dia     = "";
    Mes     = "";
    MesAlfa = "";
    Year    = "";
    alfa1 = "";
    alfa2 = "";
    alfa3 = "";
    alfa4 = "";
    nume1 = 0;
    nume2 = 0;
    nume3 = 0;
    nume4 = 0;
    para1 = 0;
    para2 = 0;
    para3 = 0;
    para4 = 0;
    fech1 = @;
    fech2 = @;
    fech3 = @;
    fech4 = @;
    elsw1 = 0;
    elsw2 = 0;
    elsw3 = 0;
    elsw4 = 0;
    FEstado = 0;
    seleccio = 0;
    modo = 0;
    sw_rup = 0;
    nHandle = 0;

    &Temporal = "";
    &Cliente01 = 0;
    &Cliente02 = 0;
    &Cliente03 = 0;
    &Cliente04 = 0;
    &Cliente05 = 0;
    &Cliente06 = 0;
    &Cliente07 = 0;
    &Cliente08 = 0;
    &Cliente09 = 0;
    &Cliente10 = 0;
    &Cliente11 = 0;
    &Cliente12 = 0;
    &Cliente13 = 0;
    &Cliente14 = 0;
    &Cliente15 = 0;
    &Cliente16 = 0;
    &Cliente17 = 0;
    &Cliente18 = 0;

    &FechaEmis = "";
    &Nif       = "";
    &Fj        = "";
    &Nombre    = "";
    &Sg        = "";
    &Direccion = "";
    &Numero    = "";
    &Localidad = "";
    &Provincia = "";
    &CodPost   = "";
    &TipoRenta = "";
    &CodPais   = "";
    &NomPais   = "";
    &Escalera  = "";
    &Piso      = "";
    &Puerta    = "";
    &Telefono  = "";

    Cliente = 0;
    Ctipo = "";
    Parte = 0;
    Familiar = 0;
    Varios = 0;
    &Firma = "";
    &Cuenta = "";
    Valido = 0;
|FIN;

|INCLUYE i_consul;
____________________________________/

|PROCESO Mayus;
  #0Campo = #0Campo > #0Campo;  |PINTA #0Campo;
|FPRC;

|PROCESO Fichero;
|DDEFECTO #11;
    #11#0 = #3#0;
|SI Ctipo = "T";    #11#8 = 1;     |FINSI;
|SI Ctipo = "C";    #11#8 = 2;     |FINSI;
|SI Ctipo = "1";    #11#8 = 3;     |FINSI;
|SI Ctipo = "2";    #11#8 = 4;     |FINSI;
|SI Ctipo = "3";    #11#8 = 5;     |FINSI;
|SI Ctipo = "4";    #11#8 = 6;     |FINSI;
|LEE 101#11=;
    FEstado = FSalida;

    #11#1 = Ctipo;  || tipo TC1234
    #11#2 = #3#29;  || NIF
    #11#3 = #3#31;  || nombre
    #11#4 = #3#117; || cuota 210
|SI #3#95 = "X";    #11#5 = "A";   |FINSI;        || tipo 210 A
|SI #3#99 = "X";    #11#5 = "B";   |FINSI;        || tipo 210 B
|SI #3#104= "X";    #11#5 = "C";   |FINSI;        || tipo 210 C
    #11#10 = #3#121;     || impreso S/N

|SI FEstado = 0;    |GRABA 020#11a;     |FINSI;
|SI FEstado ! 0;    |GRABA 020#11n;     |FINSI;
|LIBERA #11;

aAlfa = "C:\AEAT\INTERNET\210\"; |RMKDIR aAlfa;

aAlfa = aAlfa + "210" + (("00000" + #rem00003#0) % -105);
|SI #rem00003#0 ! nAntEmp;
   |XABRE "WCB", aAlfa, Handle;
   nAntEmp = #rem00003#0;
|SINO;
   |XABRE "ACB", aAlfa, Handle;
|FINSI;
|SI FSalida ] 0;
   Cadena = "";
   LongDec = 0; TipoDato = "A";
   Long = 3;  aAlfa = "210";         |HAZ Formateo; Cadena = Cadena + aAlfa;
   Long = 16; aAlfa = #rehacien#2;   |HAZ Formateo; Cadena = Cadena + aAlfa;
   Long = 20; aAlfa = #rehacien#3;   |HAZ Formateo; Cadena = Cadena + aAlfa;
   Long = 5;  aAlfa = (("00" + #rehacien#0) % -102) + (("000" + #rehacien#1) % -103);
                                     |HAZ Formateo; Cadena = Cadena + aAlfa;
   aAlfax = #rem00003#3; |QBF aAlfax;
   Long = 2;  aAlfa = aAlfax % 0102; |HAZ Formateo; Cadena = Cadena + aAlfa;
   Long = 2;  aAlfa = aAlfax % 0402; |HAZ Formateo; Cadena = Cadena + aAlfa;
   Long = 4;  aAlfa = aAlfax % 0704; |HAZ Formateo; Cadena = Cadena + aAlfa;
   Long = 9;  aAlfa = Nif;           |HAZ Formateo; Cadena = Cadena + aAlfa;
   Cadena = Cadena + "20050A";
   Long = 40; aAlfa = Nombre;        |HAZ Formateo; Cadena = Cadena + aAlfa;
   Long = 2;  aAlfa = Sg;            |HAZ Formateo; Cadena = Cadena + aAlfa;
   Long = 25; aAlfa = Direccion;     |HAZ Formateo; Cadena = Cadena + aAlfa;
   Long = 5;  aAlfa = Numero;        |HAZ Formateo; Cadena = Cadena + aAlfa;
   Cadena = Cadena + " ";
   Long = 2;  aAlfa = Escalera;      |HAZ Formateo; Cadena = Cadena + aAlfa;
   Long = 2;  aAlfa = Piso;          |HAZ Formateo; Cadena = Cadena + aAlfa;
   TipoDato = "A";
   Long = 2;  aAlfa = Puerta;        |HAZ Formateo; Cadena = Cadena + aAlfa;
   |XGRABA Handle, Cadena; Cadena = "";

   Long = 20; aAlfa = Localidad;     |HAZ Formateo; Cadena = Cadena + aAlfa;
   Long = 2;  aAlfa = CodPost % 0102;|HAZ Formateo; Cadena = Cadena + aAlfa;
   Long = 5;  aAlfa = CodPost;       |HAZ Formateo; Cadena = Cadena + aAlfa;
   Long = 14; aAlfa = NomPais;       |HAZ Formateo; Cadena = Cadena + aAlfa;
   TipoDato = "N";
   Long = 3;  aAlfa = CodPais;       |HAZ Formateo; Cadena = Cadena + aAlfa;
   |SI #rem00003#14 = "S"; aAlfa = "X    "; |FINSI;
   |SI #rem00003#14 = "R"; aAlfa = " X   "; |FINSI;
   |SI #rem00003#14 = "P"; aAlfa = "  X  "; |FINSI;
   |SI #rem00003#14 = "D"; aAlfa = "   X "; |FINSI;
   |SI #rem00003#14 = "G"; aAlfa = "    X"; |FINSI;
   Cadena = Cadena + aAlfa;

   || Contribuyente
   TipoDato = "A";
   Long = 9;  aAlfa = #rem00003#15;  |HAZ Formateo; Cadena = Cadena + aAlfa;
   Long = 1;  aAlfa = #rem00003#16;  |HAZ Formateo; Cadena = Cadena + aAlfa;
   Long = 40; aAlfa = #rem00003#17;  |HAZ Formateo; Cadena = Cadena + aAlfa;
   Cadena = Cadena + "0000000000000000";
   Long = 2;  aAlfa = #rem00003#18;  |HAZ Formateo; Cadena = Cadena + aAlfa;
   Long = 25; aAlfa = #rem00003#19;  |HAZ Formateo; Cadena = Cadena + aAlfa;
   TipoDato = "N";
   Long = 5;  aAlfa = #rem00003#20;  |HAZ Formateo; Cadena = Cadena + aAlfa;
   Cadena = Cadena + " ";
   TipoDato = "A";
   Long = 20; aAlfa = #rem00003#27;  |HAZ Formateo; Cadena = Cadena + aAlfa;
   Long = 14; aAlfa = #rem00003#28;  |HAZ Formateo; Cadena = Cadena + aAlfa;
   |XGRABA Handle, Cadena; Cadena = "";

   || Representante
   TipoDato = "A";
   Long = 9;  aAlfa = #rem00003#37;  |HAZ Formateo; Cadena = Cadena + aAlfa;
   Long = 1;  aAlfa = #rem00003#38;  |HAZ Formateo; Cadena = Cadena + aAlfa;
   Long = 40; aAlfa = #rem00003#39;  |HAZ Formateo; Cadena = Cadena + aAlfa;
   Long = 2;  aAlfa = #rem00003#40;  |HAZ Formateo; Cadena = Cadena + aAlfa;
   Long = 25; aAlfa = #rem00003#41;  |HAZ Formateo; Cadena = Cadena + aAlfa;
   TipoDato = "N";
   Long = 5;  aAlfa = #rem00003#42;  |HAZ Formateo; Cadena = Cadena + aAlfa;
   Cadena = Cadena + " ";
   TipoDato = "A";
   Long = 20; aAlfa = #rem00003#49;  |HAZ Formateo; Cadena = Cadena + aAlfa;
   |XGRABA Handle, Cadena; Cadena = "";
   TipoDato = "N";
   Long = 2;  aAlfa = #rem00003#47;  |HAZ Formateo; Cadena = Cadena + aAlfa;
   TipoDato = "A";
   aAlfa = (("00" + #rem00003#47) % -102) + (("000" + #rem00003#48) % -103);
   Long = 5;                         |HAZ Formateo; Cadena = Cadena + aAlfa;
   |XGRABA Handle, Cadena; Cadena = "";

   || Pagador / Retenedor
   TipoDato = "A";
   Long = 9;  aAlfa = #rem00003#51;  |HAZ Formateo; Cadena = Cadena + aAlfa;
   Long = 1;  aAlfa = #rem00003#52;  |HAZ Formateo; Cadena = Cadena + aAlfa;
   Long = 40; aAlfa = #rem00003#53;  |HAZ Formateo; Cadena = Cadena + aAlfa;
   Long = 2;  aAlfa = #rem00003#54;  |HAZ Formateo; Cadena = Cadena + aAlfa;
   Long = 25; aAlfa = #rem00003#55;  |HAZ Formateo; Cadena = Cadena + aAlfa;
   TipoDato = "N";
   Long = 5;  aAlfa = #rem00003#56;  |HAZ Formateo; Cadena = Cadena + aAlfa;
   Cadena = Cadena + " ";
   TipoDato = "A";
   Long = 20; aAlfa = #rem00003#62;  |HAZ Formateo; Cadena = Cadena + aAlfa;
   |XGRABA Handle, Cadena; Cadena = "";
   TipoDato = "N";
   Long = 2;  aAlfa = #rem00003#60;  |HAZ Formateo; Cadena = Cadena + aAlfa;
   TipoDato = "A";
   aAlfa = (("00" + #rem00003#60) % -102) + (("000" + #rem00003#61) % -103);
   Long = 5;                         |HAZ Formateo; Cadena = Cadena + aAlfa;
   |XGRABA Handle, Cadena; Cadena = "";
   Long = 2;  aAlfa = #rem00003#122;  |HAZ Formateo; Cadena = Cadena + aAlfa;
   Long = 25; aAlfa = #rem00003#123;  |HAZ Formateo; Cadena = Cadena + aAlfa;
   TipoDato = "N";
   Long = 5;  aAlfa = #rem00003#124;  |HAZ Formateo; Cadena = Cadena + aAlfa;
   TipoDato = "A";
   Cadena = Cadena + " ";
   Long = 2;  aAlfa = #rem00003#125;  |HAZ Formateo; Cadena = Cadena + aAlfa;
   TipoDato = "N";
   Long = 2;  aAlfa = #rem00003#126;  |HAZ Formateo; Cadena = Cadena + aAlfa;
   TipoDato = "A";
   Long = 2;  aAlfa = #rem00003#127;  |HAZ Formateo; Cadena = Cadena + aAlfa;
   Long = 20; aAlfa = #rem00003#128;  |HAZ Formateo; Cadena = Cadena + aAlfa;
   aAlfa = ("00" + #rem00003#129) % -102;
   Long = 2;                          |HAZ Formateo; Cadena = Cadena + aAlfa;
   TipoDato = "N";
   aAlfa = (("00" + #rem00003#129) % -102) + (("000" + #rem00003#130) % -103);
   Cadena = Cadena + aAlfa;
   TipoDato = "A";
   Long = 3;  aAlfa = #rem00003#131;  |HAZ Formateo; Cadena = Cadena + aAlfa;
   TipoDato = "N";
   Long = 2;  aAlfa = #rem00003#132;  |HAZ Formateo; Cadena = Cadena + aAlfa;
   Long = 3;  aAlfa = #rem00003#133;  |HAZ Formateo; Cadena = Cadena + aAlfa;
   |XGRABA Handle, Cadena; Cadena = "";

   TipoDato = "N"; Long = 11; LongDec = 2;
              aAlfa = #rem00003#98;   |HAZ Formateo; Cadena = Cadena + aAlfa;
              aAlfa = #rem00003#100;  |HAZ Formateo; Cadena = Cadena + aAlfa;
              aAlfa = #rem00003#101;  |HAZ Formateo; Cadena = Cadena + aAlfa;
              aAlfa = #rem00003#102;  |HAZ Formateo; Cadena = Cadena + aAlfa;
              aAlfa = #rem00003#103;  |HAZ Formateo; Cadena = Cadena + aAlfa;
   TipoDato = "F"; Long = 8; LongDec = 0;
              aAlfa = #rem00003#107;  |HAZ Formateo; Cadena = Cadena + aAlfa;
   TipoDato = "N"; Long = 11; LongDec = 2;
              aAlfa = #rem00003#108;  |HAZ Formateo; Cadena = Cadena + aAlfa;
              aAlfa = #rem00003#109;  |HAZ Formateo; Cadena = Cadena + aAlfa;
              aAlfa = #rem00003#110;  |HAZ Formateo; Cadena = Cadena + aAlfa;
              aAlfa = #rem00003#112;  |HAZ Formateo; Cadena = Cadena + aAlfa;
                   Long = 2;  LongDec = 2;
              aAlfa = #rem00003#113;  |HAZ Formateo; Cadena = Cadena + aAlfa;
   Cadena = Cadena + #rem00003#134 + #rem00003#135 + #rem00003#136;
   TipoDato = "N"; Long = 11; LongDec = 2;
              aAlfa = #rem00003#114;  |HAZ Formateo; Cadena = Cadena + aAlfa;
              aAlfa = #rem00003#115;  |HAZ Formateo; Cadena = Cadena + aAlfa;
              aAlfa = #rem00003#116;  |HAZ Formateo; Cadena = Cadena + aAlfa;
              aAlfa = #rem00003#117;  |HAZ Formateo; Cadena = Cadena + aAlfa;
   |XGRABA Handle, Cadena; Cadena = "";
   TipoDato = "A"; LongDec = 0;
   Long = 50;  aAlfa = #rem00003#17;  |HAZ Formateo; Cadena = Cadena + aAlfa;
   Long = 50;  aAlfa = "";            |HAZ Formateo; Cadena = Cadena + aAlfa;
   Long = 20;  aAlfa = #rem00003#24;  |HAZ Formateo; Cadena = Cadena + aAlfa;
   |XGRABA Handle, Cadena; Cadena = "";

   Cadena = " " * 50;  |XGRABA Handle, Cadena; Cadena = "";
   Cadena = " " * 50;  |XGRABA Handle, Cadena; Cadena = "";
   Cadena = " " * 50;  |XGRABA Handle, Cadena; Cadena = "";
   Cadena = " " * 50;  |XGRABA Handle, Cadena; Cadena = "";
   Cadena = " " * 50;  |XGRABA Handle, Cadena; Cadena = "";
   Cadena = " " * 50;  |XGRABA Handle, Cadena; Cadena = "";
   Cadena = " " * 50;  |XGRABA Handle, Cadena; Cadena = "";
   Cadena = &13 + &10; |XGRABA Handle, Cadena; Cadena = "";

   |XCIERRA Handle;
|SINO;
   aAlfa = "    No puedo generar el fichero para la empresa " + (("00000" + #rem00003#0) % -105);
   |MENAV aAlfa;
   nSwError = 1;
|FINSI;

|SI #rem00003#14 = "S";
|FINSI;
|FPRC;

|PROCESO Impresora;
  |SI #0#19 = "N";
      |INFOR "m210h001";   |PRINT;   |PIEINF;  |FININF;
      |INFOR "m210h002";   |PRINT;   |PIEINF;  |FININF;
  |SINO;
      |INFOR "m210h002";   |PRINT;   |PIEINF;  |FININF;
      |INFOR "m210h001";   |PRINT;   |PIEINF;  |FININF;
  |FINSI;
|FPRC;

|PROCESO ClaveA;
|ACABA;
    Valido = 0;

    #10#0 = #2#0;
    #10#1 = Ctipo;
|LEE 000#10=;
|SI FSalida = 0;
    |SI #10#2 ! #2#1;
            Valido = 1;
    |SINO;
            #3#98 = #10#3;         || base imponible
            #3#114 = #10#6;        || cuota integra
            #3#115 = #10#4;        || deducciones
            #3#116 = #10#5;        || retenciones
            #3#117 = #10#7;        || cuota diferencial
    |FINSI;
|FINSI;
|FPRC;

|PROCESO CargaTipo;
    Cuenta = #1#53;

|SI Ctipo = "C";
   #3#29 = #1#35;             || NIF conyuge (3)
   #3#31 = #1#39;             || nombre conyuge (3)
   ||SI #3#14 = "S";
      #3#15 = #1#35;         || NIF conyuge (1) (SUJETO PASIVO)
      #3#17 = #1#39;         || nombre conyuge (1)
   ||FINSI;
   Cuenta = #1#54;
|FINSI;

|SI Ctipo ] "1";|Y Ctipo [ "4";
        Familiar = 0;
    |SI Ctipo = "1";    Familiar = #1#84;   |FINSI;
    |SI Ctipo = "2";    Familiar = #1#85;   |FINSI;
    |SI Ctipo = "3";    Familiar = #1#86;  |FINSI;
    |SI Ctipo = "4";    Familiar = #1#87;  |FINSI;
    |SI Familiar ! 0;
       #rem00101#0 = #rem00100#0;
       #rem00101#1 = Familiar;
       |LEE 010#rem00101.=;
       |SI FSalida = 0;
           |SI #3#14 = "S";
              #3#15 = #rem00101#2;        || nif (1)
           |FINSI;
           #3#29 = #rem00101#2;           || nif (3)
           |SI #3#14 = "S";
              #3#17 = #rem00101#3;        || nombre (1)
           |FINSI;
           #3#31 = #rem00101#3;           || nombre (3)

           Cuenta = #rem00101#10;         || cuenta corriente
       |FINSI;
    |SINO;
        alfa1 = "    Familiar [" + Ctipo + "] erroneo en [" + #1#0 + "] ...";
        |MENAV alfa1;
    |FINSI;
|FINSI;
|FPRC;

|PROCESO RecalculaParte;
    #3#98  = #3#98 % Parte;        || base imponible
    #3#100 = #3#100 % Parte;       || ingresos netos
    #3#101 = #3#101 % Parte;       || gastos personal
    #3#102 = #3#102 % Parte;       || gastos aprovis.
    #3#103 = #3#103 % Parte;       || base imponible
    #3#108 = #3#108 % Parte;       || valor transmision
    #3#109 = #3#109 % Parte;       || valor adquisicion
    #3#110 = #3#110 % Parte;       || diferencia
    #3#111 = #3#111 % Parte;       || incremento
    #3#112 = #3#112 % Parte;       || base imponible
    #3#114 = #3#114 % Parte;       || cuota integra
    #3#115 = #3#115 % Parte;       || deducciones donativo
    #3#116 = #3#116 % Parte;       || retenciones
    #3#117 = #3#117 % Parte;       || cuota diferencial
|FPRC;

|PROCESO MiraMes;
   |SI Mes = "01"; MesAlfa = "Enero";      |FINSI;
   |SI Mes = "02"; MesAlfa = "Febrero";    |FINSI;
   |SI Mes = "03"; MesAlfa = "Marzo";      |FINSI;
   |SI Mes = "04"; MesAlfa = "Abril";      |FINSI;
   |SI Mes = "05"; MesAlfa = "Mayo";       |FINSI;
   |SI Mes = "06"; MesAlfa = "Junio";      |FINSI;
   |SI Mes = "07"; MesAlfa = "Julio";      |FINSI;
   |SI Mes = "08"; MesAlfa = "Agosto";     |FINSI;
   |SI Mes = "09"; MesAlfa = "Septiembre"; |FINSI;
   |SI Mes = "10"; MesAlfa = "Octubre";    |FINSI;
   |SI Mes = "11"; MesAlfa = "Noviembre";  |FINSI;
   |SI Mes = "12"; MesAlfa = "Diciembre";  |FINSI;
|FPRC;

|PROCESO Modelo;
    #3#0 = #2#0;
    #3#1 = #2#1;
|LEE 020#3=;             || vuelve a leer 210

|HAZ RecalculaParte;
|ABRE #rem00101;
|HAZ CargaTipo;
|CIERRA #rem00101;
|HAZ ClaveA;

Dia  = #rey00102#18 % 0102;
Mes  = #rey00102#18 % 0402; |HAZ MiraMes;
Year = #rey00102#18 % -104;

FechaEmis = Dia + " de " + MesAlfa + " de " + Year;

|SI #rem00003#14 = "S";
   Nif       = #rem00003#15;
   Fj        = #rem00003#16;
   Nombre    = #rem00003#17;
   Comodin   = #rem00003#18; |QBF Comodin;
   Direccion = Comodin + " " + #rem00003#19;
   Numero    = #rem00003#20;
   Localidad = #rem00003#27;
   Provincia = #rem00003#28;
   Comodin   = (("00" + #rem00003#25) % -102) + (("000" + #rem00003#26) % -103);
   CodPost   = Comodin;
   CodPais   = #rem00003#34;
   NomPais   = #rem00003#33;
   Escalera  = #rem00003#21;
   Piso      = #rem00003#22;
   Puerta    = #rem00003#23;
   Telefono  = #rem00003#24;
|FINSI;

|SI #rem00003#14 = "R";
   Nif       = #rem00003#37;
   Fj        = #rem00003#38;
   Nombre    = #rem00003#39;
   Sg        = #rem00003#40;
   Direccion = #rem00003#41;
   Numero    = #rem00003#42;
   Localidad = #rem00003#49;
   Provincia = #rem00003#50;
   Comodin   = (("00" + #rem00003#47) % -102) + (("000" + #rem00003#48) % -103);
   CodPost   = Comodin;
   Escalera  = #rem00003#43;
   Piso      = #rem00003#44;
   Puerta    = #rem00003#45;
   Telefono  = #rem00003#46;
|FINSI;

|SI #rem00003#14 = "P";
   Nif       = #rem00003#51;
   Fj        = #rem00003#52;
   Nombre    = #rem00003#53;
   Sg        = #rem00003#54;
   Direccion = #rem00003#55;
   Numero    = #rem00003#56;
   Localidad = #rem00003#62;
   Provincia = #rem00003#63;
   Comodin   = (("00" + #rem00003#60) % -102) + (("000" + #rem00003#61) % -103);
   CodPost   = Comodin;
   Escalera  = #rem00003#57;
   Piso      = #rem00003#58;
   Puerta    = #rem00003#59;
|FINSI;

|SI #rem00003#14 = "D";
   Nif       = #rem00003#64;
   Fj        = #rem00003#65;
   Nombre    = #rem00003#66;
   Comodin   = #rem00003#67; |QBF Comodin;
   Direccion = Comodin + " " + #rem00003#68;
   Numero    = #rem00003#69;
   Localidad = #rem00003#75;
   Provincia = #rem00003#76;
   Comodin   = (("00" + #rem00003#73) % -102) + (("000" + #rem00003#74) % -103);
   CodPost   = Comodin;
   Escalera  = #rem00003#70;
   Piso      = #rem00003#71;
   Puerta    = #rem00003#72;
|FINSI;

|SI #rem00003#14 = "G";
   Nif       = #rem00003#77;
   Fj        = #rem00003#78;
   Nombre    = #rem00003#79;
   Comodin   = #rem00003#80; |QBF Comodin;
   Direccion = Comodin + " " + #rem00003#81;
   Numero    = #rem00003#82;
   Localidad = #rem00003#88;
   Provincia = #rem00003#89;
   Comodin   = (("00" + #rem00003#86) % -102) + (("000" + #rem00003#87) % -103);
   CodPost   = Comodin;
   Escalera  = #rem00003#83;
   Piso      = #rem00003#84;
   Puerta    = #rem00003#85;
|FINSI;
|SI #rem00003#95  = "X"; TipoRenta = "01"; |FINSI;
|SI #rem00003#99  = "X"; TipoRenta = "02"; |FINSI;
|SI #rem00003#104 = "X"; TipoRenta = "03"; |FINSI;

||SI Valido = 0;
        Firma = #3#27;  |QBT Firma;
        Firma = (#3#27 - Firma) + Firma;     || ajusta a la derecha
    |SI #rey00102#20 = "P";
        |HAZ Impresora;
    |SINO;
        |HAZ Fichero;
    |FINSI;
||FINSI;
|FPRC;

|PROCESO Emite1;
    #3#0 = #2#0;
    #3#1 = #2#1;
|LEE 101#3=;             || lee modelo 210 (rem00003)
|SI FSalida ! 0;
    |ACABA;
|SINO;
    |SI Temporal = "";
            #3#121 = "S";          || impreso SI
        |GRABA 020#3a;
    |FINSI;
|FINSI;
|LIBERA #3;

    seleccio = 1;

|DDEFECTO #4;
    #4#0 = #1#69;
    #4#1 = #1#70;
|LEE 000#4=;        || datos hacienda

|SI #2#3 = "G";                                        || gananciales
        Ctipo = "T";     Parte = 50;    |HAZ Modelo;
        Ctipo = "C";     Parte = 50;    |HAZ Modelo;
|FINSI;
|SI #2#3 = "V";                                        || varios
    |PARA Varios = 8; |SI Varios [ 13; |HACIENDO Varios = Varios + 1;
        |SI #2Varios ! 0;
                Parte = #2Varios;
            |SI Varios =  8;   Ctipo = "T";   |HAZ Modelo;   |FINSI;
            |SI Varios =  9;   Ctipo = "C";   |HAZ Modelo;   |FINSI;
            |SI Varios = 10;   Ctipo = "1";   |HAZ Modelo;   |FINSI;
            |SI Varios = 11;   Ctipo = "2";   |HAZ Modelo;   |FINSI;
            |SI Varios = 12;   Ctipo = "3";   |HAZ Modelo;   |FINSI;
            |SI Varios = 13;   Ctipo = "4";   |HAZ Modelo;   |FINSI;
        |FINSI;
    |SIGUE;
|FINSI;
|SI #2#3 ! "G";|Y #2#3 ! "V";                          || resto
        Ctipo = #2#3;    Parte = 100;   |HAZ Modelo;
|FINSI;
|FPRC;

|DEFBUCLE Emite;
 #2#1;
 ;
 #1#0,  1;
 #1#0, 99;
 ;
 NULL, Emite1;
|FIN;

|PROCESO GrabaTemporal;
|DDEFECTO #10;
    #10#0 = #2#0;
    #10#1 = Ctipo;
|LEE 101#10=;
    FEstado = FSalida;

|SI #10#2 = 0; #10#2 = #2#1;  |FINSI;   || la linea sera del primero
    #10#3 = #10#3 + (#3#98 % Parte);    || base imponible
    #10#4 = #10#4 + (#3#115 % Parte);   || deducciones donativo
    #10#5 = #10#5 + (#3#116 % Parte);   || retenciones
    #10#6 = #10#6 + (#3#114 % Parte);   || cuota integra
    #10#7 = #10#7 + (#3#117 % Parte);   || cuota diferencial

|SI FEstado = 0;    |GRABA 020#10a;     |FINSI;
|SI FEstado ! 0;    |GRABA 020#10n;     |FINSI;
|LIBERA #10;
|FPRC;

|PROCESO Temporal1;
    #3#0 = #2#0;
    #3#1 = #2#1;
|LEE 000#3=;
|SI FSalida ! 0;    |ACABA;   |FINSI;        || lee modelo 210 (rem00003)
|SI #3#95 ! "X";    |ACABA;   |FINSI;        || solo modelo A

|SI #2#3 = "G";                                        || gananciales
        Ctipo = "T";     Parte = 50;    |HAZ GrabaTemporal;
        Ctipo = "C";     Parte = 50;    |HAZ GrabaTemporal;
|FINSI;
|SI #2#3 = "V";                                        || varios
    |PARA Varios = 8; |SI Varios [ 13; |HACIENDO Varios = Varios + 1;
        |SI #2Varios ! 0;
                Parte = #2Varios;
            |SI Varios =  8;   Ctipo = "T";   |HAZ GrabaTemporal; |FINSI;
            |SI Varios =  9;   Ctipo = "C";   |HAZ GrabaTemporal; |FINSI;
            |SI Varios = 10;   Ctipo = "1";   |HAZ GrabaTemporal; |FINSI;
            |SI Varios = 11;   Ctipo = "2";   |HAZ GrabaTemporal; |FINSI;
            |SI Varios = 12;   Ctipo = "3";   |HAZ GrabaTemporal; |FINSI;
            |SI Varios = 13;   Ctipo = "4";   |HAZ GrabaTemporal; |FINSI;
        |FINSI;
    |SIGUE;
|FINSI;
|SI #2#3 ! "G";|Y #2#3 ! "V";                          || resto
        Ctipo = #2#3;    Parte = 100;   |HAZ GrabaTemporal;
|FINSI;
|FPRC;

|DEFBUCLE Temporal;
 #2#1;
 ;
 #1#0,  1;
 #1#0, 99;
 ;
 NULL, Temporal1;
|FIN;

|PROCESO Tipo3;
    alfa1 = Usuario; |QBT alfa1;
|NOME_DAT #10 alfa1;
|DELINDEX #10;

|ABRE #1;       || datos contribuyentes
|ABRE #3;       || modelo 210
|ABRE #4;       || datos hacienda
|ABRE #10;      || temporal modelo 210 (A)

nSwError = 0;
|PARA Cliente = 0; |SI Cliente [ 17; |HACIENDO Cliente = Cliente + 1;
    |SI #0Cliente ! 0;
            #1#0 = #0Cliente;
        |LEE 000#1=;
        |SI FSalida = 0;|Y #1#10 = "N";
            |BUCLE Temporal;
        |FINSI;
    |FINSI;
|SIGUE;

|PARA Cliente = 0; |SI Cliente [ 17; |HACIENDO Cliente = Cliente + 1;
    |SI #0Cliente ! 0;
            #1#0 = #0Cliente;
        |LEE 000#1=;
        |SI FSalida = 0;|Y #1#10 = "N";
            |BUCLE Emite;
        |FINSI;
    |FINSI;
|SIGUE;
|SI #rey00102#20 = "F";
   |SI nSwError = 0;
      |MENAV "    Ficheros generados correctamente en el directorio 'AEAT/INTERNET/210'";
   |SINO;
      |MENAV "    Se han encontrado errores en la emision de los modelos";
   |FINSI;
|FINSI;

|SI seleccio = 0;|Y Temporal = "";
    |MENAV "    Seleccion vacia ...";
|FINSI;
|CIERRA #1;
|CIERRA #3;
|CIERRA #4;
|CIERRA #10;

|DELINDEX #10;
|FPRC;

|PROGRAMA;
|SI Temporal = "";
    |CLS;
    |CABEZA "Impresion 210";
    |PINPA #0#0;
    |DDEFECTO #0;
    |PINDA #0#0; ||HAZ PintaMoneda;
    |ENDATOS #1#0;
    |SI FSalida = 0;
       |SI #rey00102#20 = "P";
          |IMPRE 0;
       |SINO;
          FSalida = 0;
       |FINSI;
        |SI FSalida = 0;
            |ABRE #11;
            |HAZ Tipo3;
            |CIERRA #11;
        |SINO;
            |MENAV "    Proceso abortado ...";
        |FINSI;
        |SI #rey00102#20 = "P";
           |FINIMP;
        |FINSI;
    |FINSI;
|SINO;
        #0#0 = Cliente01;     #0#1 = Cliente02;   #0#2 = Cliente03;
        #0#3 = Cliente04;     #0#4 = Cliente05;   #0#5 = Cliente06;
        #0#6 = Cliente07;     #0#7 = Cliente08;   #0#8 = Cliente09;
        #0#9 = Cliente10;     #0#10 = Cliente11;  #0#11 = Cliente12;
        #0#12 = Cliente13;    #0#13 = Cliente14;  #0#14 = Cliente15;
        #0#15 = Cliente16;    #0#16 = Cliente17;  #0#17 = Cliente18;
    |NOME_DAT #11 Temporal;
    |ABRE #11;
    |HAZ Tipo3;
    |CIERRA #11;
|FINSI;
|FPRO;

|PROCESO PintaMoneda;
|VARIABLES;
  {-1}aMoneda   = "";
      aMoneda1  = "LEER";
      aMoneda2  = "";
|FIN;
     aAlfa = " Moneda de emision : ";
     IaMoneda = aMoneda1<-;
     |ESPECIFICA "MODO_VER_EURO", aMoneda;
     |SI aMoneda2 = "0";
         aAlfa = aAlfa + "PESETAS";
     |SINO;
         aAlfa = aAlfa + "EUROS  ";
     |FINSI;
     |ATRI I; |PINTA 1822, aAlfa; |ATRI N;
|FPRC;

|PROCESO Formateo;
   |QBF aAlfa;
   |SI TipoDato = "F";
      aAlfa = aAlfa - "."; aAlfa = aAlfa - ".";
      |QBF aAlfa;
      |SI aAlfa = "";         aAlfa = "0" * 8; |FINSI;
      |SI aAlfa = "01010000"; aAlfa = "0" * 8; |FINSI;
      |ACABA;
   |FINSI;
   |SI TipoDato = "N";
      Vacio1 = "0" * Long;
      Vacio2 = "0" * LongDec;
      aAlfa = aAlfa - "-";
      |SI FSalida ! 0;
         nNegativo = 1;
      |SINO;
         nNegativo = 0;
      |FINSI;
   |SINO;
      Vacio1 = " " * Long;
   |FINSI;

   |SI TipoDato ! "N";
      Calc1 = 100 + Long; aAlfa = (aAlfa + Vacio1) % Calc1;
   |SINO;
      |SI LongDec = 0;
         Calc1 = 0 - (100 + Long);
         aAlfa = (Vacio1 + aAlfa) % Calc1;
      |SINO;
         aAlfa = aAlfa - ".";
         |SI FSalida = 0;
            Calc1 = 0 - (100 + Long);
            aAlfa = (Vacio1 + aAlfa) % Calc1;
            aAlfa1 = "0" * LongDec;
         |SINO;
            Calc1 = (((FSalida / 100) ! 0) - 1) + 100;
            Calc2 = (((FSalida / 100) ! 0) * 100) + 99;
            aAlfa1 = aAlfa % Calc2;
            aAlfa = aAlfa % Calc1;
            Calc1 = 0 - (100 + Long);
            aAlfa = (Vacio1 + aAlfa)  % Calc1;
            Calc1 = 100 + LongDec;
            aAlfa1 = (aAlfa1 + Vacio2) % Calc1;
         |FINSI;
         aAlfa = aAlfa + aAlfa1;
      |FINSI;
      |SI nNegativo = 1;
         Calc1 = 0 - (100 + (Long + LongDec)); Calc1 = Calc1 + 1;
         aAlfa = aAlfa % Calc1;
         aAlfa = "N" + aAlfa;
      |FINSI;
   |FINSI;

   aAlfa2 = "";

   |SI TipoDato = "A"; |O TipoDato = "An";
      aLong = aAlfa % 0; nLong = aLong;
      |PARA nCont1 = 1; |SI nCont1 [ nLong; |HACIENDO nCont1 = nCont1 + 1;
         Calc1 = (nCont1 * 100) + 1; aAlfa1  = aAlfa % Calc1;
         aAlfa1 = aAlfa1 > aAlfa1;
         |SI aAlfa1 = " ";                  |VETE Sigue1; |FINSI;
         |SI aAlfa1 ] "A"; |Y aAlfa1 [ "Z"; |VETE Sigue1; |FINSI;
         |SI aAlfa1 ] "0"; |Y aAlfa1 [ "9"; |VETE Sigue1; |FINSI;
         |SI aAlfa1 = "¥"; aAlfa1 = &209;   |VETE Sigue1; |FINSI;
         |SI aAlfa1 = "¤"; aAlfa1 = &209;   |VETE Sigue1; |FINSI;
         |SI aAlfa1 = "ç"; aAlfa1 = &199;   |VETE Sigue1; |FINSI;
         |SI aAlfa1 = "Ç"; aAlfa1 = &199;   |VETE Sigue1; |FINSI;
         |SI aAlfa1 = "-"; aAlfa1 = &45;    |VETE Sigue1; |FINSI;
         |SI aAlfa1 = "'"; aAlfa1 = &49;    |VETE Sigue1; |FINSI;
         |SI TipoDato = "An";
            |SI aAlfa1 = "&"; aAlfa1 = &38;    |VETE Sigue1; |FINSI;
            |SI aAlfa1 = ","; aAlfa1 = &44;    |VETE Sigue1; |FINSI;
            |SI aAlfa1 = "."; aAlfa1 = &46;    |VETE Sigue1; |FINSI;
            |SI aAlfa1 = ":"; aAlfa1 = &58;    |VETE Sigue1; |FINSI;
            |SI aAlfa1 = ";"; aAlfa1 = &59;    |VETE Sigue1; |FINSI;
            |SI aAlfa1 = "_"; aAlfa1 = &95;    |VETE Sigue1; |FINSI;
         |FINSI;
         aAlfa1 = " ";
|ET Sigue1;
         aAlfa2 = aAlfa2 + aAlfa1;
      |SIGUE;
      aAlfa = aAlfa2;
   |FINSI;
   Calc1 = aAlfa;
   |SI nSwDatos = 0; |Y Calc1 ! 0; nSwDatos = 1; |FINSI;
|FPRC;
