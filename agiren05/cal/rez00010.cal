|FICHEROS;
     rez00010 #0;
|FIN;

|VARIABLES;
     aBase        = "";
     aDirPatron   = "";
     aFichero     = "";
     aFicheroD    = "";
     aFicheroH    = "";
     aNombreFich  = "";
     aNombreTexto = "";
     aCadena1     = "";
     aCadena2     = "";
     aQueEs       = "";
     aOrig        = "";
     aDest        = "";
     aOrigen      = "";
     aDestino     = "";
     aDirTexto    = "";
     aExtension   = "";
     aAlfa        = "";
     aIPRemoto    = "";

     nWord        = 0;
|FIN;

|RUTINA ClaveBaja0;
     #0#0 = "            ";
|FRUT;

|RUTINA ClaveAlta0;
     #0#0 = "zzzzzzzzzzzz";
|FRUT;

|PROCESO Tipo11;  |TIPO 11;
     |SI FSalida ! 10;  |ACABA;  |FINSI;

     |DBASE aBase;   |QBF aBase;

     aAlfa = "C:/MODELOS";                   |RMKDIR aAlfa;
     aAlfa = "C:/MODELOS/DOCUMENTOS";        |RMKDIR aAlfa;
     nWord = 1;

     aOrigen = aBase + "word/" + #0#0;
     aDestino  = "C:/MODELOS/DOCUMENTOS/" + #0#0;

     aOrig     = aOrigen;
     aDest     = aDestino;
     aIPRemoto = "";
     |IP_REMOTO aIPRemoto;  |QBT aIPRemoto;
     |SI aIPRemoto ! "";
         |ENVIA_FICHERO aOrig, aDest;
     |SINO;
         |COPIA_FICHERO aOrig, aDest;
     |FINSI;

     |MENSA "     Abriendo el Documento, Espere un momento ";

     |WORD_ABRE nWord, aDestino;

     |MENSA "     Documento Abierto              ";

     |WORD_PROPIEDADES nWord, "Visible", "SI";

     FSalida = "NO";
     |PARA;  |SI FSalida = "NO";  |HACIENDO;
             |PULSATECLA;
             |WORD_PROPIEDADES nWord, "Quit", "";
     |SIGUE;

     |MENSA "     Cerrando el Documento, Espere un momento ";

     aOrig = aDestino;
     aDest = aOrigen;

     |SI aIPRemoto ! "";
         |RECIBE_FICHERO aOrig, aDest;
     |SINO;
         |COPIA_FICHERO aOrig, aDest;
     |FINSI;

     |RFBORRA aDestino;
|FPRC;

|PROGRAMA;
     |DBASE aBase;

     aDirPatron = aBase + "word/";

     aFichero = ("10" + Usuario + "zzzzzzzz") % 108;
     |NOME_DAT #0 aFichero;

     |ABRE #0;  |CIERRA #0;  |DELINDEX #0;

     |ABRE #0;

     |DDEFECTO #0;

     aOrigen    = aDirPatron;
     aExtension = ".doc";
     |_PDIR aOrigen;
     |PARA; |SI FSalida = 0; |HACIENDO;
            aCadena1 = aOrigen    > " ";
            aCadena2 = aExtension > " ";
            aDestino = aCadena1 - aCadena2;
            |SI aDestino ! aCadena1;
                aCadena1    = aOrigen > " ";
                aCadena2    = aDirPatron > " ";  |QBF aCadena2
                aNombreFich = aCadena1 % -112;
                #0#0 = aNombreFich < "";
                |SI #0#0 ! "wcompro0.doc";
                    |SI #0#0 = "wcartne1.doc"; #0#1 = "Carta necesidades (Carta)";                    |FINSI;
                    |SI #0#0 = "wcartne2.doc"; #0#1 = "Carta necesidades (Anexos)";                   |FINSI;
                    |SI #0#0 = "wcompro1.doc"; #0#1 = "Informe Comprobacion (D.Personales)";          |FINSI;
                    |SI #0#0 = "wcompro2.doc"; #0#1 = "Informe Comprobacion (Resto)";                 |FINSI;
                    |SI #0#0 = "wreprese.doc"; #0#1 = "Carta Representacion Medios Telematicos";      |FINSI;
                    |SI #0#0 = "w2plazo1.doc"; #0#1 = "Carta 2§ Plazo Domiciliados";                  |FINSI;
                    |SI #0#0 = "w2plazo2.doc"; #0#1 = "Carta 2§ Plazo No Domiciliados";               |FINSI;
                    |SI #0#0 = "wagricol.doc"; #0#1 = "Carta Devolucion Agricolas B.O.E. 23.05.2006"; |FINSI;
                    |GRABA 020#0n;
                    |LIBERA #0;
                |FINSI;
            |FINSI;

            |_SDIR aOrigen;
     |SIGUE;
     |CIERRA #0;

     |CABEZA "Editor Documentos Word";
     |ENTLINEAL #1#0, -1, 4, 22, 1, ClaveBaja0, ClaveAlta0;
|FPRO;
