|VARIABLES;
vacio     = "";
letrasnif = "TRWAGMYFPDXBNJZSQVHLCKE";
letra1 = "";
letra2 = "";
letra3 = "";
letra4 = "";
letra5  = "";
letra6  = "";
letra7  = "";
letra8  = "";
xx = "";
xnumerica1 = 0;
xnumerica2 = 0;
xnumerica3 = 0;
xnumerica4 = 0;
xnumerica5 = 0;
|FIN;

|CALCULO letrarel;     || CALCULA LETRA PARA EL NIF CUANDO SE PULSA -F1-
                       || PARAMETROS ENTRADA:  '#0Campo'
                       || PARAMETROS SALIDA:   '|PINTA #0Campo'
                       || VARIABLES: H_


|SI FSalida = 9;
    letra2 = #0Campo;                          || recoge valor en variable trabajo
    |QBT letra2;                               || le quita los espacios en blanco
        xx = letra2 % 101;                     || carga primer caracter
    |SI xx!"A";|Y xx!"B";|Y xx!"C";|Y xx!"D";
         |Y xx!"E";|Y xx!"F";                  || si es un CIF !!!
         |Y xx!"G";|Y xx!"P";|Y xx!"Q";|Y xx!"S";|Y xx!"X";
            letra4 = "";                       || inicializa variable de trabajo
            letra3 = letra2 % 0;               || halla la longitud en caracteres
            xnumerica2 = letra3;                || recoge valor en variable numerica
        |PARA xnumerica5 = 1;  |SI xnumerica5 [ xnumerica2;  |HACIENDO xnumerica5 = xnumerica5 + 1;
                xnumerica3 = (xnumerica5 * 100) + 1;       || construye literal
                letra3 = letra2 % xnumerica3;           || despreciando puntos, etc.
            |SI letra3="0";|O letra3="1";|O letra3="2";|O letra3="3";
                          |O letra3="4";|O letra3="5";|O letra3="6";
                          |O letra3="7";|O letra3="8";|O letra3="9";
                    letra4 = letra4 + letra3;
            |FINSI;
        |SIGUE;
            xnumerica4 = letra4;
        |SI xnumerica4 ! 0; |Y letra4 ! "";
                xnumerica1 = xnumerica4 $ 23;       || halla el numerica1
                xnumerica1 = xnumerica1 + 1;         || le suma uno para no operar con cero
                xnumerica3 = (xnumerica1 * 100) + 1; || halla la posicion en el literal
                letra1 = letrasnif % xnumerica3; || saca la letra segun posicion

                letra4 = "000000000000" + letra4;
                letra7 = letra4 % -103;
                letra6 = letra4 % -403;
                letra5 = letra4 % -703;

                letra8 = letra5  % 101;
                |SI letra8 = "0"; letra5 = letra5 % 202; |FINSI;

                letra8 = letra5  % 101;
                |SI letra8 = "0"; letra5 = letra5 % 201; |FINSI;

                letra8 = letra5  % 101;
                |SI letra8 = "0"; letra5 = ""; |FINSI;

                |SI letra5 ! "";
                    letra4 = letra5 + letra6 + letra7 + letra1;
                |SINO;
                    letra4 = letra6 + letra7 + letra1;
                |FINSI;

                #0Campo = letra4;
        |FINSI;
    |SINO;
        |MENAV "    ATENCION !!! No se calcular un C.I.F. ...";
    |FINSI;
    |PINTA #0Campo;
|FINSI;
|FCAL;
