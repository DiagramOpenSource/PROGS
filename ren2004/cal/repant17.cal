|FICHEROS;
     rem00101 #1;
     repant17 #917,MANTE;
     recofisc #10;
     rew00039 #939;
|FIN;

|VARIABLES;
     nCampo1   = 0;
     nTecla    = 0;

     aComa     = "";
     aAlfa     = "";
     aAlfa1    = "";
     aAlfa2    = "";
     aAlfa3    = "";

     &eaVarDni  = "";
     &enCalcCif = 0;
|FIN;

|PROCESO VeriFecha;
     |SI #917Campo = "01.01.0000";  |ACABA;  |FINSI;

     |SI #917Campo ] "01.01.2000";
         aAlfa = "     Fecha Incorrecta. Cambie los dos primeros digitos del A" + &209 + "O (20) por (19)";
         |MENAV aAlfa;
         |ERROR;
     |FINSI;
|FPRC;

|PROCESO MayusPant17;
     #917Campo = #917Campo > #917Campo;
     |PINTA #917Campo;
|FPRC;

|PROCESO Minusvalido;
     nCampo1 = Campo + 1;
     |C_M #917nCampo1, 1, "S";
     |SI #917Campo = 0;
         |C_M #917nCampo1, 1, "N";
         #917nCampo1 = "N";  |PINTA #917nCampo1;
     |FINSI;
|FPRC;

|PROCESO Convivencia;
     |C_M #917#60, 1, "S";
     |SI #917#6 ! "L"; |Y #917#6 ! "S";
         #917#60 = " ";  |PINTA #917#60;
         |C_M #917#60, 1, "N";
     |FINSI;
|FPRC;

|PROCESO HayConyuge;
     |PARA nCampo1 = 35;  |SI nCampo1 [ 52;  |HACIENDO nCampo1 = nCampo1 + 1;
           |C_M #917nCampo1, 1, "S";
     |SIGUE;
     |C_M #917#58, 1, "S";
     |C_M #917#39, 1, "N";
     |C_M #917#63, 1, "S";
     |C_M #917#64, 1, "S";
     |C_M #917#66, 1, "S";
     |C_M #917#70, 1, "S";

     |SI #917#6 ! "C";
         |PARA nCampo1 = 35;  |SI nCampo1 [ 52;  |HACIENDO nCampo1 = nCampo1 + 1;
               |C_M #917nCampo1, 1, "N";
         |SIGUE;
         |C_M #917#58, 1, "N";
         |C_M #917#63, 1, "N";
         |C_M #917#64, 1, "N";
         |C_M #917#66, 1, "N";
         |C_M #917#68, 1, "N";
         |C_M #917#70, 1, "N";

         |PDEFECTO #917, 35, 53;
         |PDEFECTO #917, 54, 55;
         |PDEFECTO #917, 63, 65;
         |PDEFECTO #917, 66, 67;
         |PDEFECTO #917, 68, 69;
         |PDEFECTO #917, 70, 71;
     |SINO;
         |SI #917#67 = "V";  #917#68 = "M";  |FINSI;
         |SI #917#67 = "M";  #917#68 = "V";  |FINSI;
     |FINSI;
|FPRC;

|PROCESO Extranjero;
     |C_M #917#61, 1, "S";
     |SI #917#10 = "S";
         |C_M #917#61, 1, "N";
         #917#61 = "N";
         |PINTA #917#61;
     |FINSI;
|FPRC;

|PROCESO Autonomia;
     nTecla = FSalida;

     |ABRE #10;
     #10#0 = #917#20;
     |LEE 040#10=;
     |SI FSalida ! 0;  |DDEFECTO #10;  |FINSI;

     |SI #917#59 = "S";
        #917#11 = "20";
     |SINO;
        #917#11 = #10#3;
     |FINSI;

     #10#0 = #917#50;
     |LEE 040#10=;
     |SI FSalida ! 0;  |DDEFECTO #10;  |FINSI;
     |CIERRA #10;

     |SI #917#59 = "S";
         #917#43 = "20";
     |SINO;
         #917#43 = #10#3;
     |FINSI;

     FSalida = nTecla;
|FPRC;

|PROCESO NombreEntero;
     nCampo1   = Campo - 3;  aAlfa1 = #917nCampo1;  |QBF aAlfa1;
     nCampo1   = Campo - 2;  aAlfa2 = #917nCampo1;  |QBF aAlfa2;
     nCampo1   = Campo - 1;  aAlfa3 = #917nCampo1;  |QBF aAlfa3;
     |SI aAlfa1 ! "";  |O aAlfa2 ! "";  |O aAlfa3 ! "";
         aComa = ",";
     |SINO;
         aComa = "";
     |FINSI;

     aAlfa     = aAlfa1 + " " + aAlfa2 + aComa + " " + aAlfa3;
     #917Campo = aAlfa;
     |PINTA #917Campo;
|FPRC;

|PROCESO Envio;  |TIPO 7;
     |SI #917#14 ! #917#24;  |O #917#15 ! #917#25;  |O #917#16 ! #917#26;
      |O #917#17 ! #917#27;  |O #917#18 ! #917#28;  |O #917#19 ! #917#29;
      |O #917#20 ! #917#30;  |O #917#21 ! #917#31;  |O #917#22 ! #917#32;
         |CONFI "0000NLos Datos de Envio son los mismos que los Datos Fiscales";
         |SI FSalida = 0;
             #917#24  = #917#14;   |PINTA #917#24;
             #917#25  = #917#15;   |PINTA #917#25;
             #917#26  = #917#16;   |PINTA #917#26;
             #917#27  = #917#17;   |PINTA #917#27;
             #917#28  = #917#18;   |PINTA #917#28;
             #917#29  = #917#19;   |PINTA #917#29;
             #917#30  = #917#20;   |PINTA #917#30;
             #917#31  = #917#21;   |PINTA #917#31;
             #917#32  = #917#22;   |PINTA #917#32;
         |FINSI;
     |FINSI;

     |SI #917#6 ! "C";  |ACABA;  |FINSI;

     |SI #917#14 ! #917#44;  |O #917#15 ! #917#45;  |O #917#16 ! #917#46;
      |O #917#17 ! #917#47;  |O #917#18 ! #917#48;  |O #917#19 ! #917#49;
      |O #917#20 ! #917#50;  |O #917#21 ! #917#51;  |O #917#22 ! #917#52;
         |CONFI "0000NLa Direccion del Conyuge es la misma que la del Contribuyente";
         |SI FSalida = 0;
             #917#44  = #917#14;   |PINTA #917#44;
             #917#45  = #917#15;   |PINTA #917#45;
             #917#46  = #917#16;   |PINTA #917#46;
             #917#47  = #917#17;   |PINTA #917#47;
             #917#48  = #917#18;   |PINTA #917#48;
             #917#49  = #917#19;   |PINTA #917#49;
             #917#50  = #917#20;   |PINTA #917#50;
             #917#51  = #917#21;   |PINTA #917#51;
             #917#52  = #917#22;   |PINTA #917#52;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO LetraNifPant17;  |TIPO 6;
     |SI FSalida = 10;
         eaVarDni  = #917Campo;
         enCalcCif = 1;
         |HAZ CalculoDNI;
         #917Campo = eaVarDni;
         |PINTA #917Campo;
         |ERROR;
         |ACABA;
     |FINSI;

     eaVarDni  = #917Campo;
     enCalcCif = 0;
     |HAZ CalculoDNI;
     eaVarDni = (eaVarDni + "            ") % 112;
     |SI #917Campo ! eaVarDni;
         |CONFI "0000SEl Nif puede ser Erroneo. Desea Calcularlo.";
         |SI FSalida = 0;
             #917Campo = eaVarDni;  |PINTA #917Campo;
             |ERROR;
             |ACABA;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO CtasCtes;
     |SI Campo = 54;  |Y #917#6 ! "C";  |ACABA;  |FINSI;

     |SI FSalida ! 0;  |ACABA;  |FINSI;

     |SI Campo = 53;
         |SI #917Campo = "                    ";
             #917Campo = #917#55;
         |FINSI;
     |FINSI;

     |SI Campo = 54;
         |SI #917Campo = "                    ";
             #917Campo = #917#55;
         |FINSI;
     |FINSI;

     #939#0 = #917Campo % 104;
     #939#1 = #917Campo % 504;
     #939#2 = #917Campo % 902;
     #939#3 = #917Campo % 1110;

     |PUSHV 0501 2380;
     |PINPA #0#939;

     |ATRI R;
     |SI Campo = 53;  |PINTA 1116, "            CUENTA CONTRIBUYENTE 1          ";  |FINSI;
     |SI Campo = 54;  |PINTA 1116, "            CUENTA CONTRIBUYENTE 2          ";  |FINSI;
     |SI Campo = 55;  |PINTA 1116, "               CUENTA CONJUNTA              ";  |FINSI;
     |ATRI r;

     |PINDA #0#939;
     |ENDATOS #1#939;
     |SI FSalida = 0;
         aAlfa     = #939#0 + #939#1 + #939#2 + #939#3;
         #917Campo = aAlfa;
     |FINSI;
     |POPV;
     |PINTA #917Campo;
|FPRC;


|RUTINA ClaveBaja1;
     #1#0 = #917#0;
     #1#1 = 1;
|FRUT;

|RUTINA ClaveAlta1;
     #1#0 = #917#0;
     #1#1 = 99;
|FRUT;

|PROCESO Tipo14Pant17; |TIPO 14;
     |ENTLINEAL #1#1, -2, 1, 18, 0, ClaveBaja1, ClaveAlta1;
|FPRC;
