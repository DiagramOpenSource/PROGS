|FICHEROS;
    ascamaut #0;
    asclient #1;    || clientes
    ascamcod #2;    || planilla cabs.
    ascamlod #3;    || planilla lins.
|FIN;

|VARIABLES;
    alfa1 = "";
    alfa2 = "";
    alfa3 = "";
    alfa4 = "";
    nume1 = 0;
    nume2 = 0;
    nume3 = 0;
    nume4 = 0;
    para1 = 0;
    para2 = 0;
    para3 = 0;
    para4 = 0;
    fech1 = @;
    fech2 = @;
    fech3 = @;
    fech4 = @;
    elsw1 = 0;
    elsw2 = 0;
    elsw3 = 0;
    elsw4 = 0;
    FEstado = 0;
    seleccio = 0;
    modo = 0;
    sw_rup = 0;

    nuevo = 0;
    linea = 0;
    ini = "                                        ";
    fin = "zzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzz";
|FIN;
____________________________________/ DESDE CAMPOS
|CALCULO mayus; |TIPO 0;
    #0Campo = #0Campo > #0Campo;
|PINTA #0Campo;
|FCAL;
____________________________________/ CALCULO PRINCIPAL
|CALCULO lee_clientes1;
    linea = linea + 1;

|SI linea [ 999;
        alfa1 = linea;  alfa1 = "000" + alfa1;   alfa1 = alfa1 % -103;
    |ATRI I;  |PINTA 1148, alfa1; |ATRI 0;

    |DDEFECTO #3;
        #3#0 = #0#4;         || codigo planilla
        #3#1 = linea;        || linea planilla
        #3#2 = #1#0;         || codigo cliente actual
        #3#3 = #1#2;         || nombre cliente
        #3#4 = nuevo;        || codigo nuevo
    |GRABA 020#3n;

    |SI FSalida = 0;
            nuevo = nuevo + 1;
    |FINSI;
|SINO;
    |MENAV "    ATENCION!!! Superado el limite de la planilla (999) ...";
    |ERROR10;
|FINSI;
|FCAL;

|DEFBUCLE lee_clave1;
 #1#1;
 ;
 #0#2;
 #0#3;
 e;
 NULL, lee_clientes1;
|FIN;

|DEFBUCLE lee_clave2;
 #1#2;
 0;
 ini, #0#2;
 fin, #0#3;
 e;
 NULL, lee_clientes1;
|FIN;

|DEFBUCLE lee_clave3;
 #1#3;
 0;
 ini, #0#2;
 fin, #0#3;
 e;
 NULL, lee_clientes1;
|FIN;

|CALCULO ini_planilla1;
    linea = #3#1;
|FCAL;

|CALCULO ini_planilla;
|DDEFECTO #2;
    #2#0 = #0#4;
|LEE 000#2=;
|SI FSalida ! 0;
    |GRABA 020#2n;
        linea = 0;
|SINO;
    |BUCLELIN 2ini_planilla1#2;
|FINSI;
|FCAL;

|CALCULO tipo3; |TIPO 3;
|SI #0#0 = "SI";
    |ABRE #2;
    |ABRE #3;
    |HAZ ini_planilla;
        nuevo = #0#1;
    |SI #0#5 = 1;   |BUCLE lee_clave1;  |FINSI;
    |SI #0#5 = 2;   |BUCLE lee_clave2;  |FINSI;
    |SI #0#5 = 3;   |BUCLE lee_clave3;  |FINSI;
    |CIERRA #2;
    |CIERRA #3;
|FINSI;
|FCAL;
