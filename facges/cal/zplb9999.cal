|FICHEROS;
     asnompat  #93;     || control directorios (facges)
     asempcon  #100;    ||Empresas Contables

     canempre  #200;    || empresas Contables
|FIN;

|VARIABLES;
     enlace_fich = "";
     &endigC     = 0;
     &eaCta      = "";
     &eswCta     = 0;
     aLong       = "";
     nLong       = 0;

     nNume1      = 0;
     nPos        = 0;
     aAlfa1      = "";
     aAlfa2      = "";
     aAlfa3      = "";
     aAlfa4      = "";

     aGuarda     = "";
     aPunto      = "";
     nCampo      = 0;
|FIN;

|PROCESO MFiltraPtos;
     ||ARA

     aGuarda = FSalida;
     |SI eaCta = "            "; |ACABA; |FINSI;

     |DBASS aAlfa1;
     enlace_fich = aAlfa1 + "agicont/fich/";
     |ABRE #93;
     |LEE 000#93p;                 || lee control directorios
     |SI FSalida = 0;
         enlace_fich = #93#2; |QBF enlace_fich;
     |FINSI;
     |CIERRA #93;

     |PATH_DAT #200 enlace_fich;       || empresas contables

     |ABRE #100;
     |DDEFECTO #100;
     |LEE 000#100=;
     |SI FSalida ! 0; |ACABA; |FINSI;
     |CIERRA #100;

     ||Ahora leo en la contagen
     |ABRE #200;
     |DDEFECTO #200;
     #200#2 = #100#1;
     #200#3 = #100#2;
     |LEE 000#200.=;
     |SI FSalida ! 0; |ACABA; |FINSI;
     nCampo = 13 + #200#13;
     endigC = #200#nCampo#;
     |CIERRA #200;

    aAlfa1 = "";
    |PARA nNume1 = 1; |SI nNume1 [ 12; |HACIENDO nNume1 = nNume1 + 1;
          aAlfa2 = nNume1; |QBF aAlfa2;
          aAlfa2 = aAlfa2 + "01";
          aAlfa3 = eaCta % aAlfa2;
          |SI aAlfa3 = "."; |O aAlfa3 = "0"; |O aAlfa3 = "1"; |O aAlfa3 = "2";
                            |O aAlfa3 = "3"; |O aAlfa3 = "4"; |O aAlfa3 = "5";
                            |O aAlfa3 = "6"; |O aAlfa3 = "7"; |O aAlfa3 = "8";
                            |O aAlfa3 = "9";
              aAlfa1 = aAlfa1 + aAlfa3;
          |FINSI;
    |SIGUE;
    eaCta = aAlfa1;

    aPunto = eaCta - ".";
    |SI FSalida ! 0;
        nNume1 = FSalida;
        nPos   = 100 + (((nNume1 / 100) ! 0) -1);
        aAlfa2 = eaCta % nPos;   |QBF aAlfa2;
        nPos   = (((nNume1 / 100) ! 0) * 100) + 199;
        aAlfa3 = eaCta % nPos;   |QBF aAlfa3;

        FSalida = nNume1;
        |PARA; |SI FSalida ! 0; |HACIENDO;
               aAlfa3 = aAlfa3 - "."; || limpiar mas puntos (los ignoramos)
        |SIGUE;

        nPos   = endigC - ( (A \ aAlfa2 % 0) + (A \ aAlfa3 % 0));
        aAlfa4 = (A\ "0" * nPos);
        eaCta  = (A\ aAlfa2 + aAlfa4 + aAlfa3);
    |FINSI;

    FSalida = aGuarda;
|FPRC;
