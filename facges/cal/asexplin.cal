|FICHEROS;
    asexpcab #0;
    asexplin #1;
    asconcep #2;
    asexpeca #3;
    asexpeli #4;
    asclient #5;
|FIN;

|VARIABLES;
    alfa1 = "";
    alfa2 = "";
    alfa3 = "";
    alfa4 = "";
    nume1 = 0;
    nume2 = 0;
    nume3 = 0;
    nume4 = 0;
    modo = 0;
    linea = 0;
    fecalf = "";
    campo0 = "";
    campo1 = "";
    campo2 = "";
    campo3 = "";
    campo4 = "";
    campo5 = "";
|FIN;

|CALCULO graba_exp; |TIPO 3;
    modo = (FEntrada / 100) ! 0;
|SI modo = 1;
        fecalf = "";
        campo0 = #1#0;  campo1 = #1#1;  campo2 = #1#2;
        campo3 = #1#3;  campo4 = #1#4;  campo5 = #1#5;
        nume1 = #1#1 $ 100;        || nro. de expediente
    |SI nume1 = 0;                 || si estamos en el expediente inicial
            fecalf = #1#3;
    |SINO;
            #1#1 = ((#1#1 / 100) ! 0) * 100;
        |LEE 001#1=;
        |SI FSalida ! 0;
            |MENAV "    ATENCION!!! No existe expediente inicial ...";
        |SINO;
                fecalf = #1#3;
        |FINSI;
    |FINSI;
        #1#0 = campo0;  #1#1 = campo1;  #1#2 = campo2;
        #1#3 = campo3;  #1#4 = campo4;  #1#5 = campo5;
    |SI fecalf ! "";
            alfa1 = fecalf % -102;      || saca a¤o
            nume1 = (#1#1 / 100) ! 0;   || saca expediente
        |HAZ grabalo;
    |SINO;
        |ERROR;
    |FINSI;
|FINSI;
|FCAL;

|CALCULO grabalo;
|HAZ concepto;
|ABRET #3;
|ABRET #4;
    linea = 0;
    #3#0  = alfa1;       || a¤o
    #3#14 = nume1;       || expediente
|LEE 001#3=;
|SI FSalida = 0;
    |BUCLELIN 2cuenta#3;
|SINO;
    |DDEFECTO #3;
        #3#00 = alfa1;   || a¤o
        #3#01 = #1#0;    || cliente
        #3#02 = #5#2;    || nombre cliente
        #3#03 = #1#3;    || fecha apertura
        #3#13 = #1#2;    || contrario
        #3#14 = nume1;   || expediente
    |GRABA 020#3n;
|FINSI;
|DDEFECTO #4;
    #4#00 = #3#0;        || a¤o
    #4#01 = #3#1;        || cliente
    #4#02 = linea + 1;   || linea
    alfa1 = #1#3;  alfa1 = alfa1 % 102;  nume1 = alfa1;
    alfa2 = #1#3;  alfa2 = alfa2 % 402;  nume2 = alfa2;
    #4#03 = (nume1 * 100) + nume2;      || fecha
    #4#04 = #2#0;        || seccion
    #4#05 = #2#1;        || concepto
    #4#06 = #2#2;        || descr. concepto
    #4#07 = #2#16;       || descr. concepto 1
    #4#08 = #2#17;       || descr. concepto 2
    #4#09 = #2#18;       || descr. concepto 3
    #4#16 = #3#14;       || contador
|GRABA 020#4n;
|CIERRAT #3;
|CIERRAT #4;
|FCAL;

|CALCULO cuenta;
    linea = #4#2;
|FCAL;

|CALCULO concepto;
|ABRE #2;
|DDEFECTO #2;
    #2#0 = "NOTA";
    #2#1 = "01";
|LEE 001#2=;
|SI FSalida ! 0;
        #2#2 = "<CONCEPTO INEXISTENTE>";
    |GRABA 020#2n;
|FINSI;
|CIERRA #2;
|FCAL;
