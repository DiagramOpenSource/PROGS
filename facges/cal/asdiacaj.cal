|FICHEROS;
   asdiacaj #0;

   asmovtos #1;
   asdiapan #2;
   asmancaj #3;
|FIN;

|VARIABLES;
   &SwIniCaja  = 0;
   &SwFinCaja  = 0;
   &SwIniFecha = 0;
   &SwFinFecha = 0;
   &SwLinea    = 0;
   &TotalFecha = 0;
   &TotalCaja  = 0;
   PuntLin     = 0;
   CajaAnt     = 0;
   Calc        = 0;
   PrimCaja    = 0;
   PrimFecha   = 0;

   Comodin     = "";
   Comodin1    = "";
   &FechAnt    = "";
   &CuentaMov  = "";
|FIN;

|PROCESO PintaDato;
   Calc = (PuntLin * 100) + 1;
   |C_P #asdiapan#0,1,Calc; |PINDA #0#asdiapan;
   PuntLin = PuntLin + 1;
   |SI PuntLin > 23;
      |PAUSA; |CLS; |PINPA #0#asdiapan;
      PuntLin = 7;
   |FINSI;
|FPRC;

|PROCESO Impresora;
   |SI CajaAnt ! #asmovtos#0;
      |SI PrimCaja = 0;
         SwFinCaja = 1; |PRINT; SwFinCaja = 0;
         TotalCaja = 0;
      |SINO;
         PrimCaja = 0;
      |FINSI;
      #asmancaj#0 = #asmovtos#0;
      |LEE 000#asmancaj.=;
      SwIniCaja = 1; |PRINT; SwIniCaja = 0;
      CajaAnt = #asmovtos#0;
   |FINSI;
   |SI FechAnt ! #asmovtos#1;
      |SI PrimFecha = 0;
         SwFinFecha = 1; |PRINT; SwFinFecha = 0;
         TotalFecha = 0;
      |SINO;
         PrimFecha = 0;
      |FINSI;
      SwIniFecha = 1; |PRINT; SwIniFecha = 0;
      FechAnt = #asmovtos#1;
   |FINSI;
   |SI #asmovtos#6 = "            ";
      CuentaMov  = #asmovtos#7 + "  H ";
      TotalCaja  = TotalCaja  - #asmovtos#8;
      TotalFecha = TotalFecha - #asmovtos#8;
   |SINO;
      CuentaMov  = #asmovtos#6 + "  D ";
      TotalCaja  = TotalCaja  + #asmovtos#8;
      TotalFecha = TotalFecha + #asmovtos#8;
   |FINSI;
   SwLinea = 1; |PRINT; SwLinea = 0;
|FPRC;

|PROCESO Pantalla;
   |SI CajaAnt ! #asmovtos#0;
      |SI PrimCaja = 0;
         #asdiapan#0 = "";      |HAZ PintaDato;
         Comodin = "             =======================================";
         #asdiapan#0 = Comodin; |HAZ PintaDato;
         Comodin = "             TOTAL CAJA .............. " + (("             " + TotalCaja) % -113);
         #asdiapan#0 = Comodin; |HAZ PintaDato;
         TotalCaja = 0;
      |SINO;
         PrimCaja = 0;
      |FINSI;
      |SI PuntLin > 15;
         |PARA; |SI PuntLin ! 7; |HACIENDO;
            #asdiapan#0 = ""; |HAZ PintaDato;
         |SIGUE;
      |FINSI;
      #asmancaj#0 = #asmovtos#0;
      |LEE 000#asmancaj.=;
      #asdiapan#0 = "";      |HAZ PintaDato;
      Comodin = ("00" + #asmovtos#0) % -102;
      Comodin1 = #asmancaj#1; |QBF Comodin1;
      Comodin = " CAJA " + Comodin + " (" + Comodin1 + ")";
      #asdiapan#0 = Comodin; |HAZ PintaDato;
      |QBF Comodin; Comodin = Comodin % 0; Calc = Comodin; Calc = Calc - 1;
      Comodin = "=" * Calc;
      #asdiapan#0 = " " + Comodin; |HAZ PintaDato;
      #asdiapan#0 = "";      |HAZ PintaDato;
      CajaAnt = #asmovtos#0;
   |FINSI;
   |SI FechAnt ! #asmovtos#1;
      |SI PrimFecha = 0;
         #asdiapan#0 = "";      |HAZ PintaDato;
         Comodin = "             =======================================";
         #asdiapan#0 = Comodin; |HAZ PintaDato;
         Comodin = "             Total fecha " + FechAnt + " .. " + (("             " + TotalFecha) % -113);
         #asdiapan#0 = Comodin; |HAZ PintaDato;
         TotalFecha = 0;
      |SINO;
         PrimFecha = 0;
      |FINSI;
      |SI PuntLin > 17;
         |PARA; |SI PuntLin ! 7; |HACIENDO;
            #asdiapan#0 = ""; |HAZ PintaDato;
         |SIGUE;
      |FINSI;
      #asdiapan#0 = "";      |HAZ PintaDato;
      Comodin = " Fecha : " + #asmovtos#1;
      #asdiapan#0 = Comodin; |HAZ PintaDato;
      #asdiapan#0 = "";      |HAZ PintaDato;
      Comodin = "      Linea Tipo Secc Cp Cuenta Cont. Sg   Importe   Cliente  Expediente ";
      #asdiapan#0 = Comodin; |HAZ PintaDato;
      Comodin = "-" * 66;
      #asdiapan#0 = "      " + Comodin; |HAZ PintaDato;
      FechAnt = #asmovtos#1;
  |FINSI;
   Comodin = ("0000" + #asmovtos#2) % -104;
   Comodin1 = ("00" + #asmovtos#3) % -102;
   Comodin = "       " + Comodin + "  " + Comodin1 + "  " + #asmovtos#4;
   Comodin = Comodin + " " + #asmovtos#5 + " ";
   |SI #asmovtos#6 = "            ";
      Comodin = Comodin + #asmovtos#7 + "  H ";
      TotalCaja  = TotalCaja  + #asmovtos#8;
      TotalFecha = TotalFecha + #asmovtos#8;
   |SINO;
      Comodin = Comodin + #asmovtos#6 + "  D ";
      TotalCaja  = TotalCaja  - #asmovtos#8;
      TotalFecha = TotalFecha - #asmovtos#8;
   |FINSI;
   Comodin1 = ("           " + #asmovtos#8) % -111;
   Comodin = Comodin + Comodin1 + "  ";
   Comodin1 = ("00000" + #asmovtos#9) % -105;
   Comodin = Comodin + Comodin1 + "   ";
   Comodin1 = (("00" + #asmovtos#10) % -102) + "/" + (("000000" + #asmovtos#11) % -106);
   Comodin = Comodin + Comodin1;
   #asdiapan#0 = Comodin; |HAZ PintaDato;
|FPRC;

|PROCESO Diario;
   |SI #asdiacaj#4 = "S";
      |HAZ Pantalla;
   |SINO;
      |HAZ Impresora;
   |FINSI;
|FPRC;

|DEFBUCLE Diario;
#asmovtos#1;
;
#asdiacaj#0,#asdiacaj#2,0001;
#asdiacaj#1,#asdiacaj#3,9999;
;
NULL,Diario;
|FIN;

|PROGRAMA;
   |CLS;
   |CABEZA "Diario de caja";
   |PINPA #0#0; |PINDA #0#0;
   |ENDATOS #1#0;
   |SI FSalida = 0;
      |SI #asdiacaj#4 = "N";
         |IMPRE 0;
         |SI FSalida ! 0; |MENAV "    Proceso abortado "; |ACABA; |FINSI;
         |INFOR "asdiacaj";
         |SI FSalida ! 0; |MENAV "    Proceso abortado "; |ACABA; |FINSI;
      |FINSI;
      |ABRE #asmancaj;
      PuntLin = 7; PrimCaja = 1; PrimFecha = 1;
      TotalCaja = 0; TotalFecha = 0;
      |CLS; |PINPA #0#asdiapan;
      |BUCLE Diario;
      |SI TotalFecha ! 0;
         |SI #asdiacaj#4 = "S";
            #asdiapan#0 = "";      |HAZ PintaDato;
            Comodin = "             =======================================";
            #asdiapan#0 = Comodin; |HAZ PintaDato;
            Comodin = "             Total fecha " + FechAnt + " .. " + (("             " + TotalFecha) % -113);
            TotalFecha = 0;
            #asdiapan#0 = Comodin; |HAZ PintaDato;
         |SINO;
            SwFinFecha = 1; |PRINT; SwFinFecha = 0;
         |FINSI;
      |FINSI;
      |SI TotalCaja ! 0;
         |SI #asdiacaj#4 = "S";
            #asdiapan#0 = "";      |HAZ PintaDato;
            Comodin = "             =======================================";
            #asdiapan#0 = Comodin; |HAZ PintaDato;
            Comodin = "             TOTAL CAJA .............. " + (("             " + TotalCaja) % -113);
            #asdiapan#0 = Comodin; |HAZ PintaDato;
         |SINO;
            SwFinCaja = 1; |PRINT; SwFinCaja = 0;
         |FINSI;
      |FINSI;
      |CIERRA #asmancaj;

      |SI PuntLin > 7; |PAUSA; |FINSI;

      |SI #asdiacaj#4 = "N";
         |PIEINF;
         |FININF;
         |FINIMP;
      |FINSI;
   |FINSI;
|FPRO;
