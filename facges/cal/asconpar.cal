|FICHEROS;
     asconpar #0;
     canempre #1;
     asparcia #2;
     ashisrec #3;
     catradia #4;
     camaasic #5;
     camaasil #6;
     asnompat #7;
     caconcep #8;
     caconasi #9;
     ascontmp #10;
     catradia #11;
     cacuenta #12;
     cacuebal #13;
     asclient #14;
     camandia #15;
     ashismic #90;
|FIN;

|VARIABLES;
    guarda  = 0;
    &modcon = 0;
    &codcon = 0;
    opera = "A";
    &path = "";
    &path1 = "";
    &dig1 = 0;
    &dig2 = 0;
    &dig3 = 0;
    &dig4 = 0;
    &dig5 = 0;
    &dig6 = 0;
    &dig7 = 0;
    &dig8 = 0;
    &dig9 = 0;
    importe = 0;
    empresa = "";
    anyo = "";
    cuenta = "";         ||Cuenta Contable
    puente = "";         ||Digito de la Cuenta
    digito = 0;
    digito1 = 0;
    digito2 = 0;
    nivel = 0;
    long = "";
    i = 0;
    fecha = @;
    pongo = 0;
    cojo = 0;
    cojo1 = "";
    alfa = "";
    alfa1 = "";
    sw = 0;
    lin = 0;
    nopasa   = "";
    prim_lin = "S";
    cuenta1  = "";
    imp = 0;
    imp_d = 0;
    imp_h = 0;
    signo = "";
    desc = "";
    desc1 = "";
    d_bueno = 0;
    c_buena = "";
    ult_fec = @;
    conc = 0;
    linea = 0;
    ini = 0;
    fecalf = "";
    mes = "";
    mesfec = 0;
    mesac = 0;
    a = 0;
    b = 0;
    c = 0;
    uno = "";
    dos = "";
    mensa2 = "    No Esta definida Esta cuenta en Cuentas de Balance";
    mascara = "";

    nURemesa = 0;
    nDRemesa = 0;
    nHRemesa = 0;
|FIN;

|PROCESO Mayus;
     #0Campo = #0Campo > #0Campo;
     |PINTA #0Campo;

     |SI Campo = 17;
         |SI #0#17 = "M";
             |C_M #0#21, 1, "S";
         |SINO;
             |C_M #0#21, 1, "N";
             #0#21 = 0;  |PINTA #0#21;
         |FINSI;
    |FINSI;
|FPRC;

|PROCESO Empresa;          ||Busco las empresas
     |ABRE #7;
     |DDEFECTO #7;
     |LEE 001#7p;
     |SI FSalida = 0;
         path = #7#2; |QBF path;
     |SINO;
         |MENAV "0000No Existe el fichero Control Directorios";
         |CIERRA #7;
         |ERROR;
         |ACABA;
     |FINSI;
     |CIERRA #7;

     empresa = #0#14;
     empresa = ("00000" + empresa) % -105;

     anyo = #0#15;
     anyo = "0" + anyo;
     anyo = anyo % -101;
     empresa = empresa + anyo;
     |PATH_DAT #1 path;
     |ABRE #1;
     #1#2 = #0#14;
     #1#3 = #0#15;
     |LEE 040#1=;
     |SI FSalida ! 0;
         |MENAV "      No existe Empresa Contable";
         |CIERRA #1;
         |ERROR;
         |ACABA;
     |SINO;
         long = path % 0;
         digito = long - 5;
         long = digito; long = "1" + long;
         path1 = (path % long) + "pan/";
         path = path + empresa +  "/";
         |PATH_DAT #5 path;
         |PATH_DAT #6 path;
         |PATH_DAT #9 path;
         |PATH_DAT #12 path;
         |PATH_DAT #13 path;
         |PATH_DAT #15 path;
         |PATH_PAN #12 path1;
         |PATH_DAT #8 path;
         |PATH_PAN #8 path1;

         dig1 = #1#11;    ||Desde mes
         dig3 = #1#13;    ||Num. niveles
         dig4 = #1#14;    ||digito 1
         dig5 = #1#15;    ||Digito 2
         dig6 = #1#16;    ||3
         dig7 = #1#17;    ||4
         dig8 = #1#18;    ||5
         dig9 = #1#19;    ||6
     |FINSI;
|FPRC;

|PROCESO CuenCli;
     |SI #0#13 = "CLIE        ";
         digito = 999;
         |ACABA;
     |FINSI;

     cuenta = "";
     puente = #0#13;
     |QBF puente;
     long = puente % 0;
     digito = long;
     |SI digito = 0;
         |MENAV "0000*** Introduzca Cuenta ***";
         |ERROR;
         |ACABA;
     |FINSI;
     |PARA i = 1; |SI i [ digito; |HACIENDO i = i + 1;
           long = i + "01";
           long = puente % long;
           |SI long = "*";
               digito = 999;
               |SAL;
           |FINSI;
           cuenta = cuenta + long;
     |SIGUE;

     |SI digito = 999;   || Tiene que a¤adir el cliente
         |CAMPO_MODIFICA #0#18 , 1 , "S";
         |ENCAMPO #18#0;
         |CAMPO_MODIFICA #0#18 , 1 , "N";
         c_buena = cuenta;
         d_bueno = #0#18;
     |SINO;
         cuenta = #0#13; |QBF cuenta;
         digito1 = 0;
         |SI digito = dig4; digito1 = 1; |FINSI;
         |SI digito = dig5; digito1 = 2; |FINSI;
         |SI digito = dig6; digito1 = 3; |FINSI;
         |SI digito = dig7; digito1 = 4; |FINSI;
         |SI digito = dig8; digito1 = 5; |FINSI;
         |SI digito = dig9; digito1 = 6; |FINSI;
         |SI digito1 = 0;
             |MENAV "0000Longitud fuera de nivel";
             |ERROR;
             |ACABA;
         |FINSI;
         #0#18 = digito1;
         |PINTA #0#18;
     |FINSI;
|FPRC;

|PROCESO FechaFac;
     |SI #0#4 = "N";
         |CAMPO_MODIFICA #0#5 , 1 , "S";
     |SINO;
         |CAMPO_MODIFICA #0#5 , 1 , "N";
     |FINSI;
|FPRC;

|PROCESO Contador;
     |ABRE #9;
     |LEE 101#9p;
     |SI FSalida = 0;
          #9#1 = #9#1 + 1;
          |GRABA 021#9a;
     |SINO;
          #9#1 = 1;
          |GRABA 021#9n;
     |FINSI;
     |LIBERA #9;
     |CIERRA #9;
|FPRC;

|PROCESO Miracont;
     FSalida = 0;
     |PARA; |SI FSalida = 0; |HACIENDO;
          |HAZ Contador;
          #5#0 = #0#16;  ||Diario
          #5#1 = fecha;  ||Fecha
          #5#2 = #9#1;   ||Documento
          |LEE 001#5=;
     |SIGUE;
     |DDEFECTO #5;
     #5#0 = #0#16; #5#1 = fecha; #5#2 = #9#1; #5#3 = #5#2;
     |GRABA 021#5b;
|FPRC;

|PROCESO GrabaTmp;
     |DDEFECTO #10;
     |SI #0#4 = "S";
        #10#0 = #2#3;
     |SINO;
        #10#0 = #0#5;
     |FINSI;
     #10#1 = #6#4;
     #10#2 = #6#5;
     #10#3 = signo;
     |LEE 101#10=;
     |SI FSalida ! 0;
          |GRABA 021#10b;
     |FINSI;
     #10#4 = #10#4 + imp;
     #10#5 = conc;
     #10#6 = desc1;
     #10#7 = #2#14;
     |GRABA 021#10a;
     |LIBERA #10;
|FPRC;

|PROCESO AltaTrabajo;
     modcon = 1;
     |ABRE #4;
     #4#0 = codcon;
     #4#1 = #6#4;     || cuenta
     #4#2 = #6#5;     || digito
     #4#3 = fecha;    || fecha
     #4#4 = signo;    || signo
     #4#6 = #0#16;    || diario
     |LEE 101#4=;
     |SI FSalida ! 0;
         |DDEFECTO #4;
         #4#0 = codcon;
         #4#1 = #6#4;|| cuenta
         #4#2 = #6#5;|| digito
         #4#3 = fecha;|| fecha
         #4#4 = signo;|| signo
         #4#6 = #0#16;|| diario
         |GRABA 020#4b;
    |FINSI;

    importe = imp;       || importe
    |SI opera = "B";
        importe = -importe;
    |FINSI;
    #4#5 = #4#5 + importe;
    |GRABA 020#4a;
    |LIBERA #4;
    |CIERRA #4;
|FPRC;


|PROCESO Saca;
     #12#4 = #13#1;
     #12#5 = #13#2;
     #12#6 = #13#3;
     #12#7 = #13#4;
     #12#8 = #13#5;

     nivel   = #12#1;
     mascara = #12#0;  |QBF mascara;
     mascara = mascara +"zzzzzzzzzzzz";
     mascara = mascara % 112;
     #12#54  = mascara;
     #12#55  = nivel;
     |CIERRA #13;
|FPRC;

|PROCESO Defecto;
     |ABRE #13;
     uno   = #6#4;  |QBF uno;
     dos   = uno + "000000000000";
     uno   = dos % A101;
     #13#0 = uno;
     |LEE 040#13=;
     |SI FSalida = 0;
         |HAZ Saca;
     |FINSI;

     uno   = dos % A102;
     #13#0 = uno;
     |LEE 040#13=;
     |SI FSalida = 0;
         |HAZ Saca;
     |FINSI;

     uno = dos % A103;
     #13#0 = uno;
     |LEE 040#13=;
     |SI FSalida = 0;
         |HAZ Saca;
     |FINSI;

     uno = dos % A104;
     #13#0 = uno;
     |LEE 040#13=;
     |SI FSalida = 0;
         |HAZ Saca;
     |SINO;
         |MENAV  "    No Esta definida Esta cuenta en Cuentas de Balance";
     |FINSI;

     |CIERRA #13;
|FPRC;

|PROCESO Actualiza2;                || rutina 'a_cuentas'
     ini    = dig1;
     fecalf = fecha;
     mes    = fecalf % 402;
     mesfec = mes;
     |SI ini = 1;
         mesac = mesfec;
     |SINO;
         mesac = (mesfec - ini) + 1;
         |SI mesac [ 0;
             mesac = (13 - ini) + mesfec;
         |FINSI;
     |FINSI;
     a = (mesac * 3) + 9;
     b = a + 1;
     c = a + 2;
     |SI signo = "D";
         #12a = #12a +. #6#9;
         #12#51 = #12#51 +. #6#9;
     |SINO;
         #12b = #12b +. #6#9;
         #12#52 = #12#52 +. #6#9;
     |FINSI;
     #12c = #12a - #12b;
     #12#53 = #12#51 - #12#52;
|FPRC;

|PROCESO Actualiza1;
     |DDEFECTO #12;
     #12#0 = #6#4;
     #12#1 = #6#5;           || actualizacion cuentas
     |LEE 101#12=;
     |SI FSalida ! 0;
         |HAZ Defecto;
         |SI lin = 2;
             |ABRE #14;
             #14#0 = #2#9;
             |LEE  001#14=; |SI FSalida ! 0; |DDEFECTO #14; |FINSI;
             |CIERRA #14;
             #12#2 = #14#1;
         |FINSI;
         |PUSHV 0501 2380;
         |PINPA #0#12;
         |PINDA #0#12;
         FSalida = 9;
         |PARA; |SI FSalida ! 0; |HACIENDO ;
              |ENDATOS #1#12;
         |SIGUE;
         |GRABA 021#12b;
         |POPV;
     |FINSI;

     |HAZ Actualiza2;           || saldos del mes correspondiente

     |SI #12#58 < fecha;
         #12#58 = fecha;               || fecha ultimo apunte
     |FINSI;
     |GRABA 020#12a;
     |LIBERA #12;
|FPRC;


|PROCESO GrabaLin;
     |DDEFECTO #6;
     #6#0 = #5#0;
     #6#1 = #5#1;
     #6#2 = #5#2;
     #6#3 = lin;
     #6#8 = signo;
     #6#9 = imp;
     |SI signo = "D";
          #6#6   = #0#6;   ||Concepto Cargos
          desc   = #0#7;   |QBF desc;
          conc   = #0#6;
          puente = #0#8;
     |SINO;
          #6#6   = #0#9;   ||Concepto Abonos
          desc   = #0#10;   |QBF desc;
          conc   = #0#9;
          puente = #0#11;
     |FINSI;

     desc1 = desc;
     |SI puente = "N";
         desc1 = desc + " " + (#2#0 + "/" + (("00000" + #2#1) % -105));
         desc1 = desc1 % 130;
     |FINSI;

     |SI puente = "F";
         desc1 = (desc + " " + #2#3) % 130;
     |FINSI;

     |SI puente = "M";
         desc1 = (desc + " " + #2#14) % 130;
     |FINSI;

     #6#7 = desc1;
     |SI lin = 2;        ||Es debe;      es la cuenta del cliente
          #6#4 = cuenta1;
          #6#5 = digito1;
          |SI signo = "D";
               #6#10 = "";
               #6#11 = 0;
               #6#16 = cuenta1;
               #6#17 = digito1;
               imp_d = imp_d + imp;
          |SINO;
               #6#10 = cuenta1;
               #6#11 = digito1;
               #6#16 = "";
               #6#17 = 0;
               imp_h = imp_h + imp;
          |FINSI;

          |SI #0#17 ! "R";
               |HAZ GrabaTmp;
          |SINO;
               |GRABA 021#6n;
               |LIBERA #6;
          |FINSI;

          |HAZ AltaTrabajo;

          |SI prim_lin = "S";
              #5#10 = cuenta1;
              #5#11 = digito1;
              prim_lin = "N";
          |FINSI;
          |HAZ Actualiza1;
     |SINO;              ||Es haber
          #6#4 = #2#13;
          #6#5 = digito2;

          |SI signo = "H";
              #6#10 = #2#13;      #6#11 = digito2;
              #6#16 = "";         #6#17 = 0;
              imp_h = imp_h + imp;
          |SINO;
              #6#10 = "";            #6#11 = 0;
              #6#16 = #2#13;         #6#17 = digito2;
              imp_d = imp_d + imp;
          |FINSI;

          |SI #0#17 ! "R";
              |HAZ GrabaTmp;
          |SINO;
              |GRABA 021#6n;
              |LIBERA #6;
          |FINSI;

         |HAZ AltaTrabajo;

          |SI prim_lin = "S";
              #5#8 = #2#13;
              #5#9 = digito2;
          |FINSI;

          |HAZ Actualiza1;
     |FINSI;
|FPRC;

|PROCESO PonCuentas;          ||Viene del hacer
     |SI #0#13 = "CLIE        ";
         |ABRE #14;
         #14#0 = #2#9;
         |LEE  000#14=;
         |SI FSalida ! 0; |DDEFECTO #14; |FINSI;
         |CIERRA #14;

         puente  =  #14#52;  |QBF puente;
         long    = puente % 0;
         digito  = long;

         digito1 = 0;
         |SI digito = dig4; digito1 = 1; |FINSI;
         |SI digito = dig5; digito1 = 2; |FINSI;
         |SI digito = dig6; digito1 = 3; |FINSI;
         |SI digito = dig7; digito1 = 4; |FINSI;
         |SI digito = dig8; digito1 = 5; |FINSI;
         |SI digito = dig9; digito1 = 6; |FINSI;

         cuenta1 = #14#52;
         digito  = 999;
         |ACABA;
     |FINSI;

     cuenta1 = c_buena;
     long    = cuenta1 % 0;
     |SI d_bueno = 1; digito2 = dig4; digito1 = 1; |FINSI;
     |SI d_bueno = 2; digito2 = dig5; digito1 = 2; |FINSI;
     |SI d_bueno = 3; digito2 = dig6; digito1 = 3; |FINSI;
     |SI d_bueno = 4; digito2 = dig7; digito1 = 4; |FINSI;
     |SI d_bueno = 5; digito2 = dig8; digito1 = 5; |FINSI;
     |SI d_bueno = 6; digito2 = dig9; digito1 = 6; |FINSI;
     pongo = digito2 - long;
     alfa = #2#9;
     alfa = "000000000000" + alfa;
     cojo = (100 + pongo) * -1;
     cojo1 = cojo; ||nuevo
     alfa1 = alfa % cojo1;
     |QBT alfa1;
     |SI alfa1 = "";
          alfa = #2#9;
          |QBT alfa;
          alfa = "0000000000000000" + alfa;
          alfa1 = alfa % cojo1;
     |FINSI;
     cuenta1 = cuenta1 + alfa1;
|FPRC;

|PROCESO GrabaCabe;      ||Grabo la cabecera del apunte
     #5#4 = imp_d;
     #5#5 = imp_h;
     #5#6 = imp_d - imp_h;
     |GRABA 021#5a;
     |LIBERA #5;
     #15#2 = #15#2 + #5#4;
     #15#3 = #15#3 + #5#5;
     imp_d = 0;
     imp_h = 0;
|FPRC;

|PROCESO ActuaHist;
     |ABRE #3;
     #3#0 = #2#0;
     #3#1 = #2#1;
     |LEE 101#3=;
     |SI FSalida = 0;
         #3#12 = "M";
         #3#13 = #3#13 + #2#8;
         |GRABA 021#3a;
         |LIBERA #3;

         |ABRE #90;
         #90#0 = #3#0;
         #90#1 = #3#1;
         |LIBERA #90;
         |LEE 140#90=;
         |SI FSalida = 0;
             #90#33 = #3#12;
             |GRABA 021#90a;
         |FINSI;
         |CIERRA #90;
     |FINSI;
     #2#10 = "S";
     #2#11 = ~;
     |GRABA 021#2a;
     |LIBERA #2;
     |CIERRA #3;
|FPRC;

|PROCESO NoPasa;
     cuenta = #2#13;  |QBF cuenta;

     long = cuenta % 0;
     digito2 = long;
     |SI digito2 > 0;
         |SI long = dig4; digito2 = 1; |FINSI;
         |SI long = dig5; digito2 = 2; |FINSI;
         |SI long = dig6; digito2 = 3; |FINSI;
         |SI long = dig7; digito2 = 4; |FINSI;
         |SI long = dig8; digito2 = 5; |FINSI;
         |SI long = dig9; digito2 = 6; |FINSI;
     |SINO;
         nopasa = "N";
     |FINSI;
|FPRC;

|PROCESO Hacer;          ||Proceso de Contabilizacion
     |SI #0#4 = "S";
         fecha = #2#3;
     |SINO;
         fecha = #0#5;
     |FINSI;

     nopasa = "S";
     |HAZ NoPasa;
     |SI nopasa = "N";  |ACABA;  |FINSI;

     |SI #0#17 = "R";
         |HAZ Miracont;
     |FINSI;

     ||Primero pasaremos la cuenta del debe

     imp = #2#8;
     |SI #2#8 < 0; |Y #0#12 = "S";
          signo = "H";        ||D
          imp = -#2#8;
     |SINO;
          signo = "D";        ||H
     |FINSI;
     lin = 1;            ||Grabo la linea del haber
     |HAZ GrabaLin;

     |SI #2#8 < 0; |Y #0#12 = "S";
         signo = "D";        ||H
         imp   = -#2#8;
     |SINO;
         signo = "H";        ||D
     |FINSI;

     lin = 2;            ||Grabo la linea del debe
     |SI digito = 999;
         |HAZ PonCuentas;
     |FINSI;

     |HAZ GrabaLin;

     |SI #0#17 = "R";
         |HAZ GrabaCabe;
     |FINSI;

     |HAZ ActuaHist;
|FPRC;

|DEFBUCLE asparciaFecha;
     #2#2;
     ;
     #0#0, #0#2, #0#19, "            ", "N", nDRemesa;
     #0#1, #0#3, #0#20, "zzzzzzzzzzzz", "N", nHRemesa;
     be;
     NULL , NULL , NULL , NULL , NULL , Hacer;
     #2#3;
|FIN;

|DEFBUCLE asparciaRemesa;
     #2#2;
     ;
     #0#0, #0#2, #0#19, "            ", "N", nDRemesa;
     #0#1, #0#3, #0#20, "zzzzzzzzzzzz", "N", nHRemesa;
     be;
     NULL , NULL , NULL , NULL , NULL , Hacer;
     #2#14;
|FIN;

|DEFBUCLE asparciaNormal;
     #2#2;
     ;
     #0#0 , #0#2 , #0#19 , "            " , "N", nDRemesa;
     #0#1 , #0#3 , #0#20 , "zzzzzzzzzzzz" , "N", nHRemesa;
     be;
     NULL, Hacer;
|FIN;

|PROCESO FinFecha1;
     linea = linea + 1;

     |SI #0#17 = "F";
         |SI sw = 0;
             fecha = #10#0;
             |HAZ Miracont;
             ult_fec = #10#0;
             sw = 1;
             imp_d = 0;
             imp_h = 0;
         |FINSI;

         |SI ult_fec ! #10#0;
             |GRABA 021#5a;
             |LIBERA #5;
             fecha = #10#0;
             |HAZ Miracont;
             linea = 1;
         |FINSI;
     |SINO;
         |SI sw = 0;
             fecha = #10#0;
             |HAZ Miracont;
             nURemesa = #10#7;
             sw = 1;
             imp_d = 0;
             imp_h = 0;
         |FINSI;

         |SI nURemesa ! #10#7;
             |GRABA 021#5a;
             |LIBERA #5;
             fecha  = #10#0;
             |HAZ Miracont;
             linea = 1;
         |FINSI;
     |FINSI;

     |DDEFECTO #6;
     #6#0 = #5#0;
     #6#1 = #5#1;
     #6#2 = #5#2;
     #6#3 = linea;
     #6#4 = #10#1;
     #6#5 = #10#2;
     #6#6 = #10#5;
     #6#7 = #10#6;
     #6#8 = #10#3;
     #6#9 = #10#4;
     |SI #10#3 = "D";
         |SI imp_d = 0;
             #5#10 = #6#4;
             #5#11 = #6#5;
         |FINSI;
         imp_d = imp_d + #10#4;
         #5#4  = #5#4 + #10#4;
         #5#6  = #5#6 + #10#4;
         #15#2 = #15#2 + #10#4;
         #6#10 = "";
         #6#11 = 0;
         #6#16 = #6#4;
         #6#17 = #6#5;
     |SINO;
         |SI imp_h = 0; #5#8 = #6#4; #5#9 = #6#5; |FINSI;
         imp_h = imp_h + #10#4;
         #5#5 = #5#5 + #10#4;
         #5#6 = #5#6 - #10#4;
         #15#3 = #15#3 + #10#4;
         #6#10 = #6#4
         #6#11 = #6#5;
         #6#16 = "";
         #6#17 = 0;
     |FINSI;
     |GRABA 021#6n;
     |LIBERA #6;

     ult_fec  = #10#0;
     nURemesa = #10#7;
|FPRC;

|PROCESO FinFecha2;
     |GRABA 021#5a;
     |LIBERA #5;
|FPRC;

|DEFBUCLE ascontmpFecha;
     #10#1;
     ;
     "00.00.0000" , "            " , 0 , " ";
     "31.12.9999" , "zzzzzzzzzzzz" , 9 , "z";
     e;
     NULL , FinFecha1, NULL , NULL , FinFecha2;
|FIN;

|DEFBUCLE ascontmpRemesa;
     #10#2;
     ;
     nDRemesa, "            " , 0 , " ";
     nHRemesa, "zzzzzzzzzzzz" , 9 , "z";
     e;
     NULL , FinFecha1, NULL , NULL , FinFecha2;
|FIN;

|PROCESO Termino;
     |SI modcon = 0;
         |ABRE #4;
         |DDEFECTO #4;
         #4#0 = codcon;
         |LEE 040#4=;
         |BORRA 101#4a;
         |CIERRA #4;
     |SINO;
         |CORRE "camaapua"
     |FINSI;
|FPRC;

|PROCESO Inicial;
     |ABRE #4;
     |LEE 101#4u;
     |SI FSalida ! 0;
         |DDEFECTO #4;
         #4#0 = 1;
     |SINO;
         #4#0 = #4#0 + 1;
     |FINSI;
     |GRABA 101#4n;
     codcon = #4#0;
     |LIBERA #4;
     |CIERRA #4;
|FPRC;

|PROCESO Tipo3;
     |HAZ Inicial;

     |SI #0#21 = 0;
         nDRemesa = 0;
         nHRemesa = 999999;
     |SINO;
         nDRemesa = #0#21;
         nHRemesa = #0#21;
     |FINSI;

     |ABRE #5;
     |ABRE #6;
     |ABRE #12;
     |ABRE #15;
     |DDEFECTO #15;
     #15#0 = #0#16;
     |LEE 101#15=;
     |SI FSalida ! 0; #15#1 = "INEXISTENTE"; |GRABA 021#15b; |FINSI;

     |SI #0#17 = "F";
         |ABRE #10;  |CIERRA #10;  |DELINDEX #10;

         |ABRE #10;
         |BUCLE asparciaFecha;
         |CIERRA #10;

         |BUCLE ascontmpFecha;
     |FINSI;

     |SI #0#17 = "M";
         |ABRE #10;  |CIERRA #10;  |DELINDEX #10;

         |ABRE #10;
         |BUCLE asparciaRemesa;
         |CIERRA #10;

         |BUCLE ascontmpRemesa;
     |FINSI;

     |SI #0#17 = "R";
         |BUCLE asparciaNormal;
     |FINSI;

     |CIERRA #5;
     |CIERRA #6;
     |CIERRA #12;
     |GRABA 021#15a;
     |LIBERA #15;
     |CIERRA #15;

     |HAZ Termino;
|FPRC;

||--------------------------------------------------------------------------
|PROGRAMA;
     |CLS;
     |CABEZA "PASE COBROS";
     |ABRE #0;
     |DDEFECTO #0;
     |LEE 000#0];
     |PINPA #0#0;
     |ENCAMPO #14#0;
     |SI FSalida = 0;
          |LEE 141#0=;
          |SI FSalida = 0;
               |PINDA #0#0;
               |ENDATOS #2#0;
               |SI FSalida = 0; |GRABA 121#0a; |LIBERA #0; |HAZ Tipo3; |FINSI;
          |SINO;
               guarda = #0#14;
               |DDEFECTO #0;
               #0#14 = guarda;
               |PINDA #0#0;
               |ENDATOS #1#0;
               |SI FSalida = 0; |GRABA 121#0n; |LIBERA #0; |HAZ Tipo3; |FINSI;
          |FINSI;
     |FINSI;
     |CIERRA #0;
|FPRO;
