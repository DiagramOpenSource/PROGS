|FICHEROS;
  ascopdis #0;
|FIN;

|VARIABLES;
     aPath     = "";
     aCopia    = "";
     aFichero  = "";
     aTecla    = "";
     aOrigen   = "";
     aDestino  = "";
     aAlfa     = "";
     aIPRemoto = "";
     aAlfa1    = "";
     aAlfa2    = "";
     aAlfa3    = "";
     nInkey    = 0;
     Existe    = 0;
     nCanal    = 0;
     aNombre   = "";

     &eaFichero = "";
     &eaNombre1 = "";
     &SwXml     = 0;
|FIN;

|CALCULO PonBarra; |TIPO 0;
     |C_M #0Campo, 0, aAlfa1;
     |SI aAlfa1 = "N"; |ACABA; |FINSI;

     nInkey = FSalida;
     |SI nInkey = 10;
         aAlfa1 = "D c:\";
         |BROWSE aAlfa1;
         aAlfa1 = aAlfa1 % 280;
         #0Campo = aAlfa1;
         |PINTA #0Campo
    |FINSI;

    aAlfa1 = #0Campo; |QBF aAlfa1;
    |SI aAlfa1 = ""; |ERROR; |ACABA; |FINSI;

    aAlfa2 = aAlfa1 % -101;
    |SI aAlfa2 ! "/";|Y aAlfa2 ! "\";
         #0Campo = aAlfa1 + "/";
         |PINTA #0Campo;
    |FINSI;
    |PINTA #0Campo;

    Existe = 0;
    aAlfa1 = #0Campo; |QBF aAlfa1;
    aAlfa1 = aAlfa1 + "prova.txt";

    |XABRE "WB", aAlfa1, nCanal;
    |SI FSalida < 0;
        |XABRE "CWB", aAlfa1, nCanal;
    |FINSI;
    |XGRABA nCanal, aAlfa1;
    |SI FSalida < 0; Existe = 1; |FINSI;
    |XCIERRA nCanal;
    |SI Existe = 0; |RFBORRA aAlfa1; |FINSI;

    |SI Existe = 1;
         |MENAV "    No existe directorio. ";
         |ERROR;
    |FINSI;
|FCAL;

|PROCESO LaUnidad;
 |SI #0#0 ! "A"; |Y #0#0 ! "B";
     |C_M #0#3, 1, "S";
     |C_V #0#3, 1, "S";
     |PINTA #0#3;
 |SINO;
     |C_M #0#3, 1, "N";
     |C_V #0#3, 1, "N";
     |PINTA 1222, "                                   ";
 |FINSI;
|FPRC;


|PROGRAMA;

    |ABRE #0;
    |LEE 101#0p;
    |SI FSalida ! 0;
        |DDEFECTO #0;
        |GRABA 020#0b;
    |FINSI;
    aNombre = #0#2;

    |CLS;
    |PINPA #0#0;
    |CABEZA "Copiar Fichero a Diskette.";
    |SI SwXml = 1;
        #0#2 = eaNombre1;
    |FINSI;

    |PINDA #0#0;
    |ENDATOS #1#0;
    |SI FSalida ! 0;
        |LIBERA #0;
        |CIERRA #0;
        |ACABA;
    |FINSI;
    aAlfa = #0#2;
    |SI SwXml = 1;
        #0#2 = aNombre;
    |FINSI;
    |GRABA 020#0a;
    |LIBERA #0;
    |CIERRA #0;
    #0#2 = aAlfa;

    |DFICE  aPath;

    |ATRI P; |ATRI R; |PINTA 2470," Copiando "; |ATRI 0;

    aAlfa = "C:/MODELOS";                      |RMKDIR aAlfa;
    aAlfa = "C:/MODELOS/DISKETTE";             |RMKDIR aAlfa;
    aAlfa = "C/MODELOS/DISKETTE/";
    |R_PDIR aAlfa;
    |PARA; |SI FSalida = 0; |HACIENDO;
           |RFBORRA aAlfa;
           |R_SDIR aAlfa;
    |SIGUE;

     |SI SwXml = 1;
         aOrigen  = eaFichero;
     |SINO;
         aOrigen  = aPath + "assecrec.seq";
     |FINSI;
     aDestino = "C:/MODELOS/DISKETTE/" + #0#2;  |QBT aDestino;

     aIPRemoto = "";
     |IP_REMOTO aIPRemoto;  |QBT aIPRemoto;
     |SI aIPRemoto ! "";
         |ENVIA_FICHERO aOrigen, aDestino;
     |SINO;
         |COPIA_FICHERO aOrigen, aDestino;
     |FINSI;

     aFichero = #0#2;  |QBT aFichero;
     |SI #0#0 = "A"; |O #0#0 = "B";
         aCopia   = "COPY " + aDestino + " " + #0#0 + ":" + aFichero;
         aAlfa3 = "    El Disquette se copio sastisfactoriamente.";
     |SINO;
         aAlfa1 = #0#3; |QBF aAlfa1;
         aCopia   = "COPY " + aDestino + " " + aAlfa1 + aFichero;
         aAlfa3 = "    El fichero se copio sastisfactoriamente en el directorio elegido.";
     |FINSI;
     |RSYSTEM aCopia;

     |MENAV aAlfa3;
|FPRO;
