|FICHEROS;
    ascabmin #1;
    aslinmin #2;
    ascontro #4;
    asclient #5;
    asconcep #6;
    ashisfac #7;
    ashiscon #8;
    asbancos #9;
    ashisrec #10;
    asactual #11;
    asfpagos #12;
    ashismic #90;
    ashismil #91;

    asm00003 #500;
|FIN;

|VARIABLES;
    tdto    = 0;
    timpo   = 0;
    FEstado = 0;
    ivabo   = 0;
    acum    = 0;
    hono    = 0;
    supli   = 0;
    tacum   = 0;
    thono   = 0;
    tsupli  = 0;
    tfac    = 0;
    tpagar  = 0;
    tret    = 0;
    timp    = 0;
    ret     = 0;
    retenci = 0;
    fecli   = @;
    serie   = "  ";
    fechv   = @;
    em      = 0;
    j       = 0;
    xxx     = 0;
    yyy     = 0;
    mes     = "";
    mes_num = 0;
    campo   = 0;

    &EURODB   = 0;
|FIN;

|PROCESO concepto;
mes = #2#15 % 0402;
mes_num = mes;

#6#0 = #2#4;
#6#1 = #2#5;
|LIBERA #6;
|LEE 110#6=;
|SI FSalida = 0;
    #6#31 = #6#31 + #2#13;
    #6#6  = #6#6  + #2#13;
    campo = 18 + mes_num; #6campo = #6campo +  #2#13;
    |GRABA 121#6a;
|SINO;
    |PINTA 0410, "    CONCEPTO ";
    |PINTA 0423, #6#0;
    |PINTA 0427, #6#1;
    |PINTA 0429, " INEXISTENTE ";
    |AVISO;
    |PAUSA;
    |BLIN 4;
|FINSI;
|LIBERA #6;
|FPRC;

|PROCESO histocon;
#8#0  = #1#27;
#8#1  = #1#28;
#8#2  = #2#2;
|LIBERA #8;
|LEE 110#8=;
FEstado = FSalida;

|SI FSalida = 0;
    |MENAV "     Ya existia la Factura en el Historico de Conceptos";
|FINSI;

#8#0  = #1#27;
#8#1  = #1#28;
#8#2  = #2#2;
#8#3  = #2#15;
#8#4  = #2#1;
#8#5  = #2#4;
#8#6  = #2#5;
#8#7  = #2#7;
#8#8  = #2#10;
#8#9  = #2#13;
#8#10 = #2#8;
#8#11 = #2#9;
|SI FEstado ! 0; |GRABA 020#8n; |SINO; |GRABA 020#8a; |FINSI;
|LIBERA #8;
|FPRC;

|PROCESO graba_hislin;
#91#0  = #1#27;
#91#1  = #1#28;
#91#2  = #2#2;
#91#3  = #2#15;
#91#4  = #2#4;
#91#5  = #2#5;
#91#6  = #2#6;
#91#7  = #2#7;
#91#8  = #2#8;
#91#9  = #2#9;
#91#10 = #2#10;
#91#11 = #2#11;
#91#12 = #2#12;
#91#13 = #2#13;
#91#14 = #2#14;
|GRABA 040#91n;
|FPRC;

|PROCESO leelinea;
hono  = 0;  supli = 0;  acum  = 0;

|SI #2#10 = "H"; hono  = #2#13; supli = 0; acum  = 0; |FINSI;
|SI #2#10 = "S"; supli = #2#13; hono  = 0; acum  = 0; |FINSI;
|SI #2#10 = "A"; acum  = #2#13; hono  = 0; supli = 0; |FINSI;

thono  = thono  + hono;
tsupli = tsupli + supli;
tacum  = tacum  + acum;

|HAZ concepto;
|HAZ histocon;
|HAZ graba_hislin;
|FPRC;

|PROCESO bancos;
mes = #1#29 % 0402;
mes_num = mes;

|ABRE #9;
#9#0 = #1#11;
|LIBERA #9;
|LEE 110#9=;
|SI FSalida = 0;
    #9#20 = #9#20 + tpagar;
    #9#22 = #9#22 + tpagar;
    campo = 7 + mes_num;
    #9campo = #9campo + tpagar;
    |GRABA 020#9a;
|SINO;
    |PINTA 0410, "    BANCO  ";
    |PINTA 0423, #9#0;
    |PINTA 0426, " INEXISTENTE ";
    |AVISO;
    |PAUSA;
    |BLIN 4;
|FINSI;
|LIBERA #9;
|CIERRA #9;
|FPRC;

|PROCESO cliente;
mes = #1#29 % 0402;
mes_num = mes;

|ABRE #5;
#5#0 = #1#1;
|LIBERA #5;
|LEE 110#5=;
|SI FSalida = 0;
    #5#49 = #5#49 + thono;
    #5#50 = #5#50 + tsupli;
    #5#18 = #5#18 + thono;
    #5#19 = #5#19 + tsupli;

    campo = 24 + mes_num; #5campo = #5campo +  thono;
    campo = 36 + mes_num; #5campo = #5campo +  tsupli;

    #5#24 = tpagar;
    |GRABA 020#5a;
|SINO;
    |PINTA 0410, "    CLIENTE  ";
    |PINTA 0423, #5#0;
    |PINTA 0429, " INEXISTENTE ";
    |AVISO;
    |PAUSA;
    |BLIN 4;
|FINSI;
|LIBERA #5;
|CIERRA #5;
|FPRC;

|PROCESO histofac;
|ABRE #7;
#7#0 = #1#27;
#7#1 = #1#28;
|LIBERA #7;
|LEE 110#7=;
FEstado = FSalida;

|SI FSalida = 0;
    |MENAV "     Ya existia la Factura en el Historico de Facturas";
|FINSI;

#7#2  = #1#29;
#7#3  = #1#1;
#7#4  = #11#1;
#7#5  = thono;
#7#6  = tsupli;
#7#7  = ivabo;
#7#8  = timpo % ivabo;
#7#9  = ret;
#7#10 = tret;
#7#11 = tfac;
#7#12 = "N";
#7#13 = "N";
#7#14 = tacum;
#7#15 = #1#20;
#7#16 = tdto;
#7#17 = timpo;
#7#18 = tpagar;
|SI FEstado ! 0; |GRABA 020#7n; |SINO; |GRABA 020#7a; |FINSI;
|LIBERA #7;
|CIERRA #7;
|FPRC;

|PROCESO mira_ven;
|ABRE #12;
#12#0 = #1#10;
|LEE 040#12=;
|SI FSalida ! 0;
    fechv = #1#29;
|SINO;
    |SI #12#2 = "D"; em = 1; |SINO; em = 1000; |FINSI;
    j = #12#5 / em;
    fechv = #1#29 + j;
|FINSI;
|CIERRA #12;
|FPRC;

|PROCESO historec;
|ABRE #10;
|DDEFECTO #10;
#10#0 = #1#27;
#10#1 = #1#28;
|LIBERA #10;
|LEE 110#10=;
FEstado = FSalida;

|SI FSalida = 0;
    |MENAV "     Ya existia la Factura en el Historico de Recibos";
|FINSI;

#10#2 = #1#29;
#10#3 = #1#1;
#10#4 = #11#1;

|HAZ mira_ven;

#10#5  = fechv;
#10#6  = tpagar;
#10#7  = #1#11;
#10#11 = #1#10;
|SI FEstado ! 0; |GRABA 020#10n; |SINO; |GRABA 020#10a; |FINSI;
|LIBERA #10;
|CIERRA #10;
|FPRC;

|PROCESO HistCuotCli;
   #asm00003#10 = "S";
   |GRABA 020#asm00003.a;
|FPRC;

|DEFBUCLE HistCuotCli;
#asm00003#1;
;
#1#0,#1#1,"  ",00000,"N",001,01;
#1#0,#1#1,"zz",99999,"N",999,99;
be;
NULL,HistCuotCli;
|FIN;

|PROCESO graba_hiscab;
     |ABRE #90;
     #90#0 = #1#27;
     #90#1 = #1#28;
     |LEE 101#90=;
     |SI FSalida ! 0;
         |DDEFECTO #90;
         #90#0 = #1#27;
         #90#1 = #1#28;
         |GRABA 020#90b;
     |FINSI;

     |PARA xxx = 0; |SI xxx [ 26; |HACIENDO xxx = xxx + 1;
           yyy = xxx + 2;
           #90yyy = #1xxx;
     |SIGUE;

     #90#29 = #1#29;
     #90#34 = #1#30;
     #90#45 = ivabo;
     #90#46 = ret;
     |GRABA 020#90a;
     |LIBERA #90;
     |CIERRA #90;

     |BUCLE HistCuotCli;
|FPRC;

|PROCESO imprimir;
#5#0 = #1#1;
|LEE 040#5=;

|PRINT;

thono  = 0;
tsupli = 0;
timp   = 0;
tacum  = 0;
tret   = 0;
tpagar = 0;
|FPRC;

|PROCESO leeminuta;
|SI #1#28 = 0; |ACABA; |FINSI;

|ABRE #6;
|ABRE #8;
|ABRE #91;
|BUCLELIN 0leelinea#1;
|CIERRA #6;
|CIERRA #8;
|CIERRA #91;

tdto   = (thono % #1#20) ! EURODB;
timpo  = thono - tdto;
timp   = (timpo % ivabo) ! EURODB;
tfac   = timpo + tsupli + timp;
ret    = 0;
tret   = 0;
|SI #1#12 = "S"; ret = retenci; |FINSI;
|SI #1#12 = "S";|Y thono ! 0;
    tret = (timpo % ret) ! EURODB;
|FINSI;
tpagar = tfac - tret - tacum;

|HAZ bancos;
|HAZ cliente;
|HAZ histofac;
|HAZ historec;
|HAZ graba_hiscab;
|HAZ imprimir;
|BUCLELIN 1NULL#1;
|BORRA 020#1a;
|LIBERA #1;
|FPRC;

|DEFBUCLE ascabmin;
#1#1;
;
#11#1, 00001;
#11#1, 99999;
e;
NULL, leeminuta;
|FIN;

|PROCESO calprin;
|IMPRE 0;
|SI FSalida ! 0; |ACABA; |FINSI;

|INFOR "asactual";
|SI FSalida ! 0; |FINIMP; |ACABA; |FINSI;

|BUCLE ascabmin;

|PIEINF;
|FININF;
|FINIMP;
|FPRC;

|PROCESO ivabo;
|ABRE #4;
|LEE 121#4p;
|SI FSalida ! 0; |DDEFECTO #4; |FINSI;

|SI #11#1 = 0; ivabo = #4#21; |FINSI;
|SI #11#1 = 1; ivabo = #4#22; |FINSI;
|SI #11#1 = 2; ivabo = #4#23; |FINSI;
|SI #11#1 = 3; ivabo = #4#24; |FINSI;
|SI #11#1 = 4; ivabo = #4#25; |FINSI;
|SI #11#1 = 5; ivabo = #4#26; |FINSI;
|SI #11#1 = 6; ivabo = #4#27; |FINSI;
|SI #11#1 = 7; ivabo = #4#28; |FINSI;
|SI #11#1 = 8; ivabo = #4#29; |FINSI;
|SI #11#1 = 9; ivabo = #4#30; |FINSI;
retenci = #4#31;

|CIERRA #4;
|FPRC;

|PROCESO actualiza; |TIPO 3;
|SI #11#0 ! "SI"; |ACABA; |FINSI;

|HAZ ivabo;
|HAZ calprin;
|FPRC;
