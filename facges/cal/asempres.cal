|FICHEROS;
     asempres #0;                           || CALCULO PARA OPCION DE IM-   ;
|FIN;                                       || PRESION EN MANTENIMIENTOS    ;

|VARIABLES;
     nume1 = 0;
     nume2 = 0;
     lugar = 0;
     nifnum = 0;
     resto = 0;

     informe = "asempres";                  || CAMBIAR VARIABLE `informe',  ;
     men1 = "    NO EXISTE INFORME.";       || FICHERO Y NOMBRE DE CALCULO. ;
     men2 = "    NO EXISTE IMPRESORA.";

     aNifQbf1   = "";
     aNifQbf2   = "";

     &eaVarDni  = "";
     &enCalcCif = 0;
|FIN;

|CALCULO ttempres;|TIPO 4;
|IMPRE 0;                                   ||Abre impresora.                ;
|SI FSalida = 0;                            ||? existe impresora.            ;
    |INFOR informe;                         ||Abre informe.                  ;
    |SI FSalida = 0;                        ||? existe informe               ;
        |LEE 040#0p;                        ||Lee el primer registro.        ;
        |SI FSalida = 0;
            |PRINT;                         ||Comienza informe.              ;
        |FINSI;
        |ET leer;                           ||Bucle de lectura e impresion.  ;
        |LEE 040#0s;
        |SI FSalida = 0;
            |PRINT;
            |VETE leer;
        |FINSI;                             ||Fin de fichero.                ;
        |PIEINF;                            ||Impresion final.               ;
        |FININF;                            ||Cierra informe.                ;
        |FINIMP;                            ||Cierra impresora.              ;
    |SINO;
        |MENAV men1;                        ||No informe.                    ;
    |FINSI;
|SINO;
    |MENAV men2;                            ||No impresora.                  ;
|FINSI;
|FCAL;

|CALCULO letradni;  || CALCULA LETRA PARA EL NIF CUANDO SE PULSA -F1-
     |SI FSalida = 9;
         eaVarDni   = #0Campo;
         enCalcCif  = 1;
         |HAZ CalculoDNI;
         #0Campo = eaVarDni;
         |PINTA #0Campo;
         |ERROR;
         |ACABA;
     |FINSI;

     eaVarDni  = #0Campo;
     enCalcCif = 0;
     |HAZ CalculoDNI;

     aNifQbf1 = #0Campo;   |QBF aNifQbf1;
     aNifQbf2 = eaVarDni;  |QBF aNifQbf2;

     |SI aNifQbf1 ! aNifQbf2; |Y aNifQbf1 ! "";
         |CONFI "0000SEl Nif puede ser Erroneo. Desea Calcularlo.";
         |SI FSalida = 0;
             #0Campo = eaVarDni;  |PINTA #0Campo;
             |ERROR;
         |FINSI;
     |FINSI;
|FCAL;
