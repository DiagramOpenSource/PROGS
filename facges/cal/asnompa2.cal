|FICHEROS;
    asnompa2 #0;
    labregcl #1;
    asconcep #2;
    asclient #3;
    asnomenl #4;
    asnomta2 #5;
    ascabmin #6;
    aslinmin #7;
    ascontro #8;
    asnompat #9;
|FIN;

|VARIABLES;
    fecha  = @;
    varalf = "";
    honorario = 0;
    suplido   = 0;
    acuenta   = 0;
    seccion   = "";
    concepto  = "";
    canti     = 0;
    import    = 0;
    tlinea    = 0;
    iva       = 0;
    linea     = 0;
    FEstado   = 0;
    path      = "";
    seleccio  = 0;
    conver    = "";
    conver1   = "";
    conver2   = "";

    &empre_p  = 0;
    &empre_f  = 0;
    &mes_p    = 0;
    &anyo_p   = 0;
    &fecha_p  = @;
    &tipo_p   = 0;
    &rete_p   = "";
    &tarifa   = 0;
    &d_tipo   = "";
    &h_tipo   = "";

     nTipoFactG=0;
|FIN;
____________________________________/ CALCULOS DE PANTALLA
|PROCESO pinta; |TIPO 7;
    fecha = ~;
    varalf = fecha % 402;
    #0#2 = varalf;
    varalf = fecha % 704;
    #0#3 = varalf;
|PINTA #0#2;
|PINTA #0#3;
|FPRC;
____________________________________/ GRABACION MINUTAS
|PROCESO leeconc;
|DDEFECTO #2;
    #2#0 = seccion;
    #2#1 = concepto;
|LEE 000#2=;
|FPRC;

|PROCESO grabalin;
|ABRE #7;
    linea = linea + 5;
    #7#0  = #0#5;             || tipo facturacion
    #7#1  = empre_f;             || cliente
    #7#2  = linea;            || linea
    |HAZ conver_fec;
    #7#3  = conver;             || fecha
    #7#4  = seccion;          || seccion
    #7#5  = concepto;         || concepto
    #7#6  = #2#2;             || descrip. abreviada
    #7#7  = #2#16;            || descrip. 1
    #7#8  = #2#17;            || descrip. 2
    #7#9  = #2#18;            || descrip. 3
    #7#10 = #2#3;             || tipo
    #7#11 = canti;            || unidades
    #7#12 = import;           || ptas.
    #7#13 = tlinea;           || importe
    #7#14 = #2#5;             || unitario o global
    #7#15 = #0#4;             || Fecha DD/MM/AAAA
|GRABA 020#7n;
|CIERRA #7;
|FPRC;

|PROCESO conver_fec; |TIPO 0;
conver1 = #0#4 % 0102;
conver2 = #0#4 % 0402;
conver  = conver1 + conver2;
|FPRC;

|PROCESO linealin;
|SI #7#10 = "H"; honorario = honorario + #7#13; |FINSI;
|SI #7#10 = "S"; suplido   = suplido   + #7#13; |FINSI;
|SI #7#10 = "A"; acuenta   = acuenta   + #7#13; |FINSI;
|FPRC;

|PROCESO cabecera;
|ABRE #6;
    #6#0 = #0#5;
    #6#1 = empre_f;
|LEE 101#6=;
    FEstado = FSalida;

    #6#0  = #0#5;             || tipo facturacion
    #6#1  = empre_f;             || cliente
    #6#2  = #3#1;             || razon social
    #6#3  = #3#2;             || nombre cliente
    #6#4  = #3#3;             || direccion
    #6#5  = #3#4;             || cod. postal 1
    #6#6  = #3#5;             || cod. postal 2
    #6#7  = #3#6;             || poblacion
    #6#8  = #3#7;             || provincia
    #6#9  = #3#8;             || cif
    #6#10 = #3#15;            || forma de pago
    #6#11 = #3#16;            || banco a remesar
|SI #0#6 = "S"; #6#12 = #3#17; |SINO; #6#12 = "N"; |FINSI;  || retencion
    #6#13 = #3#11;            || banco
    #6#14 = #3#12;            || direccion banco
    #6#15 = #3#13;            || ccc banco
    #6#16 = #3#14;            || cta. cte. banco

    honorario = 0;
    suplido   = 0;
    acuenta   = 0;

|BUCLELIN 0linealin#6;

    #6#17 = honorario;
    #6#18 = suplido;
    #6#19 = acuenta;
    #6#20 = #3#23;
    #6#21 = #6#17 % #6#20;
    #6#22 = #6#17 - #6#21;
|HAZ iva;
    #6#23 = #6#17 % iva;
    #6#24 = #6#22 + #6#23 + #6#18;
|SI #6#12 = "S"; #6#25 = #6#22 % #8#31; |SINO; #6#25 = 0; |FINSI;
    #6#26 = #6#24 - #6#19 - #6#25;
|SI FEstado = 0; |GRABA 020#6a; |SINO; |GRABA 020#6n; |FINSI;

|LIBERA #6;
|CIERRA #6;
|FPRC;

|PROCESO iva;
|DDEFECTO #8;
|ABRE #8;
|LEE 000#8p;
    iva = 0;
|SI #6#0 = 0; iva = #8#21; |FINSI;
|SI #6#0 = 1; iva = #8#22; |FINSI;
|SI #6#0 = 2; iva = #8#23; |FINSI;
|SI #6#0 = 3; iva = #8#24; |FINSI;
|SI #6#0 = 4; iva = #8#25; |FINSI;
|SI #6#0 = 5; iva = #8#26; |FINSI;
|SI #6#0 = 6; iva = #8#27; |FINSI;
|SI #6#0 = 7; iva = #8#28; |FINSI;
|SI #6#0 = 8; iva = #8#29; |FINSI;
|SI #6#0 = 9; iva = #8#30; |FINSI;
|CIERRA #8;
|FPRC;
____________________________________/ PROCESO DE GRABACION
|PROCESO linea;
    linea = #7#2;
|FPRC;

|DEFBUCLE leelin;
 #7#1;
 ;
 #0#5, empre_f,   1;
 #0#5, empre_f, 999;
 e;
 NULL, linea;
|FIN;

|PROCESO impresio;
    honorario = 0;
    suplido   = 0;
    linea     = 0;

|SI empre_p = 0;
    #4#0 = #1#0;
    |LEE 000#4=;                            || lee enlace codigos
    |SI FSalida ! 0; |O #4#8 = "N"; |ACABA; |FINSI;
    empre_f = #4#2;

    |SI #0#10="S";         || tipo facturacion igual para todas las empresas
          #0#5=nTipoFactG;
    |SINO;
          #0#5=#4#6;      || facturacion individual, se guarda en #0#5 porque lo lee simpre de ahi
    |FINSI;
|FINSI;

|DDEFECTO #3;
    #3#0 = empre_f;                        || lee cliente facturacion
|LEE 000#3=;

|DDEFECTO #5;
    #5#1 = #1#4;                        || lee tarifa
|LEE 000#5=;
|SI FSalida = 0;

    |BUCLE leelin;                      || ultima linea
        seccion = #5#2;
        concepto = #5#3;
    |HAZ leeconc;

        canti  = #1#5;
        import = #2#4;
        tlinea = canti * import;

    |SI tlinea ! 0;
        |HAZ grabalin;
        |HAZ cabecera;

#1#6 = "S";      || lo regraba como facturado
        |GRABA 020#1a;
            seleccio = 1;
    |FINSI;
|FINSI;
|LIBERA #1;
|FPRC;

|DEFBUCLE asnomdat;
 #1#2;
 6;
 #0#0, #0#3, #0#2, #0#7, "N";
 #0#1, #0#3, #0#2, #0#8, "N";
 be;
 NULL, impresio;
|FIN;

|PROGRAMA;
|SI empre_f = 0;
    |CLS;
    |CABEZA "Pase Minut.Nominas";
    |DDEFECTO #0;
    |PINPA #0#0;
    |PINDA #0#0;
    |ENDATOS #1#0;
    |SI FSalida = 0;
          nTipoFactG=#0#5;
          |HAZ tipo_3;
    |FINSI;
|SINO;
    #0#0 = empre_p;
    #0#1 = empre_p;
    #0#2 = mes_p;
    #0#3 = anyo_p;
    #0#4 = fecha_p;
    #0#5 = tipo_p;
    #0#6 = rete_p;
    #0#7 = d_tipo;
    #0#8 = h_tipo;
    #0#9 = "SI";
    |HAZ tipo_3;
|FINSI;
|FPRO;

|PROCESO tipo_3; |TIPO 3;
|SI #0#9 = "SI";
    |ABRE #2;
    |ABRE #3;
    |ABRE #4;
    |ABRE #5;
    |SELECT #2#5;

    |HAZ nombre;
    |PATH_DAT #1 path;
    |BUCLE asnomdat;
    |SI seleccio = 0;
        |SI empre_p = 0; |MENAV "    Seleccion vacia ..."; |FINSI;
    |FINSI;

    |CIERRA #2;
    |CIERRA #3;
    |CIERRA #4;
    |CIERRA #5;
|FINSI;
|FPRC;

|PROCESO nombre;
|ABRE #9;
|LEE 000#9p;
|SI FSalida ! 0;
    |MENAV "     No existe directorio Nominas";
|SINO;
    path = #9#1; |QBF path;
    path = path + "fich/";
|FINSI;
|CIERRA #9;
|FPRC;

|PROCESO FactGlobal;|TIPO 0;
     |SI #0#10="N";
          |CAMPO_MODIFICA #0#5,1,"N";
     |SINO;
          |CAMPO_MODIFICA #0#5,1,"S";
     |FINSI;
|FPRC;
