|FICHEROS;
    aslinfdi #0;
    asconcli #1;
    ascabfdi #2;
    asconcep #3;
    asclient #4;
    ascontfd #5;
    asenlcon #6;
    asempcon #8;
    asconmul #9;
    asw00001 #99;
|FIN;

|VARIABLES;
    modo     = 0;
    fiac     = "  ";
    guarda   = "  ";
    relleno  = "                                       ";
    aponer   = "  ";
    cont     = 0;
    fijo     = "";
    c_clie   = "";
    c_fecha  = "";
    nada     = "";
    traba    = "";
    valor    = "";
    clie     = 0;
    line     = 0;
    cero     = 0;
    secc     = "";
    conc     = "";
    xxxxfiac = "  ";
    xxaponer = "  ";
    xrelleno = "                              ";
    alfa1    = "";
    alfa2    = "";
    alfa3    = "";
    alfa4    = "";
    entra    = 0;
    sw_modi_con = 0;
    secc_cli = "";
    conc_cli = "";
    descr_ant = "";
    conver    = "";
    conver1   = "";
    conver2   = "";

    nModo     = 0;
    nSwHay    = 0;
    nLinea    = 0;
    nLinea1   = 0;
    nPtas     = 0;
    aSeccion  = "";
    aConcepto = "";

    &iva      = 0;
|FIN;

|PROCESO LeeConceMul;
     nSwHay = 0;
     |ABRE #9;
     #9#0 = aSeccion;
     #9#1 = aConcepto;
     |LEE 040#9=;
     |SI FSalida = 0;  nSwHay = 1;  |FINSI;
     |CIERRA #9;
|FPRC;

|PROCESO ConceMultiple;  |TIPO 0;
     nModo = (FEntrada / 100) ! 0;
     |SI nModo ! 1;  |ACABA;  |FINSI;

     aSeccion  = #0#4;
     aConcepto = #0#5;
     |HAZ LeeConceMul;
     |SI nSwHay = 0;  |ACABA;  |FINSI;

     nLinea = 0;
     nPtas = #0#12;
     #0#12 = #0#12 - (#0#12 < #9#3);
     |PINTA #0#12;
     nLinea = #0#2;
|FPRC;

|PROCESO LineaNueva;
     #0#0  = #2#0;
     #0#1  = #2#1;
     #0#14 = #2#27;
     #0#15 = #2#28;
     #0#2 = 999;
     |LEE 040#0];
     |SI FSalida = 0;
         |LEE 040#0a;
     |SINO;
         |LEE 040#0u;
     |FINSI;

     |SI FSalida = 0;  |Y #0#0 = #2#0;   |Y #0#1  = #2#1;
                       |Y #0#14 = #2#27; |Y #0#15 = #2#28;
         nLinea1 = #0#2 + 1;
     |SINO;
         nLinea1 = 1;
     |FINSI;

     #0#0 = #2#0;
     #0#1 = #2#1;
     #0#14 = #2#27;
     #0#15 = #2#28;
     #0#2 = nLinea;
     |LEE 040#0=;
     |SI FSalida = 0;
         aSeccion  = #0#4;
         aConcepto = #0#5;
         |HAZ LeeConceMul;

         aponer = Asino;
         |HAZ poncontr;

         |ABRE #3;
         #3#0 = #9#4;
         #3#1 = #9#5;
         |LEE 040#3=;
         |SI FSalida ! 0; |DDEFECTO #3; |FINSI;
         |CIERRA #3;

         #0#2  = nLinea1;
         #0#4  = #3#0;
         #0#5  = #3#1;
         #0#6  = #3#2;
         #0#7  = #3#16;
         #0#8  = #3#17;
         #0#9  = #3#18;
         #0#10 = #3#3;
         #0#12 = nPtas - (nPtas < #9#7);
         #0#13 = #0#11 * #0#12;

         |GRABA 140#0n;
         |LIBERA #0;

         |SI nLinea1 ! 2;
             |PTEC 809;
             |PTEC 808;
             |PTEC 806;
             |PTEC 802;
         |SINO;
             |PTEC 815;
             |PTEC 815;
             |PTEC 808;
             |PTEC 814;
             |PTEC 806;
             |PTEC 802;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO lineanue; |TIPO 7;
     |SI nLinea ! 0;
         |HAZ LineaNueva;
         nLinea = 0;
         |ACABA;
     |FINSI;

     sw_modi_con = 0;

     |ABRE #5;
     |LEE 040#5p;
     |CIERRA #5;

     |SI #5#32 ! "S"; |ACABA; |FINSI;

     cont = (FEntrada / 100) ! 0;
     |ABRE #4;
     #4#0 = #2#1;
     |LEE 040#4=;
     |SI FSalida ! 0; |CIERRA #4; |ACABA; |FINSI;
     |SI #4#24 = 0;   |CIERRA #4; |ACABA; |FINSI;
     |CIERRA #4;

     |ABRE #3;
     #3#0 = #5#33;
     #3#1 = #5#34;
     |LEE 040#3=;
     |SI FSalida ! 0; |CIERRA #3; |ACABA; |FINSI;
     |SI #3#3 ! "A";  |CIERRA #3; |ACABA; |FINSI;
     |CIERRA #3;

     |SI cont = 1; |Y FSalida ! 1; |Y FSalida  ! 7; |Y #0#2 = 1;
         aponer = Asino;
         |HAZ poncontr;
         #0#0  = #2#0;
         #0#1  = #2#1;
         #0#2  = 5;
         #0#3  = 0;
         #0#4  = #3#0;
         #0#5  = #3#1;
         #0#6  = #3#2;
         #0#7  = #3#16;
         #0#8  = #3#17;
         #0#9  = #3#18;
         #0#10 = #3#3;
         #0#11 = 1;
         #0#12 = -#4#24;
         #0#13 = -#4#24;
         #0#14 = #2#27;
         #0#15 = #2#28;
         #0#16 = "G";
         #0#17 = "00.00.0000";
         |GRABA 140#0n;
         |LIBERA #0;

         #2#19 = #2#19 + #0#13;
         #2#26 = #2#24 - #2#19 - #2#25;
         |GRABA 040#2a;

         |PINTA #2#19;
         |PINTA #2#26;

         aponer = Anosi;
        |HAZ poncontr;
     |FINSI;
|FPRC;

|PROCESO poncontr;
fiac    = Control % A109;
fiac    = fiac + relleno;
guarda  = fiac % A109;
fiac    = Control % A1001;
Control = Control + fiac;
Control = Control + relleno;
Control = Control % A120;
Control = Control + aponer;
|FPRC;

|PROCESO conver_fec; |TIPO 0;
conver1 = #0#17 % 0102;
conver2 = #0#17 % 0402;
conver  = conver1 + conver2;
#0#3 = conver;
|FPRC;

|RUTINA minimo;
#1#0 = #0#1;
#1#1 = "    ";
#1#2 = "  ";
|FRUT;

|RUTINA maximo;
#1#0 = #0#1;
#1#1 = "ееее";
#1#2 = "ее";
|FRUT;

|PROCESO leeconcl;|TIPO 0;    ||si pulsa F1 accede a conceptos / clientes
|SI FSalida ! 9; |ACABA; |FINSI;

secc_cli = "";
conc_cli = "";

|PUSHV 0501 2380;
|PINPA #0#1;
|PINTA 926, #0#1;

|ENTLINEAL #1#1, 3, 4, 19, 0, minimo, maximo;
|POPV;

|SI secc_cli ! ""; |O conc_cli ! "";
    #0#4 = secc_cli; |PINTA #0#4;
    #0#5 = conc_cli; |PINTA #0#5;
|FINSI;
|FPRC;

|PROCESO leconcep;|TIPO 0;
modo = (FEntrada / 100) ! 0;

|SI secc = #0#4; |Y conc = #0#5; |Y clie = #0#1; |Y modo ! 1;
    |ACABA;
|FINSI;

|ABRET #1;
|ABRET #3;

#1#0 = #0#1;
#1#1 = #0#4;
#1#2 = #0#5;
|LEE 040#1=;
|SI FSalida = 0;
    #0#6 = #1#6;
    #0#7 = #1#7;
    #0#8 = #1#8;
    #0#9 = #1#9;
    #0#10 = #1#3;
    #0#11 = 1;
    #0#12 = #1#10;
    #0#16 = #1#5;
|SINO;
    #3#0 = #0#4;
    #3#1 = #0#5;
    |LEE 040#3=;
    |SI FSalida = 0;
        |SI #3#3 = "N";
            |MENAV "     Es una nota de Expedientes";
            |ERROR;
            |CIERRAT #1;
            |CIERRAT #3;
            |ACABA;
        |SINO;
            |ABRE #4;
            #4#0 = #0#1;
            |LEE 040#4=;
            |SI FSalida = 0;
                |SI #4#22 = 1; #0#12 = #3#4;  #0#16 = #3#5;  |FINSI;
                |SI #4#22 = 2; #0#12 = #3#8;  #0#16 = #3#9;  |FINSI;
                |SI #4#22 = 3; #0#12 = #3#10; #0#16 = #3#11; |FINSI;
                |SI #4#22 = 4; #0#12 = #3#12; #0#16 = #3#13; |FINSI;
                |SI #4#22 = 5; #0#12 = #3#14; #0#16 = #3#15; |FINSI;
            |FINSI;
            |CIERRA #4;

            #0#6 = #3#2;
            #0#7 = #3#16;
            #0#8 = #3#17;
            #0#9 = #3#18;
            #0#10 = #3#3;
            #0#11 = 1;
        |FINSI;
    |SINO;
        #0#5 = 0;
        |MENAV "    ATENCION no existe el concepto automatico";
        |PTEC 808;
        |CIERRAT #1;
        |CIERRAT #3;
        |ACABA;
    |FINSI;
|FINSI;

|SI #0#16 = "U"; #0#13 = #0#12; |FINSI;

#0#13 = #0#12 * #0#11;  |PINTA #0#13;

|CIERRAT #1;
|CIERRAT #3;

|PINTA #0#6;
|PINTA #0#7;
|PINTA #0#8;
|PINTA #0#9;
|PINTA #0#11;
|PINTA #0#12;
|PINTA #0#13;

secc = #0#4;
conc = #0#5;
clie = #0#1;
|FPRC;

|PROCESO descabre; |TIPO 0;
traba = #0#7;  |QBF traba;
valor = traba % A0;
|SI valor = 0;
    #0#7 = #0#6;
    |PINTA #0#7;
|FINSI;
|SI #0#7 = descr_ant;
    #0#7 = #0#6;
    |PINTA #0#7;
|FINSI;
|FPRC;

|PROCESO descr_ant; |TIPO 7;
descr_ant = #0#6;
|FPRC;

|CALCULO imporref; |TIPO 0;
  #0#13 = #0#12 * #0#11;
    |PINTA #0#13;
|FCAL;

|CALCULO refresca;|TIPO 2;
  clie = #0#1;
  line = #0#2;
  secc = #0#4;
  conc = #0#5;

  |SI #0#10 = "H";
      #2#17 = #2#17 +. #0#13;
  |FINSI;
  |SI #0#10 = "S";
      #2#18 = #2#18 +. #0#13;
  |FINSI;
  |SI #0#10 = "A";
      #2#19 = #2#19 +. #0#13;
  |FINSI;
  |HAZ iva;
  #2#21 = #2#17 % #2#20;
  #2#22 = #2#17 - #2#21;
  #2#23 = #2#22 % iva;
  #2#24 = #2#22 + #2#23 + #2#18;
  |SI #2#12 = "S";
      #2#25 = #2#22 % #5#31;
  |SINO;
      #2#25 = 0;
  |FINSI;
  #2#26 = #2#24 - #2#25 - #2#19;
  |PINTA #0#13;
  |PINTA #2#17;
  |PINTA #2#18;
  |PINTA #2#19;
  |PINTA #2#21;
  |PINTA #2#22;
  |PINTA #2#23;
  |PINTA #2#24;
  |PINTA #2#25;
  |PINTA #2#26;
|FCAL;

|CALCULO iva;
|ABRE #5;
|LEE 040#5p;
|SI FSalida ! 0; |DDEFECTO #5; |FINSI;
|CIERRA #5;
iva = 0;
|SI #2#0 = 0; iva = #5#21; |FINSI;
|SI #2#0 = 1; iva = #5#22; |FINSI;
|SI #2#0 = 2; iva = #5#23; |FINSI;
|SI #2#0 = 3; iva = #5#24; |FINSI;
|SI #2#0 = 4; iva = #5#25; |FINSI;
|SI #2#0 = 5; iva = #5#26; |FINSI;
|SI #2#0 = 6; iva = #5#27; |FINSI;
|SI #2#0 = 7; iva = #5#28; |FINSI;
|SI #2#0 = 8; iva = #5#29; |FINSI;
|SI #2#0 = 9; iva = #5#30; |FINSI;
|FCAL;

|CALCULO nomodi; |TIPO 7;
   |SI #0#16 = "G"; |HAZ nomodifi; |FINSI;
|FCAL;

|CALCULO nomodifi;             || RUTINA DE CONVERSION A NO MODIFICABLES
                               || VARIABLES: A_
    xxaponer = Anono;
    xxxxfiac = Control % A1001;
    Control = Control + xxxxfiac;
    Control = Control + xrelleno;
    Control = Control % A120;
    Control = Control + xxaponer;
|FCAL;

|PROCESO graba_con; |TIPO 2;
|ABRE #6;
    #6#0 = #0#4;
    #6#1 = #0#5;
|LEE 000#6=;
|SI FSalida ! 0; |CIERRA #6; |ACABA; |FINSI;
|CIERRA #6;

|ABRE #8;
|LEE 000#8p;
|SI FSalida ! 0; |DDEFECTO #8; |FINSI;
|CIERRA #8;

    c_clie  = #0#1;
    c_clie  = ("00000" + c_clie) % -105;
    fijo    = #8#3; |QBF fijo;
    fijo    = fijo + c_clie;
    conver1 = #0#17 % 0102;
    conver2 = #0#17 % 0402;
    conver  = conver1 + conver2;
    c_fecha = conver;
    c_fecha = ("0000" + c_fecha) % -104;

    modo = (FEntrada / 100) ! 0;
|SI modo = 1;                      |HAZ alta_con; |FINSI;
|SI modo = 2;|Y sw_modi_con = 0;   |HAZ modi_con; |FINSI;
|SI modo = 3;                      |HAZ baja_con; |FINSI;
|FPRC;

|PROCESO alta_con;
|ABRE #99;
#99#0 = #0#0;
#99#1 = #0#1;
#99#2 = #0#2;
|LEE 101#99=;
|SI FSalida = 0;
   #99#3  = #8#1;
   #99#4  = #8#2;
   #99#5  = c_fecha;
   #99#6  = fijo;
   #99#7  = #6#2;
   #99#8  = #6#6;
   #99#9  = #6#3;
   #99#10 = #6#4;
   #99#11 = #0#13;
   #99#12 = #6#5;
   #99#13 = 0;
   #99#14 = "";
   |GRABA 020#99a;
|SINO;
   |DDEFECTO #99;
   #99#0  = #0#0;        || tipo
   #99#1  = #0#1;        || cliente
   #99#2  = #0#2;        || linea
   #99#3  = #8#1;        || empresa
   #99#4  = #8#2;        || ejercicio
   #99#5  = c_fecha;     || fecha
   #99#6  = fijo;        || cuenta contable
   #99#7  = #6#2;        || contrapartida
   #99#8  = #6#6;        || signo
   #99#9  = #6#3;        || concepto
   #99#10 = #6#4;        || descripcion
   #99#11 = #0#13;       || importe
   #99#12 = #6#5;        || diario
   #99#13 = 0;           || asiento
   #99#14 = "";          || marcado
   |GRABA 020#99n;
|FINSI;
|CIERRA #99;
|FPRC;

|PROCESO baja_con;
|ABRE #99;
    #99#0 = #0#0;
    #99#1 = #0#1;
    #99#2 = #0#2;
|LEE 101#99=;
|SI FSalida = 0;
    |SI #99#14 = "*";
            alfa1 = #99#13;  alfa1 = "000000" + alfa1;  alfa1 = alfa1 % -106;
            alfa2 = #99#12;  alfa2 = "00"     + alfa2;  alfa2 = alfa2 % -102;
            alfa3 = #99#5;
            alfa4 = "    AVISO: Debera actualizar el asiento " + alfa2 + "-" + alfa3 + "-" + alfa1;
        |AVISO;
        |MENSA alfa4;
        |CONFI "2400SConfirme la baja (S/N): ";
        |SI FSalida ! 0;
            |ERROR;
        |SINO;
            |BORRA 020#99a;
        |FINSI;
        |BLIN 4;
    |SINO;
        |BORRA 020#99a;
    |FINSI;
|FINSI;
|LIBERA #99;
|CIERRA #99;
|FPRC;

|PROCESO modi_con;
    entra = 0 +. 1;

|ABRE #99;
    #99#0 = #0#0;
    #99#1 = #0#1;
    #99#2 = #0#2;
|LEE 101#99=;
|SI FSalida = 0;
    |SI #99#14 = "*";|Y entra ! 1;
            alfa1 = #99#13;  alfa1 = "000000" + alfa1;  alfa1 = alfa1 % -106;
            alfa2 = #99#12;  alfa2 = "00"     + alfa2;  alfa2 = alfa2 % -102;
            alfa3 = #99#5;
            alfa4 = "    AVISO: Debera actualizar el asiento " + alfa2 + "-" + alfa3 + "-" + alfa1;
        |AVISO;
        |MENSA alfa4;
        |CONFI "2400SConfirme la modificacion (S/N): ";
        |SI FSalida ! 0;
            |ERROR;
                sw_modi_con = 1;
        |SINO;
            |HAZ modi_con_2;
        |FINSI;
        |BLIN 4;
    |SINO;
        |HAZ modi_con_2;
    |FINSI;
|FINSI;
|LIBERA #99;
|CIERRA #99;
|FPRC;

|PROCESO modi_con_2;
    #99#3  = #8#1;
    #99#4  = #8#2;
    #99#5  = c_fecha;
    #99#6  = fijo;
    #99#7  = #6#2;
    #99#8  = #6#6;
    #99#9  = #6#3;
    #99#10 = #6#4;
    #99#11 = #99#11 +. #0#13;
    #99#12 = #6#5;
|GRABA 020#99a;
|FPRC;

|| ************************************************************************
||                  PROCESOS LLAMADOS DESDE CONCEPTOS CLIENTES
|| ************************************************************************

|PROCESO este_bueno; |TIPO 11;
|SI FSalida ! 9; |ACABA; |FINSI;

|CAMPO_ATRIBUTO #1#1, R, 0, 0; |PINTA #1#1;
|CAMPO_ATRIBUTO #1#2, R, 0, 0; |PINTA #1#2;
|CAMPO_ATRIBUTO #1#6, R, 0, 0; |PINTA #1#6;
|CAMPO_ATRIBUTO #1#3, R, 0, 0; |PINTA #1#3;

|CONFI "2400SEl concepto es el correcto : ";
|SI FSalida = 0;
    secc_cli = #1#1;
    conc_cli = #1#2;
|FINSI;

|CAMPO_ATRIBUTO #1#1, D, 0, 0; |PINTA #1#1;
|CAMPO_ATRIBUTO #1#2, D, 0, 0; |PINTA #1#2;
|CAMPO_ATRIBUTO #1#6, D, 0, 0; |PINTA #1#6;
|CAMPO_ATRIBUTO #1#3, D, 0, 0; |PINTA #1#3;

|SI secc_cli ! ""; |O conc_cli ! ""; |PTEC 807; |FINSI;
|FPRC;


|PROCESO Tipo0C29;  |TIPO 0;
     |HAZ iva;

     |SI #2#29 [ "30.06.2010";
         |SI iva = 8;  |O iva = 18; |O iva = 10; |O iva = 21;
             alfa1 = iva;
             alfa1 = "0000Se va a facturar con un IVA erroneo ("+alfa1+"%), segun la fecha de factura.";
             |MENAV alfa1;
             |CONFI "0000NQuiere continuar";
             |SI FSalida ! 0;
                 |PTEC 807;
                 |ACABA;
             |FINSI;
         |FINSI;
     |FINSI;

     |SI #2#29 ] "01.07.2010"; |Y #2#29 < "01.09.2012";
         |SI iva = 7;  |O iva = 16; |O iva = 10; |O iva = 21;
             alfa1 = iva;
             alfa1 = "0000Se va a facturar con un IVA erroneo ("+alfa1+"%), segun la fecha de factura.";
             |MENAV alfa1;
             |CONFI "0000NQuiere continuar";
             |SI FSalida ! 0;
                 |PTEC 807;
                 |ACABA;
             |FINSI;
         |FINSI;
     |FINSI;

     |SI #2#29 ] "01.09.2012";
         |SI iva = 7;  |O iva = 16; |O iva = 8; |O iva = 18;
             alfa1 = iva;
             alfa1 = "0000Se va a facturar con un IVA erroneo ("+alfa1+"%), segun la fecha de factura.";
             |MENAV alfa1;
             |CONFI "0000NQuiere continuar";
             |SI FSalida ! 0;
                 |PTEC 807;
                 |ACABA;
             |FINSI;
         |FINSI;
     |FINSI;
|FPRC;
