|FICHEROS;
   asmancaj #0;
   asnompat #1;

   canempr@ #500;
   cacuent@ #501;
|FIN;

|VARIABLES;
   Comodin     = "";
   Comodin1    = "";
   Comodin2    = "";
   Cuenta      = "";
   aLong       = "";
   Parte       = "";
   Part1       = "";
   Part2       = "";
   LongAlfa    = "";
   aTipo       = "";

   ModoEntrada = 0;
   Nivel       = 0;
   MaxDigit    = 0;
   Parte1      = 0;
   Parte2      = 0;
   Long        = 0;
   nLong       = 0;
   nBloque     = 0;
   nAgrupa     = 0;
   nEpigra     = 0;
   nPartid     = 0;
   nCont       = 0;
   Calc        = 0;
|FIN;

|PROCESO MiraEmpresa;
   |SI FSalida = 2; |ACABA; |FINSI;
   |CIERRA #cacuent@;

   #canempr@#2 = #asmancaj#4;
   #canempr@#3 = #asmancaj#5;
   |LEE 000#canempr@.=;
   |SI FSalida ! 0;
      |MENAV "    Empresa inexistente";
      |ERROR;
   |SINO;
      #asmancaj#8 = #canempr@#1;
      |PINTA 1537, #asmancaj#8;
      Comodin = #asnompat#2; |QBF Comodin; Comodin = Comodin - "fich/";
      Comodin1 = Comodin + "fich/" + (("00000" + #asmancaj#4) % -105) + #asmancaj#5 + "/";
      Comodin2 = Comodin + "pan/";
      |PATH_DAT #501 Comodin1; |ABRE #cacuent@; |PATH_PAN #501 Comodin2;
      MaxDigit = 0;
      |SI #canempr@#14 > MaxDigit; MaxDigit = #canempr@#14; |FINSI;
      |SI #canempr@#15 > MaxDigit; MaxDigit = #canempr@#15; |FINSI;
      |SI #canempr@#16 > MaxDigit; MaxDigit = #canempr@#16; |FINSI;
      |SI #canempr@#17 > MaxDigit; MaxDigit = #canempr@#17; |FINSI;
      |SI #canempr@#18 > MaxDigit; MaxDigit = #canempr@#18; |FINSI;
      |SI #canempr@#19 > MaxDigit; MaxDigit = #canempr@#19; |FINSI;
      #asmancaj#10 = #canempr@#14;
      #asmancaj#11 = #canempr@#15;
      #asmancaj#12 = #canempr@#16;
      #asmancaj#13 = #canempr@#17;
      #asmancaj#14 = #canempr@#18;
      #asmancaj#15 = #canempr@#19;
   |FINSI;
|FPRC;

|PROCESO MiraCuenta;
   Cuenta = #asmancaj#Campo#; |HAZ SacaNivel;
   #cacuent@#0 = #asmancaj#Campo#;
   #cacuent@#1 = Nivel;
   |LEE 000#cacuent@.=;
   |SI FSalida ! 0;
      |PUSHV 0501 2380;
      #cacuent@#0 = #asmancaj#Campo#;
      #cacuent@#1 = Nivel;
      |PINPA #0#cacuent@; |PINDA #0#cacuent@;
      |CONFI "    NDar de alta ? ";
      |SI FSalida = 0;
         |PARA nCont = #canempr@#13; |SI nCont ] 1; |HACIENDO nCont = nCont - 1;
            Cuenta = #asmancaj#Campo#; |QBF Cuenta;
            Calc = nCont + 13;
            Calc = 100 + #canempr@#Calc#;
            Cuenta = Cuenta % Calc;
            #cacuent@#0 = Cuenta;
            #cacuent@#1 = nCont;
            |LEE 000#cacuent@.=;
            |SI FSalida = 0;
               nBloque = #cacuent@#4;
               aTipo   = #cacuent@#5;
               nAgrupa = #cacuent@#6;
               nEpigra = #cacuent@#7;
               nPartid = #cacuent@#8;
               |SAL;
            |FINSI;
         |SIGUE;
         |DDEFECTO #cacuent@;
         #cacuent@#0 = #asmancaj#Campo#;
         #cacuent@#1 = Nivel;
         |GRABA 020#cacuent@.b;
         #cacuent@#3 = "S";
         #cacuent@#4 = nBloque;
         #cacuent@#5 = aTipo;
         #cacuent@#6 = nAgrupa;
         #cacuent@#7 = nEpigra;
         #cacuent@#8 = nPartid;
         |PINDA #0#cacuent@;
         |ENDATOS #1#cacuent@;
         |SI FSalida = 0;
            |GRABA 020#cacuent@.a; |LIBERA #cacuent@;
            |POPV;
         |SINO;
            |BORRA 020#cacuent@.a;
            |MENAV "    Cuenta inexistente"; |LIBERA #cacuent@;
            |POPV; |ERROR; |ACABA;
         |FINSI;
      |SINO;
         |MENAV "    Cuenta inexistente";
         |POPV; |ERROR; |ACABA;
      |FINSI;
   |SINO;
      #asmancaj#9 = #cacuent@#2;
      |PINTA 1737, #asmancaj#9;
   |FINSI;
|FPRC;

|PROCESO Cuenta; |TIPO 6;
   Comodin = #asmancaj#Campo#;
   Comodin = Comodin - ".";
   |SI FSalida ! 0;
      Parte = FSalida;
      Parte1 = 100 + (((Parte / 100) ! 0) - 1);
      Parte2 = ((((Parte / 100) ! 0) + 1) * 100) + 99;
      Part1 = #asmancaj#Campo# % Parte1;
      Part2 = #asmancaj#Campo# % Parte2;
      |QBF Part2;
      Comodin = Part1 + Part2;
      LongAlfa = Comodin % 0;
      Long = MaxDigit - LongAlfa;
      LongAlfa = "0" * Long;
      Comodin = Part1 + LongAlfa + Part2;
      #asmancaj#Campo# = Comodin;
      |PINTA #asmancaj#Campo#;
   |FINSI;
|FPRC;

|PROCESO SacaNivel;
   Nivel = 0;
   |QBF Cuenta; aLong = Cuenta % 0; nLong = aLong;
   |SI #canempr@#14 = nLong; Nivel = 1; |FINSI;
   |SI #canempr@#15 = nLong; Nivel = 2; |FINSI;
   |SI #canempr@#16 = nLong; Nivel = 3; |FINSI;
   |SI #canempr@#17 = nLong; Nivel = 4; |FINSI;
   |SI #canempr@#18 = nLong; Nivel = 5; |FINSI;
   |SI #canempr@#19 = nLong; Nivel = 6; |FINSI;
|FPRC;

|PROCESO Importe;
    ModoEntrada = (FEntrada / 100) ! 0;
    |SI ModoEntrada = 1;
       #asmancaj#7 = #asmancaj#3; |PINTA #asmancaj#7;
    |FINSI;
|FPRC;

|PROCESO Tipo8; |TIPO 80;
   |ABRE #asnompat;
   |LEE 000#asnompat.p;
   |SI FSalida ! 0;
      |MENAV "    No esta definido el control de directorios";
      |CIERRA #asnompat; |ACABA;
   |FINSI;
   Comodin = #asnompat#2; |QBF Comodin; Comodin = Comodin - "fich/";
   Comodin1 = Comodin + "def/canempre.mas";
   |CARGA_DEF Comodin1, canempr@;
   |SI FSalida ! 0;
      |MENAV "    No encuentro el fichero de empresas de contabilidad";
      |ACABA;
   |FINSI;

   Comodin1 = Comodin + "fich/"; |PATH_DAT #500 Comodin1; |ABRE #canempr@;

   Comodin1 = Comodin + "def/cacuenta.mas";
   |CARGA_DEF Comodin1, cacuent@;
   |SI FSalida ! 0;
      |MENAV "    No encuentro el fichero de plan de cuentas";
      |ACABA;
   |FINSI;
|FPRC;

|PROCESO Tipo9; |TIPO 9;
   |CIERRA #cacuent@; |DESCARGA_DEF #cacuent@;
   |CIERRA #canempr@; |DESCARGA_DEF #canempr@;
|FPRC;
