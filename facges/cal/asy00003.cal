|FICHEROS;
     asy00003 #0;
     ashismic #1;
     asw00011 #99;
|FIN;

|VARIABLES;
     FechaSys = @;
     VAlfa    = "";
     FechaD   = "";
     FechaH   = "";
     Fichero  = "";
     SwGraba  = 0;

 {-1}Menu         = "";
     Menu1        = "2400";
     Menu2        = "1";
     Menu3        = "Elija Impresion : ";
     Menu4        = "PI";
     Menu5        = "  Pantalla  ";
     Menu6        = "  Impresora  ";
|FIN;

|PROCESO PintaAnyo; |TIPO 7;
  FechaSys = ~;
  VAlfa    = FechaSys % -104;
  #0#2     = VAlfa;  |PINTA #0#2;
|FPRC;

|| ******************************** Procesos grabacion del temporal

|PROCESO PonNombreMes;
  |SI #99#1 = 1;   #99#2 = "ENERO     ";  |FINSI;
  |SI #99#1 = 2;   #99#2 = "FEBRERO   ";  |FINSI;
  |SI #99#1 = 3;   #99#2 = "MARZO     ";  |FINSI;
  |SI #99#1 = 4;   #99#2 = "ABRIL     ";  |FINSI;
  |SI #99#1 = 5;   #99#2 = "MAYO      ";  |FINSI;
  |SI #99#1 = 6;   #99#2 = "JUNIO     ";  |FINSI;
  |SI #99#1 = 7;   #99#2 = "JULIO     ";  |FINSI;
  |SI #99#1 = 8;   #99#2 = "AGOSTO    ";  |FINSI;
  |SI #99#1 = 9;   #99#2 = "SEPTIEMBRE";  |FINSI;
  |SI #99#1 = 10;  #99#2 = "OCTUBRE   ";  |FINSI;
  |SI #99#1 = 11;  #99#2 = "NOVIEMBRE ";  |FINSI;
  |SI #99#1 = 12;  #99#2 = "DICIEMBRE ";  |FINSI;
|FPRC;

|PROCESO GrabaTempo;
  |DDEFECTO #99;
  VAlfa = #1#29 % 402;
  #99#0 = #1#0;
  #99#1 = VAlfa;
  |LIBERA #99;
  |LEE 140#99=;
  |SI FSalida ! 0;
      |DDEFECTO #99;
      #99#0 = #1#0;
      #99#1 = VAlfa;
      |GRABA 140#99b;
  |FINSI;

  |HAZ PonNombreMes;

  #99#3  = #99#3  +  #1#19;
  #99#4  = #99#4  +  #1#23;
  #99#5  = #99#5  +  #1#24;
  #99#6  = #99#6  +  #1#25;
  #99#7  = #99#7  +  #1#20;
  #99#8  = #99#8  +  #1#26;
  #99#9  = #99#9  +  #1#27;
  #99#10 = #99#10 +  #1#21;
  #99#11 = #99#11 +  #1#28;
  |GRABA 140#99a;
  |LIBERA #99;

  #00#3  = #00#3  +  #1#19;
  #00#4  = #00#4  +  #1#23;
  #00#5  = #00#5  +  #1#24;
  #00#6  = #00#6  +  #1#25;
  #00#7  = #00#7  +  #1#20;
  #00#8  = #00#8  +  #1#26;
  #00#9  = #00#9  +  #1#27;
  #00#10 = #00#10 +  #1#21;
  #00#11 = #00#11 +  #1#28;

  SwGraba = 1;
|FPRC;

|DEFBUCLE ashismic;
  #1#2;
  ;
  FechaD, "  ",     0;
  FechaH, "zz", 99999;
  ;
  NULL, GrabaTempo;
|FIN;


|| ******************************** Procesos por impresora

|PROCESO Imprime;
  |PRINT;
|FPRC;

|DEFBUCLE asw00011;
  #99#1;
  ;
  00, "  ";
  99, "zz";
  ;
  NULL, Imprime;
|FIN;

|PROCESO Impresora;
  |IMPRE 0;
  |SI FSalida ! 0;  |ACABA;  |FINSI;

  |INFOR "asy00003";
  |SI FSalida ! 0;  |FINIMP;  |ACABA;  |FINSI;

  |BUCLE asw00011;

  |PIEINF;
  |FININF;
  |FINIMP;
|FPRC;

|| ******************************** Procesos por pantalla

|RUTINA ClaveBaja;
  #99#0 = "  ";
  #99#1 = 0;
|FRUT;

|RUTINA ClaveAlta;
  #99#0 = "zz";
  #99#1 = 12;
|FRUT;

|PROCESO Pantalla;
  |PINPA #0#99;

  |ATRI I;
  |PINTA 0710, #0#2;
  |PINTA 1166, #0#3;
  |PINTA 1266, #0#4;
  |PINTA 1366, #0#5;
  |PINTA 1466, #0#6;
  |PINTA 1566, #0#7;
  |PINTA 1666, #0#8;
  |PINTA 1866, #0#9;
  |PINTA 1966, #0#10;
  |PINTA 2066, #0#11;
  |ATRI 0;

  |ENTLINEAL #1#99, 2, 4, 20, 0, ClaveBaja, ClaveAlta;
|FPRC;

|| ******************************** Procesos Comienzo

|PROCESO MontaFecha;
  VAlfa = #0#0;
  VAlfa = ("00" + VAlfa) % -102;
  FechaD = "01." + VAlfa + "." + #0#2;

  VAlfa = #0#1;
  VAlfa = ("00" + VAlfa) % -102;
  FechaH = "31." + VAlfa + "." + #0#2;
|FPRC;

|PROCESO Tipo3;  |TIPO 3;
  |MENU Menu;
  Menu2 = FSalida;
  |SI Menu2 ! 1; |Y Menu2 ! 2; |ACABA; |FINSI;

  Fichero = Usuario % 808;  |QBT Fichero;
  Fichero = ("Y3" + Fichero + "zzzzzzzz") % 108;
  |NOME_DAT #99 Fichero;
  |ABRE #99;  |CIERRA #99;  |DELINDEX #99;

  |HAZ MontaFecha;

  |ABRE #99;
  |BUCLE ashismic;
  |CIERRA #99;

  |SI SwGraba  = 0;  |DELINDEX #99; |ACABA;  |FINSI;

  |SI Menu2 = 1;  |HAZ Pantalla;   |FINSI;
  |SI Menu2 = 2;  |HAZ Impresora;  |FINSI;

  |DELINDEX #99;
|FPRC;
