|FICHEROS;
  ascacuen #0;
  asclient #1;    || clientes
  cacuenta #2;    || plan contable
  canempre #3;    || empresas
  asnompat #4;    || control
  cacuebal #6;    || cuentas de balance
  caclipro #9;    || Clientes/proveedores  Contabilidad
|FIN;

|VARIABLES;
  alfa1 = "";
  alfa2 = "";
  nume1 = 0;
  FEstado = 0;
  nivel = 0;
  relleno = "";
  path_empresa = "";
  uno = "";
  dos = "";
  mensa1 = "    No esta definida esta cuenta en Cuentas de Balance ...";
  nro_niv = 0;
  Relleno = "";
|FIN;
____________________________________/ DESDE PANTALLA
|PROCESO tipo8; |TIPO 8;
  |ABRE #4;
  |LEE 000#4p;
  |SI FSalida ! 0;
      |MENAV "    No existe Control de directorios ...";
      |PTEC 807;
  |FINSI;
  |CIERRA #4;
|FPRC;

|PROCESO mayus; |TIPO 0;
  #0Campo = #0Campo > #0Campo;
  |PINTA #0Campo;
|FPRC;

|PROCESO lee_empresa; |TIPO 0;
  |SI FSalida = 2;  |ACABA;  |FINSI;
  alfa1 = #4#2;    |QBF alfa1;
  |PATH_DAT #3 alfa1;
  |DDEFECTO #3;
  |ABRE #3;
  #3#2 = #0#2;
  #3#3 = #0#3;
  |LEE 000#3=;
  |ATRI I;   |PINTA 1416, #3#1;  |ATRI 0;
  |SI FSalida ! 0;
      |MENAV "    Empresa Inexistente ...";
      |CIERRA #3;
      |ERROR;
      |ACABA;
  |FINSI;
  |SI #3#13 = 1;  nivel = #3#14;  nro_niv = 1;   |FINSI;
  |SI #3#13 = 2;  nivel = #3#15;  nro_niv = 2;   |FINSI;
  |SI #3#13 = 3;  nivel = #3#16;  nro_niv = 3;   |FINSI;
  |SI #3#13 = 4;  nivel = #3#17;  nro_niv = 4;   |FINSI;
  |SI #3#13 = 5;  nivel = #3#18;  nro_niv = 5;   |FINSI;
  |SI #3#13 = 6;  nivel = #3#19;  nro_niv = 6;   |FINSI;
  |SI nivel < 7;
      |MENAV "    ATENCION!!! Nivel de Trabajo excesivamente corto (7) ...";
      |ERROR;
  |SINO;
      alfa1 = #0#2; alfa1 = "00000" + alfa1;   alfa1 = alfa1 % -105;
      alfa2 = #0#3;
      path_empresa = alfa1 + alfa2;
  |FINSI;
  |CIERRA #3;
|FPRC;
____________________________________/
|PROCESO defect;
  uno = #2#0;
  |QBF uno;
  dos = uno + "000000000000";
  uno = dos % A101;
  #6#0 = uno;
  |LEE 000#6=;
  |SI FSalida = 0;  |HAZ saca;  |ACABA;  |FINSI;
  uno = dos % A102;
  #6#0 = uno;
  |LEE 000#6=;
  |SI FSalida = 0;  |HAZ saca;  |ACABA;  |FINSI;

  uno = dos % A103;
  #6#0 = uno;
  |LEE 000#6=;
  |SI FSalida = 0;  |HAZ saca;  |ACABA;  |FINSI;

  uno = dos % A104;
  #6#0 = uno;
  |LEE 000#6=;
  |SI FSalida = 0;  |HAZ saca;  |SINO;  |MENAV mensa1;  |FINSI;
|FPRC;

|PROCESO saca;
  #2#4    = #6#1;
  #2#5    = #6#2;
  #2#6    = #6#3;
  #2#7    = #6#4;
  #2#8    = #6#5;
  Relleno = #2#0; |QBF Relleno;
  Relleno = (Relleno + "zzzzzzzzzzzz") % 112;
  #2#54   = Relleno;
  #2#55   = #2#1;
|FPRC;

|PROCESO enlaza_cli;
  #9#23 = "C";
  #9#10 = #2#0;       ||Cuenta
  #9#11 = #2#1;       ||digito control;
  |LEE 101#9=;
  |SI FSalida ! 0;
      |DDEFECTO #9;
      #9#23 = "C";
      #9#10 = #2#0;       ||Cuenta
      #9#11 = #2#1;       ||digito control;
      ||#9#0 = #1#0;     Lo que ponia antes
      #9#0 = #2#0;
      |HAZ actua_cli;
      |GRABA 020#9n;
  |SINO;
       |SI #0#5 = "S";
          |HAZ actua_cli;
          |GRABA 040#9a;
       |FINSI;
  |FINSI;
  |LIBERA #9;
|FPRC;

|PROCESO actua_cli;
  |SI #0#6 = "R"; #9#1 = #1#1;   |FINSI;        || razon social
  |SI #0#6 = "N"; #9#1 = #1#2;   |FINSI;        || nombre
  #9#2 = #1#3;        ||Direccion
  #9#3 = #1#4;        ||CP 1
  #9#4 = #1#5;        ||CP 2
  #9#5 = #1#7;        ||Provincia
  #9#6 = #1#6;        ||Poblacion
  #9#7 = #1#9;        ||Telefono
  #9#8 = #1#10;       ||Fax
  #9#9 = #1#8;        ||Cif
  #9#10 = #2#0;       ||Cuenta
  #9#11 = #2#1;       ||digito control;
  #9#13 = #1#11;      ||nombre del banco;
  #9#14 = #1#12;      ||direccion del banco;
  #9#17 = #1#14;      ||cta corriente;
  #9#18 = #1#13;      ||ccc
|FPRC;

|PROCESO lee_clientes1;
  alfa1 = #1#52;
  |QBF alfa1;
  |SI alfa1 = "";
  alfa1 = #1#0;  alfa1 = "00000" + alfa1;    alfa1 = alfa1 % -105;
  #2#0 = relleno + alfa1;
  #2#1 = nro_niv;
  |SINO;
       alfa1 = #1#52;
       #2#0 = alfa1;
       #2#1 = nro_niv;
  |FINSI;
  |LEE 101#2=;
  FEstado = FSalida;
  |SI FEstado ! 0;|O #0#5 = "S";
      |SI #0#6 = "R"; #2#2 = #1#1;   |FINSI;        || razon social
      |SI #0#6 = "N"; #2#2 = #1#2;   |FINSI;        || nombre
      #2#3 = "S";                || pase directo SI
      |HAZ defect;
      |SI FEstado = 0;     |GRABA 020#2a; |FINSI;
      |SI FEstado ! 0;     |GRABA 020#2n; |FINSI;
      |SI FSalida = 0;
          |ATRI I;
          |PINTA 2139, #2#0;
          |ATRI 0;
      |FINSI;
  |FINSI;
  |LIBERA #2;
  |HAZ enlaza_cli;
|FPRC;

|DEFBUCLE lee_clientes;
 #1#1;
 ;
 #0#0;
 #0#1;
 e;
 NULL, lee_clientes1;
|FIN;

|PROCESO relleno;
  relleno = "43";
  |SI nivel > 7;
      nume1 = nivel - 7;
      alfa1 = "0" * nume1;
      relleno = relleno + alfa1;
  |FINSI;
|FPRC;

|PROCESO tipo3; |TIPO 3;
  |SI #0#4 ! "SI";  |ACABA;  |FINSI;
  |HAZ relleno;
  alfa1 = #4#2;    |QBF alfa1;
  path_empresa = alfa1 + path_empresa + "/";
  |PATH_DAT #2 path_empresa;
  |PATH_DAT #6 path_empresa;
  |PATH_DAT #9 path_empresa;
  |ABRE #2;
  |ABRE #6;
  |ABRE #9;
  |BUCLE lee_clientes;
  |CIERRA #2;
  |CIERRA #6;
  |CIERRA #9;
|FPRC;
