|FICHEROS;
  asconcli #0;                           || CALCULO PARA OPCION DE IM-   ;
  asclient #1;
  asconcep #2;
|FIN;                                       || PRESION EN MANTENIMIENTO.    ;

|VARIABLES;
     informe = "asconcli";                  || CAMBIAR VARIABLE `informe',  ;
     men1 = "    NO EXISTE INFORME.";       || FICHERO Y NOMBRE DE CALCULO. ;
     men2 = "    NO EXISTE IMPRESORA.";
     nada = " ";
     nada1 = " ";
     cero = 0;
|FIN;

|CALCULO ttconcli;|TIPO 4;
|IMPRE 0;                                   ||Abre impresora.                ;
|SI FSalida = 0;                            ||? existe impresora.            ;
    |INFOR informe;                         ||Abre informe.                  ;
    |SI FSalida = 0;                        ||? existe informe               ;
        |LEE 040#0p;                        ||Lee el primer registro.        ;
        |SI FSalida = 0;
            |PRINT;                         ||Comienza informe.              ;
        |FINSI;
        |ET leer;                           ||Bucle de lectura e impresion.  ;
        |LEE 040#0s;
        |SI FSalida = 0;
            |PRINT;
            |VETE leer;
        |FINSI;                             ||Fin de fichero.                ;
        |PIEINF;                            ||Impresion final.               ;
        |FININF;                            ||Cierra informe.                ;
        |FINIMP;                            ||Cierra impresora.              ;
    |SINO;
        |MENAV men1;                        ||No informe.                    ;
    |FINSI;
|SINO;
    |MENAV men2;                            ||No impresora.                  ;
|FINSI;
|FCAL;

|CALCULO cliente; |TIPO 0;
|ABRE #1;
|ABRE #2;

#1#0 = #0#0;
|LEE 040#1=;
|SI FSalida = 0;
   #2#0 = #0#1;
   #2#1 = #0#2;
   |LEE 040#2=;
   |SI FSalida = 0;
      |SI #2#3 = "N";
          |MENAV "    Es una Nota de Expedientes";
          |ERROR;
      |SINO;
          |SI #1#22 = 1; #0#10 = #2#4;  #0#5 = #2#5;  |FINSI;
          |SI #1#22 = 2; #0#10 = #2#8;  #0#5 = #2#9;  |FINSI;
          |SI #1#22 = 3; #0#10 = #2#10; #0#5 = #2#11; |FINSI;
          |SI #1#22 = 4; #0#10 = #2#12; #0#5 = #2#13; |FINSI;
          |SI #1#22 = 5; #0#10 = #2#14; #0#5 = #2#15; |FINSI;
          |PINTA #0#10;
          |PINTA #0#5;
      |FINSI;
   |SINO;
      |MENAV "    No Existe Concepto";
   |FINSI;
|SINO;
   |MENAV "     No existe Cliente";
|FINSI;
|CIERRA #1;
|CIERRA #2;
|FCAL;

|CALCULO descrip;|TIPO 6;
    nada = #0#7;
|QBF nada;
    nada1 = nada % Acero;
|SI nada1 = 0
    #0#7 = #0#6;
|PINTA #0#7;
|FINSI;
|FCAL;
