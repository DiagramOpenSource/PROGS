|FICHEROS;
   ascontca #0;
   asmancaj #1;
   asnompat #2;
   asmovtos #3;

   canempr@ #500;
   camandi@ #501;
   camasic@ #502;
   camasil@ #503;
   cacuent@ #504;
   caconas@ #505;
|FIN;

|VARIABLES;
   Comodin  = "";
   Comodin1 = "";
   Comodin2 = "";
   aLong    = "";
   Cuenta   = "";
   Cuenta1  = "";

   SwHay     = 0;
   NumLinea  = 0;
   SaldoMovs = 0;
   ComodNum  = 0;
   Documento = 0;
   Calc      = 0;
   nLong     = 0;
   Nivel     = 0;
   nImporte  = 0;
   CmpDebe   = 0;
   CmpHaber  = 0;
   CmpSaldo  = 0;
   TotDebe   = 0;
   TotHaber  = 0;
   TotSaldo  = 0;
   nCont     = 0;

   nBloque   = 0;
   aTipo     = 0;
   nAgrupa   = 0;
   nEpigra   = 0;
   nPartid   = 0;
|FIN;

|PROCESO Contador;
   |LEE 101#caconas@.p;
   |SI FSalida ! 0;
      |DDEFECTO #caconas@;
      |GRABA 020#caconas@.n;
      |LEE 101#caconas@.p;
   |FINSI;

   #caconas@#1 = #caconas@#1 + 1;
   Comodin = "|NC"; Calc = #caconas@#Comodin#;
   |SI Calc > 2; #caconas@#2 = #caconas@#2 + 1; |FINSI;
   Documento = #caconas@#1;

   |GRABA 020#caconas@.a; |LIBERA #caconas@;
|FPRC;

|PROCESO TomaPath;
   |CIERRA #camandi@;
   |ABRE #asmancaj;
   #asmancaj#0 = #ascontca#0;
   |LEE 000#asmancaj.=;
   |SI FSalida = 0;
      Comodin1 = Comodin + "fich/" + (("00000" + #asmancaj#4) % -105) + #asmancaj#5 + "/";
      Comodin2 = Comodin + "pan/";
      |PATH_DAT #501 Comodin1; |ABRE #camandi@;
      |PATH_DAT #502 Comodin1; |ABRE #camasic@;
      |PATH_DAT #503 Comodin1; |ABRE #camasil@;
      |PATH_DAT #504 Comodin1; |ABRE #cacuent@; |PATH_PAN #504 Comodin2;
      |PATH_DAT #505 Comodin1; |ABRE #caconas@;
   |FINSI;
   |CIERRA #asmancaj;
|FPRC;

|PROCESO MiraDiario;
   #camandi@#0 = #ascontca#2;
   |LEE 000#camandi@.=;
   |SI FSalida = 0;
      |PINTA 1434, #camandi@#1;
   |SINO;
      |MENAV "    Diario inexistente";
      |ERROR;
   |FINSI;
|FPRC;

|PROCESO Actualiza;
   || Actualiza el diario
   #camandi@#0 = #camasic@#0;
   |LEE 101#camandi@.=;
   |SI #camasil@#8 = "D";
      #camandi@#2 = #camandi@#2 + nImporte;
   |SINO;
      #camandi@#3 = #camandi@#3 + nImporte;
   |FINSI;
   |GRABA 020#camandi@.a; |LIBERA #camandi@;

   || Actualiza la cuenta del apunte
   Cuenta = #camasil@#4; |HAZ SacaNivel1;
   |SI Nivel = 0;
      Comodin = #camasil@#4; |QBF Comodin;
      Comodin = "    La cuenta " + Comodin + " no tiene longitud correcta";
      |MENAV Comodin;
   |FINSI;

   Cuenta1 = Cuenta; |HAZ ActCuenta;

   || Actualiza los niveles superiores
   Nivel = Nivel - 1;
   |PARA; |SI Nivel > 0; |HACIENDO Nivel = Nivel - 1;
          |SI Nivel = 1; Calc = #canempr@#14; |FINSI;
          |SI Nivel = 2; Calc = #canempr@#15; |FINSI;
          |SI Nivel = 3; Calc = #canempr@#16; |FINSI;
          |SI Nivel = 4; Calc = #canempr@#17; |FINSI;
          |SI Nivel = 5; Calc = #canempr@#18; |FINSI;
          |SI Nivel = 6; Calc = #canempr@#19; |FINSI;
          Calc = Calc + 100;
          Comodin = Cuenta1; |QBF Comodin;
          Cuenta = Comodin % Calc; |HAZ ActCuenta;
   |SIGUE;
|FPRC;

|PROCESO ActCuenta;
   #cacuent@#0 = Cuenta; |HAZ SacaNivel1;
   #cacuent@#1 = Nivel;
   |LEE 101#cacuent@.=;
   |SI FSalida ! 0;
      |PUSHV 0501 2380;
      #cacuent@#0 = Cuenta;
      #cacuent@#1 = Nivel;
      |PINPA #0#cacuent@; |PINDA #0#cacuent@;
      |CONFI "    NDar de alta ? ";
      |SI FSalida = 0;
         |PARA nCont = #canempr@#13; |SI nCont ] 1; |HACIENDO nCont = nCont - 1;
            Calc = nCont + 13;
            Calc = 100 + #canempr@#Calc#;
            Comodin = Cuenta % Calc;
            #cacuent@#0 = Comodin;
            #cacuent@#1 = nCont;
            |LEE 000#cacuent@.=;
            |SI FSalida = 0;
               nBloque = #cacuent@#4;
               aTipo   = #cacuent@#5;
               nAgrupa = #cacuent@#6;
               nEpigra = #cacuent@#7;
               nPartid = #cacuent@#8;
               |SAL;
            |FINSI;
         |SIGUE;
         |DDEFECTO #cacuent@;
         #cacuent@#0 = Cuenta;
         #cacuent@#1 = Nivel;
         |GRABA 020#cacuent@.b;
         #cacuent@#3 = "S";
         #cacuent@#4 = nBloque;
         #cacuent@#5 = aTipo;
         #cacuent@#6 = nAgrupa;
         #cacuent@#7 = nEpigra;
         #cacuent@#8 = nPartid;
         |PINDA #0#cacuent@;
         |ENDATOS #1#cacuent@;
         |SI FSalida = 0;
            |GRABA 020#cacuent@.a; |LIBERA #cacuent@;
            |POPV;
         |SINO;
            |BORRA 020#cacuent@.a;
            |MENAV "    Cuenta inexistente"; |LIBERA #cacuent@;
            |POPV; |ERROR; |ACABA;
         |FINSI;
      |SINO;
         |MENAV "    Cuenta inexistente";
         |POPV; |ERROR; |ACABA;
      |FINSI;
   |FINSI;

   Comodin = #camasil@#1; |QBF Comodin;
   Comodin = Comodin % 0402; ComodNum = Comodin;
   CmpDebe = (ComodNum * 3) + 9;
   CmpHaber = CmpDebe + 1;
   CmpSaldo = CmpDebe + 2;
   |SI #camasil@#8 = "D";
      #cacuent@#CmpDebe# = #cacuent@#CmpDebe# + nImporte;
   |SINO;
      #cacuent@#CmpHaber# = #cacuent@#CmpHaber# + nImporte;
   |FINSI;
   #cacuent@#CmpSaldo# = #cacuent@#CmpDebe# - #cacuent@#CmpHaber#;

   CmpDebe = 0; CmpHaber = 0; CmpSaldo = 0;
   TotDebe = 0; TotHaber = 0; TotSaldo = 0;
   |PARA nCont = 12; |SI nCont [ 45; |HACIENDO nCont = nCont + 3;
        CmpDebe = nCont; CmpHaber = nCont + 1; CmpSaldo = nCont + 2;
        TotDebe  = TotDebe  + #cacuent@#CmpDebe#;
        TotHaber = TotHaber + #cacuent@#CmpHaber#;
        TotSaldo = TotSaldo + #cacuent@#CmpSaldo#;
   |SIGUE;

   #cacuent@#51 = #cacuent@#9 + TotDebe;
   #cacuent@#52 = #cacuent@#10 + TotHaber;
   #cacuent@#53 = #cacuent@#11 + TotSaldo;
   |SI #camasic@#1 > #cacuent@#58; #cacuent@#58 = #camasic@#1; |FINSI;

   |GRABA 020#cacuent@.a;
   |LIBERA #cacuent@;
|FPRC;

|PROCESO SacaNivel1;
   Comodin = Cuenta; |QBF Comodin;
   aLong = Comodin % 0; nLong = aLong;
   Nivel = 0;
   |SI nLong = #canempr@#14; Nivel = 1; |FINSI;
   |SI nLong = #canempr@#15; Nivel = 2; |FINSI;
   |SI nLong = #canempr@#16; Nivel = 3; |FINSI;
   |SI nLong = #canempr@#17; Nivel = 4; |FINSI;
   |SI nLong = #canempr@#18; Nivel = 5; |FINSI;
   |SI nLong = #canempr@#19; Nivel = 6; |FINSI;
|FPRC;

|PROCESO Movimientos;

   SwHay = 1;

   |DDEFECTO #camasil@;
   #camasil@#0 = #camasic@#0;
   #camasil@#1 = #camasic@#1;
   #camasil@#2 = #camasic@#2;
   #camasil@#3 = NumLinea;
   |GRABA 020#camasil@.b;
   |SI #asmovtos#6 = "            ";
      #camasil@#4 = #asmovtos#7; Cuenta = #camasil@#4; |HAZ SacaNivel1;
      #camasil@#5 = Nivel;
      #camasil@#8 = "H";
      SaldoMovs = SaldoMovs - #asmovtos#8;
   |SINO;
      #camasil@#4 = #asmovtos#6; Cuenta = #camasil@#4; |HAZ SacaNivel1;
      #camasil@#5 = Nivel;
      #camasil@#8 = "D";
      SaldoMovs = SaldoMovs + #asmovtos#8;
   |FINSI;
   #camasil@#6 = 1;
   Comodin = "MOV.CAJA " + (("00" + #ascontca#0) % -102) + " FECHA " + #ascontca#1;
   #camasil@#7 = Comodin;
   #camasil@#9 = #asmovtos#8;
   #camasil@#10 = #asmovtos#6; Cuenta = #asmovtos#6;
   |HAZ SacaNivel1; #camasil@#11 = Nivel;
   #camasil@#16 = #asmovtos#7; Cuenta = #asmovtos#7;
   |HAZ SacaNivel1; #camasil@#17 = Nivel;
   |GRABA 020#camasil@.a; |LIBERA #camasil@;
   NumLinea = NumLinea + 1;
   nImporte = #asmovtos#8; |HAZ Actualiza;

   #asmovtos#13 = "S"; |GRABA 020#asmovtos.a; |LIBERA #asmovtos;
|FPRC;

|DEFBUCLE Movimientos;
#asmovtos#1;
13;
#ascontca#0,#ascontca#1,0001,"N";
#ascontca#0,#ascontca#1,9999,"N";
be;
NULL,Movimientos;
|FIN;

|PROGRAMA;
   |CLS;
   |CABEZA "Contabilizacion de cajas";
   |PINPA #0#ascontca;
   |PINDA #0#ascontca;

   |ABRE #asnompat;
   |LEE 000#asnompat.p;
   |SI FSalida ! 0;
      |MENAV "    No esta definido el control de directorios";
      |CIERRA #asnompat; |ACABA;
   |FINSI;
   Comodin = #asnompat#2; |QBF Comodin; Comodin = Comodin - "fich/";
   Comodin1 = Comodin + "def/canempre.mas";
   |CARGA_DEF Comodin1, canempr@;
   |SI FSalida ! 0;
      |MENAV "    No encuentro el fichero de empresas de contabilidad";
      |ACABA;
   |FINSI;
   Comodin1 = Comodin + "def/camandia.mas";
   |CARGA_DEF Comodin1, camandi@;
   |SI FSalida ! 0;
      |CIERRA #canempr@; |DESCARGA_DEF #canempr@;
      |MENAV "    No encuentro el fichero de mantenimiento de diarios";
      |ACABA;
   |FINSI;
   Comodin1 = Comodin + "def/camaasic.mas";
   |CARGA_DEF Comodin1, camasic@;
   |SI FSalida ! 0;
      |CIERRA #canempr@; |DESCARGA_DEF #canempr@;
      |CIERRA #camandi@; |DESCARGA_DEF #camandi@;
      |MENAV "    No encuentro el fichero de cabecera de asientos";
      |ACABA;
   |FINSI;
   Comodin1 = Comodin + "def/camaasil.mas";
   |CARGA_DEF Comodin1, camasil@;
   |SI FSalida ! 0;
      |CIERRA #camasic@; |DESCARGA_DEF #camasic@;
      |CIERRA #canempr@; |DESCARGA_DEF #canempr@;
      |CIERRA #camandi@; |DESCARGA_DEF #camandi@;
      |MENAV "    No encuentro el fichero de lineas de asientos";
      |ACABA;
   |FINSI;
   Comodin1 = Comodin + "def/cacuenta.mas";
   |CARGA_DEF Comodin1, cacuent@;
   |SI FSalida ! 0;
      |CIERRA #camasil@; |DESCARGA_DEF #camasil@;
      |CIERRA #camasic@; |DESCARGA_DEF #camasic@;
      |CIERRA #canempr@; |DESCARGA_DEF #canempr@;
      |CIERRA #camandi@; |DESCARGA_DEF #camandi@;
      |MENAV "    No encuentro el fichero de plan de cuentas";
      |ACABA;
   |FINSI;
   Comodin1 = Comodin + "def/caconasi.mas";
   |CARGA_DEF Comodin1, caconas@;
   |SI FSalida ! 0;
      |CIERRA #cacuent@; |DESCARGA_DEF #cacuent@;
      |CIERRA #camasil@; |DESCARGA_DEF #camasil@;
      |CIERRA #camasic@; |DESCARGA_DEF #camasic@;
      |CIERRA #canempr@; |DESCARGA_DEF #canempr@;
      |CIERRA #camandi@; |DESCARGA_DEF #camandi@;
      |MENAV "    No encuentro el fichero de contador de asientos";
      |ACABA;
   |FINSI;

   Comodin1 = Comodin + "fich/"; |PATH_DAT #500 Comodin1; |ABRE #canempr@;
   |ENDATOS #1#ascontca;
   |SI FSalida = 0;

      #canempr@#2 = #asmancaj#4;
      #canempr@#3 = #asmancaj#5;
      |LEE 000#canempr@.=;
      |SI FSalida ! 0;
         |MENAV "    La empresa asociada a la caja no existe";
         |CIERRA #caconas@; |DESCARGA_DEF #caconas@;
         |CIERRA #cacuent@; |DESCARGA_DEF #cacuent@;
         |CIERRA #camasil@; |DESCARGA_DEF #camasil@;
         |CIERRA #camasic@; |DESCARGA_DEF #camasic@;
         |CIERRA #canempr@; |DESCARGA_DEF #canempr@;
         |CIERRA #camandi@; |DESCARGA_DEF #camandi@;
         |CIERRA #asnompat;
      |FINSI;

      SaldoMovs = 0; NumLinea = 11;
      |HAZ Contador;

      SwHay = 0;
      |DDEFECTO #camasic@;
      #camasic@#0 = #ascontca#2;
      #camasic@#1 = #ascontca#1;
      #camasic@#2 = Documento;
      #camasic@#3 = Documento;
      |GRABA 020#camasic@.b;
      |BUCLE Movimientos;
      |SI SwHay = 0; |BORRA 020#camasic@.a; |LIBERA #camasic@; |VETE Salir; |FINSI;

      #camasil@#3 = 1;
      |GRABA 020#camasil@.b;
      #camasil@#4 = #asmancaj#6; Cuenta = #camasil@#4; |HAZ SacaNivel1;
      #camasil@#5 = Nivel;
      |SI SaldoMovs ] 0;
         #camasil@#8 = "D";
         #camasil@#16 = #camasil@#4; #camasil@#17 = #camasil@#5;
         #camasil@#10 = "";          #camasil@#11 = #camasil@#5;
      |SINO;
         #camasil@#10 = #camasil@#4; #camasil@#11 = #camasil@#5;
         #camasil@#16 = "";          #camasil@#17 = #camasil@#5;
         #camasil@#8 = "H";
         SaldoMovs = 0 - SaldoMovs;
      |FINSI;
      #camasil@#6 = 1;
      Comodin = "MOV.CAJA " + (("00" + #ascontca#0) % -102) + " FECHA " + #ascontca#1;
      #camasil@#7 = Comodin;
      #camasil@#9 = SaldoMovs;
      |GRABA 020#camasil@.a; |LIBERA #camasil@;
      nImporte = SaldoMovs; |HAZ Actualiza;

      #camasic@#4 = #asmovtos#8;
      #camasic@#5 = #asmovtos#8;
      #camasic@#6 = 0;
      #camasic@#7 = 0;
      |GRABA 020#camasic@.a; |LIBERA #camasic@;

   |FINSI;

|ET Salir;
   |CIERRA #caconas@; |DESCARGA_DEF #caconas@;
   |CIERRA #cacuent@; |DESCARGA_DEF #cacuent@;
   |CIERRA #camasil@; |DESCARGA_DEF #camasil@;
   |CIERRA #camasic@; |DESCARGA_DEF #camasic@;
   |CIERRA #camandi@; |DESCARGA_DEF #camandi@;
   |CIERRA #canempr@; |DESCARGA_DEF #canempr@;
   |CIERRA #asnompat;
|FPRO;
