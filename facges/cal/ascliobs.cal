
||                      OBSERVACIONES CLIENTES;


|FICHEROS;
     asobserv #918;
     asclient #0;
|FIN;

|VARIABLES;
     aDirectorio    = "";
     aNFichero      = "";
     aVariableCarga = "";
     aFichero       = "";
     aAlfa          = "";

     nVariable      = 0;
     nBaffer        = 0;
     nTotalLinea    = 0;
     nTope          = 0;
     nVari1         = 0;
     nVari2         = 0;

     &eaCodigo      = "";
     &eaTipoCodigo  = "";
     &eaNombreList  = "";
     &enCuadroIni   = 0;
     &enCuadroFin   = 0;
     &enCampoAncho  = 0;
     &enModoEdita   = 0;
     &enTotalVaria  = 0;

{100}&epPuntEdita   = "";
|FIN;

|PROCESO GrabaSecuencial;
     IepPuntEdita  = epPuntEdita1 <-;
     |PARA nTotalLinea = 0;  |SI nTotalLinea [ enTotalVaria;  |HACIENDO nTotalLinea = nTotalLinea + 1;
          |AL_BUF nBaffer, nTotalLinea, epPuntEdita;
          IepPuntEdita = IepPuntEdita + 1;
     |SIGUE;
|FPRC;

|PROCESO Edicion;
     nTope  = nTotalLinea - 1;
     nVari1 = 0;
     nVari2 = 0;
     enModoEdita = enModoEdita + 100;
     |EDITA nBaffer, 1, nTope, enCuadroIni, enCuadroFin, enModoEdita, eaNombreList, enCampoAncho, nVari1, nVari2;
     enModoEdita = enModoEdita $ 100;
|FPRC;

|PROCESO CogePuntero;
     IepPuntEdita  = epPuntEdita1 <-;
     |PARA nTotalLinea = 0;  |SI nTotalLinea [ enTotalVaria;  |HACIENDO nTotalLinea = nTotalLinea + 1;
          |DEL_BUF nBaffer, nTotalLinea, epPuntEdita;
          IepPuntEdita = IepPuntEdita + 1;
     |SIGUE;
|FPRC;

|PROCESO Edita;
     |PUSHV 0501 2380;
     |DIMENSIONA  200, nBaffer, 0;

     |HAZ GrabaSecuencial;
     |HAZ Edicion;
     |HAZ CogePuntero;

     |FINDIM nBaffer;
     |POPV;
|FPRC;

|PROCESO observacion;
     enTotalVaria  = 100;
     eaNombreList  = "";
     enCuadroIni   = 0611;
     enCuadroFin   = 2070;
     enCampoAncho  = 60;
     enModoEdita   = 0;

     |ABRE #918;
     |PARA nVariable = 1;  |SI nVariable [ 100;  |HACIENDO nVariable = nVariable + 1;
           IepPuntEdita  = epPuntEdita1 <-;
           IepPuntEdita = IepPuntEdita + (nVariable - 1);

           #918#0 = #0#0;
           #918#1 = nVariable;
           |LIBERA #918;
           |LEE 101#918=;
           |SI FSalida ! 0;
               epPuntEdita  = "";
           |SINO;
               epPuntEdita  = #918#2;
               |BORRA 020#918a;
           |FINSI;
     |SIGUE;

     |PUSHV  0501 2380;
     |BLANCO 0811 2071;
     |ATRI R;  |PINTA 0511, "Observaciones ";  |ATRI r;

     |HAZ Edita;
     |POPV;

     |PARA nVariable = 1;  |SI nVariable [ 100;  |HACIENDO nVariable = nVariable + 1;
           IepPuntEdita = epPuntEdita1 <-;
           IepPuntEdita = IepPuntEdita + (nVariable - 1);

           |DDEFECTO #918;
           #918#0 = #0#0;
           #918#1 = nVariable;
           |LIBERA #918;
           |LEE 101#918=;
           |SI FSalida ! 0;  |GRABA 020#918b;  |FINSI;
           #918#2 = epPuntEdita;
           |GRABA 020#918a;
     |SIGUE;

     |PARA nVariable = 100;  |SI nVariable ] 1;  |HACIENDO nVariable = nVariable - 1;
           #918#0 = #0#0;
           #918#1 = nVariable;
           |LIBERA #918;
           |LEE 101#918=;
           aAlfa = #918#2;  |QBF aAlfa;

           |SI aAlfa ! "";  |SAL;  |FINSI;

           |BORRA 020#918a;
     |SIGUE;
     |CIERRA #918;
|FPRC;
