|FICHEROS;
   ascabmov #0;
   asmovtos #1;

   asmancaj #2;
   asmanmov #3;
   ascnsexp #4,MANTE;
   asexpeca #5;
   asexpeli #6;
   asconcep #7;
   asnompat #8;

   canempr@ #500;
   cacuent@ #501;
|FIN;

|VARIABLES;
   aAlfa    = "";
   Comodin  = "";
   Comodin1 = "";
   Comodin2 = "";
   Cuenta   = "";
   aLong    = "";
   aTipo    = "";
   Parte    = "";
   Part1    = "";
   Part2    = "";
   LongAlfa = "";

   ModoEntrada = 0;

   nTecla    = 0;
   MaxDigit  = 0;
   Parte1    = 0;
   Parte2    = 0;
   Long      = 0;
   nSwLleno  = 0;
   nBloque   = 0;
   nAgrupa   = 0;
   nEpigra   = 0;
   nPartid   = 0;
   Calc      = 0;
   nCont     = 0;
   nLong     = 0;
   Nivel     = 0;
   nCliente  = 0;
   &enAnyo   = 0;
   &enNumExp = 0;
   nLinea    = 0;
   &enSw     = 0;
   Flag      = 0;
   NumLinea  = 0;
|FIN;

|PROCESO SinBarra; |TIPO 5;
   FSalida = "SINBARRA";
|FPRC;

|PROCESO PintaClient; |TIPO 7;
   |PINTA 2211, " Cliente :                                  Exped.   /       ";
   |C_V #asmovtos#10,1,"S"; |PINTA #asmovtos#10;
   |C_V #asmovtos#11,1,"S"; |PINTA #asmovtos#11;
   |C_V #asmovtos#12,1,"S"; |PINTA #asmovtos#12;
|FPRC;

|PROCESO BorraMensa; |TIPO 7;
   |C_V #asmovtos#10,1,"N";
   |C_V #asmovtos#11,1,"N";
   |C_V #asmovtos#12,1,"N";
   |PINTA 2211, "                                                             ";
|FPRC;

|PROCESO Actualiza; |TIPO 2;
   #asconcep#0 = #asmovtos#4;
   #asconcep#1 = #asmovtos#5;
   |LEE 000#asconcep.=;

   Flag = 0 +. 1;
   |SI #1#13 = "S";
       |SI Flag < 0;
          |MENAV "    Esta linea esta contabilizada. No puede ser modificada";
          |ERROR;
       |FINSI;
       |ACABA;
   |FINSI;

   |SI #asmanmov#2 = "I";
      #ascabmov#2 = #ascabmov#2 +. #asmovtos#8;
   |SINO;
      #ascabmov#2 = #ascabmov#2 -. #asmovtos#8;
   |FINSI;
   |PINTA #ascabmov#2;
   #asexpeca#0  = #asmovtos#10;
   #asexpeca#14 = #asmovtos#11;
   |LEE 101#asexpeca.=;
   |SI FSalida = 0;
      |SI Flag > 0;
         #asexpeli#0  = #asexpeca#0;
         #asexpeli#16 = #asexpeca#14;
         #asexpeli#2  = 1;
         |LEE 000#asexpeli.];
         |SI FSalida = 0; |Y #asexpeli#0  = #asexpeca#0; |Y #asexpeli#16 = #asexpeca#14;
            |PARA; |SI FSalida = 0; |Y #asexpeli#0  = #asexpeca#0; |Y #asexpeli#16 = #asexpeca#14; |HACIENDO;
               NumLinea = #asexpeli#2 + 1;
               |LEE 000#asexpeli.s;
            |SIGUE;
         |FINSI;
         |DDEFECTO #asexpeli;
         #asexpeli#0  = #asexpeca#0;
         #asexpeli#16 = #asexpeca#14;
         #asexpeli#2  = NumLinea;
         |GRABA 020#asexpeli.b;
         #asexpeli#1  = #asexpeca#1;
         aAlfa = #asmovtos#1; |QBF aAlfa; aAlfa = aAlfa % 0104;
         #asexpeli#3  = aAlfa;
         #asexpeli#4  = #asmovtos#4;
         #asexpeli#5  = #asmovtos#5;
         #asexpeli#6  = #asconcep#2;
         #asexpeli#7  = #asconcep#16;
         #asexpeli#8  = #asconcep#17;
         #asexpeli#9  = #asconcep#18;
         #asexpeli#10 = #asconcep#3;
         #asexpeli#11 = 1;
         #asexpeli#12 = #asmovtos#8;
         #asexpeli#13 = #asmovtos#8;
         #asexpeli#14 = "U";
         #asexpeli#15 = "N";
         #asexpeli#17 = #asmovtos#1;
         |GRABA 020#asexpeli.a; |LIBERA #asexpeli;
         |SI #asexpeli#10 = "H";
            #asexpeca#6 = #asexpeca#6 +. #asexpeli#13;
            |SI #asexpeli#15 = "N"; #asexpeca#9 = #asexpeca#9 +. #asexpeli#13; |FINSI;
         |FINSI;
         |SI #asexpeli#10 = "S";
            #asexpeca#7 = #asexpeca#7 +. #asexpeli#13;
            |SI #asexpeli#15 = "N"; #asexpeca#10 = #asexpeca#10 +. #asexpeli#13; |FINSI;
         |FINSI;
         |SI #asexpeli#10 = "A";
            #asexpeca#8 = #asexpeca#8 +. #asexpeli#13;
            |SI #asexpeli#15 = "N"; #asexpeca#11 = #asexpeca#11 +. #asexpeli#13; |FINSI;
         |FINSI;
         |GRABA 020#asexpeca.a;
         #asmovtos#14 = NumLinea;
      |SINO;
         #asexpeli#0  = #asmovtos#10;
         #asexpeli#16 = #asmovtos#11;
         #asexpeli#2  = #asmovtos#14;
         |LEE 101#asexpeli.=;
         |SI FSalida = 0;
            |SI #asexpeli#10 = "H";
               #asexpeca#6 = #asexpeca#6 +. #asexpeli#13;
               |SI #asexpeli#15 = "N"; #asexpeca#9 = #asexpeca#9 +. #asexpeli#13; |FINSI;
            |FINSI;
            |SI #asexpeli#10 = "S";
               #asexpeca#7 = #asexpeca#7 +. #asexpeli#13;
               |SI #asexpeli#15 = "N"; #asexpeca#10 = #asexpeca#10 +. #asexpeli#13; |FINSI;
            |FINSI;
            |SI #asexpeli#10 = "A";
               #asexpeca#8 = #asexpeca#8 +. #asexpeli#13;
               |SI #asexpeli#15 = "N"; #asexpeca#11 = #asexpeca#11 +. #asexpeli#13; |FINSI;
            |FINSI;
            |GRABA 020#asexpeca.a;
            |SI Flag < 0;
               |BORRA 020#asexpeli.a;
            |FINSI;
            |LIBERA #asexpeli;
         |FINSI;
      |FINSI;
      |LIBERA #asexpeca;
   |FINSI;
|FPRC;

|PROCESO TomaSaldo;
   |ABRE #asmancaj;
   #asmancaj#0 = #ascabmov#0;
   |LEE 000#asmancaj.=;
   |SI FSalida = 0;
      #ascabmov#2 = #asmancaj#7; |PINTA #ascabmov#2;
   |FINSI;
   |CIERRA #asmancaj;
|FPRC;

|PROCESO RecogeTipo;
   ModoEntrada = (FEntrada / 100) ! 0;
   |SI ModoEntrada = 1; |O #asmovtos#3 ! Contenido;
      #asmovtos#15 = #asmanmov#1; |PINTA #asmovtos#15;
   |FINSI;
   |SI ModoEntrada = 2; |ACABA; |FINSI;
   |ABRE #asmanmov;
   #asmanmov#0 = #asmovtos#3;
   |LEE 000#asmanmov.=;
   |SI FSalida = 0;
      ||Comodin = #asmanmov#4; |QBF Comodin;
      ||C_M #asmovtos#4,1,Comodin; |C_M #asmovtos#5,1,Comodin;
      |SI #asmanmov#2 = "I";
         ||C_M #asmovtos#6,1,"N"; |C_M #asmovtos#7,1,"S";
         #asmovtos#6 = "";
         #asmovtos#7 = #asmanmov#3;
      |FINSI;
      |SI #asmanmov#2 = "G";
         ||C_M #asmovtos#6,1,"S"; |C_M #asmovtos#7,1,"N";
         #asmovtos#6 = #asmanmov#3;
         #asmovtos#7 = "";
      |FINSI;
      |PINTA #asmovtos#6; |PINTA #asmovtos#7;
   |FINSI;
   |CIERRA #asmanmov;
|FPRC;

|RUTINA inf;
   #ascnsexp#1  = nCliente;
   #ascnsexp#0  = 00;
   #ascnsexp#14 = 000001;
|FRUT;

|RUTINA sup;
   #ascnsexp#1  = nCliente;
   #ascnsexp#0  = 99;
   #ascnsexp#14 = 999999;
|FRUT;

|PROCESO SelecExped;
   |SI #asmanmov#4 = "S";
      |PUSHV 0501 2380;
      nCliente = #asmovtos#9;
      |ABRE #ascnsexp;
      enSw = -1;
      |ENTLINEAL #3#ascnsexp,-3,4,19,1,inf,sup;
      |SI enSw = 0;
         #asmovtos#10 = enAnyo;
         #asmovtos#11 = enNumExp;
         |CIERRA #ascnsexp;
      |SINO;
         |CIERRA #ascnsexp;
         |ERROR;
      |FINSI;
      |POPV;
   |FINSI;
|FPRC;

|PROCESO Tipo3; |TIPO 3;
   |ABRE #asmancaj;
   #asmancaj#0 = #ascabmov#0;
   |LEE 000#asmancaj.=;
   |SI FSalida = 0;
      #asmancaj#7 = #ascabmov#2;
      |GRABA 020#asmancaj.a;
   |FINSI;
   |CIERRA #asmancaj;

   |CIERRA #cacuent@; |DESCARGA_DEF #cacuent@;
   |CIERRA #canempr@; |DESCARGA_DEF #canempr@;
   nSwLleno  = 0;
|FPRC;

|PROCESO SacaNivel1;
   Nivel = 0;
   |QBF Cuenta; aLong = Cuenta % 0; nLong = aLong;
   |SI #canempr@#14 = nLong; Nivel = 1; |FINSI;
   |SI #canempr@#15 = nLong; Nivel = 2; |FINSI;
   |SI #canempr@#16 = nLong; Nivel = 3; |FINSI;
   |SI #canempr@#17 = nLong; Nivel = 4; |FINSI;
   |SI #canempr@#18 = nLong; Nivel = 5; |FINSI;
   |SI #canempr@#19 = nLong; Nivel = 6; |FINSI;
|FPRC;

|PROCESO Cuentas;
   |SI FSalida = 2; |ACABA; |FINSI;
   nTecla = FSalida;
   Comodin = #asmovtos#Campo#;
   Comodin = Comodin - ".";
   |SI FSalida ! 0;
      Parte = FSalida;
      Parte1 = 100 + (((Parte / 100) ! 0) - 1);
      Parte2 = ((((Parte / 100) ! 0) + 1) * 100) + 99;
      Part1 = #asmovtos#Campo# % Parte1;
      Part2 = #asmovtos#Campo# % Parte2;
      |QBF Part2;
      Comodin = Part1 + Part2;
      LongAlfa = Comodin % 0;
      Long = MaxDigit - LongAlfa;
      LongAlfa = "0" * Long;
      Comodin = Part1 + LongAlfa + Part2;
      #asmovtos#Campo# = Comodin;
      |PINTA #asmovtos#Campo#;
   |FINSI;

   |SI nTecla = 10;
      Cuenta = #asmovtos#Campo#; |HAZ SacaNivel1;
      #cacuent@#0 = Cuenta;
      #cacuent@#1 = Nivel;
      |LEE 000#cacuent@.=;
      |CONSULTA_CLAVE #1#cacuent@;
      #asmovtos#Campo# = #cacuent@#0; |PINTA #asmovtos#Campo#;
   |FINSI;
   Cuenta = #asmovtos#Campo#; |HAZ SacaNivel1;
   #cacuent@#0 = #asmovtos#Campo#;
   #cacuent@#1 = Nivel;
   |LEE 000#cacuent@.=;
   |SI FSalida ! 0;
      |PUSHV 0501 2380;
      #cacuent@#0 = #asmovtos#Campo#;
      #cacuent@#1 = Nivel;
      |PINPA #0#cacuent@; |PINDA #0#cacuent@;
      |CONFI "    NDar de alta ? ";
      |SI FSalida = 0;
         |PARA nCont = #canempr@#13; |SI nCont ] 1; |HACIENDO nCont = nCont - 1;
            Cuenta = #asmovtos#Campo#; |QBF Cuenta;
            Calc = nCont + 13;
            Calc = 100 + #canempr@#Calc#;
            Cuenta = Cuenta % Calc;
            #cacuent@#0 = Cuenta;
            #cacuent@#1 = nCont;
            |LEE 000#cacuent@.=;
            |SI FSalida = 0;
               nBloque = #cacuent@#4;
               aTipo   = #cacuent@#5;
               nAgrupa = #cacuent@#6;
               nEpigra = #cacuent@#7;
               nPartid = #cacuent@#8;
               |SAL;
            |FINSI;
         |SIGUE;
         |DDEFECTO #cacuent@;
         #cacuent@#0 = #asmovtos#Campo#;
         #cacuent@#1 = Nivel;
         |GRABA 020#cacuent@.b;
         #cacuent@#3 = "S";
         #cacuent@#4 = nBloque;
         #cacuent@#5 = aTipo;
         #cacuent@#6 = nAgrupa;
         #cacuent@#7 = nEpigra;
         #cacuent@#8 = nPartid;
         |PINDA #0#cacuent@;
         |ENDATOS #1#cacuent@;
         |SI FSalida = 0;
            |GRABA 020#cacuent@.a; |LIBERA #cacuent@;
            |POPV;
         |SINO;
            |BORRA 020#cacuent@.a;
            |MENAV "    Cuenta inexistente"; |LIBERA #cacuent@;
            |POPV; |ERROR; |ACABA;
         |FINSI;
      |SINO;
         |MENAV "    Cuenta inexistente";
         |POPV; |ERROR; |ACABA;
      |FINSI;
   |FINSI;
|FPRC;

|PROCESO ProcesaConta;
   |SI nSwLleno = 1;
      |CIERRA #cacuent@; |DESCARGA_DEF #cacuent@;
      |CIERRA #canempr@; |DESCARGA_DEF #canempr@;
   |FINSI;
   |ABRE #asnompat;
   |LEE 000#asnompat.p;
   |SI FSalida ! 0;
      |MENAV "    No esta definido el control de directorios";
      |CIERRA #asnompat; |ACABA;
   |FINSI;
   Comodin = #asnompat#2; |QBF Comodin; Comodin = Comodin - "fich/";
   Comodin1 = Comodin + "def/canempre.mas";
   |CARGA_DEF Comodin1, canempr@;
   |SI FSalida ! 0;
      |MENAV "    No encuentro el fichero de empresas de contabilidad";
      |ACABA;
   |FINSI;

   MaxDigit = 0;
   Comodin1 = Comodin + "fich/"; |PATH_DAT #500 Comodin1; |ABRE #canempr@;
   #canempr@#2 = #asmancaj#4;
   #canempr@#3 = #asmancaj#5;
   |LEE 000#canempr@.=;
   |SI FSalida = 0;
      |SI #canempr@#14 > MaxDigit; MaxDigit = #canempr@#14; |FINSI;
      |SI #canempr@#15 > MaxDigit; MaxDigit = #canempr@#15; |FINSI;
      |SI #canempr@#16 > MaxDigit; MaxDigit = #canempr@#16; |FINSI;
      |SI #canempr@#17 > MaxDigit; MaxDigit = #canempr@#17; |FINSI;
      |SI #canempr@#18 > MaxDigit; MaxDigit = #canempr@#18; |FINSI;
      |SI #canempr@#19 > MaxDigit; MaxDigit = #canempr@#19; |FINSI;
   |FINSI;

   Comodin1 = Comodin + "def/cacuenta.mas";
   |CARGA_DEF Comodin1, cacuent@;
   |SI FSalida ! 0;
      |MENAV "    No encuentro el fichero de plan de cuentas";
      |CIERRA #canempr@; |DESCARGA_DEF #canempr@;
      |ACABA;
   |FINSI;
   nSwLleno = 1;

   Comodin1 = Comodin + "fich/" + (("00000" + #asmancaj#4) % -105) + #asmancaj#5 + "/";
   Comodin2 = Comodin + "pan/";
   |PATH_DAT #501 Comodin1; |ABRE #cacuent@; |PATH_PAN #501 Comodin2;
|FPRC;

|PROGRAMA;
   |CLS;
   |CABEZA "Entrada movimientos de caja";
   |PINPA #0#ascabmov; |PINDA #0#ascabmov;
   |ABRE #asconcep; |ABRE #asexpeca; |ABRE #asexpeli;
   |PARA; |SI; |HACIENDO;
      |ENDATOS #1#ascabmov;
      |SI FSalida ! 0;
         |SAL;
      |FINSI;
   |SIGUE;
   |SI nSwLleno = 1;
      |CIERRA #cacuent@; |DESCARGA_DEF #cacuent@;
      |CIERRA #canempr@; |DESCARGA_DEF #canempr@;
   |FINSI;
   |CIERRA #asexpeli; |CIERRA #asexpeca; |CIERRA #asconcep;
|FPRO;
