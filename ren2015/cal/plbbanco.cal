-----------------------------------------------------------------------------
CALCULO IBAN
-----------------------------------------------------------------------------

|VARIABLES;
     aCta     = "";
     aIban    = "";
     aLon     = "";
     aNuevo   = "";
     aCar     = "";
     aDC      = "";
     aAlfa    = "";
     aAlfa1   = "";
     aAlfa2   = "";

     nPos     = 0;
     i        = 0;
     nCalc    = 0;
     nNume    = 0;
     nResto   = 0;

     &eaCta   = "";
     &eaIBAN =  "";
|FIN;

|PROCESO Mod9710;
     aAlfa  = aIban;
     aAlfa2 = aAlfa % 0101;

     |PARA; |SI aAlfa2 = "0"; |HACIENDO;
        aAlfa = aAlfa % 0299; aAlfa2 = aAlfa % 0101;
     |SIGUE;

     aAlfa1 = "";
     |PARA; |SI aAlfa ! ""; |HACIENDO;
        aAlfa2 = aAlfa % 0; nCalc = aAlfa2;

        |SI nCalc > 5;
           aAlfa2 = aAlfa % 0105; aAlfa = aAlfa % 0699;
        |SINO;
           aAlfa2 = aAlfa; aAlfa = "";
        |FINSI;

        aAlfa1 = aAlfa1 + aAlfa2;
        nCalc  = aAlfa1;
        nCalc  = nCalc / 97;
        aAlfa2 = nCalc;
        aAlfa2 = aAlfa2 - ".";
        nCalc  = 0;

       |SI FSalida ! 0;
           nCalc  = FSalida + 98;
           aAlfa2 = aAlfa2 % nCalc;
           aAlfa2 = "0." + aAlfa2; nCalc = aAlfa2;
        |SINO;
        |FINSI;
        nCalc  = (nCalc * 97) ! 0;
        aAlfa1 = nCalc;
     |SIGUE;

     nCalc  = aAlfa1;
     nCalc  = 98 - nCalc;
     aDC    = ("00" + nCalc) % -102;
|FPRC;

|PROCESO CambiaLetra;
     aNuevo = "";
     |PARA i = 1; |SI i [ 24; |HACIENDO i = i + 1;
          nPos = i * 100 + 1;
          aCar = aIban % nPos;
          |SI aCar ] "0"; |Y aCar [ "9";
               aNuevo = aNuevo + aCar;
          |SINO;
               nNume = &aCar; nNume = nNume - 55; aCar = nNume;
               aNuevo = aNuevo + aCar;
          |FINSI;
     |SIGUE;

     aIban = aNuevo;
|FPRC;

|PROCESO CalcuIBAN;
     eaIBAN = "";
     aCta   = eaCta; |QBF aCta;

     nNume = aCta;
     aLon  = aCta % A0;
     |SI aLon = 20; |Y nNume ! 0;
          aIban = aCta + "ES" + "00";
          |HAZ CambiaLetra;
          |HAZ Mod9710;
          eaIBAN = "ES" + aDC + aCta;
     |FINSI;
|FPRC;
