|FICHEROS;
     rew00202 #202;

     recofisc #2;
|FIN;

|VARIABLES;
     nCampo1      = 0;

     aAlfa        = "";
     aAlfa1       = "";
     aAlfa2       = "";
     aAlfa3       = "";
     aComa        = "";

     nCampo      = 0;
     nLCod       = 0;
     nLDes       = 0;
     nCalc       = 0;

     fHoy        = @;

     &eaTabla    = "";
     &eaDestino  = "";
     &eaAlfa     = "";
     &enPosicion = 0;
     &enComp     = 0;
     &enError    = 0;
     &enEmpresa  = 0;
     &eaVarDni   = "";
|FIN;

|PROCESO Tipo12Fw202;  |TIPO 12;
     |HAZ Tipo7C5Fw202;

     |SI #202#14 = "N";
         aAlfa = #202#16;
     |SINO;
         aAlfa = #202#31;
     |FINSI;

     |QBF aAlfa;

     |SI aAlfa = "";
         |MENAV "0000 Introduzca datos del domicilio habitual";
         |ERROR;
     |FINSI;
|FPRC;

|PROCESO Tipo6C0Fw202;  |TIPO 6;
     |SI FSalida ! 10;  |ACABA;  |FINSI;

     eaVarDni = #202Campo;
     |HAZ CalculoDNI;
     #202Campo = eaVarDni;
     |PINTA #202Campo;
     |ERROR;
|FPRC;

|PROCESO Tipo7C5Fw202;  |TIPO 7;
     aAlfa1 = #202#2;  |QBF aAlfa1;
     aAlfa2 = #202#3;  |QBF aAlfa2;
     aAlfa3 = #202#4;  |QBF aAlfa3;
     |SI aAlfa1 ! "";  |O aAlfa2 ! "";  |O aAlfa3 ! "";
         aComa = ",";
     |SINO;
         aComa = "";
     |FINSI;

     aAlfa  = aAlfa1 + " " + aAlfa2 + aComa + " " + aAlfa3;
     #202#1 = aAlfa;
     |PINTA #202#1;
|FPRC;

|PROCESO Tipo0C6Fw202a;
     |C_M #202#10, 1, "S";
     |SI #202#6 ! "L"; |Y #202#6 ! "S";
         #202#10 = " ";  |PINTA #202#10;
         |C_M #202#10, 1, "N";
     |FINSI;
|FPRC;

|PROCESO PonSiglo20_202;
     aAlfa1    = (#202Campo % 106) + "19" + (#202Campo % -102);
     #202Campo = aAlfa1;
     |PINTA #202Campo;
|FPRC;

|PROCESO Tipo0C7Fw202;
     |SI #202Campo = "01.01.0000";  |ACABA;  |FINSI;

     fHoy = ~;

     |SI #202Campo > fHoy;        || nacimientos futuros como que no
          |HAZ PonSiglo20_202;    ||/se cambia sin preguntar
     |FINSI;

     |SI #202Campo ] "01.01.2000"; |Y #202Campo [ fHoy;     || presuntos ni¤os,
                                                            ||/se pregunta
         aAlfa = "0000SLa fecha puede ser incorrecta. Ha introducido un contribuyente menor de edad. Corregir siglo?";
         |CONFI aAlfa;
         |SI FSalida = 0;
              |HAZ PonSiglo20_202;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO Tipo0C8Fw202;
     nCampo1 = Campo + 1;
     |C_M #202nCampo1, 1, "S";
     |SI #202Campo = 0;
         |C_M #202nCampo1, 1, "N";
         #202nCampo1 = "N";  |PINTA #202nCampo1;
     |FINSI;
|FPRC;

|PROCESO Tipo0C11Fw202;
     |C_M #202#13, 1, "S";

     aAlfa = #202#11;  |QBF aAlfa;
     |SI aAlfa = "";
         |C_M #202#13, 1, "N";  #202#13 = "N";  |PINTA #202#13;
     |FINSI;
|FPRC;

|PROCESO CampoObligaF202; |TIPO 6;
     |C_M #202Campo, 0, aAlfa;
     |SI aAlfa = "N";  |ACABA;  |FINSI;

     aAlfa = #202Campo;  |QBF aAlfa;

     |SI aAlfa = "";
         |MENAV "0000Contenido obligatorio. ";
         |ERROR;
         |ACABA;
     |FINSI;
|FPRC;

|PROCESO Tipo0C17Fw202; |TIPO 0;
     |C_M #202#17, 0, aAlfa;
     |SI aAlfa = "N";  |ACABA;  |FINSI;

     |SI #202#17 ! "NUM";  |Y #202#17 ! "KM.";  |Y #202#17 ! "S/N";
      |Y #202#17 ! "OTR";  |Y #202#17 ! "   ";
         |MENAV "0000Contenido incorrecto.";
         |ERROR;
         |ACABA;
     |FINSI;

     |C_M #202#18, 1, "S";
     |SI #202#17 = "S/N";
         |C_M #202#18, 1, "N";   #202#18 = "0000";  |PINTA #202#18;
     |FINSI;
|FPRC;

|PROCESO Tipo7C15Fw202; |TIPO 7;
     |C_M #202#15, 0, aAlfa;
     |SI aAlfa = "N";  |ACABA;  |FINSI;

     eaTabla = "zsiglas.txt";
     nLCod   = 5;
     nLDes   = 15;
     |HAZ CargaTabla;
|FPRC;

|PROCESO Tipo66C15Fw202;  |TIPO 66;
     nCampo   = Campo + 1;

     |HAZ SacaTabla;
     nCalc = 100 + nLCod;
     aAlfa1   = eaDestino % nCalc;
     #202Campo  = aAlfa1;
     |PINTA #202Campo;
|FPRC;

|PROCESO Tipo0C15Fw202;  |TIPO 0;
     |C_M #202#15, 0, aAlfa;
     |SI aAlfa = "N";  |ACABA;  |FINSI;

     nCampo   = Campo + 1;

     |SI #202Campo = "     ";
         |MENAV "0000 Contenido Incorrecto. Consulta la Tabla con F2";
         |ERROR; |ACABA;
     |FINSI;

     enComp = nLCod;
     aAlfa  = "|C" + (("000" + Campo) % -103) + "TIPO";
     aAlfa1 = #202#aAlfa#;
     aAlfa  = #202Campo;   |QBF aAlfa;

     |SI aAlfa1 = "N";
         nCalc = - (100 + nLCod);
         aAlfa = (("0" * enComp) + aAlfa) % nCalc;
     |FINSI;

     |SI aAlfa1 ! "N";
         nCalc = 100 + nLCod;
         aAlfa = (aAlfa + (" " * enComp)) % nCalc;
     |FINSI;
     eaAlfa = aAlfa;
     |HAZ CompruebaTabla;

     |SI enError = 1;
         |MENAV "0000 Contenido Incorrecto. Consulta la Tabla con F2";
         |ERROR; |ACABA;
     |FINSI;

     nCalc      = 100 + nLCod;
     aAlfa1     = eaDestino % nCalc;
     nCalc      = ((nLCod + 2) * 100) + nLDes;
     aAlfa2     = eaDestino % nCalc;
     #202Campo  = aAlfa1;

     |PINTA #202Campo;

     |HAZ FinTabla;
|FPRC;

|PROCESO Tipo0C14Fw202;  |TIPO 0;
     |PARA nCampo = 15;  |SI nCampo [ 41;  |HACIENDO nCampo = nCampo + 1;
           |C_M #202nCampo, 1, "S";
     |SIGUE;

     |C_M #202#30, 1, "N";
     |C_M #202#40, 1, "N";

     |SI #202#14 = "N";
         |PARA nCampo = 31;  |SI nCampo [ 38;  |HACIENDO nCampo = nCampo + 1;
               |C_M #202nCampo, 1, "N";  #202nCampo = "";  |PINTA #202nCampo;
         |SIGUE;
     |SINO;
         aAlfa1 = #202#16;   |QBF aAlfa1;
         aAlfa2 = #202#31;   |QBF aAlfa2;

         |SI aAlfa2 = "";
             |SI aAlfa1 ! "";
                 aAlfa = #202#15;  |QBF aAlfa;
                 |SI aAlfa ! "";  aAlfa = aAlfa + " ";  |FINSI;
                 aAlfa = aAlfa + #202#16;   |QBF aAlfa;
                 |SI #202#18 ! "    ";  aAlfa = aAlfa + " " + #202#18;   |FINSI;
                 |SI #202#22 ! "   ";   aAlfa = aAlfa + " " + #202#22;   |FINSI;
                 |SI #202#23 ! "  ";    aAlfa = aAlfa + " " + #202#23;   |FINSI;
                 |SI #202#24 ! "  ";    aAlfa = aAlfa + " " + #202#24;   |FINSI;

                 #202#31 = aAlfa;    |PINTA #202#31;
                 #202#32 = #202#25;  |PINTA #202#32;
                 #202#33 = #202#29;  |PINTA #202#33;
                 #202#35 = (("00" + #202#27) % -102) + (("000" + #202#28) % -103);  |PINTA #202#35;
                 #202#36 = #202#30;  |PINTA #202#36;
             |FINSI;
         |FINSI;

         |PARA nCampo = 15;  |SI nCampo [ 30;  |HACIENDO nCampo = nCampo + 1;
               |C_M #202nCampo, 1, "N";  #202nCampo = "";  |PINTA #202nCampo;
         |SIGUE;
     |FINSI;
|FPRC;

|PROCESO Tipo0C27Fw202;
     |ABRE #2;
     #2#0 = #202#27;
     |LEE 040#2=;
     |SI FSalida ! 0;  |DDEFECTO #2;  |FINSI;
     |CIERRA #2;

     #202#39 = #2#3;
     #202#40 = #2#4;

     |PINTA #202#39;
     |PINTA #202#40;
|FPRC;
