|FICHEROS;
     rem00901;
|FIN;

|VARIABLES;
     aCodigo1   = "";
     aCodigo2   = "";
     aCodigo3   = "";
     aCodigo4   = "";
     aContenido = "";
     aCaracter  = "";
     aLineaMenu = "";
     aAlfa      = "";
     aAlfa1     = "";
     aAlfa2     = "";
     aAbre      = "";
     aPrograma  = "";
     aRetu      = "";
     aEsc1      = "";
     aEsc2      = "";
     aTecla     = "";

     nCodigos   = 0;
     nCodigo1   = 0;
     nCodigo2   = 0;
     nCodigo3   = 0;
     nPos       = 0;
     nPosi      = 0;
     nRango     = 0;
     nBoton1    = 0;
     nHandle    = 0;
     nLong      = 0;
     nCaracter  = 0;
     nLLaves    = 0;
     nGrid      = 0;
     nVent      = 0;
|FIN;

|PROGRAMA;
     |CONSULTA_CLAVE #1#rem00901;
|FPRO;

|PROCESO RecogeDatos;
     aAlfa = "/u/dsprofes/ren2015/cogetxt/rem00901.txt";
     |XABRE "",  aAlfa,  nHandle;

     |PARA;  |SI;  |HACIENDO;
          |XLEE nHandle, 255, aLineaMenu;
          |SI FSalida [ 0;  |SAL;  |FINSI;

          aCodigo1   = "";
          aCodigo2   = "";
          aCodigo3   = "";
          aCodigo4   = "";
          aPrograma  = "";
          nCodigos   = 1;
          nPos       = 1;
          nLLaves    = 0;
          aContenido = "";
          |PARA;  |SI;  |HACIENDO nPos = nPos + 1;
               nPosi = (nPos * 100) + 1;

               aCaracter = aLineaMenu % nPosi;

               |SI aCaracter = "}";  |SAL;  |FINSI;

               |SI aCaracter = ":";
                   nCodigos = 0;
               |FINSI;

               |SI aCaracter = "{";
                   nLLaves   = nLLaves + 1;
               |FINSI;

               |SI aCaracter = ",";  |Y nCodigos ! 0;
                   nCodigos = nCodigos + 1;
               |FINSI;

               |SI nCodigos = 1;  |Y aCaracter ! ",";
                   aCodigo1 = aCodigo1 + aCaracter;
               |FINSI;

               |SI nCodigos = 2;  |Y aCaracter ! ",";
                   aCodigo2 = aCodigo2 + aCaracter;
               |FINSI;

               |SI nCodigos = 3;  |Y aCaracter ! ",";
                   aCodigo3 = aCodigo3 + aCaracter;
               |FINSI;

               |SI nCodigos = 4;  |Y aCaracter ! ",";
                   aCodigo4 = aCodigo4 + aCaracter;
               |FINSI;

               |SI nCodigos = 0;  |Y nLLaves = 0;  |Y aCaracter ! ":";
                   aContenido = aContenido + aCaracter;
               |FINSI;

               |SI nLLaves = 2;  |Y aCaracter ! "{";
                   aPrograma = aPrograma + aCaracter;
               |FINSI;
          |SIGUE;

          |DDEFECTO #rem00901;
          #rem00901#0 = aCodigo1;
          #rem00901#1 = aCodigo2;
          #rem00901#2 = aCodigo3;
          #rem00901#3 = aCodigo4;
          #rem00901#4 = aContenido;
          #rem00901#5 = aPrograma;
          |GRABA 020#rem00901.n;
          |LIBERA #rem00901;
     |SIGUE;
     |XCIERRA nHandle;
|FPRC;

|PROCESO rem00901;
     aAlfa      = "";
     aContenido = #rem00901#5;  |QBF aContenido;

                      aAlfa2 = "{{" + aContenido + "}}";
                      aAlfa1 = #rem00901#0;
     |SI #rem00901#1 ! 0;  aAlfa1 = aAlfa1 + "," + #rem00901#1;  |FINSI;
     |SI #rem00901#2 ! 0;  aAlfa1 = aAlfa1 + "," + #rem00901#2;  |FINSI;
     |SI #rem00901#3 ! 0;  aAlfa1 = aAlfa1 + "," + #rem00901#3;  |FINSI;

     aAlfa1 = aAlfa1 + ":" + #rem00901#4;  |QBF aAlfa1;
     aAlfa1 = aAlfa1 + " " + aAlfa + " " + aAlfa2;  |QBF aAlfa1;

     aAlfa  = aAlfa1 % 0;
     nLong  = aAlfa;
     |PARA nCaracter = nLong;  |SI nCaracter [ 148;  |HACIENDO nCaracter = nCaracter + 1;
           aAlfa1  = aAlfa1 + " ";
     |SIGUE;

     aAlfa1 = aAlfa1 + &13 + &10;
     |XGRABA nHandle, aAlfa1;
|FPRC;

|DEFBUCLE rem00901;
     #rem00901#1;
     ;
     INICIO;
     FINAL;
     ;
     NULL, rem00901;
|FIN;

|PROCESO HazElTxt;
     aAbre = "/u/dsprofes/ren2015/cogetxt/rem00901.txt";

     |XABRE "WB", aAbre, nHandle;
     |BUCLE rem00901;
     |XCIERRA nHandle;
|FPRC;

|PROCESO Tipo80Fm901;  |TIPO 80;
     ||HAZ HazElTxt;

     |ABRET #rem00901;
     |LEE 000#rem00901.p;
     |SI FSalida ! 0;
         |HAZ RecogeDatos;
     |FINSI;

     FSalida = 2799;
|FPRC;

|PROCESO Tipo90Fm901;  |TIPO 90;
     |CIERRAT #rem00901;
|FPRC;
