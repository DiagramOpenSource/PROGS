|FICHEROS;
    reconenl #00;
    reconfac #01;
    ascabmin #02;
    aslinmin #03;
    asclient #04;
    reliquid #05;
    redatcon #06;
    repoblac #07;
    ascontro #08;
    asconcep #09;
    asconcli #10;
    reconhis #20;
    recontar #21;
    reconcod #22;
|FIN;

|VARIABLES;
    alfa1 = "";
    alfa2 = "";
    alfa3 = "";
    alfa4 = "";
    alfa5 = "";
    nume1 = 0;
    nume2 = 0;
    nume3 = 0;
    nume4 = 0;
    fech1 = @;
    fech2 = @;
    fech3 = @;
    fech4 = @;
    fuera = 0;
    path = "";
    direccion = "";
    seccion = "";
    concepto = "";
    fecha = 0;
    honorar = 0;
    suplido = 0;
    descuen = 0;
    bimponi = 0;
    tot_iva = 0;
    tot_fac = 0;
    tot_ret = 0;
    tot_pag = 0;
    serie = "";
    factura = 0;
    iva = 0;
    reten = 0;
    tipo = "";
    uni_glo = "";
    descri1 = "";
    descri2 = "";
    descri3 = "";
    descri4 = "";
    ptas = 0;
    linea = 0;
    putada = "";
    codigo = 0;
    d_clien = 0;
    h_clien = 0;
    xxx     = 0;
|FIN;

|PROCESO parrilla; |TIPO 0;
|SI #0#35 = "N";
    |PARA xxx = 5; |SI xxx [ 34; |HACIENDO xxx = xxx + 1;
          #0xxx = 0; |PINTA #0xxx;
    |SIGUE;
    |CAMPO_MODIFICA #0#05, 1, "N";  |CAMPO_MODIFICA #0#06, 1, "N";
    |CAMPO_MODIFICA #0#07, 1, "N";  |CAMPO_MODIFICA #0#08, 1, "N";
    |CAMPO_MODIFICA #0#09, 1, "N";  |CAMPO_MODIFICA #0#10, 1, "N";
    |CAMPO_MODIFICA #0#11, 1, "N";  |CAMPO_MODIFICA #0#12, 1, "N";
    |CAMPO_MODIFICA #0#13, 1, "N";  |CAMPO_MODIFICA #0#14, 1, "N";
    |CAMPO_MODIFICA #0#15, 1, "N";  |CAMPO_MODIFICA #0#16, 1, "N";
    |CAMPO_MODIFICA #0#17, 1, "N";  |CAMPO_MODIFICA #0#18, 1, "N";
    |CAMPO_MODIFICA #0#19, 1, "N";  |CAMPO_MODIFICA #0#20, 1, "N";
    |CAMPO_MODIFICA #0#21, 1, "N";  |CAMPO_MODIFICA #0#22, 1, "N";
    |CAMPO_MODIFICA #0#23, 1, "N";  |CAMPO_MODIFICA #0#24, 1, "N";
    |CAMPO_MODIFICA #0#25, 1, "N";  |CAMPO_MODIFICA #0#26, 1, "N";
    |CAMPO_MODIFICA #0#27, 1, "N";  |CAMPO_MODIFICA #0#28, 1, "N";
    |CAMPO_MODIFICA #0#29, 1, "N";  |CAMPO_MODIFICA #0#30, 1, "N";
    |CAMPO_MODIFICA #0#31, 1, "N";  |CAMPO_MODIFICA #0#32, 1, "N";
    |CAMPO_MODIFICA #0#33, 1, "N";  |CAMPO_MODIFICA #0#34, 1, "N";
    |CAMPO_MODIFICA #0#00, 1, "S";  |CAMPO_MODIFICA #0#01, 1, "S";
|SINO;
    #0#0 = 1;     |PINTA #0#0;
    #0#1 = 99999; |PINTA #0#1;

    |CAMPO_MODIFICA #0#05, 1, "S";  |CAMPO_MODIFICA #0#06, 1, "S";
    |CAMPO_MODIFICA #0#07, 1, "S";  |CAMPO_MODIFICA #0#08, 1, "S";
    |CAMPO_MODIFICA #0#09, 1, "S";  |CAMPO_MODIFICA #0#10, 1, "S";
    |CAMPO_MODIFICA #0#11, 1, "S";  |CAMPO_MODIFICA #0#12, 1, "S";
    |CAMPO_MODIFICA #0#13, 1, "S";  |CAMPO_MODIFICA #0#14, 1, "S";
    |CAMPO_MODIFICA #0#15, 1, "S";  |CAMPO_MODIFICA #0#16, 1, "S";
    |CAMPO_MODIFICA #0#17, 1, "S";  |CAMPO_MODIFICA #0#18, 1, "S";
    |CAMPO_MODIFICA #0#19, 1, "S";  |CAMPO_MODIFICA #0#20, 1, "S";
    |CAMPO_MODIFICA #0#21, 1, "S";  |CAMPO_MODIFICA #0#22, 1, "S";
    |CAMPO_MODIFICA #0#23, 1, "S";  |CAMPO_MODIFICA #0#24, 1, "S";
    |CAMPO_MODIFICA #0#25, 1, "S";  |CAMPO_MODIFICA #0#26, 1, "S";
    |CAMPO_MODIFICA #0#27, 1, "S";  |CAMPO_MODIFICA #0#28, 1, "S";
    |CAMPO_MODIFICA #0#29, 1, "S";  |CAMPO_MODIFICA #0#30, 1, "S";
    |CAMPO_MODIFICA #0#31, 1, "S";  |CAMPO_MODIFICA #0#32, 1, "S";
    |CAMPO_MODIFICA #0#33, 1, "S";  |CAMPO_MODIFICA #0#34, 1, "S";
    |CAMPO_MODIFICA #0#00, 1, "N";  |CAMPO_MODIFICA #0#01, 1, "N";
|FINSI;
|FPRC;

|PROCESO control;        || lee el control y construye 'path'
    fuera = 0;
|ABRE #1;
|LEE 001#1p;
|SI FSalida ! 0;
    |MENAV "    ATENCION!!! Control de 'AGIREN' sin definir ...";
        fuera = 1;
|SINO;
        putada = #1#0;   |QBT putada;
    |SI putada ! "*";
            alfa1 = #1#1;  alfa1 = "00" + alfa1;  alfa1 = alfa1 % -102;
            path = putada + alfa1 + "/";
        |PATH_DAT #8 path;
        |ABRE #8;
        |LEE 001#8p;
        |SI FSalida ! 0;
            |MENAV "    ATENCION!!! Control de 'FACGES' sin definir ...";
                fuera = 1;
        |SINO;
            |SI #0#3=0; serie=#8#01; factura=#8#11; iva=#8#21; |FINSI;
            |SI #0#3=1; serie=#8#02; factura=#8#12; iva=#8#22; |FINSI;
            |SI #0#3=2; serie=#8#03; factura=#8#13; iva=#8#23; |FINSI;
            |SI #0#3=3; serie=#8#04; factura=#8#14; iva=#8#24; |FINSI;
            |SI #0#3=4; serie=#8#05; factura=#8#15; iva=#8#25; |FINSI;
            |SI #0#3=5; serie=#8#06; factura=#8#16; iva=#8#26; |FINSI;
            |SI #0#3=6; serie=#8#07; factura=#8#17; iva=#8#27; |FINSI;
            |SI #0#3=7; serie=#8#08; factura=#8#18; iva=#8#28; |FINSI;
            |SI #0#3=8; serie=#8#09; factura=#8#19; iva=#8#29; |FINSI;
            |SI #0#3=9; serie=#8#10; factura=#8#20; iva=#8#30; |FINSI;
                reten = #8#31;
        |FINSI;
        |CIERRA #8;
    |SINO;
        |MENAV "    ATENCION!!! Control para facturacion directa ...";
            fuera = 1;
    |FINSI;
|FINSI;
|CIERRA #1;
|FPRC;

|PROCESO conceptos;
|PATH_DAT #9 path;
|ABRE #9;
    #9#0 = seccion;
    #9#1 = concepto;
|LEE 001#9=;                  || conceptos
|SI FSalida ! 0;
    |DDEFECTO #9;
|FINSI;
    descri1 = #9#2;
    descri2 = #9#16;
    descri3 = #9#17;
    descri4 = #9#18;
|SI #4#22 = 1; ptas = #9#04;  uni_glo = #9#05;    |FINSI;
|SI #4#22 = 2; ptas = #9#08;  uni_glo = #9#09;    |FINSI;
|SI #4#22 = 3; ptas = #9#10;  uni_glo = #9#11;    |FINSI;
|SI #4#22 = 4; ptas = #9#12;  uni_glo = #9#13;    |FINSI;
|SI #4#22 = 5; ptas = #9#14;  uni_glo = #9#15;    |FINSI;
    tipo = #9#3;
|CIERRA #9;

|PATH_DAT #10 path;
|ABRE #10;
    #10#0 = seccion;
    #10#1 = concepto;
    #10#2 = #4#0;
|LEE 001#10=;                 || conceptos/clientes
|SI FSalida = 0;
        descri1 = #10#6;
        descri2 = #10#7;
        descri3 = #10#8;
        descri4 = #10#9;
        tipo = #10#3;
        ptas = #10#10;
        uni_glo = #10#5;
|FINSI;
|CIERRA #10;
|FPRC;

|PROCESO leehistor;
|ABRE #20;
    #20#0 = #5#0;
|LEE 001#20=;
|SI FSalida = 0;|Y #20#10 = "D";
        fuera = 1;
|FINSI;
|CIERRA #20;
|FPRC;

|PROCESO leecontri;
    #6#0 = #5#0;     || lee el contribuyente
|LEE 001#6=;
|SI FSalida ! 0;  |DDEFECTO #6;  |FINSI;
    alfa1 = #6#06 + "/" + #6#07;   |QBF alfa1;    || cl+calle
    alfa2 = #6#08;                 |QBT alfa2;    || numero
    alfa3 = #6#09;                 |QBT alfa3;    || escalera
    alfa4 = #6#10;                 |QBT alfa4;    || piso
    alfa5 = #6#11;                 |QBT alfa5;    || puerta
    direccion = alfa1+" "+alfa2+" "+alfa3+" "+alfa4+" "+alfa5;
|FPRC;

|PROCESO provincia;
|ABRE #7;
    #7#0 = #6#12;
    #7#1 = #6#13;
|LEE 001#7=;
|SI FSalida ! 0;  |DDEFECTO #7;  |FINSI;
|CIERRA #7;
|FPRC;

|PROCESO fecha;
    alfa1 = #0#2;
    alfa2 = alfa1 % 102;
    alfa3 = alfa1 % 402;
    alfa4 = alfa2 + alfa3;
    fecha = alfa4;                 || fecha en formato 'DDMM'
|FPRC;

|PROCESO lee_tabla;
|ABRE #22;
    #22#0 = #5#0;
|LEE 001#22=;
|SI FSalida = 0;
        codigo = #22#2;
|SINO;
        codigo = #5#0;
|FINSI;
|CIERRA #22;
|FPRC;

|PROCESO alta_cli;
|HAZ lee_tabla;
    #4#0 = codigo;
|LEE 001#4=;             || lee el cliente de 'facges'
|SI FSalida ! 0;
    |DDEFECTO #4;
        #4#00 = codigo;  || codigo
        #4#01 = #6#1;    || razon social
        #4#02 = #6#1;    || nombre
        #4#03 = direccion;    || esta claro!
        #4#04 = #6#12;   || codigo provincia
        #4#05 = #6#13;   || codigo poblacion
        #4#06 = #6#14;   || poblacion
        #4#07 = #7#2;    || provincia
        #4#08 = #6#2;    || cif/nif
        #4#09 = #6#15;   || telefono
        #4#10 = "";      || fax
        #4#11 = "";      || banco
        #4#12 = "";      || direccion banco
        #4#13 = "";      || ccc
        #4#14 = #6#175;  || cuenta corriente
        #4#15 = 0;       || forma de pago
        #4#16 = 0;       || banco a remesar
        #4#17 = #21#1;   || retencion IRPF
        #4#18 = 0;       || ptas. honorarios per.
        #4#19 = 0;       || ptas. suplido per.
        #4#20 = 0;       || ptas. honorarios eje.
        #4#21 = 0;       || ptas. suplido eje.
        #4#22 = #21#5;   || tarifa
        #4#23 = 0;       || descuento
    |GRABA 020#4n;
|FINSI;
|FPRC;

|PROCESO tarifas;
|ABRE #21;
    #21#0 = #6#102;
|LEE 021#21=;
|SI FSalida ! 0;
    |DDEFECTO #21;
|FINSI;
|CIERRA #21;
|FPRC;

|PROCESO null;
|FPRC;

|PROCESO borra_min;      || borra la minuta
     #2#0 = #0#3;
     #2#1 = codigo;
|LEE 001#2=;
|SI FSalida = 0;
    |BUCLELIN 1null#2;
    |BORRA 020#2a;
|FINSI;
|FPRC;

|PROCESO calcula;
    linea = 0;
|SI #21#2 = "S";     || honorarios
        linea = linea + 1;
        seccion = #21#3;
        concepto = #21#4;
    |HAZ conceptos;
    |HAZ gra_linmin;
|FINSI;
|SI #21#6 = "S";     || suplidos
        linea = linea + 1;
        seccion = #21#7;
        concepto = #21#8;
    |HAZ conceptos;
        ptas = 0;
    |SI #21#09="S"; ptas=ptas+#5#08+#5#09+#5#10+#5#11+#5#12+#5#13+#5#14; |FINSI;
    |SI #21#10="S"; ptas=ptas+#5#15+#5#16+#5#17+#5#18+#5#19+#5#20+#5#21; |FINSI;
    |SI #21#11="S"; ptas=ptas+#5#35+#5#36+#5#37+#5#38+#5#39+#5#40;       |FINSI;
    |HAZ gra_linmin;
|FINSI;
|SI linea ! 0;
        descuen = honorar % #4#23;     descuen = descuen ! 0;
        bimponi = honorar-descuen;
        tot_iva = bimponi % iva;       tot_iva = tot_iva ! 0;
        tot_fac = bimponi + suplido + tot_iva;
    |SI #4#17 = "S";
            tot_ret = bimponi % reten;     tot_ret = tot_ret ! 0;
    |SINO;
            tot_ret = 0;
    |FINSI;
        tot_pag = tot_fac - tot_ret;
|FINSI;
|FPRC;

|PROCESO gra_linmin;
    #3#00 = #0#3;        || tipo
    #3#01 = #4#0;        || codigo
    #3#02 = linea;       || linea
    #3#03 = fecha;       || fecha
    #3#04 = seccion;     || seccion
    #3#05 = concepto;    || concepto
    #3#06 = descri1;     || descrip. abreviada
    #3#07 = descri2;     || descrip. 1
    #3#08 = descri3;     || descrip. 2
    #3#09 = descri4;     || descrip. 3
    #3#10 = tipo;        || tipo
    #3#11 = 1;           || unidades
    #3#12 = ptas;        || ptas.
    #3#13 = #3#11*#3#12; || importe
    #3#14 = uni_glo;     || unitario/global
|GRABA 020#3n;

|SI #3#10 = "H";  honorar = honorar + #3#13;  |FINSI;
|SI #3#10 = "S";  suplido = suplido + #3#13;  |FINSI;
|FPRC;

|PROCESO gra_cabmin;          || graba la cabecera
    #2#00 = #0#3;        || tipo
    #2#01 = #4#0;        || codigo
    #2#02 = #4#1;        || razon social
    #2#03 = #4#2;        || nombre
    #2#04 = #4#3;        || direccion
    #2#05 = #4#4;        || codigo provincia
    #2#06 = #4#5;        || codigo poblacion
    #2#07 = #4#6;        || poblacion
    #2#08 = #4#7;        || provincia
    #2#09 = #4#8;        || cif
    #2#10 = #4#15;       || forma de pago
    #2#11 = #4#16;       || banco a remesar
    #2#12 = #4#17;       || retencion IRPF S/N
    #2#13 = #4#11;       || banco
    #2#14 = #4#12;       || direccion banco
    #2#15 = #4#13;       || ccc
    #2#16 = #4#14;       || cta. cte.
    #2#17 = honorar;     || ptas. honorarios
    #2#18 = suplido;     || ptas suplido
    #2#19 = 0;           || a cuenta
    #2#20 = #4#23;       || descuento
    #2#21 = descuen;     || total descuento
    #2#22 = bimponi;     || base imponible
    #2#23 = tot_iva;     || total iva
    #2#24 = tot_fac;     || total factura
    #2#25 = tot_ret;     || total retencion
    #2#26 = tot_pag;     || total a pagar
    #2#27 = serie;       || serie
    #2#28 = factura;     || factura
    #2#29 = #0#2;        || fecha factura
|GRABA 020#2n;
|FPRC;

|PROCESO limpia;
    factura = factura + 1;
    honorar = 0;
    suplido = 0;
|FPRC;

|PROCESO gra_histor;
|ABRE #20;
    #20#0 = #5#0;
|LEE 001#20=;
|SI FSalida = 0;   |BORRA 020#20a;   |FINSI;
|DDEFECTO #20;
    #20#00 = #5#0;       || codigo
    #20#01 = #6#102;     || tarifa
    #20#02 = #0#2;       || fecha
    #20#03 = #21#3;      || seccion honorarios
    #20#04 = #21#4;      || concepto honorarios
    #20#05 = #21#7;      || seccion suplidos
    #20#06 = #21#8;      || concepto suplidos
    #20#07 = honorar;    || ptas. honorarios
    #20#08 = suplido;    || ptas. suplidos
    #20#09 = tot_iva;    || ptas iva
    #20#10 = "E";        || enlazado (E)
|GRABA 020#20n;
|CIERRA #20;
|FPRC;

|PROCESO pasando;
    #0#4 = #5#0;
|PINTA #0#4;
    fuera = 0;
|HAZ leehistor;
|SI fuera = 0;
    |HAZ leecontri;
    |SI #6#142 = "S";|Y #6#102 ! "  ";  || solo si esta preparada y tiene
        |HAZ provincia;                 || /tarifa
        |HAZ tarifas;
        |SI #21#0 = #6#102;        || si no esta la tarifa
            |HAZ limpia;
            |HAZ alta_cli;
            |HAZ borra_min;
            |HAZ calcula;
            |SI linea ! 0;
                |HAZ gra_cabmin;
                |HAZ gra_histor;
            |FINSI;
        |FINSI;
    |FINSI;
|FINSI;
|FPRC;

|DEFBUCLE lee_liqui;
 #5#1;
 ;
 d_clien;
 h_clien;
 e;
 NULL, pasando;
|FIN;

|PROCESO enlace; |TIPO 3;
|HAZ control;
|SI fuera = 0;
    |PATH_DAT #2 path;   || cabs. minutas
    |PATH_DAT #3 path;   || lins. minutas
    |PATH_DAT #4 path;   || clientes
    |ABRET #2;
    |ABRET #3;
    |ABRET #4;
    |ABRET #6;
    |HAZ fecha;

    |SI #0#35 = "N";
        d_clien = #0#0;
        h_clien = #0#1;
        |BUCLE lee_liqui;
    |SINO;
        |PARA xxx = 5; |SI xxx [ 34; |HACIENDO xxx = xxx + 1;
              |SI #0xxx ! 0;
                  d_clien = #0xxx;
                  h_clien = #0xxx;
                  |BUCLE lee_liqui;
              |FINSI;
        |SIGUE;
    |FINSI;

    |HAZ gra_con;
    |CIERRAT #2;
    |CIERRAT #3;
    |CIERRAT #4;
    |CIERRAT #6;
|FINSI;
|FPRC;

|PROCESO gra_con;        || actualiza control de 'facges'
|PATH_DAT #8 path;
|ABRE #8;
|LEE 121#8p;        || da todos los mensajes (tiene que estar)
|SI FSalida = 0;
    |SI #0#3=0; #8#11 = factura; |FINSI;
    |SI #0#3=1; #8#12 = factura; |FINSI;
    |SI #0#3=2; #8#13 = factura; |FINSI;
    |SI #0#3=3; #8#14 = factura; |FINSI;
    |SI #0#3=4; #8#15 = factura; |FINSI;
    |SI #0#3=5; #8#16 = factura; |FINSI;
    |SI #0#3=6; #8#17 = factura; |FINSI;
    |SI #0#3=7; #8#18 = factura; |FINSI;
    |SI #0#3=8; #8#19 = factura; |FINSI;
    |SI #0#3=9; #8#20 = factura; |FINSI;
    |GRABA 020#8a;
|FINSI;
|LIBERA #8;
|CIERRA #8;
|FPRC;
