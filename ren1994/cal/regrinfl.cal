|FICHEROS;
    regrinfl #0;
    recasilv #1;
    regrincv #2;
    regrinfc #3;
    retipcav #4;
|FIN;

|| ATENCION! Los valores mayores de 250 pertenecen al retotrea, los menores al retotren.

|VARIABLES;
 {-1}menu = "";
     menu1 = "2400";
     menu2 = "1";
     menu3 = "";
     menu4 = "VCSAPU";
     menu5 = " Validar ";
     menu6 = " Clave ";
     menu7 = " Siguiente ";
     menu8 = " Anterior ";
     menu9 = " Primera ";
     menu10= " Ultima ";
       &sw_entrada = 0;
       &casilla = 0;
       &casini  = 0;
       opcion = 0;
       campo  = 0;
       casant = 0;
       xxxxfiac = "  ";
       xxaponer = "  ";
       xrelleno = "                              ";
       sn  = "";
       sn1 = "";
       pos_menu = "1";
       modo = 0;
|FIN;

|PROCESO linea; |TIPO 7;
         opcion = (FEntrada / 100) ! 0;
         |SI opcion = 1; |Y FSalida ! 7;
             #0#1 = #0#1 + 9;
             |PINTA #0#1;
         |FINSI;
|FPRC;

|PROCESO tipo; |TIPO 0;
|FPRC;

|PROCESO entra_casilla; |TIPO 7;
         |SI Campo = 2;
             casant = #0#2;
         |SINO;
             |SI #0#2 > 0;
                 #0Campo = 0;
                 |PINTA #0Campo;
                 |HAZ nomodi;
             |FINSI;
         |FINSI;
|FPRC;

|PROCESO casillas; |TIPO 0;
         campo = Campo;
         |SI #0campo < 0;
             casilla = -#0campo;
         |SINO;
             casilla = #0campo;
         |FINSI;
         |SI FSalida = 18; |HAZ consulta_cas; |FINSI;
         |SI casilla > 0;
             |SI #0campo < 0;
                 #0campo = -casilla;
             |SINO;
                 #0campo = casilla;
             |FINSI;
             |PINTA #0campo;
             |HAZ lee_casilla;
         |SINO;
             |DDEFECTO #1;
         |FINSI;
         |SI campo = 2;
             #0#3 = #1#2;  ||  Campo de retotren o retotrea.
             |SI #0#2 = 0;
                 |SI casant > 0; #0#4 = ""; |FINSI;
                 sn = "S";
             |SINO;
                 #0#4 = #1#1;  ||  Descripcion de casilla.
                 sn = "N";
             |FINSI;
             |PINTA #0#4;
             |CAMPO_MODIFICA #0#4,1,sn;
         |SINO;
             campo = campo + 7;
             #0campo = #1#2;
         |FINSI;
|FPRC;

|PROCESO consulta_cas;
         |PUSHV 0501 2380;
         casini = 0;
         |SI casilla > 0; |HAZ lee_tipo; |FINSI;
         |ENTLINEAL #1#4,1,4,9,1,min,max;
         |POPV;
|FPRC;

|PROCESO lee_tipo;
         |ABRE #4;
         |DDEFECTO #4;
         #4#0 = casilla;
         |LEE 001#4];
         |SI FSalida ! 0; |LEE 001#4u; |FINSI;
         |SI FSalida = 0;
             |SI #4#0 > casilla;
                 |LEE 001#4a;
                 |SI FSalida = 0; |Y casilla ] #4#0; |Y casilla [ #4#1;
                     casini = #4#0;
                 |FINSI;
             |FINSI;
         |FINSI;
         |CIERRA #4;
|FPRC;

|PROCESO lee_casilla;
         |ABRE #1;
         |DDEFECTO #1;
         |SI #0campo < 0;
             #1#0 = -#0campo
         |SINO;
             #1#0 = #0campo;
         |FINSI;
         |LEE 001#1=;
         |SI FSalida ! 0;
             |DDEFECTO #1;
             |MENAV "0000La casilla no existe, consulte a su Distribuidor.";
         |FINSI;
         |CIERRA #1;
|FPRC;

|PROCESO min;
         #4#0 = 1;
|FPRC;

|PROCESO max;
         #4#0 = 999;
|FPRC;

|PROCESO nomodi;
         xxaponer = Anono;
         xxxxfiac = Control % A1001;
         Control = Control + xxxxfiac;
         Control = Control + xrelleno;
         Control = Control % A120;
         Control = Control + xxaponer;
|FPRC;

|PROCESO grupo; |TIPO 0;
         |SI FSalida = 18;
             |CIERRAT #3;
             |HAZ consulta_grupo;
             |ABRET #3;
         |SINO;
             |SI #0#2 = #0#0; |ERROR; |FINSI;
         |FINSI;
|FPRC;

|PROCESO consulta_grupo;
         |ABRE #2;
         #2#0 = #0#2;
         |LEE 001#2];
         |SI FSalida ! 0;
             |LEE 001#2p;
         |FINSI;
         |SI FSalida = 0;
             |PUSHV 0808 1178;
             |PINPA #0#2;
             |PINDA #0#2;
             FSalida = 2;
             |PARA; |SI FSalida > 1; |HACIENDO;
                     menu2 = pos_menu;
                     |MENU menu;
                     pos_menu = FSalida;
                     |SI FSalida = 0; FSalida = -1; |FINSI;
                     |SI FSalida = 2; |HAZ clave; |FINSI;
                     |SI FSalida = 3; |HAZ siguiente; |FINSI;
                     |SI FSalida = 4; |HAZ anterior; |FINSI;
                     |SI FSalida = 5; |LEE 001#2p; |FINSI;
                     |SI FSalida = 6; |LEE 001#2u; |FINSI;
                     |SI FSalida = 0;
                         |PINDA #0#2;
                         FSalida = 999;
                     |FINSI;
             |SIGUE;
             |POPV;
         |FINSI;
         |CIERRA #2;
|FPRC;

|PROCESO clave;
         FSalida = 2;
         |PARA; |SI FSalida ! 0; |Y FSalida ! 1; |Y FSalida ! 7; |HACIENDO;
                |ENCAMPO #0#2;
         |SIGUE;
         |SI FSalida = 0;
             |LEE 001#2];
         |FINSI;
|FPRC;

|PROCESO siguiente;
         |LEE 001#2s;
         |SI FSalida ! 0;
             |MENAV "0000Ultima Ficha";
             |LEE 001#2=;
         |FINSI;
|FPRC;

|PROCESO anterior;
         |LEE 001#2a;
         |SI FSalida ! 0;
             |MENAV "0000Primera Ficha";
             |LEE 001#2=;
         |FINSI;
|FPRC;

|PROCESO cuenta_lin; |TIPO 2;
         #3#2 = #3#2 +. 1;
|FPRC;
