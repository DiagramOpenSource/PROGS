|FICHEROS;
    recapreo #00;   || captura
    reproedi #01;   || profesionales estimacion directa(PC)
    reprofco #02;   || profesionales por coeficientes  (PB)
    reempedi #03;   || empresarios estimacion directa  (EC)
    reemprco #04;   || empresarios por coeficientes    (EB)
    reempmod #05;   || empresarios por modulos         (EA)

    eosclien #06;   || clientes REO
    eosactiv #07;   || actividades REO
    eosfimod #08;   || modulos REO
    eosm2130 #09;   || 130 REO
    retmpreo #10;   || temporal para lectura por nif
    redatcon #11;   || datos contribuyentes
    reeleact #12;   || elementos por epigrafe
    eosma131 #13;   || 131 REO
    eoscomul #14;   || Lineas Comuneros
|FIN;

|VARIABLES;
    alfa1 = "";
    alfa2 = "";
    alfa3 = "";
    alfa4 = "";
    nume1 = 0;
    nume2 = 0;
    nume3 = 0;
    nume4 = 0;
    fech1 = @;
    fech2 = @;
    fech3 = @;
    fech4 = @;

    d_empre  = 0;
    d_activ  = 0;
    rendi    = 0;
    sw_estar = 0;
    porce    = 0;
    nombre   = "";
    agieos   = "";

    volumen  = 0;
    e_final  = 0;
    e_inicio = 0;
    compras  = 0;
    benefici = 0;
    v_1cien  = 0;
    g_20cien = 0;
    rdto_net = 0;
    retencio = 0;
    resultad = 0;
    r_20cien = 0;
    trimes1 = 0;
    trimes2 = 0;
    trimes3 = 0;
    trimes4 = 0;
    ultima = 0;
    tipo = "";
    perc = "";
    seleccio = 0;
    grabalo = 0;
    encuentra = 0;
    buscanif = "";
    empresa = 0;
    porce_ga = 0;

    &dni = "";
    &codigo = 0;
    &error = "";
    &empre_e = 0;
    &empre_r = 0;
    &activ_e = 0;
    &tipo_e  = "";


|FIN;

|INCLUYE i_activo;

     *********************************************************************
     LLAMADAS DESDE CAMPOS
     *********************************************************************

|PROGRAMA;
|ACABA;
|HAZ path;

|SI empre_r = 0;
    |CLS;
    |CABEZA "Captura R.E.O.";
    |DDEFECTO #0;
    |PINPA #0#0;
    |PINDA #0#0;
    |ENDATOS #1#0;
    |SI FSalida = 0; |HAZ tipo_3; |FINSI;
|SINO;
    |ABRE #7;
    #7#0 = empre_e;
    #7#1 = activ_e;
    #7#2 = 94;
    |LEE 001#7=;
    |SI FSalida ! 0; |CIERRA #7; |ACABA; |FINSI;
    |CIERRA #7;

    |SI #7#46 = "N";
        #0#0 = "SI";
        #0#1 = empre_e;
        #0#2 = activ_e;
        #0#3 = empre_r;
        #0#4 = tipo_e;
        #0#5 = 0;
        #0#6 = 0;
        |PINPA #0#0;
        |PINDA #0#0;
        |HAZ tipo_3;
    |FINSI;
|FINSI;
|FPRO;

|PROCESO path; |TIPO 8;
|DBASS alfa1;
    agieos = alfa1 + "agieos/fich/";
|PATH_DAT #06 agieos;  || empresas
|PATH_DAT #07 agieos;  || empresas/actividades
|PATH_DAT #08 agieos;  || elementos por epigrafes
|PATH_DAT #09 agieos;  || 130
|PATH_DAT #13 agieos;  || 131
|PATH_DAT #14 agieos;  || Comuneros
|FPRC;

|PROCESO lee_eos1; |TIPO 0;        || lee empresa en R.E.O.
|ABRE #6;
    #6#0 = #0#1;
|LEE 001#6=;
|SI FSalida ! 0;
    |DDEFECTO #6;
    |MENAV "    ATENCION!!! Empresa inexistente en R.E.O. ...";
    |ERROR;
|FINSI;
|ATRI I;
|PINTA 1419, #6#2;
|ATRI 0;
|CIERRA #6;
|FPRC;

|PROCESO lee_eos2; |TIPO 0;   || lee actividad en R.E.O.
|ABRE #7;
    #7#0 = #0#1;
    #7#1 = #0#2;
    #7#2 = 94;
|LEE 001#7=;

|SI FSalida ! 0;
    |DDEFECTO #7;
    |MENAV "    ATENCION!!! Actividad inexistente en R.E.O. ...";
    |ERROR;
    |ACABA;
|FINSI;

|SI #7#14 = "S";
    |MENAV "    ATENCION!!! Actividad Comunero, introduzca la Comunidad.";
    |ERROR;
    |ACABA;
|FINSI;

|SI #7#46 = "S";
    |CAMPO_MODIFICA #0#5, 1, "S";
    |CAMPO_MODIFICA #0#6, 1, "S";
|SINO;
    |CAMPO_MODIFICA #0#5, 1, "N";
    |CAMPO_MODIFICA #0#6, 1, "N";
    #0#5 = 0;  |PINTA #0#5;
    #0#6 = 0;  |PINTA #0#6;
    |PINTA 1719, "                                       ";
|FINSI;

|ATRI I;
|PINTA 1519, #7#6;
|ATRI 0;
|CIERRA #7;
|FPRC;

|PROCESO es_comuner;
|SI #14#0 = #0#1; |Y #14#1 = #0#2;
    sw_estar = 1;
    porce    = #14#6;
    nombre   = #14#4;
|FINSI;
|FPRC;

|DEFBUCLE eoscomul;
#14#2;
;
#0#5, #0#6, 94;
#0#5, #0#6, 94;
e;
NULL, es_comuner;
|FIN;

|PROCESO mira_comu; |TIPO 0;
|SI #7#46 = "N"; |ACABA; |FINSI;
sw_estar = 0;
|BUCLE eoscomul;
|SI sw_estar = 0;
    |MENAV "    La empresa comunero no pertenece a la Comunidad de Bienes";
    |ERROR;
    |ACABA;
|SINO;
    |PINTA 1719, nombre;
|FINSI;
|FPRC;

|PROCESO lee_renta1; |TIPO 0;
|ABRE #11;
#11#0 = #0#3;
|LEE 001#11=;
|SI FSalida ! 0;
    |DDEFECTO #11;
    |MENAV "    ATENCION!!! Contribuyente inexistente en RENTA ...";
    |ERROR;
|FINSI;
|CIERRA #11;
|FPRC;

|PROCESO lee_renta2; |TIPO 0;
|ATRI I;
|SI #0#4 = "T";  |PINTA 1919, #11#01;  |FINSI;
|SI #0#4 = "C";  |PINTA 1919, #11#28;  |FINSI;
|ATRI 0;
|FPRC;

     *********************************************************************
     LECTURA DE ACTIVIDADES
     *********************************************************************

|PROCESO captura1;
tipo = #0#4;
|SI tipo = "T";  perc = "TITULAR";  |FINSI;
|SI tipo = "C";  perc = "CONYUGE";  |FINSI;

|SI #7#46 = "S";
    |ABRE #7;
    #7#0 = #0#5;
    #7#1 = #0#6;
    #7#2 = 94;
    |LEE 040#7=;
    |SI FSalida ! 0; |DDEFECTO #7; |FINSI;
    |CIERRA #7;
|SINO;
    porce = 100;
|FINSI;

    alfa1 = #7#23 + #7#22;
|SI alfa1 = "PC";   |HAZ profdire1; |FINSI;
|SI alfa1 = "PB";   |HAZ profcoef1; |FINSI;
|SI alfa1 = "EC";   |HAZ emprdire1; |FINSI;
|SI alfa1 = "EB";   |HAZ emprcoef1; |FINSI;
|SI alfa1 = "EA";   |HAZ emprmodu1; |FINSI;
|FPRC;

|PROCESO tipo_3; |TIPO 3;
|SI #0#0 = "SI";
    |HAZ captura1;
    |SI grabalo = 0;
        |MENAV "    ATENCION!!! Empresa/Actividad sin 130, 131 o modulos ...";
    |FINSI;
|FINSI;
|FPRC;

*********************************************************************
LECTURA DEL 130
*********************************************************************

|PROCESO lee_130b;
grabalo = 1;

volumen  = ((#9#8 + #9#30) % porce);
||e_final  = #9#11 % porce;
||e_inicio = #9#17 % porce;
||compras  = #9#14 % porce;
||benefici = #9#18 % porce;
v_1cien  = #9#19 % porce;
||g_20cien = #9#21 % porce;
||rdto_net = #9#22 % porce;
retencio = ((#9#25 + #9#32) % porce);
resultad = #9#26 % porce;
r_20cien = #9#27 % porce;
porce_ga = #9#20;

|SI #9#2 = 1;  trimes1 = #9#29 % porce;  |FINSI;
|SI #9#2 = 2;  trimes2 = #9#29 % porce;  |FINSI;
|SI #9#2 = 3;  trimes3 = #9#29 % porce;  |FINSI;
|SI #9#2 = 4;  trimes4 = #9#29 % porce;  |FINSI;
|FPRC;

|PROCESO lee_130a;
    volumen  = 0;
    e_final  = 0;
    e_inicio = 0;
    compras  = 0;
    benefici = 0;
    v_1cien  = 0;
    g_20cien = 0;
    rdto_net = 0;
    retencio = 0;
    resultad = 0;
    r_20cien = 0;
    porce_ga = 0;
    trimes1  = 0;
    trimes2  = 0;
    trimes3  = 0;
    trimes4  = 0;
|FPRC;

|DEFBUCLE lee_130;
#9#1;
;
#0#1, #0#2, 1, 94;
#0#1, #0#2, 4, 94;
e;
lee_130a, lee_130b;
|FIN;

*********************************************************************
LECTURA DEL 131 Y MODULOS
*********************************************************************

|PROCESO lee_131b;
    grabalo = 1;
|SI #13#24 > 0;
    |SI #13#1 = 1;  trimes1 = #13#24;  |FINSI;
    |SI #13#1 = 2;  trimes2 = #13#24;  |FINSI;
    |SI #13#1 = 3;  trimes3 = #13#24;  |FINSI;
    |SI #13#1 = 4;  trimes4 = #13#24;  |FINSI;
|FINSI;
|FPRC;

|PROCESO lee_131a;
    trimes1 = 0;
    trimes2 = 0;
    trimes3 = 0;
    trimes4 = 0;
|FPRC;

|DEFBUCLE lee_131;
 #13#1;
 ;
 #7#0, 1, 94;
 #7#0, 4, 94;
 e;
 lee_131a, lee_131b;
|FIN;

     *********************************************************************
     GRABACION DE PROFESIONALES EN ESTIMACION DIRECTA
     *********************************************************************

|PROCESO profdire2;
|ABRE #1;
|HAZ ultimo1;
|DDEFECTO #1;
    #1#00 = #0#3;                          || codigo
    #1#01 = ultima;                        || linea
    #1#02 = tipo;                          || tipo
    #1#03 = perc;                          || perceptor
    #1#13 = #7#4;                          || epigrafe -1-
    #1#14 = #7#5;                          || epigrafe -2-
    #1#15 = #7#6;                          || nombre actividad
    #1#16 = volumen;                       || ingresos integros
    #1#24 = compras + e_inicio - e_final;  || Otros Gastos
    #1#25 = v_1cien;                       || 1% ingresos integros
    #1#26 = #1#24 + #1#25;                 || total gastos
    #1#28 = rdto_net;                      || rendimiento neto
    #1#29 = trimes1;                       || 1er. trimestre
    #1#30 = trimes2;                       || 2do. trimestre
    #1#31 = trimes3;                       || 3er. trimestre
    #1#32 = trimes4;                       || 4to. trimestre
    #1#36 = retencio;                      || retenciones
    #1#37 = #7#11;       || Codigo Provincia
    #1#38 = #7#12;       || Codigo Poblacion
    #1#39 = #7#9;        || Domicilio

|GRABA 020#1n;
|SI FSalida=0;  npro=7;  empresa=#0#3;  |HAZ sigrabo;  |FINSI;
|CIERRA #1;
|FPRC;

|PROCESO ultimo1;
    #1#0 = #0#3;
    #1#1 = 99;
|LEE 001#1];
|SI FSalida = 0;
    |LEE 001#1a;
|SINO;
    |LEE 001#1u;
|FINSI;
|SI FSalida = 0; |Y #1#0 = #0#3;
        ultima = #1#1 + 1;
|SINO;
        ultima = 1;
|FINSI;
|FPRC;

|PROCESO profdire1;
grabalo = 0;
|BUCLE lee_130;
|SI grabalo ! 0; |HAZ profdire2; |FINSI;
|FPRC;

*********************************************************************
GRABACION DE PROFESIONALES EN COEFICIENTES
*********************************************************************

|PROCESO profcoef2;
|ABRE #2;
|HAZ ultimo2;
|DDEFECTO #2;
    #2#00 = #0#3;                           || codigo
    #2#01 = ultima;                         || linea
    #2#02 = tipo;                           || tipo
    #2#03 = perc;                           || perceptor
    #2#13 = #7#4;                           || epigrafe -1-
    #2#14 = #7#5;                           || epigrafe -2-
    #2#15 = #7#6;                           || nombre actividad
    #2#17 = volumen;                        || ingresos integros
    #2#39 = compras;                        || compras
    #2#37 = e_final;                        || Existencias finales
    #2#38 = e_inicio;                       || Existencias inicio
    #2#25 = g_20cien;                       || 10% Coeficientes de gastos
    #2#30 = #2#39 + #2#38 + #2#25 - #2#37;  || total gastos
    #2#32 = rdto_net;                       || rendimiento neto
    #2#33 = trimes1;                        || 1er. trimestre
    #2#34 = trimes2;                        || 2do. trimestre
    #2#35 = trimes3;                        || 3er. trimestre
    #2#36 = trimes4;                        || 4to. trimestre
    #2#40 = #2#17;                          || total ingresos
    #2#41 = #2#32;                          || rendimiento bruto
    #2#43 = retencio;                       || retenciones
    #2#44 = #7#11;                          || Codigo Provincia
    #2#45 = #7#12;                          || Codigo Poblacion
    #2#46 = #7#9;                           || Domicilio

|GRABA 020#2n;
|SI FSalida=0;  npro=8;  empresa=#0#3;  |HAZ sigrabo;  |FINSI;
|CIERRA #2;
|FPRC;

|PROCESO ultimo2;
    #2#0 = #0#3;
    #2#1 = 99;
|LEE 001#2];
|SI FSalida = 0;
    |LEE 001#2a;
|SINO;
    |LEE 001#2u;
|FINSI;
|SI FSalida = 0; |Y #2#0 = #0#3;
        ultima = #2#1 + 1;
|SINO;
        ultima = 1;
|FINSI;
|FPRC;

|PROCESO profcoef1;
    grabalo = 0;
|BUCLE lee_130;
|SI grabalo ! 0;
    |HAZ profcoef2;
|FINSI;
|FPRC;

     *********************************************************************
     GRABACION DE EMPRESARIALES EN ESTIMACION DIRECTA
     *********************************************************************

|PROCESO emprdire2;
|ABRE #3;
|HAZ ultimo3;
|DDEFECTO #3;
    #3#00 = #0#3;                          || codigo
    #3#01 = ultima;                        || linea
    #3#02 = tipo;                          || tipo
    #3#03 = perc;                          || perceptor
    #3#13 = #7#4;                          || epigrafe -1-
    #3#14 = #7#5;                          || epigrafe -2-
    #3#15 = #7#6;                          || nombre actividad
    #3#17 = volumen;                       || ingresos ordinarios por ventas
    #3#23 = #3#17;                         || total ingresos
    #3#41 = compras + e_inicio - e_final;  || Otros Gastos
    #3#42 = #3#41;                         || Total Gastos
    #3#43 = #3#17;                         || total ingresos computados
    #3#44 = #3#41;                         || Total Gastos computados
    #3#45 = rdto_net;                      || rendimiento neto previo
    #3#49 = rdto_net;                      || rendimiento neto
    #3#50 = trimes1;     || 1er. trimestre
    #3#51 = trimes2;     || 2do. trimestre
    #3#52 = trimes3;     || 3er. trimestre
    #3#53 = trimes4;     || 4to. trimestre
    #3#55 = #7#11;       || Codigo Provincia
    #3#56 = #7#12;       || Codigo Poblacion
    #3#57 = #7#9;        || Domicilio

|GRABA 020#3n;
|SI FSalida=0;  npro=9;  empresa=#0#3;  |HAZ sigrabo;  |FINSI;
|CIERRA #3;
|FPRC;

|PROCESO ultimo3;
    #3#0 = #0#3;
    #3#1 = 99;
|LEE 001#3];
|SI FSalida = 0;
    |LEE 001#3a;
|SINO;
    |LEE 001#3u;
|FINSI;
|SI FSalida = 0; |Y #3#0 = #0#3;
        ultima = #3#1 + 1;
|SINO;
        ultima = 1;
|FINSI;
|FPRC;

|PROCESO emprdire1;
    grabalo = 0;
|BUCLE lee_130;
|SI grabalo ! 0;
    |HAZ emprdire2;
|FINSI;
|FPRC;

     *********************************************************************
     GRABACION DE EMPRESARIALES EN COEFICIENTES
     *********************************************************************

|PROCESO emprcoef2;
|ABRE #4;
|HAZ ultimo4;
|DDEFECTO #4;
    #4#00 = #0#3;                                    || codigo
    #4#01 = ultima;                                  || linea
    #4#02 = tipo;                                    || tipo
    #4#03 = perc;                                    || perceptor
    #4#13 = #7#4;                                    || epigrafe -1-
    #4#14 = #7#5;                                    || epigrafe -2-
    #4#15 = #7#6;                                    || nombre actividad
    #4#17 = volumen;                                 || ingresos integros
    #4#25 = g_20cien;                                || cuota gastos
    #4#30 = compras + e_inicio + g_20cien - e_final; || total gastos
    #4#32 = rdto_net;                                || rendimiento neto
    #4#33 = porce_ga;                                || (%) gastos
    #4#34 = trimes1;       || 1er. trimestre
    #4#35 = trimes2;       || 2do. trimestre
    #4#36 = trimes3;       || 3er. trimestre
    #4#37 = trimes4;       || 4to. trimestre
    #4#38 = e_final;       || Existencias finales
    #4#39 = e_inicio;      || Existencias inicio
    #4#40 = compras;       || Compras
    #4#41 = #4#17;         || total ingresos
    #4#42 = #4#41 - #4#30; || rendimiento bruto
    #4#44 = #7#11;       || Codigo Provincia
    #4#45 = #7#12;       || Codigo Poblacion
    #4#46 = #7#9;        || Domicilio

|GRABA 020#4n;
|SI FSalida=0;  npro=10;  empresa=#0#3;  |HAZ sigrabo;  |FINSI;
|CIERRA #4;
|FPRC;

|PROCESO ultimo4;
    #4#0 = #0#3;
    #4#1 = 99;
|LEE 001#4];
|SI FSalida = 0;
    |LEE 001#4a;
|SINO;
    |LEE 001#4u;
|FINSI;
|SI FSalida = 0; |Y #4#0 = #0#3;
        ultima = #4#1 + 1;
|SINO;
        ultima = 1;
|FINSI;
|FPRC;

|PROCESO emprcoef1;
    grabalo = 0;
|BUCLE lee_130;
|SI grabalo ! 0;
    |HAZ emprcoef2;
|FINSI;
|FPRC;

     *********************************************************************
     GRABACION DE EMPRESARIALES EN MODULOS
     *********************************************************************

|PROCESO emprmodu4;
rendi = 0;
|ABRE #5;
|HAZ ultimo5;
|DDEFECTO #5;
    #5#00 = #0#3;        || codigo
    #5#01 = ultima;      || linea
    #5#02 = tipo;        || tipo
    #5#03 = perc;        || perceptor
    #5#13 = #7#4;        || epigrafe -1-
    #5#14 = #7#5;        || epigrafe -2-
    #5#15 = #7#6;        || nombre actividad
|PARA nume1 = 16; |SI nume1 [ 22; |HACIENDO nume1 = nume1 + 1;
        nume2 = nume1 + 113;
        #5nume1 = #8nume2;    || definiciones 1-7
|SIGUE;
|PARA nume1 = 23; |SI nume1 [ 29; |HACIENDO nume1 = nume1 + 1;
        nume2 = nume1 + 69;
        #5nume1 = ((#8nume2 * #8#121) / 365) % porce;    || numeros 1-7
|SIGUE;
|PARA nume1 = 30; |SI nume1 [ 36; |HACIENDO nume1 = nume1 + 1;
        nume2 = nume1 + 108;
        #5nume1 = #8nume2;    || cuota rendimientos 1-7
|SIGUE;
    #5#37 = (#5#23 * #5#30) + (#5#24 * #5#31) + (#5#25 * #5#32) + (#5#26 * #5#33);
    #5#37 = #5#37 + (#5#27 * #5#34) + (#5#28 * #5#35) + (#5#29 * #5#36);

    #5#62 = #12#26 % porce;      || Exceso 1.25;
    #5#38 = #8#59 + 1;           || indice por actividad  (formato AGIREN)
    #5#40 = #8#60 + 1;           || indice por temporada  (formato AGIREN)
    #5#42 = 1;                   || indice cuantia nulo

    rendi = #5#37;
    |SI #5#38 ! 1;
        #5#39 = (#5#38 - 1) * #5#37;
        rendi = #5#39;
    |SINO;
        #5#39 = 0;
    |FINSI;

    |SI #5#40 ! 1;
        #5#41 = (#5#40 - 1) * rendi;
        rendi = #5#41;
    |SINO;
        #5#41 = 0;
    |FINSI;

    |SI rendi > #5#62; |Y #5#38 = 1;
        #5#42 = 1.25;
        #5#43 = (rendi - #5#62) * .25; || resultado cuantia
        rendi = rendi + #5#43;
    |SINO;
        #5#42 = 1;
        #5#43 = 0;
    |FINSI;

    #5#44 = rendi;               || rendimiento corregido
    rendi = #5#44 - #5#47 - #5#48;

    #5#59 = #8#178;
    |SI #5#59 ! 0;
        #5#60 = rendi % #5#59;
        rendi = rendi - #5#60;
    |SINO;
        #5#60 = 0;
    |FINSI;

    #5#63 = #8#181;
    |SI #5#63 ! 0;
        #5#64 = rendi % #5#63;
        rendi = rendi - #5#64;
    |SINO;
        #5#64 = 0;
    |FINSI;

    rendi = rendi + #5#45 - #5#46 - #5#61;
    #5#49 = rendi;

    #5#50 = trimes1;     || 1er. trimestre
    #5#51 = trimes2;     || 2do. trimestre
    #5#52 = trimes3;     || 3er. trimestre
    #5#53 = trimes4;     || 4to. trimestre
    #5#56 = #7#11;       || Codigo Provincia
    #5#57 = #7#12;       || Codigo Poblacion
    #5#58 = #7#9;        || Domicilio

|GRABA 020#5n;
|SI FSalida=0;  npro=11;  empresa=#0#3;  |HAZ sigrabo;  |FINSI;
|CIERRA #5;
|FPRC;

|PROCESO ultimo5;
    #5#0 = #0#3;
    #5#1 = 99;
|LEE 001#5];
|SI FSalida = 0;
    |LEE 001#5a;
|SINO;
    |LEE 001#5u;
|FINSI;
|SI FSalida = 0; |Y #5#0 = #0#3;
        ultima = #5#1 + 1;
|SINO;
        ultima = 1;
|FINSI;
|FPRC;

|PROCESO emprmodu3;      || lee el epigrafe (para saber el exceso)
|ABRE #12;
    #12#0 = #7#4;
    #12#1 = #7#5;
    #12#2 = #8#157;
    #12#3 = 94;
|LEE 001#12=;
|SI FSalida = 0;
        grabalo = 1;
|SINO;
        dni = #7#4 + #7#5;
        codigo = #7#0;
        error = "EPIGRAFE de REO inexistente en AGIREN: " + dni + "/" + codigo;
    |MENAV error;               || imprime linea como incidencia
|FINSI;
|CIERRA #12;
|FPRC;

|PROCESO emprmodu2;      || lee el modulo
|ABRE #8;
    #8#0   = #0#1;
    #8#156 = #0#2;
    #8#1   = 94;
|LEE 001#8=;
|SI FSalida = 0;
        grabalo = 1;
|FINSI;
|CIERRA #8;
|FPRC;

|PROCESO emprmodu1;
    grabalo = 0;
|BUCLE lee_131;
|SI grabalo ! 0;
        grabalo = 0;
    |HAZ emprmodu2;      || lee el modulo en REO
    |SI grabalo ! 0;
            grabalo = 0;
        |HAZ emprmodu3;       || lee el epigrafe en AGIREN
        |SI grabalo ! 0;
            |HAZ emprmodu4;        || grabacion en AGIREN
        |FINSI;
    |FINSI;
|FINSI;
|FPRC;
