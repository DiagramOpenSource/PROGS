|FICHEROS;
    recapinm #0;
    rerendtr #1;
    revalore #2;
    revalono #3;
    rectacor #4;
    reincide #5;
    retotren #6;
    redatcon #7;
    retotpat #8;
    retotrea #9;
    reprogra #10;
|FIN;

|VARIABLES;
    reten      = 0;
    swpasa     = 0;
    anual      = 0;
    resto      = 0;
    liquido    = 0;
    sw_fich    = 0;
    numero1    = 0;
    numero2    = 0;
    numero3    = 0;
    numero4    = 0;
    numero5    = 0;
    numero6    = 0;
    coefi1     = 0;
    suma       = 0;
    mayor      = 0;
    tope       = 0;
    maximo     = 0;
    FEstado    = 0;
    FEstad1    = 0;
    FEstad2    = 0;
    sw         = 0;
    traba      = "";
    cinco      = 0;

    tipod      = "";
    perceptor  = "";
    tipore     = "";
    minusva    = "";
    numero     = "";
    s          = "S";
    alfa1      = "";
    linea      = "";
    rendi      = "";
    descri     = "";
    contenido  = "";
    blanco     = "                                        ";
    aaa        = 0;
    bbb        = 0;
    dias       = 0;
    feini      = @;
    fefin      = @;
    valor1     = 0;
    rendimi    = 0;
    interes    = 0;
    empreac    = 0;
    tipop      = "";
    tipoprop   = "";
    cantidad   = 0;
    cantidad1  = 0;
    cantidad2  = 0;
    cantidad3  = 0;
    cantidad4  = 0;
    anyos      = 0;
    anyou      = 0;
    porce      = 0;
    var        = 0;
    sumaalq    = 0;

    &incisino = "";
    &modo     = 0;
    &empresa  = 0;
    &emprefi  = 0;
    &nombre   = "";
    &lininci  = 0;
    &swcalcu  = 0;
    &minusva1  = "";
    &minusva2  = "";
    &minusva3  = "";
    &minusva4  = "";
    &minusva5  = "";
    &minusva6  = "";
    &declara   = "";
    &individu  = 0;
    &acceso    = 0;
    &recalcu   = 0;
|FIN;

|| **************************************************************************
                    PROCESOS DE LECTURAS DE LOS DISTINTOS FICHEROS
|| **************************************************************************

|PROCESO fichero1;     || Trata los Rendimientos del Trabajo;
alfa1="RENDIMIENTOS DEL TRABAJO --------------------------------------------";
tipore    = #1#28;
linea     = #1#1;
descri    = #1#4;
rendi     = #1#5;
sw_fich   = 1;
tipod     = #1#2;
perceptor = #1#3;
empreac   = #1#0;

|SI #1#28 = "X";  |HAZ incidenci1;  |FINSI;
|SI #1#28 ! "X";  |HAZ tipodecl;    |FINSI;
|FPRC;


|PROCESO fichero0;            || Trata el Capital Inmobiliario
alfa1 = "CAPITAL INMOBILIARIO------------------------------------------------";
tipod     = #0#2;
tipore    = #0#48;
descri    = #0#5;
linea     = #0#1;
rendi     = #0#47;
perceptor = #0#3;
numero1   = #0#49;
numero2   = #0#50;
numero3   = #0#51;
numero4   = #0#52;
numero5   = #0#53;
numero6   = #0#54;
sw_fich   = 0;
empreac   = #0#0;
tipoprop  = #0#15;
cantidad1 = 0;

|SI #0#48 = "X"; |O #0#48 = "P"; |O #0#48 = "R";
    |HAZ incidenci1;
|FINSI;

|SI #0#48 = "A"; |O #0#48 = "R";
    |SI #0#15 ! "N"; |HAZ tipodecl; |FINSI;
|FINSI;

|SI #0#48 = "P"; |O #0#48 = "A";
   |SI #0#4 = "R"; tipop = "B1"; |SINO; tipop = "A1"; |FINSI;
   |SI #0#31 = "N";
       cantidad1 = #0#8;
       |SI cantidad1 < #0#17; cantidad1 = #0#17; |FINSI;
       |SI cantidad1 < #0#19; cantidad1 = #0#19; |FINSI;
       tipod = #0#2;
       |SI #0#15 ! "P"; anyou = #0#16; |HAZ calcuprop; |FINSI;
   |FINSI;
   |SI cantidad1 [ 0; |ACABA; |FINSI;

   |SI #0#2 ! "G"; |Y #0#2 ! "V";
       tipod = #0#2; cantidad = cantidad1; |HAZ grabapat;
       |ACABA;
   |FINSI;

   |SI #0#2 = "V";
       tipod = "T"; cantidad = cantidad1 % #0#49; |HAZ grabapat;
       tipod = "C"; cantidad = cantidad1 % #0#50; |HAZ grabapat;
       tipod = "1"; cantidad = cantidad1 % #0#51; |HAZ grabapat;
       tipod = "2"; cantidad = cantidad1 % #0#52; |HAZ grabapat;
       tipod = "3"; cantidad = cantidad1 % #0#53; |HAZ grabapat;
       tipod = "4"; cantidad = cantidad1 % #0#54; |HAZ grabapat;
       |ACABA;
   |FINSI;

   |SI #0#2 = "G";
       tipod = "T"; cantidad = cantidad1 % 50; |HAZ grabapat;
       tipod = "C"; cantidad = cantidad1 % 50; |HAZ grabapat;
       |ACABA;
   |FINSI;
|FINSI;
|FPRC;

|PROCESO fichero2;            || Trata Valores Bolsa
alfa1 = "VALORES BOLSA ------------------------------------------------------";
tipod     = #2#2;
tipore    = #2#4;
descri    = #2#5;
linea     = #2#1;
rendi     = #2#33;
perceptor = #2#3;
numero1   = #2#7;
numero2   = #2#8;
numero3   = #2#9;
numero4   = #2#10;
numero5   = #2#11;
numero6   = #2#12;
sw_fich   = 2;
empreac   = #2#0;
tipoprop  = #2#5;
cantidad1 = 0;

|SI #2#4 = "X"; |O #2#4 = "P"; |O #2#4 = "R";
    |HAZ incidenci1;
|FINSI;

|SI #2#4 = "A"; |O #2#4 = "R";
    |SI #2#5 ! "N"; |HAZ tipodecl; |FINSI;
|FINSI;

|SI #2#4 = "P"; |O #2#4 = "A";
    tipop = #2#43;
    |SI #2#41 ! 0;
        cantidad1 = #2#41;
    |SINO;
        cantidad1 = #2#19 * (#2#16 - #2#27 - #2#28 - #2#29);
    |FINSI;
    |SI cantidad1 [ 0; |ACABA; |FINSI;

    tipod = #2#2;

    |SI #2#5 ! "P"; anyou = #2#6; |HAZ calcuprop; |FINSI;

    |SI #2#2 ! "G"; |Y #2#2 ! "V";
        tipod = #2#2; cantidad = cantidad1; |HAZ grabapat;
        |ACABA;
    |FINSI;

    |SI #2#2 = "V";
        tipod = "T"; cantidad = cantidad1 % #2#7;  |HAZ grabapat;
        tipod = "C"; cantidad = cantidad1 % #2#8;  |HAZ grabapat;
        tipod = "1"; cantidad = cantidad1 % #2#9;  |HAZ grabapat;
        tipod = "2"; cantidad = cantidad1 % #2#10; |HAZ grabapat;
        tipod = "3"; cantidad = cantidad1 % #2#11; |HAZ grabapat;
        tipod = "4"; cantidad = cantidad1 % #2#12; |HAZ grabapat;
        |ACABA;
    |FINSI;

    |SI #2#2 = "G";
        tipod = "T"; cantidad = cantidad1 % 50; |HAZ grabapat;
        tipod = "C"; cantidad = cantidad1 % 50; |HAZ grabapat;
        |ACABA;
    |FINSI;
|FINSI;
|FPRC;

|PROCESO fichero3;            || Trata los Valores No Bolsa
alfa1 = "VALORES NO BOLSA Y FONDOS DE PENSIONES -----------------------------";
tipod     = #3#2;
tipore    = #3#4;
descri    = #3#14;
linea     = #3#1;
rendi     = #3#33;
perceptor = #3#3;
numero1   = #3#7;
numero2   = #3#8;
numero3   = #3#9;
numero4   = #3#10;
numero5   = #3#11;
numero6   = #3#12;
sw_fich   = 3;
empreac   = #3#0;
tipoprop  = #3#5;
cantidad1 = 0;

|SI #3#4 = "X"; |O #3#4 = "P"; |O #3#4 = "R";
    |HAZ incidenci1;
|FINSI;

|SI #3#4 = "A"; |O #3#4 = "R";
    |SI #3#5 ! "N"; |HAZ tipodecl; |FINSI;
|FINSI;

|SI #3#4 = "P"; |O #3#4 = "A";
    tipop = #3#45;
    |SI #3#43 ! 0;
        cantidad1 = #3#43;
    |SINO;
        |SI #3#30 = "S";
            cantidad1 = #3#32 * (#3#16 - #3#25 - #3#26);
        |SINO;
            cantidad2 = #3#46 * (#3#16 - #3#25 - #3#26);
            cantidad3 = #3#31 * (#3#16 - #3#25 - #3#26);
            cantidad4 = #3#32 * (#3#16 - #3#25 - #3#26);
            cantidad1 = cantidad2;
            |SI cantidad1 < cantidad3; cantidad1 = cantidad3; |FINSI;
            |SI cantidad1 < cantidad4; cantidad1 = cantidad4; |FINSI;
        |FINSI;
        |SI #3#29 = "F";
            cantidad1 = (#3#17 * #3#16) - #3#35 - #3#36;
        |FINSI;
    |FINSI;
    |SI cantidad1 [ 0; |ACABA; |FINSI;

    tipod = #3#2;
    |SI #3#5 ! "P"; anyou = #3#6; |HAZ calcuprop; |FINSI;

    |SI #3#2 ! "G"; |Y #3#2 ! "V";
        tipod = #3#2; cantidad = cantidad1; |HAZ grabapat; |ACABA;
    |FINSI;

    |SI #3#2 = "V";
        tipod = "T"; cantidad = cantidad1 % #3#7;  |HAZ grabapat;
        tipod = "C"; cantidad = cantidad1 % #3#8;  |HAZ grabapat;
        tipod = "1"; cantidad = cantidad1 % #3#9;  |HAZ grabapat;
        tipod = "2"; cantidad = cantidad1 % #3#10; |HAZ grabapat;
        tipod = "3"; cantidad = cantidad1 % #3#11; |HAZ grabapat;
        tipod = "4"; cantidad = cantidad1 % #3#12; |HAZ grabapat;
    |FINSI;

    |SI #3#2 = "G";
        tipod = "T"; cantidad = cantidad1 % 50; |HAZ grabapat;
        tipod = "C"; cantidad = cantidad1 % 50; |HAZ grabapat;
    |FINSI;
|FINSI;
|FPRC;

|PROCESO fichero4;            || Trata las Cuentas Corrientes
alfa1 = "CUENTAS CORRIENTES Y OTROS -----------------------------------------";
tipod     = #4#2;
tipore    = #4#4;
descri    = #4#14;
linea     = #4#1;
rendi     = #4#23;
perceptor = #4#3;
numero1   = #4#7;
numero2   = #4#8;
numero3   = #4#9;
numero4   = #4#10;
numero5   = #4#11;
numero6   = #4#12;
sw_fich   = 4;
empreac   = #4#0;
tipoprop  = #4#5;
cantidad1 = 0;

|SI #4#4 = "X"; |O #4#4 = "P"; |O #4#4 = "R";
    |HAZ incidenci1;
|FINSI;

|SI #4#4 = "A"; |O #4#4 = "R";
    |SI #4#5 ! "N"; |HAZ tipodecl; |FINSI;
|FINSI;

|SI #4#4 = "P"; |O #4#4 = "A";
     tipop = #4#29;
     cantidad1 = #4#19;
     |SI cantidad1 [ #4#20; cantidad1 = #4#20; |FINSI;

     |SI cantidad1 [ 0; |ACABA; |FINSI;
     tipod = #4#2;
     |SI #4#5 ! "P"; anyou = #4#6; |HAZ calcuprop; |FINSI;

     |SI #4#2 ! "G"; |Y #4#2 ! "V";
         tipod = #4#2; cantidad = cantidad1; |HAZ grabapat;
         |ACABA;
     |FINSI;

     |SI #4#2 = "V";
         tipod = "T"; cantidad = cantidad1 % #4#7;  |HAZ grabapat;
         tipod = "C"; cantidad = cantidad1 % #4#8;  |HAZ grabapat;
         tipod = "1"; cantidad = cantidad1 % #4#9;  |HAZ grabapat;
         tipod = "2"; cantidad = cantidad1 % #4#10; |HAZ grabapat;
         tipod = "3"; cantidad = cantidad1 % #4#11; |HAZ grabapat;
         tipod = "4"; cantidad = cantidad1 % #4#12; |HAZ grabapat;
     |FINSI;

     |SI #4#2 = "G";
         tipod = "T"; cantidad = cantidad1 % 50; |HAZ grabapat;
         tipod = "C"; cantidad = cantidad1 % 50; |HAZ grabapat;
     |FINSI;
|FINSI;
|FPRC;

|| **************************************************************************
                        TRATAMIENTO DE LA LIQUIDACION
|| **************************************************************************

|PROCESO tipodecl;
  |SI tipod = "G";                || Si es Ganancial
      coefi1 = 50;  tipod  = "T"; |HAZ leeliqui;
      coefi1 = 50;  tipod  = "C"; |HAZ leeliqui;
      coefi1 = 100; tipod  = "A"; |HAZ leeliqui;
      |ACABA;
  |FINSI;
  |SI tipod ! "V";        ||     SI ES   TITULAR O CONYUGE;
      coefi1 = 100; |HAZ leeliqui;
      coefi1 = 100; tipod  =  "A"; |HAZ leeliqui;
      |ACABA;
  |FINSI;
                || Si es Varios
  |SI numero1 ! 0; coefi1 = numero1; tipod = "T"; |HAZ leeliqui; |FINSI;
  |SI numero2 ! 0; coefi1 = numero2; tipod = "C"; |HAZ leeliqui; |FINSI;
  |SI numero3 ! 0; coefi1 = numero3; tipod = "1"; |HAZ leeliqui; |FINSI;
  |SI numero4 ! 0; coefi1 = numero4; tipod = "2"; |HAZ leeliqui; |FINSI;
  |SI numero5 ! 0; coefi1 = numero5; tipod = "3"; |HAZ leeliqui; |FINSI;
  |SI numero6 ! 0; coefi1 = numero6; tipod = "4"; |HAZ leeliqui; |FINSI;
  coefi1 = numero1 + numero2 + numero3 + numero4 + numero5 + numero6;
  tipod = "A"; |HAZ leeliqui;
|FPRC;

|PROCESO calcuprop;
|ABRE #7;
#7#0 = empreac;
|LEE 040#7=;
|SI FSalida = 0;
    |SI tipoprop = "U";
        |SI tipod = "T"; anyos = #7#3;  |FINSI;
        |SI tipod = "C"; anyos = #7#30; |FINSI;
        |SI tipod = "1";
            |SI #7#98 ! 0; var = 67 + #7#98; anyos = #7var; |FINSI;
        |FINSI;
        |SI tipod = "2";
            |SI #7#99 ! 0; var = 67 + #7#99; anyos = #7var; |FINSI;
        |FINSI;
        |SI tipod = "3";
            |SI #7#100 ! 0; var = 67 + #7#100; anyos = #7var; |FINSI;
        |FINSI;
        |SI tipod = "4";
            |SI #7#101 ! 0; var = 67 + #7#101; anyos = #7var; |FINSI;
        |FINSI;
        |SI tipod = "G"; anyos = #7#3;  |FINSI;
        |SI tipod = "V"; anyos = #7#3;  |FINSI;
        anyos = 1994 - anyos;
    |SINO;
        anyos = anyou;
    |FINSI;
    |SI anyos [ 20; porce = 70; |FINSI;
    |SI anyos > 20;
        porce = 70 - (anyos - 20);
        |SI porce < 10; porce = 10; |FINSI;
    |FINSI;
    |SI tipoprop = "N"; porce = 100 - porce; |FINSI;
    cantidad1 = cantidad1 % porce;
|FINSI;
|CIERRA #7;
|FPRC;

|PROCESO leeliqui;                 || Lee la Liquidacion
  #6#0 = empreac;
  #6#141 = tipod;
  |LIBERA #6; |LEE 110#6=;
  FEstado = FSalida;
  |SI FSalida ! 0;
      |DDEFECTO #6;
      #6#0 = empreac;
      #6#141 = tipod;
  |FINSI;
  #9#0 = empreac;
  #9#1 = tipod;
  |LIBERA #9; |LEE 110#9=;
  FEstad1 = FSalida;
  |SI FSalida ! 0;
      |DDEFECTO #9;
      #9#0 = empreac;
      #9#1 = tipod;
  |FINSI;
  |SI sw_fich = 0; |HAZ grabafi0; |FINSI;
  |SI sw_fich = 1; |HAZ grabafi1; |FINSI;
  |SI sw_fich = 2; |HAZ grabafi2; |FINSI;
  |SI sw_fich = 3; |HAZ grabafi3; |FINSI;
  |SI sw_fich = 4; |HAZ grabafi4; |FINSI;
  |SI FEstado = 0; |GRABA 121#6a; |SINO; |GRABA 121#6n; |FINSI;
  |SI FEstad1 = 0; |GRABA 121#9a; |SINO; |GRABA 121#9n; |FINSI;
|FPRC;

|PROCESO grabafi0;          || Graba Capital Inmobiliario
  mayor = #0#8;
  |SI #0#19 > mayor; mayor = #0#19; |FINSI;
  |SI #0#17 > mayor; mayor = #0#17; |FINSI;

  |SI #0#13 = "H"; |Y #0#4 = "U";
      #6#147 = #6#147 + (#0#009 % coefi1);
      |HAZ pordos;

      |SI #0#71 ! "S"; #6#156 = #6#156 + (rendimi % coefi1); |FINSI;
      |SI #0#71 = "S"; #6#43  = #6#43  + (rendimi % coefi1); |FINSI;

      |SI #0#71 ! "S"; #6#6   = #6#6   + (rendimi % coefi1); |FINSI;
      |SI #0#71 = "S"; #6#39  = #6#39  + (rendimi % coefi1); |FINSI;

      #6#7   = #6#7   + (#0#9 % coefi1);
      |SI tipod = "A"; tope = 1000000; |SINO; tope = 800000; |FINSI;
      interes = #0#10 % coefi1;
      #6#148 = #6#148 + interes;
      |SI #6#148 > tope;
          |SI sw = 0;  |HAZ grabacion;  sw = 1;  |FINSI;
          alfa1 = "INTERESES VIV. HABITUAL AJUSTADOS EN LINEA ";
          alfa1 = alfa1 + #0#1 + " T. DECL. " + tipod;
          |HAZ grabacion;
          #6#7   = #6#7 - (#6#148 - tope) + interes;
          #6#148 = tope;
      |SINO;
          #6#7   = #6#7 + interes;
      |FINSI;
      #6#95 = #6#95   + ((#0#14 % coefi1) % 15);
      #6#166 = #6#166 + (#0#14 % coefi1);
  |SINO;
    |SI #0#21 = "N"; |Y #0#13 = "O"; |Y #0#4 = "U";  || No Alquilada
        #6#149 = #6#149 + (#0#009 % coefi1);
        |HAZ pordos;

        |SI #0#71 ! "S"; #6#152 = #6#152 + (rendimi % coefi1); |FINSI;
        |SI #0#71 = "S"; #6#44  = #6#44  + (rendimi % coefi1); |FINSI;

        |SI #0#71 ! "S"; #6#6   = #6#6   + (rendimi % coefi1); |FINSI;
        |SI #0#71 = "S"; #6#39  = #6#39  + (rendimi % coefi1); |FINSI;

        #6#7   = #6#7   + (#0#9   % coefi1);
        #6#97  = #6#97  + ((#0#14 % coefi1) % 10);
        #6#168 = #6#168 + (#0#14 % coefi1);
    |SINO;
        #6#97  = #6#97  + ((#0#14 % coefi1) % 10);  || PUESTO POR MANOLO
        #6#168 = #6#168 + (#0#14 % coefi1);         ||   ""    ""   ""
        |SI #0#22 = "N";    || No Familiar
            #6#151 = #6#151 + (#0#009 % coefi1);
            suma = #0#10;
            |SI #0#10 > #0#23;
                |SI sw = 0;  |HAZ grabacion;  sw = 1;  |FINSI;
                alfa1 = "INTERESES VIV. ALQUILER AJUSTADOS EN LINEA ";
                alfa1 = alfa1 + #0#1 + " T. DECL. " + tipod;
                |HAZ grabacion;
                suma = #0#23;
            |FINSI;
            #6#151 = #6#151 + (#0#26 % coefi1);
            #6#150 = #6#150 + (suma  % coefi1);
            #6#153 = #6#153 + (#0#29 % coefi1) + (#0#28 % coefi1);
            |SI #0#30 [ 0;
                #6#154 = #6#154 - (#0#30 % coefi1);
            |SINO;
                #6#154 = #6#154 + (#0#30 % coefi1);
            |FINSI;
            #6#155 = #6#155 + (#0#27 % coefi1);
            #6#157 = #6#157 + (#0#23 % coefi1);

            |SI #0#71 ! "S"; #6#158 = #6#158 + (#0#24 % coefi1); |FINSI;
            |SI #0#71 = "S"; #6#45  = #6#45  + (#0#24 % coefi1); |FINSI;

            |SI #0#71 ! "S"; #6#6   = #6#6   + (#0#24 % coefi1); |FINSI;
            |SI #0#71 = "S"; #6#39  = #6#39  + (#0#24 % coefi1); |FINSI;

            #6#159 = #6#159 + (#0#25 % coefi1);
        |SINO;
            suma = #0#10;
            |SI #0#10 > #0#23;
                |SI sw = 0;  |HAZ grabacion;  sw = 1;  |FINSI;
                alfa1 = "INTERESES VIV. ALQUILER AJUSTADOS EN LINEA ";
                alfa1 = alfa1 + #0#1 + " T. DECL. " + tipod;
                |HAZ grabacion;
                suma = #0#23;
            |FINSI;
            |SI #0#30 [ 0;
                suma = (suma+#0#9+#0#26+#0#27+#0#28+#0#29-#0#30) % coefi1;
            |SINO;
                suma = (suma+#0#9+#0#26+#0#27+#0#28+#0#29+#0#30) % coefi1;
            |FINSI;
            sumaalq = ((#0#23 + #0#24 + #0#25) % coefi1);
            |HAZ pordos;
            #6#162 = (rendimi % coefi1);
            #6#163 = sumaalq - suma;
            |SI #6#162 > #6#163; #6#163 = #6#162; |FINSI;
            #6#160 = #6#160 + #6#163;
        |FINSI;
    |FINSI;
  |FINSI;
  |SI #0#13 = "H";  |Y #0#31 = "S";
      |SI #0#66 ! 0;  |O #0#37 ! 0;
          #9#136 = #9#136 + #0#35 - #0#36;
          #9#137 = #9#137 + #0#70;
          #9#138 = #9#138 + #0#37;
          #9#139 = #9#139 + #0#66;
          aaa = (#9#138 + #9#139);
          bbb = aaa / #9#136;
          #9#140 = #9#137 * bbb;
      |FINSI;
  |FINSI;
  #6#8 = (#6#6 + #6#39) - #6#7;
  #6#9 = (#6#157 + #6#159 + #6#160) - (#6#150 + #6#151 + #6#153 + #6#154 + #6#155);
|FPRC;

|PROCESO grabafi1;          || Graba los Rendimientos del Trabajo
  |SI recalcu = 1;  |HAZ leepaga; |FINSI;
  cinco = #6#5;
  #6#1 = #6#1 + #1#7;
  #6#130 = #6#130 + #1#9;
  #6#142 = #6#142 + #1#17;
  #6#143 = #6#143 + #1#18;
  #6#144 = #6#144 + #1#19;
  aaa = #1#7 + #1#15 + #1#16;    ||RENDIMIENTOS INTEGROS;
  |SI #1#2 = "T"; minusva = minusva1; |FINSI;
  |SI #1#2 = "C"; minusva = minusva2; |FINSI;
  |SI #1#2 = "1"; minusva = minusva3; |FINSI;
  |SI #1#2 = "2"; minusva = minusva4; |FINSI;
  |SI #1#2 = "3"; minusva = minusva5; |FINSI;
  |SI #1#2 = "4"; minusva = minusva6; |FINSI;
  |SI #1#2 = "T"; |Y aaa ! 0; #6#194 = 1; |FINSI;
  |SI #1#2 = "C"; |Y aaa ! 0; #6#195 = 1; |FINSI;
  |SI #1#2 = "1"; |Y aaa ! 0; #6#196 = 1; |FINSI;
  |SI #1#2 = "2"; |Y aaa ! 0; #6#197 = 1; |FINSI;
  |SI #1#2 = "3"; |Y aaa ! 0; #6#198 = 1; |FINSI;
  |SI #1#2 = "4"; |Y aaa ! 0; #6#199 = 1; |FINSI;
  |SI minusva = "S";
      #6#220 = #6#220 + #1#20;
  |SINO;
      #6#145 = #6#145 + #1#20;
  |FINSI;
  aaa = #6#220;
  |SI #6#220 > 600000;
      aaa = 600000;
      traba = "600.000";
      |HAZ otrosgas;
  |FINSI;
  |SI #6#145 > 250000;
      aaa = aaa + 250000;
      traba = "250.000";
     |HAZ otrosgas;
  |SINO;
      aaa = aaa + #6#145;
  |FINSI;
  |SI aaa > 600000;
      aaa = 600000;
      traba = "600.000";
      |HAZ otrosgas;
  |FINSI;
  #6#2 = #6#2 + #1#15 + #1#16;
  #6#131 = #6#131 + #1#16;
  #6#4 = #6#4 + #1#12;
  #6#146 = #6#146 + #1#11;
  #6#3 = #6#142 + #6#143 + #6#144 + aaa;
  #6#5 = #6#1 + #6#2 + #6#4 - #6#3;
  #9#150 = #9#150 + #1#27;
  #9#151 = #9#151 + (#1#26 - #1#27);
  |SI tipod = "A"; |O tipod = "T";
      |SI #1#2 = "T";
          #6#200 = #6#200 + #1#11 + #1#12 + #1#13;
          #6#214 = #6#214 + #1#13;
          #6#201 = #6#201 + (#6#5 - cinco);
          #6#202 = #6#202 + #1#26;
      |FINSI;
  |FINSI;
  |SI tipod = "A";  |O tipod = "C";
      |SI #1#2 = "C";
          #6#206 = #6#206 + #1#11 + #1#12 + #1#13;
          #6#215 = #6#215 + #1#13;
          #6#207 = #6#207 + (#6#5 - cinco);
          #6#208 = #6#208 + #1#26;
      |FINSI;
  |FINSI;
  #6#135 = #6#135 + #1#23;
  traba = #1#14 % 102;
  |SI traba = "**";
      #9#155 = #9#155 + #1#15;
  |FINSI;
|FPRC;

|PROCESO grabafi2;
reten = #2#21;
|SI coefi1 ! 100;
    #2#21 = #2#21 - 1 + .49;
    |SI #2#21 = -1; #2#21 = 0; |FINSI;
|FINSI;

#6#10 = #6#10 + (#2#20 % coefi1);
#6#12 = #6#12 + ((#2#22 + #2#23) % coefi1);
#6#132 = #6#132 + (#2#21 % coefi1);

|SI #2#42  = "S";
    #6#105 = #6#105 + ((#2#20 % coefi1) % 10);
|FINSI;

#9#2 = #9#2 + (#2#21 % coefi1);
#9#8 = #9#8 + (#2#20 % coefi1);
#9#14 = #9#14 + ((#2#22 + #2#23) % coefi1);

#2#21  = reten;
|FPRC;

|PROCESO grabafi3;
reten = #3#20;
|SI coefi1 ! 100;
    #3#20 = #3#20 - 1 + .49;
    |SI #3#20 = -1; #3#20 = 0; |FINSI;
|FINSI;

#6#10 = #6#10 + (#3#19 % coefi1);
#6#12 = #6#12 + ((#3#21 + #3#22) % coefi1);
#6#132 = #6#132 + (#3#20 % coefi1);

#9#2 = #9#2 + (#3#20 % coefi1);
#9#8 = #9#8 + (#3#19 % coefi1);
#9#14 = #9#14 + ((#3#21 + #3#22) % coefi1);

|SI #3#44 = "S";
    #6#105 = #6#105 + ((#3#019 % coefi1) % 10);
|FINSI;

#3#20  = reten;
|FPRC;

|PROCESO grabafi4;
reten = #4#18;
|SI coefi1 ! 100;
    #4#18 = #4#18 - 1 + .49;
    |SI #4#18 = -1; #4#18 = 0; |FINSI;
|FINSI;

|SI #4#29 = "S";
    #6#105 = #6#105  + (#4#17 % coefi1);
|FINSI;
|SI #4#13 = "RE";
    #9#23 = #9#23 + (#4#18 % coefi1);
    #6#12 = #6#12 + ((#4#21 + #4#22) % coefi1);
    #9#14 = #9#14 + ((#4#21 + #4#22) % coefi1);
    |ACABA;
|FINSI;
|SI #4#26 = "R";
    |SI #4#13 = "CC";  |O #4#13 = "CD";   |O #4#13 = "LB";  |O #4#13 = "IP";
        #9#3 = #9#3 + (#4#18 % coefi1);
        #9#9 = #9#9 + (#4#17 % coefi1);
        #9#14 = #9#14 + ((#4#21 + #4#22) % coefi1);
    |FINSI;
    |SI #4#13 = "AF";
        #9#4 = #9#4 + (#4#18 % coefi1);
        #9#10 = #9#10 + (#4#17 % coefi1);
        #9#14 = #9#14 + ((#4#21 + #4#22) % coefi1);
    |FINSI;
    |SI #4#13 = "AB";
        #9#5  = #9#5 + (#4#18 % coefi1);
        #9#11 = #9#11 + (#4#17 % coefi1);
        #9#14 = #9#14 + (#4#21  % coefi1);
        #9#15 = #9#15 + (#4#22 % coefi1);
    |FINSI;
    |SI #4#13 = "RV";  |O #4#13 = "RT";
        #9#6 = #9#6 + (#4#18 % coefi1);
        #9#12 = #9#12 + (#4#17 % coefi1);
        #9#14 = #9#14 + ((#4#21 + #4#22) % coefi1);
    |FINSI;
    |SI #4#13 = "OR";  |O #4#13 = "PI";   |O #4#13 = "SI";
        #9#7 = #9#7 + (#4#18 % coefi1);
        #9#13 = #9#13 + (#4#17 % coefi1);
        #9#14 = #9#14 + ((#4#21 + #4#22) % coefi1);
    |FINSI;

    |SI #4#13 = "PA"; |O #4#13 = "LT";
        |SI #4#18 ! 0;
            #9#7 = #9#7 + (#4#18 % coefi1);
            #9#13 = #9#13 + (#4#17 % coefi1);
            #6#10 = #6#10 + (#4#17 % coefi1);
            #9#14 = #9#14 + ((#4#21 + #4#22) % coefi1);
        |SINO;
            #6#11 = #6#11 + (#4#17 % coefi1);
            #9#14 = #9#14 + ((#4#21 + #4#22) % coefi1);
        |FINSI;
    |SINO;
        #6#10 = #6#10 + (#4#17 % coefi1);
    |FINSI;

    #6#12  = #6#12 + ((#4#21 + #4#22) % coefi1);
    #6#132 = #6#132 + (#4#18 % coefi1);
|SINO;
    #6#135  = #6#135 + (#4#18 % coefi1);
    liquido = (#4#17 - #4#21 - #4#22) % coefi1;
    |SI #4#27 = 0;  #4#27 = 1;  |FINSI;
    anual   = liquido / #4#27;
    resto   = liquido - anual;
    #9#150 = #9#150 + anual;
    #9#151 = #9#151 + resto;
|FINSI;
#4#18  = reten;
|FPRC;

|| **************************************************************************
                           PROCESO DE INCIDENCIAS
|| **************************************************************************

|PROCESO incidenci1;
|SI incisino = "S";
   |SI sw = 0; |HAZ grabacion; sw = 1; |FINSI;

   linea = "00" + linea;
   linea = linea % -102;

   rendi = "          " + rendi;
   rendi = rendi % -110;

   |QBF descri;
   descri = descri + blanco;
   descri = descri % 130;

   alfa1 = "NO CALCULO ->  " + linea + " " + descri + "    " + rendi;

   |SI tipore = "R";
       alfa1 = "RENTA ------>  " + linea + " " + descri + "    " + rendi;
   |FINSI;
   |SI tipore = "P";
       alfa1 = "PATRIMONIO ->  " + linea + " " + descri + "    " + rendi;
   |FINSI;
   |HAZ grabacion;
|FINSI;
|FPRC;

|PROCESO grabacion;
swcalcu = 1;
lininci = lininci + 5;
#5#0 = empreac;
#5#1 = lininci;
#5#2 = alfa1;
#5#3 = "S";
|GRABA 020#5n;
|LIBERA #5;
|FPRC;

|| **************************************************************************
                        PROCESO BUCLES
|| **************************************************************************

|DEFBUCLE leefic1;   || Bucle Rendimientos del Trabajo
#1#1;
;
empresa, 01;
emprefi, 99;
e;
NULL, fichero1;
|FIN;

|DEFBUCLE leefic0;    || Bucle del Capital Inmobiliario
#0#1;
;
empresa, 01;
emprefi, 99;
e;
NULL, fichero0;
|FIN;

|DEFBUCLE leefic2;    || Bucle Valor Bolsa
#2#1;
;
empresa, 01;
emprefi, 99;
e;
NULL, fichero2;
|FIN;

|DEFBUCLE leefic3;    || Bucle No Valor Bolsa
#3#1;
;
empresa, 01;
emprefi, 99;
e;
NULL, fichero3;
|FIN;

|DEFBUCLE leefic4;    || Bucle No Valor Bolsa
#4#1;
;
empresa, 01;
emprefi, 99;
e;
NULL, fichero4;
|FIN;

|| **************************************************************************
                            INICIO
|| **************************************************************************

|PROGRAMA;
  |ABRE #10;
  |ABRE #5;
  |ABRE #6;
  |ABRE #9;
|SI recalcu = 0;
    #10#0 = empresa;
    |LEE 040#10=;
    |SI FSalida = 0;
        lininci = 0;
        swpasa = 0;
        |SI #10#1=1; sw=0; lininci = 0;    |BUCLE leefic1;  swpasa = 1; |FINSI;
        |SI #10#2=1; sw=0; lininci = 300;  |BUCLE leefic0;  swpasa = 1; |FINSI;
        |SI #10#3=1; sw=0; lininci = 600;  |BUCLE leefic2;  swpasa = 1; |FINSI;
        |SI #10#4=1; sw=0; lininci = 900;  |BUCLE leefic3;  swpasa = 1; |FINSI;
        |SI #10#5=1; sw=0; lininci = 1200; |BUCLE leefic4;  swpasa = 1; |FINSI;
    |FINSI;
|SINO;
    swpasa = 0;
    sw = 0; lininci = 0;    |BUCLE leefic1;  swpasa = 1;
    sw = 0; lininci = 300;  |BUCLE leefic0;  swpasa = 1;
    sw = 0; lininci = 600;  |BUCLE leefic2;  swpasa = 1;
    sw = 0; lininci = 900;  |BUCLE leefic3;  swpasa = 1;
    sw = 0; lininci = 1200; |BUCLE leefic4;  swpasa = 1;
|FINSI;

  |CIERRA #10;
  |CIERRA #5;
  |CIERRA #6;
  |CIERRA #9;

|SI #10#6=1; |O #10#7=1; |O #10#8=1; |O #10#9=1; |O #10#10=1; |O recalcu=1;
    |CORRE "recalcu2.run";
    |ACABA;
|FINSI;

|SI #10#11=1; |O #10#12=1; |O #10#13=1; |O #10#14=1; |O #10#15=1; |O recalcu=1;
    |CORRE "recalcu3.run";
    |ACABA;
|FINSI;

|SI #10#16=1; |O #10#17=1; |O #10#18=1; |O #10#19=1; |O #10#20=1; |O recalcu=1;
    |CORRE "recalcu4.run";
    |ACABA;
|FINSI;

|SI #10#21=1; |O #10#22=1; |O #10#23=1; |O #10#24=1; |O #10#25=1; |O recalcu=1;
    |CORRE "recalcu5.run";
    |ACABA;
|FINSI;

|SI #10#26=1; |O #10#27=1; |O #10#28=1; |O #10#29=1; |O recalcu=1;
    |CORRE "recalcu6.run";
    |ACABA;
|FINSI;

|SI swpasa = 0; |Y #10#30 = 1; |CORRE "recalcu7.run"; |ACABA; |FINSI;

|SI swpasa = 1; |CORRE "recalcu8.run"; |ACABA; |FINSI;

|SI acceso = 1;
    |CORRE "reacceso.run";
|SINO;
    |CORRE "reacceso.run";
|FINSI;
|FPRO;

|PROCESO pordos;
  rendimi = 0;
  feini = "01.01.1994";
  fefin = "31.12.1994";
  |SI #0#18 > "01.01.1994";
      feini = #0#18;
  |FINSI;
  |SI #0#31 = "S";
      fefin = #0#34;
  |FINSI;
  dias = fefin - feini + 1;
  valor1 = mayor * dias / 365;

  |SI #0#71 = "S";
      rendimi = valor1 % 1.3;
  |SINO;
      rendimi = valor1 % 2;
  |FINSI;
|FPRC;

|PROCESO grabapat;
|SI cantidad = 0; |ACABA; |FINSI;
|ABRE #8;
#8#0  = empreac;
#8#28 = tipod;
|LIBERA #8;
|LEE 110#8=;
FEstad2 = FSalida;
|SI FSalida ! 0; |DDEFECTO #8; #8#0 = empreac; #8#28 = tipod; |FINSI;
|SI tipop = "A1"; #8#1  = #8#1  + cantidad; |FINSI;
|SI tipop = "B1"; #8#2  = #8#2  + cantidad; |FINSI;
|SI tipop = "C1"; #8#3  = #8#3  + cantidad; |FINSI;
|SI tipop = "C2"; #8#3  = #8#3  + cantidad; |FINSI;
|SI tipop = "C3"; #8#3  = #8#3  + cantidad; |FINSI;
|SI tipop = "C3"; #8#46 = #8#46 + cantidad; |FINSI;    || exentos a parte
|SI tipop = "D1"; #8#4  = #8#4  + cantidad; |FINSI;
|SI tipop = "E1"; #8#5  = #8#5  + cantidad; |FINSI;
|SI tipop = "E2"; #8#6  = #8#6  + cantidad; |FINSI;
|SI tipop = "F1"; #8#7  = #8#7  + cantidad; |FINSI;
|SI tipop = "F2"; #8#8  = #8#8  + cantidad; |FINSI;
|SI tipop = "F3"; #8#44 = #8#44 + cantidad; |FINSI;
|SI tipop = "F4"; #8#45 = #8#45 + cantidad; |FINSI;
|SI tipop = "F5"; #8#45 = #8#45 + cantidad; |FINSI;
|SI tipop = "F5"; #8#47 = #8#47 + cantidad; |FINSI;    || exentos a parte
|SI tipop = "H1"; #8#10 = #8#10 + cantidad; |FINSI;
|SI FEstad2 = 0; |GRABA 020#8a; |SINO; |GRABA 020#8n; |FINSI;
|CIERRA #8;

#10#0 = empreac;
|LIBERA #10;
|LEE 101#10=;
|SI FSalida = 0;
    #10#30 = 1;
    |GRABA 020#10a;
|FINSI;
|FPRC;

|PROCESO otrosgas;
  |SI sw = 0; |HAZ grabacion;   sw = 1;  |FINSI;
  alfa1 = "OTROS GASTOS AJUSTADOS A: " + traba + " T. DECL.: ";
  alfa1 = alfa1 + tipod;
  |HAZ grabacion;
|FPRC;

|PROCESO leepaga;
  |ABRE #7;
  #7#0 = empreac;
  |LEE 040#7=;
  |SI FSalida ! 0; |DDEFECTO #7; |FINSI;
  minusva1 = #7#5;
  minusva2 = #7#31;
  minusva3 = #7#88;
  minusva4 = #7#89;
  minusva5 = #7#90;
  minusva6 = #7#91;
  individu = 0;
  |SI #7#34 = "X";
      individu = 1;
  |FINSI;
  |CIERRA #7;
|FPRC;
