|FICHEROS;
    repasdec #0;   ||
    redatcon #1;   ||  Datos del Contribuyente 1994.
    m2totrea #2;   ||  retotrea 1993.
    repasde1 #3;   ||  Temporal pase deducciones.
    rededu01 #4;   ||  Deducciones Pendientes ejercicios anteriores 1994.
    rededu02 #5;   ||  Deducciones Ejercicio actual 1994.
    rededu03 #6;   ||  Deducciones Canarias 1994.
    rededu04 #7;   ||  Deducciones Expo, etc..., 1994.
    rededu05 #8;   ||  Deducciones Creacion de empleo 1994.
    reprogra #9;
    m2datcon #10;  ||  Datos del Contribuyente 1993.
    m2dedu01 #14;  ||  Deducciones Pendientes ejercicios anteriores 1993.
    m2dedu02 #15;  ||  Deducciones Ejercicio actual 1993.
    m2dedu03 #16;  ||  Deducciones Canarias 1993.
    m2dedu04 #17;  ||  Deducciones Expo, etc..., 1993.
    m2dedu05 #18;  ||  Deducciones Creacion de empleo 1993.
|FIN;

|VARIABLES;
    pen_1      = 0;
    pen_2      = 0;
    pen_3      = 0;
    pen_4      = 0;
    pen_5      = 0;
    apl_1      = 0;
    apl_2      = 0;
    apl_3      = 0;
    apl_4      = 0;
    apl_5      = 0;
    sw_fich03  = 0;
    sw_fich04  = 0;
    fichero = "";
    path   = "";
    path93 = "";
    path94 = "";
    tipdec_ini = "";
    tipdec_fin = "";
    tipo_ini = "";
    tipo_fin = "";
    any_min = 89;
    any_max = 93;
    orden_rededu = 0;
    camtmp = 0;
    camred = 0;
    campte = 0;
    camapl = 0;
    existe_emp = 0;
    existe_red = 0;
    linea_red1 = 0;
    linea_red2 = 0;
    linea_red3 = 0;
    linea_red4 = 0;
    linea_red5 = 0;
|FIN;

|PROCESO graba_05;
         |SI #0#2 = "N";
             |SI linea_red5 = 0;
                 FSalida = 0;
                 #8#0 = #3#0;      ||  Codigo de empresa.
                 #8#1 = 0;
                 |PARA; |SI FSalida = 0; |Y #8#0 = #3#0; |HACIENDO;
                        linea_red5 = #8#1;
                        |LEE 001#8>;
                 |SIGUE;
             |FINSI;
             existe_red = 1;
         |SINO;
             #8#0 = #3#0;  ||  Codigo de empresa.
             #8#1 = #3#1;  ||  Numero de linea.
             |LEE 101#8=;
             existe_red = FSalida;
         |FINSI;
         #3#9 = #3#10;              ||  Saldo = Aplicado.
         #8#0 = #3#0;               ||  Codigo de empresa.
         #8#1 = #3#1 + linea_red5;  ||  Numero de linea.
         #8#2 = #3#2;               ||  Tipo de declaracion.
         #8#3 = #3#3;               ||  Perceptor.
         #8#4 = #3#5;               ||  A¤o.
         #8#5 = #3#7;               ||  Descripcion.
         |PARA camtmp = 9; |SI camtmp < 21; |HACIENDO camtmp = camtmp + 1;
               camred = camtmp - 3;
               #8camred = #3camtmp;
         |SIGUE;
         |SI existe_red = 0;
             |GRABA 020#8a;
         |SINO;
             |GRABA 020#8b;
         |FINSI;
         |LIBERA #8;
         #9#24 = 1;
|FPRC;

|PROCESO trata_05;
         |SI #3#5 = 88; |ACABA; |FINSI;

         |SI #3#5 = 89; campte = 131; camapl = 126; |FINSI;
         |SI #3#5 = 90; campte = 132; camapl = 127; |FINSI;
         |SI #3#5 = 91; campte = 133; camapl = 128; |FINSI;
         |SI #3#5 = 92; campte = 134; camapl = 129; |FINSI;
         |SI #3#5 = 93; campte = 248; camapl = 245; |FINSI;

         |SI #2campte > 0;
             |SI #2camapl > 0;
                 |SI #3#10 > #2camapl;
                     #3#10 = #3#10 - #2camapl;
                     #2camapl = 0;
                     |HAZ graba_05;
                 |SINO;
                     #2camapl = #2camapl - #3#10;
                 |FINSI;
             |SINO;
                 |HAZ graba_05;
             |FINSI;
         |FINSI;
|FPRC;

|PROCESO graba_04;
         |SI #0#2 = "N";
             |SI linea_red4 = 0;
                 FSalida = 0;
                 #7#0 = #3#0;      ||  Codigo de empresa.
                 #7#1 = 0;
                 |PARA; |SI FSalida = 0; |Y #7#0 = #3#0; |HACIENDO;
                        linea_red4 = #7#1;
                        |LEE 001#7>;
                 |SIGUE;
             |FINSI;
             existe_red = 1;
         |SINO;
             #7#0 = #3#0;  ||  Codigo de empresa.
             #7#1 = #3#1;  ||  Numero de linea.
             |LEE 101#7=;
             existe_red = FSalida;
         |FINSI;
         #3#9 = (#3#10 * 100) / #3#8;         ||  Saldo = Aplicado % Deduccion.
         #7#0 = #3#0;               ||  Codigo de empresa.
         #7#1 = #3#1 + linea_red4;  ||  Numero de linea.
         |PARA camtmp = 2; |SI camtmp < 21; |HACIENDO camtmp = camtmp + 1;
               #7camtmp = #3camtmp;
         |SIGUE;
         |SI existe_red = 0;
             |GRABA 020#7a;
         |SINO;
             |GRABA 020#7b;
         |FINSI;
         |LIBERA #7;
         #9#23 = 1;
|FPRC;

|PROCESO trata_04;
         |SI sw_fich04 = 0;
             sw_fich04 = 1;
             pen_1 = #2#099 + #2#100 + #2#101;
             pen_2 = #2#246 + #2#247;
             pen_3 = #2#113 + #2#114 + #2#115 + #2#116 + #2#117;

             apl_1 = #2#096 + #2#097 + #2#098;
             apl_2 = #2#240 + #2#242;
             apl_3 = #2#108 + #2#109 + #2#110 + #2#111 + #2#112;
         |FINSI;

         |SI #3#21 = 3; |O #3#21 = 4; |O #3#21 = 5;
             |SI #3#5 < 93;
                 |SI pen_1 > 0;
                     |SI apl_1 > 0;
                         |SI #3#10 > apl_1;
                             #3#10 = #3#10 - apl_1;
                             apl_1 = 0;
                             |HAZ graba_04;
                         |SINO;
                             apl_1 = apl_1 - #3#10;
                         |FINSI;
                     |SINO;
                         |HAZ graba_04;
                     |FINSI;
                 |FINSI;
             |SINO;
                 |SI pen_3 > 0;
                     |SI apl_3 > 0;
                         |SI #3#10 > apl_3;
                             #3#10 = #3#10 - apl_3;
                             apl_3 = 0;
                             |HAZ graba_04;
                         |SINO;
                             apl_3 = apl_3 - #3#10;
                         |FINSI;
                     |SINO;
                         |HAZ graba_04;
                     |FINSI;
                 |FINSI;
             |FINSI;
         |SINO;
             |SI #3#5 < 93;
                 |SI pen_2 > 0;
                     |SI apl_2 > 0;
                         |SI #3#10 > apl_2;
                             #3#10 = #3#10 - apl_2;
                             apl_2 = 0;
                             |HAZ graba_04;
                         |SINO;
                             apl_2 = apl_2 - #3#10;
                         |FINSI;
                     |SINO;
                         |HAZ graba_04;
                     |FINSI;
                 |FINSI;
             |SINO;
                 |SI pen_3 > 0;
                     |SI apl_3 > 0;
                         |SI #3#10 > apl_3;
                             #3#10 = #3#10 - apl_3;
                             apl_3 = 0;
                             |HAZ graba_04;
                         |SINO;
                             apl_3 = apl_3 - #3#10;
                         |FINSI;
                     |SINO;
                         |HAZ graba_04;
                     |FINSI;
                 |FINSI;
             |FINSI;
         |FINSI;
|FPRC;

|PROCESO graba_03;
         |SI #0#2 = "N";
             |SI linea_red3 = 0;
                 FSalida = 0;
                 #6#0 = #3#0;      ||  Codigo de empresa.
                 #6#1 = 0;
                 |PARA; |SI FSalida = 0; |Y #6#0 = #3#0; |HACIENDO;
                        linea_red3 = #6#1;
                        |LEE 001#6>;
                 |SIGUE;
             |FINSI;
             existe_red = 1;
         |SINO;
             #6#0 = #3#0;  ||  Codigo de empresa.
             #6#1 = #3#1;  ||  Numero de linea.
             |LEE 101#6=;
             existe_red = FSalida;
         |FINSI;
         #3#9 = (#3#10 * 100) / #3#8;         ||  Saldo = Aplicado % Deduccion.
         #6#0 = #3#0;               ||  Codigo de empresa.
         #6#1 = #3#1 + linea_red3;  ||  Numero de linea.
         #6#2 = #3#2;               ||  Tipo de declaracion.
         #6#3 = #3#3;               ||  Perceptor.
         #6#4 = #3#4;               ||  Tipo de deduccion;
         |PARA camtmp = 6; |SI camtmp < 21; |HACIENDO camtmp = camtmp + 1;
               camred = camtmp - 1;
               #6camred = #3camtmp;
         |SIGUE;
         #6#20 = #3#5;
         |SI existe_red = 0;
             |GRABA 020#6a;
         |SINO;
             |GRABA 020#6b;
         |FINSI;
         |LIBERA #6;
         #9#22 = 1;
|FPRC;

|PROCESO trata_03;
         |SI sw_fich03 = 0;
             sw_fich03 = 1;
             apl_4 = #2#214 + #2#215 + #2#216 + #2#217 + #2#218 + #2#219;
             pen_4 = #2#227;
             apl_5 = #2#80 + #2#81 + #2#82 + #2#83 + #2#84 + #2#85;
             pen_5 = #2#92;
         |FINSI;

         |SI #3#5 < 93;
             |SI pen_4 > 0;
                 |SI apl_4 > 0;
                     |SI #3#10 > apl_4;
                         #3#10 = #3#10 - apl_4;
                         apl_4 = 0;
                         |HAZ graba_03;
                     |SINO;
                         apl_4 = apl_4 - #3#10;
                     |FINSI;
                 |SINO;
                    |HAZ graba_03;
                 |FINSI;
             |FINSI;
         |SINO;
             |SI pen_5 > 0;
                 |SI apl_5 > 0;
                     |SI #3#10 > apl_5;
                         #3#10 = #3#10 - apl_5;
                         apl_5 = 0;
                         |HAZ graba_03;
                     |SINO;
                         apl_5 = apl_5 - #3#10;
                     |FINSI;
                 |SINO;
                    |HAZ graba_03;
                 |FINSI;
             |FINSI;
         |FINSI;
|FPRC;

|PROCESO graba_02;  ||  ATENCION!  No graba en rededu02 sino en rededu01.
         |SI linea_red2 = 0;
             FSalida = 0;
             #4#0 = #3#0;      ||  Codigo de empresa.
             #4#1 = 0;
             |PARA; |SI FSalida = 0; |Y #4#0 = #3#0; |HACIENDO;
                    linea_red2 = #4#1;
                    |LEE 001#4>;
             |SIGUE;
         |FINSI;
         #3#9 = #3#10;              ||  Saldo = Aplicado.
         #4#0 = #3#0;               ||  Codigo de empresa.
         #4#1 = #3#1 + linea_red2;  ||  Numero de linea.
         #4#2 = #3#2;               ||  Tipo de declaracion.
         #4#3 = #3#3;               ||  Perceptor.
         |PARA camtmp = 5; |SI camtmp < 21; |HACIENDO camtmp = camtmp + 1;
               camred = camtmp - 1;
               #4camred = #3camtmp;
         |SIGUE;
         |GRABA 020#4b;
         |LIBERA #4;
         #9#20 = 1;
|FPRC;

|PROCESO trata_02;
         |SI #3#4 = "AF"; campte = 66; |FINSI;
         |SI #3#4 = "EL"; campte = 67; |FINSI;
         |SI #3#4 = "AE"; campte = 68; |FINSI;
         |SI #3#4 = "GI"; campte = 69; |FINSI;
         |SI #3#4 = "GA"; campte = 70; |FINSI;
         |SI #3#4 = "BI"; campte = 71; |FINSI;
         |SI #2campte > 0;
             camapl = campte -  6;
             |SI #2camapl > 0;
                 |SI #3#10 > #2camapl;
                     #3#10 = #3#10 - #2camapl;
                     #2camapl = 0;
                     |HAZ graba_02;
                 |SINO;
                     #2camapl = #2camapl - #3#10;
                 |FINSI;
             |SINO;
                 |HAZ graba_02;
             |FINSI;
         |FINSI;
|FPRC;

|PROCESO graba_01;
         |SI #0#2 = "N";
             |SI linea_red1 = 0;
                 FSalida = 0;
                 #4#0 = #3#0;      ||  Codigo de empresa.
                 #4#1 = 0;
                 |PARA; |SI FSalida = 0; |Y #4#0 = #3#0; |HACIENDO;
                        linea_red1 = #4#1;
                        |LEE 001#4>;
                 |SIGUE;
             |FINSI;
             existe_red = 1;
         |SINO;
             #4#0 = #3#0;  ||  Codigo de empresa.
             #4#1 = #3#1;  ||  Numero de linea.
             |LEE 101#4=;
             existe_red = FSalida;
         |FINSI;
         #3#9 = #3#10;              ||  Saldo = Aplicado.
         #4#0 = #3#0;               ||  Codigo de empresa.
         #4#1 = #3#1 + linea_red1;  ||  Numero de linea.
         #4#2 = #3#2;               ||  Tipo de declaracion.
         #4#3 = #3#3;               ||  Perceptor.
         |PARA camtmp = 5; |SI camtmp < 21; |HACIENDO camtmp = camtmp + 1;
               camred = camtmp - 1;
               #4camred = #3camtmp;
         |SIGUE;
         |SI existe_red = 0;
             |GRABA 020#4a;
         |SINO;
             |GRABA 020#4b;
         |FINSI;
         |LIBERA #4;
         #9#20 = 1;
|FPRC;

|PROCESO trata_01;
                            ||  A partir del a¤o calculo el campo pendiente.
         |SI #3#5 = 89; campte = 50;   camapl = 46;  |FINSI;
         |SI #3#5 = 90; campte = 51;   camapl = 47;  |FINSI;
         |SI #3#5 = 91; campte = 52;   camapl = 48;  |FINSI;
         |SI #3#5 = 92; campte = 206;  camapl = 205; |FINSI;

         |SI #2campte > 0;
             |SI #2camapl > 0;
                 |SI #3#10 > #2camapl;
                     #3#10 = #3#10 - #2camapl;
                     #2camapl = 0;
                     |HAZ graba_01;
                 |SINO;
                     #2camapl = #2camapl - #3#10;
                 |FINSI;
             |SINO;
                 |HAZ graba_01;
             |FINSI;
         |FINSI;
|FPRC;

|PROCESO control_tmp;
         |SI #3#21 = 1;
             |HAZ trata_01;
         |SINO;
             |SI #3#21 = 2;
                 |HAZ trata_03;
             |SINO;
                 |SI #3#21 = 8;
                     |HAZ trata_02;
                 |SINO;
                     |SI #3#21 = 9;
                         |HAZ trata_05;
                     |SINO;
                         |HAZ trata_04;
                     |FINSI;
                 |FINSI;
             |FINSI;
         |FINSI;
         |BORRA 020#3a;
|FPRC;

|DEFBUCLE temporal;
          #3#1;
          ;
          #1#0, any_min, 1, "N", 01;
          #1#0, any_max, 9, "S", 99;
          e;
          NULL,control_tmp;
|FIN;

|PROCESO carga_05;  ||  Carga en temporal Deducciones Creacion empleo.
         #3#0 = #18#0;  ||  Codigo de empresa.
         #3#1 = #18#1;  ||  Numero de linea.
         #3#2 = #18#2;  ||  Tipo de declaracion.
         #3#3 = #18#3;  ||  Perceptor.
         #3#4 = "";     ||  Tipo de deduccion.
         #3#5 = #18#4;  ||  A¤o.
         #3#6 = 0;      ||  % Limite;
         #3#7 = #18#5;  ||  Descripcion.
         #3#8 = 0;      ||  % Deduccion.
         |PARA camtmp = 9; |SI camtmp < 21; |HACIENDO camtmp = camtmp + 1;
               camred = camtmp - 3;
               #3camtmp = #18camred;
         |SIGUE;
         #3#21 = orden_rededu;
|FPRC;

|PROCESO carga_04;  ||  Carga en temporal Deducciones Expo, etc. (rededu04)
         |PARA camtmp = 0; |SI camtmp < 21; |HACIENDO camtmp = camtmp + 1;
               #3camtmp = #17camtmp;
         |SIGUE;
         |SI #17#4 = "E"; #3#21 = 3; |FINSI;
         |SI #17#4 = "J"; #3#21 = 4; |FINSI;
         |SI #17#4 = "M"; #3#21 = 5; |FINSI;
         |SI #17#4 = "1"; #3#21 = 6; |FINSI;
         |SI #17#4 = "2"; #3#21 = 7; |FINSI;
|FPRC;

|PROCESO carga_03;  ||  Carga en temporal Deducciones Canarias (rededu03).
         #3#0 = #16#0;  ||  Codigo de empresa.
         #3#1 = #16#1;  ||  Numero de linea.
         #3#2 = #16#2;  ||  Tipo de declaracion.
         #3#3 = #16#3;  ||  Perceptor.
         #3#4 = #16#4;  ||  Tipo de deduccion.
         #3#5 = #16#20; ||  A¤o.
         |PARA camtmp = 6; |SI camtmp < 21; |HACIENDO camtmp = camtmp + 1;
               camred = camtmp - 1;
               #3camtmp = #16camred;
         |SIGUE;
         #3#21 = orden_rededu;
|FPRC;

|PROCESO carga_02;  ||  Carga en temporal Deducciones ejercicio actual (rededu02).
         #3#0 = #15#0;  ||  Codigo de empresa.
         #3#1 = #15#1;  ||  Numero de linea.
         #3#2 = #15#2;  ||  Tipo de declaracion.
         #3#3 = #15#3;  ||  Perceptor.
         #3#4 = #15#4;  ||  Tipo de deduccion.
         #3#5 = 93;     ||  A¤o.
         |PARA camtmp = 6; |SI camtmp < 21; |HACIENDO camtmp = camtmp + 1;
               camred = camtmp - 1;
               #3camtmp = #15camred;
         |SIGUE;
         #3#21 = orden_rededu;
|FPRC;

|PROCESO carga_01;  ||  Carga en temporal Deducciones pendientes (rededu01).
         #3#0 = #14#0;  ||  Codigo de empresa.
         #3#1 = #14#1;  ||  Numero de linea.
         #3#2 = #14#2;  ||  Tipo de declaracion.
         #3#3 = #14#3;  ||  Perceptor.
         #3#4 = "";     ||  Tipo de deduccion.
         |PARA camtmp = 5; |SI camtmp < 21; |HACIENDO camtmp = camtmp + 1;
               camred = camtmp - 1;
               #3camtmp = #14camred;
         |SIGUE;
         #3#21 = orden_rededu;
|FPRC;

|PROCESO graba_tmp;
         |DDEFECTO #3;
         |SI orden_rededu = 1; |HAZ carga_01; |FINSI;
         |SI orden_rededu = 2; |HAZ carga_03; |FINSI;
         |SI orden_rededu = 3; |HAZ carga_04; |FINSI;
         |SI orden_rededu = 8; |HAZ carga_02; |FINSI;
         |SI orden_rededu = 9; |HAZ carga_05; |FINSI;
         |GRABA 020#3n;
         |SI #3#20 = "N"; |Y #3#11 > 0;
             #3#10 = #3#11;
             #3#11 = 0;
             #3#20 = "S";
             |GRABA 020#3n;
         |FINSI;
|FPRC;

|DEFBUCLE rededu05;
          #18#1;
          2,4;
          #1#0, 01, tipdec_ini, any_min;
          #1#0, 99, tipdec_fin, any_max;
          e;
          NULL,graba_tmp;
|FIN;

|DEFBUCLE rededu04;
          #17#1;
          2,5;
          #1#0,01,tipdec_ini,any_min;
          #1#0,99,tipdec_fin,any_max;
          e;
          NULL,graba_tmp;
|FIN;

|DEFBUCLE rededu03;
          #16#1;
          2;
          #1#0,01,tipdec_ini;
          #1#0,99,tipdec_fin;
          e;
          NULL,graba_tmp;
|FIN;

|DEFBUCLE rededu02;
          #15#1;
          2;
          #1#0,01,tipdec_ini;
          #1#0,99,tipdec_fin;
          e;
          NULL,graba_tmp;
|FIN;

|DEFBUCLE rededu01;
          #14#1;
          2,4;
          #1#0, 01, tipdec_ini, any_min;
          #1#0, 99, tipdec_fin, any_max;
          e;
          NULL,graba_tmp;
|FIN;

|PROCESO calcula;
         #2#049 = #2#041 - #2#045;
         #2#050 = #2#042 - #2#046;
         #2#051 = #2#043 - #2#047;
         #2#052 = #2#044 - #2#048;

         #2#066 = #2#053 - #2#060;
         #2#067 = #2#054 - #2#061;
         #2#068 = #2#055 - #2#062;
         #2#069 = #2#056 - #2#063;
         #2#070 = #2#057 - #2#064;
         #2#071 = #2#058 - #2#065;
         #2#059 = #2#041 + #2#042 + #2#043 + #2#044 + #2#053 + #2#054 + #2#055;
         #2#059 = #2#059 + #2#056 + #2#057 + #2#058;
         #2#072 = #2#049 + #2#050 + #2#051 + #2#052 + #2#066 + #2#067 + #2#068;
         #2#072 = #2#072 + #2#069 + #2#070 + #2#071;

         #2#086 = #2#073 - #2#080;
         #2#087 = #2#074 - #2#081;
         #2#088 = #2#075 - #2#082;
         #2#089 = #2#076 - #2#083;
         #2#090 = #2#077 - #2#084;
         #2#091 = #2#078 - #2#085;
         #2#079 = #2#073 + #2#074 + #2#075 + #2#076 + #2#077 + #2#078;
         #2#092 = #2#086 + #2#087 + #2#088 + #2#089 + #2#090 + #2#091;

         #2#099 = #2#093 - #2#096;
         #2#100 = #2#094 - #2#097;
         #2#101 = #2#095 - #2#098;
         #2#113 = #2#102 - #2#108;
         #2#114 = #2#103 - #2#109;
         #2#115 = #2#104 - #2#110;
         #2#116 = #2#105 - #2#111;
         #2#117 = #2#106 - #2#112;
         #2#107 = #2#093 + #2#094 + #2#095 + #2#102 + #2#103 + #2#104 + #2#105 + #2#106;
         #2#118 = #2#099 + #2#100 + #2#101 + #2#113 + #2#114 + #2#115 + #2#116 + #2#117;

         #2#130 = #2#119 - #2#125;
         #2#131 = #2#120 - #2#126;
         #2#132 = #2#121 - #2#127;
         #2#133 = #2#122 - #2#128;
         #2#134 = #2#123 - #2#129;
         #2#124 = #2#119 + #2#120 + #2#121 + #2#122 + #2#123;
         #2#135 = #2#130 + #2#131 + #2#132 + #2#133 + #2#134;
|FPRC;

|PROCESO deduce;
         |SI #2#1 ! "A"; |O #1#35 = "X";
             |HAZ calcula;
             |ABRE #3;
             |SI #1#35 = " ";
                 tipdec_ini = #2#1;
                 tipdec_fin = #2#1;
             |FINSI;
             orden_rededu = 1; |BUCLE rededu01;
             orden_rededu = 2; |BUCLE rededu03;
             orden_rededu = 3; |BUCLE rededu04;
             orden_rededu = 8; |BUCLE rededu02;
             orden_rededu = 9; |BUCLE rededu05;
             |CIERRA #3;
             |ABRE #4;
             |ABRE #6;
             |ABRE #7;
             |ABRE #8;
             linea_red1 = 0;
             linea_red2 = 0;
             linea_red3 = 0;
             linea_red4 = 0;
             linea_red5 = 0;
             |HAZ borra_var;
             |BUCLE temporal;
             |CIERRA #4;
             |CIERRA #6;
             |CIERRA #7;
             |CIERRA #8;
         |FINSI;
|FPRC;

|PROCESO borra_var;
             sw_fich03  = 0;
             sw_fich04  = 0;
             linea_red1 = 0;
             linea_red2 = 0;
             linea_red3 = 0;
             linea_red4 = 0;
             linea_red5 = 0;
             pen_1      = 0;
             pen_2      = 0;
             pen_3      = 0;
             pen_4      = 0;
             pen_5      = 0;
             apl_1      = 0;
             apl_2      = 0;
             apl_3      = 0;
             apl_4      = 0;
             apl_5      = 0;
|FPRC;

|DEFBUCLE retotrea;
          #2#1;
          ;
          #1#0,tipo_ini;
          #1#0,tipo_fin;
          e;
          NULL,deduce;
|FIN;

|PROCESO tipo_decl;
         |SI #1#35 = "X";
             tipo_ini = "A";
             tipo_fin = "A";
             tipdec_ini = "1";
             tipdec_fin = "T";
         |SINO;
             tipo_ini = "1";
             tipo_fin = "T";
         |FINSI;
         #9#0 = #1#0;
         |LEE 101#9=;
         |SI FSalida ! 0;
             |DDEFECTO #9;
             #9#0 = #1#0;
             |GRABA 020#9b;
         |FINSI;
         |BUCLE retotrea;
         |GRABA 020#9a;
         |LIBERA #9;
|FPRC;

|PROCESO redatcon;
         existe_emp = 0;
         |PARA; |SI existe_emp = 0; |Y #0#0 [ #0#1; |HACIENDO;
                |ABRE #10;  ||  redatcon 93;
                #10#0 = #0#0;
                |LEE 001#10];
                existe_emp = FSalida;
                |CIERRA #10;
                |SI existe_emp = 0; |Y #10#0 [ #0#1;
                    #0#0 = #10#0;
                    |ABRE #1;  ||  redatcon 94;
                    #1#0 = #0#0;
                    |LEE 001#1=;
                    existe_emp = FSalida;
                    |CIERRA #1;
                    |SI existe_emp = 0; |HAZ tipo_decl; |FINSI;
                    existe_emp = 0;
                |FINSI;
                #0#0 = #0#0 + 1;
         |SIGUE;
|FPRC;

|PROCESO cambia_paths;
         |PATH_DAT #2  path93;
         |PATH_DAT #10 path93;
         |PATH_DAT #14 path93;
         |PATH_DAT #15 path93;
         |PATH_DAT #16 path93;
         |PATH_DAT #17 path93;
         |PATH_DAT #18 path93;
         |NOME_DAT #2  "retotrea";
         |NOME_DAT #10 "redatcon";
         |NOME_DAT #14 "rededu01";
         |NOME_DAT #15 "rededu02";
         |NOME_DAT #16 "rededu03";
         |NOME_DAT #17 "rededu04";
         |NOME_DAT #18 "rededu05";
|FPRC;

|PROCESO fichero;
         fichero = Usuario; |QBT fichero;
         fichero = (fichero + "zzzzzzzz") % 106;
         fichero = "pd" + fichero;
|FPRC;

|PROCESO inicio; |TIPO 10;
         |HAZ renta93;
         |HAZ renta94;
|FPRC;

|PROCESO final; |TIPO 3;
         |HAZ cambia_paths;
         |HAZ fichero;
         |NOME_DAT #3 fichero;
         |ABRE #3; |CIERRA #3; |DELINDEX #3;
         |ABRE #9;
         |HAZ redatcon;
         |CIERRA #9;
|FPRC;

|PROCESO renta93; |TIPO 7;
         |DBASS path93;
         #0#4 = path93 + "ren1993/fich/";
         |PINTA #0#4;
         path93 = #0#4;
         |QBF path93;
|FPRC;

|PROCESO path93; |TIPO 0;
         path93 = #0#4;
         |QBF path93;
         path = path93;
         |HAZ directorio;
|FPRC;

|PROCESO renta94; |TIPO 7;
         |DBASS path94;
         #0#5 = path94 + "ren1994/fich/";
         |PINTA #0#5;
         path94 = #0#5;
         |QBF path94;
|FPRC;

|PROCESO path94; |TIPO 0;
         path94 = #0#5;
         |QBF path94;
         path = path94;
         |HAZ directorio;
|FPRC;

|PROCESO directorio;
         |SI path93 = path94;
             |MENAV "0000No pueden ser el mismo directorio.";
             |ERROR;
         |SINO;
             |SI path = path93;
                 |PATH_DAT #10 path93;
                 |NOME_DAT #10 "redatcon";
                 |ABRE #10;
                 existe_emp = FSalida;
                 |CIERRA #10;
             |SINO;
                 |ABRE #1;
                 existe_emp = FSalida;
                 |CIERRA #1;
             |FINSI;
             |SI existe_emp ! 0;
                 |PTEC 18;
                 |MENAV "0000Existe algun error en la entrada del directorio. Revise el texto.";
                 |ERROR;
             |FINSI;
         |FINSI;
|FPRC;

|PROCESO confirma; |TIPO 0;
         |SI #0#3 = "N"; |PTEC 807; |FINSI;
|FPRC;
