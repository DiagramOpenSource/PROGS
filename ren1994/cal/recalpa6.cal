|FICHEROS;
    rededu07 #0;
    rededu08 #1;
    rededu09 #2;
    rededu10 #3;
    reincide #5;
    retotren #6;
    reprogra #10;
|FIN;

|VARIABLES;
    sw_fich   = 0;
    numero1   = 0;
    numero2   = 0;
    numero3   = 0;
    numero4   = 0;
    numero5   = 0;
    numero6   = 0;
    coefi1    = 0;
    coefi11    = 0.00;
    zz         = 0;
    funeral    = 0;
    periodo    = 0;
    suma      = 0;
    mayor     = 0;
    tope      = 0;
    maximo    = 0;
    FEstado   = 0;
    sw        = 0;

    tipod     = "";
    perceptor = "";
    tipore    = "";
    minusva   = "";
    numero   = "";
    s        = "S";
    alfa1    = "";
    linea    = "";
    rendi    = "";
    descri   = "";
    contenido = "";
    blanco    = "                                        ";
    empreac   = 0;

    &incisino = "";
    &modo     = 0;
    &empresa  = 0;
    &emprefi  = 0;
    &nombre   = "";
    &lininci  = 0;
    &swcalcu  = 0;
    &minusva1 = "";
    &minusva2 = "";
    &minusva3 = "";
    &minusva4 = "";
    &minusva5 = "";
    &minusva6 = "";
    &declara  = "";
    &individu = 0;
    &acceso   = 0;
    &recalcu  = 0;
    &tippar    = 0;
    &parte1    = 0.0000;
    &parte2    = 0.0000;
    &muerto    = "";
    &fecpar    = @;
    &netot     = 0;
    &netoc     = 0;
    &netoa     = 0;
    &trabat    = 0;
    &trabac    = 0;
    &trabaa    = 0;
    &obitus    = 0;
|FIN;


|| **************************************************************************
||                   PROCESO DE LECTURA DE LOS DISTINTOS FICHEROS
|| **************************************************************************

|PROCESO fichero0;         || Trata Enfermedad, Seguros, Alquileres y guarderia
alfa1 = "ENFERMEDAD, SEGUROS, ALQUILERES Y GUARDERIA ------------------------";
tipod     = #0#2;
tipore    = #0#9;
descri    = #0#5;
rendi     = #0#8;
linea     = #0#1;
perceptor = #0#3;
numero1   = #0#10;
numero2   = #0#11;
numero3   = #0#12;
numero4   = #0#13;
numero5   = #0#14;
numero6   = #0#15;
sw_fich   = 0;
empreac   = #0#0;
periodo   = #0#16;
|SI tippar = 2;
    |SI muerto = "T";  |Y #0#2 = "T";
        periodo = 1;
    |FINSI;
    |SI muerto = "C";  |Y #0#2 = "C";
        periodo = 1;
    |FINSI;
|FINSI;

|SI #0#9 = "X"; |O #0#9 = "P"; |O #0#9 = "R";
    |HAZ incidenci1;
|FINSI;
|SI #0#9 = "A"; |O #0#9 = "R"; |HAZ tipodecl; |FINSI;
|FPRC;

|PROCESO fichero1;         || Trata Impuestos Municipales
alfa1 = "IMPUESTOS MUNICIPALES ----------------------------------------------";
tipod     = #1#2;
tipore    = #1#8;
descri    = #1#4;
rendi     = #1#7;
linea     = #1#1;
perceptor = #1#3;
numero1   = #1#9;
numero2   = #1#10;
numero3   = #1#11;
numero4   = #1#12;
numero5   = #1#13;
numero6   = #1#14;
sw_fich   = 1;
empreac   = #1#0;

periodo   = #1#15;
|SI tippar = 2;
    |SI muerto = "T";  |Y #1#2 = "T";
        periodo = 1;
    |FINSI;
    |SI muerto = "C";  |Y #1#2 = "C";
        periodo = 1;
    |FINSI;
|FINSI;

|SI #1#8 = "X"; |O #1#8 = "P"; |O #1#8 = "R";
    |HAZ incidenci1;
|FINSI;
|SI #1#8 = "A"; |O #1#8 = "R"; |HAZ tipodecl; |FINSI;
|FPRC;

|PROCESO fichero2;   || Trata Ceuta y Melilla , Doble imposicion Internacional
alfa1 = "CEUTA Y MELILLA / DOBLE IMPOSICION INTERNACIONAL -------------------";
tipod     = #2#2;
tipore    = #2#7;
descri    = #2#5;
rendi     = #2#6;
linea     = #2#1;
perceptor = #2#3;
numero1   = #2#8;
numero2   = #2#9;
numero3   = #2#10;
numero4   = #2#11;
numero5   = #2#12;
numero6   = #2#13;
sw_fich   = 2;
empreac   = #2#0;

periodo   = #2#14;
|SI tippar = 2;
    |SI muerto = "T";  |Y #2#2 = "T";
        periodo = 1;
    |FINSI;
    |SI muerto = "C";  |Y #2#2 = "C";
        periodo = 1;
    |FINSI;
|FINSI;

|SI #2#7 = "X"; |O #2#7 = "P"; |O #2#7 = "R";
    |HAZ incidenci1;
|FINSI;
|SI #2#7 = "A"; |O #2#7 = "R"; |HAZ tipodecl; |FINSI;
|FPRC;

|PROCESO fichero3;         || Trata Incrementos/Disminuciones Cta.Vivienda
alfa1 = "INCREMENTOS / DISMINUCIONES CUENTA VIVIENDA -------------------------";
tipod     = #3#2;
tipore    = #3#8;
descri    = #3#5;
rendi     = #3#6;
linea     = #3#1;
perceptor = #3#3;
numero1   = #3#9;
numero2   = #3#10;
numero3   = #3#11;
numero4   = #3#12;
numero5   = #3#13;
numero6   = #3#14;
sw_fich   = 3;
empreac   = #3#0;

periodo   = #3#15;
|SI tippar = 2;
    |SI muerto = "T";  |Y #3#2 = "T";
        periodo = 1;
    |FINSI;
    |SI muerto = "C";  |Y #3#2 = "C";
        periodo = 1;
    |FINSI;
|FINSI;

|SI #3#8 = "X"; |O #3#8 = "P"; |O #3#8 = "R";
    |HAZ incidenci1;
|FINSI;
|SI #3#8 = "A"; |O #3#8 = "R"; |HAZ tipodecl; |FINSI;
|FPRC;

|| **************************************************************************
                        TRATAMIENTO DE LA LIQUIDACION
|| **************************************************************************

|PROCESO tipodecl;
  coefi11 = 1;
  funeral = 0;
  |SI tipod = "G";  |Y tippar = 1;   || Si es Ganancial  Y MATRIMONIO;
      |SI periodo = 0;
          coefi1= 50*parte1; coefi11 = .5; zz = 25; tipod="T"; |HAZ leeliqui;
          coefi1= 50*parte1; coefi11 = .5; zz = 25; tipod="C"; |HAZ leeliqui;
          coefi1=100*parte2; coefi11 =  1; zz=  50;  tipod="A"; |HAZ leeliqui;
      |SINO;
          |SI periodo = 1;
              coefi1 = 50; coefi11 = .5; zz = 50; tipod  = "T"; |HAZ leeliqui;
              coefi1 = 50; coefi11 = .5; zz = 50; tipod  = "C"; |HAZ leeliqui;
          |SINO;
              coefi1 = 100; coefi11 = 1; zz =100; tipod  = "A"; |HAZ leeliqui;
          |FINSI;
      |FINSI;
      |ACABA;
  |FINSI;

  |SI tipod = "G";  |Y tippar = 3;   || Si es Ganancial  Y SEPARACION;
      |SI periodo = 0;
          coefi1= 50*parte2; coefi11 = .5; zz = 25; tipod="T"; |HAZ leeliqui;
          coefi1= 50*parte2; coefi11 = .5; zz = 25; tipod="C"; |HAZ leeliqui;
          coefi1=100*parte1; coefi11 =  1; zz = 50; tipod="A"; |HAZ leeliqui;
      |SINO;
          |SI periodo = 1;
              zz = 100;
              coefi1 = 100; coefi11 = 1; tipod  = "A"; |HAZ leeliqui;
          |SINO;
              zz = 50;
              coefi1 =  50; coefi11 = .5;  tipod  = "T"; |HAZ leeliqui;
              coefi1 =  50; coefi11 = .5;  tipod  = "C"; |HAZ leeliqui;
          |FINSI;
      |FINSI;
      |ACABA;
  |FINSI;


  || Si es Ganancial, conjunta y SE MUERE EL;

  |SI tipod = "G";  |Y tippar = 2; |Y muerto = "T";  |Y obitus = 1;
      |SI periodo = 0;
          coefi1= 100*parte2; coefi11 = 1; zz = 50; tipod="C"; |HAZ leeliqui;
          coefi1= 100*parte1; coefi11 = 1; zz = 50; tipod="A"; |HAZ leeliqui;
      |SINO;
          |SI periodo = 1;
              zz = 100;
              coefi1 = 100; coefi11 = 1; tipod  = "A"; |HAZ leeliqui;
          |SINO;
              zz = 100;
              coefi1 = 100; coefi11 = 1; tipod  = "C"; |HAZ leeliqui;
          |FINSI;
      |FINSI;
      |ACABA;
  |FINSI;


  || Si es Ganancial, conjunta y SE MUERE ELLA;

  |SI tipod = "G";  |Y tippar = 2; |Y muerto = "C";  |Y obitus = 1;
      |SI periodo = 0;
          coefi1= 100*parte2; coefi11 = 1; zz = 50; tipod="T"; |HAZ leeliqui;
          coefi1= 100*parte1; coefi11 = 1; zz = 50; tipod="A"; |HAZ leeliqui;
      |SINO;
          |SI periodo = 1;
              zz = 100;
              coefi1 = 100; coefi11 = 1; tipod  = "A"; |HAZ leeliqui;
          |SINO;
              zz = 100;
              coefi1 = 100; coefi11 = 1; tipod  = "T"; |HAZ leeliqui;
          |FINSI;
      |FINSI;
      |ACABA;
  |FINSI;


  ||Si es Ganancial, individual Y SE MUERE EL;

  |SI tipod = "G";  |Y tippar = 2; |Y muerto = "T"; |Y obitus = 2;
      |SI periodo = 0;
          funeral = 1;
          coefi1=  50 * parte1; coefi11=.5; zz=  50; tipod = "T"; |HAZ leeliqui;
          coefi1= 100 - coefi1; coefi11= 1; zz=  50; tipod = "C"; |HAZ leeliqui;
      |SINO;
          |SI periodo = 1;
              coefi1  = 50;
              coefi11 = .5;
              zz = 50;
              tipod  = "T"; |HAZ leeliqui;
              tipod  = "C"; |HAZ leeliqui;
          |SINO;
              zz = 100; coefi11 = 1; coefi1 = 100; tipod  = "C"; |HAZ leeliqui;
          |FINSI;
      |FINSI;
      |ACABA;
  |FINSI;

  ||Si es Ganancial, individual Y SE MUERE ELLA;

 |SI tipod = "G"; |Y tippar = 2; |Y muerto = "C"; |Y obitus = 2;
      |SI periodo = 0;
          funeral = 1;
          coefi1=  50 * parte1;  coefi11=.5; zz= 50; tipod = "C"; |HAZ leeliqui;
          coefi1 = 100 - coefi1; coefi11= 1; zz= 50; tipod = "T"; |HAZ leeliqui;
      |SINO;
          |SI periodo = 1;
              coefi11 = .5;
              coefi1 = 50;
              zz = 50;
              tipod  = "T"; |HAZ leeliqui;
              tipod  = "C"; |HAZ leeliqui;
          |SINO;
              zz = 100; coefi11 = 1; coefi1 = 100;  tipod  = "T"; |HAZ leeliqui;
          |FINSI;
      |FINSI;
      |ACABA;
  |FINSI;

  |SI tipod ! "V";  |Y tippar = 1;      ||SI ES TITULAR O CONYUGE Y SE CASAN;
      |SI periodo = 0;
          coefi11 = 1;
          zz = 100;
          coefi1 =  100 * parte1;                |HAZ leeliqui;
          coefi1 =  100 * parte2;  tipod = "A";  |HAZ leeliqui;
      |SINO;
          |SI periodo = 1;
              coefi11 = 1;
              zz = 100;
              coefi1 = 100; |HAZ leeliqui;
          |SINO;
              coefi11 = 1;
              zz = 100;
              coefi1 = 100;  tipod = "A";  |HAZ leeliqui;
          |FINSI;
      |FINSI;
      |ACABA;
  |FINSI;

  |SI tipod ! "V";  |Y tippar = 3;      ||SI TITULAR O CONYUGE Y SE SEPARAN;
      |SI periodo = 0;
          coefi11 = 1;
          zz = 100;
          coefi1 =  100 * parte2;                |HAZ leeliqui;
          coefi1 =  100 * parte1;  tipod = "A";  |HAZ leeliqui;
      |SINO;
          |SI periodo = 1;
              coefi11 = 1;
              zz = 100;
              coefi1 = 100;  tipod = "A";  |HAZ leeliqui;
          |SINO;
              coefi11 = 1;
              zz = 100;
              coefi1 = 100;                |HAZ leeliqui;
          |FINSI;
      |FINSI;
      |ACABA;
  |FINSI;


  ||SI TITULAR O CONYUGE, conjunta Y SE MUERE UNO;
  |SI tipod ! "V";  |Y tippar = 2; |Y obitus = 1;
      |SI periodo = 0;
          coefi11 = 1;
          zz = 100;
          |SI muerto = "T";  |Y tipod = "T";
              coefi1 =  100;  tipod = "A";  |HAZ leeliqui;
          |SINO;
              |SI muerto = "C";  |Y tipod = "C";
                  coefi1 =  100;  tipod = "A";  |HAZ leeliqui;
              |SINO;
                  coefi1 =  100 * parte2;                |HAZ leeliqui;
                  coefi1 =  100 * parte1;  tipod = "A";  |HAZ leeliqui;
              |FINSI;
          |FINSI;
      |SINO;
          |SI periodo = 1;
              coefi11 = 1;
              zz = 100;
              coefi1 = 100;  tipod = "A";  |HAZ leeliqui;
          |SINO;
              coefi11 = 1;
              zz = 100;
              coefi1 = 100;                |HAZ leeliqui;
          |FINSI;
      |FINSI;
      |ACABA;
  |FINSI;

||SI TITULAR O CONYUGE, individual  Y SE MUERE UNO;

  |SI tipod ! "V"; |Y tippar = 2; |Y obitus = 2;
       coefi11 = 1;
       zz = 100;
       coefi1 = 100; |HAZ leeliqui;
      |ACABA;
  |FINSI;

  |SI tippar = 1;          ||VARIOS Y SE CASAN;
      |SI periodo = 0;
          |SI numero1 ! 0;
              zz = numero1;
              coefi11 = numero1 / 100;
              coefi1 =  numero1 * parte1;
              tipod = "T";
              |HAZ leeliqui;
          |FINSI;
          |SI numero2 ! 0;
              zz = numero2;
              coefi11 = numero2 / 100;
              coefi1 =  numero2 * parte1;
              tipod = "C";
              |HAZ leeliqui;
          |FINSI;
          zz = numero1 + numero2;
          coefi1 = ( numero1 + numero2 ) * parte2;
          coefi11 = ( numero1 + numero2 ) / 100;
          tipod = "A"; |HAZ leeliqui;
      |SINO;
          |SI periodo = 1;
             |SI numero1 ! 0;
                 zz = numero1;
                 coefi11 = numero1 / 100;
                 coefi1 = numero1;
                 tipod = "T";
                 |HAZ leeliqui;
             |FINSI;
             |SI numero2 ! 0;
                 zz = numero2;
                 coefi11 = numero2 / 100;
                 coefi1 = numero2;
                 tipod = "C";
                 |HAZ leeliqui;
             |FINSI;
          |SINO;
             zz = numero1 + numero2;
             coefi1 = numero1 + numero2;
             coefi11 = (numero1 + numero2) / 100;
             tipod = "A"; |HAZ leeliqui;
          |FINSI;
      |FINSI;
      |ACABA;
  |FINSI;

  |SI tippar = 3;          ||VARIOS Y SE SEPARAN;
      |SI periodo = 0;
          |SI numero1 ! 0;
              zz = numero1;
              coefi11 = numero1 / 100;
              coefi1 =  numero1 * parte2;
              tipod = "T";
              |HAZ leeliqui;
          |FINSI;
          |SI numero2 ! 0;
              zz = numero2;
              coefi11 = numero2 / 100;
              coefi1 =  numero2 * parte2;
              tipod = "C";
              |HAZ leeliqui;
          |FINSI;
          zz = numero1 + numero2;
          coefi1 = ( numero1 + numero2 ) * parte1;
          coefi11 = ( numero1 + numero2 ) / 100;
          tipod = "A"; |HAZ leeliqui;
      |SINO;
          |SI periodo = 1;
              zz = numero1 + numero2;
              coefi1 = numero1 + numero2;
              coefi11 = (numero1 + numero2) / 100;
              tipod = "A"; |HAZ leeliqui;
          |SINO;
              |SI numero1 ! 0;
                  zz = numero1;
                  coefi1 = numero1;
                  coefi11 = numero1 / 100;
                  tipod = "T";
                  |HAZ leeliqui;
              |FINSI;
              |SI numero2 ! 0;
                  zz = numero2;
                  coefi1 = numero2;
                  coefi11 = numero2 / 100;
                  tipod = "C";
                  |HAZ leeliqui;
              |FINSI;
          |FINSI;
      |FINSI;
      |ACABA;
  |FINSI;


  |SI tippar = 2;  |Y obitus = 1;     ||VARIOS, conjunta Y SE MUERE;
      |SI periodo = 0;
          |SI numero1 ! 0;
              zz = numero1;
              coefi11 = numero1 / 100;
              coefi1 =  numero1 * parte2;
              tipod = "T";
              |HAZ leeliqui;
          |FINSI;
          |SI numero2 ! 0;
              zz = numero2;
              coefi11 = numero2 / 100;
              coefi1 =  numero2 * parte2;
              tipod = "C";
              |HAZ leeliqui;
          |FINSI;
          zz = numero1 + numero2;
          coefi1 = ( numero1 + numero2 ) * parte1;
          coefi11 = ( numero1 + numero2 ) / 100;
          tipod = "A"; |HAZ leeliqui;
      |SINO;
          |SI periodo = 1;
              zz = numero1 + numero2;
              coefi1 = numero1 + numero2;
              coefi11 = (numero1 + numero2) / 100;
              tipod = "A"; |HAZ leeliqui;
          |SINO;
              |SI numero1 ! 0;
                  zz = numero1;
                  coefi1 = numero1;
                  coefi11 = numero1 / 100;
                  tipod = "T";
                  |HAZ leeliqui;
              |FINSI;
              |SI numero2 ! 0;
                  zz = numero2;
                  coefi1 = numero2;
                  coefi11 = numero2 / 100;
                  tipod = "C";
                  |HAZ leeliqui;
              |FINSI;
          |FINSI;
      |FINSI;
      |ACABA;
  |FINSI;

  |SI tippar = 2;  |Y obitus = 2;     ||VARIOS, individual Y SE MUERE ALGUIEN;
      |SI numero1 ! 0;
          zz = numero1;
          coefi1 =  numero1;
          coefi11 =  numero1 / 100;
          tipod = "T";
          |HAZ leeliqui;
      |FINSI;
      |SI numero2 ! 0;
          zz = numero2;
          coefi1 =  numero2;
          coefi11 =  numero2 / 100;
          tipod = "C";
          |HAZ leeliqui;
      |FINSI;
  |FINSI;
|FPRC;


|PROCESO leeliqui;                 || Lee la Liquidacion
#6#0 = empreac;
#6#141 = tipod;
|LIBERA #6; |LEE 110#6=;
FEstado = FSalida;
|SI FSalida ! 0;
   |DDEFECTO #6;
   #6#000 = empreac;
   #6#141 = tipod;
|FINSI;
|SI sw_fich = 0; |HAZ grabafi0; |FINSI;
|SI sw_fich = 1; |HAZ grabafi1; |FINSI;
|SI sw_fich = 2; |HAZ grabafi2; |FINSI;
|SI sw_fich = 3; |HAZ grabafi3; |FINSI;

#6#213 = obitus;
|SI FEstado = 0; |GRABA 021#6a; |SINO; |GRABA 021#6n; |FINSI;
|FPRC;

|PROCESO grabafi0;
  |SI #0#4 = "E"; #6#91 = #6#91 + ((#0#7 + #0#8) % coefi1); |FINSI;
  |SI #0#4 = "E"; #6#164 = #6#164 + (#0#6 % coefi1); |FINSI;
  |SI #0#4 = "A"; #6#92 = #6#92 + ((#0#7 + #0#8) % coefi1); |FINSI;
  |SI #0#4 = "A"; #6#172 = #6#172 + (#0#6 % coefi1);  |FINSI;
  |SI #0#4 = "G"; #6#93 = #6#93 + ((#0#7 + #0#8) % coefi1); |FINSI;
  |SI #0#4 = "G"; #6#173 = #6#173 + (#0#6 % coefi1);  |FINSI;
  |SI #0#4 = "S"; #6#94 = #6#94 + ((#0#7 + #0#8) % coefi1); |FINSI;
  |SI #0#4 = "S"; #6#165 = #6#165 + (#0#6 % coefi1);  |FINSI;
|FPRC;

|PROCESO grabafi1;
  #6#106 = #6#106 + (#1#006 % coefi1);
|FPRC;

|PROCESO grabafi2;
  |SI #2#004 = "C"; #6#108 = #6#108 + (#2#006 % coefi1); |FINSI;
  |SI #2#004 = "D"; #6#122 = #6#122 + (#2#006 % coefi1); |FINSI;
|FPRC;

|PROCESO grabafi3;
  |SI #3#4 = "D"; #6#167 = #6#167 + (#3#6 % coefi1); |FINSI;
  |SI #3#4 = "D"; #6#96 = #6#96 + ((#3#6 % coefi1) * .15); |FINSI;
  |SI #3#004 = "I"; #6#123 = #6#123 + (#3#6 % coefi1); |FINSI;
  |SI #3#004 = "I"; #6#124 = #6#124 + (#3#7 % coefi1); |FINSI;
|FPRC;


|| **************************************************************************
||                            DILIGENCIAS
|| **************************************************************************

|PROCESO incidenci1;
|SI incisino = "S";
   |SI sw = 0; |HAZ grabacion; sw = 1; |FINSI;
   linea = "00" + linea;
   linea = linea % -102;

   rendi = "          " + rendi;
   rendi = rendi % -110;

   |QBF descri;
   descri = descri + blanco;
   descri = descri % 130;

  alfa1 = "NO CALCULO ->  " + linea + " " + descri + "    " + rendi;

  |SI tipore = "R";
      alfa1 = "RENTA ------>  " + linea + " " + descri + "    " + rendi;
  |FINSI;
  |SI tipore = "P";
       alfa1 = "PATRIMONIO ->  " + linea + " " + descri + "    " + rendi;
  |FINSI;
  |HAZ grabacion;
|FINSI;
|FPRC;

|PROCESO grabacion;
swcalcu = 1;
lininci = lininci + 5;
#5#0 = empreac;
#5#1 = lininci;
#5#2 = alfa1;
#5#3 = "S";
|GRABA 020#5n;
|LIBERA #5;
|FPRC;


|| **************************************************************************
||                             PROCESO BUCLES
|| **************************************************************************

|DEFBUCLE leefic0;   || Bucle Transparencias Fiscal
#0#1;
;
empresa, 01;
emprefi, 99;
e;
NULL, fichero0;
|FIN;

|DEFBUCLE leefic1;   || Bucle Profesional Directa
#1#1;
;
empresa, 01;
emprefi, 99;
e;
NULL, fichero1;
|FIN;

|DEFBUCLE leefic2;    || Bucle Profesional Coeficiente
#2#1;
;
empresa, 01;
emprefi, 99;
e;
NULL, fichero2;
|FIN;

|DEFBUCLE leefic3;    || Bucle Empresarial Directa
#3#1;
;
empresa, 01;
emprefi, 99;
e;
NULL, fichero3;
|FIN;


|| **************************************************************************
                              INICIO
|| **************************************************************************

|PROGRAMA;
  |ABRE #10;
  |ABRE #5;
  |ABRE #6;
  |SI recalcu = 0;  |O recalcu = 2;
      #10#0 = empresa;
      |LEE 040#10=;
      |SI FSalida = 0;
          |SI #10#26 = 1; sw = 0; lininci = 7500; |BUCLE leefic0; |FINSI;
          |SI #10#27 = 1; sw = 0; lininci = 7800; |BUCLE leefic1; |FINSI;
          |SI #10#28 = 1; sw = 0; lininci = 8100; |BUCLE leefic2; |FINSI;
          |SI #10#29 = 1; sw = 0; lininci = 8300; |BUCLE leefic3; |FINSI;
      |FINSI;
  |SINO;
      sw = 0; lininci = 7500; |BUCLE leefic0;
      sw = 0; lininci = 7800; |BUCLE leefic1;
      sw = 0; lininci = 8100; |BUCLE leefic2;
      sw = 0; lininci = 8300; |BUCLE leefic3;
  |FINSI;
  |CIERRA #10;
  |CIERRA #5;
  |CIERRA #6;

|CORRE "recalpa8.run";

|FPRO;
