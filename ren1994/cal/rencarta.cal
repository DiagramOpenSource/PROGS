|FICHEROS;
    rencarta #0;   ||  Seleccion de empresas requerimiento documentacion.
    retmpcar #1;   ||  Temporal de datos.
    redatcon #2;   ||  Datos del contribuyente.
    m2progra #3;   ||  Programas en activo.
    retmpca1 #4;   ||  Def de impresion de linea de temporal.
    regrinfl #6;   ||
    redefin2 #7;   ||  Cabec. Definidor de informe.
    redefin3 #8;   ||  Lineas Grupos Definidor de informe.
    redefin4 #9;   ||  Lineas Campos Definidor de informe.
|FIN;

|VARIABLES;
    titulo = "";
    xxxxfiac = "  ";
    xxaponer = "  ";
    xrelleno = "                              ";
    existe_emp = 0;
    existe_ren = 0;
    campo      = 0;
    informe = "rencarta";
    sw_grabatmp = 0;
    &lin_tmp = 0;
    suma  = 0;
    suma1 = 0;
    suma2 = 0;
    camgrl_campo = 0;
    camtmp = 0;
    campro = 0;
    impresora = "";
    tmpnum = 0;
    tmpnu1 = 0;
    bucle  = 0;
    tmpalf = "";
    filtro = "";
    sw_activo = 0;
    &fichero = "";
    &swdef  =  0;
    &empresa = 0;
    &path = "";
    &programa = 0;
    &numinfor = 0;
    progsub = "";
    numcasi  = 0;
{11}casillas = 0;
    txt_def = "";
    pag_def = 0;
    fec_def = @;
    filtro_def = 0;
    tmpfec_def = @;
    tmpalf_def = "";
    tmpnum_def = 0;
    long_tmp = 0;
    relleno_def = "                                                            ";
    camren = 0;
    sw_ctrlimp = 0;
 {-1}atriimp = "";
     atriimp1 = "DESACT";
     atriimp2 = "ACOMPR";
     atriimp3 = "DCOMPR";
     atriimp4 = "AEXPAN";
     atriimp5 = "DEXPAN";
     atriimp6 = "ANEGRI";
     atriimp7 = "DNEGRI";
|FIN;


|CAMPOS;
    #1#1 descrip;
|FIN;

|| --------------------- PROCESOS DE CONTROL DE IMPRESORA -------------------

|PROCESO ctrl_impr;
         |SI sw_activo > 0; |O numcasi = 0;
             |SI #6#2 = 950; txt_def = "";       |HAZ avance; |FINSI;
             |SI #6#2 = 951; txt_def = "PIEINF"; |HAZ avance; |FINSI;
             |SI #6#2 = 952; txt_def = "PAGINA"; |HAZ avance; |FINSI;
             |SI #6#2 > 952; |Y #6#2 < 957; |HAZ imprim_fec; |FINSI;
             |SI #6#2 > 956; |Y #6#2 < 964; |HAZ activ_atri; |FINSI;
         |FINSI;
|FPRC;

|PROCESO avance;
         |DDEFECTO #1;
         |PARA bucle = 1; |SI bucle [ #9#9; |HACIENDO bucle = bucle + 1;
               descrip = txt_def;
               |HAZ graba_tmp;
         |SIGUE;
|FPRC;

|PROCESO imprim_fec;
         fec_def = ~;
         |SI #6#2 > 953;
             filtro_def = #6#2 - 954;
             tmpfec_def = fec_def % filtro_def;
             tmpalf_def = tmpfec_def;
             txt_def    = tmpalf_def % 11;
         |SINO;
             txt_def = fec_def;
         |FINSI;
         |HAZ situ_col;
|FPRC;

|PROCESO activ_atri;
         tmpnum_def = #6#2 - 957;
         Iatriimp = atriimp1 <-;
         Iatriimp = Iatriimp + tmpnum_def;
         descrip = atriimp;
         |HAZ graba_tmp;
|FPRC;

|| -------------------------- PROCESOS DE IMPRESION -------------------------

|PROCESO print;
         sw_ctrlimp = 0;
         tmpalf_def = #1#1;
         |QBF tmpalf_def;
         |SI tmpalf_def = "PIEINF";
             |PIEINF;
             sw_ctrlimp = 1;
         |SINO;
             |SI tmpalf_def = "PAGINA";
                 FSalida = "PAGINA"; |PRINT;
                 sw_ctrlimp = 1;
             |SINO;
                 |PARA bucle = 0; |SI bucle < 7; |HACIENDO bucle = bucle + 1;
                       Iatriimp = atriimp1 <-;
                       Iatriimp = Iatriimp + bucle;
                       |SI tmpalf_def = atriimp;
                           |CIMPR bucle;
                           sw_ctrlimp = 1;
                           |SAL;
                       |FINSI;
                 |SIGUE;
             |FINSI;
         |FINSI;
         |SI sw_ctrlimp = 0; #4#1 = #1#1; |PRINT; |FINSI;
|FPRC;

|DEFBUCLE lee_tmp;
          #1#1;
          ;
          001;
          999;
          e;
          NULL,print;
|FIN;

|PROCESO imprime;
         |SI impresora = "";
             |IMPRE 0;
             impresora = Impresora;
         |SINO;
             |IMPRE -1;
         |FINSI;
         |SI FSalida = 0;
             |INFOR informe;
             |SI FSalida = 0;
                 |BUCLE lee_tmp;
                 |PIEINF;
                 |FININF;
             |FINSI;
             |FINIMP;
         |FINSI;
|FPRC;

|| --------------------- PROCESOS DE CREACION DE TEMPORAL -------------------

|PROCESO sub;
         |CIERRA #1;
         |SI swdef >  0; |Y swdef <  7; progsub = "recarta1"; |FINSI;
         |SI swdef >  6; |Y swdef <  9; progsub = "recarta2"; |FINSI;
         |SI swdef >  8; |Y swdef < 12; progsub = "recarta3"; |FINSI;
         |SI swdef > 11; |Y swdef < 15; progsub = "recarta4"; |FINSI;
         |SI swdef > 14; |Y swdef < 20; progsub = "recarta5"; |FINSI;
         |SI swdef > 19; |Y swdef < 30; progsub = "recarta6"; |FINSI;
         progsub = progsub + ".run";
         |SUB_EJECUTA progsub;
         |ABRE #1;
|FPRC;

|PROCESO busca_casilla;
         |PARA bucle = 0; |SI bucle < numcasi; |HACIENDO bucle = bucle + 1;
               Icasillas = casillas1 <-;
               Icasillas = Icasillas + bucle;
               tmpnum = -#6#3;
               |SI casillas = #6#3; |O casillas = tmpnum;
                   swdef = casillas;
                   tmpnum = bucle;
                   |SAL;
               |FINSI;
         |SIGUE;
|FPRC;

|PROCESO sube_casillas;
         |PARA bucle = tmpnum; |SI bucle < numcasi; |HACIENDO bucle = bucle + 1;
               tmpnu1 = bucle + 1;
               Icasillas = casillas1 <-;
               Icasillas = Icasillas + tmpnu1;
               tmpnu1 = casillas;
               Icasillas = casillas1 <-;
               Icasillas = Icasillas + bucle;
               casillas = tmpnu1;
         |SIGUE;
|FPRC;

|PROCESO deflineal;
         campro = #6#3;
         |SI numcasi > 0; |HAZ busca_casilla; |FINSI;
         |SI swdef = 0;
             Icasillas = casillas1 <-;
             Icasillas = Icasillas + numcasi;
             |SI #3campro = 1;
                 casillas = campro;
             |SINO;
                 casillas = -campro;
             |FINSI;
             numcasi = numcasi + 1;
             sw_activo = sw_activo + #3campro;
         |FINSI;
         |SI #3campro = 1; |Y #9#8 = "S";
             lin_tmp = lin_tmp + 1;
             #1#0  = lin_tmp;
             #1#1  = #9#6;  ||  Descripcion.
             |GRABA 020#1n;
         |FINSI;
         |SI swdef ! 0;
             |SI swdef > 0;
                 |HAZ sub;
                 sw_activo = sw_activo - 1;
             |FINSI;
             |HAZ sube_casillas;
             numcasi = numcasi - 1;
             swdef = 0;
         |FINSI;
|FPRC;

|PROCESO datcontri;
         |SI sw_activo > 0; |O numcasi = 0;
             camren = #6#3;
             descrip = #2camren;
             |HAZ graba_tmp;
         |FINSI;
|FPRC;


|| ---------------------------- PROCESOS VARIOS -----------------------------

|PROCESO situ_col;
         |SI #9#9 > 0;
             filtro_def = (long_tmp + 1) - (long_tmp - #9#9);
             tmpalf_def = relleno_def % filtro_def;
             tmpalf_def = tmpalf_def + txt_def
         |SINO;
             tmpalf_def = txt_def;
         |FINSI;
         descrip = tmpalf_def;
         |HAZ graba_tmp;
|FPRC;

|PROCESO graba_tmp;
         lin_tmp = lin_tmp + 1;
         #1#0 = lin_tmp;
         |GRABA 020#1n;
|FPRC;

|PROCESO regrinfl;
         |SI #9#5 > 0;
             |DDEFECTO #1;
             #6#0 = #9#4;  ||  Grupo.
             #6#1 = #9#5;  ||  Campo.
             |LEE 001#6=;
             |SI FSalida = 0;
                 |SI #6#2 > 499; |Y #6#2 < 751; |HAZ datcontri; |FINSI;
                 |SI #6#2 > 899; |Y #6#2 < 950; |HAZ deflineal; |FINSI;
                 |SI #6#2 > 949;                |HAZ ctrl_impr; |FINSI;
             |FINSI;
         |SINO;
             |SI sw_activo > 0; |O numcasi = 0;
                 descrip = #9#6;
                 |HAZ graba_tmp;
             |FINSI;
         |FINSI;
|FPRC;

|DEFBUCLE redefin4;
          #9#1;
          ;
          #8#0,#8#1,#8#2,001;
          #8#0,#8#1,#8#2,999;
          e;
          NULL,regrinfl;
|FIN;

|PROCESO redefin3;
         |DDEFECTO #1;
         |SI #8#5 = "S";
             descrip = #8#4;
             |HAZ graba_tmp;
         |FINSI;
         numcasi = 0;
         |BUCLE redefin4;
|FPRC;

|PROCESO crea_tmp;
         lin_tmp = 0;
         sw_grabatmp = 0;
         |BUCLELIN 2redefin3#7;
|FPRC;

|PROCESO comprueba;  ||  Comprueba si existe renta.
         |ABRE #3;
         #3#0 = #2#0;
         |LEE 001#3=;
         existe_emp = FSalida;
         |CIERRA #3;
         |SI existe_emp = 0; |Y #3#0 = #2#0;
             empresa = #2#0;
             |ABRE #1;
             |DDEFECTO #1;
             tmpalf_def = #1#1;
             tmpalf_def = tmpalf_def % 0;
             long_tmp = tmpalf_def;
             |HAZ crea_tmp;
             |CIERRA #1;
             |HAZ imprime;
             |DELINDEX #1;
         |FINSI;
|FPRC;

|DEFBUCLE redatcon;
          #2#1;
          ;
          #0#1;
          #0#2;
          e;
          NULL,comprueba;
|FIN;

|PROCESO redefin2;
         |ABRE #7;
         |DDEFECTO #7;
         #7#0 = programa;
         #7#1 = numinfor;
         |LEE 001#7=;
         |CIERRA #7;
|FPRC;

|PROCESO fichero;
         fichero = Usuario; |QBT fichero;
         fichero = (fichero + "zzzzzzzz") % 106;
         fichero = "ci" + fichero;
|FPRC;

|PROCESO inicio; |TIPO 10;
         |HAZ redefin2;
         titulo = #7#2;
         |QBF titulo;
         |SI titulo ! "";
             titulo = " " + titulo + " ";
         |SINO;
             titulo = " DOCUMENTOS NECESARIOS PARA EL CALCULO DE LA RENTA ";
         |FINSI;
         tmpalf = titulo % 0;
         tmpnum = 7100911;
         tmpnum = tmpnum + tmpalf;
         |CUADRO tmpnum;
         |ATRI R;
         |PINTA 0811,titulo;
         |ATRI r;
|FPRC;

|PROCESO final; |TIPO 3;
         |HAZ directorio;
         |SI path ! "";
             |HAZ cambia_paths;
             |HAZ fichero;
             |NOME_DAT #1 fichero;
             |ABRE #1; |CIERRA #1; |DELINDEX #1;
             |ABRE #6;
             |SI #0#0 = "S";
                 |BUCLE redatcon;
             |SINO;
                 |PARA campo = 3; |SI campo < 33; |HACIENDO campo = campo + 1;
                       |SI #0campo > 0;
                           #0#1 = #0campo;
                           #0#2 = #0campo;
                           |BUCLE redatcon;
                       |FINSI;
                 |SIGUE;
             |FINSI;
             |CIERRA #6;
         |SINO;
             |MENAV "0000Existe algun error al intentar leer los datos del ejercicio anterior";
             |ERROR;
         |FINSI;
|FPRC;

|PROCESO directorio;
         |DBASE path;
         tmpalf = path % -103;
         tmpalf = tmpalf % 102;
         tmpnum = tmpalf;
         path = "";
         |SI tmpnum ] 94;
             tmpnum = tmpnum - 1;
             tmpalf = tmpnum;
             |DBASS path;
             path = path + "ren19" + tmpalf + "/fich/";
         |FINSI;
|FPRC;

|PROCESO cambia_paths;
         |PATH_DAT #3 path;
         |NOME_DAT #3 "reprogra";
|FPRC;

|| ---------------------- PROCESOS DE ENTRADA DE DATOS -----------------------

|PROCESO empresas; |TIPO 7;
         |SI #0#0 = "N"; |HAZ nomodi; |FINSI;
|FPRC;

|PROCESO parrilla; |TIPO 7;
         |SI #0#0 = "S"; |HAZ nomodi; |FINSI;
|FPRC;

|PROCESO nomodi;
         xxaponer = Anono;
         xxxxfiac = Control % A1001;
         Control = Control + xxxxfiac;
         Control = Control + xrelleno;
         Control = Control % A120;
         Control = Control + xxaponer;
|FPRC;
