|FICHEROS;
     rew00203;
     rem00101;
|FIN;

|VARIABLES;
      aAlfa         = "";
      aAlfa1        = "";
      aAlfa2        = "";
      aAlfa3        = "";
      aAlfa4        = "";
      aAlfa5        = "";
      aTecla        = "";
      aEsc1         = "";
      aEsc2         = "";
      aRetu         = "";

      nForma        = 0;
      nEdad         = 0;
      nRango        = 0;
      nGrid         = 0;
      nLabel1       = -1;
      nVentana      = -1;

      fHoy          = @;

 {-1}aVent          = "";
     aVent1         = 0;
     aVent2         = 0;
     aVent3         = 0;
     aVent4         = 0;
     aVent5         = "";

     &enEmpresa     = 0;
     &enModo        = 0;
     &eaVarDni      = "";
     &enCalcCif     = 0;
     &eaConvive     = "";
     &eaEstado      = "";
     &enPanta       = 0;
     &eaIBAN        = "";
     &eaCta         = "";
     &eaEntidad     = "";
     &eaSucursal    = "";
     &eaDC          = "";
     &eaCuenta      = "";
|FIN;

|PROCESO FinVentana;
     |SI nVentana ! -1;
         |FINVENTANA nVentana;
         nVentana = -1;
     |FINSI;

     |PULSATECLA;
|FPRC;

|PROCESO Tipo12Fw203;  |TIPO 12;
     |SI nLabel1 ! -1;
         |FIN_CONTROL_WINDOWS nLabel1;
         nLabel1 = -1;
     |FINSI;

     |HAZ FinVentana;
     |PULSATECLA;

     aAlfa   = #rew00203#23 + #rew00203#24 + #rew00203#25 + #rew00203#26;
     #rew00203#10 = aAlfa;
|FPRC;

|PROCESO IBANw203;  |TIPO 0;
     eaCta = #rew00203#23 + #rew00203#24 + #rew00203#25 + #rew00203#26;
     |QBF eaCta;
     |SI eaCta = "";  |ACABA;  |FINSI;

     eaEntidad  = #rew00203#23;
     eaSucursal = #rew00203#24;
     eaDC       = #rew00203#25;
     eaCuenta   = #rew00203#26;
     |HAZ CalculoDC;
     |SI eaDC ! #rew00203#25;
         aAlfa = "0000El D¡gito de Control deberia ser " + eaDC;
         |MENAV aAlfa;
     |FINSI;

     |HAZ CalcuIBAN;
     #rew00203#29 = eaIBAN;  |PINTA #rew00203#29;
|FPRC;

|PROCESO Tipo0C1Fw203;  |TIPO 0;
     |C_M #rew00203#1, 0, aAlfa;
     |SI aAlfa = "N";  |ACABA;  |FINSI;

     #rem00101#0 = #rew00203#0;
     #rem00101#1 = #rew00203#1;
     |LEE 000#rem00101.=;
     |SI FSalida = 0;
         |MENAV "0000El familiar ya existe.";
         |ERROR;
     |FINSI;
|FPRC;

|PROCESO Tipo0C2Fw203;  |TIPO 0;
     |SI nLabel1 ! -1;
         |FIN_CONTROL_WINDOWS nLabel1;
         nLabel1 = -1;
     |FINSI;

     |SI #rew00203#4 =  "DESCENDIENTE";
         |C_M #rew00203#8,  1, "S";
         |C_M #rew00203#12, 1, "S";
         |C_M #rew00203#18, 1, "S";
         |C_M #rew00203#22, 1, "S";
         |C_M #rew00203#9,  1, "N";  #rew00203#9  = "";   |PINTA #rew00203#9;
         |C_M #rew00203#19, 1, "N";  #rew00203#19 = "";   |PINTA #rew00203#19;
     |SINO;
         |SI nLabel1 = -1;
             aAlfa =  "LABEL,{{16}}Si el ASCENDIENTE tiene Rentas superiores a  ";
             aAlfa =  aAlfa + "8000 euros, no debe relacionarse";
             |CONTROL_SIMPLE 0, aAlfa, 3302, 3450, NULL;
             nLabel1 = FSalida;
         |FINSI;

         |C_M #rew00203#9,  1, "S";
         |C_M #rew00203#8,  1, "N";  #rew00203#8  = "";  |PINTA #rew00203#8;
         |C_M #rew00203#12, 1, "N";  #rew00203#12 = "";  |PINTA #rew00203#12;
         |C_M #rew00203#18, 1, "N";  #rew00203#18 = "01.01.0000";  |PINTA #rew00203#18;
         |C_M #rew00203#22, 1, "N";  #rew00203#22 = "N";           |PINTA #rew00203#22;
         |C_M #rew00203#19, 1, "S";

         |SI #rew00203#6 = 0;
             |C_M #rew00203#19, 1, "N";  #rew00203#19 = "";  |PINTA #rew00203#19;
         |FINSI;

         |SI #rew00203#2 = "            ";
             |MENAV "     Introduzca el NIF del Ascendiente";
             |ERROR;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO Tipo0C3Fw203;  |TIPO 0;
     aAlfa = #rew00203#3;  |QBF aAlfa;
     |SI aAlfa = "";
         |MENAV "     Introduzca Apellidos y Nombre.";
         |ERROR;
         |ACABA;
     |FINSI;
|FPRC;

|PROCESO PonSiglo20;
     aAlfa1    = (#rew00203#Campo# % 106) + "19" + (#rew00203#Campo# % -102);
     #rew00203#Campo# = aAlfa1;
     |PINTA #rew00203#Campo#;
|FPRC;

|PROCESO Tipo0C17Fw203;  |TIPO 0;
     #rew00203#5 = #rew00203#17 % -104;

     |SI #rew00203#Campo# = "01.01.0000";    |ACABA;  |FINSI;

     |SI #rew00203#4  = "DESCENDIENTE";
         |SI #rew00203#Campo# > "31.12.2016";
             |MENAV "     Fecha Incorrecta.";
             |ERROR;
         |FINSI;

         nEdad = 2016 - #rew00203#5;
         |SI nEdad ] 16;
             |SI #rew00203#2 = "            ";
                 |MENAV "     Descendiente > = a 16, Introduzca el NIF del Descendiente";
                 |ERROR;

                 |POSICIONATE #rew00203#2;
             |FINSI;
         |FINSI;

         |ACABA;
     |FINSI;

     fHoy = ~;

     |SI #rew00203#Campo# > fHoy;    || nacimientos futuros como que no
          |HAZ PonSiglo20;    ||/se cambia sin preguntar
     |FINSI;

     |SI #rew00203#Campo# ] "01.01.2000"; |Y #rew00203#Campo# [ fHoy;     || presuntos ni¤os,
                                                            ||/se pregunta
         aAlfa = "0000SLa fecha puede ser incorrecta. Ha introducido un contribuyente menor de edad. Corregir siglo?";
         |CONFI aAlfa;
         |SI FSalida = 0;
              |HAZ PonSiglo20;
         |FINSI;
     |FINSI;

     |SI #rew00203#5 = "0000";
         |MENAV "     Datos incorrectos. Informe la Fecha de Nacimiento ";
         |ERROR;
         |ACABA;
     |FINSI;
|FPRC;

|PROCESO MensaContri;  |TIPO 7;
     |HAZ FinVentana;

     |VENTANA 2455, 3198, -1, 17, "Recuerde";
     nVentana = FSalida;

     aAlfa =  "LABEL,{{16}}No deben relacionarse en este apartado los ";
     |CONTROL_SIMPLE 0, aAlfa, 2456, 2497, NULL;

     aAlfa =  "LABEL,{{16}}descendientes con rentas superiores a 8.000 euros,";
     |CONTROL_SIMPLE 0, aAlfa, 2556, 2597, NULL;

     aAlfa =  "LABEL,{{16}}o que presenten declaraci¢n individual con rentas";
     |CONTROL_SIMPLE 0, aAlfa, 2656, 2697, NULL;

     aAlfa =  "LABEL,{{16}}superiores a 1.800 euros (excepto los menores de edad";
     |CONTROL_SIMPLE 0, aAlfa, 2756, 2797, NULL;

     aAlfa =  "LABEL,{{16}}que presenten declaraci¢n conjunta con los padres)";
     |CONTROL_SIMPLE 0, aAlfa, 2856, 2897, NULL;

     aAlfa =  "LABEL,{{16}}Si el descendiente tiene ingresos superiores a 1.800 ";
     |CONTROL_SIMPLE 0, aAlfa, 3056, 3097, NULL;

     aAlfa =  "LABEL,{{16}}euros, revise el campo % imputaci¢n";
     |CONTROL_SIMPLE 0, aAlfa, 3156, 3197, NULL;

     |PTEC 802;  |PAUSA;
|FPRC;

|PROCESO ReponContri;  |TIPO 0;
     |HAZ FinVentana;

     |SI #rew00203#4  ! "DESCENDIENTE";  |ACABA;  |FINSI;
     |SI #rew00203#13 ! "S";             |ACABA;  |FINSI;

     nEdad = 2016 - #rew00203#5;
     |SI nEdad ] 18;  |Y #rew00203#6 = 0;
         |MENAV "0000Est  marcando como contribuyente a un descendiente mayor de edad y no es minusv lido";
     |FINSI;

     |PULSATECLA;
|FPRC;

|PROCESO Tipo7C19Fw203;  |TIPO 7;
     |SI #rew00203#4 = "DESCENDIENTE";
         |C_M #rew00203#19, 1, "N";  #rew00203#19 = "";  |PINTA #rew00203#19;
     |SINO;
         |C_M #rew00203#19, 1, "S";
         |SI #rew00203#6 = 0;
             |C_M #rew00203#19, 1, "N";  #rew00203#19 = "";  |PINTA #rew00203#19;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO Tipo7C7Fw203;  |TIPO 7;
     |HAZ FinVentana;

     |SI #rew00203#4 =  "DESCENDIENTE";
         |VENTANA 2810, 3398, -1, 16, "Vinculaci¢n DESCENDIENTES";
         nVentana = FSalida;

         |CONTROL_SIMPLE 0, "LABEL,{{16}}     En blanco de ambos.", 2816, 2897, NULL;
         |CONTROL_SIMPLE 0, "LABEL,{{16}}1.- Del declarante si no convive con el otro progenitor.", 2916, 2997, NULL;
         |CONTROL_SIMPLE 0, "LABEL,{{16}}2.- Del conyuge si no convive con el otro progenitor.", 3016, 3097, NULL;
         |CONTROL_SIMPLE 0, "LABEL,{{16}}3.- Del declarante si tambien convive con el otro progenitor.Ded.50%", 3116, 3197, NULL;
         |CONTROL_SIMPLE 0, "LABEL,{{16}}4.- Del conyuge si tambien convive con el otro progenitor.Ded.50%", 3216, 3297, NULL;
     |SINO;
         |VENTANA 2810, 3030, -1, 17, "Vinculaci¢n ASCENDIENTE";
         nVentana = FSalida;

         |CONTROL_SIMPLE 0, "LABEL,{{16}}1.- Del declarante", 2811, 2829, NULL;
         |CONTROL_SIMPLE 0, "LABEL,{{16}}2.- Del conyuge", 2911, 2929, NULL;
     |FINSI;

     |PTEC 802;  |PAUSA;
|FPRC;

|PROCESO Tipo0C7Fw203;  |TIPO 0;
     |HAZ FinVentana;
     |PULSATECLA;

     |SI #rew00203#4 = "DESCENDIENTE";
         |C_M #rew00203#27, 1, "S";
         |C_M #rew00203#28, 1, "S";

         |SI #rew00203#7 ! "3";  |Y #rew00203#7 ! "4";
             |C_M #rew00203#27, 1, "N";  #rew00203#27 = "";
             |C_M #rew00203#28, 1, "N";  #rew00203#28 = "";
         |FINSI;

         |PINTA #rew00203#27;
         |PINTA #rew00203#28;

         |ACABA;
     |FINSI;

     |SI #rew00203#7 = "3";  |O #rew00203#7 = "4";
         |ERROR;
         |ACABA;
     |FINSI;

     |SI #rew00203#Campo# = " ";
         |MENAV "     Debe ser 1 o 2 dependiendo del Titular o del Conyuge.";
         |ERROR;
         |ACABA;
     |FINSI;
|FPRC;

|PROCESO Tipo7C8Fw203;  |TIPO 7;
     |SI #rew00203#4 !  "DESCENDIENTE";  |ACABA;  |FINSI;

     |HAZ FinVentana;

     |VENTANA 2605, 3090, -1, 16, "Otras situaciones";
     nVentana = FSalida;

     aAlfa1 = "LABEL,{{16}}    En blanco ninguna de estas cuatro";
     aAlfa2 = "LABEL,{{16}}1.- Descendiente distinto de los hijos (Nietos, bisnietos) o";
     aAlfa2 = aAlfa2 + " persona en tutela o acogimiento.";
     aAlfa3 = "LABEL,{{16}}2.- Hijo/a mayor de edad incapacitado/a judicialmente sujeto/a a";
     aAlfa3 = aAlfa3 + " patria potestad prorrogada.";
     aAlfa4 = "LABEL,{{16}}3.- Hijo/a del declarante que conviva con otro progenitor sin matrimonio";
     aAlfa4 = aAlfa4 + " entre los padres.";
     aAlfa5 = "LABEL,{{16}}4.- Hijo/a del declarante cuando concurran simultaneamente las";
     aAlfa5 = aAlfa5 + " situaciones 2 y 3";

     |CONTROL_SIMPLE 0, aAlfa1, 2606, 2690, NULL;
     |CONTROL_SIMPLE 0, aAlfa2, 2706, 2790, NULL;
     |CONTROL_SIMPLE 0, aAlfa3, 2806, 2890, NULL;
     |CONTROL_SIMPLE 0, aAlfa4, 2906, 2990, NULL;
     |CONTROL_SIMPLE 0, aAlfa5, 3006, 3090, NULL;

     |PTEC 802;  |PAUSA;
|FPRC;

|PROCESO Tipo0C8Fw203;  |TIPO 0;
     |HAZ FinVentana;

     |SI #rew00203#7 = "3";  |Y #rew00203#8 = "3";
         |SI eaConvive ! "S";
             |MENAV "0000 No procede este dato ya que el contribuyente no tiene una S en el campo de Convivencia";
             |ERROR;
         |FINSI;
     |FINSI;

     |SI #rew00203#8 = "2";  |Y #rew00203#6 [ 65;
         |MENAV "0000 No procede este dato ya que el contribuyente no tiene una minusval¡a superior al 65%";
         |ERROR;
     |FINSI;
|FPRC;

|PROCESO Tipo7C11Fw203;  |TIPO 7;
     |HAZ FinVentana;

     |SI #rew00203#4 =  "DESCENDIENTE";
         |VENTANA 0501, 0870, -1, 16, "Imputaci¢n DESCENDIENTES";
         nVentana = FSalida;

         |CONTROL_SIMPLE 0, "LABEL,{{16}}-1  No calcula deduccion para el minimo familiar.", 0502, 0568, NULL;
         |CONTROL_SIMPLE 0, "LABEL,{{16}}-2  Hijo con ingresos superiores a 1800 euros con padres separados  ", 0602, 0668, NULL;
         |CONTROL_SIMPLE 0, "LABEL,{{16}}    o sin matrimonio se computa el 100% del minimo por descendiente", 0702, 0768, NULL;
         |CONTROL_SIMPLE 0, "LABEL,{{16}}    en tributacion conjunta.", 0802, 0868, NULL;
     |SINO;
         |VENTANA 2870, 2998, -1, 17, "Imputaci¢n ASCENDIENTE";
         nVentana = FSalida;

         |CONTROL_SIMPLE 0, "LABEL,{{16}}Porcentaje de imputacion", 2871, 2898, NULL;
     |FINSI;

     |PTEC 802;  |PAUSA;
|FPRC;

|PROCESO Tipo0C11Fw203;  |TIPO 0;
     |HAZ FinVentana;

     |SI #rew00203#11 = -2;
         |SI #rew00203#4 = "ASCENDIENTE ";
             |ERROR;
             |ACABA;
         |FINSI;

         |SI eaEstado ! "S";  |Y eaEstado ! "L";
             |MENAV "0000El estado civil del contribuyente debe ser soltero o divorciado";
             |ERROR;
             |ACABA;
         |FINSI;

         |SI #rew00203#7 < 3;
             |MENAV "0000La vinculacion debe ser 3 o 4";
             |ERROR;
             |ACABA;
         |FINSI;

         |SI #rew00203#8 < 3;
             |MENAV "0000Otras situaciones debe ser 3 o 4";
             |ERROR;
             |ACABA;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO Tipo0C9Fw203;
     |SI #rew00203#9 = "0";  #rew00203#9 = "";  |FINSI;
     |SI #rew00203#9 = "1";  #rew00203#9 = "";  |FINSI;

     |SI #rew00203#9 = " ";
         #rew00203#15 = "N";   |PINTA #rew00203#15;
         #rew00203#11 = 100;   |PINTA #rew00203#11;
         |ACABA;
     |FINSI;

     #rew00203#15 = "S";          |PINTA #rew00203#15;
     #rew00203#11 = 100 / #rew00203#9;   |PINTA #rew00203#11;
|FPRC;

|PROCESO LetraNifm101;  |TIPO 6;
     |SI FSalida = 10;
         eaVarDni  = #rew00203#Campo#;
         enCalcCif = 1;
         |HAZ CalculoDNI;
         #rew00203#Campo# = eaVarDni;
         |PINTA #rew00203#Campo#;
         |ERROR;
         |ACABA;
     |FINSI;

     eaVarDni  = #rew00203#Campo#;
     enCalcCif = 0;
     |HAZ CalculoDNI;
     eaVarDni = (eaVarDni + "            ") % 112;
     |SI #rew00203#Campo# ! eaVarDni;
         |CONFI "0000SEl Nif puede ser Erroneo. Desea Calcularlo.";
         |SI FSalida = 0;
             #rew00203#Campo# = eaVarDni;  |PINTA #rew00203#Campo#;
             |ERROR;
             |ACABA;
         |FINSI;
     |FINSI;
|FPRC;

|PROCESO Tipo0C18Fw203;  |TIPO 0;
     |C_M #rew00203#22, 1, "S";

     |SI #rew00203#18 = "01.01.0000";  |O #rew00203#18 = "00.00.0000";
         |C_M #rew00203#22, 1, "N";  #rew00203#22 = "N";           |PINTA #rew00203#22;
     |FINSI;
|FPRC;

|PROCESO Tipo0C21Fw203;  |TIPO 0;
     |SI #rew00203#21 = "01.01.0000";  |ACABA;  |FINSI;
     |SI #rew00203#21 = "00.00.0000";  |ACABA;  |FINSI;

     |SI #rew00203#21 < "01.01.2016";  |O #rew00203#21 > "31.12.2016";
         |MENAV "0000    La fecha de defuncion debe ser en el periodo del 2016";
         |ERROR;
     |FINSI;
|FPRC;

|PROCESO Tipo7C15Fw203;  |TIPO 7;
     |C_M #rew00203#15, 1, "S";
     |SI #rew00203#4 = "DESCENDIENTE";
         |C_M #rew00203#15, 1, "N";
         #rew00203#15 = "N";  |PINTA #rew00203#15;
     |FINSI;
|FPRC;
